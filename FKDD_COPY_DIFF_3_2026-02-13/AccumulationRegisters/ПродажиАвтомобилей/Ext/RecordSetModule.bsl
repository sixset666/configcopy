Перем Покупатель Экспорт; // Ссылка на контрагента
Перем ДоговорВзаиморасчетов Экспорт; // Ссылка на договор взаиморасчетов с контрагентом
Перем СкладКомпании Экспорт; // Ссылка на склад компании
Перем ДокументПродажи Экспорт; // Документ продажи
Перем Комиссия Экспорт; // Булево. Истина - продажи комиссионером, Ложь - продажи наши
Перем Сторно Экспорт; // Булево. Истина - возврат от покупателя
Перем ПодразделениеКомпании Экспорт; // Ссылка на подразделение компании
Перем ДокументОбъект Экспорт; // Документа объект выполняющий движения
Перем РезультатЗапросаПоАвтомобилям Экспорт; // Результат запроса по автомобилям

// получаем то, что продали
//// Продаем - Булево. Истина - продаем товар, Ложь - возвращаем
Функция ПолучитьТаблицуАвтомобилей(Продаем=Истина) Экспорт
	Комиссия=?(Комиссия=Неопределено,Ложь,Комиссия);
	ВидДок=ДокументОбъект.Метаданные().Имя;

	// посмотрим есть в документе сумма скидки
	ЕстьСкидка=(ДокументОбъект.Метаданные().ТабличныеЧасти.Автомобили.Реквизиты.Найти("СуммаСкидки")<>Неопределено);
	ЕстьДокументПродажи=(ДокументОбъект.Метаданные().ТабличныеЧасти.Автомобили.Реквизиты.Найти("ДокументПродажи")<>Неопределено);
	// формируем запрос
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументАвтомобили.Автомобиль КАК Автомобиль,
	|	"+?(ЕстьДокументПродажи,"ДокументАвтомобили.ДокументПродажи КАК ДокументПродажи,","")+"
	|	ДокументАвтомобили.СтавкаНДС КАК СтавкаНДС,
	|	"+?(СкладКомпании=Неопределено,"ДокументАвтомобили.МестоРазмещения КАК СкладКомпании,","")+"
	|	СУММА(ДокументАвтомобили.Количество) КАК Количество,
	|	СУММА(ДокументАвтомобили.СуммаВсего) КАК Сумма,
	|	СУММА(ДокументАвтомобили.СуммаНДС) КАК СуммаНДС,
	|	СУММА("+?(ЕстьСкидка,"ДокументАвтомобили.СуммаСкидки","0")+") КАК СуммаСкидки,
	|	ОстаткиАвтомобилейОбороты.Партия КАК ДокументПоставки,
	|	ОстаткиАвтомобилейОбороты.Партия.Контрагент КАК Поставщик,
	|	"+?(Комиссия,"&СтатусПартии","ОстаткиАвтомобилейОбороты.СтатусПартии")+" КАК СтатусПартии,
	|	ОстаткиАвтомобилейОбороты.ГТД КАК ГТД,
	|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилейОбороты.КоличествоРасход),0) КАК КоличествоРасход,
	|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилейОбороты.СуммаУпрРасход),0) КАК Себестоимость,
	|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилейОбороты.СуммаРасход),0) КАК СебестоимостьРегл,
	|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилейОбороты.СуммаНДСРасход),0) КАК СуммаНДСВходящий,
	|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилейОбороты.СуммаУпрРасход),0) КАК СебестоимостьКомплектация,
	|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилейОбороты.СуммаРасход),0) КАК СебестоимостьКомплектацияРегл,
	|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилейОбороты.СуммаНДСРасход),0) КАК СуммаНДСВходящийКомплектация
	|ИЗ
	|	Документ."+ВидДок+".Автомобили КАК ДокументАвтомобили";
	Если Комиссия Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Обороты(,,Регистратор,(Автомобиль В (&Автомобили))) КАК ОстаткиАвтомобилейОбороты
		|		ПО ДокументАвтомобили.Автомобиль=ОстаткиАвтомобилейОбороты.Автомобиль
		|	 	И ДокументАвтомобили.ДокументПередачи=ОстаткиАвтомобилейОбороты.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Обороты(,,Регистратор,(Автомобиль В (&Автомобили)) И Номенклатура ССЫЛКА Справочник.Опции) КАК КомплектацияАвтомобилейОбороты
		|		ПО ДокументАвтомобили.Автомобиль=КомплектацияАвтомобилейОбороты.Автомобиль
		|	 	И ДокументАвтомобили.ДокументПередачи=КомплектацияАвтомобилейОбороты.Регистратор";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Обороты(&Момент,&Момент,Регистратор,(Автомобиль В (&Автомобили))) КАК ОстаткиАвтомобилейОбороты
		|		ПО ДокументАвтомобили.Автомобиль=ОстаткиАвтомобилейОбороты.Автомобиль
		|	 	И ДокументАвтомобили.Ссылка=ОстаткиАвтомобилейОбороты.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Обороты(&Момент,&Момент,Регистратор,(Автомобиль В (&Автомобили)) И Номенклатура ССЫЛКА Справочник.Опции) КАК КомплектацияАвтомобилейОбороты
		|		ПО ДокументАвтомобили.Автомобиль=КомплектацияАвтомобилейОбороты.Автомобиль
		|	 	И ДокументАвтомобили.Ссылка=КомплектацияАвтомобилейОбороты.Регистратор";
	КонецЕсли; 
	ТекстЗапроса=ТекстЗапроса+"
	|ГДЕ
	|	ДокументАвтомобили.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументАвтомобили.Автомобиль,
	|	"+?(ЕстьДокументПродажи,"ДокументАвтомобили.ДокументПродажи,","")+"
	|	ДокументАвтомобили.СтавкаНДС,
	|	"+?(СкладКомпании=Неопределено,"ДокументАвтомобили.МестоРазмещения,","")+"
	|	"+?(Комиссия,"","ОстаткиАвтомобилейОбороты.СтатусПартии,")+"
	|	ОстаткиАвтомобилейОбороты.Партия,
	|	ОстаткиАвтомобилейОбороты.ГТД
	|";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварКупленный);
	Запрос.УстановитьПараметр("Автомобили",ДокументОбъект.Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Запрос.УстановитьПараметр("Момент",ДокументОбъект.Дата);

	Возврат Запрос.Выполнить();
КонецФункции

// фиксируем продажи
Функция Приход() Экспорт
	Результат=Истина;
	
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ДокументОбъект.Дата);
	КурсРегл	   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
	Попытка
		КурсУпр=ДокументОбъект.КурсВалютыУпр;
	Исключение
		КурсУпр=Неопределено;
	КонецПопытки; 
	Если КурсУпр=Неопределено ИЛИ обЗначениеНеЗаполнено(КурсУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ДокументОбъект.Дата);
		КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	
	// получаем таблицу автомобилей
	Если (РезультатЗапросаПоАвтомобилям=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоАвтомобилям)<>Тип("РезультатЗапроса") И ТипЗнч(РезультатЗапросаПоАвтомобилям)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоАвтомобилям=ПолучитьТаблицуАвтомобилей();
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоАвтомобилям)=Тип("РезультатЗапроса") Тогда
		ТаблицаАвтомобилей=РезультатЗапросаПоАвтомобилям.Выгрузить();
	Иначе
		ТаблицаАвтомобилей=РезультатЗапросаПоАвтомобилям;
	КонецЕсли;
	ЕстьГТД=(ТаблицаАвтомобилей.Колонки.Найти("ГТД")<>Неопределено);
	ЕстьДокументПродажиТоваров=(ТаблицаАвтомобилей.Колонки.Найти("ДокументПродажи")<>Неопределено);
	
	ЗапретПродажиНижеСебестоимости=обПраво("ЗапретитьПродажуНижеСебестоимости",ДокументОбъект.Права,,ДокументОбъект);
	
	Для Каждого СтрокаАвтомобиля Из ТаблицаАвтомобилей Цикл
		
		СебестоимостьУпр             = СтрокаАвтомобиля.Себестоимость;
		СебестоимостьКомплектацияУпр = СтрокаАвтомобиля.СебестоимостьКомплектация;
		Себестоимость                = СтрокаАвтомобиля.СебестоимостьРегл;
		СебестоимостьКомплектация    = СтрокаАвтомобиля.СебестоимостьКомплектацияРегл;
		СуммаУпр = ?(СтрокаАвтомобиля.Сумма=NULL, 0, обПересчет(СтрокаАвтомобиля.Сумма,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр));
		СебестоимостьРавнаПродаже = Ложь;
		Попытка
			Если СтрокаАвтомобиля.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия И (НЕ СтрокаАвтомобиля.ДокументПоставки.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж) Тогда
				//СебестоимостьРавнаПродаже = Истина;  // Себестоимость всегда считаем от входящей Ульянов 07,02,19
			КонецЕсли;
		Исключение
			СебестоимостьРавнаПродаже = Ложь;
		КонецПопытки;
		
		Если ЗапретПродажиНижеСебестоимости И НЕ Сторно Тогда
			
			//<<Павличенко 27.12.17
			//заявка Лукаш С.В. : для пользователей с ЗапретомПродажиНижеСебестоимости разрешить проведение документов
			//РеализацияАвтомобилей с указанной СуммойВозмещения
			//РеализацияАвтомобилей с указанной СуммойВозмещенияПоУтилизации
			СписокСкидокПоВозмещению = Новый СписокЗначений();
			СписокСкидокПоВозмещению.Добавить(ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоКоду("02055")); //сумма возмещения
			СписокСкидокПоВозмещению.Добавить(ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоКоду("02062")); //сумма возмещения по утилизации
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Автомобиль", СтрокаАвтомобиля.Автомобиль);
			Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеализацияАвтомобилей") тогда
				ЗаказМассив = ДокументОбъект.Автомобили.НайтиСтроки(ПараметрыОтбора);
				Заказ = ЗаказМассив.Получить(0).ЗаказНаАвтомобиль;
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЗначенияСвойствОбъектов.Объект,
				|	ЗначенияСвойствОбъектов.Свойство,
				|	ЗначенияСвойствОбъектов.Значение
				|ИЗ
				|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
				|ГДЕ
				|	ЗначенияСвойствОбъектов.Объект =&Объект
				|	И ЗначенияСвойствОбъектов.Свойство В (&Свойство)";
				Запрос.УстановитьПараметр("Объект", Заказ);
				Запрос.УстановитьПараметр("Свойство", СписокСкидокПоВозмещению);
				ТаблицаВыгрузки = Запрос.Выполнить().Выгрузить();
				Попытка
					ВозвращаемаяСумма = ТаблицаВыгрузки.Итог("Значение");
				Исключение
					ВозвращаемаяСумма = 0;
				КонецПопытки;
			Иначе
				ВозвращаемаяСумма = 0; //не рассмтривается случай других видов документов кроме РеализацииАвтомобилей
			КонецЕсли;
			//Если (СебестоимостьУпр+СебестоимостьКомплектацияУпр)>СуммаУпр Тогда
			Если (СебестоимостьУпр+СебестоимостьКомплектацияУпр)>СуммаУпр + ВозвращаемаяСумма Тогда
			//>>Павличенко 27.12.17
				дкСообщитьРезультат("Автомобиль """+СокрЛП(СтрокаАвтомобиля.Автомобиль)
				+". Себестоимость автомобиля "+Формат(СебестоимостьУпр+СебестоимостьКомплектацияУпр,"ЧЦ = 15; ЧДЦ = 2; ЧН = 0.00")+" сумма продажи " + Формат(СуммаУпр,"ЧЦ = 15; ЧДЦ = 2; ЧН = 0.00") + " " + ВалютаУпр
				+". Продажа ниже себестоимости запрещена.","Важное&Продажи автомобилей:",ДокументОбъект,СтрокаАвтомобиля.Автомобиль);
				Результат=Ложь;
			КонецЕсли;
			
			
			СебестоимостьБезНДС=Окр(СебестоимостьУпр+СебестоимостьКомплектацияУпр,2)-Окр(СтрокаАвтомобиля.СуммаНДСВходящий+СтрокаАвтомобиля.СуммаНДСВходящийКомплектация,2);
			СуммаПродажиБезНДС=Окр(СуммаУпр,2)-Окр(?(СтрокаАвтомобиля.СуммаНДС=NULL,0,обПересчет(СтрокаАвтомобиля.СуммаНДС,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл)),2);

            Если СебестоимостьБезНДС> СуммаПродажиБезНДС+ ВозвращаемаяСумма Тогда
								дкСообщитьРезультат("Автомобиль """+СокрЛП(СтрокаАвтомобиля.Автомобиль)
				+". Себестоимость автомобиля без НДС "+Формат(СебестоимостьБезНДС,"ЧЦ = 15; ЧДЦ = 2; ЧН = 0.00")+" сумма продажи без НДС " + Формат(СуммаПродажиБезНДС,"ЧЦ = 15; ЧДЦ = 2; ЧН = 0.00") + " " + ВалютаУпр
				+". Продажа ниже себестоимости запрещена.","Важное&Продажи автомобилей:",ДокументОбъект,СтрокаАвтомобиля.Автомобиль);
				Результат=Ложь;

			КонецЕсли;	

			
		КонецЕсли;
		
		НоваяЗапись=Добавить();
		НоваяЗапись.Период=ДокументОбъект.Дата;
		НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
		НоваяЗапись.ПодразделениеКомпании=ПодразделениеКомпании;
		НоваяЗапись.Автомобиль=СтрокаАвтомобиля.Автомобиль;
		Если ДокументПродажи<>Неопределено Тогда
			НоваяЗапись.ДокументПродажи=ДокументПродажи;
		Иначе
			Если ЕстьДокументПродажиТоваров Тогда
				НоваяЗапись.ДокументПродажи=СтрокаАвтомобиля.ДокументПродажи;
			КонецЕсли;
		КонецЕсли;
		НоваяЗапись.Поставщик=СтрокаАвтомобиля.Поставщик;
		НоваяЗапись.Покупатель=Покупатель;
		НоваяЗапись.СтатусПартии=СтрокаАвтомобиля.СтатусПартии;
		НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
		НоваяЗапись.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если НЕ Комиссия Тогда
			Если СкладКомпании=Неопределено Тогда
				НоваяЗапись.СкладКомпании=СтрокаАвтомобиля.СкладКомпании;
			Иначе
				НоваяЗапись.СкладКомпании=СкладКомпании;
			КонецЕсли; 
		КонецЕсли;
		НоваяЗапись.СтавкаНДС=СтрокаАвтомобиля.СтавкаНДС;
		НоваяЗапись.Партия=СтрокаАвтомобиля.ДокументПоставки;
		Если ЕстьГТД Тогда
			НоваяЗапись.ГТД=СтрокаАвтомобиля.ГТД;
		Иначе
			НоваяЗапись.ГТД=Справочники.ГТД.ПустаяСсылка();
		КонецЕсли; 
		НоваяЗапись.Количество=СтрокаАвтомобиля.Количество;
		НоваяЗапись.СебестоимостьУпр=Окр(СебестоимостьУпр+СебестоимостьКомплектацияУпр,2);
		НоваяЗапись.Себестоимость=Окр(Себестоимость+СебестоимостьКомплектация,2);
		НоваяЗапись.Сумма=Окр(?(СтрокаАвтомобиля.Сумма=NULL,0,обПересчет(СтрокаАвтомобиля.Сумма,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		НоваяЗапись.СуммаСкидки=Окр(?(СтрокаАвтомобиля.СуммаСкидки=NULL,0,обПересчет(СтрокаАвтомобиля.СуммаСкидки,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		НоваяЗапись.СуммаУпр=Окр(СуммаУпр,2);
		НоваяЗапись.СуммаНДС=Окр(?(СтрокаАвтомобиля.СуммаНДС=NULL,0,обПересчет(СтрокаАвтомобиля.СуммаНДС,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		НоваяЗапись.СуммаНДСВходящий = Окр(СтрокаАвтомобиля.СуммаНДСВходящий+СтрокаАвтомобиля.СуммаНДСВходящийКомплектация,2);
		
		
		Если СебестоимостьРавнаПродаже Тогда
			
			СтавкаНДСВходящее   = Окр(НоваяЗапись.СуммаНДСВходящий/(НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий), 2);
			СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
			
			НоваяЗапись.Себестоимость    = НоваяЗапись.Сумма;
			НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СуммаУпр;
			НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
			
		КонецЕсли;
		
		//anton
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтчетКомиссионераЗаАвтомобили") тогда
			РезультатЗапросаПоПартиямОтданным=ПолучитьТаблицуПартийОтданных(НоваяЗапись.Автомобиль);
			Если РезультатЗапросаПоПартиямОтданным <> Неопределено Тогда
				ТаблицаПартий=РезультатЗапросаПоПартиямОтданным.Выгрузить();
				Если ТаблицаПартий.Количество()>0 Тогда
					НоваяЗапись.Себестоимость    = ТаблицаПартий[0].СуммаСебестоимостиУпр;
					НоваяЗапись.СебестоимостьУпр = ТаблицаПартий[0].СуммаСебестоимостиУпр;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//anton
		
		// если возвраты, то сделаем все отрицательным
		Если Сторно Тогда
			НоваяЗапись.Количество=НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,-1,1);
			НоваяЗапись.СебестоимостьУпр=НоваяЗапись.СебестоимостьУпр*?(НоваяЗапись.СебестоимостьУпр>0,-1,1);
			НоваяЗапись.Себестоимость=НоваяЗапись.Себестоимость*?(НоваяЗапись.Себестоимость>0,-1,1);
			НоваяЗапись.Сумма=НоваяЗапись.Сумма*?(НоваяЗапись.Сумма>0,-1,1);
			НоваяЗапись.СуммаСкидки=НоваяЗапись.СуммаСкидки*?(НоваяЗапись.СуммаСкидки>0,-1,1);
			НоваяЗапись.СуммаУпр=НоваяЗапись.СуммаУпр*?(НоваяЗапись.СуммаУпр>0,-1,1);
			НоваяЗапись.СуммаНДС=НоваяЗапись.СуммаНДС*?(НоваяЗапись.СуммаНДС>0,-1,1);
			НоваяЗапись.СуммаНДСВходящий=НоваяЗапись.СуммаНДСВходящий*?(НоваяЗапись.СуммаНДСВходящий>0,-1,1);
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатЗапросаПоАвтомобилям=Неопределено;
	Покупатель=Неопределено;
	ДоговорВзаиморасчетов=Неопределено;
	СкладКомпании=Неопределено;
	ПодразделениеКомпании=Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	
	Возврат Результат;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

//anton
Функция ПолучитьТаблицуПартийОтданных(Автомобиль, Переоценка=Ложь )
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Автомобиль", Автомобиль);
	
	НайденныеСтроки = ДокументОбъект.Автомобили.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() >0 Тогда
		ДокументПередачи = НайденныеСтроки[0].ДокументПередачи;
		Если ДокументПередачи.Дата >= Дата(2020,1,1) Тогда
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	АвтомобилиОтданныеОстатки.Автомобиль КАК Автомобиль,
	|	АвтомобилиОтданныеОстатки.ДокументПередачи КАК ДокументПередачи,
	|	АвтомобилиОтданныеОстатки.Партия КАК Партия,
	|	АвтомобилиОтданныеОстатки.ГТД КАК ГТД,
	|	АвтомобилиОтданныеОстатки.КоличествоОстаток КАК Количество,
	|	АвтомобилиОтданныеОстатки.СуммаНДСОстаток КАК СуммаНДС,
	|	АвтомобилиОтданныеОстатки.СуммаОстаток КАК Сумма,
	|	АвтомобилиОтданныеОстатки.СуммаУпрОстаток КАК СуммаУпр,
	|	АвтомобилиОтданныеОстатки.СуммаСебестоимостиУпрОстаток КАК СуммаСебестоимостиУпр,
	|	АвтомобилиОтданныеОстатки.СуммаСебестоимостиРеглОстаток КАК СуммаСебестоимостиРегл
	|
	|ИЗ
	|	РегистрНакопления.АвтомобилиОтданные.Остатки(&Момент) КАК АвтомобилиОтданныеОстатки
	|
	|ГДЕ
	|	  АвтомобилиОтданныеОстатки.Автомобиль В (&Автомобиль)
	|   И АвтомобилиОтданныеОстатки.Контрагент=&Контрагент
	|   И АвтомобилиОтданныеОстатки.ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов
	|	"+?(Переоценка,"И АвтомобилиОтданныеОстатки.ДокументПередачи В (&ДокументПередачи)","")+"
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.АвтомобилиОтданные.Остатки
	|
	|УПОРЯДОЧИТЬ ПО ДокументПередачи Возр
	|";
	тзАвтомобилей=РезультатЗапросаПоАвтомобилям.Выгрузить();
	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Момент", ДокументОбъект.МоментВремени());
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Запрос.УстановитьПараметр("Контрагент", Покупатель);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
	Если Переоценка Тогда
		Запрос.УстановитьПараметр("ДокументПередачи", ДокументПередачи);
	КонецЕсли;
	Запрос.Текст=ТекстЗапроса;
	
	//// Наложим блокировку на считываемые данные
	//СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "АвтомобилиОтданные");
	//ЗначенияБлокировки = Новый Соответствие;
	//ЗначенияБлокировки.Вставить("Период", Новый Диапазон(,ДокументОбъект.Дата)); 
	//ЗначенияБлокировки.Вставить("Контрагент", Покупатель); 
	//ЗначенияБлокировки.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов); 
	//СтруктураПараметровБлокировки.Вставить("ИсточникДанных", тзАвтомобилей);
	//ОписаниеИсточника = Новый Соответствие;
	//ОписаниеИсточника.Вставить("Автомобиль", "Автомобиль");
	//Если Переоценка Тогда
	//	ОписаниеИсточника.Вставить("ДокументПередачи", ДокументПередачи);
	//КонецЕсли;
	//обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	Возврат Запрос.Выполнить();
КонецФункции
//anton
// Модуль набора записей регистра "ПартииТоваровКомпании"

Перем СкладКомпании Экспорт;                // склад компании
Перем СтатусПартии Экспорт;                 // статус партии
Перем Сторно Экспорт;                       // признак сторно записи
Перем СкладКомпанииКуда Экспорт; 			// для перемещения между складами
Перем РезультатЗапросаПоТоварам Экспорт; 	// если результат запроса (или таблица значений) неопределен, то подразумевается, что в документе есть таблица "Товары". И наименования колонок стандартные.
Перем ДокументОбъект Экспорт;               // документ выполняющий движения
Перем ГраницаРасчетаОстатков Экспорт; 		// граница на которую надо рассчитывать остатки 
Перем РежимВозврата Экспорт; 				// установленный режим возврата для пользователя обПраво("РежимСписанияПриВозвратеПоставщику") 
Перем РежимДопРасходы Экспорт; 				// Число. 0-нет, 1-поступление, 2-только доп расходы
Перем ЗаписыватьДвижения Экспорт; 			// Булево. Истина - движения записываются сразу по окончании выполнения процедуры. Ложь - движения записываются при записи документа.
Перем ПоБазовомуКоличеству Экспорт; 		// Булево. Ложь - количество товаров будет расcчитываться как "Количество*Коэффициент", Истина - количество будет браться из реквизита "КоличествоБазовое"
Перем ИмяРеквизитаДокумент Экспорт; 		// Строка. Имя реквизита табличной части "Товары". Этот реквизит определяет партию либо документ отгрузки
Перем ОтрицательныеОстаткиРазрешены; 		// Булево. Право разрешающее образовывать отрицательные остатки товаров
Перем ХозОперация Экспорт; 					// СправочникСсылка.ХозОперации. Если Неопределено, то движение идет по хоз операции документа
Перем МассивПартий Экспорт; 				// Массив. Если надо установить фильтр на партии, то заполняем этот массив
Перем СтратегияСписанияПартийТоваровПоДатам; // Стратегия списания партий склада-отправителя
Перем СтратегияСписанияПартийТоваровПоДатамКуда; // Стратегия списания партий склада-получателя
Перем ЕстьСтавкаНДС Экспорт; 				// Булево. Истина - в таблице товаров есть ставка НДС, Ложь - нет
Перем ШапкаДокумента Экспорт; 				// Выборка или строка таблицы значений, в которой содержаться необходимые данные о шапке документа
Перем РежимПроведения Экспорт; 				// Режим проведения документа оперативный/неоперативный
Перем ТаблицаВозвратов Экспорт;				// Таблица возвратов

// возвращает результат запроса по таблице товаров
Функция ПолучитьТаблицуТоваров(ВидДвижения)
	ПоБазовомуКоличеству=?(ПоБазовомуКоличеству=Неопределено,Ложь,ПоБазовомуКоличеству);
	ВидДок=ДокументОбъект.Метаданные().Имя;
	ЕстьГТД=ДокументОбъект.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("ГТД")<>Неопределено;
	
	// для поступления таблицу товаров получаем из документа, 
	// для расхода получаем таблицу из движений по остаткам + из документа получаем партии и ставки НДС
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,";
	Если ЕстьСтавкаНДС Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	ДокТовары.СтавкаНДС КАК СтавкаНДС,";
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент) Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	ДокТовары."+ИмяРеквизитаДокумент+" КАК Партия,";
	КонецЕсли;
	Если ЕстьГТД Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	ДокТовары.ГТД КАК ГТД,";
	КонецЕсли; 
	Если ВидДвижения<>ВидДвиженияНакопления.Расход Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	СУММА(ДокТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ДокТовары.Сумма) КАК Сумма,
		|	СУММА(ДокТовары.СуммаВсего) КАК СуммаВсего,";
		
		Если Не обЗначениеНеЗаполнено(РежимДопРасходы) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|	ВЫБОР КОГДА ДокТовары.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1
			|			ТОГДА СУММА(ДокТовары.Номенклатура.Вес * ДокТовары.Количество)
			|			ИНАЧЕ ВЫБОР КОГДА ДокТовары.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2
			|				ТОГДА СУММА(ДокТовары.ЕдиницаИзмерения.Вес * ДокТовары.Количество)
			|				ИНАЧЕ 0 КОНЕЦ КОНЕЦ КАК Вес,";
		КонецЕсли;
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|	СУММА(ДокТовары.Количество"+?(ПоБазовомуКоличеству,"Базовое","*ДокТовары.Коэффициент")+") КАК Количество,
	|	ДокТовары.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ."+ВидДок+".Товары КАК ДокТовары
	|ГДЕ
	|	  ДокТовары.Ссылка=&Ссылка
	|	И ДокТовары.Номенклатура.ВидНоменклатуры<>&Услуга
	|
	|СГРУППИРОВАТЬ ПО Номенклатура, ХарактеристикаНоменклатуры"+?(обЗначениеНеЗаполнено(ИмяРеквизитаДокумент),"",", "+ИмяРеквизитаДокумент)+?(ЕстьГТД,",ГТД","")+?(ЕстьСтавкаНДС,",СтавкаНДС","")+"
	|
	|";
	ЗапросТовары=Новый Запрос(ТекстЗапроса);
	ЗапросТовары.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	ЗапросТовары.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
	
	// если имеем расход, то таблицу товаров надо брать из движений по регистру остатков
	Если ВидДвижения=ВидДвиженияНакопления.Расход Тогда
		// если допроведение, то надо таблицу остатков брать запросом, иначе просто выгрузить
		Если ДокументОбъект.Ссылка<>ШапкаДокумента.Ссылка Тогда
			// допроведение
			ЗапросОстатки=Новый Запрос("
			|ВЫБРАТЬ
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры,
			//|	&ПустаяСтавкаНДС КАК СтавкаНДС,
			//|	Неопределено КАК Партия,
			|	СкладКомпании КАК СкладКомпании,
			|	СУММА(Количество) КАК Количество
			|ИЗ
			|	РегистрНакопления.ОстаткиТоваровКомпании
			|ГДЕ
			|	Регистратор = &Ссылка
			|	И ВидДвижения = &ВидДвиженияРасход
			|
			|СГРУППИРОВАТЬ ПО
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры,
			|	СкладКомпании
			|");
			ЗапросОстатки.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			ЗапросОстатки.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
			ЗапросОстатки.УстановитьПараметр("ПустаяСтавкаНДС",Справочники.СтавкиНДС.ПустаяСсылка());
			ТаблицаТоваров = ЗапросОстатки.Выполнить().Выгрузить();
		Иначе
			// обычное проведение
			ТаблицаТоваров=ДокументОбъект.Движения.ОстаткиТоваровКомпании.Выгрузить();
			// приведем таблицу в нормальный вид
			ТаблицаТоваров.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,СкладКомпании,ВидДвижения","Количество");
			//ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
			//ТаблицаТоваров.Колонки.Добавить("Партия");
			
			// удалим строки с видом движения "Приход"
			МассивНайденныхСтрок=ТаблицаТоваров.НайтиСтроки(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Приход));
			Для Сч=0 По МассивНайденныхСтрок.Количество()-1 Цикл
				ТаблицаТоваров.Удалить(МассивНайденныхСтрок[Сч]);
			КонецЦикла;
		КонецЕсли;
		
		// удалим строки с пустым количеством
		МассивНайденныхСтрок=ТаблицаТоваров.НайтиСтроки(Новый Структура("Количество",0));
		Для Сч=0 По МассивНайденныхСтрок.Количество()-1 Цикл
			ТаблицаТоваров.Удалить(МассивНайденныхСтрок[Сч]);
		КонецЦикла;
		
		// если в таблице документа есть СтавкаНДС или Партия, то дополним нашу таблицу
		//Если ЕстьСтавкаНДС ИЛИ НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент) Тогда
		Если НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент) Тогда
			ТаблицаТоваровДокумента=ЗапросТовары.Выполнить().Выгрузить();
			//ТаблицаТоваровДокумента.Сортировать("Номенклатура Возр,ХарактеристикаНоменклатуры Убыв");
			ТаблицаТоваровДокумента.Сортировать("Номенклатура Возр,ХарактеристикаНоменклатуры Убыв,"+ИмяРеквизитаДокумент+" Убыв");
			//Для Каждого СтрокаТовар Из ТаблицаТоваровДокумента Цикл
			//	СтруктураОтбора=Новый Структура("Номенклатура",СтрокаТовар.Номенклатура);
			//	Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
			//		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
			//	КонецЕсли;
			//	МассивНайденныхСтрок=ТаблицаТоваров.НайтиСтроки(СтруктураОтбора);
			//	Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
			//		ТекСтрока=МассивНайденныхСтрок[Сч];
			//		Если ЕстьСтавкаНДС И обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			//			ТекСтрока.СтавкаНДС=СтрокаТовар.СтавкаНДС;
			//		КонецЕсли;
			//		Если НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент) И обЗначениеНеЗаполнено(ТекСтрока.Партия) Тогда
			//			ТекСтрока.Партия=СтрокаТовар[ИмяРеквизитаДокумент];
			//		КонецЕсли;
			//	КонецЦикла;
			//КонецЦикла;
			Для Каждого СтрокаТовар Из ТаблицаТоваровДокумента Цикл
				СтруктураОтбора=Новый Структура("Номенклатура",СтрокаТовар.Номенклатура);
				Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
				КонецЕсли;
				МассивНайденныхСтрок=ТаблицаТоваров.НайтиСтроки(СтруктураОтбора);
				Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
					ТекСтрока=МассивНайденныхСтрок[Сч];
					СтрокаТовар.ХарактеристикаНоменклатуры=ТекСтрока.ХарактеристикаНоменклатуры;
					ТекСтрока.Количество=ТекСтрока.Количество-Мин(ТекСтрока.Количество,СтрокаТовар.Количество);
					Если ТекСтрока.Количество=0 Тогда
						ТаблицаТоваров.Удалить(ТекСтрока);
					КонецЕсли; 
				КонецЦикла;
			КонецЦикла;
			ТаблицаТоваров=ТаблицаТоваровДокумента;
		КонецЕсли;
		
	Иначе
		// для прихода все просто
		ТаблицаТоваров = ЗапросТовары.Выполнить().Выгрузить();
	КонецЕсли;
	
	Возврат ТаблицаТоваров;
КонецФункции

// Возвращает результат запроса, с таблицей незакрытых партий.
Функция ПолучитьДеревоПартий(ТаблицаТоваров,СведенияОПоставщике=Ложь)
	Сторно=?(Сторно=Неопределено,Ложь,Сторно);
	Если ГраницаРасчетаОстатков=Неопределено Тогда 
		Если РежимПроведения=РежимПроведенияДокумента.Оперативный Тогда
			ГраницаРасчетаОстатков=Неопределено;
		Иначе
			ГраницаРасчетаОстатков=ШапкаДокумента.МоментВремени; 
		КонецЕсли;
	КонецЕсли;
    // Получим значения настроек 
	Если РежимВозврата=НЕОПРЕДЕЛЕНО Тогда РежимВозврата=обПраво("РежимСписанияПриВозвратеПоставщику",ДокументОбъект.Права,,ДокументОбъект); КонецЕсли;
	
	//Чтение значения для списания по датам
	Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
		// Сначала попробуем взять стратегию со склада
		СтратегияСписанияПартийТоваровПоДатам = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	Иначе
		// Ежели не получилось берем из константы	
		СтратегияСписанияПартийТоваровПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
	КонецЕсли;	
	Если НЕ обЗначениеНеЗаполнено(СкладКомпанииКуда) Тогда
		Если обЗначениеНеЗаполнено(СкладКомпанииКуда.СтратегияСписанияПартийТоваровПоДатам) Тогда
			// Получим стратегию учета на складе получателе
			СтратегияСписанияПартийТоваровПоДатамКуда = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
		Иначе
			// Ежели не получилось берем из константы	
			СтратегияСписанияПартийТоваровПоДатамКуда = СкладКомпанииКуда.СтратегияСписанияПартийТоваровПоДатам;
		КонецЕсли;	
	КонецЕсли;
	
	//Чтение значения для списания по статусам
	СтратегияСписанияПартийТоваровПоСтатусам=Константы.СтратегияСписанияПартийТоваровПоСтатусам.Получить();
	Если СтратегияСписанияПартийТоваровПоСтатусам=Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаКупленныеПотомПринятые Тогда
		ПорядокСтатусов="Убыв";
	ИначеЕсли СтратегияСписанияПартийТоваровПоСтатусам=Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаПринятыеПотомКупленные Тогда
		ПорядокСтатусов="Возр";
	Иначе
		ПорядокСтатусов="";
	Конецесли;
	Если СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.ЛИФО Тогда
		ПорядокСписанияПартий="Убыв"; 
	Иначе
		ПорядокСписанияПартий="Возр";
	КонецЕсли;

	Запрос=Новый Запрос();
    ТекстЗапроса="
	|ВЫБРАТЬ
	|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпанииОстатки.СтатусПартии КАК СтатусПартии,";
	// Если возврат то в случае разрешенных "расширенных" режимов возврата нам понадобится контрагент в выборке
	Если (Сторно И РежимВозврата<>Перечисления.РежимыСписанияПриВозврате.ТолькоПоУказаннойПартии) ИЛИ СведенияОПоставщике Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	ПартииТоваровКомпанииОстатки.Партия.Контрагент КАК Контрагент,";
		Если СведенияОПоставщике Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	ПартииТоваровКомпанииОстатки.Партия.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,";
		КонецЕсли;
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,";
	Если (НЕ ПустаяСтрока(ПорядокСтатусов)) И (НЕ Сторно) Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|	ВЫБОР КОГДА ПартииТоваровКомпанииОстатки.СтатусПартии=&Купленные ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПорядокСтатуса,";
		Запрос.УстановитьПараметр("Купленные",Перечисления.СтатусыПартий.ТоварКупленный);
	КонецЕсли;
	// Сформируем фильтр на витруальную таблицу
	ФильтрНаВиртуальнуюТаблицу="Номенклатура В (&Номенклатура)";
	Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
		ФильтрНаВиртуальнуюТаблицу=ФильтрНаВиртуальнуюТаблицу+" И СкладКомпании=&СкладКомпании";
	КонецЕсли;
	Если МассивПартий<>Неопределено И ТипЗнч(МассивПартий)=Тип("Массив") И МассивПартий.Количество()>0 Тогда
		ФильтрНаВиртуальнуюТаблицу=ФильтрНаВиртуальнуюТаблицу+" И Партия В (&Партия)";
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(СтатусПартии) Тогда
		ФильтрНаВиртуальнуюТаблицу=ФильтрНаВиртуальнуюТаблицу+" И СтатусПартии=&СтатусПартии";
	КонецЕсли;
	//
	ТекстЗапроса=ТекстЗапроса+"
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток,0) КАК Количество,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаНДСОстаток,0) КАК СуммаНДС,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаОстаток,0) КАК Сумма,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаУпрОстаток,0) КАК СуммаУпр
	|	
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент,"+ФильтрНаВиртуальнуюТаблицу+") КАК ПартииТоваровКомпанииОстатки
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ПартииТоваровКомпании.Остатки
	|
	|УПОРЯДОЧИТЬ ПО "+?(Сторно ИЛИ ПустаяСтрока(ПорядокСтатусов),"","ПорядокСтатуса "+ПорядокСтатусов+",")+" ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры.Сортировка Возр ,ПартииТоваровКомпанииОстатки.Партия.Дата "+ПорядокСписанияПартий+", ПартииТоваровКомпанииОстатки.Партия "+ПорядокСписанияПартий+"
	|
	|ИТОГИ СУММА(Количество) КАК Количество, СУММА(СуммаНДС) КАК СуммаНДС, СУММА(Сумма) КАК Сумма, СУММА(СуммаУпр) КАК СуммаУпр
	|ПО ПартииТоваровКомпанииОстатки.Номенклатура
	|";

	Запрос.Текст=ТекстЗапроса;
    // Параметры запроса
	Запрос.УстановитьПараметр("Момент",ГраницаРасчетаОстатков);
	Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
	Запрос.УстановитьПараметр("Номенклатура",ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Партия",МассивПартий);
	Запрос.УстановитьПараметр("СтатусПартии",СтатусПартии);

	// Занулим массив партий
	МассивПартий=Неопределено;
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПартииТоваровКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
	ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании); 
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаТоваров);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	//обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
КонецФункции

// формирует движения по сторнированию прихода/расхода
// ВидДвижения - ВидДвиженияРегистраНакопления. Если Приход, тогда у нас возврат от поставщика иначе - от покупателя
// Возвращает Истина - все хорошо, Ложь - чего-то не так.
Функция ВозвратТоваров(ВидДвижения)
	
	ВсеОК = Истина;
	
	Если НЕ обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
		// Сначала попробуем взять стратегию со склада
		СтратегияСписанияПартийТоваровПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
	Иначе
		// Ежели не получилось берем из константы	
		СтратегияСписанияПартийТоваровПоДатам = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	КонецЕсли;
	
	Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.ЛИФО Тогда
		ПорядокСписанияПартий = "Возр";
	Иначе
		ПорядокСписанияПартий = "Убыв";
	КонецЕсли;
	
	ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	Если ВидДвижения=ВидДвиженияНакопления.Приход Тогда
		// ВОЗВРАТ ПОСТАВЩИКУ
		
		// Получим режим возврата.
		Если РежимВозврата=Неопределено Тогда 
			РежимВозврата = обПраво("РежимСписанияПриВозвратеПоставщику",ДокументОбъект.Права,,ДокументОбъект); 
		КонецЕсли;
		
		// Получим таблицу товаров
		Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
			ТаблицаТовары = РезультатЗапросаПоТоварам.Выгрузить();
		Иначе
			ТаблицаТовары = РезультатЗапросаПоТоварам;
		КонецЕсли;
		
		// Получим хитрую таблицу партий.
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпании.Партия КАК Партия,
		|	ПартииТоваровКомпании.Период КАК ДатаПоступления,
		|	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ
		|	ПартииТоваровКомпании
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|ГДЕ
		|	ПартииТоваровКомпании.ВидДвижения   = &ВидДвиженияПриход И 
		|	ПартииТоваровКомпании.СкладКомпании = &СкладКомпании И 
		|	ПартииТоваровКомпании.СтатусПартии = &СтатусПартии И 
		|	ПартииТоваровКомпании.Партия        = ПартииТоваровКомпании.Регистратор И 
		|	ПартииТоваровКомпании.Номенклатура В (&Номенклатура) И 
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ПоступлениеТоваров И ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ПоступлениеТоваров) В (&Партии) ТОГДА
		|			0
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ПоступлениеТоваров И ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ПоступлениеТоваров).Контрагент=&Контрагент ТОГДА
		|			1
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ВводОстатковТоваров И ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ВводОстатковТоваров) В (&Партии) ТОГДА
		|			0
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ВводОстатковТоваров И ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ВводОстатковТоваров).Контрагент=&Контрагент ТОГДА
		|			1
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.АвансовыйОтчет И ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.АвансовыйОтчет) В (&Партии) ТОГДА
		|			0
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.АвансовыйОтчет И ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.АвансовыйОтчет).Контрагент=&Контрагент ТОГДА
		|			1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК ПорядокСписанияПартий,
		|	ПартииТоваровКомпанииОстатки.Номенклатура,
		|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,
		|	ПартииТоваровКомпанииОстатки.СтатусПартии КАК СтатусПартии,
		|	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС,
		|	ПартииТоваровКомпании.ДатаПоступления КАК ДатаПоступления,
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
		|	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК Сумма,
		|	ПартииТоваровКомпанииОстатки.СуммаУпрОстаток КАК СуммаУпр,
		|	ПартииТоваровКомпанииОстатки.СуммаНДСОстаток КАК СуммаНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент, СкладКомпании=&СкладКомпании И Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) И СтатусПартии=&СтатусПартии) КАК ПартииТоваровКомпанииОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|ПО
		|	ПартииТоваровКомпании.Номенклатура                = ПартииТоваровКомпанииОстатки.Номенклатура И 
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры  = ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры И 
		|	ПартииТоваровКомпании.Партия                      = ПартииТоваровКомпанииОстатки.Партия
		|";
		
		Если РежимВозврата = Перечисления.РежимыСписанияПриВозврате.ТолькоПоУказаннойПартии Тогда
			ТекстЗапроса = ТекстЗапроса+"
			|ГДЕ
			|	ВЫБОР
			|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ПоступлениеТоваров ТОГДА
			|			ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ПоступлениеТоваров) В (&Партии)
			|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.АвансовыйОтчет ТОГДА
			|			ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.АвансовыйОтчет) В (&Партии)
			|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ВводОстатковТоваров ТОГДА
			|			ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ВводОстатковТоваров) В (&Партии)
			|		ИНАЧЕ
			|			ЛОЖЬ
			|	КОНЕЦ";
		ИначеЕсли РежимВозврата = Перечисления.РежимыСписанияПриВозврате.ПоВсемПартиямКонтрагента Тогда
			ТекстЗапроса = ТекстЗапроса+"
			|ГДЕ
			|	ВЫБОР
			|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ПоступлениеТоваров ТОГДА
			|			ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ПоступлениеТоваров).Контрагент  = &Контрагент
			|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.АвансовыйОтчет ТОГДА
			|			ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.АвансовыйОтчет).Контрагент      = &Контрагент
			|		КОГДА ПартииТоваровКомпанииОстатки.Партия ССЫЛКА Документ.ВводОстатковТоваров ТОГДА
			|			ВЫРАЗИТЬ(ПартииТоваровКомпанииОстатки.Партия КАК Документ.ВводОстатковТоваров).Контрагент = &Контрагент
			|		ИНАЧЕ
			|			ЛОЖЬ
			|	КОНЕЦ";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровКомпанииОстатки.Номенклатура,
		|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
		|	ПорядокСписанияПартий Возр,
		|	ПартииТоваровКомпании.ДатаПоступления " + ПорядокСписанияПартий + ",
		|	ПартииТоваровКомпанииОстатки.Партия " + ПорядокСписанияПартий + "
		|";
		
		Запрос=Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Момент",                     ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, ШапкаДокумента.МоментВремени));
		Запрос.УстановитьПараметр("СкладКомпании",              СкладКомпании);
		Запрос.УстановитьПараметр("Номенклатура",               ТаблицаТовары.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ТаблицаТовары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
		Запрос.УстановитьПараметр("СтатусПартии",               СтатусПартии);
		Запрос.УстановитьПараметр("Партии",                     ТаблицаТовары.ВыгрузитьКолонку("Партия"));
		Запрос.УстановитьПараметр("Контрагент",                 ШапкаДокумента.Контрагент);
		Запрос.УстановитьПараметр("ВидДвиженияПриход",          ВидДвиженияНакопления.Приход);
		
		// Наложим блокировку на считываемые данные
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПартииТоваровКомпании");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
		ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании); 
		ЗначенияБлокировки.Вставить("СтатусПартии",  СтатусПартии); 
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаТовары);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		
		ТаблицаПартий = Запрос.Выполнить().Выгрузить();
		
		// структура отбора
		СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
		
		Права = ДокументОбъект.Права;
		ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ДокументОбъект).Имя;
		
		// Возвращаем
		Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
			НадоВернуть=СтрокаТовар.Количество;
			// Ищем поступившие партии
			СтруктураОтбора.Номенклатура = СтрокаТовар.Номенклатура;
			СтруктураОтбора.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
			МассивНайденныхСтрок = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
			// Инициализируем переменные для расчета усредненной цены списанных партий
			ОбщаяСумма = 0;
			ОбщаяСуммаУпр = 0;
			ОбщаяСуммаНДС = 0;
			ОбщееКоличество = 0;
			ЕстьПартииТоваров = Ложь; 
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				ТекСтрока = МассивНайденныхСтрок[Сч];
				Если ТекСтрока.Количество=0 Тогда Продолжить; КонецЕсли;
				Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) И СтрокаТовар.Партия<>ТекСтрока.Партия Тогда
					Продолжить;
				КонецЕсли;
				
				ЕстьПартииТоваров = Истина; // если есть партия с которой производим списание, значит и цены будем брать из списаний
				
				Если ТекСтрока.Количество<0 Тогда
					// запомним удельные значения списания для отрицательной партии товаров
					ОбщаяСумма      = ОбщаяСумма      + Окр(ТекСтрока.Сумма, 2);
					ОбщаяСуммаУпр   = ОбщаяСуммаУпр   + Окр(ТекСтрока.СуммаУпр, 2);
					ОбщаяСуммаНДС   = ОбщаяСуммаНДС   + Окр(ТекСтрока.СуммаНДС, 2);
					ОбщееКоличество = ОбщееКоличество + ТекСтрока.Количество;
					Продолжить;
				КонецЕсли;
				
				НоваяЗапись=Добавить();
				НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период                     = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
				Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
					Партия = ПартияТоваровОтрицательныхОстатков;
				Иначе
					Партия = ТекСтрока.Партия;
				КонецЕсли;	
				НоваяЗапись.Партия                     = Партия;
				НоваяЗапись.СтатусПартии               = СтатусПартии;
				НоваяЗапись.СкладКомпании              = СкладКомпании;
				НоваяЗапись.Номенклатура               = ТекСтрока.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
				НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
				// Осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// Осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект                 = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;
				НоваяЗапись.СтавкаНДС                  = ТекСтрока.СтавкаНДС;
				
				// Возвращаем
				Если ТекСтрока.Количество>0 Тогда
					НоваяЗапись.Количество=-Мин(НадоВернуть,ТекСтрока.Количество);
					НоваяЗапись.Сумма=Окр(-?(ТекСтрока.Количество<НадоВернуть,ТекСтрока.Сумма,ТекСтрока.Сумма/ТекСтрока.Количество*НадоВернуть),2);
					НоваяЗапись.СуммаУпр=Окр(-?(ТекСтрока.Количество<НадоВернуть,ТекСтрока.СуммаУпр,ТекСтрока.СуммаУпр/ТекСтрока.Количество*НадоВернуть),2);
					НоваяЗапись.СуммаНДС=Окр(-?(ТекСтрока.Количество<НадоВернуть,ТекСтрока.СуммаНДС,ТекСтрока.СуммаНДС/ТекСтрока.Количество*НадоВернуть),2);
				ИначеЕсли   ТекСтрока.Количество<0 Тогда // в этом случае вернем все, что есть в партии
					НоваяЗапись.Количество=-ТекСтрока.Количество;
					НоваяЗапись.Сумма=Окр(-ТекСтрока.Сумма,2);
					НоваяЗапись.СуммаУпр=Окр(-ТекСтрока.СуммаУпр,2);
					НоваяЗапись.СуммаНДС=Окр(-ТекСтрока.СуммаНДС,2);
				КонецЕсли;
				
				// Запомним удельные значения списания для отрицательной партии товаров
				ОбщаяСумма = ОбщаяСумма + НоваяЗапись.Сумма;
				ОбщаяСуммаУпр = ОбщаяСуммаУпр + НоваяЗапись.СуммаУпр;
				ОбщаяСуммаНДС = ОбщаяСуммаНДС+ НоваяЗапись.СуммаНДС;
				ОбщееКоличество = ОбщееКоличество + НоваяЗапись.Количество;
				
				// Удалим ненужные строки
				ТекСтрока.Количество=ТекСтрока.Количество-НоваяЗапись.Количество;
				Если ТекСтрока.Количество=0 Тогда
					ТаблицаПартий.Удалить(ТекСтрока);
				КонецЕсли; 
				
				// Уменьшаем количество которое надо списать
				НадоВернуть=НадоВернуть-(-НоваяЗапись.Количество);
				Если НадоВернуть<=0 Тогда Прервать; КонецЕсли;
			КонецЦикла;
			
			// Проверим чего осталось
			Если НадоВернуть>0 и ОтрицательныеОстаткиРазрешены  Тогда
				НоваяЗапись = Добавить();
				НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор = ШапкаДокумента.Ссылка;
				НоваяЗапись.Партия = ПартияТоваровОтрицательныхОстатков;
				НоваяЗапись.СтатусПартии = СтатусПартии;
				НоваяЗапись.СкладКомпании = СкладКомпании;
				НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
				// Определяемся с хоз. операцией
				НоваяЗапись.ХозОперация = ШапкаДокумента.ХозОперация;
				// Осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// Осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;
				НоваяЗапись.Количество = -НадоВернуть;
				
				// Теперь попробуем определиться с суммой списания
				Если ЕстьПартииТоваров Тогда
					ЦенаПоследнейПартии = ОбщаяСумма/ОбщееКоличество;
					ЦенаУпрПоследнейПартии = ОбщаяСуммаУпр/ОбщееКоличество;
					УдельныйНДСПоследнейПартии = ОбщаяСуммаНДС/ОбщееКоличество;
				Иначе // В этом случае пробуем достать оценку из регистра сведений Цены по типу цен Нормативная цена
					ТипЦен = Справочники.ТипыЦен.НормативнаяЦена;
					// Получим цену в валюте управленческого учета компании
					ЦенаПоследнейПартии    = обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
					ЦенаУпрПоследнейПартии = обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
					// Розничную оценку берем по типу цен основной тип цен отгрузки
					ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
					ЦенаРозничнаяПоследнейПартии = обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
					// Для расчета НДС сначала определимся со ставкой
					СтавкаНДС = СтрокаТовар.Номенклатура.СтавкаНДС;
					УдельныйНДСПоследнейПартии = (ЦенаПоследнейПартии*СтавкаНДС.Ставка)/100;
				КонецЕсли;
				
				НоваяЗапись.Сумма = Окр(ЦенаПоследнейПартии*НадоВернуть,2);
				НоваяЗапись.СуммаУпр = Окр(ЦенаУпрПоследнейПартии*НадоВернуть,2);
				НоваяЗапись.СуммаНДС = Окр(УдельныйНДСПоследнейПартии*НадоВернуть,2);
				
			ИначеЕсли НадоВернуть>0 Тогда		
				// Не удалось вернуть товар
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не удалось вернуть: "+Формат(НадоВернуть,"ЧДЦ=3; ЧН=0,00"),"Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не удалось вернуть: "+Формат(НадоВернуть,"ЧДЦ=3; ЧН=0,00"),"Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				КонецЕсли; 
				ВсеОК=Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ГТДПартийТоваровКомпанииНаборЗаписей=ДокументОбъект.Движения.ГТДПартийТоваровКомпании;
		ГТДПартийТоваровКомпанииНаборЗаписей.ДокументОбъект = ДокументОбъект;
		ГТДПартийТоваровКомпанииНаборЗаписей.ШапкаДокумента = ШапкаДокумента;
		ГТДПартийТоваровКомпанииНаборЗаписей.ГраницаРасчетаОстатков = ГраницаРасчетаОстатков;
		ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаПартий = ЭтотОбъект.Выгрузить();
		ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаТовары = ТаблицаТовары;
		ГТДПартийТоваровКомпанииНаборЗаписей.СкладКомпании = СкладКомпании;
		ГТДПартийТоваровКомпанииНаборЗаписей.МассивПартий = ЭтотОбъект.Выгрузить().ВыгрузитьКолонку("Партия");
		ГТДПартийТоваровКомпанииНаборЗаписей.РежимПроведения = РежимПроведения;
		ВсеОК = ГТДПартийТоваровКомпанииНаборЗаписей.Расход() И ВсеОК;
		
	Иначе
		
		ПорядокСтатусов       = "";
		ФильтрНаТаблицу       = "";
		Если ЗначениеЗаполнено(СтатусПартии) Тогда
			ФильтрНаТаблицу  = " И 
			|	ТаблицаТоваров.СтатусПартии = &СтатусПартии";
		Иначе
			СтратегияСписанияПартийТоваровПоСтатусам=Константы.СтратегияСписанияПартийТоваровПоСтатусам.Получить();
			Если СтратегияСписанияПартийТоваровПоСтатусам=Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаКупленныеПотомПринятые Тогда
				ПорядокСтатусов = "Возр";
			ИначеЕсли СтратегияСписанияПартийТоваровПоСтатусам=Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаПринятыеПотомКупленные Тогда
				ПорядокСтатусов = "Убыв";
			Иначе
				ПорядокСтатусов = "";
			Конецесли;
		Конецесли;
		
		// ВОЗВРАТ ОТ ПОКУПАТЕЛЯ
		ДанныеДокумента = ?(ТипЗнч(ШапкаДокумента.Ссылка) = Тип("ДокументСсылка.ЗакрытиеСмены"), "ЗакрытиеСмены", "ВозвратОтПокупателя.Товары");
		Расход = 0;
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВозвратОтПокупателяТовары."+ИмяРеквизитаДокумент+" КАК Регистратор
		|ПОМЕСТИТЬ
		|	ВозвратОтПокупателяТовары
		|ИЗ
		|	Документ."+ДанныеДокумента+" КАК ВозвратОтПокупателяТовары
		|ГДЕ
		|	ВозвратОтПокупателяТовары.Ссылка = &Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.ДокументПродажи КАК ДокументПродажи,
		|	ТаблицаТоваров.Партия КАК Партия,
		|	ТаблицаТоваров.Партия.Дата КАК ПартияДата" + ?(ПустаяСтрока(ПорядокСтатусов), "", ",
		|	ВЫБОР
		|		КОГДА ТаблицаТоваров.СтатусПартии = &Купленные ТОГДА
		|			1
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ КАК ПорядокСтатуса") + ",
		|	ТаблицаТоваров.СтатусПартии КАК СтатусПартии,
		|	ТаблицаТоваров.ГТД КАК ГТД,
		|	СУММА(ТаблицаТоваров.Количество) КАК Количество,
		|	СУММА(ТаблицаТоваров.Себестоимость) КАК Сумма,
		|	СУММА(ТаблицаТоваров.СебестоимостьУпр) КАК СуммаУпр,
		|	СУММА(ТаблицаТоваров.СуммаНДСВходящий) КАК СуммаНДС
		|ИЗ
		|	РегистрНакопления.Продажи КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.Период <= &НаМомент " + ФильтрНаТаблицу + " И 
		|	ТаблицаТоваров.Номенклатура В (&Номенклатура) И 
		|	(НЕ ТаблицаТоваров.Регистратор = &Ссылка) И 
		|	ТаблицаТоваров.ДокументПродажи В 
		|	(ВЫБРАТЬ
		|		ВозвратОтПокупателяТовары.Регистратор
		|	ИЗ
		|		ВозвратОтПокупателяТовары КАК ВозвратОтПокупателяТовары" + ?(ИмяРеквизитаДокумент="Ссылка", "", "
		|	ГДЕ
		|		ВозвратОтПокупателяТовары.Регистратор ССЫЛКА Документ.РеализацияТоваров ИЛИ
		|		ВозвратОтПокупателяТовары.Регистратор ССЫЛКА Документ.ЗакрытиеСмены ИЛИ 
		|		ВозвратОтПокупателяТовары.Регистратор ССЫЛКА Документ.ЗаказНаряд ИЛИ 
		|		ВозвратОтПокупателяТовары.Регистратор ССЫЛКА Документ.Инвентаризация")+")
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.ДокументПродажи,
		|	ТаблицаТоваров.Партия,
		|	ТаблицаТоваров.СтатусПартии,
		|	ТаблицаТоваров.ГТД
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаТоваров.Количество) <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.ДокументПередачи,
		|	ТаблицаТоваров.Партия,
		|	ТаблицаТоваров.Партия.Дата" + ?(ПустаяСтрока(ПорядокСтатусов), "", ",
		|	1") + ",
		|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный),
		|	ТаблицаТоваров.ГТД,
		|	СУММА(ВЫБОР
		|		КОГДА ТаблицаТоваров.ВидДвижения = &ВидДвижения ТОГДА
		|			-ТаблицаТоваров.Количество
		|		ИНАЧЕ
		|			ТаблицаТоваров.Количество
		|	КОНЕЦ),
		|	СУММА(ВЫБОР
		|		КОГДА ТаблицаТоваров.ВидДвижения = &ВидДвижения ТОГДА
		|			-ТаблицаТоваров.СуммаСебестоимостиРегл
		|		ИНАЧЕ
		|			ТаблицаТоваров.СуммаСебестоимостиРегл
		|	КОНЕЦ),
		|	СУММА(ВЫБОР
		|		КОГДА ТаблицаТоваров.ВидДвижения = &ВидДвижения ТОГДА
		|			-ТаблицаТоваров.СуммаСебестоимостиУпр
		|		ИНАЧЕ
		|			ТаблицаТоваров.СуммаСебестоимостиУпр
		|	КОНЕЦ),
		|	СУММА(ВЫБОР
		|		КОГДА ТаблицаТоваров.Сумма = 0 ТОГДА
		|			0
		|		КОГДА ТаблицаТоваров.ВидДвижения = &ВидДвижения ТОГДА
		|			-((ТаблицаТоваров.СуммаНДС*ТаблицаТоваров.СуммаСебестоимостиРегл)/ТаблицаТоваров.Сумма)
		|		ИНАЧЕ
		|			(ТаблицаТоваров.СуммаНДС*ТаблицаТоваров.СуммаСебестоимостиРегл)/ТаблицаТоваров.Сумма
		|	КОНЕЦ)
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.Период <= &НаМомент И 
		|	ТаблицаТоваров.ВидДвижения <> &ВидДвижения " + ФильтрНаТаблицу + " И 
		|	ТаблицаТоваров.Номенклатура В (&Номенклатура) И 
		|	(НЕ ТаблицаТоваров.Регистратор = &Ссылка) И 
		|	ТаблицаТоваров.ДокументПередачи В 
		|	(ВЫБРАТЬ
		|		ВозвратОтПокупателяТовары.Регистратор
		|	ИЗ
		|		ВозвратОтПокупателяТовары КАК ВозвратОтПокупателяТовары)
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.ДокументПередачи,
		|	ТаблицаТоваров.Партия,
		|	ТаблицаТоваров.ГТД
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|		КОГДА ТаблицаТоваров.ВидДвижения = &ВидДвижения ТОГДА
		|			-ТаблицаТоваров.Количество
		|		ИНАЧЕ
		|			ТаблицаТоваров.Количество
		|	КОНЕЦ) <> 0
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ПартииТоваровОтданные
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры " + ?(ПустаяСтрока(ПорядокСтатусов), "", ",
		|	ПорядокСтатуса " + ПорядокСтатусов) + ",
		|	ПартияДата " + ПорядокСписанияПартий + ",
		|	Партия " + ПорядокСписанияПартий + "
		|");
		Запрос.УстановитьПараметр("Номенклатура", РезультатЗапросаПоТоварам.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("Ссылка",       ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("ВидДвижения",  ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("НаМомент",     ШапкаДокумента.Дата);
		Запрос.УстановитьПараметр("Купленные",    Перечисления.СтатусыПартий.ТоварКупленный);
		
		// Наложим блокировку на считываемые данные
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПартииТоваровКомпании");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
		ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании); 
		ЗначенияБлокировки.Вставить("СтатусПартии", Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		// Блокировка товаров отданных на комиссию (вводом остатков товаров)
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПартииТоваровОтданные");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		// Блокировка товаров в производстве
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ТоварыВПроизводстве");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
		ЗначенияБлокировки.Вставить("СтатусПартии", Перечисления.СтатусыПартий.ТоварПринятыйКомиссия); 
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		// РеализованныеТовары
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "РеализованныеТовары");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		
		РезультатЗапросаВозвратОтПокупателя=Запрос.Выполнить();
		ТаблицаОтгрузки=РезультатЗапросаВозвратОтПокупателя.Выгрузить();
		
		// Пройдемся по таблице товаров и отсторнируем отгрузку
		Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
			ТаблицаТовары=РезультатЗапросаПоТоварам.Выгрузить();
		Иначе
			ТаблицаТовары=РезультатЗапросаПоТоварам;
		КонецЕсли;
		
		Если ДокументОбъект.ЭтоНовый() ИЛИ ДокументОбъект.Ссылка<>ШапкаДокумента.Ссылка Тогда
			// проведение отложенное надо таблицу получать запросом
			Запрос=Новый Запрос("
			|ВЫБРАТЬ
			|	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры,
			|	СУММА(ОстаткиТоваровКомпании.Количество) КАК Количество
			|ИЗ
			|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
			|ГДЕ
			|	ОстаткиТоваровКомпании.Регистратор=&Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры
			|");
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			ТаблицаСписанияОстатков=Запрос.Выполнить().Выгрузить();
		Иначе
			// Проведение НЕ отложенное
			ТаблицаСписанияОстатков=ДокументОбъект.Движения.ОстаткиТоваровКомпании.Выгрузить();
			ТаблицаСписанияОстатков.Свернуть("ХарактеристикаНоменклатуры","Количество");
		КонецЕсли;
		
		ВалютаРегл     = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
		КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ВалютаУпр      = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
		КурсУпр        =  СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		ТипЦенСклада = Неопределено;
		Если ШапкаДокумента.СкладКомпании.Розничный Тогда
			ТипЦенСклада = ШапкаДокумента.СкладКомпании.ТипЦенРозничнойТорговли;
		КонецЕсли;
		
		// Получим таблицу номенклатуры с ручным списанием характеристик
		ТаблицаРучныхХарактеристик=дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(ШапкаДокумента.Ссылка);
		
		ТипЦенНормативнаяЦена = Справочники.ТипыЦен.НормативнаяЦена;
		
		ЕстьСтавкаНДС = (ТаблицаТовары.Колонки.Найти("СтавкаНДС")<>Неопределено);
		ЕстьГТД       = (ТаблицаТовары.Колонки.Найти("ГТД")<>Неопределено);
		
		ГТДПартийТоваровКомпанииНаборЗаписей=ДокументОбъект.Движения.ГТДПартийТоваровКомпании;
		
		Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
			//Получим остатки по текущей номенклатуре
			СтруктураОтбора=Новый Структура("Номенклатура, ДокументПродажи",СтрокаТовар.Номенклатура,СтрокаТовар.ДокументПродажи);
			
			Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ (ТаблицаРучныхХарактеристик<>Неопределено И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда 
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
			КонецЕсли;
			
			Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) 
				И обПраво("ВыборочноеСписаниеПартий",ДокументОбъект.Права,,ЭтотОбъект)Тогда
				СтруктураОтбора.Вставить("Партия",СтрокаТовар.Партия);
			КонецЕсли;
			
			Если ЕстьГТД И ЗначениеЗаполнено(СтрокаТовар.ГТД) Тогда
				СтруктураОтбора.Вставить("ГТД", СтрокаТовар.ГТД);
			КонецЕсли;
			
			// Поищем строки удовлетворяющие условию
			МассивНайденныхСтрок    = ТаблицаОтгрузки.НайтиСтроки(СтруктураОтбора);
			// Пройдемся по найденным строкам и вернем товар
			НадоВернуть             = СтрокаТовар.Количество;
			ЦенаПоследнейПартииРегл = Неопределено;
			ЦенаПоследнейПартииУпр  = Неопределено;
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				ТекСтрока  = МассивНайденныхСтрок[Сч];
				Возвращаем = Мин(НадоВернуть,ТекСтрока.Количество);
				
				// Проверим, а не разойдемся ли мы с остатками
				СтрокаОстатков = ТаблицаСписанияОстатков.Найти(ТекСтрока.ХарактеристикаНоменклатуры, "ХарактеристикаНоменклатуры");
				Если СтрокаОстатков = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Возвращаем = Мин(Возвращаем,СтрокаОстатков.Количество);
				СтрокаОстатков.Количество = СтрокаОстатков.Количество-Возвращаем;
				Если СтрокаОстатков.Количество<=0 Тогда
					ТаблицаСписанияОстатков.Удалить(СтрокаОстатков);
				КонецЕсли;
				
				НоваяЗапись = Добавить();
				НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Расход;
				НоваяЗапись.Период                     = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
				НоваяЗапись.СтатусПартии               = ТекСтрока.СтатусПартии;
				
				Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
					Партия = ПартияТоваровОтрицательныхОстатков;
				Иначе
					Партия = ТекСтрока.Партия;
				КонецЕсли;
				НоваяЗапись.Партия                     = Партия;
				НоваяЗапись.СкладКомпании              = СкладКомпании;
				НоваяЗапись.Номенклатура               = ТекСтрока.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
				НоваяЗапись.ДокументПродажи            = ТекСтрока.ДокументПродажи;
				НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
				// Осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// Осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект                 = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;
				Если ЕстьСтавкаНДС Тогда
					НоваяЗапись.СтавкаНДС              = СтрокаТовар.СтавкаНДС;
				КонецЕсли;
				НоваяЗапись.Количество                 = -Возвращаем;
				НоваяЗапись.Сумма                      = Окр(-?(ТекСтрока.Количество<=Возвращаем,ТекСтрока.Сумма,ТекСтрока.Сумма/ТекСтрока.Количество*Возвращаем),2);
				НоваяЗапись.СуммаУпр                   = Окр(-?(ТекСтрока.Количество<=Возвращаем,ТекСтрока.СуммаУпр,ТекСтрока.СуммаУпр/ТекСтрока.Количество*Возвращаем),2);
				НоваяЗапись.СуммаНДС                   = Окр(-?(ТекСтрока.Количество<=Возвращаем,ТекСтрока.СуммаНДС,ТекСтрока.СуммаНДС/ТекСтрока.Количество*Возвращаем),2);
				Расход = Расход+(-НоваяЗапись.СуммаУпр);
				ЦенаПоследнейПартииРегл = НоваяЗапись.Сумма/НоваяЗапись.Количество;
				ЦенаПоследнейПартииУпр  = НоваяЗапись.СуммаУпр/НоваяЗапись.Количество;
				
				ТекСтрока.Количество                   = ТекСтрока.Количество-(-НоваяЗапись.Количество);
				//ТекСтрока.КоличествоРеализованных      = ТекСтрока.КоличествоРеализованных-(-НоваяЗапись.Количество);
				ТекСтрока.Сумма                        = ТекСтрока.Сумма-(-НоваяЗапись.Сумма);
				ТекСтрока.СуммаУпр                     = ТекСтрока.СуммаУпр-(-НоваяЗапись.СуммаУпр);
				ТекСтрока.СуммаНДС                     = ТекСтрока.СуммаНДС-(-НоваяЗапись.СуммаНДС);
				
				Если ЕстьГТД Тогда
					ГТДПартийТоваровКомпанииНаборЗаписей.ДобавитьЗапись(НоваяЗапись,ТекСтрока.ГТД);
				КонецЕсли;
				
				// Уменьшаем количество которое надо списать
				НадоВернуть=НадоВернуть-(-НоваяЗапись.Количество);
				
				// Если списали все с этой характеристикой, то удалим данную строку.
				// Если нет, то уменьшим количество остатка
				Если ТекСтрока.Количество>0 Тогда
					//Продолжаем списывать данную партию - там еще осталось
					Сч=Сч-1;
				Иначе
					// Удалим ненужную строку
					ТаблицаОтгрузки.Удалить(ТекСтрока);
				КонецЕсли;
				Если НадоВернуть<=0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			// Если после всяких возвратов у нас еще остался товар, который вернуть не удалось, то
			// оприходуем этот товар на документ возврата
			Если НадоВернуть>0 Тогда
				НоваяЗапись = Добавить();
				НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
				НоваяЗапись.Период      = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор = ШапкаДокумента.Ссылка;
				Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
					Партия = ПартияТоваровОтрицательныхОстатков;
				ИначеЕсли ТипЗнч(ШапкаДокумента.Ссылка) = Тип("ДокументСсылка.ЗакрытиеСмены") Тогда
					Партия = СтрокаТовар.Партия;
				Иначе
					Партия = ШапкаДокумента.Ссылка;
				КонецЕсли;
				НоваяЗапись.Партия                     = Партия;
				НоваяЗапись.СтатусПартии               = Перечисления.СтатусыПартий.ТоварКупленный;
				НоваяЗапись.СкладКомпании              = СкладКомпании;
				НоваяЗапись.Номенклатура               = СтрокаТовар.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
				// определяемся с хоз. операцией
				НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
				// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект                 = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;
				Если ЕстьСтавкаНДС Тогда
					НоваяЗапись.СтавкаНДС              = СтрокаТовар.СтавкаНДС;
				КонецЕсли; 
				НоваяЗапись.Количество                 = -НадоВернуть;
				
				Попытка
					НоваяЗапись.ДокументПродажи = СтрокаТовар.ДокументПродажи;
				Исключение
				КонецПопытки;
				
				Если ЦенаПоследнейПартииРегл=Неопределено ИЛИ ЦенаПоследнейПартииУпр=Неопределено Тогда
					ПолучитьЦенуЗакупки = Истина;
					Попытка
						НоваяЗапись.Сумма    = -СтрокаТовар.Сумма;
						НоваяЗапись.СуммаУпр = -СтрокаТовар.СуммаУпр;
						НоваяЗапись.СуммаНДС = -СтрокаТовар.СуммаНДС;
						ПолучитьЦенуЗакупки = Ложь;
					Исключение
					КонецПопытки;
					
					Если ПолучитьЦенуЗакупки Тогда
						ЦенаЗакупки=Неопределено;
						Если обЗначениеНеЗаполнено(СтрокаТовар.ДокументПродажи) Тогда
							Попытка
								ЦенаЗакупки = СтрокаТовар.Себестоимость;
							Исключение
							КонецПопытки;
						КонецЕсли;
						Если ЦенаЗакупки = Неопределено Тогда
							// получим последние нормативные цены
							ЦенаЗакупки=обПолучитьЦену(ТипЦенНормативнаяЦена,СтрокаТовар.Номенклатура,ШапкаДокумента.Ссылка,,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
							Если НЕ ТипЦенНормативнаяЦена.ЦенаВключаетНДС И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Номенклатура.СтавкаНДС) Тогда
								ЦенаЗакупки = ЦенаЗакупки+(ЦенаЗакупки*СтрокаТовар.Номенклатура.СтавкаНДС.Ставка/100);
							КонецЕсли;
						КонецЕсли; 
						СуммаВсего = ЦенаЗакупки*НадоВернуть;
						Если обЗначениеНеЗаполнено(СтрокаТовар.Номенклатура.СтавкаНДС) Тогда
							СуммаНДС=0;
						Иначе
							СуммаБезНДС = (100*СуммаВсего)/(100+СтрокаТовар.Номенклатура.СтавкаНДС.Ставка);
							СуммаНДС    = СуммаВсего-СуммаБезНДС;
						КонецЕсли;
						НоваяЗапись.Сумма    = -Окр(обПересчет(СуммаВсего,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл),2);
						НоваяЗапись.СуммаУпр = -Окр(обПересчет(СуммаВсего,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр),2);
					КонецЕсли;
				Иначе
					НоваяЗапись.Сумма    = -(ЦенаПоследнейПартииРегл*НадоВернуть);
					НоваяЗапись.СуммаУпр = -(ЦенаПоследнейПартииУпр*НадоВернуть);
					СуммаБезНДС = НоваяЗапись.Сумма/((100+НоваяЗапись.СтавкаНДС.Ставка)/100);
					НоваяЗапись.СуммаНДС = НоваяЗапись.Сумма-СуммаБезНДС;
				КонецЕсли;
				Расход      = Расход+(-НоваяЗапись.СуммаУпр);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// запись движений
	ЗаписыватьДвижения = ?(ЗаписыватьДвижения=Неопределено,Ложь,ЗаписыватьДвижения);
	Если ВсеОК И ЗаписыватьДвижения Тогда
		Записать();
	КонецЕсли;
	
	// убиваем циклическую ссылку
	РезультатЗапросаПоТоварам = Неопределено;
	СкладКомпанииКуда         = Неопределено;
	Сторно                    = Неопределено;
	СтатусПартии              = Неопределено;
	РежимВозврата             = Неопределено;
	ДокументОбъект            = Неопределено;
	ШапкаДокумента            = Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Формирует движения по регистру приход (увеличение товарного запаса)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция Приход() Экспорт
	ЕстьОшибки = ЛОЖЬ;
	Сторно=?(Сторно=Неопределено,Ложь,Сторно);
	ЕстьСтавкаНДС = ?(ЕстьСтавкаНДС = Неопределено, ЛОЖЬ, ЕстьСтавкаНДС);
	РежимДопРасходы=?(РежимДопРасходы=Неопределено,0,РежимДопРасходы);
	
	// получим товары
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса") И ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		// если неопределен результат запроса по таблице товаров, значит она в документе.
		РезультатЗапросаПоТоварам=ПолучитьТаблицуТоваров(ВидДвиженияНакопления.Приход);
	КонецЕсли;
	// если передали результат запроса, то выгрузим в таблицу значений
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	// сначала попробуем взять стратегию со склада
	СтратегияСписанияКонстанта = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	СтратегияСписанияПартийТоваровПоДатам = Неопределено;
	Если ЗначениеЗаполнено(СкладКомпании) Тогда
		Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
			// ежели не получилось берем из константы	
			СтратегияСписанияПартийТоваровПоДатам = СтратегияСписанияКонстанта;
		Иначе
			СтратегияСписанияПартийТоваровПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
		КонецЕсли;
	КонецЕсли; 
	ПартияТоваровОтрицательныхОстатков = Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	
	// получим права пользователя
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки",ДокументОбъект.Права,,ДокументОбъект)<>Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	Если ОтрицательныеОстаткиРазрешены Тогда
		ВыводитьКритическиеСообщения = обПраво("ВыводитьСообщенияПроверкиПравДоступа",ДокументОбъект.Права,,ДокументОбъект);
	Иначе
		ВыводитьКритическиеСообщения = Ложь;
	КонецЕсли;
	
	// проверим на возврат
	Если Сторно Тогда
		Возврат ВозвратТоваров(ВидДвиженияНакопления.Приход);
	КонецЕсли;
	
	// посмотрим, что у нас с доп. расходами
	Если НЕ обЗначениеНеЗаполнено(РежимДопРасходы) Тогда
		МассивОшибок                 = Новый Массив;
		СуммаДопРасходов             = орРасчетДополнительныхРасходов(РезультатЗапросаПоТоварам, ШапкаДокумента.Ссылка, МассивОшибок, РежимДопРасходы);
		
		// проверим распределение доп. расходов
		Если МассивОшибок.Количество()<>0 Тогда
			ЕстьОшибки = ИСТИНА; 
			Для каждого ТекОшибка Из МассивОшибок Цикл  	
				дкСообщитьРезультат(ТекОшибка,,ДокументОбъект); 	
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// приходуем партии
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ШапкаДокумента.КурсВалютыУпр;
	КонецЕсли;	
	
	ЕстьГТД=РезультатЗапросаПоТоварам.Колонки.Найти("ГТД")<>Неопределено;
	Если ЕстьГТД Тогда
		ГТДПартийТоваровКомпанииНаборЗаписей=ДокументОбъект.Движения.ГТДПартийТоваровКомпании;
	КонецЕсли; 
	Для Каждого СтрокаТовар Из РезультатЗапросаПоТоварам Цикл
		// пропустим пустые строки
		Если РежимДопРасходы=2 И СтрокаТовар.СуммаВсего=0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СкладКомпании) Тогда
			СкладДвижения=СкладКомпании;
		Иначе
			СкладДвижения=СтрокаТовар.СкладКомпании;
		КонецЕсли;
			
		// получим партию
		Если СтратегияСписанияПартийТоваровПоДатам=Неопределено Тогда
			Если ЗначениеЗаполнено(СкладКомпании) Тогда
				Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
					// ежели не получилось берем из константы	
					СтратегияСписания = СтратегияСписанияКонстанта;
				Иначе
					СтратегияСписания = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
				КонецЕсли;
			Иначе
				Если обЗначениеНеЗаполнено(СтрокаТовар.СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
					// ежели не получилось берем из константы	
					СтратегияСписания = СтратегияСписанияКонстанта;
				Иначе
					СтратегияСписания = СтрокаТовар.СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
				КонецЕсли;
			КонецЕсли; 
			Если СтратегияСписания = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
				Партия = ПартияТоваровОтрицательныхОстатков;
			Иначе
				Попытка
					Партия = СтрокаТовар.Партия;
				Исключение
					Партия = ?(РежимДопРасходы=2, ШапкаДокумента.ДокументОснование, ШапкаДокумента.Ссылка);
				КонецПопытки;
			КонецЕсли;
		Иначе
			Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
				Партия = ПартияТоваровОтрицательныхОстатков;
			Иначе
				Попытка
					Партия = СтрокаТовар.Партия;
				Исключение
					Партия = ?(РежимДопРасходы=2, ШапкаДокумента.ДокументОснование, ШапкаДокумента.Ссылка);
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтатусПартии) Тогда
			СтатусПартииДвижения=СтатусПартии;
		Иначе
			СтатусПартииДвижения=СтрокаТовар.СтатусПартии;	
		КонецЕсли; 
		
		// записываем движения
		НоваяЗапись = Добавить();
		НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период						= ШапкаДокумента.Дата;
		НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
		НоваяЗапись.Партия						= Партия;
		НоваяЗапись.СтатусПартии				= СтатусПартииДвижения;
		НоваяЗапись.СкладКомпании				= СкладДвижения;
		НоваяЗапись.Номенклатура				= СтрокаТовар.Номенклатура;
		НоваяЗапись.ХарактеристикаНоменклатуры	= СтрокаТовар.ХарактеристикаНоменклатуры;
		НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
		// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
		// осуществляющих движения по данному регистру есть данный реквизит. 
		Попытка
			НоваяЗапись.Проект                  = ШапкаДокумента.Проект;
		Исключение
		КонецПопытки;
		
		Попытка
			НоваяЗапись.ДокументПродажи         = ШапкаДокумента.ДокументПродажи;
		Исключение
		КонецПопытки;
		
		Если ЕстьСтавкаНДС Тогда
			НоваяЗапись.СтавкаНДС = СтрокаТовар.СтавкаНДС;
		КонецЕсли;
		НоваяЗапись.Количество=СтрокаТовар.Количество;
		НоваяЗапись.Сумма=Окр(?(СтрокаТовар.СуммаВсего=NULL,0,обПересчет(СтрокаТовар.СуммаВсего,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		НоваяЗапись.СуммаУпр=Окр(?(СтрокаТовар.СуммаВсего=NULL,0,обПересчет(СтрокаТовар.СуммаВсего,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр)),2);
		НоваяЗапись.СуммаНДС=Окр(?(СтрокаТовар.СуммаНДС=NULL,0,обПересчет(СтрокаТовар.СуммаНДС,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		
		Если ЕстьГТД Тогда
			ГТДПартийТоваровКомпанииНаборЗаписей.ДобавитьЗапись(НоваяЗапись,СтрокаТовар.ГТД);
		КонецЕсли; 
	КонецЦикла;
	
	// запись движений
	ЗаписыватьДвижения = ?(ЗаписыватьДвижения = Неопределено, ЛОЖЬ, ЗаписыватьДвижения);
	Если НЕ ЕстьОшибки И ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// обнуляем переменные
	РезультатЗапросаПоТоварам	= Неопределено;
	ГраницаРасчетаОстатков		= Неопределено;
	СтатусПартии				= Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект				= Неопределено;
	ШапкаДокумента				= Неопределено;
	
	// все ОК
	Возврат НЕ ЕстьОшибки;
КонецФункции

// Формирует движения по регистру расход (уменьшение товарного запаса)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция Расход() Экспорт
	ВсеОК=Истина;
	ЕстьСтавкаНДС=?(ЕстьСтавкаНДС=Неопределено,Ложь,ЕстьСтавкаНДС);
	Сторно=?(Сторно=Неопределено,Ложь,Сторно);
	ПартииУказаны=НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент);
	
	// получим права пользователя
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки",ДокументОбъект.Права,,ДокументОбъект)<>Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	Если ОтрицательныеОстаткиРазрешены Тогда
		ВыводитьКритическиеСообщения = обПраво("ВыводитьСообщенияПроверкиПравДоступа",ДокументОбъект.Права,,ДокументОбъект);
	Иначе
		ВыводитьКритическиеСообщения = Ложь;
	КонецЕсли;
	
	// проверим на возврат
	Если Сторно Тогда Возврат ВозвратТоваров(ВидДвиженияНакопления.Расход); КонецЕсли;
	
	ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	// получим товары
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса") И ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		ТаблицаТовары=ПолучитьТаблицуТоваров(ВидДвиженияНакопления.Расход);
	Иначе
		ТаблицаТовары=РезультатЗапросаПоТоварам;
	КонецЕсли;
	Если ТипЗнч(ТаблицаТовары)=Тип("РезультатЗапроса") Тогда
		ТаблицаТовары=ТаблицаТовары.Выгрузить();
	КонецЕсли;
	
	// списываем партии
	ДеревоПартий=ПолучитьДеревоПартий(ТаблицаТовары);
	// идем по таблице товаров и списываем партии
	Расход=0;
	// упорядочим по нормальному
	ТаблицаТовары.Сортировать("Номенклатура Возр,ХарактеристикаНоменклатуры Убыв"+?(НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент),",Партия Убыв",""));
	
	// получим таблицу номенклатуры с ручным списанием характеристик
	ТаблицаРучныхХарактеристик=дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(ШапкаДокумента.Ссылка);
	
	Права=ДокументОбъект.Права;
	ИмяКолонкиКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ДокументОбъект).Имя;
	
	Если НЕ ТаблицаВозвратов = Неопределено Тогда
		ТаблицаВозвратовПартии = ТаблицаВозвратов.Скопировать();
		ТаблицаВозвратовПартии.Очистить();
		ТаблицаВозвратовПартии.Колонки.Добавить("Партия");
	Иначе
		ТаблицаВозвратовПартии = Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
		НадоСписать=Окр(СтрокаТовар.Количество,3);
		// получим строки таблицы партий с нашим товаром
		СтрокаПартийНоменклатуры=ДеревоПартий.Строки.Найти(СтрокаТовар.Номенклатура,"Номенклатура");
		ПоследнийСтатус=Перечисления.СтатусыПартий.ТоварКупленный;
		
		// инициализируем переменные для расчета усредненной цены списанных партий
		ОбщаяСумма = 0; ОбщаяСуммаУпр=0; ОбщаяСуммаНДС = 0; ОбщееКоличество = 0;
		ЕстьПартииТоваров = Ложь;
		
		КоличествоВозврата = 0;
		СтрокаВозврата = Неопределено;
		Если НЕ ТаблицаВозвратов = Неопределено Тогда
			СтруктураОтбораВозврата=Новый Структура();
			СтруктураОтбораВозврата.Вставить("Номенклатура", СтрокаТовар.Номенклатура);
			СтруктураОтбораВозврата.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
			СтрокиВозвратов = ТаблицаВозвратов.НайтиСтроки(СтруктураОтбораВозврата);
			Если СтрокиВозвратов.Количество() > 0 Тогда
				КоличествоВозврата = СтрокиВозвратов[0].Количество;
				СтрокаВозврата = СтрокиВозвратов[0];
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаПартийНоменклатуры<>Неопределено Тогда
			СтруктураОтбора=Новый Структура("Номенклатура");
			СтруктураОтбора.Номенклатура=СтрокаТовар.Номенклатура; 
			Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ (ТаблицаРучныхХарактеристик<>Неопределено И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда 
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
			КонецЕсли;
			МассивНайденныхСтрок=СтрокаПартийНоменклатуры.Строки.НайтиСтроки(СтруктураОтбора);
			// теперь идем по партиям товаров и списываем в соответствии с выбранной стратегией
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				ТекСтрока=МассивНайденныхСтрок[Сч];
				// проверки на нулевую партию или партию отрицательных остатков
				Если ТекСтрока.Количество=NULL ИЛИ ТекСтрока.Количество=0 Тогда Продолжить; КонецЕсли;
				
				// если указаны партии, то пропустим все партии не наши
				Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) И ТекСтрока.Партия<>СтрокаТовар.Партия Тогда
					Продолжить;
				КонецЕсли;
				
				//БизТех Аляль 23.12.2024г. проверить партию на заказ по гарантийным ЗН <<
				//Если Не ПартииУказаны и ПроверитьПартиюНаЗаказПоГарантии(ТекСтрока.Партия,ТекСтрока.Номенклатура) Тогда
				//	Продолжить;
				//КонецЕсли;  
				//БизТех Аляль >> 

				ЕстьПартииТоваров = Истина; // если есть партия с которой производим списание, значит и цены будем брать из списаний
				
				Если ТекСтрока.Количество<0 Тогда
					// запомним удельные значения списания для отрицательной партии товаров
					ОбщаяСумма      = ОбщаяСумма      + Окр(ТекСтрока.Сумма, 2);
					ОбщаяСуммаУпр   = ОбщаяСуммаУпр   + Окр(ТекСтрока.СуммаУпр, 2);
					ОбщаяСуммаНДС   = ОбщаяСуммаНДС   + Окр(ТекСтрока.СуммаНДС, 2);
					ОбщееКоличество = ОбщееКоличество + ТекСтрока.Количество;
					Продолжить;
				КонецЕсли;
				
				НоваяЗапись = Добавить();
				НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Расход;
				НоваяЗапись.Период                     = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
				Партия = ТекСтрока.Партия;
				НоваяЗапись.Партия                     = Партия;
				НоваяЗапись.СтатусПартии               = ТекСтрока.СтатусПартии;
				ПоследнийСтатус = ТекСтрока.СтатусПартии;
				НоваяЗапись.СкладКомпании              = СкладКомпании;
				НоваяЗапись.Номенклатура               = ТекСтрока.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
		        // определяемся с хоз. операцией
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;
				Проект = НоваяЗапись.Проект;
				
				Попытка
					НоваяЗапись.ДокументПродажи = ШапкаДокумента.ДокументПродажи;
				Исключение
				КонецПопытки;
				ДокументПродажи = НоваяЗапись.ДокументПродажи;
				
				Если ТекСтрока.Количество > НадоСписать И ТекСтрока.Количество>0 Тогда
					КоличествоСписания	= НадоСписать;
					СуммаСписания	 	= Окр(ТекСтрока.Сумма/ТекСтрока.Количество*НадоСписать, 2);
					СуммаУпрСписания 	= Окр(ТекСтрока.СуммаУпр/ТекСтрока.Количество*НадоСписать, 2);
					СуммаНДССписания	= Окр(ТекСтрока.СуммаНДС/ТекСтрока.Количество*НадоСписать, 2);
				Иначе
					КоличествоСписания	= ТекСтрока.Количество;
					СуммаСписания	 	= Окр(ТекСтрока.Сумма, 2);
					СуммаУпрСписания 	= Окр(ТекСтрока.СуммаУпр, 2);
					СуммаНДССписания	= Окр(ТекСтрока.СуммаНДС, 2);
				КонецЕсли;
				
				НоваяЗапись.Количество	= КоличествоСписания;
				НоваяЗапись.Сумма		= СуммаСписания;
				НоваяЗапись.СуммаУпр	= СуммаУпрСписания;
				НоваяЗапись.СуммаНДС	= СуммаНДССписания;
				
				Расход=Расход+СуммаУпрСписания;
				
				// запомним удельные значения списания для отрицательной партии товаров
				ОбщаяСумма = ОбщаяСумма + СуммаСписания;
				ОбщаяСуммаУпр = ОбщаяСуммаУпр + СуммаУпрСписания;
				ОбщаяСуммаНДС = ОбщаяСуммаНДС + СуммаНДССписания;
				ОбщееКоличество = ОбщееКоличество + КоличествоСписания;
							
				// если выбран склад куда, тогда сразу оприходуем партию на склад-получатель
				Если НЕ обЗначениеНеЗаполнено(СкладКомпанииКуда) Тогда
					// сохраним себестоимость и количество
					НоваяЗапись=Добавить();
					НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
					НоваяЗапись.Период=ШапкаДокумента.Дата;
					НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
					Если СтратегияСписанияПартийТоваровПоДатамКуда=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
						Партия = ПартияТоваровОтрицательныхОстатков;
					Иначе
						Партия=ТекСтрока.Партия;
					КонецЕсли;	
					НоваяЗапись.Партия = Партия;
					НоваяЗапись.СтатусПартии=ТекСтрока.СтатусПартии;
					НоваяЗапись.СкладКомпании=СкладКомпанииКуда;
					НоваяЗапись.Номенклатура=ТекСтрока.Номенклатура;
					НоваяЗапись.ХарактеристикаНоменклатуры=ТекСтрока.ХарактеристикаНоменклатуры;
					НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
					// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
					// осуществляющих движения по данному регистру есть данный реквизит. 
					Попытка
						НоваяЗапись.Проект = ШапкаДокумента.Проект;
					Исключение
					КонецПопытки;	
					НоваяЗапись.Количество=КоличествоСписания;
					НоваяЗапись.Сумма=СуммаСписания; НоваяЗапись.СуммаУпр=СуммаУпрСписания; НоваяЗапись.СуммаНДС=СуммаНДССписания;
				КонецЕсли;
				// уменьшим ресурсы для остатка текущей партии
				Если КоличествоВозврата <> 0 Тогда
					
					Возвращено = Мин(ТекСтрока.Количество, КоличествоВозврата,КоличествоСписания);
					
					// Приход
					НоваяЗапись=Добавить();
					НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
					НоваяЗапись.Период=ШапкаДокумента.Дата;
					НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
					Партия=ТекСтрока.Партия;
					НоваяЗапись.Партия = Партия;
					НоваяЗапись.СтатусПартии=ТекСтрока.СтатусПартии;
					НоваяЗапись.СкладКомпании=СкладКомпании;
					НоваяЗапись.Номенклатура=ТекСтрока.Номенклатура;
					НоваяЗапись.ХарактеристикаНоменклатуры=ТекСтрока.ХарактеристикаНоменклатуры;
					НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
					НоваяЗапись.Проект = Проект;
					НоваяЗапись.ДокументПродажи = ДокументПродажи;
					
					НоваяЗапись.Количество		= -Возвращено;
					НоваяЗапись.Сумма			= -Окр(ТекСтрока.Сумма/ТекСтрока.Количество*Возвращено, 2);
					НоваяЗапись.СуммаНДС		= -Окр(ТекСтрока.СуммаНДС/ТекСтрока.Количество*Возвращено, 2);
					НоваяЗапись.СуммаУпр		= -Окр(ТекСтрока.СуммаУпр/ТекСтрока.Количество*Возвращено, 2);
					
					КоличествоВозврата = КоличествоВозврата - Возвращено;
					
					ВозвратТовара = ТаблицаВозвратовПартии.Добавить();
					ЗаполнитьЗначенияСвойств(ВозвратТовара, СтрокаВозврата);
					ВозвратТовара.Количество = Возвращено;
					ВозвратТовара.Партия = НоваяЗапись.Партия;
					
					Текстрока.Количество = Текстрока.Количество - НоваяЗапись.Количество;
					Текстрока.Сумма = Текстрока.Сумма - НоваяЗапись.Сумма;
					Текстрока.СуммаНДС = Текстрока.СуммаНДС - НоваяЗапись.СуммаНДС;
					Текстрока.СуммаУпр = Текстрока.СуммаУпр - НоваяЗапись.СуммаУпр;
					
					СтрокаВозврата.Количество = КоличествоВозврата;
					
					Сч = Сч - 1;
				КонецЕсли;
				
				Если КоличествоВозврата = 0 И СтрокаВозврата <> Неопределено Тогда
					ТаблицаВозвратов.Удалить(СтрокаВозврата);
					СтрокаВозврата = Неопределено;
				КонецЕсли;
				Если КоличествоСписания >= ТекСтрока.Количество Тогда
					СтрокаПартийНоменклатуры.Строки.Удалить(ТекСтрока);
				Иначе
					ТекСтрока.Количество = ТекСтрока.Количество	- КоличествоСписания;
					ТекСтрока.Сумма		 = ТекСтрока.Сумма		- СуммаСписания;
					ТекСтрока.СуммаУпр	 = ТекСтрока.СуммаУпр	- СуммаУпрСписания;
					ТекСтрока.СуммаНДС	 = ТекСтрока.СуммаНДС	- СуммаНДССписания;
				КонецЕсли;
				// уменьшаем количество которое надо списать (или увеличиваем если это коррекция отрицательной партии)
				НадоСписать=НадоСписать - КоличествоСписания;
				Если НадоСписать<=0 Тогда Прервать; КонецЕсли;
			КонецЦикла;
			
			// уменьшим ресурсы для остатка по номенклатуре
			Если ОбщееКоличество >= СтрокаПартийНоменклатуры.Количество Тогда
				ДеревоПартий.Строки.Удалить(СтрокаПартийНоменклатуры);
			Иначе
				СтрокаПартийНоменклатуры.Количество	 = СтрокаПартийНоменклатуры.Количество	- ОбщееКоличество;
			КонецЕсли;
		КонецЕсли;
		
		// если после списания по партиям осталось еще что-то, то либо образуем партию отрицательных остатков,
		// либо  предупредим о не распределении по партиям
		// проверим чего осталось
		Если (НадоСписать<>0) И (ОтрицательныеОстаткиРазрешены ИЛИ НадоСписать<0) Тогда
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор = ШапкаДокумента.Ссылка;
			// если партия указана непосредственно, то на нее. Иначе служебная
			Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
				НоваяЗапись.Партия = СтрокаТовар.Партия;
				НоваяЗапись.СтатусПартии = ПоследнийСтатус;
			Иначе
				НоваяЗапись.Партия = ПартияТоваровОтрицательныхОстатков;
				НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
			КонецЕсли;
				
			НоваяЗапись.СкладКомпании = СкладКомпании;
			НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
	        // хозоперация будет соответствовать статусу партии или текущей хозоперации?
			НоваяЗапись.ХозОперация = ШапкаДокумента.Хозоперация;
			// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
			// осуществляющих движения по данному регистру есть данный реквизит. 
			Попытка
				НоваяЗапись.Проект = ШапкаДокумента.Проект;
			Исключение
			КонецПопытки;
			
			Попытка
				НоваяЗапись.ДокументПродажи = ШапкаДокумента.ДокументПродажи;
			Исключение
			КонецПопытки;
			
			НоваяЗапись.Количество = НадоСписать;
			
			// теперь попробуем определиться с суммой списания
			Если ЕстьПартииТоваров Тогда
				Если ОбщееКоличество=0 Тогда
					ЦенаПоследнейПартии = 0;
					ЦенаУпрПоследнейПартии = 0;
					УдельныйНДСПоследнейПартии = 0;
				Иначе
					ЦенаПоследнейПартии = ОбщаяСумма/ОбщееКоличество;
					ЦенаУпрПоследнейПартии = ОбщаяСуммаУпр/ОбщееКоличество;
					УдельныйНДСПоследнейПартии = ОбщаяСуммаНДС/ОбщееКоличество;
				КонецЕсли; 
			Иначе // в этом случае пробуем достать оценку из регистра сведений Цены по типу цен Нормативная цена
				ТипЦен = Справочники.ТипыЦен.НормативнаяЦена;
				// получим цену в валюте управленческого учета компании
				ЦенаПоследнейПартии = обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
				ЦенаУпрПоследнейПартии = обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
				// розничную оценку берем по типу цен основной тип цен отгрузки
				ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
				// для расчета НДС сначала определимся со ставкой
				СтавкаНДС = СтрокаТовар.Номенклатура.СтавкаНДС;
				УдельныйНДСПоследнейПартии = (ЦенаПоследнейПартии*СтавкаНДС.Ставка)/100;
			КонецЕсли;
			
			НоваяЗапись.Сумма = Окр(ЦенаПоследнейПартии*НадоСписать,2);
			НоваяЗапись.СуммаУпр = Окр(ЦенаУпрПоследнейПартии*НадоСписать,2);
			НоваяЗапись.СуммаНДС = Окр(УдельныйНДСПоследнейПартии*НадоСписать,2);
			
			// ПОДЛЕЖИТ ИСПРАВЛЕНИЮ? Стоит ли влиять на сумму расхода?		
			Расход=Расход+НоваяЗапись.СуммаУпр;
		
			// если выбран склад куда, тогда сразу оприходуем партию на склад-получатель
			Если НЕ обЗначениеНеЗаполнено(СкладКомпанииКуда) Тогда
				// сохраним себестоимость и количество
				Сумма = НоваяЗапись.Сумма; СуммаУпр = НоваяЗапись.СуммаУпр; СуммаНДС = НоваяЗапись.СуммаНДС; Количество = НоваяЗапись.Количество;
				НоваяЗапись=Добавить();
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период=ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
				// если партия указана непосредственно, то на нее. Иначе служебная
				Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
					НоваяЗапись.Партия = СтрокаТовар.Партия;
					НоваяЗапись.СтатусПартии = ПоследнийСтатус;
				Иначе
					НоваяЗапись.Партия = ПартияТоваровОтрицательныхОстатков;
					НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
				КонецЕсли;
				НоваяЗапись.СкладКомпании = СкладКомпанииКуда;
				НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
		        // определяемся с хоз. операцией
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;	
                НоваяЗапись.Количество = НадоСписать;
				НоваяЗапись.Сумма=Окр(Сумма,2); НоваяЗапись.СуммаУпр=Окр(СуммаУпр,2); НоваяЗапись.СуммаНДС=Окр(СуммаНДС,2);
			КонецЕсли;
			
		ИначеЕсли НадоСписать<>0 Тогда
			Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
				// не хватило товара по партии. Ошибка
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по партии "+СокрЛП(СтрокаТовар.Партия)+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе 
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по партии "+СокрЛП(СтрокаТовар.Партия)+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);					
				КонецЕсли; 
				ВсеОК=Ложь;
			Иначе
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по партиям !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по партиям !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				КонецЕсли; 
				ВсеОК=Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ГТДПартийТоваровКомпанииНаборЗаписей=ДокументОбъект.Движения.ГТДПартийТоваровКомпании;
	ГТДПартийТоваровКомпанииНаборЗаписей.ДокументОбъект = ДокументОбъект;
	ГТДПартийТоваровКомпанииНаборЗаписей.ШапкаДокумента = ШапкаДокумента;
	ГТДПартийТоваровКомпанииНаборЗаписей.ГраницаРасчетаОстатков = ГраницаРасчетаОстатков;
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаПартий = ЭтотОбъект.Выгрузить();
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаВозвратТоваров = ТаблицаВозвратовПартии;
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаТовары = ТаблицаТовары.Скопировать(); //ЭтотОбъект.Выгрузить();
	ГТДПартийТоваровКомпанииНаборЗаписей.СкладКомпании = СкладКомпании;
	ГТДПартийТоваровКомпанииНаборЗаписей.МассивПартий = ЭтотОбъект.Выгрузить().ВыгрузитьКолонку("Партия");
	ГТДПартийТоваровКомпанииНаборЗаписей.РежимПроведения = РежимПроведения;
	ВсеОК = ГТДПартийТоваровКомпанииНаборЗаписей.Расход() И ВсеОК;
	// Если выбран склад куда, тогда сразу оприходуем ГТД на склад-получатель
	Если НЕ обЗначениеНеЗаполнено(СкладКомпанииКуда) Тогда
		ГТДПартийТоваровКомпанииРасход=ГТДПартийТоваровКомпанииНаборЗаписей.Выгрузить();
		Для каждого ГТДРасхода Из ГТДПартийТоваровКомпанииРасход Цикл
			НоваяЗапись=ГТДПартийТоваровКомпанииНаборЗаписей.Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗапись.СкладКомпании = СкладКомпанииКуда;
			НоваяЗапись.Номенклатура = ГТДРасхода.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = ГТДРасхода.ХарактеристикаНоменклатуры;
			НоваяЗапись.Партия = ГТДРасхода.Партия;
			НоваяЗапись.ГТД = ГТДРасхода.ГТД;
			НоваяЗапись.ХозОперация = ГТДРасхода.ХозОперация;
			НоваяЗапись.Количество = ГТДРасхода.Количество;
		КонецЦикла; 
	КонецЕсли;
	
	// запись движений
	ЗаписыватьДвижения=?(ЗаписыватьДвижения=Неопределено,Ложь,ЗаписыватьДвижения);
	Если ВсеОК И ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// обнуляем переменные
	ТаблицаТовары=Неопределено;
	РезультатЗапросаПоТоварам=Неопределено;
	ГраницаРасчетаОстатков=Неопределено;
	СтатусПартии=Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	ШапкаДокумента=Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// пересортица товаров
Функция Пересортица() Экспорт
	ПартииУказаны=НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент);
	СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	ВсеОк=Истина; Сторно=Ложь;
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса") И ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		// если неопределен результат запроса по таблице товаров, значит она в документе.
		ВидДок=ДокументОбъект.Метаданные().Имя;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокТовары.Номенклатура КАК НоменклатураПриход,
		|	ДокТовары.НоменклатураРасход КАК Номенклатура,
		|	ДокТовары.Количество*ДокТовары.Коэффициент КАК КоличествоПриход,
		|	ДокТовары.КоличествоРасход*ДокТовары.КоэффициентРасход КАК Количество,
		|	ДокТовары.Сумма КАК Сумма,
		|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыПриход,
		|	ДокТовары.ХарактеристикаНоменклатурыРасход КАК ХарактеристикаНоменклатуры,
		|	"+?(обЗначениеНеЗаполнено(ИмяРеквизитаДокумент),"","ДокТовары."+ИмяРеквизитаДокумент+" КАК Партия,")+"
		|	ДокТовары.ГТДРасход КАК ГТДРасход,
		|	ДокТовары.ГТД КАК ГТД,
		|	ДокТовары.ПонижениеСортности КАК ПонижениеСортности
		|ИЗ
		|	Документ."+ВидДок+".Товары КАК ДокТовары
		|ГДЕ
		|	ДокТовары.Ссылка=&Ссылка
		|");
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		РезультатЗапросаПоТоварам=Запрос.Выполнить();
	КонецЕсли;                                                                       
	// если передали результат запроса, то выгрузим в таблицу значений               
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	// валюты
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	// получим таблицу партий
	ДеревоПартий=ПолучитьДеревоПартий(РезультатЗапросаПоТоварам);
	// упорядочим по нормальному
	РезультатЗапросаПоТоварам.Сортировать("Номенклатура Возр,ХарактеристикаНоменклатуры Убыв");
	// получим таблицу номенклатуры с ручным списанием характеристик
	ТаблицаРучныхХарактеристик=дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(ШапкаДокумента.Ссылка,"НоменклатураРасход");
	// получим права пользователя
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки",ДокументОбъект.Права,,ДокументОбъект)<>Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	
	Права=ДокументОбъект.Права;
	ИмяКолонкиКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ДокументОбъект).Имя;
	// идем по товарам
	Для Каждого СтрокаТовар Из РезультатЗапросаПоТоварам Цикл
		// полезные переменные
		НадоСписать=Окр(СтрокаТовар.Количество,3);
		ОбщаяСумма = 0; ОбщаяСуммаУпр=0; ОбщаяСуммаНДС = 0; ОбщееКоличество = 0;
		ПоследнийСтатус=Перечисления.СтатусыПартий.ТоварКупленный;
		ЕстьПартииТоваров = Ложь; 
		СтрокаПартийНоменклатуры=ДеревоПартий.Строки.Найти(СтрокаТовар.Номенклатура,"Номенклатура");
		Если СтрокаПартийНоменклатуры<>Неопределено Тогда
			// определяем фильтр
			СтруктураОтбора=Новый Структура("Номенклатура");
			СтруктураОтбора.Номенклатура=СтрокаТовар.Номенклатура; 
			Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ (ТаблицаРучныхХарактеристик<>Неопределено И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда 
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ИмяРеквизитаДокумент) И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда 
				СтруктураОтбора.Вставить("Партия",СтрокаТовар.Партия);
			КонецЕсли;
			// ищем партии
			МассивНайденныхПартий=СтрокаПартийНоменклатуры.Строки.НайтиСтроки(СтруктураОтбора);
			
			// списываем партии
			Для Сч=0 По МассивНайденныхПартий.ВГраница() Цикл
				СтрокаПартии=МассивНайденныхПартий[Сч];
				// пропустим отрицательные остатки
				Если СтрокаПартии.Количество<=0 Тогда Продолжить; КонецЕсли;
				// если указаны партии, то пропустим все партии не наши
				Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) И СтрокаПартии.Партия<>СтрокаТовар.Партия Тогда
					Продолжить;
				КонецЕсли;
				
				ЕстьПартииТоваров = Истина; // если есть партия с которой производим списание, значит и цены будем брать из списаний
				
				Если СтрокаПартии.Количество<0 Тогда
					// запомним удельные значения списания для отрицательной партии товаров
					ОбщаяСумма      = ОбщаяСумма      + Окр(СтрокаПартии.Сумма, 2);
					ОбщаяСуммаУпр   = ОбщаяСуммаУпр   + Окр(СтрокаПартии.СуммаУпр, 2);
					ОбщаяСуммаНДС   = ОбщаяСуммаНДС   + Окр(СтрокаПартии.СуммаНДС, 2);
					ОбщееКоличество = ОбщееКоличество + СтрокаПартии.Количество;
					Продолжить;
				КонецЕсли;
				
				НоваяЗапись=Добавить();
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
				НоваяЗапись.Период=ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
				Если СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
					Партия = ПартияТоваровОтрицательныхОстатков;
				Иначе
					Партия=СтрокаПартии.Партия;
				КонецЕсли;	
				НоваяЗапись.Партия = Партия;
				НоваяЗапись.СтатусПартии=СтрокаПартии.СтатусПартии;
				ПоследнийСтатус=СтрокаПартии.СтатусПартии;
				НоваяЗапись.СкладКомпании=СкладКомпании;
				НоваяЗапись.Номенклатура=СтрокаПартии.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры=СтрокаПартии.ХарактеристикаНоменклатуры;
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
				// осуществляющих движения по данному регистру есть данный реквизит. 
				Попытка
					НоваяЗапись.Проект = ШапкаДокумента.Проект;
				Исключение
				КонецПопытки;	
				// показатели
				НоваяЗапись.Количество=Мин(НадоСписать,СтрокаПартии.Количество);
				НоваяЗапись.Сумма=Окр(?(СтрокаПартии.Количество<НадоСписать,СтрокаПартии.Сумма,СтрокаПартии.Сумма/СтрокаПартии.Количество*НадоСписать),2);
				НоваяЗапись.СуммаУпр=Окр(?(СтрокаПартии.Количество<НадоСписать,СтрокаПартии.СуммаУпр,СтрокаПартии.СуммаУпр/СтрокаПартии.Количество*НадоСписать),2);
				НоваяЗапись.СуммаНДС=Окр(?(СтрокаПартии.Количество<НадоСписать,СтрокаПартии.СуммаНДС,СтрокаПартии.СуммаНДС/СтрокаПартии.Количество*НадоСписать),2);
				
				ОбщаяСумма = ОбщаяСумма + НоваяЗапись.Сумма;
				ОбщаяСуммаУпр = ОбщаяСуммаУпр + НоваяЗапись.СуммаУпр;
				ОбщаяСуммаНДС = ОбщаяСуммаНДС+ НоваяЗапись.СуммаНДС;
				ОбщееКоличество = ОбщееКоличество + НоваяЗапись.Количество;
				
				// если у нас понижение сортности, то движения идут "зеркально"
				Если СтрокаТовар.ПонижениеСортности Тогда
					ПересортицаПриход(СтрокаТовар,Партия,НоваяЗапись.Количество,НоваяЗапись.Сумма,НоваяЗапись.СуммаУпр,НоваяЗапись.СуммаНДС);
				КонецЕсли;
				
				// вычтем из строки партий показатели
				СтрокаПартии.Количество=СтрокаПартии.Количество-НоваяЗапись.Количество;
				СтрокаПартийНоменклатуры.Количество=СтрокаПартийНоменклатуры.Количество-НоваяЗапись.Количество;
				Если СтрокаПартии.Количество<=0 Тогда
					// если списали все из партии, то такая строка нам больше не нужна
					СтрокаПартийНоменклатуры.Строки.Удалить(СтрокаПартии);
				Иначе
					// разбираемся с остальными показателями
					СтрокаПартии.Сумма=СтрокаПартии.Сумма-НоваяЗапись.Сумма;
					СтрокаПартии.СуммаУпр=СтрокаПартии.СуммаУпр-НоваяЗапись.СуммаУпр;
					СтрокаПартии.СуммаНДС=СтрокаПартии.СуммаНДС-НоваяЗапись.СуммаНДС;
				КонецЕсли;
				
				НадоСписать=НадоСписать-НоваяЗапись.Количество;
				Если НадоСписать<=0 Тогда Прервать; КонецЕсли;
			КонецЦикла;
			
			// если списали всю номенклатуру, то уберем лишнюю стоку дерева
			Если СтрокаПартийНоменклатуры.Количество<=0 Тогда
				ДеревоПартий.Строки.Удалить(СтрокаПартийНоменклатуры);
			КонецЕсли;
		КонецЕсли;
		
		// если остались товары, не распределенные по партия, то сообщаем об этом
		Если НадоСписать<>0 И ОтрицательныеОстаткиРазрешены  Тогда
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор = ШапкаДокумента.Ссылка;
			// если партия указана непосредственно, то на нее. Иначе служебная
			Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
				НоваяЗапись.Партия = СтрокаТовар.Партия;
				НоваяЗапись.СтатусПартии = ПоследнийСтатус;
			Иначе
				НоваяЗапись.Партия = ПартияТоваровОтрицательныхОстатков;
				НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
			КонецЕсли;
			НоваяЗапись.СкладКомпании = СкладКомпании;
			НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
	        // хозоперация будет соответсвовать статусу партии или текущей хозоперации?
			НоваяЗапись.ХозОперация = ШапкаДокумента.Хозоперация;
			// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
			// осуществляющих движения по данному регистру есть данный реквизит. 
			Попытка
				НоваяЗапись.Проект = ШапкаДокумента.Проект;
			Исключение
			КонецПопытки;	
            НоваяЗапись.Количество = НадоСписать;

			Если ЕстьПартииТоваров Тогда
				Если ОбщееКоличество=0 Тогда
					ЦенаПоследнейПартии = 0;
					ЦенаУпрПоследнейПартии = 0;
					УдельныйНДСПоследнейПартии = 0;
				Иначе
					ЦенаПоследнейПартии = ОбщаяСумма/ОбщееКоличество;
					ЦенаУпрПоследнейПартии = ОбщаяСуммаУпр/ОбщееКоличество;
					УдельныйНДСПоследнейПартии = ОбщаяСуммаНДС/ОбщееКоличество;
				КонецЕсли; 
			Иначе
				ТипЦен = Справочники.ТипыЦен.НормативнаяЦена;
				// получим цену в валюте управленческого учета компании
				ЦенаПоследнейПартии		= обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
				ЦенаУпрПоследнейПартии	= обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,ШапкаДокумента.МоментВремени,,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),, СтрокаТовар.ХарактеристикаНоменклатуры,, СкладКомпании.Подразделение);
				// для расчета НДС сначала определимся со ставкой
				СтавкаНДС = СтрокаТовар.Номенклатура.СтавкаНДС;
				УдельныйНДСПоследнейПартии = (ЦенаПоследнейПартии*СтавкаНДС.Ставка)/100;
			КонецЕсли;
			
			НоваяЗапись.Сумма = Окр(ЦенаПоследнейПартии*НадоСписать,2);
			НоваяЗапись.СуммаУпр = Окр(ЦенаУпрПоследнейПартии*НадоСписать,2);
			НоваяЗапись.СуммаНДС = Окр(УдельныйНДСПоследнейПартии*НадоСписать,2);
			
			// сообщаем
			Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
				// не хватило товара по партии. Ошибка
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по партии "+СокрЛП(СтрокаТовар.Партия)+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по партии "+СокрЛП(СтрокаТовар.Партия)+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
 				КонецЕсли; 
			Иначе
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по партиям"+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по партиям"+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				КонецЕсли; 
			КонецЕсли;

		ИначеЕсли НадоСписать>0 Тогда
			// сообщаем
			Если ПартииУказаны И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
				// не хватило товара по партии. Ошибка
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по партии "+СокрЛП(СтрокаТовар.Партия)+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по партии "+СокрЛП(СтрокаТовар.Партия)+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				КонецЕсли; 
				ВсеОК=Ложь;
			Иначе
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по партиям"+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по партиям"+" !","Важное&Партии товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				КонецЕсли; 
				ВсеОК=Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Приходуем, приходуемые, товары =), если непонижение сортности (т.к. уже все оприходовали)
		Если НЕ СтрокаТовар.ПонижениеСортности Тогда
			СуммаУпрПриход	= обПересчет(СтрокаТовар.Сумма, ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ШапкаДокумента.КурсВалютыУпр, 2);
			СуммаПриход		= обПересчет(СтрокаТовар.Сумма, ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента, ВалютаРегл,КурсРегл, 2);
			СтавкаНДС		= СтрокаТовар.НоменклатураПриход.СтавкаНДС.Ставка;
			
			Если ШапкаДокумента.ТипЦен.ЦенаВключаетНДС Тогда
				СуммаНДСПриход = Окр((СуммаПриход * СтавкаНДС)/(100 + СтавкаНДС), 2);
			Иначе
				СуммаНДСПриход = Окр((СуммаПриход * СтавкаНДС) / 100, 2);
			КонецЕсли;
			
			ПересортицаПриход(СтрокаТовар,?(СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя,ПартияТоваровОтрицательныхОстатков,ШапкаДокумента.Ссылка),СтрокаТовар.КоличествоПриход, СуммаПриход,СуммаУпрПриход,СуммаНДСПриход);
		КонецЕсли;
		
	КонецЦикла;
	
	ГТДПартийТоваровКомпанииНаборЗаписей=ДокументОбъект.Движения.ГТДПартийТоваровКомпании;
	ГТДПартийТоваровКомпанииНаборЗаписей.ДокументОбъект = ДокументОбъект;
	ГТДПартийТоваровКомпанииНаборЗаписей.ШапкаДокумента = ШапкаДокумента;
	ГТДПартийТоваровКомпанииНаборЗаписей.ГраницаРасчетаОстатков = ГраницаРасчетаОстатков;
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаПартий = ЭтотОбъект.Выгрузить();
	МассивНайденныхСтрок=ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаПартий.НайтиСтроки(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Приход));
	Для Сч=0 По МассивНайденныхСтрок.Количество()-1 Цикл
		ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаПартий.Удалить(МассивНайденныхСтрок[Сч]);
	КонецЦикла;
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаТовары = РезультатЗапросаПоТоварам.Скопировать();
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаТовары.Колонки.Удалить("ГТД");
	ГТДПартийТоваровКомпанииНаборЗаписей.ТаблицаТовары.Колонки.ГТДРасход.Имя="ГТД";
	ГТДПартийТоваровКомпанииНаборЗаписей.СкладКомпании = СкладКомпании;
	ГТДПартийТоваровКомпанииНаборЗаписей.МассивПартий = ЭтотОбъект.Выгрузить().ВыгрузитьКолонку("Партия");
	ГТДПартийТоваровКомпанииНаборЗаписей.РежимПроведения = РежимПроведения;
	ВсеОК = ГТДПартийТоваровКомпанииНаборЗаписей.Расход() И ВсеОК;
	
	ЗаписыватьДвижения=?(ЗаписыватьДвижения=Неопределено,Ложь,ЗаписыватьДвижения);
	Если ВсеОК И ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// обнуляем переменные
	РезультатЗапросаПоТоварам=Неопределено;
	ГраницаРасчетаОстатков=Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	ШапкаДокумента=Неопределено;
	
	Возврат ВсеОк;
КонецФункции

// вспомогательная процедура, оприходование товаров при пересортице
Процедура ПересортицаПриход(СтрокаТовар,Партия,КоличествоПриход,СуммаПриход,СуммаУпрПриход,СуммаНДСПриход)
	НоваяЗапись=Добавить();
	НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
	НоваяЗапись.Период=ШапкаДокумента.Дата;
	НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
	НоваяЗапись.Партия = Партия;
	НоваяЗапись.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	НоваяЗапись.СкладКомпании=СкладКомпании;
	НоваяЗапись.Номенклатура=СтрокаТовар.НоменклатураПриход;
	НоваяЗапись.ХарактеристикаНоменклатуры=СтрокаТовар.ХарактеристикаНоменклатурыПриход;
	НоваяЗапись.СтавкаНДС  = СтрокаТовар.НоменклатураПриход.СтавкаНДС;
	НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
	// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
	// осуществляющих движения по данному регистру есть данный реквизит. 
	Попытка
		НоваяЗапись.Проект = ШапкаДокумента.Проект;
	Исключение
	КонецПопытки;	
	// показатели
	НоваяЗапись.Количество=КоличествоПриход;
	НоваяЗапись.Сумма=СуммаПриход;
	НоваяЗапись.СуммаУпр=СуммаУпрПриход;
	НоваяЗапись.СуммаНДС=СуммаНДСПриход;
	
	ГТДПартийТоваровКомпанииНаборЗаписей=ДокументОбъект.Движения.ГТДПартийТоваровКомпании;
	ГТДПартийТоваровКомпанииНаборЗаписей.ДобавитьЗапись(НоваяЗапись,СтрокаТовар.ГТД);
КонецПроцедуры

//переоценка товаров принятых на комиссию
Функция Переоценка() Экспорт
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса")) Тогда
		// если неопределен результат запроса по таблице товаров, значит она в документе.
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ПереоценкаТовары.Номенклатура КАК Номенклатура,
		|	ПереоценкаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПереоценкаТовары.ДокументПередачи КАК ДокументПередачи,
		|	ПереоценкаТовары.Количество*ПереоценкаТовары.Коэффициент КАК Количество,
		|	ПереоценкаТовары.Цена/ПереоценкаТовары.Коэффициент КАК ЦенаНовая
		|ИЗ
		|	Документ.Переоценка.Товары КАК ПереоценкаТовары
		|ГДЕ
		|	ПереоценкаТовары.Ссылка=&Ссылка
		|");
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		РезультатЗапросаПоТоварам=Запрос.Выполнить();
	КонецЕсли;
	// списываем партии
	РезультатЗапросаПоПартиямПринятым=ПолучитьТаблицуПартийПринятых();
	// открываем выборки
	ВыборкаТовар=РезультатЗапросаПоТоварам.Выбрать();
	ВыборкаПартии=РезультатЗапросаПоПартиямПринятым.Выбрать();
	// всякие валюты и структуры отбора
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ШапкаДокумента.КурсВалютыУпр;
	КонецЕсли;	
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	СтруктураОтбора=Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Партия");
	// идем по выборкам
	Пока ВыборкаТовар.Следующий() Цикл
		// определяем фильтр
		СтруктураОтбора.Номенклатура=ВыборкаТовар.Номенклатура;
		СтруктураОтбора.ХарактеристикаНоменклатуры=ВыборкаТовар.ХарактеристикаНоменклатуры;
		СтруктураОтбора.Партия=ВыборкаТовар.ДокументПередачи;
		НадоПереоценить=ВыборкаТовар.Количество;
		ВыборкаПартии.Сбросить();
		Пока ВыборкаПартии.НайтиСледующий(СтруктураОтбора) Цикл
			// расчет суммы переоценки
			НадоПереоценить=?(НадоПереоценить=0,ВыборкаПартии.Количество,НадоПереоценить);
			СуммаПереоценки=обПересчет(ВыборкаТовар.ЦенаНовая,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)*Мин(НадоПереоценить,ВыборкаПартии.Количество);
			СуммаПереоценки=Окр(СуммаПереоценки-?(ВыборкаПартии.Количество=0,0,(ВыборкаПартии.Сумма/ВыборкаПартии.Количество)*Мин(НадоПереоценить,ВыборкаПартии.Количество)),2);
			СуммаПереоценкиУпр=обПересчет(ВыборкаТовар.ЦенаНовая,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр)*Мин(НадоПереоценить,ВыборкаПартии.Количество);
			СуммаПереоценкиУпр=Окр(СуммаПереоценкиУпр-?(ВыборкаПартии.Количество=0,0,(ВыборкаПартии.СуммаУпр/ВыборкаПартии.Количество)*Мин(НадоПереоценить,ВыборкаПартии.Количество)),2);
			// если нечего переоценивать, то выходим
			Если СуммаПереоценки=0 Тогда
				Продолжить;
			Конецесли;
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения=?(СуммаПереоценки>0,ВидДвиженияНакопления.Приход,ВидДвиженияНакопления.Расход);
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.СкладКомпании=ВыборкаПартии.СкладКомпании;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗапись.Партия=ВыборкаПартии.Партия;
			НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
			НоваяЗапись.Номенклатура=ВыборкаПартии.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры=ВыборкаПартии.ХарактеристикаНоменклатуры;
			// определяемся с хоз. операцией
			НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
			// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
			// осуществляющих движения по данному регистру есть данный реквизит. 
			Попытка
				НоваяЗапись.Проект = ШапкаДокумента.Проект;
			Исключение
			КонецПопытки;

			НоваяЗапись.Количество=0;
			// суммы
			НоваяЗапись.Сумма=Окр(?(СуммаПереоценки>0,СуммаПереоценки,-СуммаПереоценки),2);
			НоваяЗапись.СуммаУпр=Окр(?(СуммаПереоценкиУпр>0,СуммаПереоценкиУпр,-СуммаПереоценкиУпр),2);
			НоваяЗапись.СуммаНДС=НоваяЗапись.Сумма/ВыборкаПартии.Сумма*ВыборкаПартии.СуммаНДС;

			НадоПереоценить=НадоПереоценить-Мин(НадоПереоценить,ВыборкаПартии.Количество);
			Если НадоПереоценить=0 Тогда Прервать; КонецЕсли;
		КонецЦикла
	КонецЦикла;
	
	ЗаписыватьДвижения=?(ЗаписыватьДвижения=Неопределено,Ложь,ЗаписыватьДвижения);
	Если ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// обнуляем переменные
	РезультатЗапросаПоТоварам=Неопределено;
	ГраницаРасчетаОстатков=Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	ШапкаДокумента=Неопределено;
	
	Возврат Истина;	
КонецФункции

// возвращает результат запроса по таблице отданных партий
//для переоценки партий принятых
Функция ПолучитьТаблицуПартийПринятых()
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,
	|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
	|	ПартииТоваровКомпанииОстатки.СуммаНДСОстаток КАК СуммаНДС,
	|	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК Сумма,
	|	ПартииТоваровКомпанииОстатки.СуммаУпрОстаток КАК СуммаУпр
	|
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент,СкладКомпании = &СкладКомпании И Номенклатура В (&Номенклатура) И Партия.Контрагент=&Контрагент И Партия.ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов И Партия В (&ДокументПередачи)) КАК ПартииТоваровКомпанииОстатки
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ПартииТоваровКомпании.Остатки
    |
	|УПОРЯДОЧИТЬ ПО ХарактеристикаНоменклатуры.Сортировка Возр, Партия Возр
	|";

	тзТоваров=РезультатЗапросаПоТоварам.Выгрузить();
	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Момент",?(РежимПроведения=РежимПроведенияДокумента.Оперативный,Неопределено,ШапкаДокумента.МоментВремени));
	Запрос.УстановитьПараметр("Номенклатура",тзТоваров.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Контрагент",ШапкаДокумента.Контрагент);
	Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ШапкаДокумента.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ДокументПередачи",тзТоваров.ВыгрузитьКолонку("ДокументПередачи"));
	Запрос.Текст=ТекстЗапроса;
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПартииТоваровКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, ШапкаДокумента.Дата)); 
	ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании); 
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", тзТоваров);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("Партия", "ДокументПередачи");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	Возврат Запрос.Выполнить();
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

Функция ПроверитьПартиюНаЗаказПоГарантии(Партия, Номенклатура)
	Запрос = Новый запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ЗаказыРаспределение.ЗаказПокупателя КАК ЗаказПокупателя
	                |ИЗ
	                |	РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
	                |ГДЕ
	                |	ЗаказыРаспределение.Регистратор = &Регистратор
	                |	И (ЗаказыРаспределение.ЗаказПокупателя.ДокументОснование.ВидРемонта = &ВидРемонта
	                |			ИЛИ ЗаказыРаспределение.ЗаказПокупателя.ДокументОснование.ВидРемонта = &ВидРемонта2)
	                |	И ЗаказыРаспределение.Номенклатура = &Номенклатура";
	Запрос.УстановитьПараметр("Регистратор", Партия); 
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ВидРемонта", Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000010"));
	Запрос.УстановитьПараметр("ВидРемонта2", Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000049"));
	Рез = Запрос.Выполнить();
	Если не Рез.Пустой() тогда
		//Необходимо проверить закрыт ли заказ 
		ВыборкаДетальныеЗаписи = Рез.Выбрать(); 
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗапросЗаказ = Новый Запрос;
			ЗапросЗаказ.Текст =  "ВЫБРАТЬ
			                     |	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК ЗаказаноОстаток
			                     |ИЗ
			                     |	РегистрНакопления.ЗаказыПокупателей.Остатки(&Дата, ) КАК ЗаказыПокупателейОстатки
			                     |ГДЕ
			                     |	ЗаказыПокупателейОстатки.Заказ = &Заказ";
			ЗапросЗаказ.УстановитьПараметр("Заказ", ВыборкаДетальныеЗаписи.ЗаказПокупателя);
			ЗапросЗаказ.УстановитьПараметр("Дата", ЭтотОбъект.ГраницаРасчетаОстатков.Дата);
			РезЗаказ = ЗапросЗаказ.Выполнить();
			Если не РезЗаказ.Пустой() Тогда
				Возврат Истина; 
			Иначе
				Возврат Ложь;
			КонецЕсли;    
		КонецЦикла;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции;

ХозОперация=Неопределено;
РежимДопРасходы=0;
ПоБазовомуКоличеству=Ложь;
ТаблицаВозвратов=Неопределено;

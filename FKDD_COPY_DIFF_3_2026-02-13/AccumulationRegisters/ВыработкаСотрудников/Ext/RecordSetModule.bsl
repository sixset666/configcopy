Перем РежимПроведения Экспорт; // Режим проведения документа. Если проведение неоперативное - проверок остатков нет

Перем ДокументОбъект Экспорт; //Документ, осуществляющий движение по регистру
Перем РезультатЗапросаПоРаботам Экспорт; //Выборка табличной части или таблица значений табличной части работ
Перем РезультатЗапросаПоИсполнителям Экспорт; //Выборка табличной части или таблица значений табличной части исполнителей
Перем ВидРемонта Экспорт; //Ссылка на справочник видов ремонта. Если значение неопределено, вид ремонта берется из объекта документа
Перем Автомобиль Экспорт; //Ссылка на справочник автомобилей. Если значение неопределено, автомобиль берется из объекта документа
Перем ПериодДвижения Экспорт; //Период движения регистра. Если период не задан, он берется из ДокументОбъект.Дата

// Возвращает результат запроса по таблице работ
Функция ПолучитьТаблицуРабот()
	// получим результат запроса по таблице работ
	ВидДок=ДокументОбъект.Метаданные().Имя;
	Запрос=Новый Запрос();
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументРаботы.Работа КАК Работа,
	|	ДокументРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	ДокументРаботы.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ДокументРаботы.Нормочас.Цена = 1 ТОГДА
	//anton
	| ДокументРаботы.Количество*ДокументРаботы.Коэффициент
	//|			0
	//anton
	|		ИНАЧЕ
	|			ДокументРаботы.Количество*ДокументРаботы.Коэффициент
	|	КОНЕЦ КАК КоличествоНормочасов,
	|	ДокументРаботы.СуммаВсего КАК Сумма,
	|	ДокументРаботы.СуммаСкидки+ДокументРаботы.СуммаСкидкиСтроки КАК СуммаСкидки
	|ИЗ
	|	Документ."+ВидДок+".Работы КАК ДокументРаботы
	|ГДЕ
	|	ДокументРаботы.Ссылка=&Ссылка И
	|	ДокументРаботы.Контрагент=ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|";
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Возврат Запрос.Выполнить();
КонецФункции

// Возвращает результат запроса по таблице исполнителей
Функция ПолучитьТаблицуИсполнителей()
	// получим результат запроса по таблице исполнителей
    ВидДок=ДокументОбъект.Метаданные().Имя;
	Запрос=Новый Запрос();
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументИсполнители.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	ДокументИсполнители.Исполнитель КАК Исполнитель,
	|	ДокументИсполнители.Цех КАК Цех,
	|	ДокументИсполнители.Процент КАК Процент
	|ИЗ
	|	Документ."+ВидДок+".Исполнители КАК ДокументИсполнители
	|ГДЕ
	|	ДокументИсполнители.Ссылка=&Ссылка
	|";
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Возврат Запрос.Выполнить();
КонецФункции


// Получение нормы времени автоработы согласно класса автомобиля
Функция НормаВремениАвтоработы(Авторабота, ВариантКомплектации) Экспорт
	
	НормаВремени = 0;
	
	Если НЕ ЗначениеЗаполнено(Авторабота) ИЛИ Авторабота.ЭтоГруппа Тогда
		Возврат НормаВремени;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
		
		ТаблицаПриоритетов = Новый ТаблицаЗначений;
		ТаблицаПриоритетов.Колонки.Добавить("Модель",    Новый ОписаниеТипов("СправочникСсылка.Модели"));
		ТаблицаПриоритетов.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		Если ТипЗнч(ВариантКомплектации) = Тип("СправочникСсылка.ВариантыКомплектации") Тогда
			Модель = ВариантКомплектации.Владелец;
			Запрос.УстановитьПараметр("Модель",              Модель);
			Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
		Иначе
			Модель = ВариантКомплектации.Родитель;
			Запрос.УстановитьПараметр("Модель",              ВариантКомплектации);
			Запрос.УстановитьПараметр("ВариантКомплектации", Справочники.ВариантыКомплектации.ПустаяСсылка());
		КонецЕсли;
		
		Приоритет = 1;
		Пока ЗначениеЗаполнено(Модель) Цикл
			НоваяСтрока = ТаблицаПриоритетов.Добавить();
			НоваяСтрока.Модель = Модель;
			НоваяСтрока.Приоритет = Приоритет;
			Приоритет = Приоритет + 1;
			Модель    = Модель.Родитель;
		КонецЦикла;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаПриоритетов.Модель КАК Модель,
		|	ТаблицаПриоритетов.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ
		|	ТаблицаПриоритетов
		|ИЗ
		|	&ТаблицаПриоритетов КАК ТаблицаПриоритетов
		|;
		|
		|ВЫБРАТЬ
		|	0 КАК Приоритет,
		|	НормыВремени.ВремяВыполненияФакт КАК ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы.НормыВремени КАК НормыВремени
		|ГДЕ
		|	НормыВремени.Ссылка = &Авторабота И 
		|	НормыВремени.Модель              = &Модель И 
		|	НормыВремени.ВариантКомплектации = &ВариантКомплектации
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаПриоритетов.Приоритет,
		|	НормыВремени.ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы.НормыВремени КАК НормыВремени
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаПриоритетов КАК ТаблицаПриоритетов
		|ПО
		|	НормыВремени.Ссылка              = &Авторабота И 
		|	НормыВремени.Модель              = ТаблицаПриоритетов.Модель И 
		|	НормыВремени.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
		|ГДЕ
		|	НормыВремени.Ссылка              = &Авторабота И 
		|	НормыВремени.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	" + Формат(Приоритет, "ЧГ=0") + ",
		|	Автоработы.ВремяВыполненияФакт
		|ИЗ
		|	Справочник.Автоработы КАК Автоработы
		|ГДЕ
		|	Автоработы.Ссылка = &Авторабота
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		
		Запрос.УстановитьПараметр("ТаблицаПриоритетов",  ТаблицаПриоритетов);
	Иначе
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Автоработы.ВремяВыполненияФакт КАК ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы КАК Автоработы
		|ГДЕ
		|	Автоработы.Ссылка = &Авторабота";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Авторабота", Авторабота);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		НормаВремени = Выборка.ВремяВыполнения;
	КонецЕсли;
	
	Возврат НормаВремени; 
КонецФункции // орНормаВремениАвтоработы() 



// Формирует движения по регистру
// Возвращаемое значение: Булево. Истина - все ОК, Ложь - ошибка.
Функция Движение() Экспорт
	ВсеОК=Истина;
	
	// получим таблицу работ
	Если (РезультатЗапросаПоРаботам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоРаботам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоРаботам)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоРаботам=ПолучитьТаблицуРабот();
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоРаботам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоРаботам=РезультатЗапросаПоРаботам.Выгрузить();
	КонецЕсли;
	// получим таблицу исполнителей
	Если (РезультатЗапросаПоИсполнителям=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоИсполнителям)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоИсполнителям)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоИсполнителям=ПолучитьТаблицуИсполнителей();
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоИсполнителям)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоИсполнителям=РезультатЗапросаПоИсполнителям.Выгрузить();
	КонецЕсли;
	
	//Получим валюту и курс управленческого учета
	ПериодДвижения=?(ПериодДвижения=Неопределено,ДокументОбъект.Дата,ПериодДвижения);
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
	КурсУпр = Неопределено;
	Попытка
		Если НЕ обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр) Тогда
			КурсУпр = ДокументОбъект.КурсВалютыУпр; 	
		КонецЕсли;
	Исключение
	КонецПопытки;
	Если КурсУпр = Неопределено Тогда
		СтруктураКурса	= обКурсДляВалюты(ВалютаУпр,ПериодДвижения);
		КурсУпр			= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли; 
	ТекВидРемонта=?(ВидРемонта=Неопределено,ДокументОбъект.ВидРемонта,ВидРемонта);
	ТекАвтомобиль=?(Автомобиль=Неопределено,ДокументОбъект.Автомобиль,Автомобиль);
	
	//Перебираем строки работ
	Для каждого СтрокаРабот Из РезультатЗапросаПоРаботам Цикл
		Работа=СтрокаРабот.Работа;
		ИдентификаторРаботы  = СтрокаРабот.ИдентификаторРаботы;
		Количество           = СтрокаРабот.Количество;
		КоличествоНормочасов = СтрокаРабот.КоличествоНормочасов;
		//Получим сумму работы с и без скидки в валюте управленческого учета
		СуммаСоСкидкой=СтрокаРабот.Сумма;
		СуммаСкидки=СтрокаРабот.СуммаСкидки;
		СуммаБезСкидки=СуммаСоСкидкой+СуммаСкидки;
		СуммаБезСкидкиУпр=обПересчет(СуммаБезСкидки,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр);
		СуммаСоСкидкойУпр=обПересчет(СуммаСоСкидкой,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр);
		ИсполнителиРаботы = РезультатЗапросаПоИсполнителям.НайтиСтроки(Новый Структура("ИдентификаторРаботы",ИдентификаторРаботы));
		КоличествоОст           = Количество;
		КоличествоНормочасовОст = КоличествоНормочасов;
		СуммаБезСкидкиУпрОст    = СуммаБезСкидкиУпр;
		СуммаСоСкидкойУпрОст    = СуммаСоСкидкойУпр;
		ОбщийПроцент=0;
		
		///Ульянов 18.04.19 получим время для Факт
		КоличествоНормочасовФакт = НормаВремениАвтоработы(Работа,ДокументОбъект.Ссылка.Автомобиль.Модель);
		Если НЕ ЗначениеЗаполнено(КоличествоНормочасовФакт) Тогда
			КоличествоНормочасовФакт = НормаВремениАвтоработы(Работа,);
			Если НЕ ЗначениеЗаполнено(КоличествоНормочасовФакт) Тогда
				КоличествоНормочасовФакт=КоличествоНормочасов;	
			КонецЕсли;	
		Иначе
			КоличествоНормочасовФакт=КоличествоНормочасовФакт*Количество;
		КонецЕсли;	
		КоличествоНормочасовОстФакт = КоличествоНормочасовФакт;

		КоличествоИсполнителей=ИсполнителиРаботы.Количество();
		Для каждого СтрокаИсполнителей Из ИсполнителиРаботы Цикл
			КоличествоИсполнителей=КоличествоИсполнителей-1;
			Исполнитель=СтрокаИсполнителей.Исполнитель;
			Цех=СтрокаИсполнителей.Цех;
			Процент=СтрокаИсполнителей.Процент;
			ОбщийПроцент=ОбщийПроцент+Процент;
			//Рассчитаем участие исполнителя в выполнении работы
			Если КоличествоИсполнителей=0 Тогда
				КоличествоИсполнителя           = КоличествоОст;
				КоличествоНормочасовИсполнителя = КоличествоНормочасовОст;
				КоличествоНормочасовИсполнителяФакт = КоличествоНормочасовОстФакт;
				СуммаИсполнителя                = СуммаБезСкидкиУпрОст;
				СуммаИсполнителяСоСкидкой       = СуммаСоСкидкойУпрОст;
			Иначе
				КоличествоИсполнителя           = (Количество*Процент)/100;
				КоличествоНормочасовИсполнителя = (КоличествоНормочасов*Процент)/100;
				КоличествоНормочасовИсполнителяФакт = (КоличествоНормочасовФакт*Процент)/100;
				
				СуммаИсполнителя=(СуммаБезСкидкиУпр*Процент)/100;
				СуммаИсполнителяСоСкидкой=(СуммаСоСкидкойУпр*Процент)/100;
			КонецЕсли; 
			Если ВсеОК Тогда
				// Создаем запись регистра
				НоваяЗапись = Добавить();
				//НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период               = ПериодДвижения;
				НоваяЗапись.Регистратор          = ДокументОбъект.Ссылка;
				НоваяЗапись.Сотрудник            = Исполнитель;
				НоваяЗапись.Работа               = Работа;
				НоваяЗапись.ВидРемонта           = ТекВидРемонта;
				НоваяЗапись.Автомобиль           = ТекАвтомобиль;
				НоваяЗапись.Цех                  = Цех;
				НоваяЗапись.ДокументПродажи		 = ДокументОбъект.Ссылка;
				НоваяЗапись.Количество           = КоличествоИсполнителя;
				НоваяЗапись.КоличествоНормочасов = КоличествоНормочасовИсполнителя;
				НоваяЗапись.КоличествоНормочасовФакт = КоличествоНормочасовИсполнителяФакт;
				
				НоваяЗапись.СуммаУпр             = Окр(СуммаИсполнителя,2);
				НоваяЗапись.СуммаУпрСоСкидкой    = Окр(СуммаИсполнителяСоСкидкой,2);
				// определяемся с хоз. операцией
				НоваяЗапись.ХозОперация          = ДокументОбъект.ХозОперация;
				КоличествоОст           = КоличествоОст           - НоваяЗапись.Количество;
				КоличествоНормочасовОст = КоличествоНормочасовОст - НоваяЗапись.КоличествоНормочасов;
				КоличествоНормочасовОстФакт = КоличествоНормочасовОстФакт - НоваяЗапись.КоличествоНормочасовФакт;
				СуммаБезСкидкиУпрОст=СуммаБезСкидкиУпрОст-НоваяЗапись.СуммаУпр;
				СуммаСоСкидкойУпрОст=СуммаСоСкидкойУпрОст-НоваяЗапись.СуммаУпрСоСкидкой;
			КонецЕсли; 
		КонецЦикла; 
		Если ОбщийПроцент<>100 Тогда
			//общий процент исполнения работ по исполнителям не равен 100 процентам
			дкСообщитьРезультат("По работе """+СокрЛП(Работа)+""" процент распределения работ по исполнителям составляет "+СокрЛП(ОбщийПроцент)+"% , а должен быть равен 100%.","Важное&Выработка сотрудников:",ДокументОбъект);
			ВсеОК=Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
	// запись движений
	Если ВсеОК Тогда Записать(); КонецЕсли; 
	
	РежимПроведения=Неопределено;
	ДокументОбъект=Неопределено;
	РезультатЗапросаПоРаботам=Неопределено;
	РезультатЗапросаПоИсполнителям=Неопределено;
	ВидРемонта=Неопределено;
	Автомобиль=Неопределено;
	ПериодДвижения=Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

////////////////////////////////////////////////////////////////////////
//Инициализация переменных модуля набора записей

РежимПроведения=РежимПроведенияДокумента.Оперативный;

ДокументОбъект=Неопределено; // !!! Обязательный к заполнению перед началом проведения
РезультатЗапросаПоРаботам=Неопределено;
РезультатЗапросаПоИсполнителям=Неопределено;
ВидРемонта=Неопределено;
Автомобиль=Неопределено;
ПериодДвижения=Неопределено;

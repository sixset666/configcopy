// Функция выполняет проверку, что автомобиль отсутствует на остатках по различным регистрам
//
// Параметры:
// ЭтотОбъект - ДокументОбъект, документ, для которого производится действие
// Отказ 	 – булево, признак отказа от действия
//
// Возвращаемое значение:
// Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//       для определения возможности дальнейшей работы приватного обработчика
//
Функция ПроверитьОстаткиАвтомобилей(ЭтотОбъект,Отказ) Экспорт
	
	Результат = Истина;
	
	Запрос=Новый Запрос();
	ВидДок=ЭтотОбъект.Метаданные().Имя;
	Запрос.Текст="ВЫБРАТЬ
	             |	ДокументАвтомобили.Автомобиль КАК Автомобиль
	             |ПОМЕСТИТЬ ТаблицаАвтомобилей
	             |ИЗ
	             |	Документ."+ВидДок+".Автомобили КАК ДокументАвтомобили
	             |ГДЕ
	             |	ДокументАвтомобили.Ссылка = &Ссылка" + ?(ВидДок="ИнвентаризацияАвтомобилей"," И (ДокументАвтомобили.КоличествоУчет-Количество)>0","") + "
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	             |	ОстаткиАвтомобилейОстатки.КоличествоОстаток КАК Остаток,
	             |	1 КАК ФлагРегистра
	             |ИЗ
	             |	РегистрНакопления.ОстаткиАвтомобилей.Остатки(
	             |			&Момент,
	             |			Автомобиль В
	             |				(ВЫБРАТЬ
	             |					ТаблицаАвтомобилей.Автомобиль
	             |				ИЗ
	             |					ТаблицаАвтомобилей)) КАК ОстаткиАвтомобилейОстатки
				 |
				 |ОБЪЕДИНИТЬ ВСЕ
				 |
				 |ВЫБРАТЬ
	             |	ПрочиеАктивыВЭксплуатацииОстатки.Автомобиль КАК Автомобиль,
	             |	ПрочиеАктивыВЭксплуатацииОстатки.КоличествоОстаток КАК Остаток,
	             |	4 КАК ФлагРегистра
	             |ИЗ
	             |	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(
	             |			&Момент,
	             |			Автомобиль В
	             |				(ВЫБРАТЬ
	             |					ТаблицаАвтомобилей.Автомобиль
	             |				ИЗ
	             |					ТаблицаАвтомобилей)) КАК ПрочиеАктивыВЭксплуатацииОстатки";
				 
	Если ЭтотОбъект.ХозОперация <> Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия
		И ЭтотОбъект.ХозОперация <> Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда			 
		Запрос.Текст = Запрос.Текст + "			 
					 |ОБЪЕДИНИТЬ ВСЕ
					 |
					 |ВЫБРАТЬ
					 |	АвтомобилиОтданныеОстатки.Автомобиль,
					 |	АвтомобилиОтданныеОстатки.КоличествоОстаток,
					 |	2
					 |ИЗ
					 |	РегистрНакопления.АвтомобилиОтданные.Остатки(
					 |			&Момент,
					 |			Автомобиль В
					 |				(ВЫБРАТЬ
					 |					ТаблицаАвтомобилей.Автомобиль
					 |				ИЗ
					 |					ТаблицаАвтомобилей)) КАК АвтомобилиОтданныеОстатки";
	КонецЕсли;
	
	Если ЭтотОбъект.ХозОперация <> Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателя Тогда			 
		Запрос.Текст = Запрос.Текст + " 			 
		             |ОБЪЕДИНИТЬ ВСЕ
		             |
		             |ВЫБРАТЬ
		             |	РеализованныеАвтомобилиОстатки.Автомобиль,
		             |	РеализованныеАвтомобилиОстатки.КоличествоОстаток,
		             |	3
		             |ИЗ
		             |	РегистрНакопления.РеализованныеАвтомобили.Остатки(
		             |			&Момент,
		             |			Автомобиль В
		             |				(ВЫБРАТЬ
		             |					ТаблицаАвтомобилей.Автомобиль
		             |				ИЗ
		             |					ТаблицаАвтомобилей)) КАК РеализованныеАвтомобилиОстатки";
	КонецЕсли;
				 
	Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Момент",Новый Граница(ЭтотОбъект.МоментВремени(),ВидГраницы.Исключая));
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		//Отказ = Истина;
		//Результат = Ложь;  
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.ФлагРегистра = 2 Тогда
				Продолжить;
			КонецЕсли;
			
			Отказ = Истина;
			Результат = Ложь;		
		
		//Пока Выборка.Следующий() Цикл
			Если Выборка.ФлагРегистра = 1 Тогда
				ТекстОписаниеРегистра = "уже имеется на складе.";	
				Статус = "Важное&Остатки автомобилей:";
			ИначеЕсли Выборка.ФлагРегистра = 2 Тогда
				ТекстОписаниеРегистра = "передавался на реализацию.";	
				Статус = "Важное&Автомобили отданные:";
			ИначеЕсли Выборка.ФлагРегистра = 3 Тогда
				ТекстОписаниеРегистра = "был взят на комиссию.";	
				Статус = "Важное&Реализованные автомобили:";
			ИначеЕсли Выборка.ФлагРегистра = 4 Тогда
				ТекстОписаниеРегистра = "находится в эксплуатации.";	
				Статус = "Важное&Прочие активы в эксплуатации:";
			КонецЕсли;
			дкСообщитьРезультат("Автомобиль """+СокрЛП(Выборка.Автомобиль) + """ " + ТекстОписаниеРегистра + " Повторный приход автомобиля недопустим." ,Статус,ЭтотОбъект,Выборка.Автомобиль);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;  	
КонецФункции

// Процедура получения ГТД автомобиля
//
// Регистратор - ДокументСсылка - Регистратор или список регистраторов, по которым необходимо получить данные.
// Автомобиль - СправочникСсылка.Автомобили - Автомобиль для поиска ГТД.
//
// Возвращаемое значение:
// Результат - ТаблицаЗначений - Найденный ГТД автомобиля по регистратору и автомобилю
//
Функция ПолучитьГТДАвтомобилей(Регистратор, Автомобиль = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Регистратор) И НЕ ЗначениеЗаполнено(Автомобиль) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтрокаУсловий = Новый Массив;
	
	Если ЗначениеЗаполнено(Регистратор) Тогда
		СтрокаУсловий.Добавить(" ОстаткиАвтомобилей.Регистратор В(&Регистратор) ");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		СтрокаУсловий.Добавить(" ОстаткиАвтомобилей.Автомобиль В(&Автомобиль) ");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОстаткиАвтомобилей.Автомобиль,
	|	ОстаткиАвтомобилей.ГТД
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|	ГДЕ "+СтрСоединить(СтрокаУсловий, "И");
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("Автомобиль",  Автомобиль);
	
	ВыполнениеЗапроса = Запрос.Выполнить();
	
	Если ВыполнениеЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ВыполнениеЗапроса.Выгрузить();
	
КонецФункции // ПолучитьГТДАвтомобилей()
Перем Покупатель Экспорт; // контрагент
Перем ДоговорВзаиморасчетов Экспорт; // договор взаиморасчетов с контрагентом
Перем СкладКомпании Экспорт; // склад компании
Перем ДокументПродажи Экспорт; // Документ продажи
Перем Комиссия Экспорт; // Булево. Истина - продажи комиссионером, Ложь - продажи наши
Перем ЕстьАвтомобиль Экспорт; // Булево. Признак дополнительной детализации по автомобилю
Перем Автомобиль Экспорт; // Автомобиль
Перем Сторно Экспорт; // Булево. Истина - возврат от покупателя
Перем ПодразделениеКомпании Экспорт;     // подразделение компании
Перем ДокументОбъект Экспорт;            // документ объект
Перем РезультатЗапросаПоТоварам Экспорт; // результат запроса по товарам
Перем РезультатЗапросаПоПартиям Экспорт; // результат запроса по партиям
Перем КэшСебестоимостиПриКомиссии Экспорт; // Кэш себестоимости при комиссии
Перем ИмяРеквизитаДокумент Экспорт; // Строка. Имя реквизита табличной части "Товары". Этот реквизит определяет партию либо документ отгрузки
Перем ПоБазовомуКоличеству Экспорт; // Булево. Ложь - количество товаров будет раcсчитываться как "Количество*Коэффициент", Истина - количество будет браться из реквизита "КоличествоБазовое"
Перем ШапкаДокумента Экспорт; 			// Выборка или строка таблицы значений, в которой содержаться необходимые данные о шапке документа
Перем РежимПроведения Экспорт;		// Режим проведения документа оперативный/неоперативный

// получаем то, что продали
// Продаем - Булево. Истина - продаем товар, Ложь - возвращаем
Функция ПолучитьТаблицуТоваров(Продаем=Истина) Экспорт
	
	Комиссия   = ?(Комиссия = Неопределено, Ложь, Комиссия);
	ВидДок     = ДокументОбъект.Метаданные().Имя;
	ПоБазовомуКоличеству = ?(ПоБазовомуКоличеству = Неопределено, Ложь, ПоБазовомуКоличеству);
	// посмотрим есть в документе сумма скидки
	ЕстьСкидка = (ДокументОбъект.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("СуммаСкидки")<>Неопределено);
	ЕстьДокументПродажи = (ДокументОбъект.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("ДокументПродажи")<>Неопределено);
	ЕстьГТД    = (ДокументОбъект.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("ГТД")<>Неопределено);
	
	ТекстСтрочнойСкидки = "";
	Если ДокументОбъект.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("СуммаСкидкиСтроки")<>Неопределено Тогда
		ТекстСтрочнойСкидки = " + ДокументТовары.СуммаСкидкиСтроки";
	КонецЕсли;
	
	АвтомобильВТаблице = (Автомобиль = Неопределено);
	
	// формируем запрос
	Запрос=Новый Запрос("
	|ВЫБРАТЬ" + ?(ЕстьАвтомобиль, ?(АвтомобильВТаблице, "
	|	ДокументАвтомобили.Автомобиль КАК Автомобиль,", "
	|	&Автомобиль КАК Автомобиль,"), "") + "
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ДокументТовары.СтавкаНДС КАК СтавкаНДС" + "
	|	"+?(СкладКомпании = Неопределено, ",ДокументТовары.МестоРазмещения КАК СкладКомпании", "") + "
	|	"+?(ЗначениеЗаполнено(ИмяРеквизитаДокумент), ",ДокументТовары." + ИмяРеквизитаДокумент + " КАК Партия", "") + ",
	|	"+?(ЕстьДокументПродажи,"ДокументТовары.ДокументПродажи КАК ДокументПродажи,","")+"
	|	"+?(ЕстьГТД,"ДокументТовары.ГТД КАК ГТД,","")+"
	|	СУММА(ДокументТовары.Количество"+?(ПоБазовомуКоличеству,"Базовое","*ДокументТовары.Коэффициент")+") КАК Количество,
	|	СУММА(ДокументТовары.СуммаВсего) КАК Сумма,
	|	СУММА(ДокументТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА("+?(ЕстьСкидка,"ДокументТовары.СуммаСкидки","0")+ТекстСтрочнойСкидки+") КАК СуммаСкидки
	|ИЗ
	|	Документ."+ВидДок+".Товары КАК ДокументТовары" + ?(ЕстьАвтомобиль И АвтомобильВТаблице, "
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Документ."+ВидДок+".Автомобили КАК ДокументАвтомобили
	|ПО
	|	ДокументТовары.ИдентификаторАвтомобиля = ДокументАвтомобили.ИдентификаторАвтомобиля И ДокументАвтомобили.Ссылка=&Ссылка", "") + "
	|ГДЕ
	|	ДокументТовары.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО" + ?(ЕстьАвтомобиль И АвтомобильВТаблице, "
	|	ДокументАвтомобили.Автомобиль,", "") + "
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры,
	|	ДокументТовары.СтавкаНДС" + ?(СкладКомпании = Неопределено,",
	|	ДокументТовары.МестоРазмещения", "") + ?(ЗначениеЗаполнено(ИмяРеквизитаДокумент), ",
	|	ДокументТовары." + ИмяРеквизитаДокумент, "") + "
	|	"+?(ЕстьДокументПродажи,",ДокументТовары.ДокументПродажи","")+"
	|	"+?(ЕстьГТД,",ДокументТовары.ГТД","")+"
	|УПОРЯДОЧИТЬ ПО
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры
	|	"+?(обЗначениеНеЗаполнено(ИмяРеквизитаДокумент),"",",ДокументТовары."+ИмяРеквизитаДокумент+" Убыв")+"
	|	"+?(ЕстьГТД,",ДокументТовары.ГТД Убыв","")+"
	|");
	Запрос.УстановитьПараметр("Ссылка",     ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// фиксируем продажи
Функция Приход() Экспорт
	
	//allgk
	НижеСеб = Ложь;
	//allgk
	
	Результат = Истина;
	// получаем товарную таблицу
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ 
		 (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса") И 
		  ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоТоварам = ПолучитьТаблицуТоваров();
	КонецЕсли;
	
	ПартииУказаны = ЗначениеЗаполнено(ИмяРеквизитаДокумент);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл, ШапкаДокумента.Дата);
	КурсРегл  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр, ШапкаДокумента.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ШапкаДокумента.КурсВалютыУпр;
	КонецЕсли;
	// формируем движения
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		ТаблицаТовары = РезультатЗапросаПоТоварам.Выгрузить();
	Иначе
		ТаблицаТовары = РезультатЗапросаПоТоварам.Скопировать();
	КонецЕсли;
	ЕстьГТДТоваров = (ТаблицаТовары.Колонки.Найти("ГТД")<>Неопределено);
	ЕстьДокументПродажиТоваров = (ТаблицаТовары.Колонки.Найти("ДокументПродажи")<>Неопределено);
	
	// получаем таблицу партий для расчета себестоимости
	ТаблицаГТД = Неопределено;
	Если РезультатЗапросаПоПартиям = Неопределено Тогда
		ЕстьДвиженияПоПартиям = (ДокументОбъект.Движения.Найти("ПартииТоваровКомпании")<>Неопределено) ИЛИ (ДокументОбъект.Движения.Найти("ПартииТоваровОтданные")<>Неопределено);
		Если ЕстьДвиженияПоПартиям Тогда
			Если НЕ Комиссия Тогда
				ТаблицаПартий = ДокументОбъект.Движения.ПартииТоваровКомпании.Выгрузить();
				ТаблицаГТД = ДокументОбъект.Движения.ГТДПартийТоваровКомпании.Выгрузить();
			Иначе
				ТаблицаПартий = ДокументОбъект.Движения.ПартииТоваровОтданные.Выгрузить();
			КонецЕсли;
		Иначе
			дкСообщитьРезультат("Документ <"+СокрЛП(ДокументОбъект)+"> не имеет информации о движениях по партиям.","Важное&Продажи:", ДокументОбъект);
		КонецЕсли; 
	Иначе //документ может и не иметь движений по партионному регистру (например реализация прочих активов)
		ТаблицаПартий = РезультатЗапросаПоПартиям.Скопировать();
	КонецЕсли;
	
	Если ТаблицаГТД = Неопределено Тогда
		ТаблицаГТД = ТаблицаПартий.Скопировать();
	КонецЕсли;
	
	Если НЕ Сторно Тогда
		СтруктураОтбора = Новый Структура("ВидДвижения", ВидДвиженияНакопления.Приход);
		МассивНайденныхПартий = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
		Для Каждого ТекСтрока Из МассивНайденныхПартий Цикл
			ТаблицаПартий.Удалить(ТекСтрока);
		КонецЦикла;
		
		МассивНайденныхПартий = ТаблицаГТД.НайтиСтроки(СтруктураОтбора);
		Для Каждого ТекСтрока Из МассивНайденныхПартий Цикл
			ТаблицаГТД.Удалить(ТекСтрока);
		КонецЦикла;
	КонецЕсли;
	
	// если у нас комиссионная операция (отчет комиссионера), то надо получить реальную себестоимость товаров
	Если КэшСебестоимостиПриКомиссии = Неопределено Тогда
		Если Комиссия Тогда
			Запрос=Новый Запрос("ВЫБРАТЬ
			                    |	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
			                    |	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			                    |	ПартииТоваровКомпании.Партия КАК Партия,
			                    |	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС,
			                    |	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
			                    |	СУММА(ПартииТоваровКомпании.СуммаНДС) КАК СуммаНДС,
			                    |	ПартииТоваровКомпании.Сумма КАК Сумма,
			                    |	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаУпр
			                    |ИЗ
			                    |	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
			                    |ГДЕ
			                    |	ПартииТоваровКомпании.Регистратор В(&Регистраторы)
			                    |	И ПартииТоваровКомпании.Партия В(&Партии)
			                    |	И ПартииТоваровКомпании.СтатусПартии = &СтатусПартии
			                    |	И ПартииТоваровКомпании.Номенклатура В(&Номенклатура)
			                    |	И ПартииТоваровКомпании.ХарактеристикаНоменклатуры В(&ХарактеристикиНоменклатуры)
			                    |	И ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
			                    |
			                    |СГРУППИРОВАТЬ ПО
			                    |	ПартииТоваровКомпании.Номенклатура,
			                    |	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
			                    |	ПартииТоваровКомпании.Партия,
			                    |	ПартииТоваровКомпании.СтавкаНДС,
			                    |	ПартииТоваровКомпании.Регистратор,
			                    |	ПартииТоваровКомпании.Сумма
			                    |");
			Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Расход);
			Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварКупленный);
			Запрос.УстановитьПараметр("Регистраторы",ТаблицаПартий.ВыгрузитьКолонку("ДокументПередачи"));
			Запрос.УстановитьПараметр("Партии",ТаблицаПартий.ВыгрузитьКолонку("Партия"));
			Запрос.УстановитьПараметр("Номенклатура",ТаблицаПартий.ВыгрузитьКолонку("Номенклатура"));
			Запрос.УстановитьПараметр("ХарактеристикиНоменклатуры",ТаблицаПартий.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
			КэшСебестоимостиПриКомиссии = Запрос.Выполнить().Выгрузить();
		Иначе
			КэшСебестоимостиПриКомиссии = Новый ТаблицаЗначений;
		КонецЕсли;
	КонецЕсли; 
	
	АвтомобильВТаблице = (ЕстьАвтомобиль И Автомобиль = Неопределено);
	
	// свернем таблицу движений
	ЕстьСклад       = (ТаблицаПартий.Колонки.Найти("СкладКомпании")<>Неопределено);
	ЕстьПартия      = (ТаблицаПартий.Колонки.Найти("Партия")<>Неопределено);
	ЕстьДокПродажи  = (ТаблицаПартий.Колонки.Найти("ДокументПродажи")<>Неопределено);
	ЕстьГТДПартий   = (ТаблицаПартий.Колонки.Найти("ГТД")<>Неопределено);
	ЕстьГТД         = (ТаблицаГТД.Колонки.Найти("ГТД")<>Неопределено);
	ЕстьГТДКомиссия = (КэшСебестоимостиПриКомиссии.Колонки.Найти("ГТД")<>Неопределено);
	ЕстьСтавкаНДС   = (ТаблицаПартий.Колонки.Найти("СтавкаНДС")<>Неопределено);
	
	ТаблицаПартий.Свернуть(?(ЕстьДокПродажи, "ДокументПродажи, ", "") + ?(АвтомобильВТаблице, "Автомобиль, ", "")+?(ЕстьСклад,"СкладКомпании, ","")+?(ЕстьГТДПартий,"ГТД, ","")+"Номенклатура, ХарактеристикаНоменклатуры, Партия"+?(Комиссия,"",", СтатусПартии") + ?(ЕстьСтавкаНДС,", СтавкаНДС",""), "Сумма, СуммаУпр, Количество, СуммаНДС");
	// надо закэшировать контрагента и договор
	КэшКонтрагент = Новый Соответствие();
	Для Каждого СтрокаПартия Из ТаблицаПартий Цикл
		Если КэшКонтрагент[СтрокаПартия.Партия]<>Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 Контрагент ИЗ Документ."+СтрокаПартия.Партия.Метаданные().Имя+" ГДЕ Ссылка=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка", СтрокаПартия.Партия);
		Попытка
			РезультатКэш = Запрос.Выполнить();
			Если НЕ РезультатКэш.Пустой() Тогда
				КэшКонтрагент.Вставить(СтрокаПартия.Партия, РезультатКэш.Выгрузить().Получить(0).Контрагент);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	СтрокаСортировки = "Номенклатура ВОЗР, ХарактеристикаНоменклатуры УБЫВ";
	Если ПартииУказаны Тогда
		// Обработаем первыми строки с выбранными партиями
		СтрокаСортировки = СтрокаСортировки + ", Партия УБЫВ";
	КонецЕсли;
	
	Если ЕстьГТДТоваров Тогда
		// Обработаем первыми строки с выбранными партиями
		СтрокаСортировки = СтрокаСортировки + ", ГТД УБЫВ";
	КонецЕсли;
	
	ТаблицаТовары.Сортировать(СтрокаСортировки);
	
	ЗапретПродажиНижеСебестоимости = обПраво("ЗапретитьПродажуНижеСебестоимости", ДокументОбъект.Права,,ДокументОбъект);
	СтруктураОтбораСебестоимостьКомиссии = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Партия");
	
	// получим таблицу номенклатуры с ручным списанием характеристик
	ТаблицаРучныхХарактеристик = дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(ШапкаДокумента.Ссылка);
	
	Права=ДокументОбъект.Права;
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ДокументОбъект).Имя;
	
	Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
		КоличествоДляСумм  = ?(СтрокаТовар.Количество=0, 1, СтрокаТовар.Количество);
		СуммаПродажи       = Окр(?(СтрокаТовар.Сумма=NULL,0,обПересчет(СтрокаТовар.Сумма,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		ЦенаПродажи        = СуммаПродажи/КоличествоДляСумм;
		СуммаПродажиУпр    = Окр(?(СтрокаТовар.Сумма=NULL,0,обПересчет(СтрокаТовар.Сумма,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр)),2);
		ЦенаПродажиУпр     = СуммаПродажиУпр/КоличествоДляСумм;
		СуммаПродажиСкидки = Окр(?(СтрокаТовар.СуммаСкидки=NULL,0,обПересчет(СтрокаТовар.СуммаСкидки,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		ЦенаПродажиСкидки  = СуммаПродажиСкидки/КоличествоДляСумм;
		СуммаПродажиНДС    = Окр(?(СтрокаТовар.СуммаНДС=NULL,0,обПересчет(СтрокаТовар.СуммаНДС,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
		ЦенаПродажиНДС     = СуммаПродажиНДС/КоличествоДляСумм;
		
		СуммаПродажиИтого       = 0;
		СуммаПродажиУпрИтого    = 0;
		СуммаПродажиСкидкиИтого = 0;
		СуммаПродажиНДСИтого    = 0;
		
		СтруктураОтбора = Новый Структура("Номенклатура", СтрокаТовар.Номенклатура);
		Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ 
			 (ТаблицаРучныхХарактеристик<>Неопределено И 
			  ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда 
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
		КонецЕсли;
		
		Если ЕстьДокПродажи Тогда
			Если ЕстьДокументПродажиТоваров Тогда
				СтруктураОтбора.Вставить("ДокументПродажи", СтрокаТовар.ДокументПродажи);
			Иначе
				СтруктураОтбора.Вставить("ДокументПродажи", ДокументПродажи);
			КонецЕсли;
		КонецЕсли;
		
		Если АвтомобильВТаблице Тогда
			СтруктураОтбора.Вставить("Автомобиль", СтрокаТовар.Автомобиль);
		КонецЕсли;
		Если ЕстьГТДТоваров И ЕстьГТДПартий И ЗначениеЗаполнено(СтрокаТовар.ГТД) Тогда
			СтруктураОтбора.Вставить("ГТД", СтрокаТовар.ГТД);
		КонецЕсли; 
		
		Если ЕстьСклад Тогда
			Если СкладКомпании = Неопределено Тогда
				СтруктураОтбора.Вставить("СкладКомпании", СтрокаТовар.СкладКомпании);
			Иначе
				СтруктураОтбора.Вставить("СкладКомпании", СкладКомпании);
			КонецЕсли;
			// Для заказ-наряда склад/цех может не совпадать в документе продажи и при перемещении в производство в другой цех (lismak 22.11.2017)
			Если ТипЗнч(ЭтотОбъект.ДокументПродажи) = Тип("ДокументСсылка.ЗаказНаряд") И ТаблицаПартий.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда
				СтруктураОтбора.Удалить("СкладКомпании");
			КонецЕсли;
		КонецЕсли;
		
		МассивНайденныхПартий=ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
		
		Если МассивНайденныхПартий.Количество()>0 Тогда
			НоваяЗапись=Неопределено;
			Для Каждого СтрокаПартий Из МассивНайденныхПартий Цикл
				
				// для возвратов проверим не лишнее ли мы сторнируем
				Если Сторно И (СтрокаПартий.Партия=ШапкаДокумента.Ссылка ИЛИ (ДокументПродажи = Неопределено И ЕстьДокументПродажиТоваров И СтрокаТовар.ДокументПродажи = Неопределено)) Тогда
					Продолжить;
				КонецЕсли;
				
				// если указаны партии, то пропустим все партии не наши
				Если ПартииУказаны И ЗначениеЗаполнено(СтрокаТовар.Партия) И СтрокаПартий.Партия <> СтрокаТовар.Партия Тогда
					Продолжить;
				КонецЕсли;
				
				СебестоимостьРавнаПродаже = Ложь;
				Попытка
					Если СтрокаПартий.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия И (НЕ СтрокаПартий.Партия.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж) Тогда
						СебестоимостьРавнаПродаже = Истина;
					КонецЕсли;
				Исключение
					СебестоимостьРавнаПродаже = Ложь;
				КонецПопытки;
				
				СтруктураОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Партия", СтрокаПартий.Номенклатура, СтрокаПартий.ХарактеристикаНоменклатуры, СтрокаПартий.Партия);
				Если АвтомобильВТаблице Тогда
					СтруктураОтбора.Вставить("Автомобиль", СтрокаПартий.Автомобиль);
				КонецЕсли;
				
				Если ЕстьГТДТоваров И ЗначениеЗаполнено(СтрокаТовар.ГТД) Тогда
					СтруктураОтбора.Вставить("ГТД", СтрокаТовар.ГТД);
				ИначеЕсли (НЕ ЕстьГТДТоваров) И ЕстьГТДПартий И ЗначениеЗаполнено(СтрокаПартий.ГТД) Тогда
					СтруктураОтбора.Вставить("ГТД", СтрокаПартий.ГТД);
				КонецЕсли;
				
				МассивНайденныхГТД = ТаблицаГТД.НайтиСтроки(СтруктураОтбора);
				БезГТД = МассивНайденныхГТД.Количество()=0;
				Для Каждого СтрокаГТД Из МассивНайденныхГТД Цикл
					
					Если Сторно Тогда
						КоличествоПродажи = Макс(-СтрокаТовар.Количество,СтрокаПартий.Количество,СтрокаГТД.Количество);
					Иначе
						КоличествоПродажи = Мин(СтрокаТовар.Количество,СтрокаПартий.Количество,СтрокаГТД.Количество);
					КонецЕсли;
					
					Сумма    = Окр(ЦенаПродажи*КоличествоПродажи,    2);
					СуммаУпр = Окр(ЦенаПродажиУпр*КоличествоПродажи, 2);
					// При комиссии с себестоимостью не так все просто
					СебестоимостьУпр = 0;
					Себестоимость    = 0;
					СуммаНДСВходящий = 0;
					Если Комиссия Тогда
						СтруктураОтбораСебестоимостьКомиссии.Номенклатура               = СтрокаПартий.Номенклатура;
						СтруктураОтбораСебестоимостьКомиссии.ХарактеристикаНоменклатуры = СтрокаПартий.ХарактеристикаНоменклатуры;
						СтруктураОтбораСебестоимостьКомиссии.Партия                     = СтрокаПартий.Партия;
						Если АвтомобильВТаблице Тогда
							СтруктураОтбораСебестоимостьКомиссии.Вставить("Автомобиль", СтрокаПартий.Автомобиль);
						КонецЕсли;
						Если ЕстьГТД И ЕстьГТДКомиссия Тогда
							СтруктураОтбораСебестоимостьКомиссии.Вставить("ГТД", СтрокаГТД.ГТД);
						КонецЕсли;
						СчетчикКоличествоПродажи = КоличествоПродажи;
						МассивНайденныхСтрокСебестоимости = КэшСебестоимостиПриКомиссии.НайтиСтроки(СтруктураОтбораСебестоимостьКомиссии);
						Для Каждого СтрокаСебестоимости Из МассивНайденныхСтрокСебестоимости Цикл
							
							Если СтрокаСебестоимости.Количество = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							Если СчетчикКоличествоПродажи = 0 Тогда
								Прервать;
							КонецЕсли;
							
							Если СтрокаСебестоимости.Количество > СчетчикКоличествоПродажи Тогда
								КоличествоДляСумм  = ?(СтрокаСебестоимости.Количество=0, 1, СтрокаСебестоимости.Количество);
								ТекСебестоимость    = Окр(СтрокаСебестоимости.Сумма/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
								ТекСебестоимостьУпр = Окр(СтрокаСебестоимости.СуммаУпр/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
								ТекСуммаНДСВходящий = Окр(СтрокаСебестоимости.СуммаНДС/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
								
								СтрокаСебестоимости.Сумма      = СтрокаСебестоимости.Сумма - ТекСебестоимость;
								СтрокаСебестоимости.СуммаУпр   = СтрокаСебестоимости.СуммаУпр - ТекСебестоимостьУпр;
								СтрокаСебестоимости.СуммаНДС   = СтрокаСебестоимости.СуммаНДС - ТекСуммаНДСВходящий;
								СтрокаСебестоимости.Количество = СтрокаСебестоимости.Количество - СчетчикКоличествоПродажи;
								
								СчетчикКоличествоПродажи = 0;
							Иначе
								ТекСебестоимость    = СтрокаСебестоимости.Сумма;
								ТекСебестоимостьУпр = СтрокаСебестоимости.СуммаУпр;
								ТекСуммаНДСВходящий = СтрокаСебестоимости.СуммаНДС;
								
								СчетчикКоличествоПродажи = СчетчикКоличествоПродажи - СтрокаСебестоимости.Количество;
								
								КэшСебестоимостиПриКомиссии.Удалить(СтрокаСебестоимости);
							КонецЕсли;
							
							Себестоимость    = Себестоимость + ТекСебестоимость;
							СебестоимостьУпр = СебестоимостьУпр + ТекСебестоимостьУпр;
							СуммаНДСВходящий = СуммаНДСВходящий + ТекСуммаНДСВходящий;
						КонецЦикла;
					ИначеЕсли Сторно И СтрокаПартий.Количество < КоличествоПродажи Тогда
						// при выборочной партие списано больше чем в строке
						КоличествоДляСумм = ?(СтрокаПартий.Количество=0, 1, СтрокаПартий.Количество);
						Себестоимость     = Окр(СтрокаПартий.Сумма/КоличествоДляСумм*КоличествоПродажи, 2);
						СебестоимостьУпр  = Окр(СтрокаПартий.СуммаУпр/КоличествоДляСумм*КоличествоПродажи, 2);
						СуммаНДСВходящий  = Окр(СтрокаПартий.СуммаНДС/КоличествоДляСумм*КоличествоПродажи, 2);
						
						СтрокаПартий.Сумма      = СтрокаПартий.Сумма - Себестоимость;
						СтрокаПартий.СуммаУпр   = СтрокаПартий.СуммаУпр - СебестоимостьУпр;
						СтрокаПартий.СуммаНДС   = СтрокаПартий.СуммаНДС - СуммаНДСВходящий;
					ИначеЕсли СтрокаПартий.Количество > КоличествоПродажи Тогда
						// при выборочной партие списано больше чем в строке
						КоличествоДляСумм = ?(СтрокаПартий.Количество=0, 1, СтрокаПартий.Количество);
						Себестоимость     = Окр(СтрокаПартий.Сумма/КоличествоДляСумм*КоличествоПродажи, 2);
						СебестоимостьУпр  = Окр(СтрокаПартий.СуммаУпр/КоличествоДляСумм*КоличествоПродажи, 2);
						СуммаНДСВходящий  = Окр(СтрокаПартий.СуммаНДС/КоличествоДляСумм*КоличествоПродажи, 2);
						
						СтрокаПартий.Сумма      = СтрокаПартий.Сумма - Себестоимость;
						СтрокаПартий.СуммаУпр   = СтрокаПартий.СуммаУпр - СебестоимостьУпр;
						СтрокаПартий.СуммаНДС   = СтрокаПартий.СуммаНДС - СуммаНДСВходящий;
					Иначе
						СебестоимостьУпр = СтрокаПартий.СуммаУпр;
						Себестоимость    = СтрокаПартий.Сумма;
						СуммаНДСВходящий = СтрокаПартий.СуммаНДС;
					КонецЕсли;  
					
					//allgk
						РасчетРегл = ШапкаДокумента.ВалютаДокумента=ВалютаРегл И ШапкаДокумента.КурсДокумента=КурсРегл;
						Если РасчетРегл Тогда
							Если Себестоимость>Сумма Тогда НижеСеб=Истина; КонецЕсли;
						Иначе
							Если СебестоимостьУпр>СуммаУпр Тогда НижеСеб=Истина; КонецЕсли;
						КонецЕсли;
					//allgk					
					Если ЗапретПродажиНижеСебестоимости И НЕ Сторно И СтрокаТовар.Количество>0 Тогда
						НижеСебестоимости=Ложь;
						РасчетРегл = ШапкаДокумента.ВалютаДокумента=ВалютаРегл И ШапкаДокумента.КурсДокумента=КурсРегл;
						Если РасчетРегл Тогда
							Если Себестоимость>Сумма Тогда НижеСебестоимости=Истина; КонецЕсли;
						Иначе
							Если СебестоимостьУпр>СуммаУпр Тогда НижеСебестоимости=Истина; КонецЕсли;
						КонецЕсли; 
						Если НижеСебестоимости Тогда
							Если РасчетРегл Тогда
								РасчетВалюта = ВалютаРегл;
							Иначе
								РасчетВалюта = ВалютаУпр;
							КонецЕсли;
							СтрокаИнформации=". Себестоимость товара "+?(РасчетРегл,Себестоимость,СебестоимостьУпр)+" "+РасчетВалюта+", сумма продажи "+?(РасчетРегл,Сумма,СуммаУпр)+" "+РасчетВалюта;
							Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
								дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""""+СтрокаИнформации+". Продажа ниже себестоимости запрещена.","Важное&Продажи:",ДокументОбъект,СтрокаТовар.Номенклатура);
							Иначе
								дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""""+СтрокаИнформации+" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""". Продажа ниже себестоимости запрещена.","Важное&Продажи:",ДокументОбъект,СтрокаТовар.Номенклатура);
							КонецЕсли; 
							Результат=Ложь;
						КонецЕсли; 
					КонецЕсли; 
					НоваяЗапись=Добавить();
					НоваяЗапись.Период                     = ШапкаДокумента.Дата;
					НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
					НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
					НоваяЗапись.Номенклатура               = СтрокаГТД.Номенклатура;
					ЭтоУслуга = НоваяЗапись.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга;
					Если ДокументПродажи<>Неопределено Тогда
						НоваяЗапись.ДокументПродажи=ДокументПродажи;
					Иначе
						Если ЕстьДокументПродажиТоваров Тогда
							НоваяЗапись.ДокументПродажи=СтрокаТовар.ДокументПродажи;
						КонецЕсли;
					КонецЕсли;
					НоваяЗапись.Поставщик                  = КэшКонтрагент[СтрокаГТД.Партия];
					НоваяЗапись.Покупатель                 = Покупатель;
					НоваяЗапись.СтатусПартии               = ?(Комиссия,Перечисления.СтатусыПартий.ТоварКупленный,СтрокаПартий.СтатусПартии);
					НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
					НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
					НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаГТД.ХарактеристикаНоменклатуры;
					Если ЕстьАвтомобиль Тогда
						НоваяЗапись.Автомобиль             = СтрокаТовар.Автомобиль;
					КонецЕсли;
					Если НЕ (Комиссия ИЛИ ЭтоУслуга) Тогда
						Если СкладКомпании=Неопределено Тогда
							НоваяЗапись.СкладКомпании      = СтрокаТовар.СкладКомпании;
						Иначе
							НоваяЗапись.СкладКомпании      = СкладКомпании;
						КонецЕсли; 
					КонецЕсли;
					НоваяЗапись.СтавкаНДС                  = СтрокаТовар.СтавкаНДС;
					НоваяЗапись.Партия                     = СтрокаПартий.Партия;
					// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
					// осуществляющих движения по данному регистру есть данный реквизит. 
					Попытка
						НоваяЗапись.Проект                 = ШапкаДокумента.Проект;
					Исключение
					КонецПопытки;
					Если ЕстьГТД Тогда
						НоваяЗапись.ГТД                    = СтрокаГТД.ГТД;
					Иначе
						НоваяЗапись.ГТД                    = Справочники.ГТД.ПустаяСсылка();
					КонецЕсли; 
					// количество
					НоваяЗапись.Количество                 = КоличествоПродажи;
					// суммы
					НоваяЗапись.СуммаНДСВходящий           = Окр(СуммаНДСВходящий, 2);
					НоваяЗапись.СебестоимостьУпр           = Окр(СебестоимостьУпр, 2);
					НоваяЗапись.Себестоимость              = Окр(Себестоимость, 2);
					НоваяЗапись.Сумма                      = Сумма;
					НоваяЗапись.СуммаСкидки                = Окр(ЦенаПродажиСкидки*КоличествоПродажи,2);
					НоваяЗапись.СуммаУпр                   = СуммаУпр;
					НоваяЗапись.СуммаНДС                   = Окр(ЦенаПродажиНДС*КоличествоПродажи,2);
					
					Если СебестоимостьРавнаПродаже И НЕ( ТипЗнч(НоваяЗапись.Регистратор) = Тип("ДокументСсылка.ВозвратОтПокупателя")) Тогда
						
						СебестоимостьБезНДСВходящей = НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий;
						СтавкаНДСВходящее   = ?(СебестоимостьБезНДСВходящей = 0, 0, Окр(НоваяЗапись.СуммаНДСВходящий/СебестоимостьБезНДСВходящей, 2));
						СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
						
						НоваяЗапись.Себестоимость    = Сумма;
						НоваяЗапись.СебестоимостьУпр = СуммаУпр;
						НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
						
					КонецЕсли;
					
					СуммаПродажиИтого       = СуммаПродажиИтого + Сумма;
					СуммаПродажиУпрИтого    = СуммаПродажиУпрИтого + СуммаУпр;
					СуммаПродажиСкидкиИтого = СуммаПродажиСкидкиИтого + НоваяЗапись.СуммаСкидки;
					СуммаПродажиНДСИтого    = СуммаПродажиНДСИтого + НоваяЗапись.СуммаНДС;
					
					// если возвраты, то сделаем все отрицательным
					Если Сторно Тогда
						НоваяЗапись.Количество       = НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,-1,1);
						НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СебестоимостьУпр*?(НоваяЗапись.СебестоимостьУпр>0,-1,1);
						НоваяЗапись.Себестоимость    = НоваяЗапись.Себестоимость*?(НоваяЗапись.Себестоимость>0,-1,1);
						НоваяЗапись.СуммаНДСВходящий = НоваяЗапись.СуммаНДСВходящий*?(НоваяЗапись.СуммаНДСВходящий>0,-1,1);
					КонецЕсли;
				
					СтрокаТовар.Количество  = СтрокаТовар.Количество-НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,1,-1);
					СтрокаПартий.Количество = СтрокаПартий.Количество-НоваяЗапись.Количество;
					СтрокаГТД.Количество    = СтрокаГТД.Количество-НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,1,-1);
					// удалим за ненадобностью
					Если СтрокаГТД.Количество = 0 Тогда
						ТаблицаГТД.Удалить(СтрокаГТД);
						СтрокаГТД=Неопределено;
					КонецЕсли;
					// несколько ГТД в одной партии
					Если СтрокаПартий.Количество = 0 Тогда
						Прервать;
					КонецЕсли;
					
					Если СтрокаТовар.Количество = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если БезГТД ИЛИ (СтрокаТовар.Количество>0 И СтрокаПартий.Количество<>0) Тогда
					
					КоличествоПродажи = Мин(СтрокаТовар.Количество, СтрокаПартий.Количество);
					Сумма    = Окр(ЦенаПродажи*КоличествоПродажи, 2);
					СуммаУпр = Окр(ЦенаПродажиУпр*КоличествоПродажи, 2);
					СебестоимостьУпр    = 0;
					Себестоимость       = 0;
					ТекСуммаНДСВходящий = 0;
					Если Комиссия Тогда
						СтруктураОтбораСебестоимостьКомиссии.Номенклатура               = СтрокаПартий.Номенклатура;
						СтруктураОтбораСебестоимостьКомиссии.ХарактеристикаНоменклатуры = СтрокаПартий.ХарактеристикаНоменклатуры;
						СтруктураОтбораСебестоимостьКомиссии.Партия                     = СтрокаПартий.Партия;
						
						Если АвтомобильВТаблице Тогда
							СтруктураОтбораСебестоимостьКомиссии.Вставить("Автомобиль", СтрокаПартий.Автомобиль);
						КонецЕсли;
						
						СчетчикКоличествоПродажи = КоличествоПродажи;
						МассивНайденныхСтрокСебестоимости = КэшСебестоимостиПриКомиссии.НайтиСтроки(СтруктураОтбораСебестоимостьКомиссии);
						Для Каждого СтрокаСебестоимости Из МассивНайденныхСтрокСебестоимости Цикл
							
							Если СтрокаСебестоимости.Количество = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							Если СчетчикКоличествоПродажи = 0 Тогда
								Прервать;
							КонецЕсли;
							
							Если СтрокаСебестоимости.Количество > СчетчикКоличествоПродажи Тогда
								КоличествоДляСумм = ?(СтрокаСебестоимости.Количество=0, 1, СтрокаСебестоимости.Количество);
								ТекСебестоимость    = Окр(СтрокаСебестоимости.Сумма/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
								ТекСебестоимостьУпр = Окр(СтрокаСебестоимости.СуммаУпр/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
								ТекСуммаНДСВходящий = Окр(СтрокаСебестоимости.СуммаНДС/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
								
								СтрокаСебестоимости.Сумма      = СтрокаСебестоимости.Сумма - ТекСебестоимость;
								СтрокаСебестоимости.СуммаУпр   = СтрокаСебестоимости.СуммаУпр - ТекСебестоимостьУпр;
								СтрокаСебестоимости.СуммаНДС   = СтрокаСебестоимости.СуммаНДС - ТекСуммаНДСВходящий;
								СтрокаСебестоимости.Количество = СтрокаСебестоимости.Количество - СчетчикКоличествоПродажи;
								
								СчетчикКоличествоПродажи = 0;
							Иначе
								ТекСебестоимость    = СтрокаСебестоимости.Сумма;
								ТекСебестоимостьУпр = СтрокаСебестоимости.СуммаУпр;
								ТекСуммаНДСВходящий = СтрокаСебестоимости.СуммаНДС;
								
								СчетчикКоличествоПродажи = СчетчикКоличествоПродажи - СтрокаСебестоимости.Количество;
								
								КэшСебестоимостиПриКомиссии.Удалить(СтрокаСебестоимости);
								
							КонецЕсли;
							
							Себестоимость    = Себестоимость + ТекСебестоимость;
							СебестоимостьУпр = СебестоимостьУпр + ТекСебестоимостьУпр;
							СуммаНДСВходящий = СуммаНДСВходящий + ТекСуммаНДСВходящий;
						КонецЦикла;
						
					ИначеЕсли СтрокаПартий.Количество > СтрокаТовар.Количество Тогда
						// при выборочной партии списано больше чем в строке
						КоличествоДляСумм = ?(СтрокаПартий.Количество=0, 1, СтрокаПартий.Количество);
						Себестоимость    = Окр(СтрокаПартий.Сумма/КоличествоДляСумм*КоличествоПродажи, 2);
						СебестоимостьУпр = Окр(СтрокаПартий.СуммаУпр/КоличествоДляСумм*КоличествоПродажи, 2);
						СуммаНДСВходящий = Окр(СтрокаПартий.СуммаНДС/КоличествоДляСумм*КоличествоПродажи, 2);
						
						СтрокаПартий.Сумма    = СтрокаПартий.Сумма - Себестоимость;
						СтрокаПартий.СуммаУпр = СтрокаПартий.СуммаУпр - СебестоимостьУпр;
						СтрокаПартий.СуммаНДС = СтрокаПартий.СуммаНДС - СуммаНДСВходящий;
					Иначе
						СебестоимостьУпр = СтрокаПартий.СуммаУпр;
						Себестоимость    = СтрокаПартий.Сумма;
						СуммаНДСВходящий = СтрокаПартий.СуммаНДС;
					КонецЕсли;
					
					//allgk
						РасчетРегл = ШапкаДокумента.ВалютаДокумента=ВалютаРегл И ШапкаДокумента.КурсДокумента=КурсРегл;
						Если РасчетРегл Тогда
							Если Себестоимость>Сумма Тогда НижеСеб=Истина; КонецЕсли;
						Иначе
							Если СебестоимостьУпр>СуммаУпр Тогда НижеСеб=Истина; КонецЕсли;
						КонецЕсли;
					//allgk
					
					Если ЗапретПродажиНижеСебестоимости И НЕ Сторно И СтрокаТовар.Количество>0 Тогда
						НижеСебестоимости = Ложь;
						РасчетРегл = ШапкаДокумента.ВалютаДокумента=ВалютаРегл И ШапкаДокумента.КурсДокумента=КурсРегл;
						Если РасчетРегл Тогда
							Если Себестоимость>Сумма Тогда НижеСебестоимости=Истина; КонецЕсли;
						Иначе
							Если СебестоимостьУпр>СуммаУпр Тогда НижеСебестоимости=Истина; КонецЕсли;
						КонецЕсли;
						Если НижеСебестоимости Тогда
							Если РасчетРегл Тогда
								РасчетВалюта = ВалютаРегл;
							Иначе
								РасчетВалюта = ВалютаУпр;
							КонецЕсли;
							СтрокаИнформации=". Себестоимость товара "+?(РасчетРегл,Себестоимость,СебестоимостьУпр)+" "+РасчетВалюта+", сумма продажи "+?(РасчетРегл,Сумма,СуммаУпр)+" "+РасчетВалюта;
							Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
								дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""""+СтрокаИнформации+". Продажа ниже себестоимости запрещена.","Важное&Продажи:",ДокументОбъект,СтрокаТовар.Номенклатура);
							Иначе
								дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""""+СтрокаИнформации+" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""". Продажа ниже себестоимости запрещена.","Важное&Продажи:",ДокументОбъект,СтрокаТовар.Номенклатура);
							КонецЕсли;
							Результат=Ложь;
						КонецЕсли;
					КонецЕсли;
					НоваяЗапись = Добавить();
					НоваяЗапись.Период                     = ШапкаДокумента.Дата;
					НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
					НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
					НоваяЗапись.Номенклатура               = СтрокаПартий.Номенклатура;
					ЭтоУслуга = НоваяЗапись.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга;
					Если ДокументПродажи<>Неопределено Тогда
						НоваяЗапись.ДокументПродажи=ДокументПродажи;
					Иначе
						Если ЕстьДокументПродажиТоваров Тогда
							НоваяЗапись.ДокументПродажи=СтрокаТовар.ДокументПродажи;
						КонецЕсли;
					КонецЕсли;
					НоваяЗапись.Поставщик                  = КэшКонтрагент[СтрокаПартий.Партия];
					НоваяЗапись.Покупатель                 = Покупатель;
					НоваяЗапись.СтатусПартии               = ?(Комиссия, Перечисления.СтатусыПартий.ТоварКупленный, СтрокаПартий.СтатусПартии);
					НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
					НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
					НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаПартий.ХарактеристикаНоменклатуры;
					Если ЕстьАвтомобиль Тогда
						НоваяЗапись.Автомобиль             = СтрокаТовар.Автомобиль;
					КонецЕсли;
					Если НЕ (Комиссия ИЛИ ЭтоУслуга) Тогда
						Если СкладКомпании=Неопределено Тогда
							НоваяЗапись.СкладКомпании      = СтрокаТовар.СкладКомпании;
						Иначе
							НоваяЗапись.СкладКомпании      = СкладКомпании;
						КонецЕсли; 
					КонецЕсли;
					НоваяЗапись.СтавкаНДС                  = СтрокаТовар.СтавкаНДС;
					НоваяЗапись.Партия                     = СтрокаПартий.Партия;
					// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
					// осуществляющих движения по данному регистру есть данный реквизит. 
					Попытка
						НоваяЗапись.Проект                 = ШапкаДокумента.Проект;
					Исключение
					КонецПопытки;
					НоваяЗапись.ГТД                        = Справочники.ГТД.ПустаяСсылка();
					// количество
					НоваяЗапись.Количество                 = КоличествоПродажи;
					// суммы
					
					НоваяЗапись.Сумма                      = Сумма;
					НоваяЗапись.СуммаСкидки                = Окр(ЦенаПродажиСкидки*КоличествоПродажи, 2);
					НоваяЗапись.СуммаУпр                   = СуммаУпр;
					НоваяЗапись.СуммаНДС                   = Окр(ЦенаПродажиНДС*КоличествоПродажи, 2);
					НоваяЗапись.Себестоимость              = Окр(Себестоимость, 2);
					НоваяЗапись.СебестоимостьУпр           = Окр(СебестоимостьУпр, 2);
					НоваяЗапись.СуммаНДСВходящий           = Окр(СуммаНДСВходящий, 2);
					
					Если СебестоимостьРавнаПродаже И НЕ( ТипЗнч(НоваяЗапись.Регистратор) = Тип("ДокументСсылка.ВозвратОтПокупателя")) Тогда
						
						СебестоимостьБезНДСВходящей = НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий;
						СтавкаНДСВходящее   = ?(СебестоимостьБезНДСВходящей = 0, 0, Окр(НоваяЗапись.СуммаНДСВходящий/СебестоимостьБезНДСВходящей, 2));
						СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
						
						НоваяЗапись.Себестоимость    = Сумма;
						НоваяЗапись.СебестоимостьУпр = СуммаУпр;
						НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
						
					КонецЕсли;
					
					СуммаПродажиИтого       = СуммаПродажиИтого + Сумма;
					СуммаПродажиУпрИтого    = СуммаПродажиУпрИтого + СуммаУпр;
					СуммаПродажиСкидкиИтого = СуммаПродажиСкидкиИтого + НоваяЗапись.СуммаСкидки;
					СуммаПродажиНДСИтого    = СуммаПродажиНДСИтого + НоваяЗапись.СуммаНДС;
					
					// если возвраты, то сделаем все отрицательным
					Если Сторно Тогда
						НоваяЗапись.Количество       = НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,-1,1);
						НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СебестоимостьУпр*?(НоваяЗапись.СебестоимостьУпр>0,-1,1);
						НоваяЗапись.Себестоимость    = НоваяЗапись.Себестоимость*?(НоваяЗапись.Себестоимость>0,-1,1);
						НоваяЗапись.СуммаНДСВходящий = НоваяЗапись.СуммаНДСВходящий*?(НоваяЗапись.СуммаНДСВходящий>0,-1,1);
					КонецЕсли;
				
					СтрокаТовар.Количество  = СтрокаТовар.Количество-НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,1,-1);
					СтрокаПартий.Количество = СтрокаПартий.Количество-НоваяЗапись.Количество;
				КонецЕсли;
				
				Если СтрокаПартий.Количество = 0 Тогда
					ТаблицаПартий.Удалить(СтрокаПартий);
				КонецЕсли;
				
				Если СтрокаТовар.Количество = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			Если НоваяЗапись<>Неопределено Тогда
				//Записи по возврату может и не быть если возвращается товар, который не продавался
				//Прибавим погрешность округления к последней партии
				Если Сторно Тогда
					СуммаПродажи = ?(СуммаПродажи>0,-СуммаПродажи,СуммаПродажи);
					СуммаПродажиУпр = ?(СуммаПродажиУпр>0,-СуммаПродажиУпр,СуммаПродажиУпр);
					СуммаПродажиСкидки = -СуммаПродажиСкидки;
					СуммаПродажиНДС = ?(СуммаПродажиНДС>0,-СуммаПродажиНДС,СуммаПродажиНДС);
				КонецЕсли;
				СуммаПродажиПогрешность=СуммаПродажи-СуммаПродажиИтого;
				Если СуммаПродажиПогрешность<>0 Тогда
					НоваяЗапись.Сумма=НоваяЗапись.Сумма+?(Сторно И НоваяЗапись.Сумма>0,-СуммаПродажиПогрешность,СуммаПродажиПогрешность);
				КонецЕсли; 
				СуммаПродажиПогрешностьУпр=СуммаПродажиУпр-СуммаПродажиУпрИтого;
				Если СуммаПродажиПогрешностьУпр<>0 Тогда
					НоваяЗапись.СуммаУпр=НоваяЗапись.СуммаУпр+?(Сторно И НоваяЗапись.СуммаУпр>0,-СуммаПродажиПогрешностьУпр,СуммаПродажиПогрешностьУпр);
				КонецЕсли; 
				СуммаПродажиСкидкиПогрешность=СуммаПродажиСкидки-СуммаПродажиСкидкиИтого;
				Если СуммаПродажиСкидкиПогрешность<>0 Тогда
					НоваяЗапись.СуммаСкидки=НоваяЗапись.СуммаСкидки+?(Сторно И НоваяЗапись.СуммаСкидки>0,-СуммаПродажиСкидкиПогрешность,СуммаПродажиСкидкиПогрешность);
				КонецЕсли; 
				СуммаПродажиНДСПогрешность=СуммаПродажиНДС-СуммаПродажиНДСИтого;
				Если СуммаПродажиНДСПогрешность<>0 Тогда
					НоваяЗапись.СуммаНДС=НоваяЗапись.СуммаНДС+?(Сторно И НоваяЗапись.СуммаНДС>0,-СуммаПродажиНДСПогрешность,СуммаПродажиНДСПогрешность);
				КонецЕсли; 
			КонецЕсли; 
		Иначе
			
			Если Сторно И ЕстьДокПродажи Тогда
				СтруктураОтбора.Вставить("ДокументПродажи", Неопределено);
				СтруктураОтбора.Вставить("Партия",          ШапкаДокумента.Ссылка);
				МассивНайденныхПартий=ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
				Если МассивНайденныхПартий.Количество()>0 Тогда
					// Есть движения по партии самого возврата
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			// наверное услуга, т.к. нет движений по партиям
			СебестоимостьУпр=0;
			СуммаУпр=обПересчет(СтрокаТовар.Сумма,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр);
			
			НоваяЗапись=Добавить();
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			// всякие измерения и реквизиты
			НоваяЗапись.ПодразделениеКомпании=ПодразделениеКомпании;
			// осуществим движения по реквизиту Проект через попытку, т.к. не у всех документов
			// осуществляющих движения по данному регистру есть данный реквизит. 
			Попытка
				НоваяЗапись.Проект=ШапкаДокумента.Проект;
			Исключение
			КонецПопытки;
			НоваяЗапись.Номенклатура=СтрокаТовар.Номенклатура;
			ЭтоУслуга = НоваяЗапись.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга;
			Если ДокументПродажи<>Неопределено Тогда
				НоваяЗапись.ДокументПродажи=ДокументПродажи;
			Иначе
				Если ЕстьДокументПродажиТоваров Тогда
					НоваяЗапись.ДокументПродажи=СтрокаТовар.ДокументПродажи;
				КонецЕсли;
			КонецЕсли;
			НоваяЗапись.СтавкаНДС = СтрокаТовар.СтавкаНДС;
			НоваяЗапись.Покупатель=Покупатель;
			НоваяЗапись.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
			НоваяЗапись.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
			НоваяЗапись.ХарактеристикаНоменклатуры=СтрокаТовар.ХарактеристикаНоменклатуры;
			Если ЕстьАвтомобиль Тогда
				НоваяЗапись.Автомобиль = СтрокаТовар.Автомобиль;
			КонецЕсли;
			Если НЕ (Комиссия ИЛИ ЭтоУслуга) Тогда
				Если СкладКомпании=Неопределено Тогда
					НоваяЗапись.СкладКомпании=СтрокаТовар.СкладКомпании;
				Иначе
					НоваяЗапись.СкладКомпании=СкладКомпании;
				КонецЕсли; 
			КонецЕсли;
			НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
			// количество
			НоваяЗапись.Количество=СтрокаТовар.Количество;
			// суммы
			НоваяЗапись.СебестоимостьУпр=Окр(СебестоимостьУпр,2);
			НоваяЗапись.Сумма=Окр(?(СтрокаТовар.Сумма=NULL,0,обПересчет(СтрокаТовар.Сумма,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
			НоваяЗапись.СуммаСкидки=Окр(?(СтрокаТовар.СуммаСкидки=NULL,0,обПересчет(СтрокаТовар.СуммаСкидки,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
			НоваяЗапись.СуммаУпр=Окр(СуммаУпр,2);
			НоваяЗапись.СуммаНДС=Окр(?(СтрокаТовар.СуммаНДС=NULL,0,обПересчет(СтрокаТовар.СуммаНДС,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл)),2);
			// если возвраты, то сделаем все отрицательным
			Если Сторно Тогда
				НоваяЗапись.Количество=НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,-1,1);
				НоваяЗапись.СебестоимостьУпр=НоваяЗапись.СебестоимостьУпр*?(НоваяЗапись.СебестоимостьУпр>0,-1,1);
				НоваяЗапись.Сумма=НоваяЗапись.Сумма*?(НоваяЗапись.Сумма>0,-1,1);
				НоваяЗапись.СуммаСкидки=-НоваяЗапись.СуммаСкидки;
				НоваяЗапись.СуммаУпр=НоваяЗапись.СуммаУпр*?(НоваяЗапись.СуммаУпр>0,-1,1);
				НоваяЗапись.СуммаНДС=НоваяЗапись.СуммаНДС*?(НоваяЗапись.СуммаНДС>0,-1,1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	ШапкаДокумента=Неопределено;
	РезультатЗапросаПоТоварам=Неопределено; ТаблицаТовары=Неопределено;
	РезультатЗапросаПоПартиям=Неопределено; ТаблицаПартий=Неопределено;
	РезультатЗапросаПоТоварам=Неопределено;
	КэшСебестоимостиПриКомиссии=Неопределено;
	
	//allgk
	Попытка
		Если НижеСеб тогда сообщить("ВНИМАНИЕ!   Продажа ниже себестоимости!"); КонецЕсли;
	Исключение
	КонецПопытки;
	//allgk
	
	Возврат Результат;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

ПоБазовомуКоличеству=Ложь;
ЕстьАвтомобиль = Ложь;
Автомобиль = Неопределено;
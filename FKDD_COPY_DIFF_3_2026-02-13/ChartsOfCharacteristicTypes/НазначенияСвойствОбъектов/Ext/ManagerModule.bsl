// Заполняет предопределенные элементы
//
// Без параметров
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	// Получим макет настроек по умолчанию
	Макет = ПланыВидовХарактеристик.НазначенияСвойствОбъектов.ПолучитьМакет("НастройкиПоУмолчанию");
	Если Макет = Неопределено Тогда
		Сообщить("Не обнаружен макет начальных настроек!");
		Возврат;
	КонецЕсли;
	
	ТекНазначение = Неопределено; ТекНазначениеИмя = "";
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		СтрокаНазначение = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрокаСвойство   = СокрЛП(Макет.Область(НомерСтроки,2).Текст);
		// Если виды свойств задаются раньше назначений или это пустая строка, то продолжаем поиск назначения свойства
		Если ПустаяСтрока(СтрокаНазначение) И ПустаяСтрока(СтрокаСвойство) Тогда Продолжить; КонецЕсли;
		Если ПустаяСтрока(СтрокаНазначение) И НЕ ПустаяСтрока(СтрокаСвойство) И ТекНазначение = Неопределено Тогда Продолжить; КонецЕсли; 
		Если НЕ ПустаяСтрока(СтрокаНазначение) Тогда // Это назначение свойства
			Если ТекНазначение <> Неопределено Тогда
				Если ТекНазначениеИмя <> СтрокаНазначение Тогда //Запишем предыдущий элемент со всеми его свойствами
					ТекНазначение.Записать();
					ТекНазначение = Неопределено;
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			// Нам встретилось новое назначение свойств. Найдем ссылку на предопределенный элемент ПВХ "Назначения свойств"
			Попытка
				СсылкаНаНазначение = ПланыВидовХарактеристик.НазначенияСвойствОбъектов[СтрокаНазначение];
				ТекНазначение    = СсылкаНаНазначение.ПолучитьОбъект();
				ТекНазначениеИмя = СтрокаНазначение;  
			Исключение
				Сообщить("Ошибка при заполнении назначения свойства: '" + СтрокаНазначение + "'. Указан отсутствующий элемент ПВХ Назначения свойств объектов!");
				ТекНазначение = Неопределено	
			КонецПопытки;
		Иначе	
			Попытка
				СсылкаНаСвойство = ПланыВидовХарактеристик.СвойстваОбъектов[СтрокаСвойство];				
				Если ТекНазначение.ВидыСвойств.Найти(СсылкаНаСвойство,"Свойство")= Неопределено Тогда
					СтрокаВидыСвойств = ТекНазначение.ВидыСвойств.Добавить();
					СтрокаВидыСвойств.Свойство = СсылкаНаСвойство
				КонецЕсли;
			Исключение
				Сообщить("Ошибка при заполнении назначения свойства: '" + ТекНазначениеИмя + "'. Указан отсутствующий элемент ПВХ Свойства объектов: '"+СтрокаСвойство+"'!");
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	Попытка
		ТекНазначение.Записать()
	Исключение	
	КонецПопытки
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

//заполняет предопределённые элементы справочника
//
// Параметры
//	ЗаполнятьВсе - Булево
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе = Ложь) Экспорт
	Макет = ПланыВидовХарактеристик.ТипыСделок.ПолучитьМакет("НастройкиПоУмолчанию");
	Если Макет = Неопределено Тогда
		Сообщить("Не обнаружен макет начальных настроек!");
		Возврат;
	КонецЕсли;
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		// Считаем из макета Имя, Наименование, Код, ЗапретитьПереплатуПоСделкам, Дебетовый, Кредитовый
		СтрИмя = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрНаименование = СокрЛП(Макет.Область(НомерСтроки,2).Текст);
		СтрКод = СокрЛП(Макет.Область(НомерСтроки,3).Текст);
		СтрЗапретитьПереплатуПоСделкам = СокрЛП(Макет.Область(НомерСтроки,5).Текст);
		СтрДебетовый =	СокрЛП(Макет.Область(НомерСтроки,6).Текст);
		СтрКредитовый =	СокрЛП(Макет.Область(НомерСтроки,7).Текст);
		СсылкаТипСделки = ПланыВидовХарактеристик.ТипыСделок[СтрИмя];
		Если СсылкаТипСделки.Пустая() Тогда
			Сообщить("Ошибка при заполнении типа сделки: '" + СтрНаименование + "'. Указан отсутствующий элемент ПВХ Типы сделок!");
			Продолжить;
		Иначе
			ОбъектТипСделки = СсылкаТипСделки.ПолучитьОбъект();
		КонецЕсли;
		//заполняем реквизиты
		Если ЗаполнятьВсе Тогда
			ОбъектТипСделки.Наименование = СтрНаименование;
		ИначеЕсли обЗначениеНеЗаполнено(ОбъектТипСделки.Наименование) Тогда
			ОбъектТипСделки.Наименование = СтрНаименование;
		КонецЕсли;
		ЗапретитьПереплатуПоСделкам = ?(СтрЗапретитьПереплатуПоСделкам = "Истина",Истина,Ложь);
		Если ОбъектТипСделки.ЗапретитьПереплатуПоСделкам<>ЗапретитьПереплатуПоСделкам Тогда
			ОбъектТипСделки.ЗапретитьПереплатуПоСделкам = ЗапретитьПереплатуПоСделкам;
		КонецЕсли;
		Дебетовый = ?(СтрДебетовый = "Истина",Истина,Ложь);
		Если ОбъектТипСделки.Дебетовый <>Дебетовый Тогда
			ОбъектТипСделки.Дебетовый = Дебетовый;
		КонецЕсли;
		Кредитовый = ?(СтрКредитовый = "Истина",Истина,Ложь);
		Если ОбъектТипСделки.Кредитовый <>Кредитовый Тогда
			ОбъектТипСделки.Кредитовый = Кредитовый;
		КонецЕсли;
		//если что-то изменили, то записываем
		Если ОбъектТипСделки.Модифицированность() Тогда
			Попытка		
				ОбъектТипСделки.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

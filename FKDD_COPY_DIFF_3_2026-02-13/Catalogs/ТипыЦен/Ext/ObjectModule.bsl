// Модуль справочника ТипыЦен

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеПроцентыСкидкиНаценки=Новый Структура();
	ОбязательныеПроцентыСкидкиНаценки.Вставить("ТипНоменклатуры",3);
	
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	ОбязательныеРеквизиты.Вставить("АлгоритмПолученияЦены",1);
	
	Если ДляЭлемента Тогда
		Если НЕ ВВалютеУчета Тогда
			ОбязательныеРеквизиты.Вставить("ВалютаЦены",1);
		КонецЕсли;
		ОбязательныеРеквизиты.Вставить("ВидОплаты",1);
		Если Рассчитывается Тогда
			ОбязательныеРеквизиты.Вставить("БазовыйТипЦен",1);
		КонецЕсли;
		ОбязательныеРеквизиты.Вставить("ПроцентыСкидкиНаценки",ОбязательныеПроцентыСкидкиНаценки);
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Доступность = 1;
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Возврат Доступность;
	КонецЕсли;
	БлокироватьРеквизиты=Новый Структура();
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Цены.Цена
	|ИЗ
	|	РегистрСведений.Цены КАК Цены
	|ГДЕ
	|	Цены.ТипЦен = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Цены.Цена
	|ИЗ
	|	РегистрСведений.ЦеныАвторабот КАК Цены
	|ГДЕ
	|	Цены.ТипЦен = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Цены.Цена
	|ИЗ
	|	РегистрСведений.ЦеныАвтомобилей КАК Цены
	|ГДЕ
	|	Цены.ТипЦен = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		БлокироватьРеквизиты.Вставить("Рассчитывается","Рассчитывается");
		БлокироватьРеквизиты.Вставить("БазовыйТипЦен","БазовыйТипЦен");
		БлокироватьРеквизиты.Вставить("ВалютаЦены","ВалютаЦены");
		БлокироватьРеквизиты.Вставить("ВВалютеУчета","ВВалютеУчета");
		БлокироватьРеквизиты.Вставить("ЦенаВключаетНДС","ЦенаВключаетНДС");
		Доступность = Доступность*0;
	КонецЕсли;
	
	Возврат Доступность;
	
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя="БазовыйТипЦен" Тогда
		ТекБазовыйТипЦен=БазовыйТипЦен;
		Пока НЕ обЗначениеНеЗаполнено(ТекБазовыйТипЦен) Цикл
			Если Ссылка=ТекБазовыйТипЦен Тогда
				#Если Клиент Тогда
				Сообщить("Текущий тип цен не может быть базовым самому себе."); 
				#КонецЕсли
				Возврат Ложь;
			КонецЕсли; 
			ТекБазовыйТипЦен=ТекБазовыйТипЦен.БазовыйТипЦен;
		КонецЦикла; 
		Возврат Истина;
		
	Иначе
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли; 
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если ЭтоНовый() Тогда
		ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата;
		Закупка = Истина;
		Продажа = Истина;
		ЦенаВключаетНДС = Истина;
		ВалютаЦены = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Если (НЕ ДляАвтомобилей) И (НЕ ДляРабот) И (НЕ ДляТоваров) Тогда
			ДляАвтомобилей = Истина;
			ДляРабот       = Истина;
			ДляТоваров     = Истина;
		КонецЕсли;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(АлгоритмПолученияЦены) Тогда
		АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре;
	КонецЕсли; 
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ Рассчитывается Тогда
		БазовыйТипЦен=Справочники.ТипыЦен.ПустаяСсылка();
		ПроцентСкидкиНаценки=0;
		ПроцентыСкидкиНаценки.Очистить();
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Если НЕ ОбработкаРеквизита("БазовыйТипЦен") Тогда
			Отказ=Истина;
			Возврат;
		КонецЕсли;
		Если (НЕ ДляАвтомобилей) И (НЕ ДляРабот) И (НЕ ДляТоваров) Тогда
			ДляАвтомобилей = Истина;
			ДляРабот       = Истина;
			ДляТоваров     = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
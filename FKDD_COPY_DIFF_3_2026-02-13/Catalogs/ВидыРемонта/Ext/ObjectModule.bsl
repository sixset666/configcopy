// Модуль справочника ВидыРемонта

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	
	ОбязательныеГарантия=Новый Структура();
	ОбязательныеГарантия.Вставить("Модель",3);
	ОбязательныеГарантия.Вставить("Контрагент",1);
	ОбязательныеГарантия.Вставить("ДоговорВзаиморасчетов",1);
	
	ОбязательныеРаботы=Новый Структура();
	ОбязательныеРаботы.Вставить("Авторабота",3);
	ОбязательныеРаботы.Вставить("Количество",1);
	
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	ОбязательныеРеквизиты.Вставить("ТипРемонта",1);
	ОбязательныеРеквизиты.Вставить("Гарантия",ОбязательныеГарантия);
	ОбязательныеРеквизиты.Вставить("Работы",ОбязательныеРаботы);
	
	Если ОтправлятьУведомления Тогда
		ОбязательныеНастройкиУведомлений=Новый Структура();
		ОбязательныеНастройкиУведомлений.Вставить("СостояниеЗаказНаряда",3);
		ОбязательныеНастройкиУведомлений.Вставить("ШаблонСообщения",1);
		ОбязательныеРеквизиты.Вставить("НастройкаУведомлений",ОбязательныеНастройкиУведомлений);
		Если ЗначениеЗаполнено(ШаблонПриЗаписиНаРемонт) Тогда
			ОбязательныеРеквизиты.Вставить("ОтправлятьЗаВремя",1);	
		КонецЕсли;
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если НЕ ЭтоГруппа Тогда
		Для Каждого ГарантийныйПлательщик Из Гарантия Цикл
			Если НЕ обЗначениеНеЗаполнено(ГарантийныйПлательщик.ДоговорВзаиморасчетов) Тогда
				Если (ГарантийныйПлательщик.ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Продажа) И
					 (ГарантийныйПлательщик.ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее) Тогда
					 Сообщить("Гарантия: У контрагента <"+ГарантийныйПлательщик.Контрагент+"> выбран некорректный вид договора.");
					 Отказ=Истина;
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Возврат Истина;
КонецФункции

// Обработка изменения реквизитов справочников
// Параметры
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя="Гарантия.Контрагент" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
			Если обЗначениеНеЗаполнено(ТекСтрока.Контрагент) Тогда 
				// если контрагент не указан то очистим договор
				ТекСтрока.ДоговорВзаиморасчетов=Неопределено;
			Иначе
				Если ТекСтрока.ДоговорВзаиморасчетов.Владелец<>ТекСтрока.Контрагент Тогда
					ТекСтрока.ДоговорВзаиморасчетов=Неопределено;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		Результат=Истина;
		
	ИначеЕсли Имя="Гарантия.ДоговорВзаиморасчетов" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
			Если (ТекСтрока.ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Продажа) И
				 (ТекСтрока.ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее) Тогда
				 Сообщить("Гарантия: У контрагента <"+ТекСтрока.Контрагент+"> выбран некорректный вид договора.");
				 ТекСтрока.ДоговорВзаиморасчетов=Неопределено;
			КонецЕсли;
		КонецЕсли;
		Результат=Истина;
		
	Иначе
		Результат=спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	КонецЕсли; 
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если ТипРемонта=Перечисления.ТипыРемонта.Платный ИЛИ
			 Ссылка=Справочники.ВидыРемонта.КомплектацияАвтомобиля Тогда
			 Если НаСебестоимость Тогда
				НаСебестоимость=Ложь;
			 КонецЕсли;
		КонецЕсли;
		Если ТипРемонта=Перечисления.ТипыРемонта.Бесплатный Тогда
			Гарантия.Очистить();
		КонецЕсли;
		Если ТипРемонта=Перечисления.ТипыРемонта.Бесплатный И (НЕ НаСебестоимость)Тогда
			СтатьяДоходаДетали=Неопределено;
			СтатьяДоходаРаботы=Неопределено;
		КонецЕсли;
		Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон") Тогда
			НаСебестоимость=Ложь;
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
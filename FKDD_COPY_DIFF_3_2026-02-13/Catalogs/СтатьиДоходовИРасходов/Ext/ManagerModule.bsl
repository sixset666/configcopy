//Процедура заполнения предопределённых элементов справочника
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	Макет = ПолучитьМакет("НастройкиПоУмолчанию");
	Если Макет = Неопределено Тогда
		Сообщить("Не обнаружен макет начальных настроек!");
		Возврат;
	КонецЕсли;
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		
		// Считаем из макета Имя, Код, Наименование, ВидДоходаИРасхода, КоррСчет, ВидСтатьи, СтатьяРаспределения
		СтрИмя = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрКод = СокрЛП(Макет.Область(НомерСтроки,2).Текст);
		СтрНаименование = СокрЛП(Макет.Область(НомерСтроки,3).Текст);
		СтрВидДоходаИРасхода = СокрЛП(Макет.Область(НомерСтроки,4).Текст);
		СтрКоррСчет = СокрЛП(Макет.Область(НомерСтроки,5).Текст);
		СтрВидСтатьи = СокрЛП(Макет.Область(НомерСтроки,6).Текст);
		СтрСтатьяРаспределения = СокрЛП(Макет.Область(НомерСтроки,7).Текст);
		
		//будем искать с таким же наименованием
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СтатьиДоходовИРасходов.Ссылка КАК СтатьяДоходаРасхода
		|ИЗ
		|	Справочник.СтатьиДоходовИРасходов КАК СтатьиДоходовИРасходов
		|ГДЕ
		|	СтатьиДоходовИРасходов.Наименование = &Наименование И СтатьиДоходовИРасходов.ЭтоГруппа = Ложь И СтатьиДоходовИРасходов.Предопределенный = Ложь
		|");
		Запрос.УстановитьПараметр("Наименование",СтрНаименование);
		Выборка = Запрос.Выполнить().Выбрать();
		
		//берем предопределенный элемент
		ПредопределеннаяСтатья = Справочники.СтатьиДоходовИРасходов[СтрИмя];
		Если ПредопределеннаяСтатья.Пустая() Тогда
			Сообщить("Ошибка при заполнении справочника: '" + СтрНаименование + "'. Указан отсутствующий элемент справочника Статьи доходов и расходов!");
			Продолжить;
		КонецЕсли;
		ПредопределеннаяСтатьяОбъект = ПредопределеннаяСтатья.ПолучитьОбъект();
		Номер = 1;
		
		//если с таким именем есть, будем заменять
		Пока Выборка.Следующий() Цикл
			//используем стандартную обработку
			ФормаЗаменаЗначений = Обработки.ПоискИЗаменаЗначений.ПолучитьФорму();
			ФормаЗаменаЗначений.Программно = Истина;
			СтрокаЗамены = ФормаЗаменаЗначений.ЗаменяемыеЗначения.Добавить();
			СтрокаЗамены.ЧтоЗаменять = Выборка.СтатьяДоходаРасхода;
			СтрокаЗамены.НаЧтоЗаменять = ПредопределеннаяСтатья;
			СтрокаЗамены.Пометка = Истина;
			Сообщить("Справочник ""Статьи доходов и расходов"". Произведена замена ссылок на элемент """ + Выборка.СтатьяДоходаРасхода + """ ссылками на элемент """ + ПредопределеннаяСтатья + """");
			ФормаЗаменаЗначений.Открыть();
			//
			ОбъектСтатьяДоходаРасхода = Выборка.СтатьяДоходаРасхода.ПолучитьОбъект();
			ОбъектСтатьяДоходаРасхода.Наименование = СтрНаименование+"_" + Номер;
			ОбъектСтатьяДоходаРасхода.Записать();
			Номер = Номер + 1;
		КонецЦикла;
		
		//перезаполняем реквизиты
		Если ЗаполнятьВсе Тогда
			ПредопределеннаяСтатьяОбъект.Наименование = СтрНаименование;
		ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.Наименование) Тогда
			ПредопределеннаяСтатьяОбъект.Наименование = СтрНаименование;
		КонецЕсли;
		
		// Заполняем Вид дохода и расхода
		Если НЕ обЗначениеНеЗаполнено(СтрВидДоходаИРасхода) Тогда
			СсылкаВидДоходаРасхода = Перечисления.ВидыДоходовИРасходов[СтрВидДоходаИРасхода];
			Если СсылкаВидДоходаРасхода.Пустая() Тогда
				Сообщить("Ошибка при заполнении статьи доходов и расходов: '" + СтрНаименование + "' Виды доходов и расходов: '"+СтрВидДоходаИРасхода+"' не найден");
			Иначе
				Если ЗаполнятьВсе Тогда
					ПредопределеннаяСтатьяОбъект.ВидДоходаИРасхода = СсылкаВидДоходаРасхода;
				ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.ВидДоходаИРасхода) Тогда
					ПредопределеннаяСтатьяОбъект.ВидДоходаИРасхода = СсылкаВидДоходаРасхода;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Заполняем Корр. cчет
		Если НЕ обЗначениеНеЗаполнено(СтрКоррСчет) Тогда
			СсылкаКоррСчет = ПланыСчетов.Регламентированный.НайтиПоКоду(СтрКоррСчет);
			Если СсылкаКоррСчет.Пустая() Тогда
				Сообщить("Ошибка при заполнении статьи доходов и расходов: '" + СтрНаименование + "' Корреспондирующий счет: '"+СтрКоррСчет+"' не найден");
			Иначе
				Если ЗаполнятьВсе Тогда
					ПредопределеннаяСтатьяОбъект.КоррСчет = СсылкаКоррСчет;
				ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.КоррСчет) Тогда
					ПредопределеннаяСтатьяОбъект.КоррСчет = СсылкаКоррСчет;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Заполняем Статья распределения
		Если НЕ обЗначениеНеЗаполнено(СтрВидСтатьи) Тогда
			СсылкаВидСтатьиДоходаРасхода = Перечисления.ВидыСтатейДоходовИРасходов[СтрВидСтатьи];
			Если СсылкаВидСтатьиДоходаРасхода .Пустая() Тогда
				Сообщить("Ошибка при заполнении статьи доходов и расходов: '" + СтрНаименование + "' Вид статьи дохода расхода: '"+СтрВидСтатьи+"' не найдена");
			Иначе
				Если ЗаполнятьВсе Тогда
					ПредопределеннаяСтатьяОбъект.ВидСтатьи = СсылкаВидСтатьиДоходаРасхода ;
				ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.ВидСтатьи) Тогда
					ПредопределеннаяСтатьяОбъект.ВидСтатьи = СсылкаВидСтатьиДоходаРасхода ;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Заполняем табличную часть Статьи распределения
		Если НЕ обЗначениеНеЗаполнено(СтрСтатьяРаспределения) Тогда
			СсылкаСтатьяРаспределения = Справочники.СтатьиДоходовИРасходов[СтрСтатьяРаспределения];
			Если СсылкаСтатьяРаспределения.Пустая() Тогда
				Сообщить("Ошибка при заполнении статьи доходов и расходов: '" + СтрНаименование + "'Статья распределения: '"+СтрСтатьяРаспределения+"' не найдена");
			Иначе
				Если ПредопределеннаяСтатьяОбъект.СтатьиРаспределения.Найти(СсылкаСтатьяРаспределения, "СтатьяРаспределения") = Неопределено Тогда
					СтрокаСтатьяРаспределения = ПредопределеннаяСтатьяОбъект.СтатьиРаспределения.Добавить();
					СтрокаСтатьяРаспределения.СтатьяРаспределения = СсылкаСтатьяРаспределения;
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
		
		//Сохранение справочника
		НовыйЭлемент = ПредопределеннаяСтатьяОбъект.ЭтоНовый();
		Если ПредопределеннаяСтатьяОбъект.Модифицированность() Тогда
			Попытка
				ПредопределеннаяСтатьяОбъект.Записать();
				Сообщить("Справочник ""Статьи доходов и расходов"". "+?(НовыйЭлемент,"Добавлен","Обновлен")+" элемент """+СокрЛП(ПредопределеннаяСтатьяОбъект.Наименование)+"""");
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

// Процедура загружает элементы справочника из макета обработки "Метрополитен"
Процедура ЗагрузитьРайоныИзМакета() Экспорт
	Отказ = Ложь;
	спПроверкаПраваДоступа(ЭтотОбъект, Отказ);
	Если Отказ Тогда
		Возврат
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Районы.Ссылка КАК Ссылка,
	               |	Районы.Родитель КАК Родитель,
	               |	Районы.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.Районы КАК Районы
	               |ГДЕ
	               |	Районы.ПометкаУдаления = ЛОЖЬ";

	ТаблицаРайоновВСправочнике = Запрос.Выполнить().Выгрузить();
	
	СписокМакетов = Новый СписокЗначений;
	
	Для Каждого МакетРайонов Из Метаданные.Справочники.Районы.Макеты Цикл
		СписокМакетов.Добавить(МакетРайонов.Имя, МакетРайонов.Синоним);
	КонецЦикла;
	
	ЗагружаемыйМакет = СписокМакетов.ВыбратьЭлемент("Выберите набор районов для загрузки");
	
	Если ЗагружаемыйМакет = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	#Если Клиент Тогда
	Состояние("Загружаются городские районы...");
	#КонецЕсли		
	Макет = Справочники.Районы.ПолучитьМакет(ЗагружаемыйМакет.Значение);
	Попытка
		ОбластьТаблицаРайонов = Макет.ПолучитьОбласть("ТаблицаРайонов");
		
		#Если Клиент Тогда
//		Отображаем форму индикации
		ФормаИндикатора = ПолучитьОбщуюФорму("Индикация", Справочники.Районы.ПолучитьФормуСписка());
		ФормаИндикатора.СтрокаСостояния = "Загрузка справочника городские районы...";
		Сч = (ОбластьТаблицаРайонов.ВысотаТаблицы - 3) * 2;
		ФормаИндикатора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = Сч;
		ФормаИндикатора.ЭлементыФормы.Индикатор.Шаг = Сч / 100;
		ФормаИндикатора.Открыть();
		Сч = 0;
		#КонецЕсли		
					
	//   получим список всех станций
		ТаблицаРайонов = Новый ТаблицаЗначений;
		ТаблицаРайонов.Колонки.Добавить("НазваниеРайона");
				
		Для НомерСтрокиОбласти = 3 По ОбластьТаблицаРайонов.ВысотаТаблицы Цикл
			НазваниеРайона = ОбластьТаблицаРайонов.Область(НомерСтрокиОбласти, 1).Текст;
			
			#Если Клиент Тогда
			Сч = Сч + 1;
			ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
			#КонецЕсли		
		
	//		 проверим заполнение текущей строки макета
			Если ПустаяСтрока(НазваниеРайона) Тогда
				Прервать;
			КонецЕсли;
			СтрокаТаблицыРайонов = ТаблицаРайонов.Добавить();
			СтрокаТаблицыРайонов.НазваниеРайона = НазваниеРайона;
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыРайонов Из ТаблицаРайонов Цикл
			ТекущийРодитель = Справочники.Районы.ПустаяСсылка();
			СтрокаТаблицыРайоновВСправочнике = ТаблицаРайоновВСправочнике.Найти(СокрЛП(СтрокаТаблицыРайонов.НазваниеРайона), "Наименование");
			
			#Если Клиент Тогда
			Сч = Сч + 1;
			ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
			#КонецЕсли		
		
			Если СтрокаТаблицыРайоновВСправочнике = Неопределено Тогда
				ТекущийРайон = Справочники.Районы.СоздатьЭлемент();
				ТекущийРайон.УстановитьНовыйКод("");
				ТекущийРайон.Родитель     = ТекущийРодитель;
				ТекущийРайон.Наименование = СокрЛП(СтрокаТаблицыРайонов.НазваниеРайона);
				ТекущийРайон.Записать();
			Иначе
				Если Не СтрокаТаблицыРайоновВСправочнике.Родитель = ТекущийРодитель Тогда
					ТекущийРайон = СтрокаТаблицыРайоновВСправочнике.Ссылка.ПолучитьОбъект();
					ТекущийРайон.Родитель = ТекущийРодитель;
					ТекущийРайон.Записать();
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;	
	Исключение
		#Если Клиент Тогда
		Предупреждение("Не заполнен макет!");
		#КонецЕсли		
	КонецПопытки;	
	#Если Клиент Тогда
	Состояние("");
	Если ФормаИндикатора.Открыта() Тогда ФормаИндикатора.Закрыть(); КонецЕсли;
	#КонецЕсли		
КонецПроцедуры// ЗагрузитьРайоныИзМакета() Экспорт	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль справочника Автомобили

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем КопированиеХозяин Экспорт; // Признак копирования
Перем РазрешениеИзмененияНаименования Экспорт; // Разрешение изменения наименования
Перем РазрешениеИзмененияПолногоНаименования Экспорт; // Разрешение изменения полного наименования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА


// Проверка корректности VIN
Функция ПроверитьКорректностьVIN(Отказ,Знач ВремVIN)
	
	РазрешенныеСимволыVIN="0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ- ";
	
	ЗапрещенныеСимволы = СокрЛП(обПраво("КонтрольВводаЗапрещенныхСимволовVIN",Права));
	Результат = "";
	
	Если НЕ ЗапрещенныеСимволы = "" Тогда
		// удалим запрещенные символы
		Пока СтрДлина(ЗапрещенныеСимволы)>0 Цикл
			ТекСимвол = Лев(ЗапрещенныеСимволы, 1);
			ЗапрещенныйСимволVINВРег = ВРег(ТекСимвол);
			ЗапрещенныйСимволVINНРег = НРег(ТекСимвол);
			
			Если Найти(РазрешенныеСимволыVIN, ЗапрещенныйСимволVINВРег)<>0 Тогда
				РазрешенныеСимволыVIN = СтрЗаменить(РазрешенныеСимволыVIN, ЗапрещенныйСимволVINВРег, "");
			КонецЕсли;
			
			Если Найти(РазрешенныеСимволыVIN, ЗапрещенныйСимволVINНРег)<>0 Тогда
				РазрешенныеСимволыVIN = СтрЗаменить(РазрешенныеСимволыVIN, ЗапрещенныйСимволVINНРег, "");
			КонецЕсли;
			
			ЗапрещенныеСимволы = Сред(ЗапрещенныеСимволы, 2);
		КонецЦикла;
	КонецЕсли;
	
	Пока СтрДлина(ВремVIN)>0 Цикл
		СимволVIN = Лев(ВремVIN, 1);
		Если Найти(РазрешенныеСимволыVIN, СимволVIN)=0 Тогда
			Если Найти(Результат, СимволVIN) = 0 Тогда
				Результат = Результат + ?(Результат = "", "", ", ") + СимволVIN;
			КонецЕсли;
			Отказ = Истина;
		КонецЕсли;
		ВремVIN=Сред(ВремVIN,2);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректностьVIN()

// Процедура формирования отчета "ИсторияАвтомобиля"
Процедура ОтчетИсторияАвтомобиля(ДатаНачала=Неопределено,ДатаКонца=Неопределено) Экспорт
	Если Ссылка.Пустая() ИЛИ Ссылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли; 
	
	ИсторияАвтомобилей=Отчеты.ИсторияАвтомобилей.Создать();
	ИсторияАвтомобилей.РежимВыводаОтчета=Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
	ИсторияАвтомобилей.РежимНастройки=Перечисления.РежимыНастройкиОтчетов.Эксперт;
	ИсторияАвтомобилей.ИмяФормыНастроек = "НастройкиОбороты";
	ИсторияАвтомобилей.ЗаполнитьНачальныеНастройки("ТекстЗапросаОбороты");
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КлючОбъекта",     "Отчет.ИсторияАвтомобилей.Форма");
	СтруктураОтбора.Вставить("КлючНастроек",    "СохраненныеНастройки");
	СтруктураОтбора.Вставить("ИмяПользователя", ИмяПользователя());
	
	Выборка = ХранилищеНастроекДанныхФорм.Выбрать(СтруктураОтбора);
	Если Выборка.Следующий() Тогда
		
		СохраненныеНастройки = Выборка.Настройки;
		Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
			
			Попытка ИсторияАвтомобилей.Измерения.Загрузить(СохраненныеНастройки.Измерения); Исключение КонецПопытки;
			Попытка ИсторияАвтомобилей.ДополнительныеПоля.Загрузить(СохраненныеНастройки.ДополнительныеПоля); Исключение КонецПопытки;
			Попытка ИсторияАвтомобилей.ИтоговыеДополнительныеПоля.Загрузить(СохраненныеНастройки.ИтоговыеДополнительныеПоля); Исключение КонецПопытки;
			Попытка ИсторияАвтомобилей.ДоступныеПоля.Загрузить(СохраненныеНастройки.ДоступныеПоля); Исключение КонецПопытки;
			Попытка ИсторияАвтомобилей.ДеревоНастройкиДополнительныхПолей=СохраненныеНастройки.ДеревоНастройкиДополнительныхПолей; Исключение КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИсторияАвтомобилей.ДатаНачала=?(ДатаНачала=Неопределено,Неопределено,НачалоДня(ДатаНачала));
	ИсторияАвтомобилей.ДатаКонца=КонецДня(?(ДатаКонца=Неопределено,ТекущаяДата(),ДатаКонца));
	
	Элемент = ИсторияАвтомобилей.ФильтрыОтчета.Найти("Автомобиль");
	
	ЗаголовокОтчета = "История автомобилей";
	Если Элемент<>Неопределено Тогда
		Элемент.Использование=Истина;
		Элемент.Значение=Ссылка;
		Если ЭтоГруппа Тогда
			Элемент.ВидСравнения=ВидСравнения.ВИерархии;
			ЗаголовокОтчета="История автомобиля";
		Иначе
			Элемент.ВидСравнения=ВидСравнения.Равно;
		КонецЕсли; 
	КонецЕсли; 
	
	ФормаОтчета = ПолучитьОбщуюФорму("Отчет");
	ФормаОтчета.ОтчетОбъект = ИсторияАвтомобилей;
	ФормаОтчета.Заголовок   = ЗаголовокОтчета;
	ФормаОтчета.Открыть();
	
КонецПроцедуры

// Процедура формирования отчета "ОборудованиеАвтомобиля"
Процедура ОтчетОборудованиеАвтомобиля(ДатаКонца=Неопределено) Экспорт
	
	Если Ссылка.Пустая() ИЛИ Ссылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли; 
	
	#Если Клиент Тогда
	
	Отчет = Отчеты.ОтчетОборудованияАвтомобилей.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.Основной;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(?(ДатаКонца=Неопределено,ТекущаяДата(),ДатаКонца)));
	
	Если ЭтоГруппа Тогда
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Автомобиль", Ссылка, ВидСравненияКомпоновкиДанных.ВИерархии);
	Иначе
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Автомобиль", Ссылка);
	КонецЕсли; 
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
	#КонецЕсли
	
КонецПроцедуры 

// Процедура формирования отчета "РекомендацииПоАвтомобилям"
//
Процедура ОтчетРекомендацииПоАвтомобилям(ДатаНачала = Неопределено, ДатаКонца=Неопределено) Экспорт
	
	Если Ссылка.Пустая() ИЛИ Ссылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли; 
	
	#Если Клиент Тогда
	
	Отчет = Отчеты.РекомендацииПоАвтомобилям.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.ИсторияРекомендаций;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(?(ДатаКонца=Неопределено,КонецМесяца(ТекущаяДата()),ДатаКонца)));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",  НачалоДня(?(ДатаНачала=Неопределено,НачалоМесяца(ТекущаяДата()),ДатаНачала)));
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ТипЦен", обПраво("ОсновнойТипЦенПродажиАвтомобилей"));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ТипЦенРабот",обПраво("ОсновнойТипЦенРабот"));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ВалютаОтчета", Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
	
	СтруктураОтчета = НастройкиВарианта.Структура;
	СтруктураОтчета.Очистить();
	
	ТаблицаОтчета = СтруктураОтчета.Добавить(Тип("ТаблицаКомпоновкиДанных"));
	
	//добавляем измерения строки
	ГруппировкаТиповРекомендации = ОтчетыСервер.СКД_ДобавитьГруппировку(ТаблицаОтчета.Строки, "ТипРекомендации");
	ГруппировкаСтатус    = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаТиповРекомендации.Структура, "Статус");
	//ГруппировкаАвтомобиль     = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаСтатус.Структура, "Автомобиль");
	ГруппировкаРекомендация     = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаСтатус.Структура, "Рекомендация");

	ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Автомобиль", Ссылка, ВидСравненияКомпоновкиДанных.Равно);


	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
	#КонецЕсли
	
КонецПроцедуры


// Процедура создания заказа на автомобиль
Процедура СоздатьЗаказНаАвтомобиль() Экспорт
	Если ЭтоГруппа Тогда Возврат; КонецЕсли; 
	
	НовыйЗаказ = Документы.ЗаказНаАвтомобиль.СоздатьДокумент();
	НовыйЗаказ.Заполнить(Ссылка);
	НовыйЗаказ.ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобиль;
	НовыйЗаказ.ОбработкаРеквизита("ХозОперация");
	НовыйЗаказ.Автомобиль = Ссылка;
	НовыйЗаказ.ОбработкаРеквизита("Автомобиль");
	НовыйЗаказ.ОбработкаРеквизита("ПроцентПредоплаты");
	ФормаЗаказа = НовыйЗаказ.ПолучитьФорму();
	ФормаЗаказа.Открыть();
КонецПроцедуры

//Формирование наименования автомобиля на основе модели, цвета и VIN-кода
Процедура ФормированиеНаименованияАвтомобиля(ГосНомер,ИмяРеквизитаНаименования=Неопределено) Экспорт
	НовоеНаименование="";
	НовоеНаименованиеПолное="";
	Если РазрешениеИзмененияНаименования = Неопределено Тогда
		РазрешениеИзмененияНаименования = Ложь;
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Модель) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+СокрЛП(Модель);
		Если НЕ ПустаяСтрока(НовоеНаименованиеПолное) Тогда
			НовоеНаименованиеПолное=НовоеНаименованиеПолное+" ";
		КонецЕсли;
		Если ПустаяСтрока(СокрЛП(Модель.НаименованиеПолное)) Тогда
			НовоеНаименованиеПолное=НовоеНаименованиеПолное+СокрЛП(Модель.Наименование);
		Иначе
			НовоеНаименованиеПолное=НовоеНаименованиеПолное+СокрЛП(Модель.НаименованиеПолное);
		КонецЕсли; 
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Цвет) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+СокрЛП(Цвет);
		Если НЕ ПустаяСтрока(НовоеНаименованиеПолное) Тогда
			НовоеНаименованиеПолное=НовоеНаименованиеПолное+" ";
		КонецЕсли; 
		НовоеНаименованиеПолное=НовоеНаименованиеПолное+СокрЛП(Цвет);
	КонецЕсли; 
	Если НЕ обЗначениеНеЗаполнено(ГосНомер) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+"№ "+СокрЛП(ГосНомер);
		Если НЕ ПустаяСтрока(НовоеНаименованиеПолное) Тогда
			НовоеНаименованиеПолное=НовоеНаименованиеПолное+" ";
		КонецЕсли; 
		НовоеНаименованиеПолное=НовоеНаименованиеПолное+"№ "+СокрЛП(ГосНомер);
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(VIN) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+"VIN "+СокрЛП(VIN);
		Если НЕ ПустаяСтрока(НовоеНаименованиеПолное) Тогда
			НовоеНаименованиеПолное=НовоеНаименованиеПолное+" ";
		КонецЕсли; 
		НовоеНаименованиеПолное=НовоеНаименованиеПолное+"VIN "+СокрЛП(VIN);
	КонецЕсли;
	Если Наименование <> НовоеНаименование Тогда
		РазрешениеИзмененияНаименования = Истина;
	КонецЕсли;
	Если НаименованиеПолное <> НовоеНаименованиеПолное И Наименование = НаименованиеПолное Тогда
		РазрешениеИзмененияПолногоНаименования = Истина;
	КонецЕсли;
	Если НЕ РазрешениеИзмененияНаименования И РазрешениеИзмененияПолногоНаименования Тогда
		НаименованиеПолное = Наименование;
	КонецЕсли;
	Если (ИмяРеквизитаНаименования=Неопределено ИЛИ ИмяРеквизитаНаименования="Наименование") И РазрешениеИзмененияНаименования Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			Наименование=НовоеНаименование;
		КонецЕсли; 
	КонецЕсли; 
	Если (ИмяРеквизитаНаименования=Неопределено ИЛИ ИмяРеквизитаНаименования="НаименованиеПолное") И РазрешениеИзмененияПолногоНаименования
		И РазрешениеИзмененияНаименования Тогда
		Если НЕ ПустаяСтрока(НовоеНаименованиеПолное) Тогда
			НаименованиеПолное=НовоеНаименованиеПолное;
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("Наименование",1);
		ОбязательныеРеквизиты.Вставить("Модель",1);
		Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон") Тогда
			ОбязательныеРеквизиты.Вставить("ВалютаУчета",1);
			ОбязательныеРеквизиты.Вставить("ВключатьВПрайсЛист",1);
		ИначеЕсли НЕ обПраво("РазрешитьПустойVIN", Права) Тогда
			ОбязательныеРеквизиты.Вставить("VIN", 1);
		КонецЕсли;
		Если Прослеживаемый Тогда
			ОбязательныеРеквизиты.Вставить("КодТНВЭД",1);
		КонецЕсли;
	КонецЕсли;
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// проверка на заполненость и уникальность реквизитов VIN
	// заполним флаги
	Результат        = Истина;
	ПроверятьVIN     = Ложь;
	ПроверятьОригVIN = Ложь;
	Если НЕ ЭтоГруппа Тогда
		ПроверятьVIN     = НЕ обЗначениеНеЗаполнено(VIN);
		ПроверятьОригVIN = НЕ обЗначениеНеЗаполнено(ОригинальныйVIN);
	КонецЕсли;
	
	// проверка реквизита VIN
	Если ПроверятьVIN Тогда
		Если обЗначениеНеЗаполнено(VIN) Тогда
			Результат = Ложь;
			дкДобавитьОшибку(Ошибки,"Реквизит ""VIN"" не заполнен.");
		Иначе
			
			//Проверим корректность VIN автомобиля
			Отказ = Ложь;
			ЗапрещенныеСимволы = ПроверитьКорректностьVIN(Отказ,VIN);
			Если Отказ Тогда
				Результат = Ложь;
				дкДобавитьОшибку(Ошибки, "Внимание! VIN автомобиля содержит недопустимые символы:
				|	" + ЗапрещенныеСимволы);
			КонецЕсли;
			
			Если Результат Тогда
				ТекстЗапроса =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
				|	Автомобили.Ссылка
				|ИЗ
				|	Справочник.Автомобили КАК Автомобили
				|ГДЕ
				|	(Автомобили.VIN = &VIN
				|			ИЛИ Автомобили.ОригинальныйVIN = &VIN)
				|	И Автомобили.Ссылка <> &Авто";
				
				Запрос = Новый Запрос(ТекстЗапроса);
				Запрос.УстановитьПараметр("Авто" , Ссылка);
				Запрос.УстановитьПараметр("VIN"  , СокрЛП(VIN));
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					Результат = Ложь;
					дкДобавитьОшибку(Ошибки,"Реквизит ""VIN"" не уникален.");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// проверка реквизита оригинальный VIN
	Если ПроверятьОригVIN Тогда
		
		Отказ = Ложь;
		ЗапрещенныеСимволыОриг = ПроверитьКорректностьVIN(Отказ, ОригинальныйVIN);
		Если Отказ И НЕ ПустаяСтрока(ЗапрещенныеСимволыОриг)Тогда
			Результат = Ложь;
			Сообщить("Внимание! Оригинальный VIN автомобиля содержит недопустимые символы:
			|	" + ЗапрещенныеСимволыОриг, СтатусСообщения.Важное);
		КонецЕсли;
		
		Если Результат Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Автомобили.Ссылка
			|ИЗ
			|	Справочник.Автомобили КАК Автомобили
			|ГДЕ
			|	(Автомобили.VIN = &VIN
			|			ИЛИ Автомобили.ОригинальныйVIN = &VIN)
			|	И Автомобили.Ссылка <> &Авто";
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Авто" , Ссылка);
			Запрос.УстановитьПараметр("VIN"  , СокрЛП(ОригинальныйVIN));
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				Результат = Ложь;
				дкДобавитьОшибку(Ошибки,"Реквизит ""оригинальный VIN"" не уникален.");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Возврат Истина;
КонецФункции

// Обработка изменения реквизитов справочников
// Параметры
//  Имя			- Строка			- Имя реквизита справочника с полным путем.
//  ЭтаФорма	- Форма				- Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части справочника, реквизит которой
//										 обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя = "Модель" Тогда
		Если Модель <> ВариантКомплектации.Владелец Тогда
			ВариантКомплектации = Справочники.ВариантыКомплектации.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

//Получает опции и оборудование автомобиля
Функция ПолучитьОпцииИОборудованиеАвтомобиля(тзТаблица) Экспорт
	Если ЭтоНовый() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса=
	"ВЫБРАТЬ
	|	КомплектацияАвтомобилейОстатки.Номенклатура КАК Номенклатура,
	|	КомплектацияАвтомобилейОстатки.ХарактеристикаНоменклатуры,
	|	СУММА(КомплектацияАвтомобилейОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаПродажиУпрОстаток) КАК СуммаПродажиУпрОстаток,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаУпрОстаток) КАК СуммаУпрОстаток
	|ИЗ
	|	РегистрНакопления.КомплектацияАвтомобилей.Остатки(, Автомобиль = &Автомобиль) КАК КомплектацияАвтомобилейОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	КомплектацияАвтомобилейОстатки.Номенклатура,
	|	КомплектацияАвтомобилейОстатки.ХарактеристикаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Автомобиль",Ссылка);
	тзТаблица=Запрос.Выполнить().Выгрузить();
	
	Возврат НЕ (тзТаблица.Количество() = 0);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") ИЛИ 
		ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") ИЛИ 
		ТипЗнч(Основание)=Тип("ДокументОбъект.ЗаказНаАвтомобиль") ИЛИ 
		ТипЗнч(Основание)=Тип("ДокументОбъект.ЗаказПоставщикуНаАвтомобиль") Тогда
		
		Модель              = Основание.Модель;
		ВариантКомплектации = Основание.ВариантКомплектации;
		Цвет                = Основание.Цвет;
		ЦветКод             = Основание.ЦветКод;
		Если ЗначениеЗаполнено(Основание.ВариантКомплектации) Тогда
			ТипКузова       = Основание.ВариантКомплектации.ТипКузова;
			ТипДвигателя    = Основание.ВариантКомплектации.ТипДвигателя;
			ТипКПП          = Основание.ВариантКомплектации.ТипКПП;
		КонецЕсли;
		ТипСалона           = Основание.ТипСалона;
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") ИЛИ 
			ТипЗнч(Основание)=Тип("ДокументОбъект.ЗаказПоставщикуНаАвтомобиль") Тогда
			Поставщик       = Основание.Контрагент;
		КонецЕсли;
		ФормированиеНаименованияАвтомобиля("");
	КонецЕсли;
	
	Если (НЕ ЭтоГруппа) И (обЗначениеНеЗаполнено(ВалютаУчета)) Тогда
		ВалютаУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И (обЗначениеНеЗаполнено(ВключатьВПрайсЛист)) Тогда
		ВключатьВПрайсЛист = Родитель.ВключатьВПрайсЛист;
		Если обЗначениеНеЗаполнено(ВключатьВПрайсЛист) Тогда
			ВключатьВПрайсЛист = Перечисления.ВидВключенияВПрайсЛист.ПоУмолчанию;
		КонецЕсли; 
	КонецЕсли;
	
	РазрешениеИзмененияНаименования = Истина;
	Автор = Неопределено;
	ДатаРегистрации = Неопределено;
КонецПроцедуры

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	АвтомобилиСрезПоследних.Значение КАК Хозяин
	|ИЗ
	|	РегистрСведений.Автомобили.СрезПоследних(,Автомобиль = &Автомобиль И ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК АвтомобилиСрезПоследних
	|";
	Запрос.УстановитьПараметр("Автомобиль",ОбъектКопирования.Ссылка);
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КопированиеХозяин=Выборка.Хозяин;
	Иначе
		КопированиеХозяин=Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	Автор = Неопределено;
	ДатаРегистрации = Неопределено;
КонецПроцедуры

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если НЕ обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
			ТипКузова=Справочники.ТипыКузовов.ПустаяСсылка();
			ТипДвигателя=Справочники.ТипыДвигателей.ПустаяСсылка();
			ТипКПП=Справочники.ТипыКПП.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли; 
	
	// Заполним автора и дату регистрации, если объект новый
	Если ЭтоНовый() Тогда
		Автор = ПараметрыСеанса.Пользователь;
		ДатаРегистрации = ТекущаяДата();
	КонецЕсли;    
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Не Отказ Тогда
		атСервер.АвтомобильПриЗаписи(ЭтотОбъект.Ссылка);
	КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

КопированиеХозяин=Справочники.Контрагенты.ПустаяСсылка();
РазрешениеИзмененияПолногоНаименования = Ложь;

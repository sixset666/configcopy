// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт; // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ХарактеристикаНоменклатуры Экспорт; // характеристика номенклатуры текущей строки документа ВводВЭксплуатацию
Перем ТипНоменклатурыВрем; //временная переменная

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Получает минимальный срок полезного использования из перечисления
//
// Параметры:
// АГ - ПеречислениеСсылка - Амортизационная группа
//
// Возвращаемое значение:
// Число - Срок полезного использования
//
Функция ПолучитьСрокПолезногоИспользования(АГ) Экспорт
	П_АГ = Перечисления.АмортизационныеГруппы;
	//простой case по группам
	Если      АГ = П_АГ.ПерваяГруппа    Тогда СрокЧисло = 12;
	ИначеЕсли АГ = П_АГ.ВтораяГруппа    Тогда СрокЧисло = 25;
	ИначеЕсли АГ = П_АГ.ТретьяГруппа    Тогда СрокЧисло = 37;
	ИначеЕсли АГ = П_АГ.ЧетвертаяГруппа Тогда СрокЧисло = 61;
	ИначеЕсли АГ = П_АГ.ПятаяГруппа     Тогда СрокЧисло = 85;
	ИначеЕсли АГ = П_АГ.ШестаяГруппа    Тогда СрокЧисло = 121;
	ИначеЕсли АГ = П_АГ.СедьмаяГруппа   Тогда СрокЧисло = 181;
	ИначеЕсли АГ = П_АГ.ВосьмаяГруппа   Тогда СрокЧисло = 241;
	ИначеЕсли АГ = П_АГ.ДевятаяГруппа   Тогда СрокЧисло = 301;
	ИначеЕсли АГ = П_АГ.ДесятаяГруппа   Тогда СрокЧисло = 361;
	Иначе СрокЧисло = 12;
	КонецЕсли;
		
	Возврат СрокЧисло;
КонецФункции // ПолучитьСрокПолезногоИспользования()

//Выводит отчет "История актива"
Процедура СформироватьОтчетИсторияАктива(ЭтаФорма) Экспорт
	Отчет = Отчеты.ИсторияАктива.Создать();
	Отчет.Актив = Ссылка;
	Отчет.ДатаКонца = ТекущаяДата();
	Отчет.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
	Отчет.ИмяФормыНастроек = "Форма";
	ФормаОтчета = Отчет.ПолучитьФорму("Форма");
	Отчет.СформироватьОтчет(ФормаОтчета, "");
КонецПроцедуры // СформироватьОтчетИсторияАктива()

#Если Клиент Тогда
// Функция формирует табличный документ с печатной формой инвентарной карточки ОС (форма ОС-6)
// Утверждена постановлением Госкомстата России от 21.01.2003 № 7
// Возвращаемое значение:
//  Табличный документ - печатная форма инвентарной карточки ОС
Процедура ПечатьОС6() Экспорт
		
	Макет = ПолучитьМакет("ОС6");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ШапкаРазделов1и2  = Макет.ПолучитьОбласть("ШапкаРазделов1и2");
	ШапкаРаздела3     = Макет.ПолучитьОбласть("ШапкаРаздела3");
	СтрокиРаздела3    = Макет.ПолучитьОбласть("СтрокиРаздела3");
	ШапкаРаздела4     = Макет.ПолучитьОбласть("ШапкаРаздела4");
	СтрокаРаздела4    = Макет.ПолучитьОбласть("СтрокаРаздела4");
	ПодвалСтраницы1   = Макет.ПолучитьОбласть("ПодвалСтраницы1");
	ШапкаРазделов5и6  = Макет.ПолучитьОбласть("ШапкаРазделов5и6");
	СтрокаРазделов5и6 = Макет.ПолучитьОбласть("СтрокаРазделов5и6");
	ШапкаРаздела7_1   = Макет.ПолучитьОбласть("ШапкаРаздела7_1");
	ШапкаРаздела7_2   = Макет.ПолучитьОбласть("ШапкаРаздела7_2");
	ПодвалСтраницы2   = Макет.ПолучитьОбласть("ПодвалСтраницы2");
		
	ТабДокумент = Новый ТабличныйДокумент();
		
	Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	        |	ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании,
	        |	ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании.Организация КАК Организация,
	        |	ПрочиеАктивыВЭксплуатации.ТипЭксплуатации,
	        |	ПрочиеАктивыВЭксплуатации.МОЛ
	        |ИЗ
	        |	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	        |ГДЕ
	        |	ПрочиеАктивыВЭксплуатации.ПрочийАктив = &ВыбПрочийАктив
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ПрочиеАктивыВЭксплуатации.МоментВремени УБЫВ";
		
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("ВыбПрочийАктив", Ссылка);
		
	ВыБорка = Запрос.Выполнить().Выбрать();
		
	Если Не ВыБорка.Следующий() Тогда
		Сообщить("На момент формирования отчета прочий актив не введен в эксплуатацию."+Символы.ПС+
		"Нельзя сформировать инвентарную карточку объекта!",СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли; 
		
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права);
		
	Подразделение  		= Выборка.ПодразделениеКомпании;
	Организация			= Выборка.Организация;
	Ответственный		= Выборка.МОЛ;
	ДатаВвода			= Ссылка.ДатаВводаВЭксплуатацию;
	ДатаСписания		= Ссылка.ДатаВыбытия;
	СрокИспользования	= Ссылка.СрокПолезногоИспользования;
		
	Шапка.Параметры.Организация  	    = Организация;
	Шапка.Параметры.Подразделение	    = Подразделение;
	Шапка.Параметры.НаименованиеОС		= Ссылка.Наименование;
	Шапка.Параметры.ОбъектОС			= Ссылка;
	Шапка.Параметры.НомерДок         	= Ссылка.ИнвентарныйНомер;
	Шапка.Параметры.ДатаДок          	= Формат(ТекущаяДата(),"ДФ=dd.MM.yyyy");
	Шапка.Параметры.МестоНахождениеОС 	= Подразделение;
	//Шапка.Параметры.ИзготовительОС  	= Изготовитель;
	//Шапка.Параметры.КодПоОКОФ        	= КодПоОКОФ.Код;
	Шапка.Параметры.НомерГруппы			= Выборка.ТипЭксплуатации.АмортизационнаяГруппа;
	//Шапка.Параметры.НомерПаспорта	    = НомерПаспорта;
	//Шапка.Параметры.ЗаводскойНомер 	= ЗаводскойНомер;
	Шапка.Параметры.ИнвентарныйНомер 	= Ссылка.ИнвентарныйНомер;
	//Шапка.Параметры.СубСчет        	= Строка(СчетУчетаСтоимостиБУ);
	Шапка.Параметры.ДатаВвода    	 	= ДатаВвода;
	Шапка.Параметры.ДатаСписания	 	= ДатаСписания;
		
	ТабДокумент.Вывести(Шапка);              
		
	Текст = "ВЫБРАТЬ
	|	ПрочиеАктивыВЭксплуатации.Регистратор,
	|	ПрочиеАктивыВЭксплуатации.ХозОперация,
	|	ПрочиеАктивыВЭксплуатации.Период КАК Период,
	|	СУММА(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимость - ПрочиеАктивыВЭксплуатации.СуммаАмортизации) КАК ПервоначальнаяСтоимость
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	|ГДЕ
	|	ПрочиеАктивыВЭксплуатации.ПрочийАктив = &ПрочийАктив
	|	И ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеАктивыВЭксплуатации.ХозОперация,
	|	ПрочиеАктивыВЭксплуатации.Регистратор,
	|	ПрочиеАктивыВЭксплуатации.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
		
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("ПрочийАктив",Ссылка);
	Запрос.УстановитьПараметр("Организация",Организация);
		
	Выборка = Запрос.Выполнить().Выбрать();
				
	Пока Выборка.Следующий() Цикл
		Если Выборка.ХозОперация = Справочники.ХозОперации.ВводВЭксплуатацию
			ИЛИ Выборка.ХозОперация = Справочники.ХозОперации.ВводВЭксплуатациюАвтомобилей
			ИЛИ Выборка.ХозОперация = Справочники.ХозОперации.ВводОстатковПрочихАктивов Тогда
			ДатаВвода           			= Выборка.Период;
			ДокументВвода      				= Выборка.Регистратор;
			ДокументВводаОперация			= Выборка.ХозОперация;
			ДокументВводаНомер  			= Выборка.Регистратор.Номер;
			ТекПервоначальнаяСтоимость		= Выборка.ПервоначальнаяСтоимость;
		ИначеЕсли Выборка.ХозОперация = Справочники.ХозОперации.РеализацияАктивов 
			ИЛИ Выборка.ХозОперация = Справочники.ХозОперации.СписаниеАктивов Тогда
			ДатаСписания        			= Выборка.Период;
			ДокументСписания    			= Выборка.Регистратор;
			ОперацияСписания				= Выборка.ХозОперация;
			РегистраторСписания				= Выборка.Регистратор;
		ИначеЕсли Выборка.ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаРасходыПодразделения
			ИЛИ Выборка.ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаСтоимостьАктива Тогда
			ДатаПоследнейМодернизации     	= Выборка.Период;
			ДокументПоследнейМодернизации 	= Выборка.Регистратор;
		КонецЕсли; 
	КонецЦикла;
	
	//Если после выполнения цикла остались незаполненные значения, то заполним их пустыми строками
	Если ОбЗначениеНеЗаполнено(ДатаВвода) Тогда    
		ДатаВвода = "";	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ДокументВвода) Тогда    
		ДокументВвода = "";  	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ДокументВводаНомер) Тогда    
		ДокументВводаНомер = ""; 	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ДатаСписания) Тогда    
		ДатаСписания = "";	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ДокументСписания) Тогда    
		ДокументСписания = "";	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(РегистраторСписания) Тогда    
		РегистраторСписания = "";	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ОперацияСписания) Тогда    
		ОперацияСписания = "";	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ДатаПоследнейМодернизации) Тогда    
		ДатаПоследнейМодернизации = "";	       
	КонецЕсли; 
	Если ОбЗначениеНеЗаполнено(ДокументПоследнейМодернизации) Тогда    
		ДокументПоследнейМодернизации = "";	       
	КонецЕсли; 
		
	// Сведения об объекте ОС на дату передачи	
	Если не ОбЗначениеНеЗаполнено(ДатаСписания) Тогда       
			
		//ШапкаРазделов1и2.Параметры.ДатаВыпуска        = ДатаВводаВЭксплуатацию;
		ШапкаРазделов1и2.Параметры.ДатаКапремонта     	= ДатаПоследнейМодернизации;
		ШапкаРазделов1и2.Параметры.ДокументКапремонта	= ДокументПоследнейМодернизации;
		ШапкаРазделов1и2.Параметры.ДокументВвода      	= ДокументВвода; 
		ШапкаРазделов1и2.Параметры.ОперацияВвода		= ДокументВводаОперация;
		ШапкаРазделов1и2.Параметры.ДокументВводаНомер 	= ДокументВводаНомер;
		ШапкаРазделов1и2.Параметры.ДокументВводаДата  	= ДатаВвода;
			
		Если ОперацияСписания = Справочники.ХозОперации.РеализацияАктивов Тогда
			ШапкаРазделов1и2.Параметры.СрокЭксплуатации = Цел((ДатаСписания - ДатаВвода) / 2592000);
				
			Текст = "ВЫБРАТЬ
			|	ОбъединенныйЗапрос.ПрочийАктив КАК ПрочийАктив,
			|	СУММА(ОбъединенныйЗапрос.ОстаточнаяСтоимость) КАК ОстаточнаяСтоимость,
			|	СУММА(ОбъединенныйЗапрос.СуммаАмортизации) КАК СуммаАмортизации
			|ИЗ
			|	(ВЫБРАТЬ
			|		ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив КАК ПрочийАктив,
			|		ПрочиеАктивыВЭксплуатацииОстатки.БалансоваяСтоимостьОстаток - ПрочиеАктивыВЭксплуатацииОстатки.СуммаАмортизацииОстаток КАК ОстаточнаяСтоимость,
			|		0 КАК СуммаАмортизации
			|	ИЗ
			|		РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(&МоментВыбытия, ПрочийАктив = &ПрочийАктив) КАК ПрочиеАктивыВЭксплуатацииОстатки
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПрочиеАктивыВЭксплуатацииОбороты.ПрочийАктив,
			|		0,
			|		ПрочиеАктивыВЭксплуатацииОбороты.СуммаАмортизацииОборот
			|	ИЗ
			|		РегистрНакопления.ПрочиеАктивыВЭксплуатации.Обороты(
			|			&МоментПоступления,
			|			&МоментВыбытия,
			|			,
			|			ПодразделениеКомпании.Организация = &Организация
			|			    И ПрочийАктив = &ПрочийАктив) КАК ПрочиеАктивыВЭксплуатацииОбороты) КАК ОбъединенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ОбъединенныйЗапрос.ПрочийАктив";
			Запрос = Новый Запрос(Текст);
			Запрос.УстановитьПараметр("МоментВыбытия", Новый МоментВремени(ДатаСписания,РегистраторСписания));
			Запрос.УстановитьПараметр("МоментПоступления", ДатаВвода);
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("ПрочийАктив", Ссылка);
			СостояниеНаМоментПередачи = Запрос.Выполнить().Выгрузить();
				
			// Износ и остаточная стоимость ОС на момент передачи
			Если СостояниеНаМоментПередачи.Количество()>0 Тогда
				ШапкаРазделов1и2.Параметры.СуммаНачисленнойАмортизации = Формат(СостояниеНаМоментПередачи[0].СуммаАмортизации,ФорматВыводаСуммы);
				ШапкаРазделов1и2.Параметры.ОстаточнаяСтоимость         = Формат(СостояниеНаМоментПередачи[0].ОстаточнаяСтоимость,ФорматВыводаСуммы);
			Иначе
				ШапкаРазделов1и2.Параметры.СуммаНачисленнойАмортизации = "";
				ШапкаРазделов1и2.Параметры.ОстаточнаяСтоимость         = "";
			КонецЕсли; 
		Иначе
			ШапкаРазделов1и2.Параметры.СуммаНачисленнойАмортизации = "";
			ШапкаРазделов1и2.Параметры.ОстаточнаяСтоимость         = "";
		КонецЕсли;
	КонецЕсли;
		
	ШапкаРазделов1и2.Параметры.СрокПолезногоИспользования = СрокИспользования;
		
	// Балансовая стоимость ОС на момент поступления
	ШапкаРазделов1и2.Параметры.ПервоначальнаяСтоимость = Формат(ТекПервоначальнаяСтоимость,ФорматВыводаСуммы);
		
	ТабДокумент.Вывести(ШапкаРазделов1и2);                            
	ТабДокумент.Вывести(ШапкаРаздела3);
	ТабДокумент.Вывести(СтрокиРаздела3);
		
	// Сведения о приемке, внутренних перемещениях и выбытии	
	ТабДокумент.Вывести(ШапкаРаздела4);
		
	Текст = "ВЫБРАТЬ
	        |	ПрочиеАктивыВЭксплуатации.Регистратор КАК Документ,
	        |	ПрочиеАктивыВЭксплуатации.МОЛ КАК МОЛ,
	        |	ПрочиеАктивыВЭксплуатации.МОЛ.Наименование КАК МОЛНаименование,
	        |	ПрочиеАктивыВЭксплуатации.ХозОперация КАК ХозОперация,
	        |	ПрочиеАктивыВЭксплуатации.Период КАК Период,
	        |	ПрочиеАктивыВЭксплуатации.Регистратор.Номер КАК НомерДокумента,
	        |	ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании КАК ПодразделениеКомпании
	        |ИЗ
	        |	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	        |ГДЕ
	        |	ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании.Организация = &Организация
	        |	И ПрочиеАктивыВЭксплуатации.ПрочийАктив = &ПрочийАктив
	        |	И ПрочиеАктивыВЭксплуатации.ХозОперация В(&МассивХозОпераций)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ПрочиеАктивыВЭксплуатации.ХозОперация,
	        |	ПрочиеАктивыВЭксплуатации.Период,
	        |	ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании,
	        |	ПрочиеАктивыВЭксплуатации.МОЛ,
	        |	ПрочиеАктивыВЭксплуатации.Регистратор,
	        |	ПрочиеАктивыВЭксплуатации.МоментВремени,
	        |	ПрочиеАктивыВЭксплуатации.Регистратор.Номер
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ПрочиеАктивыВЭксплуатации.МоментВремени";
		
	МассивХозОпераций = Новый Массив;
	МассивХозОпераций.Добавить(Справочники.ХозОперации.ВводВЭксплуатацию);
	МассивХозОпераций.Добавить(Справочники.ХозОперации.ВводВЭксплуатациюАвтомобилей);
	МассивХозОпераций.Добавить(Справочники.ХозОперации.ВводОстатковПрочихАктивов);
	МассивХозОпераций.Добавить(Справочники.ХозОперации.РеализацияАктивов);
	МассивХозОпераций.Добавить(Справочники.ХозОперации.СписаниеАктивов);
	МассивХозОпераций.Добавить(Справочники.ХозОперации.ПеремещениеАктивов);
		
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПрочийАктив", Ссылка);
	Запрос.УстановитьПараметр("МассивХозОпераций", МассивХозОпераций);
		
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
			
		ТекущаяОперация = Выборка.ХозОперация;
		СтрокаРаздела4.Параметры.ДатаНомерДокумента = ТекущаяОперация.Наименование + " № "+Выборка.НомерДокумента+" от "+Формат(Выборка.Период,"ДФ=dd.MM.yyyy");
		СтрокаРаздела4.Параметры.Документ			= Выборка.Документ;
		СтрокаРаздела4.Параметры.ВидОперации        = ТекущаяОперация;
		СтрокаРаздела4.Параметры.ФИОМОЛДвижения     = Выборка.МОЛНаименование;
		СтрокаРаздела4.Параметры.МОЛ     			= Выборка.МОЛ;
		СтрокаРаздела4.Параметры.Подразделение      = Выборка.ПодразделениеКомпании;
			
		Если ТекущаяОперация = Справочники.ХозОперации.РеализацияАктивов ИЛИ ТекущаяОперация = Справочники.ХозОперации.СписаниеАктивов Тогда
				
			Если ОперацияСписания = Справочники.ХозОперации.РеализацияАктивов Тогда
				//Параметры выбытия уже определены в таблице СостояниеНаМоментПередачи
			Иначе
				// Износ и остаточная стоимость ОС  на момент выбытия
				Текст = "ВЫБРАТЬ
				|	ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив,
				|	ПрочиеАктивыВЭксплуатацииОстатки.БалансоваяСтоимостьОстаток - ПрочиеАктивыВЭксплуатацииОстатки.СуммаАмортизацииОстаток КАК ОстаточнаяСтоимость
				|ИЗ
				|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(
				|		&МоментВыбытия, ПрочийАктив = &ПрочийАктив) КАК ПрочиеАктивыВЭксплуатацииОстатки";
				Запрос = Новый Запрос(Текст);
				Запрос.УстановитьПараметр("МоментВыбытия", Новый МоментВремени(Выборка.Период,Выборка.Документ));
				Запрос.УстановитьПараметр("ПрочийАктив", Ссылка);
				СостояниеНаМоментПередачи = Запрос.Выполнить().Выгрузить();
			КонецЕсли; 
				
			ОстаточнаяСтоимость = 0;
				
			Если СостояниеНаМоментПередачи.Количество()>0 тогда
				ОстаточнаяСтоимость = СостояниеНаМоментПередачи[0].ОстаточнаяСтоимость;
			КонецЕсли;
			СтрокаРаздела4.Параметры.ОстаточнаяСтоимость = Формат(ОстаточнаяСтоимость,ФорматВыводаСуммы);
		КонецЕсли;
		ТабДокумент.Вывести(СтрокаРаздела4);
	КонецЦикла;
		
	ТабДокумент.Вывести(ПодвалСтраницы1);
		
	// модернизация ос и ремонт
	ТаблицаМодернизаций = Новый ТаблицаЗначений;
	ТаблицаМодернизаций.Колонки.Добавить("ВидОперации");
	ТаблицаМодернизаций.Колонки.Добавить("ХозОперация");
	ТаблицаМодернизаций.Колонки.Добавить("Название");
	ТаблицаМодернизаций.Колонки.Добавить("Дата");
	ТаблицаМодернизаций.Колонки.Добавить("Номер");
	ТаблицаМодернизаций.Колонки.Добавить("Сумма");
		
	ТаблицаРемонтов = Новый ТаблицаЗначений;
	ТаблицаРемонтов.Колонки.Добавить("ВидОперации");
	ТаблицаРемонтов.Колонки.Добавить("ХозОперация");
	ТаблицаРемонтов.Колонки.Добавить("Название");
	ТаблицаРемонтов.Колонки.Добавить("Дата");
	ТаблицаРемонтов.Колонки.Добавить("Номер");
	ТаблицаРемонтов.Колонки.Добавить("Сумма");
		
	//ШапкаРазделов5и6.Параметры.Валюта = ВалютаПечати;
	ТабДокумент.Вывести(ШапкаРазделов5и6);
		
	Текст = "ВЫБРАТЬ
	|	ПрочиеАктивыВЭксплуатации.Период КАК Период,
	|	ПрочиеАктивыВЭксплуатации.Регистратор КАК Регистратор,
	|	ПрочиеАктивыВЭксплуатации.Регистратор.Номер КАК НомерДокумента,
	|	ПрочиеАктивыВЭксплуатации.ХозОперация,
	|	ПрочиеАктивыВЭксплуатации.ТипОбслуживания,
	|	СУММА(ПрочиеАктивыВЭксплуатации.СуммаОбслуживания) КАК СуммаОбслуживания
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	|ГДЕ
	|	ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании.Организация = &Организация
	|	И ПрочиеАктивыВЭксплуатации.ПрочийАктив = &ПрочийАктив
	|	И ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ОбслуживаниеАктива
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеАктивыВЭксплуатации.Регистратор,
	|	ПрочиеАктивыВЭксплуатации.ХозОперация,
	|	ПрочиеАктивыВЭксплуатации.ТипОбслуживания,
	|	ПрочиеАктивыВЭксплуатации.Период,
	|	ПрочиеАктивыВЭксплуатации.Регистратор.Номер
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
		
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПрочийАктив", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
			
		Если Выборка.ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаСтоимостьАктива Тогда
			СтрокаТаблицыМодернизаций = ТаблицаМодернизаций.Добавить();
			СтрокаТаблицыМодернизаций.ВидОперации = Выборка.ТипОбслуживания;
			СтрокаТаблицыМодернизаций.ХозОперация = Выборка.ХозОперация;
			СтрокаТаблицыМодернизаций.Название    = Выборка.Регистратор;
			СтрокаТаблицыМодернизаций.Номер       = Выборка.НомерДокумента;
			СтрокаТаблицыМодернизаций.Дата        = Выборка.Период;
			СтрокаТаблицыМодернизаций.Сумма       = Выборка.СуммаОбслуживания;
		Иначе
			СтрокаТаблицыРемонтов = ТаблицаРемонтов.Добавить();
			СтрокаТаблицыРемонтов.ВидОперации		= Выборка.ТипОбслуживания;
			СтрокаТаблицыРемонтов.ХозОперация		= Выборка.ХозОперация;
			СтрокаТаблицыРемонтов.Название   	    = Выборка.Регистратор;
			СтрокаТаблицыРемонтов.Номер         	= Выборка.НомерДокумента;
			СтрокаТаблицыРемонтов.Дата             	= Выборка.Период;
			СтрокаТаблицыРемонтов.Сумма           	= Выборка.СуммаОбслуживания;
		КонецЕсли;
			
	КонецЦикла;
		
	КоличествоСтрок = Макс(ТаблицаМодернизаций.Количество(),ТаблицаРемонтов.Количество(),1); 
	Для СчетСтрок = 1 По КоличествоСтрок Цикл
		Если СчетСтрок <= ТаблицаМодернизаций.Количество() Тогда
			СтрокаТаблицы = ТаблицаМодернизаций.Получить(СчетСтрок - 1);
			СтрокаРазделов5и6.Параметры.Модернизация          = СтрокаТаблицы.ВидОперации;
			СтрокаРазделов5и6.Параметры.ОперацияМодернизации  = СтрокаТаблицы.ХозОперация;
			СтрокаРазделов5и6.Параметры.ДокМодернизации       = СтрокаТаблицы.Название;
			СтрокаРазделов5и6.Параметры.ДокМодернизацииДата   = СтрокаТаблицы.Дата;
			СтрокаРазделов5и6.Параметры.ДокМодернизацииНомер  = СтрокаТаблицы.Номер;
			СтрокаРазделов5и6.Параметры.ЗатратыНаМодернизацию = Формат(СтрокаТаблицы.Сумма,ФорматВыводаСуммы); 
		Иначе
			СтрокаРазделов5и6.Параметры.Модернизация          = "";
			СтрокаРазделов5и6.Параметры.ОперацияМодернизации  = "";
			СтрокаРазделов5и6.Параметры.ДокМодернизации       = "";
			СтрокаРазделов5и6.Параметры.ДокМодернизацииДата   = "";
			СтрокаРазделов5и6.Параметры.ДокМодернизацииНомер  = "";
			СтрокаРазделов5и6.Параметры.ЗатратыНаМодернизацию = ""; 
		КонецЕсли;
			
		Если СчетСтрок <= ТаблицаРемонтов.Количество() Тогда
			СтрокаТаблицы = ТаблицаРемонтов.Получить(СчетСтрок - 1);
			СтрокаРазделов5и6.Параметры.Ремонт         	 = СтрокаТаблицы.ВидОперации;
			СтрокаРазделов5и6.Параметры.ОперацияРемонта  = СтрокаТаблицы.ХозОперация;
			СтрокаРазделов5и6.Параметры.ДокРемонта       = СтрокаТаблицы.Название;
			СтрокаРазделов5и6.Параметры.ДокРемонтаДата   = СтрокаТаблицы.Дата;
			СтрокаРазделов5и6.Параметры.ДокРемонтаНомер  = СтрокаТаблицы.Номер;
			СтрокаРазделов5и6.Параметры.ЗатратыНаРемонт	 = Формат(СтрокаТаблицы.Сумма,ФорматВыводаСуммы);
		Иначе
			СтрокаРазделов5и6.Параметры.Ремонт         	 = "";
			СтрокаРазделов5и6.Параметры.ОперацияРемонта  = "";
			СтрокаРазделов5и6.Параметры.ДокРемонта     	 = "";
			СтрокаРазделов5и6.Параметры.ДокРемонтаДата 	 = "";
			СтрокаРазделов5и6.Параметры.ДокРемонтаНомер	 = "";
			СтрокаРазделов5и6.Параметры.ЗатратыНаРемонт	 = "";
		КонецЕсли;
		ТабДокумент.Вывести(СтрокаРазделов5и6);
	КонецЦикла;
		
	ТабДокумент.Вывести(ШапкаРаздела7_1);
	ТабДокумент.Вывести(ШапкаРаздела7_2);
		
	Текст = "ВЫБРАТЬ
	        |	СведенияОСотрудникахСрезПоследних.Должность
	        |ИЗ
	        |	РегистрСведений.СведенияОСотрудниках.СрезПоследних КАК СведенияОСотрудникахСрезПоследних
	        |ГДЕ
	        |	СведенияОСотрудникахСрезПоследних.Подразделение = &Подразделение
	        |	И СведенияОСотрудникахСрезПоследних.Сотрудник = &Сотрудник";
		
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Сотрудник", Ответственный);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПодвалСтраницы2.Параметры.ДолжностьОтветственного = Выборка.Должность;
	Иначе
		ПодвалСтраницы2.Параметры.ДолжностьОтветственного = Ответственный.Должность;
	КонецЕсли;
	ПодвалСтраницы2.Параметры.ФИООтветственного	= спПолучитьНаименование(Ответственный);
	ПодвалСтраницы2.Параметры.Ответственный 	= Ответственный;
		
	ТабДокумент.Вывести(ПодвалСтраницы2);
		
	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
		
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.ОтображатьЗаголовки	= Ложь;
	ТабличныйДокумент.ОтображатьСетку		= Ложь;
	ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",Права);
	ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
	// Перенесем все настройки из табличного документа в поле табличного документа формы печати
	СписокКопируемыхСвойств = "АвтоМасштаб,Защита,ИмяПараметровПечати,ИмяПринтера,ИмяСохраненияПоложенияОкна,ИтогиСнизу,ИтогиСправа,КодЯзыкаМакета,КоличествоЭкземпляров,НаправлениеПерехода,ОбластьПечати,ОриентацияСтраницы,ОтображатьГруппировки,ОтображатьЗаголовки,ОтображатьСетку,ПовторятьПриПечатиКолонки,ПовторятьПриПечатиСтроки,ПолеСверху,ПолеСлева,ПолеСнизу,ПолеСправа,РазмерКолонтитулаСверху,РазмерКолонтитулаСнизу,СохранятьСвойстваОтображения,ТекущаяОбласть,ТолькоПросмотр,ФиксацияСверху,ФиксацияСлева,ФиксированныйФон,ФоноваяКартинка,ЧерноБелыйПросмотр";
	ЗаполнитьЗначенияСвойств(ТабДокумент, ТабличныйДокумент, СписокКопируемыхСвойств);
		
	//Выведем печатную форму	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ДополнительноКПредставлению	= "Инвентарная карточка актива";
	ФормаПечати.Открыть();
		
КонецПроцедуры // ПечатьОС6()
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код", 1);
	ОбязательныеРеквизиты.Вставить("Наименование", 1);
	
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ТипНоменклатуры", 1);
		ОбязательныеРеквизиты.Вставить("Номенклатура", 1);
		ОбязательныеРеквизиты.Вставить("ВидПрочегоАктива", 1);
		Если НЕ (ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
			     ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
				 ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты) Тогда
				 ОбязательныеРеквизиты.Вставить("ИнвентарныйНомер", 3);
		КонецЕсли;	
		ОбязательныеРеквизиты.Вставить("СрокПолезногоИспользования", 1);
		Если ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Оборудование Тогда
			ОбязательныеРеквизиты.Вставить("РесурсВыработки", 1);
			ОбязательныеРеквизиты.Вставить("ЕдиницаВыработки", 1);
		КонецЕсли;
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Если (НЕ ЭтоГруппа) И (ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Оборудование) Тогда
		ДопРеквизиты = Новый Структура("РесурсВыработки, ЕдиницаВыработки", 1, 1);
	КонецЕсли;
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Для данного актива, если он эксплуатируется, выводятся балансовая стоимость, амортизация, остаточная стоимость
Процедура ЗаполнитьИнформационныеПоля(ФормаЭлемента) Экспорт
	Если ЭтоНовый() ИЛИ Ссылка.Пустая() Тогда
		//нет смысла получать остатки по не записанному активу
	Иначе
		ТаблПараметрыАктива = дкПолучитьОстаткиАктивов(, , Ссылка, ТекущаяДата());
		
		ФормаЭлемента.БалансоваяСтоимость  = ?(ТаблПараметрыАктива.Количество() = 0, 0, ТаблПараметрыАктива[0].БалансоваяСтоимость);
		ФормаЭлемента.Амортизация 		  = ?(ТаблПараметрыАктива.Количество() = 0, 0, ТаблПараметрыАктива[0].Амортизация);
		ФормаЭлемента.ОстаточнаяСтоимость = ?(ТаблПараметрыАктива.Количество() = 0, 0, ТаблПараметрыАктива[0].БалансоваяСтоимость - ТаблПараметрыАктива[0].Амортизация);
	КонецЕсли;
КонецПроцедуры // ЗаполнитьИнформационныеПоля()
 
// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя = "ТипНоменклатуры" И ТипНоменклатуры <> ТипНоменклатурыВрем Тогда
		Рез = спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ДопПараметры = "ПодставитьСрокЭксплуатацииПоУмолчаниюИзАГ" Тогда
		ИначеЕсли ЭтоГруппа Тогда
			ОсновнойТипЭксплуатации = ТипНоменклатуры.ОсновнойТипЭксплуатации;
			Если ТипНоменклатуры = Справочники.ТипыНоменклатуры.МатериалыИСпецоснастка Тогда
				ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка;
			ИначеЕсли ТипНоменклатуры = Справочники.ТипыНоменклатуры.НематериальныйАктив Тогда
				ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.НМА;
			Иначе
				ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.ОсновноеСредство;
			КонецЕсли;
		Иначе
			Если ЭтаФорма <> Неопределено Тогда
				ЭтаФорма.ОбработкаОбъектЗначенияСвойствОбъекта.ОбъектОтбораЗначений = Ссылка;
				ЭтаФорма.ОбработкаОбъектЗначенияСвойствОбъекта.ПрочитатьЗаполнитьСвойстваИЗначения();
			КонецЕсли;
			ОсновнойТипЭксплуатации = ТипНоменклатуры.ОсновнойТипЭксплуатации;
			Если Номенклатура.ТипНоменклатуры <> ТипНоменклатуры Тогда
				Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		Если НЕ ЭтоГруппа Тогда
			СрокПолезногоИспользования = ПолучитьСрокПолезногоИспользования(ОсновнойТипЭксплуатации.АмортизационнаяГруппа);
		КонецЕсли; 
		ТипНоменклатурыВрем = ТипНоменклатуры;
		Возврат Рез;
			
	Иначе //остальные реквизиты
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА
 
// Стандартный обработчик заполнения
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	// заполним что можно
	Если ЭтоНовый() Тогда
		Если обЗначениеНеЗаполнено(Основание) Тогда
			Если Не ЭтотОбъект.ЭтоГруппа Тогда
				Если обЗначениеНеЗаполнено(КоэффициентУскорения) Тогда
					КоэффициентУскорения = 1;
				КонецЕсли;
			КонецЕсли;	
		Иначе
			Возврат;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Родитель) Тогда
			Если обЗначениеНеЗаполнено(ТипНоменклатуры) Тогда
				ТипНоменклатуры  = Справочники.ТипыНоменклатуры.ОсновноеСредство;
				ОсновнойТипЭксплуатации = ТипНоменклатуры.ОсновнойТипЭксплуатации;
			КонецЕсли;
		Иначе //попытаемся заполнить из родителя
			ВидПрочегоАктива        = Родитель.ВидПрочегоАктива;
			ТипНоменклатуры         = Родитель.ТипНоменклатуры;
			ОсновнойТипЭксплуатации = Родитель.ОсновнойТипЭксплуатации;
		КонецЕсли;
		ОбработкаРеквизита("ТипНоменклатуры", , , "ПодставитьСрокЭксплуатацииПоУмолчаниюИзАГ")
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта                 
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
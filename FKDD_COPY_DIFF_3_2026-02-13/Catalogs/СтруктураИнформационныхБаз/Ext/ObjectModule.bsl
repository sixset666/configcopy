// Модуль справочника Должности

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",2);
	ОбязательныеРеквизиты.Вставить("Префикс",1);
	// настройка доставки сообщений нам необходима для запуска обменов через ком.файлы
	ОбязательныеРеквизиты.Вставить("НастройкаДоставкиПоУмолчанию", 1);
	ОбязательныеРеквизиты.Вставить("РежимПроведенияПоПартиям",1);

	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Рез = Истина;
	Если ОбменДанными.Загрузка Тогда
		Возврат Истина;
	КонецЕсли;
	Если ЭтотОбъект.Подразделения.Количество() = 0 Тогда
		Ошибки = "Не заполнена табличная часть ""Подразделения""";
		Рез = Ложь;
	КонецЕсли;
	Возврат Рез И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Возврат Истина;
КонецФункции

// Обработка изменения реквизитов справочников
// Параметры
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	спПриКопировании(ЭтотОбъект, ОбъектКопирования);
КонецПроцедуры

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	спПередЗаписью(ЭтотОбъект, Отказ);
	
	Если ЭтоНовый() Тогда
		ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
		УзелИБ = ЭтотУзел.ИнформационнаяБаза;
		// ограничение - можно создавать только подчиненные узлы
		Если НЕ Родитель = УзелИБ Тогда
		   Ошибки = "Разрешено создавать только подчиненные узлы";
		   #Если Клиент Тогда
	   	   Сообщить(Ошибки,СтатусСообщения.Внимание);
		   #КонецЕсли
	   	   Отказ = Истина;
	   КонецЕсли;
	   
	    // ограничение - можно создавать новые узлы, а не "натягивать" текущий
		// элемент Справочника СтруктураИБ на уже существующий узел плана обмена (ИБ придет из дочки)
		// для новых узлов - ограничения нет - можем просто хотим создать узел, а затем
		// сделать для него начальный образ (ИБ опять таки придет из дочки в первом сообщении от нее)
		Если НЕ Отказ Тогда
			УзелОбмена = ПланыОбмена.УдаленныеПодразделения.НайтиПоРеквизиту("Префикс",Префикс);
			Если ЗначениеЗаполнено(УзелОбмена) Тогда
				Ошибки = "Для узла-родителя разрешено создавать только новые узлы.
					| Подчиненный узел появится по завершении с ним сеанса обмена";
				#Если Клиент Тогда
					Сообщить(Ошибки,СтатусСообщения.Внимание);
				#КонецЕсли
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		// имеет смысл только изменение для подчиненных узлов из текущего узла
		УзелОбмена = ПланыОбмена.УдаленныеПодразделения.НайтиПоРеквизиту("Префикс",Ссылка.Префикс);
		Если НЕ УзелОбмена.Пустая() Тогда
			Если Код <> Ссылка.Код
				ИЛИ Префикс <> Ссылка.Префикс
				ИЛИ Наименование <> Ссылка.Наименование Тогда
				Если НЕ (ПланыОбмена.ГлавныйУзел() = Неопределено 
					ИЛИ (УзелОбмена <> ПланыОбмена.ГлавныйУзел() 
					И  УзелОбмена <> ПланыОбмена.УдаленныеПодразделения.ЭтотУзел())) Тогда
					Ошибки = "Изменение реквизитов: <Код>, <Префикс>, <Наименование> производится
					| для подчинных узлов только в главном узле и появится по завершении с ними сеанса обмена";
					#Если Клиент Тогда
						Сообщить(Ошибки,СтатусСообщения.Внимание);
					#КонецЕсли
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Если ЭтоНовый() Тогда
			ДополнительныеСвойства.Вставить("Префикс",Префикс);
		Иначе
			ДополнительныеСвойства.Вставить("Префикс",Ссылка.Префикс);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	
	спПриЗаписи(ЭтотОбъект, Отказ);
	Если Отказ Тогда
	    Возврат;
	КонецЕсли; 
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// произведем синхронизацию с узлом обмена
	// SUIG - учитываем, что могли изменить префикс ИБ
	// причем поиск надо производить именно по еще неизмененному префиксу
	ПрефиксЗначение = неопределено;
	ДополнительныеСвойства.Свойство("Префикс",ПрефиксЗначение);
	ПрефиксЗначение = ?(ПрефиксЗначение = неопределено,Префикс,ПрефиксЗначение);
	ДополнительныеСвойства.Удалить("Префикс");
		
	УзелОбмена = ПланыОбмена.УдаленныеПодразделения.НайтиПоРеквизиту("Префикс",ПрефиксЗначение);
	// в этом узле синхронизация уже проведена при старте системы
	// SUIG - ничего подобного - возможна ситуация изменения того же префикса ИБ
	// с последующей отправкой главному узлу, в результате чего при редактировании 
	// полученного элемента ИБ в главном узле появится "левый" узел-двойник текущего узла,
	// т.к. не сработает поиск по префиксу
	//Если УзелОбмена = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел() Тогда
	//	Возврат;
	//КонецЕсли;
	
	СохранитьИзменения = Ложь;
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
	    УзелОбменаОбъект = УзелОбмена.ПолучитьОбъект();
	Иначе
		УзелОбменаОбъект = ПланыОбмена.УдаленныеПодразделения.СоздатьУзел();
	КонецЕсли;
	// пока код не можем менять - иначе на той стороне будет невозможно
	// определить сообщение (C_Ц...)
	Если НЕ ЗначениеЗаполнено(УзелОбменаОбъект.Код) Тогда
		УзелОбменаОбъект.Код = Префикс;
		СохранитьИзменения 	= Истина;
	КонецЕсли;
	Если УзелОбменаОбъект.Префикс <> Префикс Тогда
		УзелОбменаОбъект.Префикс = Префикс;
		СохранитьИзменения = Истина;
	КонецЕсли;
	Если УзелОбменаОбъект.Наименование <> Наименование Тогда
		УзелОбменаОбъект.Наименование = Наименование;
		СохранитьИзменения = Истина;
	КонецЕсли;
	Если УзелОбменаОбъект.ИнформационнаяБаза <> Ссылка Тогда
		УзелОбменаОбъект.ИнформационнаяБаза = Ссылка;
		СохранитьИзменения = Истина;
	КонецЕсли;
	Если УзелОбменаОбъект.ПометкаУдаления <> ПометкаУдаления Тогда
		УзелОбменаОбъект.ПометкаУдаления = ПометкаУдаления;
		СохранитьИзменения = Истина;
	КонецЕсли;
	
	Если СохранитьИзменения Тогда
		Попытка
			УзелОбменаОбъект.Записать();
		Исключение 
			Отказ = Истина;
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	
	спПередУдалением(ЭтотОбъект, Отказ);
	Если Отказ Тогда
	    Возврат;
	КонецЕсли; 
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// произведем синхронизацию с узлом обмена
	УзелОбмена = ПланыОбмена.УдаленныеПодразделения.НайтиПоРеквизиту("ИнформационнаяБаза",Ссылка);
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
		УзелОбменаОбъект = УзелОбмена.ПолучитьОбъект();
	Иначе
		Возврат;
	КонецЕсли; 
	// в целях предосторожности ставим только пометку удаления в узле
	УзелОбменаОбъект.ПометкаУдаления = Истина;
	УзелОбменаОбъект.Записать();

КонецПроцедуры

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если ПустаяСтрока(Префикс) Тогда
		Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда 
			Возврат; 
		КонецЕсли;
	КонецЕсли;
	//!!!DOOY Игорь тут ниже это ботва какая-то помоему, если есть смысл раскомментируй и поясни
	// в случае дочерних узлов проверки заполнения префиксации не происходит
	// поэтому вынуждены сами искать префикс
	Если НЕ ЗначениеЗаполнено(Префикс) Тогда
		ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
		Если ЗначениеЗаполнено(ЭтотУзел) Тогда
			Если  ЗначениеЗаполнено(ЭтотУзел.Префикс) Тогда
				Префикс = ЭтотУзел.Префикс;
		    КонецЕсли;
        КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
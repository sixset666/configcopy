// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	// Обязательные поля таблицы смещение
	ОбязательныеСмещение=Новый Структура();
	ОбязательныеСмещение.Вставить("НомерДня",2);
	
	// Обязательные реквизиты справочника
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	//ОбязательныеРеквизиты.Вставить("ДатаНачала",1);
	ОбязательныеРеквизиты.Вставить("Периодичность",1);
	ОбязательныеРеквизиты.Вставить("Смещение",ОбязательныеСмещение);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Рез = спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	//корректность длины смещения (количества строк смещения)
	Если Рез Тогда
		Если Периодичность = Перечисления.Периодичность.Неделя И Смещение.Количество() > 7 Тогда
			Ошибки = Ошибки + "В смещении должно быть не более 7 дней!" + Символы.ПС;
			Рез = Ложь;
		КонецЕсли;
		Если Периодичность = Перечисления.Периодичность.Декада И Смещение.Количество() > 10 Тогда
			Ошибки = Ошибки + "В смещении должно быть не более 10 дней!" + Символы.ПС;
			Рез = Ложь;
		КонецЕсли;
		Если Периодичность = Перечисления.Периодичность.Месяц И Смещение.Количество() > 31 Тогда
			Ошибки = Ошибки + "В смещении должно быть не более 31 дня!" + Символы.ПС;
			Рез = Ложь;
		КонецЕсли;
		Если Периодичность = Перечисления.Периодичность.Квартал И Смещение.Количество() > 92 Тогда
			Ошибки = Ошибки + "В смещении должно быть не более 92 дней!" + Символы.ПС;
			Рез = Ложь;
		КонецЕсли;
		Если Периодичность = Перечисления.Периодичность.Год И Смещение.Количество() > 366 Тогда
			Ошибки = Ошибки + "В смещении должно быть не более 366 дней!" + Символы.ПС;
			Рез = Ложь;
		КонецЕсли;
	КонецЕсли;
	//корректность уникальности смещений (не должно быть строк с дублирующимся смещением)
	Если Рез Тогда
		тблСмещений = Смещение.Выгрузить();
		тблСмещений.Свернуть("НомерДня");
		Если тблСмещений.Количество() <> тблСмещений.Количество() Тогда
			Ошибки = Ошибки + "Дублирование шаблона по величине смещения (номеру дня)!" + Символы.ПС;
			Рез = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя = "Смена" Тогда
		ИнтервалыСмены = СменаПоУмолчанию.Интервалы;
		НачалоРабочегоВремениПоУмолчанию = '00010101';
		КонецРабочегоВремениПоУмолчанию = '00010101';
		ПродолжительностьВСекундах = 0;
		
		Если ИнтервалыСмены.Количество() > 0 Тогда
			НачалоРабочегоВремениПоУмолчанию = ИнтервалыСмены[0].НачалоРабочегоВремени;
			КонецРабочегоВремениПоУмолчанию = ИнтервалыСмены[ИнтервалыСмены.Количество()-1].КонецРабочегоВремени;
			Для Каждого ТекИнтервал Из ИнтервалыСмены Цикл
				Если ТекИнтервал.ВидИнтервала.РабочийИнтервал Тогда
					ПродолжительностьВСекундах = ПродолжительностьВСекундах + (ТекИнтервал.Продолжительность - Дата('00010101'));
				КонецЕсли;	
				КонецЦикла;
		КонецЕсли;
		ПродолжительностьПоУмолчанию = '00010101' + ПродолжительностьВСекундах;
		
	ИначеЕсли Имя = "Продолжительность" Тогда
		Если КонецРабочегоВремениПоУмолчанию <> '00010101' Тогда
			Если НачалоРабочегоВремениПоУмолчанию <= КонецРабочегоВремениПоУмолчанию Тогда
				ПродолжительностьМаксимум = КонецРабочегоВремениПоУмолчанию - НачалоРабочегоВремениПоУмолчанию;
			Иначе
				ПродолжительностьМаксимум = 24*3600 - (НачалоРабочегоВремениПоУмолчанию - КонецРабочегоВремениПоУмолчанию);
			КонецЕсли;
			ПродолжительностьНовая = ПродолжительностьПоУмолчанию - '00010101';
			Если ПродолжительностьНовая > ПродолжительностьМаксимум Тогда
				ПродолжительностьПоУмолчанию = '00010101' + ПродолжительностьМаксимум;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя = "НачалоРабочегоВремени" Тогда
		Возврат ОбработкаРеквизита("КонецРабочегоВремени", , ЭтаФорма);
		
	ИначеЕсли Имя = "КонецРабочегоВремени" Тогда
		Если НачалоРабочегоВремениПоУмолчанию <> '00010101' И КонецРабочегоВремениПоУмолчанию = '00010101' Тогда
			КонецРабочегоВремениПоУмолчанию = '00010101235959';
			Возврат ОбработкаРеквизита("КонецРабочегоВремени", , ЭтаФорма);
		КонецЕсли;
		Если НачалоРабочегоВремениПоУмолчанию > КонецРабочегоВремениПоУмолчанию Тогда
			ПродолжительностьПоУмолчанию = '00010101' + (24*3600 - (НачалоРабочегоВремениПоУмолчанию - КонецРабочегоВремениПоУмолчанию));
			Возврат Истина;
		КонецЕсли;
		
		ПродолжительностьПоУмолчанию = '00010101' + (КонецРабочегоВремениПоУмолчанию - НачалоРабочегоВремениПоУмолчанию);
		Если КонецРабочегоВремениПоУмолчанию = '00010101235959' Тогда
			ПродолжительностьПоУмолчанию = ПродолжительностьПоУмолчанию + 1;
		КонецЕсли;
		
	ИначеЕсли Имя = "СмещениеПродолжительность" Тогда
		Если ТекСтрока.КонецРабочегоВремени <> '00010101' Тогда
			Если ТекСтрока.НачалоРабочегоВремени <= ТекСтрока.КонецРабочегоВремени Тогда
				ПродолжительностьМаксимум = ТекСтрока.КонецРабочегоВремени - ТекСтрока.НачалоРабочегоВремени;
			Иначе
				ПродолжительностьМаксимум = 24*3600 - (ТекСтрока.НачалоРабочегоВремени - ТекСтрока.КонецРабочегоВремени);
			КонецЕсли;
			ПродолжительностьНовая = ТекСтрока.Продолжительность - '00010101';
			Если ПродолжительностьНовая > ПродолжительностьМаксимум Тогда
				ТекСтрока.Продолжительность = '00010101' + ПродолжительностьМаксимум;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя = "СмещениеНачалоКонецРабочегоВремени" Тогда
		Если ТекСтрока.НачалоРабочегоВремени <> '00010101' И ТекСтрока.КонецРабочегоВремени = '00010101' Тогда
			ТекСтрока.КонецРабочегоВремени = '00010101235959';
			Возврат ОбработкаРеквизита("СмещениеНачалоКонецРабочегоВремени", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		Если ТекСтрока.НачалоРабочегоВремени < ТекСтрока.КонецРабочегоВремени Тогда
			ТекСтрока.Продолжительность = '00010101' + (ТекСтрока.КонецРабочегоВремени - ТекСтрока.НачалоРабочегоВремени);
		Иначе
			ТекСтрока.Продолжительность = '00010101' + (24*3600 - (ТекСтрока.НачалоРабочегоВремени - ТекСтрока.КонецРабочегоВремени));
		КонецЕсли;
		
		Если ТекСтрока.КонецРабочегоВремени = '00010101235959' Тогда
			ТекСтрока.Продолжительность = ТекСтрока.Продолжительность + 1;
		КонецЕсли;
		
	ИначеЕсли Имя = "СмещениеСмена" Тогда
		Если НЕ ТекСтрока.Смена.Пустая() Тогда
			ИнтервалыСмены = ТекСтрока.Смена.Интервалы;
			ТекСтрока.НачалоРабочегоВремени = '00010101';
			ТекСтрока.КонецРабочегоВремени = '00010101';
			ПродолжительностьВСекундах = 0;
			
			Если ИнтервалыСмены.Количество() > 0 Тогда
				ТекСтрока.НачалоРабочегоВремени = ИнтервалыСмены[0].НачалоРабочегоВремени;
				ТекСтрока.КонецРабочегоВремени = ИнтервалыСмены[ИнтервалыСмены.Количество()-1].КонецРабочегоВремени;
				Для Каждого ТекИнтервал Из ИнтервалыСмены Цикл
					Если ТекИнтервал.ВидИнтервала.РабочийИнтервал Тогда
						ПродолжительностьВСекундах = ПродолжительностьВСекундах + (ТекИнтервал.Продолжительность - Дата('00010101'));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ТекСтрока.Продолжительность = '00010101' + ПродолжительностьВСекундах;
		Иначе
			ИнтервалыСмены = ТекСтрока.Смена.Интервалы;
			ТекСтрока.НачалоРабочегоВремени = '00010101';
			ТекСтрока.КонецРабочегоВремени = '00010101';
			ТекСтрока.Продолжительность = 0;
		КонецЕсли;
		
	Иначе
		Возврат спОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	спПриКопировании(ЭтотОбъект, ОбъектКопирования);
КонецПроцедуры

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	спПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	спПриЗаписи(ЭтотОбъект, Отказ);
КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	спПередУдалением(ЭтотОбъект, Отказ);
КонецПроцедуры

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
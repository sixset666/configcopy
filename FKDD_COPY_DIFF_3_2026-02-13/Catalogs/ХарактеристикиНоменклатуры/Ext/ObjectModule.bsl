// Модуль справочника ХарактеристикиНоменклатуры

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	
	Если ДляЭлемента Тогда
		// получим ограничения
		Ограничение=НЕОПРЕДЕЛЕНО; ТипНоменклатуры=НЕОПРЕДЕЛЕНО; 
		Если ТипЗнч(Владелец)=Тип("СправочникСсылка.Номенклатура") Тогда
			ТипНоменклатуры=Владелец.ТипНоменклатуры;
		ИначеЕсли ТипЗнч(Владелец)=Тип("СправочникСсылка.ТипыНоменклатуры") Тогда
			ТипНоменклатуры=Владелец;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ТипНоменклатуры) Тогда
			Ограничение=ТипНоменклатуры.ОграничениеДанныхХарактеристик;
			Если ТипНоменклатуры.УникальностьСерийногоНомера Тогда
				УникальностьСерНомера=3;	
			КонецЕсли;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Ограничение) Тогда
			Ограничение=Перечисления.ОграниченияДанныхХарактеристики.Все;
		КонецЕсли;
		Если УникальностьСерНомера <> 3 И Ограничение=Перечисления.ОграниченияДанныхХарактеристики.СерийныйНомер_Свойства Тогда
			УникальностьСерНомера = 1;			
		КонецЕсли;
		// в зависимости от вида ограничения расставляем обязательные реквизиты
		Если	(Ограничение=Перечисления.ОграниченияДанныхХарактеристики.Все) Тогда
			Если НЕ ОбЗначениеНеЗаполнено(УникальностьСерНомера) Тогда 
				ОбязательныеРеквизиты.Вставить("СерийныйНомер",УникальностьСерНомера);  
			КонецЕсли;
		ИначеЕсли (Ограничение=Перечисления.ОграниченияДанныхХарактеристики.Качество) Тогда
			ОбязательныеРеквизиты.Вставить("Качество",1);
		ИначеЕсли (Ограничение=Перечисления.ОграниченияДанныхХарактеристики.СерийныйНомер_Свойства) Тогда
			ОбязательныеРеквизиты.Вставить("СерийныйНомер",УникальностьСерНомера);
		ИначеЕсли (Ограничение=Перечисления.ОграниченияДанныхХарактеристики.Сроки_Сертификаты) Тогда
			ОбязательныеРеквизиты.Вставить("СрокГодности",1);
		КонецЕсли;
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
// Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

//Процедура формирует наименование объекта
//
// Параметры:
//	СвойстваИЗначения - ТабличноеПоле - Таблица свойств и значений
//
// Возвращаемое значение:
// Строка - Наименование
//
Функция СформироватьНаименование(СвойстваИЗначения=Неопределено) Экспорт
	Стр = "";
	// формируем наименование в зависимости от заполненных реквизитов
	// серийный номер		
	Если НЕ ПустаяСтрока(СокрЛП(СерийныйНомер)) Тогда
		Стр = Стр + " с/н "+СокрЛП(СерийныйНомер);
	КонецЕсли;
    //качество
	Если НЕ обЗначениеНеЗаполнено(Качество) Тогда
		Стр=СокрЛП(Качество);
	КонецЕсли;
	// сертификат
	Если НЕ обЗначениеНеЗаполнено(Сертификат) Тогда
		Стр=Стр+" "+СокрЛП(Сертификат);
	КонецЕсли;
	// срок годности
	Если НЕ обЗначениеНеЗаполнено(СрокГодности) Тогда
		Стр=Стр+" годен до "+Формат(СрокГодности,"ДФ=dd.MM.yy");
	КонецЕсли;
	// доп.пробел перед прочими св-вами
	Если НЕ ПустаяСтрока(Стр) Тогда
		Стр = Стр + " ";
	КонецЕсли;
	// свойства	
	Если (СвойстваИЗначения  <> НЕОПРЕДЕЛЕНО) Тогда
		Для Каждого ТекСтрока Из СвойстваИЗначения Цикл
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Значение) Тогда
				Стр = Стр + СокрЛП(ТекСтрока.Значение)+" ";
			КонецЕсли;	
		КонецЦикла; 
	КонецЕсли;
	// обрежем лишние пробелы
	Стр=СокрЛП(Стр);
	
		// проверим что получилось
	Если ПустаяСтрока(Стр) Тогда
		Стр="<не заполнены реквизиты>";
	КонецЕсли;
	// вернем результат	
	Возврат Стр;
КонецФункции // СформироватьНаименование()
 
// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
	
// Процедура добавляет кнопку "СоздатьШК"
// Параметры:
//	ЭтаФорма - Форма объект
//
Процедура ДобавитьКнопкуСоздатьШК(ЭтаФорма) Экспорт
	
	Кнопки = ЭтаФорма.ЭлементыФормы.ОсновныеДействияФормы.Кнопки;
	Кнопки.Добавить("СоздатьШК",ТипКнопкиКоманднойПанели.Действие,
	"СоздатьШК",Новый Действие("ОсновныеДействияФормыСоздатьШК"));
	Кнопки["СоздатьШК"].Картинка = БиблиотекаКартинок.СоздатьШтрихКод;
	Кнопки["СоздатьШК"].Отображение =  ОтображениеКнопкиКоманднойПанели.Картинка;
	Кнопки.Добавить("кРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
	Кнопки.Сдвинуть(Кнопки.кРазделитель,-Кнопки.Количество());
	Кнопки.Сдвинуть(Кнопки.СоздатьШК,-Кнопки.Количество());
	Кнопки.СоздатьШК.Подсказка = "Записать объект и создать штрих-код";
	ЭтаФорма.ЭлементыФормы.тВладелец.Ширина = ЭтаФорма.ЭлементыФормы.тВладелец.Ширина - 22;
	
КонецПроцедуры

// Процедура записи объекта
// Параметры:
//	ЭтаФорма - объект формы
//	СформированШтрихКод - признак того, что штрих код уже сформирован
//
Процедура ЗаписатьОбъектИСоздатьШК(ЭтаФорма,СформированШтрихКод) Экспорт

	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ПараметрОтборНоменклатура) Тогда
	    Возврат;
	КонецЕсли;
	ЭтаФорма.ЗаписатьВФорме();
	// создаем новый штрих-код
	СформированШтрихКод = Ложь;
	ОбработкаОбъектШтрихКоды = Обработки.ШтрихКоды.Создать();
	ОбработкаОбъектШтрихКоды.Объект = ЭтаФорма.ПараметрОтборНоменклатура.ПолучитьОбъект();
	НовыйШтрихКод=ОбработкаОбъектШтрихКоды.СформироватьШтрихКодТовара(ЭтаФорма.ПараметрОтборНоменклатура.ТипНоменклатуры.Весовой);
	Оповестить("СформированШтрихКод",Новый Структура("ХарактеристикаНоменклатуры,ШтрихКод",
		Ссылка,НовыйШтрихКод),ЭтаФорма.ВладелецФормы);
	СформированШтрихКод = Истина;
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	Наименование=СокрЛП(Наименование);
	Если ПустаяСтрока(Наименование) ИЛИ Наименование="<не заполнены реквизиты>" Тогда
		Наименование=СформироватьНаименование();
	КонецЕсли;
		
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Отказ Тогда Возврат; КонецЕсли;
	
	// получим направление сортировки
	АвтоСписаниеХарактеристик=Неопределено;
	Если ТипЗнч(Владелец)=Тип("СправочникСсылка.Номенклатура") Тогда
		АвтоСписаниеХарактеристик=Владелец.ТипНоменклатуры.АвтоСписаниеХарактеристик;
	Иначе
		АвтоСписаниеХарактеристик=Владелец.АвтоСписаниеХарактеристик;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(АвтоСписаниеХарактеристик) Тогда
		АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание;
	КонецЕсли;
		
	// хэш сортировки
	Сортировка=0;
	Если АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.ДатаИзготовленияВозр Тогда
		Сортировка=ДатаИзготовления-'0001-01-01';
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.ДатаИзготовленияУбыв Тогда
		Сортировка='0001-01-01'-ДатаИзготовления;
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.КачествоВозр Тогда
		Сортировка=Число(Качество.Код);
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.КачествоУбыв Тогда
		// считаем количество качеств =)
		Запрос=Новый Запрос("ВЫБРАТЬ КОЛИЧЕСТВО(*) КАК Кол ИЗ Справочник.Качество");
		КоличествоКачеств=Запрос.Выполнить().Выгрузить().Получить(0).Кол;
		Сортировка=Число(Качество.Код)-КоличествоКачеств;
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.СерийныйНомерВозр Тогда
		
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.СерийныйНомерУбыв Тогда
		//Сортировка=99999999999999999999-;
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.СрокГодностиВозр Тогда
		Сортировка=СрокГодности-'0001-01-01';
	ИначеЕсли АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.СрокГодностиУбыв Тогда
		Сортировка='0001-01-01'-СрокГодности;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
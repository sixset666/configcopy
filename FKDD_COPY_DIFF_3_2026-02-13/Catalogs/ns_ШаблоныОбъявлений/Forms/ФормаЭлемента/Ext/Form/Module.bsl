
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РеквизитыДляШаблона = ns_ШаблонизаторСервер.ПолучитьРеквизиты();
	РеквизитыНоменклатуры = ns_ШаблонизаторСервер.ПолучитьРеквизитыНоменклатуры();
	
	ИндексКоманды = 1;
	Для каждого Строка Из РеквизитыДляШаблона Цикл
		ns_ОбщегоНазначения.ДобавитьКнопкуНаФорму(Элементы, Команды, "ДобавитьОбщийШаблон" + ИндексКоманды, "[" + Строка.Наименование + "]", "ДобавитьВШаблон", Элементы.ГруппаРеквизитыДляШаблона); 
		
		НовШаблон = СписокШаблонов.Добавить();
		НовШаблон.Шаблон = "[" + Строка.Наименование + "]";
		НовШаблон.Команда = "КомандаДобавитьОбщийШаблон" + ИндексКоманды;
		
		ИндексКоманды = ИндексКоманды + 1;
	КонецЦикла; 
	
	ИндексКоманды = 1;
	Для каждого Строка Из РеквизитыНоменклатуры Цикл
		ns_ОбщегоНазначения.ДобавитьКнопкуНаФорму(Элементы, Команды, "ДобавитьШаблонНоменклатуры" + ИндексКоманды, "[ном_" + Строка.Наименование + "]", "ДобавитьВШаблон", Элементы.ГруппаРеквизитыНоменклатуры); 
		
		НовШаблон = СписокШаблонов.Добавить();
		НовШаблон.Шаблон = "[ном_" + Строка.Наименование + "]";
		НовШаблон.Команда = "КомандаДобавитьШаблонНоменклатуры" + ИндексКоманды;
		
		ИндексКоманды = ИндексКоманды + 1;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВШаблон(Элемент)
	
	ПозицияПослеВставки = 0;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Команда", Элемент.Имя);
	НайденныеСтроки = СписокШаблонов.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Шаблон = НайденныеСтроки[0].Шаблон;
		
		НачалоСтроки = 0; НачалоКолонки = 0; КонецСтроки = 0; КонецКолонки = 0;
		Элементы.ОбщийТекстОбъявлений.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки); 
		
		Результат = "";
		Данные = Объект.ОбщийТекстОбъявлений;
		
		Счетчик = 1;
		Пока Счетчик < НачалоСтроки цикл
			Результат = Результат + СтрПолучитьСтроку(Данные, Счетчик) + Символы.ПС;
			Счетчик = Счетчик + 1;
		КонецЦикла;
		
		Если НачалоКолонки > 1 Тогда
			Результат = Результат + Лев(СтрПолучитьСтроку(Данные, НачалоСтроки), НачалоКолонки - 1);   
		КонецЕсли;
		
		Результат = Результат + Шаблон;
		ПозицияПослеВставки = СтрДлина(Результат);
		
		Результат = Результат + Сред(СтрПолучитьСтроку(Данные, КонецСтроки), КонецКолонки);
		Счетчик = КонецСтроки + 1;
		НачалоКолонки = СтрЧислоСтрок(Данные);
		
		Пока Счетчик <= НачалоКолонки Цикл
			Результат = Результат + Символы.ПС + СтрПолучитьСтроку(Данные, Счетчик);
			Счетчик = Счетчик + 1;
		КонецЦикла; 
		
		Объект.ОбщийТекстОбъявлений = Результат;
	КонецЕсли; 
	
	ПодключитьОбработчикОжидания("УстановитьГраницыВыделенияТекстШаблона", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГраницыВыделенияТекстШаблона()
	Если ПозицияПослеВставки > 0 Тогда
	    Элементы.ОбщийТекстОбъявлений.УстановитьГраницыВыделения(ПозицияПослеВставки + 1, ПозицияПослеВставки + 1);
		ПозицияПослеВставки = 0;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура Параграф(Команда)
	ПозицияПослеВставки = 0;
	
	ШаблонПараграфОткрытие = "<p>";
	ШаблонПараграфЗакрытие = "</p>";
	
	НачалоСтроки = 0; НачалоКолонки = 0; КонецСтроки = 0; КонецКолонки = 0;
	Элементы.ОбщийТекстОбъявлений.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки); 
	
	Результат = "";
	Данные = Объект.ОбщийТекстОбъявлений;
	
	ВыделеннаяСтрока = "";
	Счетчик = НачалоСтроки;
	Пока Счетчик <= КонецСтроки цикл
		Если Счетчик = КонецСтроки Тогда
			ВыделеннаяСтрока = ВыделеннаяСтрока + СтрПолучитьСтроку(Данные, Счетчик);
		Иначе	
			ВыделеннаяСтрока = ВыделеннаяСтрока + СтрПолучитьСтроку(Данные, Счетчик) + Символы.ПС;
		КонецЕсли; 
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Результат = ШаблонПараграфОткрытие + ВыделеннаяСтрока + ШаблонПараграфЗакрытие;
	
	Объект.ОбщийТекстОбъявлений = СтрЗаменить(Объект.ОбщийТекстОбъявлений, ВыделеннаяСтрока, Результат);
	
	ПодключитьОбработчикОжидания("УстановитьГраницыВыделенияТекстШаблона", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДопРеквизиты(Команда)
	ЗагрузитьДопРеквизитыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДопРеквизитыНаСервере()
	СписокДопРеквизитов = Новый СписокЗначений;
	ns_Переопределяемый.ЗаполнитьДопРеквизиты(СписокДопРеквизитов);
	
	ИндексКоманды = 1;
	Для каждого Строка Из СписокДопРеквизитов Цикл
		ns_ОбщегоНазначения.ДобавитьКнопкуНаФорму(Элементы, Команды, "ДобавитьШаблонДопРеквизиты" + ИндексКоманды, "[" + Строка.Значение + "]", "ДобавитьВШаблон", Элементы.ГруппаДопРеквизиты); 
		
		НовШаблон = СписокШаблонов.Добавить();
		НовШаблон.Шаблон = "[" + Строка.Значение + "]";
		НовШаблон.Команда = "КомандаДобавитьШаблонДопРеквизиты" + ИндексКоманды;
		
		ИндексКоманды = ИндексКоманды + 1;
	КонецЦикла;
	
	Если СписокДопРеквизитов.Количество() > 0 Тогда
		Элементы.ЗагрузитьДопРеквизиты.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры




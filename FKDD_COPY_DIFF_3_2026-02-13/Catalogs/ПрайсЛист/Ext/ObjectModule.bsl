// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда

// процедура печати прайс-листа
Процедура ПечатьПрайса(НаДату,ТекущийВладелец,ГруппаПрайса,НастройкаПечатиКолонок) Экспорт
	ПечатнаяФорма = ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",, Новый УникальныйИдентификатор);
	ТабДокумент = ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент;
	
	Макет = Справочники.ПрайсЛист.ПолучитьМакет("ПрайсЛист");
	ПечататьКод				= НастройкаПечатиКолонок.ПечататьКод;
	ПечататьАртикул			= НастройкаПечатиКолонок.ПечататьАртикул;
	ПечататьШтрихКод		= НастройкаПечатиКолонок.ПечататьШтрихКод;	
	ПечататьЕдиницуИзмерения= НастройкаПечатиКолонок.ПечататьЕдиницуИзмерения;
	МассивТиповЦен			= НастройкаПечатиКолонок.МассивТиповЦен;
	
	//Формируем текст запроса в зависимости от параметров формы настройки
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПрайсЛист.Номенклатура КАК Товар,
	|	ПрайсЛист.Наименование КАК Наименование,
	|	ПрайсЛист.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ПрайсЛист.Номенклатура.Код,
	|	ПрайсЛист.Номенклатура.Артикул,
    |	ПрайсЛист.Номенклатура КАК Номенклатура,
	|	ПрайсЛист.Номенклатура.ОсновнаяЕдиницаИзмерения";
	Если ПечататьШтрихКод Тогда ТекстЗапроса = ТекстЗапроса + ", 
		|	ШтрихКоды.ШтрихКод"; КонецЕсли;
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		ТекстЗапроса = ТекстЗапроса + ",
		|	ЦеныСрезПоследних"+сч+".ТипЦен КАК ТипЦен"+сч+",
		|	СРЕДНЕЕ(ЦеныСрезПоследних"+сч+".Цена) КАК Цена"+сч;
	КонецЦикла;
	ТекстЗапроса = ТекстЗапроса + " 	
	|ИЗ
	|	Справочник.ПрайсЛист КАК ПрайсЛист";
	//Соединяем с регистром штрих-кодов
	Если ПечататьШтрихКод Тогда
		ТекстЗапроса = ТекстЗапроса + "	
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|		ПО ПрайсЛист.Номенклатура = ШтрихКоды.Объект
		|			И ((НЕ ШтрихКоды.Запрет))
		|			И (ШтрихКоды.ХарактеристикаНоменклатуры = &Характеристика)
		|			И (ВЫБОР
		|				КОГДА ПрайсЛист.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 1
		|					ТОГДА ШтрихКоды.ЕдиницаИзмерения = &ЕдиницаИзмерения
		|				ИНАЧЕ ПрайсЛист.Номенклатура.ОсновнаяЕдиницаИзмерения = ШтрихКоды.ЕдиницаИзмерения 
		|			КОНЕЦ)"	
	КонецЕсли;
    //Соединяем с регистром цен
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		ТекстЗапроса = ТекстЗапроса + "	
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
		|		&НаДату,
		|		Номенклатура В
		|		    (ВЫБРАТЬ
		|		        Справочник.ПрайсЛист.Номенклатура
		|		    ИЗ
		|		        Справочник.ПрайсЛист
		|		    ГДЕ
		|		        (НЕ Справочник.ПрайсЛист.ЭтоГруппа)
		|		        И
		|		        (НЕ Справочник.ПрайсЛист.ПометкаУдаления)
		|		        И
		|		        Справочник.ПрайсЛист.Владелец = &Владелец";
		
		Если НЕ ГруппаПрайса.Пустая() Тогда 
			ТекстЗапроса = ТекстЗапроса + "		
			|И Справочник.ПрайсЛист.Ссылка В ИЕРАРХИИ(&Родитель)" 	
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ") И ХарактеристикаНоменклатуры=ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка
		| И ЕдиницаИзмерения=ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) И ПодразделениеКомпании=ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))) КАК ЦеныСрезПоследних"+сч+"
		|		ПО ПрайсЛист.Номенклатура = ЦеныСрезПоследних"+сч+".Номенклатура
		|			И (ЦеныСрезПоследних"+сч+".ТипЦен = &ТипЦен"+сч+")";
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса  + "	
	|ГДЕ
	|	ПрайсЛист.Владелец = &Владелец
	|	И ПрайсЛист.ПометкаУдаления = ЛОЖЬ
	|	И ПрайсЛист.ЭтоГруппа = ЛОЖЬ";
	
	Если НЕ ГруппаПрайса.Пустая() Тогда ТекстЗапроса = ТекстЗапроса + "	
		|	И ПрайсЛист.Ссылка В ИЕРАРХИИ(&Родитель)"; КонецЕсли;
	
	СтрокаТипЦен = "";
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		СтрокаТипЦен = ?(СтрокаТипЦен = "","", СтрокаТипЦен + " ИЛИ ") + "(НЕ ЦеныСрезПоследних"+сч+".ТипЦен ЕСТЬ NULL )";
	КонецЦикла;
	Если СтрокаТипЦен <> "" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|   И ("+СтрокаТипЦен+")";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|СГРУППИРОВАТЬ ПО
	|	ПрайсЛист.Ссылка,";
	
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		ТекстЗапроса = ТекстЗапроса + "	
		|	ЦеныСрезПоследних"+сч+".ТипЦен,"
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ПрайсЛист.Наименование,
	|	ПрайсЛист.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ПрайсЛист.Номенклатура.Код,
	|	ПрайсЛист.Номенклатура.Артикул";
	
	Если ПечататьШтрихКод Тогда ТекстЗапроса = ТекстЗапроса + ",
		|	ШтрихКоды.ШтрихКод" КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "	
	|УПОРЯДОЧИТЬ ПО
	|	Наименование
	|ИТОГИ ";
	
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		ТекстЗапроса = ТекстЗапроса + "	
		|	СРЕДНЕЕ(Цена"+сч+ ")" +?(сч<МассивТиповЦен.Количество()-1,",","");
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ПО
	|	ПрайсЛист.Ссылка ИЕРАРХИЯ
	|";

	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Владелец",ТекущийВладелец);
	Запрос.УстановитьПараметр("Родитель",ГруппаПрайса);
	Запрос.УстановитьПараметр("Характеристика",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЕдиницаИзмерения",Справочники.ЕдиницыИзмерения.ПустаяСсылка());
	Запрос.УстановитьПараметр("НаДату",НаДату);
	
	//Заполняем типы цен в запросе в таком же порядке как и они в таблице цен настройки
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		ПараметрЗапросаТипЦен = "ТипЦен"+сч;
		Запрос.УстановитьПараметр(ПараметрЗапросаТипЦен,МассивТиповЦен.Получить(сч));
	КонецЦикла;
	
	РезультатПрайс=Запрос.Выполнить();
	
	// получаем области
	ОбластьНачалоШапка	= Макет.ПолучитьОбласть("Шапка|Начало");
	ОбластьНачалоГруппа	= Макет.ПолучитьОбласть("Группа|Начало");
	ОбластьНачалоСтрока	= Макет.ПолучитьОбласть("Строка|Начало");
	ОбластьЦенаШапка	= Макет.ПолучитьОбласть("Шапка|Цена");
	ОбластьЦенаГруппа	= Макет.ПолучитьОбласть("Группа|Цена");
	ОбластьЦенаСтрока	= Макет.ПолучитьОбласть("Строка|Цена");
	ОбластьКодСтрока	= Макет.ПолучитьОбласть("Строка|Код");
	ОбластьКодГруппа	= Макет.ПолучитьОбласть("Группа|Код");	
	ОбластьАртикулСтрока	= Макет.ПолучитьОбласть("Строка|Артикул");	
	ОбластьАртикулГруппа	= Макет.ПолучитьОбласть("Группа|Артикул");		
	ОбластьШтрихКодСтрока	= Макет.ПолучитьОбласть("Строка|ШтрихКод");
	ОбластьШтрихКодГруппа	= Макет.ПолучитьОбласть("Группа|ШтрихКод");	
	ОбластьЕдиницаИзмеренияСтрока = Макет.ПолучитьОбласть("Строка|ЕдиницаИзмерения");	
	ОбластьЕдиницаИзмеренияГруппа = Макет.ПолучитьОбласть("Группа|ЕдиницаИзмерения");		
	
	// выведем шапку
	ОбластьНачалоШапка.Параметры.ЗаголовокОтчета = "Прайс-лист" + ?(НаДату = '00010101',""," на " + Формат(НаДату,"ДЛФ=DD"));
	ТабДокумент.Вывести(ОбластьНачалоШапка);
	Если ПечататьКод Тогда ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Шапка|Код")) КонецЕсли;
	Если ПечататьАртикул Тогда ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Шапка|Артикул")) КонецЕсли;
	Если ПечататьШтрихКод Тогда ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Шапка|ШтрихКод")) КонецЕсли;
	Если ПечататьЕдиницуИзмерения Тогда ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Шапка|ЕдиницаИзмерения")) КонецЕсли;
	Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
		ТипЦен = МассивТиповЦен.Получить(сч);
		ТипЦен = ?(ТипЦен.ВВалютеУчета, ТипЦен.Наименование, ТипЦен.Наименование + Символы.ПС + " (" + обВалютаТипаЦены(Неопределено,ТипЦен,Ложь) + ")");
		ОбластьЦенаШапка.Параметры.ТипЦен = ТипЦен;
		ТабДокумент.Присоединить(ОбластьЦенаШапка);
	КонецЦикла;
	
	// строки
	ТабДокумент.НачатьАвтогруппировкуСтрок();
	НомерСтроки = 0;
	ВыборкаТовар = РезультатПрайс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Ссылка","Все");
	Пока ВыборкаТовар.Следующий() Цикл
		ОбластьНачало			= ОбластьНачалоСтрока;
		ОбластьЦена				= ОбластьЦенаСтрока;
		ОбластьКод				= ОбластьКодСтрока;
		ОбластьАртикул			= ОбластьАртикулСтрока;
		ОбластьШтрихКод			= ОбластьШтрихКодСтрока;
		ОбластьЕдиницаИзмерения	= ОбластьЕдиницаИзмеренияСтрока;
		Если ВыборкаТовар.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
			ОбластьНачало			= ОбластьНачалоГруппа;
			ОбластьЦена				= ОбластьЦенаГруппа;
			ОбластьКод				= ОбластьКодГруппа;
			ОбластьАртикул			= ОбластьАртикулГруппа;
			ОбластьШтрихКод			= ОбластьШтрихКодГруппа;
			ОбластьЕдиницаИзмерения	= ОбластьЕдиницаИзмеренияГруппа;
		Иначе
			НомерСтроки = НомерСтроки + 1;
		КонецЕсли;
		
		// выведем начало строки
		ОбластьНачало.Параметры.Заполнить(ВыборкаТовар);
		ОбластьНачало.Параметры.ТоварНаименование = ВыборкаТовар.Наименование;
		Попытка // если у нас группа, то не нужно выводит номер строки и расшифровку
			ОбластьНачало.Параметры.НомерСтроки	= НомерСтроки;
		Исключение
		КонецПопытки;
		ТабДокумент.Вывести(ОбластьНачало,ВыборкаТовар.Уровень());
		
		Попытка 
			ОбластьКод.Параметры.Код							= ВыборкаТовар.НоменклатураКод;
			ОбластьАртикул.Параметры.Артикул					= ВыборкаТовар.НоменклатураАртикул;
			ОбластьЕдиницаИзмерения.Параметры.ЕдиницаИзмерения	= ВыборкаТовар.НоменклатураБазоваяЕдиницаИзмерения;
			ВыборкаШтрихКод										= ВыборкаТовар.Выбрать();
			ОбластьШтрихКод.Параметры.ШтрихКод					= ?(ВыборкаШтрихКод.Следующий(),ВыборкаШтрихКод.ШтрихКод,"");
		Исключение 
		КонецПопытки; 
		
        // присоединим нужные колонки
		Если ПечататьКод Тогда ТабДокумент.Присоединить(ОбластьКод); КонецЕсли;
		Если ПечататьАртикул Тогда ТабДокумент.Присоединить(ОбластьАртикул); КонецЕсли;
		Если ПечататьШтрихКод Тогда ТабДокумент.Присоединить(ОбластьШтрихКод); КонецЕсли;
		Если ПечататьЕдиницуИзмерения Тогда ТабДокумент.Присоединить(ОбластьЕдиницаИзмерения); КонецЕсли;
		
		Если ВыборкаТовар.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
			Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
				ТабДокумент.Присоединить(ОбластьЦена)
			КонецЦикла;
			Продолжить
		КонецЕсли;
		
		Для сч = 0 По МассивТиповЦен.Количество()-1 Цикл
			Цена	= ВыборкаТовар["Цена"+сч];
			ТипЦен	= МассивТиповЦен.Получить(сч);
			ОбластьЦена.Параметры.Цена = ?(ТипЦен.ВВалютеУчета,Формат(Цена, "ЧЦ=15; ЧДЦ=2; ЧН=0.00") + ?(обЗначениеНеЗаполнено(Цена), "", " " + ВыборкаТовар.Номенклатура.ВалютаУчета.Наименование),Формат(Цена, "ЧЦ=15; ЧДЦ=2; ЧН=0.00"));
			ТабДокумент.Присоединить(ОбластьЦена);
		КонецЦикла;
	КонецЦикла;
	
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	// показ
	ТабДокумент.ТолькоПросмотр		= ИСТИНА;
	ТабДокумент.АвтоМасштаб			= ИСТИНА;
	ТабДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	
	ПечатнаяФорма.ОбъектПредставление			= "Прайс-лист на "+Формат(НаДату,"ДЛФ=DD");
	ПечатнаяФорма.ДополнительноКПредставлению	= "Вид прайс-листа: " + СокрЛП(ТекущийВладелец);
	ПечатнаяФорма.Открыть();
КонецПроцедуры // ПечатьПрайса()

#КонецЕсли

// Проверяет, корректно ли заполнен новый элемент справочника
//
// Параметры:
//  Элемент		– СправочникСсылка	– Элемент справочника.
//
Функция ПроверкаЭлемента(Элемент)
	Разрешено = Истина;
	Если Владелец.ФлагУникальности Тогда
		Разрешено = Разрешено И обЗначениеНеЗаполнено(Справочники.ПрайсЛист.НайтиПоРеквизиту("Номенклатура", Элемент.Номенклатура, , Владелец));
		Если Не Разрешено Тогда
			Сообщить("Номенклатура """ + Элемент.Номенклатура + """ не уникальна, элемент не добавлен.", СтатусСообщения.Внимание);
		КонецЕсли;
	КонецЕсли;
	Возврат Разрешено;
КонецФункции // ПроверкаЭлемента()

// Заполняет прайс-лист в соответствии с переданными параметрами заполнения
// ВладелецПрайсЛиста - вид прайс-листа, который заполняем
// СпособЗаполнения:  0 - по складским остаткам, 1 - по товарам поставщика, 2 - по справочнику
// "Номенклатура", 3 - копирование другого прайс-листа  
// Параметры заполнения - структура: зависит от способа заполнения, ключи - Склад, Дата,
// Поставщик, ГруппаНоменклатуры,ВидПрайсЛиста  
// Способ создания групп: 0 - не создавать, 1 - создавать по видам товаров, 2 - создавать по группам товаров
// Текущий родитель - текущий родитель прайс-листа
// ФлОчищать = Истина,если текущий прайс-лист будет очищен перед заполнением
//
// Параметры:
// ВладелецПрайсЛиста 	- Строка 			- Вид прайс-листа
// СпособЗаполнения 	- Число 			- Способ заполнения
// ПараметрыЗаполнения 	- Структура 		- Параметры заполнения  
// СпособСозданияГрупп 	- Число 			- Способ создания групп
// Текущий родитель 	- СправочникСсылка	- Текущий родитель прайс-листа
// ФлОчищать 			- Булево			- Флаг очистки прайс-листа перед заполнением
//
Процедура ЗаполнитьПрайсЛист(ВладелецПрайсЛиста,СпособЗаполнения=2,ПараметрыЗаполнения,СпособСозданияГрупп=2,ФлажокОчищать = Истина,ТекущийРодитель = Неопределено) Экспорт
	Если обЗначениеНеЗаполнено(ВладелецПрайсЛиста) Тогда
		Сообщить("Не задан владелец прайс-листа!");
		Возврат
	КонецЕсли;
	// очистим если надо
	Если ФлажокОчищать Тогда
		УдаляемыеЭлементы = Новый СписокЗначений;
		Запрос=Новый Запрос("ВЫБРАТЬ
			|	ПрайсЛист.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ПрайсЛист КАК ПрайсЛист
			|ГДЕ
			|	ПрайсЛист.Владелец = &ИсходныйВладелец");
		Запрос.УстановитьПараметр("ИсходныйВладелец",ВладелецПрайсЛиста);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Ссылка.Уровень()=0 Тогда
				УдаляемыеЭлементы.Добавить(Выборка.Ссылка);
			КонецЕсли;
		КонецЦикла;
		Для Каждого УдаляемЭлемент Из УдаляемыеЭлементы Цикл
			УдаляемЭлементОбъект = УдаляемЭлемент.Значение.ПолучитьОбъект();
			УдаляемЭлементОбъект.Удалить();
		КонецЦикла;
	КонецЕсли;
	Если ОбЗначениеНЕЗаполнено(ПараметрыЗаполнения)  Тогда ПараметрыЗаполнения = Новый Структура(); КонецЕсли;
	Если НЕ ПараметрыЗаполнения.Свойство("Склад")    Тогда ПараметрыЗаполнения.Вставить("Склад"); КонецЕсли;
	Если НЕ ПараметрыЗаполнения.Свойство("Дата")     Тогда ПараметрыЗаполнения.Вставить("Дата"); КонецЕсли;	
	Если НЕ ПараметрыЗаполнения.Свойство("Поставщик")Тогда ПараметрыЗаполнения.Вставить("Поставщик"); КонецЕсли;
	Если НЕ ПараметрыЗаполнения.Свойство("ГруппаНоменклатуры")Тогда ПараметрыЗаполнения.Вставить("ГруппаНоменклатуры"); КонецЕсли;
	Если НЕ ПараметрыЗаполнения.Свойство("ВидПрайсЛиста")Тогда ПараметрыЗаполнения.Вставить("ВидПрайсЛиста"); КонецЕсли;
	// Получим значения структуры
	Склад = ПараметрыЗаполнения.Склад;
	Дата  = ПараметрыЗаполнения.Дата;
	Поставщик = ПараметрыЗаполнения.Поставщик;
	ГруппаНоменклатуры = ПараметрыЗаполнения.ГруппаНоменклатуры;
	ВидПрайсЛиста = ПараметрыЗаполнения.ВидПрайсЛиста;
	// Непосредственно заполнение
	Запрос = Новый Запрос;
	СкладПустой  = обЗначениеНеЗаполнено(Склад); 
	ГруппаПустая = обЗначениеНеЗаполнено(ГруппаНоменклатуры);
	Если СпособЗаполнения = 0 Тогда // по остаткам
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|   ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Ссылка,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.Код КАК КодНоменклатуры,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|   ОстаткиТоваровКомпанииОстатки.Номенклатура.ЭтоГруппа КАК ЭтоГруппа,
		|   ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель КАК Родитель,
		|   ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование КАК Наименование
		|ИЗ
		|   РегистрНакопления.ОстаткиТоваровКомпании.Остатки(" + ?(обЗначениеНеЗаполнено(Дата),",","&Дата,")
			+ ?(СкладПустой,"","СкладКомпании = &Склад")+ ?(Не СкладПустой И Не ГруппаПустая," И ","")+ ?(ГруппаПустая, ")", "Номенклатура В ИЕРАРХИИ(&ГруппаНоменклатуры))") + " КАК ОстаткиТоваровКомпанииОстатки
		|ГДЕ НЕ ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование ЕСТЬ NULL
		|ИТОГИ ПО Ссылка ТОЛЬКО ИЕРАРХИЯ АВТОУПОРЯДОЧИВАНИЕ";
		
	ИначеЕсли СпособЗаполнения = 1 Тогда //по поставщику
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПартииТоваровКомпании.Номенклатура КАК Ссылка,
		|	ПартииТоваровКомпании.Номенклатура.Код КАК КодНоменклатуры,
		|	ПартииТоваровКомпании.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ПартииТоваровКомпании.Номенклатура.ЭтоГруппа КАК ЭтоГруппа,
		|	ПартииТоваровКомпании.Номенклатура.Родитель КАК Родитель,
		|	ПартииТоваровКомпании.Номенклатура.Наименование КАК Наименование
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|ГДЕ Не ПартииТоваровКомпании.Номенклатура.Наименование ЕСТЬ NULL И
		|	ПартииТоваровКомпании.ХозОперация=&ХозОперация " + ?(обЗначениеНеЗаполнено(Поставщик),"","И ПартииТоваровКомпании.Регистратор.Контрагент = &Контрагент ") 
		 + ?(ГруппаПустая,"","И ПартииТоваровКомпании.Номенклатура В ИЕРАРХИИ(&ГруппаНоменклатуры)")+"
      	|СГРУППИРОВАТЬ по Номенклатура		 
		|ИТОГИ ПО
        |	Ссылка ТОЛЬКО ИЕРАРХИЯ АВТОУПОРЯДОЧИВАНИЕ";
		
	ИначеЕсли СпособЗаполнения = 2 Тогда // по номенклатуре
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Код КАК КодНоменклатуры,
		|   ВидНоменклатуры КАК ВидНоменклатуры,
		|	ЭтоГруппа КАК ЭтоГруппа,
		|   Родитель КАК Родитель,
		|   Наименование КАК Наименование,
		|   Ссылка КАК Ссылка
		|ИЗ
		|   Справочник.Номенклатура" + ?(ГруппаПустая, "", " ГДЕ Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры) ") + "
		|УПОРЯДОЧИТЬ ПО Ссылка ИЕРАРХИЯ АВТОУПОРЯДОЧИВАНИЕ";
	ИначеЕсли СпособЗаполнения = 3 Тогда // по другому виду прайс-листа
		Если обЗначениеНеЗаполнено(ВидПрайсЛиста)Тогда
			Сообщить("Не задан исходный вид прайс-листа!");			
			Возврат;
		КонецЕсли;
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПрайсЛист.Код КАК КодНоменклатуры,
		|	ПрайсЛист.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ПрайсЛист.ЭтоГруппа,
		|	ПрайсЛист.Родитель,
		|	ПрайсЛист.Наименование,
		|	ПрайсЛист.Ссылка КАК ПрайсЛистСсылка, 
		|   ВЫБОР КОГДА ПрайсЛист.ЭтоГруппа
		|	ТОГДА
		|		ПрайсЛист.Ссылка 
		|	ИНАЧЕ
		|		ПрайсЛист.Номенклатура
		|	КОНЕЦ КАК Ссылка
		|ИЗ
		|	Справочник.ПрайсЛист КАК ПрайсЛист
		|ГДЕ
		|	ПрайсЛист.Владелец = &ВидПрайсЛиста";
		Если НЕ ГруппаПустая Тогда ТекстЗапроса = ТекстЗапроса + " И ПрайсЛист.Номенклатура В ИЕРАРХИИ(&ГруппаНоменклатуры) "; КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО ПрайсЛистСсылка ИЕРАРХИЯ
		|АВТОУПОРЯДОЧИВАНИЕ";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("Контрагент",Поставщик);
	Запрос.УстановитьПараметр("ВидПрайсЛиста",ВидПрайсЛиста);
	Запрос.УстановитьПараметр("ХозОперация",Справочники.ХозОперации.ПоступлениеТоваров);
	
	ТаблицаЗаполнения = Запрос.Выполнить().Выгрузить();
	
	СоответствиеГрупп = Новый Структура;
	Всего = ТаблицаЗаполнения.Количество();
	Счетчик = 0;
	
	//Если у владельца стоит флаг уникальности и не очищать прайс-лист, заполним соответствие
	
	Если ВладелецПрайсЛиста.ФлагУникальности И НЕ ФлажокОчищать И (СпособСозданияГрупп = 2 ИЛИ СпособСозданияГрупп = 1) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ПрайсЛист.Ссылка,
		|	ПрайсЛист.Наименование,
		|	ПрайсЛист.Номенклатура.Код КАК Код
		|ИЗ
		|	Справочник.ПрайсЛист КАК ПрайсЛист
		|ГДЕ
		|	ПрайсЛист.Владелец = &Владелец
		|	И ПрайсЛист.ЭтоГруппа = ИСТИНА");
		
		Запрос.УстановитьПараметр("Владелец", ВладелецПрайсЛиста);
		ВыборкаГруппПрайса = Запрос.Выполнить().Выбрать();
		Пока ВыборкаГруппПрайса.Следующий() Цикл
			Если СпособСозданияГрупп = 2 Тогда 
				ГруппаНоменклатуры = Справочники.Номенклатура.НайтиПоНаименованию(ВыборкаГруппПрайса.Наименование);
				Если ГруппаНоменклатуры.Пустая() Тогда Продолжить Иначе ИмяГруппыВыборки = ГруппаНоменклатуры.Код; КонецЕсли;
			Иначе
				ИмяГруппыВыборки = ВыборкаГруппПрайса.Наименование;
			КонецЕсли;
			ИмяГруппыВыборки = "К_" + СокрЛП(СтрЗаменить(ИмяГруппыВыборки," ","_"));
			Если  СоответствиеГрупп.Свойство(ИмяГруппыВыборки) Тогда Продолжить; 
			Иначе 
				СоответствиеГрупп.Вставить(ИмяГруппыВыборки,ВыборкаГруппПрайса.Ссылка);
			КонецЕсли
		КонецЦикла; 
	КонецЕсли;
	
	
	Для Каждого Стр Из ТаблицаЗаполнения Цикл
		
		//Уберем общий итог, если группа не задана
		Если Стр.Наименование = NULL Тогда Продолжить КонецЕсли;
		#Если Клиент Тогда
		Состояние("Заполнение: " + Цел(Счетчик / Всего * 100) + "%");
		#КонецЕсли
		Родитель = Неопределено;
		Если Не обЗначениеНеЗаполнено(ТекущийРодитель) Тогда
			Если Найти(Строка(ТекущийРодитель), "Объект не найден") = 0 Тогда
				Родитель = ТекущийРодитель;
			КонецЕсли;	
		КонецЕсли;
		
		Если СпособСозданияГрупп = 1 И Не Стр.ЭтоГруппа Тогда
			ИмяГруппы = СтрЗаменить(СокрЛП(Строка(Стр.ВидНоменклатуры))," ","_");
			Если ПустаяСтрока(ИмяГруппы) Тогда
				ИмяГруппы = "Неопределенный_вид";
			КонецЕсли;
			Группа = Неопределено;
			Если СоответствиеГрупп.Свойство("К_"+ИмяГруппы, Группа) Тогда
				Родитель = Группа;
			Иначе
				НоваяГруппа = Справочники.ПрайсЛист.СоздатьГруппу();
				НоваяГруппа.Владелец = ВладелецПрайсЛиста;
				НоваяГруппа.УстановитьНовыйКод();
				НоваяГруппа.Наименование = СтрЗаменить(ИмяГруппы,"_"," ");
				Если Не Родитель = Неопределено Тогда
					НоваяГруппа.Родитель = Родитель;
				КонецЕсли;
				НоваяГруппа.Записать();
				СоответствиеГрупп.Вставить("К_"+ИмяГруппы, НоваяГруппа.Ссылка);
				Родитель = НоваяГруппа.Ссылка;
			КонецЕсли;
		ИначеЕсли СпособСозданияГрупп = 2 И Стр.ЭтоГруппа Тогда
			Если СпособЗаполнения = 0 И Не обЗначениеНеЗаполнено(ГруппаНоменклатуры) Тогда
				Если ГруппаНоменклатуры.ПринадлежитЭлементу(Стр.Ссылка) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ИмяГруппы = СокрЛП(Стр.КодНоменклатуры);
			Группа = Неопределено;
			Если СоответствиеГрупп.Свойство("К_"+ИмяГруппы, Группа) Тогда
				Родитель = Группа;
			Иначе
				НоваяГруппа = Справочники.ПрайсЛист.СоздатьГруппу();
				НоваяГруппа.Владелец = ВладелецПрайсЛиста;
				НоваяГруппа.УстановитьНовыйКод();
				НоваяГруппа.Наименование = Стр.Наименование;
				Если Не обЗначениеНеЗаполнено(Стр.Родитель) Тогда
					Если СоответствиеГрупп.Свойство("К_" + СокрЛП(СтрЗаменить(Стр.Родитель.Код," ","_")), Группа) Тогда
						НоваяГруппа.Родитель = Группа;
					Иначе
						Если Не Родитель = Неопределено Тогда
							НоваяГруппа.Родитель = Родитель;
						КонецЕсли;
					КонецЕсли;
				Иначе
					Если Не Родитель = Неопределено Тогда
						НоваяГруппа.Родитель = Родитель;
					КонецЕсли;
				КонецЕсли;
				НоваяГруппа.Записать();
				СоответствиеГрупп.Вставить("К_"+ИмяГруппы, НоваяГруппа.Ссылка);
				Родитель = НоваяГруппа.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		Если Не Стр.ЭтоГруппа Тогда
			НовыйЭлемент = Справочники.ПрайсЛист.СоздатьЭлемент();
			НовыйЭлемент.Владелец = ВладелецПрайсЛиста;
			НовыйЭлемент.УстановитьНовыйКод();
			НовыйЭлемент.Наименование = Стр.Наименование;
			НовыйЭлемент.Номенклатура = Стр.Ссылка;
			
			Группа = Неопределено;
			Если (Не обЗначениеНеЗаполнено(Стр.Родитель)) И (Не СпособСозданияГрупп = 0) Тогда
				Если СпособСозданияГрупп = 1 Тогда
					Группа = СоответствиеГрупп["К_"+СтрЗаменить(СокрЛП(Строка(Стр.ВидНоменклатуры))," ","_")];
				ИначеЕсли СпособСозданияГрупп = 2 Тогда
					Если Не СоответствиеГрупп.Свойство("К_"+СокрЛП(Стр.Родитель.Код), Группа) Тогда
						Если Не Родитель = Неопределено Тогда
							Группа = Родитель;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если Не Родитель = Неопределено Тогда
					Группа = Родитель;
				КонецЕсли;
			КонецЕсли;
			Если Не Группа = Неопределено Тогда
				НовыйЭлемент.Родитель = Группа;
			КонецЕсли;
			
			Если ПроверкаЭлемента(НовыйЭлемент) Тогда
				НовыйЭлемент.Записать();
			КонецЕсли;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПрайсЛист()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	//ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
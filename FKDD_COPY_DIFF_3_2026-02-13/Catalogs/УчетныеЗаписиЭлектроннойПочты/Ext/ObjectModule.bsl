// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	  // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ЭтотОбъектНовый;    // Нужно создать группы писем только для новых объектов, но после записи объекта 
					      // в переменной хранится признак что учетная запись новая.
Перем Скопирован Экспорт; // Признак что элемент создан копированием						

Перем СтандартныйАдресRSS; // Адрес по умолчанию

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	
	Если ДляЭлемента И (НЕ ЭтоУчетнаяЗаписьНовостей)Тогда
		
		Если НЕ обЗначениеНеЗаполнено(SMTPСервер) Тогда			
			ОбязательныеРеквизиты.Вставить("ПортSMTP",1);  
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(POP3Сервер) Тогда
			ОбязательныеРеквизиты.Вставить("АдресЭлектроннойПочты",1);
			ОбязательныеРеквизиты.Вставить("POP3Сервер",1);
			ОбязательныеРеквизиты.Вставить("ПортPOP3",1);
			ОбязательныеРеквизиты.Вставить("Логин",1);
			ОбязательныеРеквизиты.Вставить("Пароль",1);              
			ОбязательныеРеквизиты.Вставить("ВремяОжиданияСервера",1);
		КонецЕсли; 		
	Иначе
		ОбязательныеРеквизиты.Вставить("АдресЭлектроннойПочты",1); 
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	// Обязательные поля таблицы Пользователи
	ОбязательныеПользователи = Новый Структура();
	ОбязательныеПользователи.Вставить("Пользователь",3);
	ОбязательныеРеквизиты.Вставить("Пользователи",ОбязательныеПользователи);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Если Пользователи.Итог("Администрирование") = 0 Тогда
		Сообщить("Для учетной записи необходимо наличие администратора!
				 |Установите флажок ""Администрирование"" хотя бы для одного пользователя.", СтатусСообщения.Важное);	
		Возврат Ложь;
	КонецЕсли;
	
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

// Процедура создает обязательные группы писем (только для новых учетных записей)
Процедура СоздатьПапкиУчетнойЗаписиПоУмолчанию(Объект = Неопределено) Экспорт
	Входящие     = Истина;
	Исходящие    = Истина;
	Отправленные = Истина;
	Удаленные    = Истина;
	
	Если Объект<>Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыПисемЭлектроннойПочты.Наименование
		|ИЗ
		|	Справочник.ГруппыПисемЭлектроннойПочты КАК ГруппыПисемЭлектроннойПочты
		|ГДЕ
		|	ГруппыПисемЭлектроннойПочты.Владелец = &Владелец
		|	И ГруппыПисемЭлектроннойПочты.Наименование В(&МассивНаименований)";
		МассивНаименований = Новый Массив;
		МассивНаименований.Добавить("Входящие");
		МассивНаименований.Добавить("Исходящие");
		МассивНаименований.Добавить("Отправленные");
		МассивНаименований.Добавить("Удаленные");
		
		Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
		Запрос.УстановитьПараметр("МассивНаименований", МассивНаименований);
		
		тзПапкиУчетнойЗаписи = Запрос.Выполнить().Выгрузить();
		Если тзПапкиУчетнойЗаписи.Найти("Входящие", "Наименование")<>Неопределено Тогда
			Входящие = Ложь;	
		КонецЕсли;
		Если тзПапкиУчетнойЗаписи.Найти("Исходящие", "Наименование")<>Неопределено Тогда
			Исходящие = Ложь;	
		КонецЕсли;
		Если тзПапкиУчетнойЗаписи.Найти("Отправленные", "Наименование")<>Неопределено Тогда
			Отправленные = Ложь;	
		КонецЕсли;
		Если тзПапкиУчетнойЗаписи.Найти("Удаленные", "Наименование")<>Неопределено Тогда
			Удаленные = Ложь;	
		КонецЕсли;				
	КонецЕсли;
	
	ЭтоНовости = ?(Объект=Неопределено, ЭтотОбъект.ЭтоУчетнаяЗаписьНовостей, Объект.ЭтоУчетнаяЗаписьНовостей);
	Если ЭтоНовости Тогда
		Отправленные = Ложь;
		Исходящие    = Ложь;
	КонецЕсли;
	
	Если Входящие Тогда 	
		ГруппыПисемУчетнойЗаписи = Справочники.ГруппыПисемЭлектроннойПочты;
		НоваяГруппа = ГруппыПисемУчетнойЗаписи.СоздатьЭлемент();
		НоваяГруппа.Владелец = ?(Объект=Неопределено, ЭтотОбъект.Ссылка, Объект.Ссылка); 		 
		НоваяГруппа.Наименование = "Входящие";
		НоваяГруппа.УстановитьНовыйКод();
		НоваяГруппа.Записать();
	КонецЕсли;
	Если Исходящие Тогда
		ГруппыПисемУчетнойЗаписи = Справочники.ГруппыПисемЭлектроннойПочты;
		НоваяГруппа = ГруппыПисемУчетнойЗаписи.СоздатьЭлемент();
		НоваяГруппа.Владелец     = ?(Объект=Неопределено, ЭтотОбъект.Ссылка, Объект.Ссылка);
		НоваяГруппа.Наименование = "Исходящие";
		НоваяГруппа.УстановитьНовыйКод();
		НоваяГруппа.Записать();
	КонецЕсли;
	Если Отправленные Тогда
		ГруппыПисемУчетнойЗаписи = Справочники.ГруппыПисемЭлектроннойПочты;
		НоваяГруппа = ГруппыПисемУчетнойЗаписи.СоздатьЭлемент();
		НоваяГруппа.Владелец     = ?(Объект=Неопределено, ЭтотОбъект.Ссылка, Объект.Ссылка);
		НоваяГруппа.Наименование = "Отправленные";
		НоваяГруппа.УстановитьНовыйКод();
		НоваяГруппа.Записать();
	КонецЕсли;
	Если Удаленные Тогда
		ГруппыПисемУчетнойЗаписи = Справочники.ГруппыПисемЭлектроннойПочты;
		НоваяГруппа = ГруппыПисемУчетнойЗаписи.СоздатьЭлемент();
		НоваяГруппа.Владелец     = ?(Объект=Неопределено, ЭтотОбъект.Ссылка, Объект.Ссылка);
		НоваяГруппа.Наименование = "Удаленные";
		НоваяГруппа.УстановитьНовыйКод();
		НоваяГруппа.Записать();
	КонецЕсли;
КонецПроцедуры // СоздатьПапкиУчетнойЗаписиПоУмолчанию()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	ПротоколВходящейПочты = "POP3";
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	Скопирован = Истина;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если ЭтотОбъект.ЭтоНовый() Тогда 
		ЭтотОбъектНовый = Истина;
	Иначе
		ЭтотОбъектНовый = Ложь;
	КонецЕсли;
	
	Если АвтоПолучениеОтправкаСообщений Тогда
		Если обЗначениеНеЗаполнено(ОтветственныйЗаАвтоПолучениеОтправкуСообщений) Тогда
			Сообщить("Не указан ответственный за автополучение/отправку писем.");
			Отказ = Истина;
		ИначеЕсли обЗначениеНеЗаполнено(ИнтервалАвтоПолученияОтправкиСообщений) Тогда
			Сообщить("Не указан интервал автополучения/отправки писем.");
			Отказ = Истина;
		Иначе
			СтрокаТЧ = Пользователи.Найти(ОтветственныйЗаАвтоПолучениеОтправкуСообщений, "Пользователь");
			Если СтрокаТЧ = Неопределено Тогда
				Отказ = Истина;
			Иначе
				Если НЕ СтрокаТЧ.Транспорт Тогда
					Отказ = Истина;
				КонецЕсли; 
			КонецЕсли; 
			Если Отказ Тогда
				Сообщить("У ответственного за автополучение/отправку нет прав на транспорт писем.");
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 

	// Очистим "лишние" реквизиты
	Если обЗначениеНеЗаполнено(SMTPСервер) Тогда
		ПортSMTP = 0;
		АутентификацияSMTP = Перечисления.СпособАутентификацииSMTP.БезАутентификации;
		ЛогинSMTP  = "";
		ПарольSMTP = "";
		POP3ПередSMTP = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(POP3Сервер) Тогда
		ПортPOP3 = 0;
		АутентификацияPOP3 = Перечисления.СпособАутентификацииPOP3.Обычная;
		Логин    = "";
		Пароль   = "";
	КонецЕсли;
	
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если ЭтотОбъектНовый И ЭтоГруппа = Ложь Тогда 
		СоздатьПапкиУчетнойЗаписиПоУмолчанию();		
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

Скопирован = Ложь;

// Заполняет предопределенные элементы(если они не существуют - создает новые(не предопределенные!))
// при обновлении релиза
// Флаг ЗаполнятьВсе=Истина - перезаполняет уже существующие элементы(строгое заполнение)
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	//предопределенный - "Предоплата"
	ЗаполнитьЭлемент(ЗаполнятьВсе,
					 "Предоплата", //Имя заполняемой номенклатуры
					 "00000001", //Код заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Услуга, //Тип заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Услуга.ВидНоменклатуры, //Вид заполняемой номенклатуры
					 Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("-",Истина).Ссылка, //Базовая единица измерения заполняемой номенклатуры
					 Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, //Ставка НДС заполняемой номенклатуры
					 0, //Вес единицы измерения
					 0); //Точность единицы измерения
	//предопределенный - "КомиссионноеВознаграждение"
	ЗаполнитьЭлемент(ЗаполнятьВсе,
					 "КомиссионноеВознаграждение", //Имя заполняемой номенклатуры
					 "00000002", //Код заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Услуга, //Тип заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Услуга.ВидНоменклатуры, //Вид заполняемой номенклатуры
					 Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("-",Истина).Ссылка, //Базовая единица измерения заполняемой номенклатуры
					 Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, //Ставка НДС заполняемой номенклатуры
					 0, //Вес единицы измерения
					 0); //Точность единицы измерения
	//предопределенный - "Авторабота"
	ЗаполнитьЭлемент(ЗаполнятьВсе,
					 "Авторабота", //Имя заполняемой номенклатуры
					 "00000003", //Код заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Услуга, //Тип заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Услуга.ВидНоменклатуры, //Вид заполняемой номенклатуры
					 Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("-",Истина).Ссылка, //Базовая единица измерения заполняемой номенклатуры
					 Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, //Ставка НДС заполняемой номенклатуры
					 0, //Вес единицы измерения
					 0); //Точность единицы измерения
	//предопределенный - "Опция производителя"
	ЗаполнитьЭлемент(ЗаполнятьВсе,
					 "ОпцияПроизводителя", //Имя заполняемой номенклатуры
					 "00000004", //Код заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Опции, //Тип заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Опции.ВидНоменклатуры, //Вид заполняемой номенклатуры
					 Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("шт",Истина).Ссылка, //Базовая единица измерения заполняемой номенклатуры
					 Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, //Ставка НДС заполняемой номенклатуры
					 0, //Вес единицы измерения
					 0); //Точность единицы измерения
	//предопределенный - "Опция производителя"
	ЗаполнитьЭлемент(ЗаполнятьВсе,
					 "Автомобиль", //Имя заполняемой номенклатуры
					 "00000005", //Код заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Автомобили, //Тип заполняемой номенклатуры
					 Справочники.ТипыНоменклатуры.Автомобили.ВидНоменклатуры, //Вид заполняемой номенклатуры
					 Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("шт",Истина).Ссылка, //Базовая единица измерения заполняемой номенклатуры
					 Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, //Ставка НДС заполняемой номенклатуры
					 0, //Вес единицы измерения
					 0); //Точность единицы измерения
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

// процедура заполнения элемента справочника
//
// Параметры:
//
// ЗаполнятьВсе 				-  Булево 				- Признак заполнения всех реквизитов элемента справочника
// НовоеНаименование 			- Строка				- Имя заполняемой номенклатуры
// НовыйКод 					- Строка 				- Код заполняемой номенклатуры
// НовыйТипНоменклатуры 		- СправочникСсылка 		- Тип заполняемой номенклатуры
// НовыйВидНоменклатуры 		- ПеречислениеСсылка 	- Вид заполняемой номенклатуры
// НоваяБазоваяЕдиницаИзмерения - СправочникСсылка 		- Базовая единица измерения заполняемой номенклатуры
// НоваяСтавкаНДС 				- СправочникСсылка		- Ставка НДС заполняемой номенклатуры
// ВесЕИ 						- Число 				- Вес единицы измерения
// ТочностьЕИ 					- Число					- Точность единицы измерения
//
Процедура ЗаполнитьЭлемент(ЗаполнятьВсе, НовоеНаименование, НовыйКод, НовыйТипНоменклатуры, НовыйВидНоменклатуры,
						   НоваяБазоваяЕдиницаИзмерения, НоваяСтавкаНДС, ВесЕИ, ТочностьЕИ)
						   
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	Если обЗначениеНеЗаполнено(ВалютаРегл) Тогда
		ВалютаРегл = Справочники.Валюты.НайтиПоКоду("643");
	КонецЕсли;
						   
	Попытка
		ЭлементНоменклатуры = Справочники.Номенклатура[НовоеНаименование];
	Исключение
		ЭлементНоменклатуры = Справочники.Номенклатура.ПустаяСсылка();
	КонецПопытки;
	Если ЭлементНоменклатуры.Пустая() Тогда
		ЭлементНоменклатуры = Справочники.Номенклатура.СоздатьЭлемент();
	Иначе
		ЭлементНоменклатуры = ЭлементНоменклатуры.ПолучитьОбъект();
	КонецЕсли;	
	Если ЗаполнятьВсе Тогда
		ЭлементНоменклатуры.Код = НовыйКод;
		ЭлементНоменклатуры.Наименование = НовоеНаименование;
		ЭлементНоменклатуры.НаименованиеИностранное = НовоеНаименование;
		ЭлементНоменклатуры.НаименованиеПолное = НовоеНаименование;
		ЭлементНоменклатуры.ТипНоменклатуры = НовыйТипНоменклатуры;
		ЭлементНоменклатуры.ВидНоменклатуры = НовыйВидНоменклатуры;
		ЭлементНоменклатуры.БазоваяЕдиницаИзмерения = НоваяБазоваяЕдиницаИзмерения;
		ЭлементНоменклатуры.СтавкаНДС = НоваяСтавкаНДС;
		ЭлементНоменклатуры.Артикул = НовоеНаименование;
		ЭлементНоменклатуры.ОбработкаРеквизита("Артикул");
		ЭлементНоменклатуры.ВалютаУчета = ВалютаРегл;
		Если ЭлементНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			ЭлементНоменклатуры.СпособРаспределенияДопРасходов=Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы;
			ЭлементНоменклатуры.СтатьяДопРасходов=Справочники.СтатьиДоходовИРасходов.ПрочиеЗатраты;
		КонецЕсли;
	Иначе
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.Код) Тогда
			ЭлементНоменклатуры.Код = НовыйКод;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.Наименование) Тогда
			ЭлементНоменклатуры.Наименование = НовоеНаименование;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.НаименованиеИностранное) Тогда
			ЭлементНоменклатуры.НаименованиеИностранное = НовоеНаименование;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.НаименованиеПолное) Тогда
			ЭлементНоменклатуры.НаименованиеПолное = НовоеНаименование;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.ТипНоменклатуры) Тогда
			ЭлементНоменклатуры.ТипНоменклатуры = НовыйТипНоменклатуры;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.ВидНоменклатуры) Тогда
			ЭлементНоменклатуры.ВидНоменклатуры = НовыйВидНоменклатуры;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.БазоваяЕдиницаИзмерения) Тогда
			ЭлементНоменклатуры.БазоваяЕдиницаИзмерения = НоваяБазоваяЕдиницаИзмерения;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.СтавкаНДС) Тогда
			ЭлементНоменклатуры.СтавкаНДС = НоваяСтавкаНДС;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.Артикул) Тогда
			ЭлементНоменклатуры.Артикул = НовоеНаименование;
			ЭлементНоменклатуры.ОбработкаРеквизита("Артикул");
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.ВалютаУчета) Тогда
			ЭлементНоменклатуры.ВалютаУчета = ВалютаРегл;
		КонецЕсли; 
		Если ЭлементНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.СпособРаспределенияДопРасходов) Тогда
				ЭлементНоменклатуры.СпособРаспределенияДопРасходов=Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы;
			КонецЕсли; 
		КонецЕсли;
		Если ЭлементНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга И
			 ЭлементНоменклатуры.СпособРаспределенияДопРасходов=Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы Тогда
			 Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.СтатьяДопРасходов) Тогда
				 ЭлементНоменклатуры.СтатьяДопРасходов=Справочники.СтатьиДоходовИРасходов.ПрочиеЗатраты;
			 КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	//сохранение элемента
	НовыйЭлемент = ЭлементНоменклатуры.ЭтоНовый();
	Если ЭлементНоменклатуры.Модифицированность() Тогда
		Попытка
			Если обЗначениеНеЗаполнено(ЭлементНоменклатуры.ОсновнаяЕдиницаИзмерения) Тогда
				ЭлементНоменклатуры.ОсновнаяЕдиницаИзмеренияОбязательная=Ложь;
			КонецЕсли;
			ЭлементНоменклатуры.Записать();
			Сообщить("Справочник ""Номенклатура"". "+?(НовыйЭлемент,"Добавлен","Обновлен")+" элемент """+СокрЛП(ЭлементНоменклатуры.Наименование)+"""");
		Исключение
		КонецПопытки;
	КонецЕсли;
	Если ЭлементНоменклатуры.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 И обЗначениеНеЗаполнено(ЭлементНоменклатуры.ОсновнаяЕдиницаИзмерения) Тогда
		ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиПоНаименованию(ЭлементНоменклатуры.БазоваяЕдиницаИзмерения.Наименование,Истина,,ЭлементНоменклатуры.Ссылка);
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
			ЕдиницаИзмерения.Владелец = ЭлементНоменклатуры.Ссылка;
			ЕдиницаИзмерения.УстановитьНовыйКод("");
			ЕдиницаИзмерения.Наименование = ЭлементНоменклатуры.БазоваяЕдиницаИзмерения.Наименование;
			ЕдиницаИзмерения.ЕдиницаПоКлассификатору = ЭлементНоменклатуры.БазоваяЕдиницаИзмерения;
			ЕдиницаИзмерения.Коэффициент=1;
			ЕдиницаИзмерения.Вес = ВесЕИ;
			ЕдиницаИзмерения.Точность = ТочностьЕИ;
			Попытка
				ЕдиницаИзмерения.Записать();
				Сообщить("Справочник ""Типы номенклатуры"" элемент """+СокрЛП(ЭлементНоменклатуры.ТипНоменклатуры.Наименование)+""" добавлена единица измерения """+ЭлементНоменклатуры.ЕдиницаИзмерения.Наименование+"""");
			Исключение
			КонецПопытки; 
		КонецЕсли; 
		ЭлементНоменклатуры.ОсновнаяЕдиницаИзмерения=ЕдиницаИзмерения.Ссылка;
		ЭлементНоменклатуры.ОсновнаяЕдиницаИзмеренияОбязательная=Истина;
		Попытка
			ЭлементНоменклатуры.Записать();
			Сообщить("Справочник ""Номенклатура"". Обновлена основная единица измерения элемента """+СокрЛП(ЭлементНоменклатуры.Наименование)+"""");
		Исключение
		КонецПопытки;
	КонецЕсли; 
КонецПроцедуры // ЗаполнитьЭлемент()

// Формирует структуру, которая содержит свойства шины
//
Функция СформироватьСтруктуруСвойствШины(Форма) Экспорт
	
	Если Форма=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("ШинаВысота",Форма.ШинаВысота);
	СтруктураСвойств.Вставить("ШинаШирина",Форма.ШинаШирина);
	СтруктураСвойств.Вставить("ШинаРадиус",Форма.ШинаРадиус);
	СтруктураСвойств.Вставить("ШинаПрофиль",Форма.ШинаПрофиль);
	СтруктураСвойств.Вставить("ШинаНазначение",Форма.ШинаНазначение);
	СтруктураСвойств.Вставить("ШинаКонструкция",Форма.ШинаКонструкция);
	СтруктураСвойств.Вставить("ШинаСезонность",Форма.ШинаСезонность);
	СтруктураСвойств.Вставить("ШинаИндексНагрузки",Форма.ШинаИндексНагрузки);
	СтруктураСвойств.Вставить("ШинаИндексСкорости",Форма.ШинаИндексСкорости);
	СтруктураСвойств.Вставить("ШинаСпособГерметизации",Форма.ШинаСпособГерметизации);
	Возврат СтруктураСвойств;
	
КонецФункции

// Формирует маркировку шины в формате в соовтветствии с нормативными документами
//
Функция СформироватьПредставлениеШины(СтруктураСвойств) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураСвойств) ИЛИ ТипЗнч(СтруктураСвойств) <> Тип("Структура") Тогда
		Возврат "";
	КонецЕсли;
	
	Представление =
		?(ЗначениеЗаполнено(СтруктураСвойств.ШинаПрофиль),СокрЛП(СтруктураСвойств.ШинаПрофиль) + " ","")+
		?(ЗначениеЗаполнено(СтруктураСвойств.ШинаНазначение),СокрЛП(СтруктураСвойств.ШинаНазначение) + " ","")+
		СокрЛП(СтруктураСвойств.ШинаШирина)+		
		?(ЗначениеЗаполнено(СтруктураСвойств.ШинаПрофиль) ИЛИ ЗначениеЗаполнено(СтруктураСвойств.ШинаВысота),"/","")+
		СокрЛП(СтруктураСвойств.ШинаВысота)+" "+
		?(ЗначениеЗаполнено(СтруктураСвойств.ШинаКонструкция),СокрЛП(СтруктураСвойств.ШинаКонструкция),"")+
		СокрЛП(СтруктураСвойств.ШинаРадиус)+" "+
		СокрЛП(СтруктураСвойств.ШинаИндексНагрузки)+""+
		СокрЛП(СтруктураСвойств.ШинаИндексСкорости)+" "+
		СокрЛП(СтруктураСвойств.ШинаСезонность)+" "+
		СокрЛП(СтруктураСвойств.ШинаСпособГерметизации);
	Возврат СокрЛП(Представление);
	
КонецФункции

// Формирует структуру, которая содержит свойства диска
//
Функция СформироватьСтруктуруСвойствДиска(Форма) Экспорт
	
	Если Форма=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("ДискМодель",Форма.ДискМодель);
	СтруктураСвойств.Вставить("ДискШирина",Форма.ДискШирина);
	СтруктураСвойств.Вставить("ДискРадиус",Форма.ДискРадиус);
	СтруктураСвойств.Вставить("ДискКоличествоОтверстий",Форма.ДискКоличествоОтверстий);
	СтруктураСвойств.Вставить("ДискРасстояниеМеждуОтверстиями",Форма.ДискРасстояниеМеждуОтверстиями);
	СтруктураСвойств.Вставить("ДискВылет",Форма.ДискВылет);
	СтруктураСвойств.Вставить("ДискДиаметрСтупицы",Форма.ДискДиаметрСтупицы);
	СтруктураСвойств.Вставить("ДискТип",Форма.ДискТип);
	Возврат СтруктураСвойств;
	
КонецФункции

// Формирует маркировку диска в формате в соовтветствии с нормативными документами
//
Функция СформироватьПредставлениеДиска(СтруктураСвойств) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураСвойств) ИЛИ ТипЗнч(СтруктураСвойств) <> Тип("Структура") Тогда
		Возврат "";
	КонецЕсли;
	
	Представление =
		?(ЗначениеЗаполнено(СтруктураСвойств.ДискМодель),СокрЛП(СтруктураСвойств.ДискМодель) + " ","")+
		СокрЛП(СтруктураСвойств.ДискШирина)+
		?(ЗначениеЗаполнено(СтруктураСвойств.ДискРадиус) И ЗначениеЗаполнено(СтруктураСвойств.ДискШирина),"x","")+
		СокрЛП(СтруктураСвойств.ДискРадиус)+" "+
		СокрЛП(СтруктураСвойств.ДискКоличествоОтверстий)+
		?(ЗначениеЗаполнено(СтруктураСвойств.ДискКоличествоОтверстий)И ЗначениеЗаполнено(СтруктураСвойств.ДискРасстояниеМеждуОтверстиями),"x","")+
		СокрЛП(СтруктураСвойств.ДискРасстояниеМеждуОтверстиями)+" "+
		?(ЗначениеЗаполнено(СтруктураСвойств.ДискВылет),"ET","")+ 
		СокрЛП(СтруктураСвойств.ДискВылет)+" "+
		?(ЗначениеЗаполнено(СтруктураСвойств.ДискДиаметрСтупицы),"d","")+ 
		СокрЛП(СтруктураСвойств.ДискДиаметрСтупицы);
	Возврат СокрЛП(Представление);
	
КонецФункции

// Функция получения ячейки хранения
//
// Параметры:
//	Номенклатура - номенклатура для которой определяется ячейка
//	Склад - склад для которого определяется ячейка
//
// Возвращает ссылку на ячейку если такая нашлась или пусиую ссылку
//
Функция ПолучитьЯчейкуХранения(Номенклатура, Склад) Экспорт
	Если (НЕ ЗначениеЗаполнено(Номенклатура)) ИЛИ (Номенклатура.ЭтоГруппа) ИЛИ (НЕ ЗначениеЗаполнено(Склад)) ИЛИ (Склад.ЭтоГруппа) Тогда
		Возврат Справочники.ЯчейкиХранения.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Номенклатура,СкладКомпании",Номенклатура,Склад);
	ЯчейкиПоУмолчанию=РегистрыСведений.ЯчейкиХраненияПоУмолчанию.Получить(СтруктураПоиска);
	Возврат ЯчейкиПоУмолчанию.ЯчейкаХранения;
КонецФункции

// функция - НайтиНоменклатуру производит поиск номенклатуры по переданным параметрам
//
// Параметры:
//  АртикулДляПоиска	 - Строка		 - Артикул, значение которого будет использовано для поиска
//  Производитель		 - СправочникСсылка.Производители	 - Ссылка на производителя по которому будет осуществляться поиск
//  Наименование		 - Строка				 - НаименованиеНоменклатуры по которому будет осуществляться поиск
//  ИсключаемаяСсылка  	 – СправочникСсылка.Номенклатура  – Ссылка на номенклатуру, которую надо исключить из выборки
//  Родитель  	         – СправочникСсылка.Номенклатура  – Ссылка на группу, в которой производить поиск
//  ВернутьВсю  	     – Булево              – Если передается "Истина", то вернется весь массив найденной номенклатуры
//
// Возвращаемое значение:
// 	СправочникСсылка.Номенклатура - Ссылка на найденную номенклатуру
//  Массив - Массив ссылок на всю найденную номенклатуру
//
Функция НайтиНоменклатуру(АртикулДляПоиска, Производитель = Неопределено, Наименование = Неопределено, ИсключаемаяСсылка = Неопределено, Родитель = Неопределено, ВернутьВсю = Ложь)Экспорт 
	
	Если обЗначениеНеЗаполнено(АртикулДляПоиска) И обЗначениеНеЗаполнено(Наименование) Тогда
		Если ВернутьВсю Тогда
			Возврат Новый Массив;
		Иначе
			Возврат Справочники.Номенклатура.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.АртикулДляПоиска = &АртикулДляПоиска
		|	И Номенклатура.Производитель = &Производитель
		|	И НЕ Номенклатура.Ссылка = &ИсключаемаяСсылка
		|	И Номенклатура.Родитель = &Родитель
		|	И Номенклатура.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	
	Если Производитель = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Номенклатура.Производитель = &Производитель","");
	Иначе	
		Запрос.УстановитьПараметр("Производитель" , Производитель);
	КонецЕсли;
	Если ИсключаемаяСсылка <> Неопределено Тогда
		Запрос.УстановитьПараметр("ИсключаемаяСсылка", ИсключаемаяСсылка);
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И НЕ Номенклатура.Ссылка = &ИсключаемаяСсылка","");
	КонецЕсли;
	Если Родитель <> Неопределено Тогда
		Запрос.УстановитьПараметр("Родитель", Родитель);
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Номенклатура.Родитель = &Родитель","");
	КонецЕсли;
	Если Наименование <> Неопределено Тогда
		Запрос.УстановитьПараметр("Наименование", Наименование);
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Номенклатура.Наименование = &Наименование","");
	КонецЕсли;
	Если ВернутьВсю Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ПЕРВЫЕ 1","");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если НЕ ВернутьВсю Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		Иначе
			Возврат Справочники.Номенклатура.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
КонецФункции // НайтиНоменклатуру()	

// Функция преобразует артикул в артикул для поиска
//
// Параметры:
//	Артикул               - Строка - Артикул, значение которого будет преобразовано
//	ДопустимыРусскиеБуквы - Булево - Истина - Русские буквы допустимы в артикуле для поиска. Ложь - Русские буквы не допустимы в артикуле для поиска
//
// Возвращаемое значение:
//	Строка - Артикул для поиска
//
Функция ПреобразоватьВАртикулДляПоиска(Знач Артикул, Знач Производитель=Неопределено) Экспорт
	Артикул=СокрЛП(ВРег(Артикул));
	// Отключим преобразование артикула исключительно при наличии производителя
	ДопустимыеСимволыАртикула="0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	ДопустимыеСимволыАртикула = ДопустимыеСимволыАртикула + "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЬЫЪЭЮЯ";
	Возврат обСтрокаПоМаске(Артикул, ДопустимыеСимволыАртикула, "");
КонецФункции // ПреобразоватьВАртикулДляПоиска()

// Функция возвращает массив производителей, связанных с передаваемым артикулом
//
// Параметры:
//	АртикулДляПоиска - Строка - Артикул, производители для которого будут искаться
//
// Возвращаемое значение:
//	Массив - Массив найденных производителей
//
Функция НайтиПроизводителейПоАртикулу(Знач Артикул) Экспорт
	СписокАртикулов = Новый СписокЗначений;
	СписокАртикулов.Добавить(Артикул);
	АртикулДляПоиска = ПреобразоватьВАртикулДляПоиска(Артикул);
	Если АртикулДляПоиска <> Артикул Тогда
		СписокАртикулов.Добавить(АртикулДляПоиска);
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Номенклатура.Производитель
		|ПОМЕСТИТЬ ТабПроизводителей
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.АртикулДляПоиска В(&СписокАртикулов)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыАналогов.Производитель
		|ИЗ
		|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|ГДЕ
		|	ГруппыАналогов.АртикулДляПоиска В(&СписокАртикулов)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрайсЛистыКонтрагентов.Производитель
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.АртикулДляПоиска В(&СписокАртикулов)
		|	И (ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте = """"
		|			ИЛИ ПрайсЛистыКонтрагентов.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрайсЛистыКонтрагентов.Производитель
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК ПрайсЛистыКонтрагентов
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.АртикулДляПоиска В(&СписокАртикулов)
		|	И (ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте = """"
		|			ИЛИ ПрайсЛистыКонтрагентов.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабПроизводителей.Производитель
		|ИЗ
		|	ТабПроизводителей КАК ТабПроизводителей
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТабПроизводителей.Производитель.Наименование";
		
	Запрос.УстановитьПараметр("СписокАртикулов", СписокАртикулов);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Производитель");
	
КонецФункции //НайтиПроизводителейПоАртикулу()

// Функция ищет производителя по Наименованию элемента и по ТЧ Синонимы
//
// Параметры:
//  Наименование - Строка	 - Искомое наименование производителя
// Возвращаемое значение:
//   - СправочникСсылка.Производители
Функция НайтиПроизводителяПоНаименованию(Знач Наименование) Экспорт
	НайденныйПроизводитель = Справочники.Производители.НайтиПоНаименованию(Наименование, Истина);
	Если НайденныйПроизводитель.Пустая() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Синонимы.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Производители.Синонимы КАК Синонимы
			|ГДЕ
			|	Синонимы.Синоним = &Наименование
			|	И НЕ Синонимы.Ошибка";
		Запрос.УстановитьПараметр("Наименование", Наименование);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	Возврат НайденныйПроизводитель;
КонецФункции

// Функция получения веса номенклатуры
//
// Параметры:
//	Номенклатура - номенклатура, для которой определяется вес
//	ЕдиницаИзмерения - единица измерения, для формирвоания цены
//
// Возвращает вес номенклатуры либо из номенклатуры или из основаной единицы измерения
//
Функция ПолучитьВесНоменклатуры(Номенклатура, ЕдиницаИзмерения) Экспорт
	
	Результат = 0;
	
	Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
		Результат = Номенклатура.Вес;
	ИначеЕсли Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 Тогда
		Результат = ЕдиницаИзмерения.Вес;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьВесНоменклатуры()


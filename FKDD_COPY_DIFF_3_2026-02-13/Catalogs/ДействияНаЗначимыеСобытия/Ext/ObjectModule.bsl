
Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


//////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА 
//////////////////////////////////////////////////////////////////////////////////

// Очистка реквизитов, относящихся к созданию произвольного объекта.
Процедура ОчиститьРеквизитыСоздаваемогоОбъекта() 	
	ТипОбъекта                    = "";
	ПредставлениеТипаОбъекта      = "";
	ПровестиДокумент              = Ложь;
	ЗадействоватьВводНаОсновании  = Ложь;
	ЗаполнитьРеквизитыПоУмолчанию = Ложь;
	Соответствия.Очистить(); 	
КонецПроцедуры // ОчиститьРеквизитыСоздаваемогоОбъекта() 

// Очистка реквизитов, относящихся к созданию напоминания.
Процедура ОчиститьРеквизитыНапоминания()
	ДатаНапоминания                  = Неопределено;
	ДатаНапоминанияПутьКДанным       = "";
	ДатаНапоминанияПроизвольныйКод   = "";
	ТемаНапоминания                  = Неопределено;
	ТемаНапоминанияПутьКДанным       = "";
	ТемаНапоминанияПроизвольныйКод   = "";
	ПрикрепленныйОбъект              = Неопределено;
	ПрикрепленныйОбъектПутьКДанным   = "";
	ПрикрепленныйОбъектПроизвольныйКод = "";
	Содержание                       = "";
	СодержаниеПроизвольныйКод        = "";
	ДатаАктуальности                 = Дата("00010101");
	СрокДоНачала                     = 0;
	ТипПериода                       = "";
	УдалитьПоИстеченииСрока          = Ложь;
	ПолучателиНапоминания.Очистить();		
КонецПроцедуры // ОчиститьРеквизитыНапоминания() 

// Очистка реквизитов, относящихся к созданию записи в журнале регистрации.
Процедура ОчиститьРеквизитыЗаписиЖурналаРегистрации()
	жрСобытие                         = "";
	жрСобытиеПроизвольныйКод          = "";
	жрУровеньВажности                 = "";
	жрОбъектМетаданных                = "";
	жрОбъектМетаданныхИмяОбъекта      = "";
	жрОбъектМетаданныхПроизвольныйКод = "";
	жрДанные                          = "";
	жрДанныеПутьКДанным               = "";
	жрДанныеПроизвольныйКод           = "";
	жрКомментарий                     = "";	
	жрКомментарийПроизвольныйКод      = "";
КонецПроцедуры // ОчиститьРеквизитыЗаписиЖурналаРегистрации() 

// Очистка реквизитов, относящихся к созданию электронного письма.
Процедура ОчиститьРеквизитыЭлектронногоПисьма()
	эпАвтор	                       = Неопределено;
	эпАвторПутьКДанным             = "";
	эпАвторПроизвольныйКод         = "";
	эпУчетнаяЗапись                = Неопределено;
	эпУчетнаяЗаписьПутьКДанным     = "";
	эпУчетнаяЗаписьПроизвольныйКод = "";
	эпТема                         = "";
	эпТемаПроизвольныйКод          = "";
	эпВажность                     = Неопределено;
	эпВажностьПроизвольныйКод      = "";
	эпКомментарий                  = "";
	эпКомментарийПроизвольныйКод   = "";
	эпФорматТекста                 = Перечисления.ФорматТекста.ПростойТекст;
	эпТекстПисьма                  = "";
	эпТекстПисьмаПроизвольныйКод   = "";
	эпВариантЗаписи                = Перечисления.ВариантЗаписиЭлектронногоПисьма.ПоместитьВПапкуИсходящие;
КонецПроцедуры // ОчиститьРеквизитыЭлектронногоПисьма() 

// Обработка изменения реквизитов справочников
// Параметры
//  Имя			- Строка			- Имя реквизита справочника с полным путем.
//  ЭтаФорма	- Форма				- Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	Если Имя="ВидДействия" Тогда
		Если ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьНапоминание Тогда
			ОчиститьРеквизитыСоздаваемогоОбъекта();				
			ОчиститьРеквизитыЗаписиЖурналаРегистрации();
			ОчиститьРеквизитыЭлектронногоПисьма();
			
		ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьИЗаписатьОбъект Тогда
			ОчиститьРеквизитыНапоминания();
			ОчиститьРеквизитыЗаписиЖурналаРегистрации();
			ОчиститьРеквизитыЭлектронногоПисьма();
			
		ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьЗаписьЖурналаРегистрации Тогда
			ОчиститьРеквизитыСоздаваемогоОбъекта();
			ОчиститьРеквизитыНапоминания();
			ОчиститьРеквизитыЭлектронногоПисьма();
			
		ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.НаписатьЭлектронноеПисьмо Тогда	
			ОчиститьРеквизитыСоздаваемогоОбъекта();
			ОчиститьРеквизитыНапоминания();
	        ОчиститьРеквизитыЗаписиЖурналаРегистрации();
		КонецЕсли;
		
	ИначеЕсли Имя="ТипОбъекта" Тогда
		Соответствия.Очистить();
		
	ИначеЕсли Имя="Соответствия.ВидПравила" Тогда
		Если ТекСтрока<>Неопределено Тогда
			ТекСтрока.Правило = "";			
		КонецЕсли;
	Иначе	
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",          1);
	ОбязательныеРеквизиты.Вставить("Наименование", 1);
	ОбязательныеРеквизиты.Вставить("ВидДействия",  1);
			
	Если ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьИЗаписатьОбъект Тогда
		// Обязательные поля таблицы
		ОбязательнаяТабличнаяЧасть = Новый Структура();
		ОбязательнаяТабличнаяЧасть.Вставить("РеквизитОбъекта",3); 	
		ОбязательныеРеквизиты.Вставить("Соответствия",  ОбязательнаяТабличнаяЧасть);
		
	ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьНапоминание Тогда
		ОбязательныеРеквизиты.Вставить("ДатаНапоминания", 1);
		ОбязательныеРеквизиты.Вставить("ТемаНапоминания", 1); 
		
	ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьЗаписьЖурналаРегистрации Тогда
		ОбязательныеРеквизиты.Вставить("жрСобытие", 1);	
		
	ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.НаписатьЭлектронноеПисьмо Тогда
		ОбязательныеРеквизиты.Вставить("эпАвтор", 1);
		ОбязательныеРеквизиты.Вставить("эпТема",  1);
		ОбязательныеРеквизиты.Вставить("эпУчетнаяЗапись", 1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	// Значение реквизита "Порядок" должно быть уникальным в пределах текущего владельца
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДействияНаЗначимыеСобытия.Ссылка,
	|	"""" + ДействияНаЗначимыеСобытия.Наименование + "" ("" + ДействияНаЗначимыеСобытия.Код + "")"" КАК Представление
	|ИЗ
	|	Справочник.ДействияНаЗначимыеСобытия КАК ДействияНаЗначимыеСобытия
	|ГДЕ
	|	ДействияНаЗначимыеСобытия.Владелец = &Владелец
	|	И ДействияНаЗначимыеСобытия.Порядок = &Порядок
	|	И ДействияНаЗначимыеСобытия.Ссылка <> &Ссылка";
	Запрос  = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Порядок",  Порядок);
	Запрос.УстановитьПараметр("Ссылка",   Ссылка);
	
	мсвСовпадения = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвСовпадения.Добавить(" - " + Выборка.Представление);		
	КонецЦикла;
	
	Результат = Истина;
	
	Если мсвСовпадения.Количество()<>0 Тогда
		Ошибки = "Значение реквизита ""Порядок"" не уникально в пределах текущего владельца. Найдены элементы с таким же значением порядка:";
		Для Каждого ТекСовпадение Из мсвСовпадения Цикл
			Ошибки = Ошибки + Символы.ПС + ТекСовпадение;		
		КонецЦикла;
		Результат = Ложь;
	КонецЕсли;
	
	Если ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьНапоминание Тогда
		Если ПолучателиНапоминания.Количество()=0 Тогда
			Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки), "", Символы.ПС) + "Не выбран ни один получатель напоминания!";
			Результат = Ложь; 	
		КонецЕсли;
		
	ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.НаписатьЭлектронноеПисьмо Тогда
		Если ПолучателиПисьма.Количество()=0 Тогда
			Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки), "", Символы.ПС) + "Не выбран ни один получатель электронного письма!";
			Результат = Ложь; 	
		КонецЕсли;
		
	ИначеЕсли ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьИЗаписатьОбъект Тогда
		// Проверим, а все ли обязательные реквизиты присутствуют в табличной части правил.
		// Проверку будем осуществлять, только если не задействован механизм ввода на основании.
		Если (Не ЗадействоватьВводНаОсновании) И (Не ЗаполнитьРеквизитыПоУмолчанию) Тогда
			// Пытаемся получить обязательные реквизиты.
			Попытка
				ОбъектМетаданных = "";
				КлассОбъекта = сбСобытия.ПолучитьКлассОбъектаМетаданных(ТипОбъекта, ОбъектМетаданных);
				Если КлассОбъекта="Справочники" Или 
					КлассОбъекта="Документы" Или
					КлассОбъекта="Справочники" Или
					КлассОбъекта="ПланыВидовХарактеристик" Или
					КлассОбъекта="ПланыСчетов" Или 					
					КлассОбъекта="ПланыВидовРасчета" Или
					КлассОбъекта="БизнесПроцессы" Или
					КлассОбъекта="Задачи" Или 
					КлассОбъекта="ПланыОбмена" Тогда
					
					ПустойСоздаваемыйОбъект         = сбСобытия.СоздатьПустоеЗначениеПоОбъектуМетаданных(ТипОбъекта);
					СтруктураОбязательныхРеквизитов = ПустойСоздаваемыйОбъект.ПолучитьОбязательныеРеквизиты();
					
					//Проходимся по обязательным реквизитам.
					Для Каждого ТекРеквизит Из СтруктураОбязательныхРеквизитов Цикл  	
						Если ТекРеквизит.Значение<>1 Тогда
							Продолжить;
						КонецЕсли;
						
						ИмяРеквизита = ТекРеквизит.Ключ;
						
						// Осуществляем поиск этого реквизита в табличной части.
						СтрокаТабличноЧасти = Соответствия.Найти("Ссылка."+ИмяРеквизита, "РеквизитОбъекта");
						Если СтрокаТабличноЧасти=Неопределено Тогда
							дкДобавитьОшибку(Ошибки, "Отсутствует правило заполнения обязательного реквизита " +  
							"<" + ИмяРеквизита + ">");
							Результат = Ложь;
						ИначеЕсли ПустаяСтрока(СтрокаТабличноЧасти.ПутьКДанным) И 
							      ПустаяСтрока(СтрокаТабличноЧасти.ПроизвольныйКод) И 
								  (Не ЗначениеЗаполнено(СтрокаТабличноЧасти.Правило)) Тогда
							
							дкДобавитьОшибку(Ошибки, "Не сформировано правило заполнения обязательного реквизита " +  
							"<" + ИмяРеквизита + ">");
							Результат = Ложь;		
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли КлассОбъекта="РегистрыСведений" Тогда
					
					// Для наборов записей проверяем реквизиты на обязательность заполнения по метаданным.
					Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл  	
						Если ТекИзмерение.ЗапрещатьНезаполненныеЗначения Тогда
							ИмяРеквизита = ТекИзмерение.Имя;
							
							// Осуществляем поиск этого реквизита в табличной части.
							СтрокаТабличноЧасти = Соответствия.Найти(ИмяРеквизита, "РеквизитОбъекта");
							Если СтрокаТабличноЧасти=Неопределено Тогда
								дкДобавитьОшибку(Ошибки, "Отсутствует правило заполнение обязательного реквизита " +  
								"<" + ИмяРеквизита + ">");
								Результат = Ложь;
							ИначеЕсли ПустаяСтрока(СтрокаТабличноЧасти.ПутьКДанным) И 
								ПустаяСтрока(СтрокаТабличноЧасти.ПроизвольныйКод) И 
								(Не ЗначениеЗаполнено(СтрокаТабличноЧасти.Правило)) Тогда
								дкДобавитьОшибку(Ошибки, "Не сформировано правило заполнения обязательного реквизита " +  
								"<" + ИмяРеквизита + ">");
								Результат = Ложь;
							КонецЕсли;							
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Исключение				
			КонецПопытки;
		КонецЕсли;		
	КонецЕсли;	
	
	Результат = Результат И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Возврат Результат;	
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции



//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА 
//////////////////////////////////////////////////////////////////////////////////

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


//////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ
//////////////////////////////////////////////////////////////////////////////////

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли///////////////////////////////////////////////////////////////////////
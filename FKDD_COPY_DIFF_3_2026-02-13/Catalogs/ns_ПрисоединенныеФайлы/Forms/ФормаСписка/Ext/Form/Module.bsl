&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Отбор.Свойство("ВладелецФайла", Владелец);
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	НачатьПомещениеФайла(Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтаФорма),,, Истина, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ВладелецФайла", Владелец);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Адрес", Адрес);
		ПараметрыФормы.Вставить("ВыбранноеИмяФайла", ВыбранноеИмяФайла);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("Справочник.ns_ПрисоединенныеФайлы.ФормаОбъекта", ПараметрыФормы, ЭтаФорма); 
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл") Тогда
		ДобавитьФотоВБазу(ПараметрыПеретаскивания.Значение);
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для каждого Строка Из ПараметрыПеретаскивания.Значение Цикл
			ДобавитьФотоВБазу(Строка);	
		КонецЦикла; 	
	КонецЕсли;
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФотоВБазу(ДанныеПеретаскивания)
	
	НоваяКартинка = Новый ДвоичныеДанные(ДанныеПеретаскивания.ПолноеИмя);
	Адрес = ПоместитьВоВременноеХранилище(НоваяКартинка, УникальныйИдентификатор);
	
	Данные = Новый Структура;
	Данные.Вставить("Владелец", Владелец);
	Данные.Вставить("Наименование", ДанныеПеретаскивания.Имя);
	Данные.Вставить("Расширение", НРег(ДанныеПеретаскивания.Расширение));
	Данные.Вставить("АдресФайла", Адрес);
	
 	ЗаписатьФотоВБазу(Данные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьФотоВБазу(Данные)
	Справочники.ns_ПрисоединенныеФайлы.ЗаписатьФотоВБазу(Данные);
КонецПроцедуры

&НаКлиенте
Процедура СдвинутьВверх(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		СдвинутьВверхНаСервере(ТекущиеДанные);	
	КонецЕсли;
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура СдвинутьВниз(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		СдвинутьВнизНаСервере(ТекущиеДанные);	
	КонецЕсли; 
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура СдвинутьВверхНаСервере(ТекущиеДанные)
	// Выше сдвигать не можем
	Если ТекущиеДанные.ПорядокФайлов = 1 Тогда
		Возврат;
	КонецЕсли; 
	
	ПорядокТекущегоФайла = ТекущиеДанные.ПорядокФайлов;
	ПорядокВерхнегоФайлаСсылка = Справочники.ns_ПрисоединенныеФайлы.ПолучитьСсылкуПоНомеруПорядка(ТекущиеДанные.ВладелецФайла, ПорядокТекущегоФайла - 1);
	
	Попытка
		НачатьТранзакцию();
		
		Если ПорядокВерхнегоФайлаСсылка = Неопределено И ПорядокТекущегоФайла > 1 Тогда
			СдвинутьПорядок(ТекущиеДанные.Ссылка, ПорядокТекущегоФайла, -1);
		Иначе
			СдвинутьПорядок(ТекущиеДанные.Ссылка, ПорядокТекущегоФайла, -1);
			СдвинутьПорядок(ПорядокВерхнегоФайлаСсылка, ПорядокВерхнегоФайлаСсылка.ПорядокФайлов, 1);
		КонецЕсли; 
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
		Сообщить(ОписаниеОшибки());
	КонецПопытки; 
КонецПроцедуры

&НаСервере
Процедура СдвинутьПорядок(Ссылка, Порядок, Значение)
	ТекущийОбъект = Ссылка.ПолучитьОбъект();
	ТекущийОбъект.ПорядокФайлов = Порядок + Значение;
	ТекущийОбъект.Записать();
КонецПроцедуры
 
&НаСервере
Процедура СдвинутьВнизНаСервере(ТекущиеДанные)

	ПорядокТекущегоФайла = ТекущиеДанные.ПорядокФайлов;
	ПоследнийПорядок = Справочники.ns_ПрисоединенныеФайлы.ПолучитьПоследнийПорядокФайла(ТекущиеДанные.ВладелецФайла);
	Если ПорядокТекущегоФайла = ПоследнийПорядок Тогда
		Возврат;	
	КонецЕсли; 
	
	ПорядокНижнегоФайлаСсылка = Справочники.ns_ПрисоединенныеФайлы.ПолучитьСсылкуПоНомеруПорядка(ТекущиеДанные.ВладелецФайла, ПорядокТекущегоФайла + 1);
	
	Попытка
		НачатьТранзакцию();
		
		// Сдвигаем только если снизу есть файл
		Если НЕ ПорядокНижнегоФайлаСсылка = Неопределено Тогда
			СдвинутьПорядок(ТекущиеДанные.Ссылка, ПорядокТекущегоФайла, 1);
			СдвинутьПорядок(ПорядокНижнегоФайлаСсылка, ПорядокНижнегоФайлаСсылка.ПорядокФайлов, -1);
		КонецЕсли; 
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
		Сообщить(ОписаниеОшибки());
	КонецПопытки; 
	
КонецПроцедуры
 






















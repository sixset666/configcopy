&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И Параметры.Свойство("Адрес") Тогда
		ЗаполнитьНаСервере(АдресИзображения, Объект.Наименование, Объект.Расширение, Параметры.Адрес, Параметры.ВыбранноеИмяФайла); 
	КонецЕсли;
	
	Если Объект.ПорядокФайлов = 0 Тогда
		ПоследнийПорядок = Справочники.ns_ПрисоединенныеФайлы.ПолучитьПоследнийПорядокФайла(Объект.ВладелецФайла);
		Объект.ПорядокФайлов = ПоследнийПорядок + 1; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьНаСервере(АдресИзображения, Наименование, Расширение, Адрес, ВыбранноеИмяФайла)
	
	Если ЭтоАдресВременногоХранилища(АдресИзображения) Тогда
		УдалитьИзВременногоХранилища(АдресИзображения);	
	КонецЕсли; 
	
	Файл = Новый Файл(ВыбранноеИмяФайла);	
	Наименование = Файл.Имя;
	Расширение = НРег(Файл.Расширение);
	
	Если ns_ОбщегоНазначения.ЭтоГрафическийФайл(Расширение) Тогда
		АдресИзображения = Адрес;
	КонецЕсли; 
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Наименование) Тогда
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Модифицированность И ЭтоАдресВременногоХранилища(АдресИзображения) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("АдресФайла", АдресИзображения);	
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	АдресИзображения = ПолучитьНавигационнуюСсылкуФайла();
КонецПроцедуры

&НаСервере
Функция ПолучитьНавигационнуюСсылкуФайла()
	
	Если ns_ОбщегоНазначения.ЭтоГрафическийФайл(Объект.Расширение) Тогда
		ЗначениеКлюча = Новый Структура;
		ЗначениеКлюча.Вставить("ПрисоединенныйФайл", Объект.Ссылка);
		КлючЗаписи = РегистрыСведений.ns_ПрисоединенныеФайлы.СоздатьКлючЗаписи(ЗначениеКлюча);
		Возврат ПолучитьНавигационнуюСсылку(КлючЗаписи, "Хранилище");
	КонецЕсли; 
	
	Возврат "";
КонецФункции

&НаКлиенте
Процедура ВыбратьДругойФайл(Команда)
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ВыбратьДругойФайлЗавершение", ЭтаФорма),,, Истина, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДругойФайлЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ЗаполнитьНаСервере(АдресИзображения, Объект.Наименование, Объект.Расширение, Адрес, ВыбранноеИмяФайла);
		Модифицированность = Истина;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Команда)
	Если ЗначениеЗаполнено(АдресИзображения) Тогда
		ПолучитьФайл(АдресИзображения, СокрЛП(Объект.Наименование) + Объект.Расширение, Истина);
	КонецЕсли;
КонецПроцедуры


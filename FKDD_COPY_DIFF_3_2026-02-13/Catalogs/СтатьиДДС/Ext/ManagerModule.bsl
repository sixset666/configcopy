//Процедура заполнения предопределённых элементов справочника
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	Макет = ПолучитьМакет("НастройкиПоУмолчанию");
	Если Макет = Неопределено Тогда
		Сообщить("Не обнаружен макет начальных настроек!");
		Возврат;
	КонецЕсли;
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		// Считаем из макета Имя, Наименование, Код, ВидДвижения, КоррСчет
		СтрИмя = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрНаименование = СокрЛП(Макет.Область(НомерСтроки,2).Текст);
		СтрКод = СокрЛП(Макет.Область(НомерСтроки,3).Текст);
		СтрВидДвижения = СокрЛП(Макет.Область(НомерСтроки,4).Текст);
		СтрКоррСчет = СокрЛП(Макет.Область(НомерСтроки,5).Текст);
		//будем искать с таким же наименованием
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СтатьиДДС.Ссылка КАК СтатьяДДС
		|ИЗ
		|	Справочник.СтатьиДДС КАК СтатьиДДС
		|ГДЕ
		|	СтатьиДДС.Наименование = &Наименование И СтатьиДДС.ЭтоГруппа = Ложь И СтатьиДДС.Предопределенный = Ложь 
		|");
		Запрос.УстановитьПараметр("Наименование",СтрНаименование);
		Выборка = Запрос.Выполнить().Выбрать();
		//берем предопределенный элемент
		ПредопределеннаяСтатья = Справочники.СтатьиДДС[СтрИмя];
		Если ПредопределеннаяСтатья.Пустая() Тогда
			Сообщить("Ошибка при заполнении справочника: '" + СтрНаименование + "'. Указан отсутствующий элемент справочника СтатьиДДС!");
			Продолжить;
		КонецЕсли;
		ПредопределеннаяСтатьяОбъект = ПредопределеннаяСтатья.ПолучитьОбъект();
		Номер = 1;
		//если с таким именем есть, будем заменять
		Пока Выборка.Следующий() Цикл
			//используем стандартную обработку
			ФормаЗаменаЗначений = Обработки.ПоискИЗаменаЗначений.ПолучитьФорму();
			ФормаЗаменаЗначений.Программно = Истина;
			СтрокаЗамены = ФормаЗаменаЗначений.ЗаменяемыеЗначения.Добавить();
			СтрокаЗамены.ЧтоЗаменять = Выборка.СтатьяДДС;
			СтрокаЗамены.НаЧтоЗаменять = ПредопределеннаяСтатья;
			СтрокаЗамены.Пометка = Истина;
			Сообщить("Справочник ""Статьи ДДС"". Произведена замена ссылок на элемент """ + Выборка.СтатьяДДС + """ ссылками на элемент """ + ПредопределеннаяСтатья + """");
			ФормаЗаменаЗначений.Открыть();
			//
			ОбъектСтатьяДДС = Выборка.СтатьяДДС.ПолучитьОбъект();
			ОбъектСтатьяДДС.Наименование = СтрНаименование+"_" + Номер;
			ОбъектСтатьяДДС.Записать();
			Номер = Номер + 1;
		КонецЦикла;
		//перезаполняем реквизиты
		Если ЗаполнятьВсе Тогда
			ПредопределеннаяСтатьяОбъект.Наименование = СтрНаименование;
		ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.Наименование) Тогда
			ПредопределеннаяСтатьяОбъект.Наименование = СтрНаименование;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(СтрВидДвижения) Тогда
			СсылкаВидДвижения = Перечисления.ВидыДвижений[СтрВидДвижения];
			Если СсылкаВидДвижения.Пустая() Тогда
				Сообщить("Ошибка при заполнении статьи ДДС элемента: '" + СтрНаименование + "' Вид движения: '"+СтрВидДвижения+"' не найден");
			Иначе
				Если ЗаполнятьВсе Тогда
					ПредопределеннаяСтатьяОбъект.ВидДвижения = СсылкаВидДвижения;
				ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.ВидДвижения) Тогда
					ПредопределеннаяСтатьяОбъект.ВидДвижения = СсылкаВидДвижения;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(СтрКоррСчет) Тогда
			СсылкаКоррСчет = ПланыСчетов.Регламентированный[СтрКоррСчет];
			Если СсылкаКоррСчет.Пустая() Тогда
				Сообщить("Ошибка при заполнении статьи ДДС элемента: '" + СтрНаименование + "' Корреспондирующий счет: '"+СтрКоррСчет+"' не найден");
			Иначе
				Если ЗаполнятьВсе Тогда
					ПредопределеннаяСтатьяОбъект.КоррСчет = СсылкаКоррСчет;
				ИначеЕсли обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.КоррСчет) Тогда
					ПредопределеннаяСтатьяОбъект.КоррСчет = СсылкаКоррСчет;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//Сохранение справочника
		НовыйЭлемент = ПредопределеннаяСтатьяОбъект.ЭтоНовый();
		Если ПредопределеннаяСтатьяОбъект.Модифицированность() Тогда
			Попытка
				ПредопределеннаяСтатьяОбъект.Записать();
				Сообщить("Справочник ""Статьи ДДС"". "+?(НовыйЭлемент,"Добавлен","Обновлен")+" элемент """+СокрЛП(ПредопределеннаяСтатьяОбъект.Наименование)+"""");
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()


//получить Список товаров и работ
Функция ПолучитьТаблицыПоМодели(СсылкаНаШаблон, МодельАвтомобиля) Экспорт
	
	// Получим данные о вариациях пакетным запросом
	ДанныеОВариациях = ПолучитьДанныеОВариациях(СсылкаНаШаблон, МодельАвтомобиля);
	
	//Данные о вариациях
	ИдентификаторВариацииПоУмолчанию = ДанныеОВариациях.ИдентификаторВариацииПоУмолчанию;
	МоделиВариацииПоУмолчанию = ДанныеОВариациях.МоделиВариацииПоУмолчанию;
	ДобавленныеВариацииПоМоделиАвтомобиля = ДанныеОВариациях.ДобавленныеВариацииПоМоделиАвтомобиля;
	
	ИспользуемаяВариация = Неопределено;
	
	// Определим какую вариацию будем использовать
	Если ДобавленныеВариацииПоМоделиАвтомобиля.Количество() = 0 Тогда
		//Если Вариаций не найдено то будем использовать вариацию по умолчанию
		Если НЕ МоделиВариацииПоУмолчанию.Количество() Тогда
			//Если у вариации по умолчанию нет ограничений то используем ее
			ИспользуемаяВариация = ИдентификаторВариацииПоУмолчанию;
		Иначе 
			//Если есть ограничения то проверим что модель есть в списке
			ИспользуемаяВариация = МоделиВариацииПоУмолчанию.Найти(МодельАвтомобиля);
			//Если не найдено то установится НЕОПРЕДЕЛЕННО
		КонецЕсли;
	ИначеЕсли МоделиВариацииПоУмолчанию.Количество() > 2 Тогда
		// Не должно быть две и более вариаций по одной модели автомобиля
		Сообщить("Ошибка! Не должно быть по одной модели несколько вариаций");
		Возврат Неопределено;
	Иначе
		ИспользуемаяВариация = ДобавленныеВариацииПоМоделиАвтомобиля[0].Вариация;
	КонецЕсли;
	
	// Проверим что мы определили вариаци.
	Если НЕ ЗначениеЗаполнено(ИспользуемаяВариация) Тогда
		Сообщить("По модели <"+МодельАвтомобиля+"> не определена вариация в шаблоне <" + СсылкаНаШаблон.Наименование + ">!");
		Возврат Неопределено;
	КонецЕсли;
	
	// Получим таблицы вариации
	СтруктураТаблицВариации = ПолучитьТаблицыВариаций(СсылкаНаШаблон, ИспользуемаяВариация);
	Возврат СтруктураТаблицВариации;
	
КонецФункции

Функция ПолучитьДанныеОВариациях(СсылкаНаШаблон, МодельАвтомобиля) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПакетыДопоборудованияКСОВариации.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ИдентификаторВариацииПоУмолчанию
		|ИЗ
		|	Справочник.ПакетыДопоборудованияКСО.Вариации КАК ПакетыДопоборудованияКСОВариации
		|ГДЕ
		|	ПакетыДопоборудованияКСОВариации.Наименование = &Наименование
		|	И ПакетыДопоборудованияКСОВариации.Ссылка = &СсылкаНаШаблон
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПакетыДопоборудованияКСОВариацииМодели.Модель КАК Модель
		|ПОМЕСТИТЬ МоделиВариацииПоУмолчанию
		|ИЗ
		|	Справочник.ПакетыДопоборудованияКСО.ВариацииМодели КАК ПакетыДопоборудованияКСОВариацииМодели
		|ГДЕ
		|	ПакетыДопоборудованияКСОВариацииМодели.Ссылка = &СсылкаНаШаблон
		|	И ПакетыДопоборудованияКСОВариацииМодели.Вариация В
		|			(ВЫБРАТЬ
		|				ИдентификаторВариацииПоУмолчанию.Идентификатор КАК Идентификатор
		|			ИЗ
		|				ИдентификаторВариацииПоУмолчанию КАК ИдентификаторВариацииПоУмолчанию)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПакетыДопоборудованияКСОВариацииМодели.Вариация КАК Вариация
		|ПОМЕСТИТЬ ДобавленныеВариацииПоМоделиАвтомобиля
		|ИЗ
		|	Справочник.ПакетыДопоборудованияКСО.ВариацииМодели КАК ПакетыДопоборудованияКСОВариацииМодели
		|ГДЕ
		|	ПакетыДопоборудованияКСОВариацииМодели.Ссылка = &СсылкаНаШаблон
		|	И ПакетыДопоборудованияКСОВариацииМодели.Модель = &МодельАвтомобиля
		|	И НЕ ПакетыДопоборудованияКСОВариацииМодели.Вариация В
		|				(ВЫБРАТЬ
		|					ИдентификаторВариацииПоУмолчанию.Идентификатор КАК Идентификатор
		|				ИЗ
		|					ИдентификаторВариацииПоУмолчанию КАК ИдентификаторВариацииПоУмолчанию)";
	
	Запрос.УстановитьПараметр("Наименование", "<По умолчанию>");
	Запрос.УстановитьПараметр("СсылкаНаШаблон", СсылкаНаШаблон);
	Запрос.УстановитьПараметр("МодельАвтомобиля", МодельАвтомобиля);
	Запрос.МенеджерВременныхТаблиц = новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	ИдентификаторВариацииПоУмолчанию 		= Запрос.МенеджерВременныхТаблиц.Таблицы[0].ПолучитьДанные().Выгрузить();
	МоделиВариацииПоУмолчанию 				= Запрос.МенеджерВременныхТаблиц.Таблицы[1].ПолучитьДанные().Выгрузить();
	ДобавленныеВариацииПоМоделиАвтомобиля 	= Запрос.МенеджерВременныхТаблиц.Таблицы[2].ПолучитьДанные().Выгрузить();
	
	//Идентификатор вариации по умолчанию всегда должен быть один
	Если ИдентификаторВариацииПоУмолчанию.Количество() = 1 Тогда
		ИдентификаторВариацииПоУмолчанию = ИдентификаторВариацииПоУмолчанию[0].Идентификатор;
	Иначе
		ИдентификаторВариацииПоУмолчанию = Неопределено;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("ИдентификаторВариацииПоУмолчанию", ИдентификаторВариацииПоУмолчанию);
	СтруктураВозврата.Вставить("МоделиВариацииПоУмолчанию", МоделиВариацииПоУмолчанию);
	СтруктураВозврата.Вставить("ДобавленныеВариацииПоМоделиАвтомобиля", ДобавленныеВариацииПоМоделиАвтомобиля);
	
	Возврат СтруктураВозврата;
	
КонецФункции
Функция ПолучитьТаблицыВариаций(СсылкаНаШаблон, ИдентификаторВариации)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПакетыДопоборудованияКСОТовары.Номенклатура КАК Номенклатура,
		|	ПакетыДопоборудованияКСОТовары.Количество КАК Количество,
		|	ПакетыДопоборудованияКСОТовары.Коэффициент КАК Коэффициент,
		|	ПакетыДопоборудованияКСОТовары.Цена КАК Цена,
		|	ПакетыДопоборудованияКСОТовары.Сумма КАК Сумма
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Справочник.ПакетыДопоборудованияКСО.Товары КАК ПакетыДопоборудованияКСОТовары
		|ГДЕ
		|	ПакетыДопоборудованияКСОТовары.Вариация = &ИдентификаторВариации
		|	И ПакетыДопоборудованияКСОТовары.Ссылка = &СсылкаНаШаблон
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПакетыДопоборудованияКСОРаботы.Работа КАК Работа,
		|	ПакетыДопоборудованияКСОРаботы.Количество КАК Количество,
		|	ПакетыДопоборудованияКСОРаботы.Нормочас КАК Нормочас,
		|	ПакетыДопоборудованияКСОРаботы.Коэффициент КАК Коэффициент,
		|	ПакетыДопоборудованияКСОРаботы.Цена КАК Цена,
		|	ПакетыДопоборудованияКСОРаботы.Сумма КАК Сумма
		|ПОМЕСТИТЬ Работы
		|ИЗ
		|	Справочник.ПакетыДопоборудованияКСО.Работы КАК ПакетыДопоборудованияКСОРаботы
		|ГДЕ
		|	ПакетыДопоборудованияКСОРаботы.Вариация = &ИдентификаторВариации
		|	И ПакетыДопоборудованияКСОРаботы.Ссылка = &СсылкаНаШаблон
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПакетыДопоборудованияКСОЗамены.Номенклатура КАК Номенклатура,
		|	ПакетыДопоборудованияКСОЗамены.ЗаменаНоменклатуры КАК ЗаменаНоменклатуры,
		|	ПакетыДопоборудованияКСОЗамены.НоваяЦена КАК НоваяЦена
		|ПОМЕСТИТЬ Замены
		|ИЗ
		|	Справочник.ПакетыДопоборудованияКСО.Замены КАК ПакетыДопоборудованияКСОЗамены
		|ГДЕ
		|	ПакетыДопоборудованияКСОЗамены.Вариация = &ИдентификаторВариации
		|	И ПакетыДопоборудованияКСОЗамены.Ссылка = &СсылкаНаШаблон";
	
	Запрос.УстановитьПараметр("ИдентификаторВариации", ИдентификаторВариации);
	Запрос.УстановитьПараметр("СсылкаНаШаблон", СсылкаНаШаблон);
	Запрос.МенеджерВременныхТаблиц = новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();
			
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Товары", Запрос.МенеджерВременныхТаблиц.Таблицы[0].ПолучитьДанные().Выгрузить());
	СтруктураВозврата.Вставить("Работы", Запрос.МенеджерВременныхТаблиц.Таблицы[1].ПолучитьДанные().Выгрузить());
	СтруктураВозврата.Вставить("Замены", Запрос.МенеджерВременныхТаблиц.Таблицы[2].ПолучитьДанные().Выгрузить());
	
	Возврат СтруктураВозврата;
	
КонецФункции
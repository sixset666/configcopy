&НаКлиенте
Перем БуферДляСтрок Экспорт;


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Добавим отборы по вариации на таблицы
	// Выберем первую вариацию
	Если Элементы.Вариации.ТекущаяСтрока = Неопределено Тогда
		Элементы.Вариации.ТекущаяСтрока = 0;
	КонецЕсли;
	
	// Заполним отбор
	ТекущаяВыбраннаяВариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
	Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура("Вариация",ТекущаяВыбраннаяВариация);
	Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Вариация",ТекущаяВыбраннаяВариация);
	Элементы.Замены.ОтборСтрок = Новый ФиксированнаяСтруктура("Вариация",ТекущаяВыбраннаяВариация);
	//
	
	ОбновитьИтогоПакета();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Если документ новый
	Если Объект.Ссылка.Пустая() Тогда
		НоваяВариацияПоУмолчанию = Объект.Вариации.Добавить();	
		НоваяВариацияПоУмолчанию.Идентификатор = Новый УникальныйИдентификатор();
		НоваяВариацияПоУмолчанию.Наименование = "<По умолчанию>";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	//Выполним проверки
	//Для вариаций, кроме по умолчанию, должны быть заполнены модели
	Для Каждого Вариация Из Объект.Вариации Цикл
		УникальныйИдентификаторСтроки = Вариация.Идентификатор;
		Если Вариация.Наименование = "<По умолчанию>" Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ Вариация.МодельСписок.Количество() Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Для вариации <"+Вариация.Наименование+"> не заполнена модель автомобиля!";
			Сообщение.Сообщить();
			Предупреждение("Для вариации <"+Вариация.Наименование+"> не заполнена модель автомобиля!");
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	// Запишем модели в отдельную таблицу
	ТекущийОбъект.ВариацииМодели.Очистить();
	
	Для Каждого Вариация Из Объект.Вариации Цикл
		УникальныйИдентификаторСтроки = Вариация.Идентификатор;
		Для Каждого Модель Из Вариация.МодельСписок Цикл
			НоваяСтрока = ТекущийОбъект.ВариацииМодели.Добавить();
			НоваяСтрока.Вариация = УникальныйИдентификаторСтроки;
			НоваяСтрока.Модель = Модель.Значение; 
		КонецЦикла;
	КонецЦикла;
	
	//Для вариаций не должно быть одинаковых моделей кроме вариации по умолчанию
	ТаблицаМоделейКопия = ТекущийОбъект.ВариацииМодели.Выгрузить();
	ВариацияПоУмолчанию = ТекущийОбъект.Вариации.Получить(0).Идентификатор;
	СтрокиВариацииПоУмолчанию = ТаблицаМоделейКопия.НайтиСтроки(Новый Структура("Вариация",ВариацияПоУмолчанию));
	Для Каждого СтрокаВариацииПоУмолчанию Из СтрокиВариацииПоУмолчанию Цикл
		ТаблицаМоделейКопия.Удалить(СтрокаВариацииПоУмолчанию);
	КонецЦикла;
	ТаблицаМоделей = ТаблицаМоделейКопия.Скопировать(,"Модель");
	ТаблицаМоделей.Свернуть("Модель");
	Для Каждого СтрокаМодель Из ТаблицаМоделей Цикл
		Модель = СтрокаМодель.Модель;
		ВариацииПоМодели = ТаблицаМоделейКопия.НайтиСтроки(Новый Структура("Модель",Модель));
		Если ВариацииПоМодели.Количество() > 1 Тогда
			СписокВариацийСтрока = "";
			Для Каждого СтрокаСопоставления Из ВариацииПоМодели Цикл
				ИдентификаторВариации = СтрокаСопоставления.Вариация;
				МассивВариаций = ТекущийОбъект.Вариации.НайтиСтроки(Новый Структура("Идентификатор",ИдентификаторВариации));
				СтрокаВариации = МассивВариаций[0];
				СписокВариацийСтрока = СписокВариацийСтрока + СтрокаВариации.Наименование + Символы.ПС;
			КонецЦикла;
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Модель <"+Модель+"> указана в нескольких вариациях! Одна модель может быть только в одной вариации." + Символы.ПС + "Список вариаций с моделью <"+Модель+"> :" + Символы.ПС + СписокВариацийСтрока;
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// перечитаем записанные данные в форму по моделям автомобиля
	Для Каждого Модель Из ТекущийОбъект.ВариацииМодели Цикл
		МассивСтрок = Объект.Вариации.НайтиСтроки(Новый Структура("Идентификатор", Модель.Вариация));
		Если МассивСтрок.Количество() Тогда
			ТекущаяСтрока = МассивСтрок[0];
			ТекущаяСтрока.МодельСписок.Добавить(Модель.Модель);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// После записи на сервере перечитаем записанные данные в форму
	Для Каждого Модель Из ТекущийОбъект.ВариацииМодели Цикл
		МассивСтрок = Объект.Вариации.НайтиСтроки(Новый Структура("Идентификатор", Модель.Вариация));
		Если МассивСтрок.Количество() Тогда
			ТекущаяСтрока = МассивСтрок[0];
			ТекущаяСтрока.МодельСписок.Добавить(Модель.Модель);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтрокиВБуфер(Команда)
	Если ТипЗнч(ЭтаФорма.ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		ИмяТаблицы = ЭтаФорма.ТекущийЭлемент.Имя;
		ВыделенныеСтроки = ЭтаФорма.ТекущийЭлемент.ВыделенныеСтроки;
		МассивСтрок = Новый Массив;
		Если ВыделенныеСтроки.Количество() Тогда
			Для Каждого ИндексСтроки Из ВыделенныеСтроки Цикл
				КопСтрока = Объект[ИмяТаблицы].НайтиПоИдентификатору(ИндексСтроки);
				МассивСтрок.Добавить(КопСтрока);
			КонецЦикла;
			СтруктураБуфера = Новый Структура();
			СтруктураБуфера.Вставить("ИмяТаблицы", ИмяТаблицы);
			СтруктураБуфера.Вставить("МассивСтрок", МассивСтрок);
			БуферДляСтрок = СтруктураБуфера;
		Иначе
			Предупреждение("Выделите строки для копирования");
		КонецЕсли;
	Иначе
		Предупреждение("Выделите строки из таблицы для копирования");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтрокиИзБуфера(Команда)
	Если БуферДляСтрок = Неопределено Тогда
		Предупреждение("Буфер пуст. Сначала скопируйте нужные строки.");
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЭтаФорма.ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		ИмяТаблицы = ЭтаФорма.ТекущийЭлемент.Имя;
		Если ИмяТаблицы <> БуферДляСтрок.ИмяТаблицы Тогда
			Предупреждение("Нельзя вставить скопированные строки из таблицы <"+БуферДляСтрок.ИмяТаблицы+"> в таблицу <"+ИмяТаблицы+">!");
			Возврат;
		КонецЕсли;
		Для Каждого ВставляемаяСтрока Из БуферДляСтрок.МассивСтрок Цикл
			НоваяСтрока = Объект[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВставляемаяСтрока,,"Вариация,ИсходныйНомерСтроки,НомерСтроки");
			НоваяСтрока.Вариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
		КонецЦикла;
	Иначе
		Предупреждение("Выберите таблицу для вставки");
	КонецЕсли;
КонецПроцедуры





&НаКлиенте
Процедура ОбновитьИтогоПакета() 
	
	ТекущаяВариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
	ОбщаяСумма = 0;

	Для Каждого Товар Из Объект.Товары.НайтиСтроки(Новый Структура("Вариация", ТекущаяВариация)) Цикл 
		ОбщаяСумма = ОбщаяСумма + Товар.Сумма;
	КонецЦикла;
	
	Для Каждого Работа Из Объект.Работы.НайтиСтроки(Новый Структура("Вариация", ТекущаяВариация)) Цикл 
		ОбщаяСумма = ОбщаяСумма + Работа.Сумма;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеКоличестваЦены(ТекСтрока)
	
    ТекСтрока.Сумма = ТекСтрока.Количество * ТекСтрока.Коэффициент * ТекСтрока.Цена;
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПоВновуюВариацию(СтараяВариация = Неопределено, НоваяВариация)
	// Получим вариацию по умолчанию или вариацию которую копируют
	Если СтараяВариация = Неопределено тогда
		ВариацияДляКопирования = Объект.Вариации.Получить(0);
		УникальныйИдентификаторДляКопирования = ВариацияДляКопирования.Идентификатор;
	Иначе
		УникальныйИдентификаторДляКопирования = СтараяВариация;
	КонецЕсли;
	
	// Таблица товаров
	ТаблицаТоваровПоУмолчанию = Объект.Товары.НайтиСтроки(Новый Структура("Вариация", УникальныйИдентификаторДляКопирования));
	Для Каждого Товар Из ТаблицаТоваровПоУмолчанию Цикл 
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Товар);
		НоваяСтрока.Вариация = НоваяВариация;
	КонецЦикла;
	//
	
	// Таблица работ
	ТаблицаРаботПоУмолчанию = Объект.Работы.НайтиСтроки(Новый Структура("Вариация", УникальныйИдентификаторДляКопирования));
	Для Каждого Работа Из ТаблицаРаботПоУмолчанию Цикл 
		НоваяСтрока = Объект.Работы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Работа);
		НоваяСтрока.Вариация = НоваяВариация;
	КонецЦикла;
	//
	
	// Таблица Замены
	ТаблицаЗаменыПоУмолчанию = Объект.Замены.НайтиСтроки(Новый Структура("Вариация", УникальныйИдентификаторДляКопирования));
	Для Каждого Замена Из ТаблицаЗаменыПоУмолчанию Цикл 
		НоваяСтрока = Объект.Замены.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Замена);
		НоваяСтрока.Вариация = НоваяВариация;
	КонецЦикла;
	//
	
	
КонецПроцедуры




&НаКлиенте
Процедура ВариацииМоделиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	МассивтипМоделиАвтомобилей = Новый Массив;
	МассивтипМоделиАвтомобилей.Добавить(Тип("СправочникСсылка.Модели"));
	Элементы.Вариации.ТекущиеДанные.МодельСписок.ТипЗначения = новый ОписаниеТипов(МассивтипМоделиАвтомобилей);
КонецПроцедуры

&НаКлиенте
Процедура ВариацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		// Создаем новую вариацию
		Если Копирование = Ложь Тогда
			// Копируем из вариации по умолчанию
			Элемент.ТекущиеДанные.Идентификатор = Новый УникальныйИдентификатор();
			ВариацииПриАктивизацииСтроки(Элемент);
			СкопироватьПоВновуюВариацию(Неопределено, Элемент.ТекущиеДанные.Идентификатор);
		Иначе
			// Копируем из другой вариации
			СтараяВариация = Элемент.ТекущиеДанные.Идентификатор;
			Элемент.ТекущиеДанные.Идентификатор = Новый УникальныйИдентификатор();
			Если Элемент.ТекущиеДанные.Наименование = "<По умолчанию>" Тогда
				Элемент.ТекущиеДанные.Наименование = "Новая вариация";
			КонецЕсли;
			ВариацииПриАктивизацииСтроки(Элемент);
			СкопироватьПоВновуюВариацию(СтараяВариация, Элемент.ТекущиеДанные.Идентификатор);
		КонецЕсли;
		ОбновитьИтогоПакета();
		
		МассивтипМоделиАвтомобилей = Новый Массив;
		МассивтипМоделиАвтомобилей.Добавить(Тип("СправочникСсылка.Модели"));
		Элемент.ТекущиеДанные.МодельСписок.ТипЗначения = новый ОписаниеТипов(МассивтипМоделиАвтомобилей);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВариацииПриАктивизацииСтроки(Элемент)
	
	// Добавим отборы по вариации на таблицы
	// Выберем первую вариацию
	Если Элементы.Вариации.ТекущаяСтрока = Неопределено Тогда
		Элементы.Вариации.ТекущаяСтрока = 0;
	КонецЕсли;
	
	// Заполним отбор
	ТекущаяВыбраннаяВариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
	Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура("Вариация",ТекущаяВыбраннаяВариация);
	Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Вариация",ТекущаяВыбраннаяВариация);
	Элементы.Замены.ОтборСтрок = Новый ФиксированнаяСтруктура("Вариация",ТекущаяВыбраннаяВариация);
	//
	
	//Обновим сумму
	ОбновитьИтогоПакета();
	//
	
КонецПроцедуры

&НаКлиенте
Процедура ВариацииПередНачаломИзменения(Элемент, Отказ)
	// По умолчанию нельзя трогать
	Если Элемент.ТекущиеДанные.Наименование = "<По умолчанию>" И Элемент.ТекущийЭлемент.Имя = "ВариацииНаименование" Тогда
		Отказ = Истина;
	КонецЕсли;
	//
	
	МассивтипМоделиАвтомобилей = Новый Массив;
	МассивтипМоделиАвтомобилей.Добавить(Тип("СправочникСсылка.Модели"));
	Элементы.Вариации.ТекущиеДанные.МодельСписок.ТипЗначения = новый ОписаниеТипов(МассивтипМоделиАвтомобилей);
КонецПроцедуры

&НаКлиенте
Процедура ВариацииПередУдалением(Элемент, Отказ)
	
	// По умолчанию нельзя трогать
	Если Элемент.ТекущиеДанные.Наименование = "<По умолчанию>" Тогда
		Предупреждение("Вариацию <По умолчанию> нельзя удалить!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	//
	
	// для начала удалим из таблиц все данные по вариации	
	МассивСтрок = Объект.Товары.НайтиСтроки(Новый Структура("Вариация", Элементы.Вариации.ТекущиеДанные.Идентификатор));
	Для Каждого Строка Из МассивСтрок Цикл 
		Объект.Товары.Удалить(Объект.Товары.Индекс(Строка));
	КонецЦикла;
	
	МассивСтрок = Объект.Работы.НайтиСтроки(Новый Структура("Вариация", Элементы.Вариации.ТекущиеДанные.Идентификатор));
	Для Каждого Строка Из МассивСтрок Цикл 
		Объект.Работы.Удалить(Объект.Работы.Индекс(Строка));
	КонецЦикла;
	
	МассивСтрок = Объект.Замены.НайтиСтроки(Новый Структура("Вариация", Элементы.Вариации.ТекущиеДанные.Идентификатор));
	Для Каждого Строка Из МассивСтрок Цикл 
		Объект.Замены.Удалить(Объект.Замены.Индекс(Строка));
	КонецЦикла;
	//
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	// запишем вариацию в строку
	Элемент.ТекущиеДанные.Вариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	ИзменениеКоличестваЦены(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	ИзменениеКоличестваЦены(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоэффициентПриИзменении(Элемент)
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	ИзменениеКоличестваЦены(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Количество) Тогда
		ТекСтрока.Количество = 1;	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Коэффициент) Тогда
		ТекСтрока.Коэффициент = 1;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	ОбновитьИтогоПакета();
КонецПроцедуры


&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	// запишем вариацию в строку
	Элемент.ТекущиеДанные.Вариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
КонецПроцедуры

&НаКлиенте
Процедура РаботыРаботаПриИзменении(Элемент)
	ТекСтрока = Элементы.Работы.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Количество) Тогда
		ТекСтрока.Количество = 1;	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Коэффициент) Тогда
		ТекСтрока.Коэффициент = 1;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПриИзменении(Элемент)
	ТекСтрока = Элементы.Работы.ТекущиеДанные;
	ИзменениеКоличестваЦены(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.Работы.ТекущиеДанные;
	ИзменениеКоличестваЦены(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоэффициентПриИзменении(Элемент)
	ТекСтрока = Элементы.Работы.ТекущиеДанные;
	ИзменениеКоличестваЦены(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриИзменении(Элемент)
	ОбновитьИтогоПакета();
КонецПроцедуры


&НаКлиенте
Процедура ЗаменыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	// Можно выбрать только те номенклатуры что существуют в пакете
	МассивТоваров = Объект.Товары.НайтиСтроки(Новый Структура("Вариация", Элементы.Вариации.ТекущиеДанные.Идентификатор));
	Элемент.СписокВыбора.Очистить();
	Для Каждого Товар Из МассивТоваров Цикл
		Если Элемент.СписокВыбора.НайтиПоЗначению(Товар.Номенклатура) = Неопределено Тогда
			Элемент.СписокВыбора.Добавить(Товар.Номенклатура);
		КонецЕсли;
	КонецЦикла;
	//
КонецПроцедуры

&НаКлиенте
Процедура ЗаменыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	// запишем вариацию в строку
	Элемент.ТекущиеДанные.Вариация = Элементы.Вариации.ТекущиеДанные.Идентификатор;
КонецПроцедуры



//Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДатаОстатков = ТекущаяДата();
	ОбновитьСписокНаСервере();
	Элементы.Category.СписокВыбора.ЗагрузитьЗначения(ns_БытоваяЭлектроникаФормы.ПолучитьСписокКатегорий());
	Элементы.GoodsType.СписокВыбора.ЗагрузитьЗначения(ns_БытоваяЭлектроникаФормы.ПолучитьСписокПодкатегорий(Category));  
	
	ns_ОбщегоНазначенияФормы.УстановитьТипСклад(Элементы);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ns_ОбщегоНазначенияФормы.ОбработкаВыбора(Элементы, ВыбранноеЗначение, ИсточникВыбора, "ns_БытоваяЭлектроника"); 
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаОстатковПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПоДокументу(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаОтметкиПоДокументу",, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура CategoryПриИзменении(Элемент)
	Элементы.GoodsType.СписокВыбора.ЗагрузитьЗначения(ns_БытоваяЭлектроникаФормы.ПолучитьСписокПодкатегорий(Category));
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сбросить(Команда)
	СброситьРеквизитыЗаполнения(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	Если ЗначениеЗаполнено(Category) Тогда
		ТекстВопроса = "При изменении Category, у всех выделеных элементов сотрутся GoodsType. Продолжить?";
	Иначе
		ТекстВопроса = "При изменении реквизитов, у всех выделеных элементов сотрутся GoodsType установленные ранее. Продолжить?";
	КонецЕсли; 
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьЗавершение", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
		УстановитьНаСервере(ВыделенныеСтроки);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СнятьБезОстатковСВыгрузки(Команда)
	СнятьБезОстатковСВыгрузкиНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры
 
//КонецОбласти

//Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура GoodsTypeПриИзменении(Элемент)
	СброситьРеквизитыЗаполнения();
КонецПроцедуры

&НаКлиенте
Процедура СброситьРеквизитыЗаполнения(Очищать = Ложь)
	Если Очищать Тогда 
		Category = Неопределено;
		GoodsType = Неопределено;
	КонецЕсли; 
	СопоставлениеРеквизитов = Неопределено;
	ConditionБытоваяЭлектроника = Неопределено;
	ProductsType = Неопределено;
	ШаблонЗаголовка = Неопределено;
	ШаблонОбъявления = Неопределено;
КонецПроцедуры

&НаСервере
Процедура УстановитьНаСервере(ВыделенныеСтроки)
	Попытка
		НачатьТранзакцию();
		
		ЗначенияСопоставленныхРеквизитов = Неопределено;
		Если ЗначениеЗаполнено(СопоставлениеРеквизитов) Тогда
			ЗначенияСопоставленныхРеквизитов = ns_Переопределяемый.ПолучитьЗначенияСопоставленныхРеквизитов(ВыделенныеСтроки);
		КонецЕсли; 
		
		Для каждого СтрокаСписка Из ВыделенныеСтроки Цикл
			Если СтрокаСписка.ЭтоГруппа Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ns_БытоваяЭлектроника.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ns_БытоваяЭлектроника КАК ns_БытоваяЭлектроника
				|ГДЕ
				|	ns_БытоваяЭлектроника.Родитель В ИЕРАРХИИ(&Родитель)
				|	И ns_БытоваяЭлектроника.ЭтоГруппа = ЛОЖЬ";
				Запрос.УстановитьПараметр("Родитель", СтрокаСписка.Ссылка);
				РезультатЗапроса = Запрос.Выполнить();
				
				Если РезультатЗапроса.Пустой() Тогда
					Возврат;
				КонецЕсли;  
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					УстановитьЗначениеЭлементу(Выборка.Ссылка, ЗначенияСопоставленныхРеквизитов);
				КонецЦикла; 
			Иначе	
				УстановитьЗначениеЭлементу(СтрокаСписка, ЗначенияСопоставленныхРеквизитов);
			КонецЕсли;
		КонецЦикла;  
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
	    ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
	 
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеЭлементу(Ссылка, ЗначенияСопоставленныхРеквизитов)
	
	Изменен = Ложь;
	ТекущийОбъект = Ссылка.ПолучитьОбъект();
	
	Если ЗначениеЗаполнено(Category) И (НЕ ТекущийОбъект.Category = Category) Тогда
		ТекущийОбъект.Category = Category;  
		ТекущийОбъект.GoodsType = Неопределено;
		Изменен = Истина;
	КонецЕсли;

	Если ЗначениеЗаполнено(GoodsType) И (НЕ ТекущийОбъект.GoodsType = GoodsType) Тогда
		ТекущийОбъект.GoodsType = GoodsType;
		Изменен = Истина;
	КонецЕсли;
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "ProductsType", ProductsType);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Condition", ConditionБытоваяЭлектроника);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Шаблон", ШаблонОбъявления);
	ns_ОбщегоНазначенияВызовСервера.ЗаполнитьСопоставлениеРеквизитов(Изменен, ТекущийОбъект, ЗначенияСопоставленныхРеквизитов, СопоставлениеРеквизитов);
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		НовыйЗаголовок = ns_ШаблонизаторСервер.СформироватьTitle(ШаблонЗаголовка, ТекущийОбъект.Title, ТекущийОбъект.Номенклатура, ТекущийОбъект.Характеристика);
		ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Title", НовыйЗаголовок);
	КонецЕсли;
	
	Если Изменен Тогда
		ТекущийОбъект.Записать();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Истина, "ns_БытоваяЭлектроника");
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Ложь, "ns_БытоваяЭлектроника");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТолькоВыгружаемые(Команда)
	ns_ОбщегоНазначенияФормы.ПоказатьТолькоВыгружаемые(Элементы, Список);
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Список.ТекстЗапроса = ns_Переопределяемый.ПолучитьТекстЗапроса_БытоваяЭлектроника(Склад);
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Склад", Склад);
	КонецЕсли; 
	
	Список.Параметры.УстановитьЗначениеПараметра("НаДату", КонецДня(ДатаОстатков));
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
	Иначе
		СсылкаНаСтроку = ТекущиеДанные.Ссылка;
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = СсылкаНаСтроку;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьБезОстатковСВыгрузкиНаСервере()
	ns_АвитоВалидация.СнятьБезОстатковСВыгрузки("ns_БытоваяЭлектроника");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	ns_ОбщегоНазначенияВызовСервера.СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
КонецПроцедуры

//КонецОбласти 













// процедура заполнения предопределенных элементов справочника
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	ПодразделенияКомпанииОсновноеПодразделение = Справочники.ПодразделенияКомпании.ОсновноеПодразделение.ПолучитьОбъект();
	//перезаполняем реквизиты
	Если ЗаполнятьВсе Тогда
		ПодразделенияКомпанииОсновноеПодразделение.Организация = Справочники.Организации.ОсновнаяОрганизация;
	ИначеЕсли обЗначениеНеЗаполнено(ПодразделенияКомпанииОсновноеПодразделение.Организация) Тогда
		ПодразделенияКомпанииОсновноеПодразделение.Организация = Справочники.Организации.ОсновнаяОрганизация;
	КонецЕсли;
	
	//Сохранение справочника
	НовыйЭлемент = ПодразделенияКомпанииОсновноеПодразделение.ЭтоНовый();
	Если ПодразделенияКомпанииОсновноеПодразделение.Модифицированность() Тогда
		Попытка		
			ПодразделенияКомпанииОсновноеПодразделение.Записать();
			Сообщить("Справочник ""Подразделения компании"". "+?(НовыйЭлемент,"Создан","Модифицирован")+" элемент """+СокрЛП(ПодразделенияКомпанииОсновноеПодразделение.Наименование)+"""");
		Исключение
		КонецПопытки;
	КонецЕсли;
		
	// На нулевом уровне может находится только предопределенный элемент "Вся компания". 
	// Поэтому все лишнее перенесем на первый уровень.
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПодразделенияКомпании.Ссылка
	|ИЗ
	|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
	|ГДЕ
	|	ПодразделенияКомпании.Родитель = &Родитель
	|	И ПодразделенияКомпании.Ссылка <> &Ссылка";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Родитель", Справочники.ПодразделенияКомпании.ПустаяСсылка());
	Запрос.УстановитьПараметр("Ссылка",   Справочники.ПодразделенияКомпании.ОсновноеПодразделение);
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл // Все элементы на этом уровне "Лишние".
		ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ТекОбъект.Родитель = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
		ТекОбъект.Записать();
	КонецЦикла;  	
	
	// Подразделение "Вся компания" (корневой элемент) всегда будет балансовым.
	// SUIG - на необходимость записи как бы тоже надо проверять (а то так и будет по обменам ходить...)
	ПодразделенияКомпанииОсновноеПодразделение = Справочники.ПодразделенияКомпании.ОсновноеПодразделение.ПолучитьОбъект();
	Если НЕ ПодразделенияКомпанииОсновноеПодразделение.Балансовое  Тогда
		ПодразделенияКомпанииОсновноеПодразделение.Балансовое = Истина;
		ПодразделенияКомпанииОсновноеПодразделение.Записать();
	КонецЕсли; 
	
	ПодразделенияКомпанииОсновноеПодразделение = Неопределено;
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

Функция ПолучитьСписокПодразделенийССП() Экспорт
	
	СписокПодразеделений = новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПодразделенияКомпании.БТ_НазваниеДляССП КАК БТ_НазваниеДляССП
		|ИЗ
		|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
		|ГДЕ
		|	НЕ ПодразделенияКомпании.БТ_НазваниеДляССП = """"";
	
	СписокПодразеделений.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("БТ_НазваниеДляССП"));
	
	Возврат СписокПодразеделений;
	
КонецФункции
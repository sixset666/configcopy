
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ


&НаСервереБезКонтекста
Функция ПолучитьОформление(ИмяОформления)
	
	Рез = Неопределено;
	ТекПараметр = БиблиотекаМакетовОформленияКомпоновкиДанных.Найти(ИмяОформления);
	Если НЕ ТекПараметр = Неопределено Тогда
		Рез = Новый Структура("Имя, Представление", ТекПараметр.Имя, ТекПараметр.Представление);
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции

&НаКлиенте
Процедура КомандаОформление(Команда)
	
	ИмяВремОформления = СтрЗаменить(Команда.Имя, Команда.Действие, "");
	ТекОформление = ПолучитьОформление(ИмяВремОформления);
	Если Не ТекОформление = Неопределено Тогда
		ТекПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыВывода.Элементы.Найти("МакетОформления");
		Если НЕ ТекПараметр = Неопределено Тогда
			ИмяОформления = ИмяВремОформления;
			ТекПараметр.Значение = ИмяОформления;
			ТекПараметр.Использование = Истина;
			ИмяЭлемента = "Оформление"+ИмяОформления;
			
			Для Каждого ТекЭлемент Из Элементы.ГруппаОформлениеТаблицы.ПодчиненныеЭлементы Цикл
				ТекЭлемент.Пометка = (ТекЭлемент.Имя = ИмяЭлемента);
			КонецЦикла;
			
			Элементы.ГруппаОформлениеТаблицы.Заголовок = "Оформление: <" + ТекОформление.Представление + ">";
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаТипДиаграммы(Команда)
	
	Позиция = Найти(Команда.Имя, Команда.Действие);
	ИмяВремОформления = Сред(Команда.Имя, Позиция+СтрДлина(Команда.Действие));
	ГруппаОформление = Элементы.ГруппаОформлениеДиаграммы;
	
	ЗначениеОформления = ПредопределенноеЗначение("ТипДиаграммы."+ИмяВремОформления);
	ТекПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыВывода.Элементы.Найти("ТипДиаграммы");
	Если НЕ ТекПараметр = Неопределено Тогда
		ИмяОформления = ИмяВремОформления;
		ТекПараметр.Значение = ЗначениеОформления;
		ТекПараметр.Использование = Истина;
		ИмяЭлемента = "Оформление"+ИмяОформления;
		
		Картинка = Неопределено;
		Для Каждого ТекЭлемент Из ГруппаОформление.ПодчиненныеЭлементы Цикл
			Если ТекЭлемент.Имя = ИмяЭлемента Тогда
				ТекЭлемент.Пометка = Истина;
				Картинка = ТекЭлемент.Картинка;
			Иначе
				ТекЭлемент.Пометка = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ГруппаОформление.Заголовок   = СокрЛП(ЗначениеОформления);
		ГруппаОформление.Картинка    = Картинка;
		ГруппаОформление.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОформлениеСтруктуры()
	
	// оформление таблицы
	ПараметрОформления = КомпоновщикНастроекКД.Настройки.ПараметрыВывода.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("МакетОформления"));
	Если НЕ ПараметрОформления = Неопределено Тогда
		Для Каждого ТекМакет Из БиблиотекаМакетовОформленияКомпоновкиДанных Цикл
			НоваяКоманда = Команды.Найти("КомандаОформление"+ТекМакет.Имя);
			Если НоваяКоманда = Неопределено Тогда
				НоваяКоманда = Команды.Добавить("КомандаОформление"+ТекМакет.Имя);
				НоваяКоманда.Действие = "КомандаОформление";
			КонецЕсли;
			
			НовоеОформление = Элементы.Добавить("Оформление"+ТекМакет.Имя, Тип("КнопкаФормы"), Элементы.ГруппаОформлениеТаблицы);
			НовоеОформление.ИмяКоманды = НоваяКоманда.Имя;
			НовоеОформление.Заголовок  = ТекМакет.Представление;
			НовоеОформление.Вид        = ВидКнопкиФормы.КнопкаКоманднойПанели;
		КонецЦикла;
	КонецЕсли;
	
	// оформление диаграммы
	ГруппаОформления = Элементы.ГруппаОформлениеДиаграммы;
	ПараметрОформления = КомпоновщикНастроекКД.Настройки.ПараметрыВывода.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ТипДиаграммы"));
	Если НЕ ПараметрОформления = Неопределено Тогда
		ЗначениеОформления = КомпоновщикНастроекКД.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрОформления.Параметр);
		
		Для Каждого ТекТипДиаграммы Из ТипДиаграммы Цикл
			ИмяТипаДиаграммы = СтрЗаменить(ПолучитьПолноеИмяПредопределенногоЗначения(ТекТипДиаграммы), "ТипДиаграммы.", "");
			
			НоваяКоманда = Команды.Найти("КомандаТипДиаграммы"+ИмяТипаДиаграммы);
			Если НоваяКоманда = Неопределено Тогда
				НоваяКоманда = Команды.Добавить("КомандаТипДиаграммы"+ИмяТипаДиаграммы);
				НоваяКоманда.Действие = "КомандаТипДиаграммы";
			КонецЕсли;
			
			НовоеОформление = Элементы.Добавить("Оформление"+ИмяТипаДиаграммы, Тип("КнопкаФормы"), ГруппаОформления);
			НовоеОформление.ИмяКоманды = НоваяКоманда.Имя;
			НовоеОформление.Заголовок  = СокрЛП(ТекТипДиаграммы);
			НовоеОформление.Вид        = ВидКнопкиФормы.КнопкаКоманднойПанели;
			НовоеОформление.Пометка    = Ложь;
			Попытка
				НовоеОформление.Картинка    = БиблиотекаКартинок[ИмяТипаДиаграммы];
				НовоеОформление.Отображение = ОтображениеКнопки.КартинкаИТекст;
			Исключение
				кк = 2;
			КонецПопытки;
			
			Если ЗначениеОформления.Значение = ТекТипДиаграммы Тогда
				НовоеОформление.Пометка = Истина;
				ГруппаОформления.Заголовок = "Тип диаграммы: <" + СокрЛП(ТекТипДиаграммы) + ">";
				ГруппаОформления.Картинка    = НовоеОформление.Картинка;
				ГруппаОформления.Отображение = НовоеОформление.Отображение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеГруппировок()
	
	СтрокаОтчеты = Объект.Отчеты.НайтиПоИдентификатору(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтрокаОтчеты.ОграниченияНастроек.НайтиПоЗначению("Группировки") = Неопределено Тогда
		Возврат;
	ИначеЕсли СтрокаОтчеты.РежимВыводаОтчета = 1 Тогда
		ЭтаФорма.Элементы.СтруктураСтрокиНаименование.Заголовок = "Группировки:";
		ЭтаФорма.Элементы.ГруппаСтруктураКолонки.Видимость = Ложь;
		Элементы.ГруппаОформлениеТаблицы.Видимость = Истина;
		Элементы.ГруппаОформлениеДиаграммы.Видимость = Ложь;
	ИначеЕсли СтрокаОтчеты.РежимВыводаОтчета = 2 Тогда
		ЭтаФорма.Элементы.СтруктураСтрокиНаименование.Заголовок = "Строки:";
		ЭтаФорма.Элементы.СтруктураКолонкиНаименование.Заголовок = "Колонки:";
		ЭтаФорма.Элементы.ГруппаСтруктураКолонки.Видимость = Истина;
		Элементы.ГруппаОформлениеТаблицы.Видимость = Истина;
		Элементы.ГруппаОформлениеДиаграммы.Видимость = Ложь;
	ИначеЕсли СтрокаОтчеты.РежимВыводаОтчета = 3 Тогда
		ЭтаФорма.Элементы.СтруктураСтрокиНаименование.Заголовок = "Серии:";
		ЭтаФорма.Элементы.СтруктураКолонкиНаименование.Заголовок = "Точки:";
		ЭтаФорма.Элементы.ГруппаСтруктураКолонки.Видимость = Истина;
		Элементы.ГруппаОформлениеТаблицы.Видимость = Ложь;
		Элементы.ГруппаОформлениеДиаграммы.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаИзменитьРежимВыводаОтчета(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Счетчик = 1;
	Для Каждого ТекВид Из Элементы.ГруппаРежимВыводаОтчета.ПодчиненныеЭлементы Цикл
		Если ТекВид.ИмяКоманды = Команда.Имя Тогда
			ТекВид.Пометка = Истина;
			ТекущиеДанные.РежимВыводаОтчета = Счетчик;
			Элементы.ГруппаРежимВыводаОтчета.Заголовок = ТекВид.Заголовок;
			Элементы.ГруппаРежимВыводаОтчета.Картинка  = ТекВид.Картинка;
		Иначе
			ТекВид.Пометка = Ложь;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Если НЕ ПревРежимВыводаОтчета = ТекущиеДанные.РежимВыводаОтчета Тогда
		ПревРежимВыводаОтчета = ТекущиеДанные.РежимВыводаОтчета;
		ОбновитьОписаниеГруппировок();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьРежимВыводаОтчета(СтрокаОтчеты)
	
	Если НЕ ПревРежимВыводаОтчета = СтрокаОтчеты.РежимВыводаОтчета Тогда
		Счетчик = 1;
		Для Каждого ТекВид Из Элементы.ГруппаРежимВыводаОтчета.ПодчиненныеЭлементы Цикл
			Если Счетчик = СтрокаОтчеты.РежимВыводаОтчета Тогда
				ТекВид.Пометка = Истина;
				Элементы.ГруппаРежимВыводаОтчета.Заголовок = ТекВид.Заголовок;
				Элементы.ГруппаРежимВыводаОтчета.Картинка  = ТекВид.Картинка;
			Иначе
				ТекВид.Пометка = Ложь;
			КонецЕсли;
			Счетчик = Счетчик + 1;
		КонецЦикла;
		
		ОбновитьОписаниеГруппировок();
		
		ПревРежимВыводаОтчета = СтрокаОтчеты.РежимВыводаОтчета;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСтруктуруОтчета(СтрокаОтчеты)
	
	СтрокаОтчеты.ПредставлениеСтрок   = "";
	СтрокаОтчеты.ПредставлениеКолонок = "";
	
	Результат = Истина;
	Если КомпоновщикНастроекКД.Настройки.Структура.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтчетыСервер.ЗагрузитьНастройкиИзСКД(КомпоновщикНастроекКД.Настройки, СтрокаОтчеты.ИзмеренияСтроки, СтрокаОтчеты.ИзмеренияКолонки);
	
	//СтруктураОтчета = КомпоновщикНастроекКД.Настройки.Структура[0];
	
	//СтрокаОтчеты.ИзмеренияСтроки.Очистить();
	//СтрокаОтчеты.ИзмеренияКолонки.Очистить();
	//
	//ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	//ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	//
	//ТипСтруктуры = ТипЗнч(СтруктураОтчета);
	//Если ТипСтруктуры = ТипСтруктурыТаблицы Тогда
	//	Для Каждого Строка Из СтруктураОтчета.Строки Цикл
	//		ЗаполнитьГруппировки(СтрокаОтчеты.ИзмеренияСтроки, Строка);
	//	КонецЦикла;
	//	Для Каждого Колонка Из СтруктураОтчета.Колонки Цикл
	//		ЗаполнитьГруппировки(СтрокаОтчеты.ИзмеренияКолонки, Колонка);
	//	КонецЦикла;
	//ИначеЕсли ТипСтруктуры = ТипСтруктурыДиаграммы Тогда
	//	Для Каждого Серия Из СтруктураОтчета.Серии Цикл
	//		ЗаполнитьГруппировки(СтрокаОтчеты.ИзмеренияСтроки, Серия);
	//	КонецЦикла;
	//	Для Каждого Точка Из СтруктураОтчета.Точки Цикл
	//		ЗаполнитьГруппировки(СтрокаОтчеты.ИзмеренияКолонки, Точка);
	//	КонецЦикла;
	//Иначе
	//	ЗаполнитьГруппировки(СтрокаОтчеты.ИзмеренияСтроки, СтруктураОтчета);
	//КонецЕсли;
	
	ТекстСтрок = "";
	Для Каждого ТекСтрока Из СтрокаОтчеты.ИзмеренияСтроки Цикл
		Если ТекСтрока.Использование Тогда
			ТекстСтрок = ТекстСтрок + ?(ТекстСтрок = "", "", ", ") + ТекСтрока.Заголовок;
		КонецЕсли;
	КонецЦикла;
	
	ТекстКолонок = "";
	Для Каждого ТекКолонка Из СтрокаОтчеты.ИзмеренияКолонки Цикл
		Если ТекКолонка.Использование Тогда
			ТекстКолонок = ТекстКолонок + ?(ТекстКолонок = "", "", ", ") + ТекКолонка.Заголовок;
		КонецЕсли;
	КонецЦикла;
	
	СтрокаОтчеты.ПредставлениеСтрок = ТекстСтрок;
	СтрокаОтчеты.ПредставлениеКолонок = ТекстКолонок;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокВидовКИ()
	
	Результат = Новый СписокЗначений();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыКонтактнойИнформации.Ссылка,
	               |	ВидыКонтактнойИнформации.Наименование
	               |ИЗ
	               |	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	               |ГДЕ
	               |	ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ И 
	               |	ВидыКонтактнойИнформации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Результат.Добавить(Выборка.Ссылка, Выборка.Наименование);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьДатуСеанса()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// Чтение хранилища значений
	Если ТекущийОбъект.ПисьмоВФорматеHTML Тогда
		СтруктураВложенийПисьмаВФорматеHTML = ТекущийОбъект.КартинкиПисьмаВФорматеHTML.Получить();
		Если СтруктураВложенийПисьмаВФорматеHTML = Неопределено Тогда
			СтруктураВложенийПисьмаВФорматеHTML = Новый Структура;
		КонецЕсли;
		ТекстПисьмаФорматированныйДокумент.УстановитьHTML(ТекущийОбъект.ТекстПисьмаВФорматеHTML, СтруктураВложенийПисьмаВФорматеHTML);
	КонецЕсли;
	
	// Перезаполнение очищаемых данных формы при повторном чтении объекта из БД
	Если Кэш <> Неопределено Тогда
		ПрочитатьПользовательскоеПредставлениеФорматов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам") Тогда
		СправочникИмя=Метаданные.Справочники.РассылкиОтчетов.Имя;
		ПравоДоступа = обПраво("Право доступа "+СправочникИмя);
		Если ПравоДоступа=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
			Отказ=Истина;
			Возврат;
		ИначеЕсли ПравоДоступа=Перечисления.ВидыПравДляСправочников.Чтение Тогда
			ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ОграниченияОтчетов = Новый Структура;
	
	СписокВыбора = Элементы.Пользователь.СписокВыбора;
	Если ПравоДоступа("Администрирование",Метаданные) Тогда
		Пользователи = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Иначе
		ТекПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
		Пользователи = Новый Массив;
		Пользователи.Добавить(ТекПользователь);
	КонецЕсли;
	Для Каждого ТекПользователь из Пользователи Цикл
		СписокВыбора.Добавить(ТекПользователь.Имя, ТекПользователь.ПолноеИмя);
	КонецЦикла;
	
	Если Объект.ПометкаУдаления Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	СтруктураАдресовСхемКД = Новый Структура;
	ЗначениеКопирования    = Параметры.ЗначениеКопирования;
	
	СписокВыбора = Элементы.ВариантЗагрузкиПериода.СписокВыбора;
	Для Каждого ТекЭлемент Из ВариантСтандартногоПериода Цикл
		СписокВыбора.Добавить(ПолучитьПолноеИмяПредопределенногоЗначения(ТекЭлемент), Строка(ТекЭлемент));
	КонецЦикла;
	
	ЗаполнитьОформлениеСтруктуры();
	
	// Используется при загрузке и записи настроек выбранного отчета
	ИдентификаторТекущейСтрокиТаблицыОтчетов = -1;
	
	// Кэш проверки
	ЭтоНовый = Объект.Ссылка.Пустая();
	СозданКопированием = НЕ ЗначениеКопирования.Пустая();
	
	Кэш = ПолучитьКэш();
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	
	РассылкаБылаПерсонализирована = Объект.Персонализирована;
	
	// Чтение
	ПрочитатьПользовательскоеПредставлениеФорматов();
	ЗаполнитьПустыеШаблоныСтандартными(Объект);
	
	Если ЭтоНовый И НЕ СозданКопированием Тогда
		ВариантРасписания = Неопределено;
		Параметры.Свойство("ВариантРасписания", ВариантРасписания);
		ЗаполнитьРасписаниеПоВарианту(ВариантРасписания);
	Иначе
		ПрочитатьРасписаниеРегламентногоЗадания();
	КонецЕсли;
	
	ТекущийПользователь = ПараметрыСеанса.Пользователь;
	
	// Заполнение автора рассылки
	Если ЭтоНовый Тогда
		// Автор рассылки
		Объект.Автор = ТекущийПользователь;
		Если НЕ ЗначениеЗаполнено(Объект.Автор) Тогда
			Отказ = Истина;
			Стр1 = Строка(ТипЗнч(ТекущийПользователь));
			Стр2 = Строка(ТекущийПользователь);
			Текст = НСтр("ru = 'Ошибка заполнения автора рассылки
				|Автор ("+Стр1+"): "+Стр2+"'");
			ЗаписьЖурналаРегистрации(
				"Рассылка отчетов. Открытие формы элемента",
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.РассылкиОтчетов,
				,
				Текст
			);
			Возврат;
		КонецЕсли;
		
		// Имя архива
		Объект.ИмяАрхива    = Кэш.Шаблоны.ИмяАрхива;
		Объект.Пользователь = СокрЛП(Объект.Автор.Код);
	КонецЕсли;
	
	// Позволяет видеть и управлять некоторыми защищенными параметры рассылки
	РассылкуРедактируетАвтор = (Объект.Автор = ТекущийПользователь);
	
	// Доступность кнопки добавления дополнительного отчета
	Элементы.ОтчетыДобавитьДополнительныйОтчет.Доступность = ?(Кэш.ПустоеЗначениеОтчета = Неопределено, Истина, Ложь);
	// "Кэш.ПустоеЗначениеОтчета = Неопределено" когда тип реквизита "отчет" составной, 
	//   соответственно используется интеграция с подсистемой Дополнительные отчеты и обработки
		
	// Доступность автора рассылки
	//Элементы.Автор.Доступность = Пользователи.ЭтоПолноправныйПользователь();
	
	// Список форматов с пометками для форматов по умолчанию
	СписокФорматовПоУмолчанию = РассылкаОтчетовПовтИсп.СписокФорматов();
	
	// Представление списка форматов по умолчанию
	СписокФорматовПоУмолчаниюПредставление = "";
	Для Каждого ЭлементСписка Из СписокФорматовПоУмолчанию Цикл
		Если ЭлементСписка.Пометка Тогда
			СписокФорматовПоУмолчаниюПредставление = СписокФорматовПоУмолчаниюПредставление + ?(СписокФорматовПоУмолчаниюПредставление = "", "", ", ") + Строка(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	// Список редактирования форматов
	СписокФорматов = СписокФорматовПоУмолчанию.Скопировать();
	
	// Представление форматов по умолчанию в пределах рассылки
	ФорматыПоУмолчанию = "";
	Найденные = Объект.ФорматыОтчетов.НайтиСтроки(Новый Структура("Отчет", Кэш.ПустоеЗначениеОтчета));
	Если Найденные.Количество() = 0 Тогда
		ФорматыПоУмолчанию = СписокФорматовПоУмолчаниюПредставление;
	Иначе
		Для Каждого СтрокаФормат Из Найденные Цикл
			ФорматыПоУмолчанию = ФорматыПоУмолчанию + ?(ФорматыПоУмолчанию = "", "", ", ") + Строка(СтрокаФормат.Формат);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураВложенийПисьмаВФорматеHTML = Неопределено Тогда
		СтруктураВложенийПисьмаВФорматеHTML = Новый Структура;
	КонецЕсли;
	
	// Список выбора почтовых адресов автора
	ПолучитьСписокПочтовыхАдресов(Объект.Автор, Элементы.ВидПочтовогоАдресаАвтора.СписокВыбора);
	
	// Список выбора почтовых адресов автора
	//ПодключитьКэшНастроекЭлектроннойПочты();
	СписокВыбора = Элементы.ТипПолучателейРассылки.СписокВыбора;
	СписокВыбора.Добавить(Новый ОписаниеТипов("СправочникСсылка.Пользователи"), "Пользователи");
	СписокВыбора.Добавить(Новый ОписаниеТипов("СправочникСсылка.Контрагенты"),  "Контрагенты");
	
	Для Каждого ТекСтрока Из СписокВыбора Цикл
		НоваяСтрока = ТаблицаТиповПолучателей.Добавить();
		НоваяСтрока.ИОМД            = ТекСтрока.Представление;
		НоваяСтрока.ТипПолучателей  = ТекСтрока.Значение;
		НоваяСтрока.Представление   = ТекСтрока.Представление;
		НоваяСтрока.ОсновнойВидКИ   = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий;
		//НоваяСтрока.ГруппаКИ
		ТипПолучателя = ТекСтрока.Значение.Типы()[0];
		НоваяСтрока.ПутьФормыВыбора = Метаданные.НайтиПоТипу(ТипПолучателя).ПолноеИмя()+".ФормаВыбора";
		НоваяСтрока.ОсновнойТип     = ТекСтрока.Значение;
		
		Если НоваяСтрока.ИОМД = Объект.ТипПолучателейРассылки Тогда
			ТипПолучателейРассылки = НоваяСтрока.ТипПолучателей;
			ГруппаКонтакнойИнформацииТипаПолучателей = НоваяСтрока.ГруппаКИ;
			Если Объект.ВидПочтовогоАдресаПолучателей.Пустая() И ЗначениеЗаполнено(НоваяСтрока.ОсновнойВидКИ) Тогда
				Объект.ВидПочтовогоАдресаПолучателей = НоваяСтрока.ОсновнойВидКИ;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Чтение настроек отчетов из объекта копирования
	Если СозданКопированием Тогда
		ПрочитатьНастройкиОтчетовОбъектаКопирования();
	КонецЕсли;
	
	// Активизация первой строки
	Если Объект.Отчеты.Количество() > 0 И ИдентификаторТекущейСтрокиТаблицыОтчетов = -1 Тогда
		СтрокаОтчеты = Объект.Отчеты[0];
		ИдентификаторСтроки = СтрокаОтчеты.ПолучитьИдентификатор();
		СтрокаПредупреждения = ОтчетыПриАктивизацииСтрокиНаСервере(ИдентификаторСтроки);
		Если СтрокаПредупреждения <> "" Тогда
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаПредупреждения, , "Объект.Отчеты[0].Представление");
		КонецЕсли;
	КонецЕсли;
	
	ВидимостьДоступностьКорректность(ЭтаФорма);
	
	ПравоВывода = ПравоДоступа("Вывод", Метаданные);
	
	Элементы.ПолеОтбора.Доступность = (ВидРассылки = "Персонализирована");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ФормаБылаМодифицированаНаСервере Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	Если НЕ ПравоВывода Тогда
		Предупреждение(НСтр("ru = 'Недостаточно прав на вывод информации. Рассылки отчетов не будут выполнены.'"));
	КонецЕсли;
	
	ОграниченияИспользованияПолей = Элементы.ПараметрыДанных.ОграниченияИспользования;
	
	НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
	НовоеОграничение.Параметр    = Новый ПараметрКомпоновкиДанных("ДатаНачала");
	НовоеОграничение.Доступность = Ложь;
	
	НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
	НовоеОграничение.Параметр    = Новый ПараметрКомпоновкиДанных("ДатаОкончания");
	НовоеОграничение.Доступность = Ложь;
	
	НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
	НовоеОграничение.Параметр    = Новый ПараметрКомпоновкиДанных("ДатаОкончанияГраница");
	НовоеОграничение.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Очистка окна сообщений
	ОчиститьСообщения();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Запись настроек текущей строки
	Если ИдентификаторТекущейСтрокиТаблицыОтчетов <> -1 Тогда
		ЗаписатьНастройкиСтрокиОтчеты(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	КонецЕсли;
	
	// Далее выполняются следующие действия:
	// [1] Сохранение настроек, сделанных пользователем.
	//     Помещение измененных настроек строк в настройки записываемого объекта (хранилище значения).
	//     Анализ выполняется для всех отчетов, если пользователь менял настройки.
	// [2] Поиск незаполненных но обязательных для заполнения настроек.
	//     Анализ выполняется для СКД отчетов, если рассылка подготовлена.
	ПроверитьОбязательные = Объект.Подготовлена;
	// [3] Поиск персонализированных полей если эта рассылка не персонализирована.
	//     Анализ выполняется для всех отчетов, если пользователь сменил вид рассылки с персонализированной на любой другой.
	ПроверитьПерсонализированные = (НЕ Объект.Персонализирована И РассылкаБылаПерсонализирована);
	Для Каждого СтрокаОтчеты Из Объект.Отчеты Цикл
		
		ОбъектСтрокаОтчеты = ТекущийОбъект.Отчеты.Получить(СтрокаОтчеты.НомерСтроки-1);
		
		//Если СтрокаОтчеты.ВнесеныИзменения Тогда
		Если СтрокаОтчеты.Инициализирован И ЭтоАдресВременногоХранилища(СтрокаОтчеты.АдресНастроек) Тогда
			// [1], [2] и [3] Чтение неинициализированных настроек
			НастройкиОтчета = ПолучитьИзВременногоХранилища(СтрокаОтчеты.АдресНастроек);
			
			//ОтчетыСервер.ЗагрузитьНастройкиВСКД(НастройкиОтчета, СтрокаОтчеты.РежимВыводаОтчета, СтрокаОтчеты.ИзмеренияСтроки, СтрокаОтчеты.ИзмеренияКолонки, СтрокаОтчеты.ВариантЗагрузкиПериода, Ложь);
			ОтчетыСервер.УстановитьПериодСКДПоВарианту(НастройкиОтчета, СтрокаОтчеты.ВариантЗагрузкиПериода);
			
			ОбъектСтрокаОтчеты.ОтправлятьЕслиПустой = СтрокаОтчеты.ОтправлятьЕслиПустой;
			ОбъектСтрокаОтчеты.Представление        = СтрокаОтчеты.Представление;
			ОбъектСтрокаОтчеты.ИмяОтчета            = СтрокаОтчеты.ИмяОтчета;
			ОбъектСтрокаОтчеты.ВариантЗагрузкиПериода       = СтрокаОтчеты.ВариантЗагрузкиПериода;
			ОбъектСтрокаОтчеты.ПолеОтбора           = СтрокаОтчеты.ПолеОтбора;
			
			// [1] Запись настроек.
			ОбъектСтрокаОтчеты.Настройки = Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9));
			
			Если НЕ ПроверитьОбязательные И НЕ ПроверитьПерсонализированные Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
			
	КонецЦикла; // Для Каждого СтрокаОтчеты Из Объект.Отчеты Цикл
	
	ТекущийОбъект.КартинкиПисьмаВФорматеHTML = Неопределено;
	Если ТекущийОбъект.ПисьмоВФорматеHTML Тогда
		ТекущийОбъект.ТекстПисьма = СокрЛП(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст());
		Если ТекущийОбъект.ТекстПисьма = "" Тогда
			ТекущийОбъект.ТекстПисьмаВФорматеHTML = "";
		Иначе
			ТекстПисьмаФорматированныйДокумент.ПолучитьHTML(ТекущийОбъект.ТекстПисьмаВФорматеHTML, СтруктураВложенийПисьмаВФорматеHTML);
			Если ТипЗнч(СтруктураВложенийПисьмаВФорматеHTML) = Тип("Структура")
				И СтруктураВложенийПисьмаВФорматеHTML.Количество() > 0 Тогда
				ТекущийОбъект.КартинкиПисьмаВФорматеHTML = Новый ХранилищеЗначения(СтруктураВложенийПисьмаВФорматеHTML, Новый СжатиеДанных(9));
			КонецЕсли;
			ТекущийОбъект.ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
		КонецЕсли;
	КонецЕсли;
	
	// Записываем значения
	Если ЗначениеЗаполнено(ТипПолучателейРассылки) Тогда
		Найденные = ТаблицаТиповПолучателей.НайтиСтроки(Новый Структура("ТипПолучателей", ТипПолучателейРассылки));
		Если Найденные.Количество() = 1 Тогда
			ТекущийОбъект.ТипПолучателейРассылки = Найденные[0].ИОМД;
		Иначе
			ТекущийОбъект.ТипПолучателейРассылки = "";
		КонецЕсли;
	Иначе
		ТекущийОбъект.ТипПолучателейРассылки = "";
	КонецЕсли;
	
	// Все операции с регламентными заданиями размещены в модуле объекта
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Расписание", Расписание);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// После записи значения реквизитов табличной части, добавленные в форме, сбрасываются
	ТекущаяСтрока = ИдентификаторТекущейСтрокиТаблицыОтчетов;
	ИдентификаторТекущейСтрокиТаблицыОтчетов = -1;
	ПрочитатьПользовательскоеПредставлениеФорматов();
	ОтчетыПриАктивизацииСтрокиНаСервере(ТекущаяСтрока);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики ожидания

&НаКлиенте
Процедура ОбработчикАктивизацииСтрокиТаблицыОтчеты()
	
	СтрокаОтчеты = Элементы.Отчеты.ТекущиеДанные;
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтроки = СтрокаОтчеты.ПолучитьИдентификатор();
	Если ИдентификаторСтроки = ИдентификаторТекущейСтрокиТаблицыОтчетов Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПредупреждения = ОтчетыПриАктивизацииСтрокиНаСервере(ИдентификаторСтроки);
	Если СтрокаПредупреждения <> "" Тогда
		Предупреждение(СтрокаПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеФоновогоЗадания()
	//РассылкаОтчетовКлиент.ПроверитьВыполнениеФоновогоЗадания(ЭтаФорма);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// Страница "Расписание"

&НаКлиенте
Процедура ВыполнятьПоРасписаниюПриИзменении(Элемент)
	ВидимостьДоступностьКорректность(ЭтаФорма, "ВыполнятьПоРасписанию");
КонецПроцедуры

&НаКлиенте
Процедура МесяцыПриИзменении(Элемент)
	Если Элемент <> Неопределено Тогда
		Расписание.Месяцы = ИзменитьИРазадресоватьМассив(ЭтаФорма[Элемент.Имя], Кэш.Соответствия.Месяцы[Элемент.Имя], Расписание.Месяцы);
	КонецЕсли;
	ВидимостьДоступностьКорректность(ЭтаФорма, "Месяцы");
КонецПроцедуры

&НаКлиенте
Процедура ДниНеделиПриИзменении(Элемент)
	Если Элемент <> Неопределено Тогда
		Расписание.ДниНедели = ИзменитьИРазадресоватьМассив(ЭтаФорма[Элемент.Имя], Кэш.Соответствия.ДниНедели[Элемент.Имя], Расписание.ДниНедели);
	КонецЕсли;
	ВидимостьДоступностьКорректность(ЭтаФорма, "ДниНедели");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасписание(Команда)
	ИзменитьРасписаниеВДиалоге();
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьРасписанияПриИзменении(Элемент)
	ВидимостьДоступностьКорректность(ЭтаФорма, "ПериодичностьРасписания");
	Если Кэш.Перечисления.Периодичности[Объект.ПериодичностьРасписания] = "Произвольное" Тогда
		ИзменитьРасписаниеВДиалоге();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНачалоКонецМесяцаНажатие(Элемент)
	Если Расписание.ДеньВМесяце = 0 Тогда
		ДеньВМесяце = 1;
		Расписание.ДеньВМесяце = -1;
	Иначе
		Расписание.ДеньВМесяце = -Расписание.ДеньВМесяце;
	КонецЕсли;
	Модифицированность = Истина;
	ВидимостьДоступностьКорректность(ЭтаФорма, "НачалоКонецМесяца");
КонецПроцедуры

&НаКлиенте
Процедура ВремяНачалаПриИзменении(Элемент)
	Расписание.ВремяНачала = ВремяНачала;
	ВидимостьДоступностьКорректность(ЭтаФорма, "ВремяНачала");
КонецПроцедуры

&НаКлиенте
Процедура ПериодПовтораДнейПриИзменении(Элемент)
	Расписание.ПериодПовтораДней = ПериодПовтораДней;
	ВидимостьДоступностьКорректность(ЭтаФорма, "ПериодПовтораДней");
КонецПроцедуры

&НаКлиенте
Процедура ДеньМесяцаПриИзменении(Элемент)
	Расписание.ДеньВМесяце = ?(Расписание.ДеньВМесяце >= 0, ДеньВМесяце, -ДеньВМесяце);
	ВидимостьДоступностьКорректность(ЭтаФорма, "ДеньВМесяце");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Страница "Доставка"

&НаКлиенте
Процедура ТипПолучателейРассылкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = ТипПолучателейРассылки Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Найденные = ТаблицаТиповПолучателей.НайтиСтроки(Новый Структура("ТипПолучателей", ВыбранноеЗначение));
	Если Найденные.Количество() <> 1 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	// Очистка получателей (если нужно)
	Если Объект.Получатели.Количество() > 0 Тогда
		
		СтрокаВопроса = НСтр("ru = 'Для продолжения необходимо очистить список получателей.'");
		
		НаборКнопок = Новый СписокЗначений;
		НаборКнопок.Добавить("Очистить", НСтр("ru = 'Очистить'"));
		НаборКнопок.Добавить(КодВозвратаДиалога.Отмена);
		
		Ответ = Вопрос(СтрокаВопроса, НаборКнопок, 60, "Очистить");
		Если Ответ <> "Очистить" Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		Объект.Получатели.Очистить();
		ПолеОтбора = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПолучателейРассылкиПриИзменении(Элемент)
	Найденные = ТаблицаТиповПолучателей.НайтиСтроки(Новый Структура("ТипПолучателей", ТипПолучателейРассылки));
	Если Найденные.Количество() = 1 Тогда
		СтрокаПолучатель = Найденные[0];
		Объект.ТипПолучателейРассылки = СтрокаПолучатель.ИОМД;
		Объект.ВидПочтовогоАдресаПолучателей = СтрокаПолучатель.ОсновнойВидКИ;
		ГруппаКонтакнойИнформацииТипаПолучателей = СтрокаПолучатель.ГруппаКИ;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипПолучателейРассылкиОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВидПочтовогоАдресаАвтораОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Объект.Автор);
КонецПроцедуры

&НаКлиенте
Процедура FTPСерверИКаталогНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПараметрыПроизвольнойФормы = Новый Структура("Сервер, Каталог, Порт, Логин, Пароль, ПассивноеСоединение");
	Для Каждого КлючИЗначение Из ПараметрыПроизвольнойФормы Цикл
		ПараметрыПроизвольнойФормы[КлючИЗначение.Ключ] = Объект["FTP" + КлючИЗначение.Ключ];
	КонецЦикла;
	ПараметрыПроизвольнойФормы.Вставить("Заголовок", НСтр("ru = 'Укажите получателя'"));

	ОткрытьФорму("Справочник.РассылкиОтчетов.Форма.ПараметрыFTP", ПараметрыПроизвольнойФормы, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура FTPСерверИКаталогОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = Неопределено ИЛИ ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	Для Каждого КлючИЗначение Из ВыбранноеЗначение Цикл
		Объект["FTP" + КлючИЗначение.Ключ] = КлючИЗначение.Значение;
	КонецЦикла;
	ВидимостьДоступностьКорректность(ЭтаФорма, "FTPСерверИКаталог");
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура FTPСерверИКаталогОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура FTPСерверИКаталогОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПолныйАдрес = Объект.FTPСервер +":"+ Формат(Объект.FTPПорт, "ЧН=21; ЧГ=0") + Объект.FTPКаталог;
	Если Объект.FTPЛогин = "" Тогда
		ПолныйАдрес = "ftp://"+ ПолныйАдрес;
	Иначе
		ПолныйАдрес = "ftp://"+ Объект.FTPЛогин +":"+ Объект.FTPПароль +"@"+ ПолныйАдрес;
	КонецЕсли;
	ЗапуститьПриложение(ПолныйАдрес, , Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВидРассылкиПриИзменении(Элемент)
	Объект.Личная            = (ВидРассылки = "Личная");
	Объект.Персонализирована = (ВидРассылки = "Персонализирована");
	
	ВидимостьДоступностьКорректность(ЭтаФорма, "ВидРассылки");
	
	Элементы.ПолеОтбора.Доступность = (ВидРассылки = "Персонализирована");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭлектроннуюПочтуПриИзменении(Элемент)
	ВидимостьДоступностьКорректность(ЭтаФорма, "ИспользоватьЭлектроннуюПочту");
	
	Если НЕ Публиковать И НЕ Объект.ИспользоватьЭлектроннуюПочту Тогда
		Публиковать = Истина;
		ВычислитьФлажкиДополнительныхСпособовДоставки();
		ВидимостьДоступностьКорректность(ЭтаФорма, "Публиковать");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТолькоУведомитьПриИзменении(Элемент)
	ВидимостьДоступностьКорректность(ЭтаФорма, "ТолькоУведомить");
КонецПроцедуры

&НаКлиенте
Процедура ДругойСпособДоставкиПриИзменении(Элемент)
	ВычислитьФлажкиДополнительныхСпособовДоставки();
	ВидимостьДоступностьКорректность(ЭтаФорма, "ДругойСпособДоставки");
КонецПроцедуры

&НаКлиенте
Процедура ПубликоватьПриИзменении(Элемент)
	ВычислитьФлажкиДополнительныхСпособовДоставки();
	ВидимостьДоступностьКорректность(ЭтаФорма, "Публиковать");
	
	Если НЕ Публиковать И НЕ Объект.ИспользоватьЭлектроннуюПочту Тогда
		Объект.ИспользоватьЭлектроннуюПочту = Истина;
		ВидимостьДоступностьКорректность(ЭтаФорма, "ИспользоватьЭлектроннуюПочту");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СетевойКаталогWindowsПриИзменении(Элемент)
	Объект.СетевойКаталогWindows = ПроцедурыОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Объект.СетевойКаталогWindows);
	Если ПустаяСтрока(Объект.СетевойКаталогLinux) Тогда
		Объект.СетевойКаталогLinux = СтрЗаменить(Объект.СетевойКаталогWindows, "\", "/");
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СетевойКаталогLinuxПриИзменении(Элемент)
	Объект.СетевойКаталогLinux = ПроцедурыОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Объект.СетевойКаталогLinux);
	Если ПустаяСтрока(Объект.СетевойКаталогWindows) Тогда
		Объект.СетевойКаталогWindows = СтрЗаменить(Объект.СетевойКаталогLinux, "/", "\");
	КонецЕсли; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Страница "Дополнительно"

&НаКлиенте
Процедура ФорматыПоУмолчаниюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаФорматов = ВыбратьФормат(Кэш.ПустоеЗначениеОтчета);
	Если СтрокаФорматов <> Неопределено Тогда
		ФорматыПоУмолчанию = СтрокаФорматов;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФорматыПоУмолчаниюОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОчиститьФормат(Кэш.ПустоеЗначениеОтчета);
	ФорматыПоУмолчанию = СписокФорматовПоУмолчаниюПредставление;
КонецПроцедуры

&НаКлиенте
Процедура АрхивироватьПриИзменении(Элемент)
	ВидимостьДоступностьКорректность(ЭтаФорма, "Архивировать");
КонецПроцедуры

&НаКлиенте
Процедура АвторПриИзменении(Элемент)
	
	ТекущийСписок = Элементы.ВидПочтовогоАдресаАвтора.СписокВыбора;
	ТекущийСписок.Очистить();
	НовыйСписок = ПолучитьСписокПочтовыхАдресов(Объект.Автор);
	Для Каждого ЭлементСписка Из НовыйСписок Цикл
		ЗаполнитьЗначенияСвойств(ТекущийСписок.Добавить(), ЭлементСписка);
	КонецЦикла;
	
	Если НовыйСписок.НайтиПоЗначению(Объект.ВидПочтовогоАдресаПолучателей) = Неопределено Тогда
		Объект.ВидПочтовогоАдресаПолучателей = Неопределено;
	КонецЕсли;
	
	Объект.Пользователь = СокрЛП(Объект.Автор.Код);
	
КонецПроцедуры

&НаКлиенте
Процедура РодительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ВыбранноеЗначение = Кэш.ГруппаЛичныхРассылок Тогда
		СтандартнаяОбработка = Ложь;
		Предупреждение(НСтр("ru = 'Выбранная группа используется только для личных рассылок по электронной почте'"));
	КонецЕсли;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Отчеты

&НаКлиенте
Процедура ОтчетыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикАктивизацииСтрокиТаблицыОтчеты", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПослеУдаления(Элемент)
	ВидимостьДоступностьКорректность(ЭтаФорма, "Отчеты");
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Получатели

&НаКлиенте
Процедура ПолучателиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	ДобавитьПеретащитьПолучателя(ПараметрыПеретаскивания.Значение);
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДобавитьПеретащитьПолучателя(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	// Контроль уникальности выбранных получателей
	Если НЕ Отказ И НЕ ОтменаРедактирования Тогда
		СтрокаПолучатели = Элемент.ТекущиеДанные;
		Если СтрокаПолучатели = Неопределено ИЛИ НЕ ЗначениеЗаполнено(СтрокаПолучатели.Получатель) Тогда
			Возврат;
		КонецЕсли;
		Найденные = Объект.Получатели.НайтиСтроки(Новый Структура("Получатель", СтрокаПолучатели.Получатель));
		Если Найденные.Количество() > 1 Тогда
			// Позиционирование на дубликате
			НомерСуществующейСтроки = ?(Найденные[0] = СтрокаПолучатели, Найденные[1].НомерСтроки, Найденные[0].НомерСтроки);
			Сообщение = НСтр("ru = 'Дублирование получателя " + Строка(СтрокаПолучатели.Получатель) + ". Ввод дубликата отменен.'");
			Поле = "Объект.Получатели["+ Формат(НомерСуществующейСтроки, "ЧН=0; ЧГ=") +"]";
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, , Поле);
			
			// Отмена редактирования
			Элементы.Получатели.ЗакончитьРедактированиеСтроки(Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ФорматыОтчетов

&НаКлиенте
Процедура ФорматыОтчетовФорматыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаОтчеты = Элементы.ФорматыОтчетов.ТекущиеДанные;
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаФорматов = ВыбратьФормат(СтрокаОтчеты.Отчет);
	Если СтрокаФорматов <> Неопределено Тогда
		СтрокаОтчеты.Форматы = СтрокаФорматов;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФорматыОтчетовФорматыОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаОтчеты = Элементы.ФорматыОтчетов.ТекущиеДанные;
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьФормат(СтрокаОтчеты.Отчет);
	СтрокаОтчеты.Форматы = ПредставлениеФорматовПоУмолчанию();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// Страница "Отчеты"

&НаСервере
Процедура ДобавитьНовыйОтчет(НовыйОтчет) Экспорт
	
	ДобавленныеОтчеты = Объект.Отчеты.НайтиСтроки(Новый Структура("Отчет", НовыйОтчет));
	Если ДобавленныеОтчеты.Количество()>0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтчеты = Объект.Отчеты.Добавить();
	СтрокаОтчеты.Представление        = НовыйОтчет.ПредставлениеОтчета + " - " + НовыйОтчет.Наименование;
	СтрокаОтчеты.ИмяОтчета            = НовыйОтчет.ИмяОтчета;
	СтрокаОтчеты.Отчет                = НовыйОтчет;
	СтрокаОтчеты.ОтправлятьЕслиПустой = Истина;
	
	ОтчетОбъект = Неопределено;
	Попытка
		Если НЕ СтруктураАдресовСхемКД.Свойство(СтрокаОтчеты.ИмяОтчета) Тогда
			ОтчетОбъект = Отчеты[СтрокаОтчеты.ИмяОтчета].Создать();
			Адрес = ПоместитьВоВременноеХранилище(ОтчетОбъект.СхемаКомпоновкиДанных, УникальныйИдентификатор);
			СтруктураАдресовСхемКД.Вставить(СтрокаОтчеты.ИмяОтчета, Адрес);
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	СтруктураОграничений = Неопределено;
	Попытка
		// Получим ограниченние на отображение Элементов структуры отчета
		Если ОграниченияОтчетов.Свойство(СтрокаОтчеты.ИмяОтчета) Тогда
			СтруктураОграничений = ОграниченияОтчетов[СтрокаОтчеты.ИмяОтчета];
		Иначе
			Если ОтчетОбъект = Неопределено Тогда
				ОтчетОбъект = Отчеты[СтрокаОтчеты.ИмяОтчета].Создать();
			КонецЕсли;
			СтруктураОграничений = ОтчетОбъект.ПолучениеОграниченийНастроек();
		КонецЕсли;
		Если ТипЗнч(СтруктураОграничений) = Тип("Структура") Тогда
			Для Каждого ТекЭлемент Из СтруктураОграничений Цикл
				СтрокаОтчеты.ОграниченияНастроек.Добавить(ТекЭлемент.Ключ);
			КонецЦикла;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ОграниченияОтчетов.Вставить(СтрокаОтчеты.ИмяОтчета, СтруктураОграничений);
	
	// Получим настройки из варианта отчета
	Настройки = СтрокаОтчеты.Отчет.Настройки.Получить();
	
	Попытка
		Если ОтчетОбъект = Неопределено Тогда
			
			ОтчетОбъект = Отчеты[СтрокаОтчеты.ИмяОтчета].Создать();
		КонецЕсли;
		Попытка
			ОтчетОбъект.ЗаполнитьНачальныеНастройки();
		Исключение
			ОтчетыСервер.ЗаполнитьНачальныеОстатки(ОтчетОбъект);
		КонецПопытки;
		
		ОтчетОбъект.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ОтчетОбъект.СхемаКомпоновкиДанных));
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		ОтчетОбъект.ПриИзмененииВарианта(Неопределено);
		Настройки = ОтчетОбъект.КомпоновщикНастроек.ПолучитьНастройки();
	Исключение
	КонецПопытки;
	
	СтрокаОтчеты.АдресНастроек = ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтчет(Команда)
	
	ВыбранныеЗначения = Новый СписокЗначений;
	Для Каждого СтрокаОтчеты Из Объект.Отчеты Цикл
		Если ТипЗнч(СтрокаОтчеты.Отчет) = Тип("СправочникСсылка.ВариантыОтчетов") Тогда
			ВыбранныеЗначения.Добавить(СтрокаОтчеты.Отчет);
		КонецЕсли;
	КонецЦикла;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПометкаУдаления", Ложь);
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна",  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормыВыбора.Вставить("Отбор",              Отбор);
	ПараметрыФормыВыбора.Вставить("ВыбранныеЗначения",  ВыбранныеЗначения);
	
	ФормаПодбора = ПолучитьФорму("Справочник.РассылкиОтчетов.Форма.ФормаПодбораВариантов", ПараметрыФормыВыбора, Элементы.Отчеты);
	Рез = ФормаПодбора.ОткрытьМодально();
	Если Рез = КодВозвратаДиалога.ОК Тогда
		
		//СтрокаОтчеты = Неопределено;
		ИндексСтроки = Объект.Отчеты.Количество();
		//ДобавленаСтрока = Ложь;
		Для Каждого ТекСтрока Из ФормаПодбора.ПодобранныеВарианты Цикл
			
			ДобавитьНовыйОтчет(ТекСтрока.Отчет);
			
		КонецЦикла;
		
		Если ИндексСтроки < Объект.Отчеты.Количество() Тогда
			
			Модифицированность = Истина;
			
			СтрокаОтчеты = Объект.Отчеты[ИндексСтроки];
			
			// Установка курсора на первый из добавленных элементов
			Элементы.Отчеты.ТекущаяСтрока = СтрокаОтчеты.ПолучитьИдентификатор();
			ОтчетыПриАктивизацииСтрокиНаСервере(Элементы.Отчеты.ТекущаяСтрока, Ложь);
			ВидимостьДоступностьКорректность(ЭтаФорма, "Отчеты");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДополнительныйОтчет(Команда)
	//РассылкаОтчетовКлиент.РассылкаОтчетовПодборДопОтчета(Элементы.Отчеты);
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрОтчета(Команда)
	
	Если ИдентификаторТекущейСтрокиТаблицыОтчетов = -1 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНастройкиСтрокиОтчеты(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	
	СтрокаОтчеты = Объект.Отчеты.НайтиПоИдентификатору(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//ОтчетыСервер.ЗагрузитьНастройкиВСКД(КомпоновщикНастроекКД.Настройки, СтрокаОтчеты.РежимВыводаОтчета, СтрокаОтчеты.ИзмеренияСтроки, СтрокаОтчеты.ИзмеренияКолонки, СтрокаОтчеты.ВариантЗагрузкиПериода);
	ОтчетыСервер.УстановитьПериодСКДПоВарианту(КомпоновщикНастроекКД.Настройки, СтрокаОтчеты.ВариантЗагрузкиПериода);
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("КлючВарианта",            СтрокаОтчеты.Отчет.КлючВарианта);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   СтрокаОтчеты.Отчет.Наименование);
	СтруктураПараметров.Вставить("Вариант",                 ОтчетыСервер.КомпоновщикПолучитьНастройки(КомпоновщикНастроекКД));
	СтруктураПараметров.Вставить("СхемаКомпоновки",         СхемаКомпоновки);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  СтрокаОтчеты.ВариантЗагрузкиПериода);
	
	ИмяФормыОтчета = СтрокаОтчеты.Отчет.КлючОтчета + ".Форма";
	ФормаРезультат = ПолучитьФорму(ИмяФормыОтчета, СтруктураПараметров, ЭтаФорма, ИмяФормыОтчета);
	ФормаРезультат.Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Страница "Расписание"

&НаКлиенте
Процедура УстановитьПометки(Команда)
	ВсеМесяцы = Новый Массив;
	Для Каждого КлючИЗначение Из Кэш.Соответствия.Месяцы Цикл
		ЭтаФорма[КлючИЗначение.Ключ] = Истина;
		ВсеМесяцы.Добавить(КлючИЗначение.Значение);
	КонецЦикла;
	Расписание.Месяцы = ВсеМесяцы;
	ВидимостьДоступностьКорректность(ЭтаФорма, "Месяцы");
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометки(Команда)
	ВсеМесяцы = Новый Массив;
	Для Каждого КлючИЗначение Из Кэш.Соответствия.Месяцы Цикл
		ЭтаФорма[КлючИЗначение.Ключ] = Ложь;
	КонецЦикла;
	Расписание.Месяцы = ВсеМесяцы;
	ВидимостьДоступностьКорректность(ЭтаФорма, "Месяцы");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	СписокВариантов = РассылкаОтчетовКлиентСервер.СписокВариантовЗаполненияРасписаний();
	
	Вариант = СписокВариантов.ВыбратьЭлемент(НСтр("ru = 'Выберите шаблон расписания'"));
	Если Вариант <> Неопределено Тогда
		ЗаполнитьРасписаниеПоВарианту(Вариант.Значение, Истина);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Страница "Доставка"

&НаКлиенте
Процедура ПолучателиПодобрать(Команда)
	ОткрытьФормуВыбораДляПодбораПолучателей(Элементы.Получатели);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзменитьШаблонДатыРассылки(Команда)
	ДобавитьШаблон();
	
	Конструктор = Новый КонструкторФорматнойСтроки;
	Конструктор.ДоступныеТипы = Новый ОписаниеТипов("Дата");
	
	СтарыйТекст = СокрЛП(ТекущийЭлемент.ВыделенныйТекст);
	Префикс = "[ДатаВыполнения(";
	Постфикс = ")]";
	ДлинаПрефикса = СтрДлина(Префикс);
	ДлинаПостфикса = СтрДлина(Постфикс);
	ПозицияПрефикса = Найти(СтарыйТекст, Префикс);
	ПозицияПостфикса = Найти(СтарыйТекст, Постфикс);
	Если ПозицияПрефикса > 0 И ПозицияПостфикса > ПозицияПрефикса Тогда
		ТекстФормата = Сред(СтарыйТекст, ПозицияПрефикса + ДлинаПрефикса, ПозицияПостфикса - ПозицияПрефикса - ДлинаПрефикса);
	Иначе
		ТекстФормата = "";
	КонецЕсли;
	
	Конструктор.Текст = ТекстФормата;
	Если НЕ Конструктор.ОткрытьМодально() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПозицияПрефикса > 0 И ПозицияПостфикса > ПозицияПрефикса Тогда
		Если Конструктор.Текст = ТекстФормата Тогда
			Возврат;
		КонецЕсли;
		ТекущийЭлемент.ВыделенныйТекст = СтрЗаменить(ТекущийЭлемент.ВыделенныйТекст, Префикс + ТекстФормата + Постфикс, Префикс + Конструктор.Текст + Постфикс);
	ИначеЕсли ТекущийЭлемент.ВыделенныйТекст = "" Тогда
		// Форматированный документ некорректно отрабатывает изменения свойства
		//  ВыделенныйТекст, в случае, если ничего не выделено,
		//  поэтому используется альтернативный метод добавления текста.
		Если ТекущийЭлемент = Элементы.ТекстПисьмаФорматированныйДокумент Тогда
			ТекстПисьмаФорматированныйДокумент.Добавить(Префикс + Конструктор.Текст + Постфикс, ТипЭлементаФорматированногоДокумента.Текст);
		Иначе
			ТекущийЭлемент.ВыделенныйТекст = Префикс + Конструктор.Текст + Постфикс;
		КонецЕсли;
	Иначе
		ТекущийЭлемент.ВыделенныйТекст = ТекущийЭлемент.ВыделенныйТекст + Префикс + Конструктор.Текст + Постфикс;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонПолучателя(Команда)
	// Очистка окна сообщений
	ОчиститьСообщения();
	
	//
	Если НЕ Объект.Персонализирована Тогда
		Стр = Элементы.ВидРассылки.СписокВыбора.НайтиПоЗначению("Персонализирована").Представление;
		СообщениеТекст = НСтр("ru = 'Использование получателя в тексте шаблона возможно только для вида рассылки """+Стр+"""'");
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "ВидРассылки");
		Возврат;
	КонецЕсли;
	
	ДобавитьШаблон("[Получатель]");
	РассылкаБылаПерсонализирована = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонСформированныхОтчетов(Команда)
	ДобавитьШаблон("[СформированныеОтчеты]", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонАвтора(Команда)
	ДобавитьШаблон("[Автор]");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонНаименованияРассылки(Команда)
	ДобавитьШаблон("[НаименованиеРассылки]");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонСистемы(Команда)
	ДобавитьШаблон("[ЗаголовокСистемы]");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонСпособаДоставки(Команда)
	ДобавитьШаблон("[СпособДоставки]");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблонПоУмолчанию(Команда)
	ОчищаетсяТема = (ТекущийЭлемент = Элементы.ТемаПисьма);
	
	ЗначениеПредмета = "";
	ШаблонПоУмолчанию = "";
	
	Если ОчищаетсяТема Тогда
		ЗначениеПредмета = Объект.ТемаПисьма;
		ШаблонПоУмолчанию = Кэш.Шаблоны.Тема;
	Иначе
		ШаблонПоУмолчанию = Кэш.Шаблоны.Текст;
		ЗначениеПредмета = СокрЛП(
			?(Объект.ПисьмоВФорматеHTML, 
				ТекстПисьмаФорматированныйДокумент.ПолучитьТекст(),
				Объект.ТекстПисьма
			)
		);
	КонецЕсли;
	
	Ответ = 1;
	Если ЗначениеПредмета = "" Тогда
		// Предмет пуст - надо заполнить без вопросов
		
	ИначеЕсли ЗначениеПредмета = ШаблонПоУмолчанию Тогда
		// Предмет соответствует шаблону - заполнять не требуется
		
		Стр = ?(ОчищаетсяТема, НСтр("ru = 'Тема письма'"), НСтр("ru = 'Текст письма'"));
		ТекстПредупреждения = НСтр("ru = '"+Стр+" уже соответствует шаблону по умолчанию.'");
		Предупреждение(ТекстПредупреждения);
		Возврат;
		
	Иначе
		// Предмет не пуст - надо запросить замену на стандартный шаблон
		
		Предмет = ?(ОчищаетсяТема, НСтр("ru = 'тему письма'"), НСтр("ru = 'текст письма'"));
		ТекстВопроса = НСтр("ru = 'Заменить "+Предмет+" на шаблон по умолчанию?'");
		
		Заголовок = НСтр("ru = 'Добавить в "+Предмет+" шаблон по умолчанию'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(1, НСтр("ru = 'Заменить'"));
		Кнопки.Добавить(2, НСтр("ru = 'Добавить'")); //  без замены
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		Ответ = Вопрос(ТекстВопроса, Кнопки, 60, 1);
	КонецЕсли;
	
	Если Ответ = 1 Тогда
		Если ОчищаетсяТема Тогда
			Объект.ТемаПисьма = ШаблонПоУмолчанию;
		Иначе
			Если Объект.ПисьмоВФорматеHTML Тогда
				ТекстПисьмаФорматированныйДокумент.Удалить();
				ТекстПисьмаФорматированныйДокумент.Добавить(ШаблонПоУмолчанию, ТипЭлементаФорматированногоДокумента.Текст);
			Иначе
				Объект.ТекстПисьма = ШаблонПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Ответ = 2 Тогда
		ДобавитьШаблон(ШаблонПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредпросмотрШаблона(Команда)
	ДобавитьШаблон();
	
	Если ТекущийЭлемент = Элементы.ТекстПисьмаФорматированныйДокумент Тогда
		Шаблон = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
	ИначеЕсли ТекущийЭлемент = Элементы.ТекстПисьма Тогда
		Шаблон = Объект.ТекстПисьма;
	ИначеЕсли ТекущийЭлемент = Элементы.ТемаПисьма Тогда
		Шаблон = Объект.ТемаПисьма;
	КонецЕсли;
	
	СписокОтчетов = "";
	Для Каждого СтрокаОтчет Из Объект.Отчеты Цикл
		СписокОтчетов = СписокОтчетов
		+ Символы.ПС
		+ СтрокаОтчет.Представление
		+ " (" 
		+ ?(СтрокаОтчет.Форматы = ПредставлениеФорматовПоУмолчанию(), ФорматыПоУмолчанию, СтрокаОтчет.Форматы) 
		+ ")";
	КонецЦикла;
	СписокОтчетов = СокрЛ(СписокОтчетов);
	
	Настройки = Новый Структура("НаименованиеРассылки, Автор, ЗаголовокСистемы, ДатаВыполнения, СформированныеОтчеты, СпособДоставки", 
		Объект.Наименование, 
		Объект.Автор, 
		Кэш.ЗаголовокСистемы, 
		ПолучитьДатуСеанса(),
		СписокОтчетов,
		""
	);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(РассылкаОтчетовКлиентСервер.ЗаполнитьШаблон(Шаблон, Настройки));
	ТекстовыйДокумент.Показать();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПубликацию(Команда)
	// Параметры доставки
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("ИспользоватьСетевойКаталог",   Объект.ИспользоватьСетевойКаталог);
	ПараметрыДоставки.Вставить("ИспользоватьFTPРесурс",        Объект.ИспользоватьFTPРесурс);
	ПараметрыДоставки.Вставить("ИспользоватьЭлектроннуюПочту", Ложь);
	
	ПроверитьРассылку(ПараметрыДоставки);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЭлектроннуюПочту(Команда)
	// Параметры доставки
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("ИспользоватьСетевойКаталог",   Ложь);
	ПараметрыДоставки.Вставить("ИспользоватьFTPРесурс",        Ложь);
	ПараметрыДоставки.Вставить("ИспользоватьЭлектроннуюПочту", Истина);
	
	ПроверитьРассылку(ПараметрыДоставки);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТипТекстаНаHTML(Команда)
	Модифицированность = Истина;
	Объект.ПисьмоВФорматеHTML = Истина;
	ТекстПисьмаИзHTML = СокрЛП(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст());
	Если ТекстПисьмаИзHTML <> Объект.ТекстПисьма Тогда
		ТекстПисьмаФорматированныйДокумент.Удалить();
		ТекстПисьмаФорматированныйДокумент.Добавить(Объект.ТекстПисьма, ТипЭлементаФорматированногоДокумента.Текст);
	КонецЕсли;
	ТекущийЭлемент = Элементы.ТекстПисьмаФорматированныйДокумент;
	ВидимостьДоступностьКорректность(ЭтаФорма, "ПисьмоВФорматеHTML");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТипТекстаНаОбычный(Команда)
	Модифицированность = Истина;
	Объект.ПисьмоВФорматеHTML = Ложь;
	ТекстПисьмаИзHTML = СокрЛП(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст());
	Если Объект.ТекстПисьма <> ТекстПисьмаИзHTML Тогда
		Объект.ТекстПисьма = ТекстПисьмаИзHTML;
	КонецЕсли;
	ТекущийЭлемент = Элементы.ТекстПисьма;
	ВидимостьДоступностьКорректность(ЭтаФорма, "ПисьмоВФорматеHTML");
КонецПроцедуры

&НаКлиенте
Процедура ВидПочтовогоАдресаПолучателейНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(ТипПолучателейРассылки) Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Перед выбором почтового адреса необходимо выбрать получателей'"),
			,
			"ТипПолучателейРассылки"
		);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = ПолучитьСписокВидовКИ();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Страница "Дополнительно"

&НаКлиенте
Процедура СнятьПометкиОтправлятьЕслиПустой(Команда)
	Если Объект.Отчеты.Количество() > 0 Тогда
		Модифицированность = Истина;
		Для Каждого СтрОтчет Из Объект.Отчеты Цикл
			СтрОтчет.ОтправлятьЕслиПустой = Ложь;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкиОтправлятьЕслиПустой(Команда)
	Если Объект.Отчеты.Количество() > 0 Тогда
		Модифицированность = Истина;
		Для Каждого СтрОтчет Из Объект.Отчеты Цикл
			СтрОтчет.ОтправлятьЕслиПустой = Истина;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Контекстные вызовы сервера

&НаСервере
Процедура ПрочитатьПользовательскоеПредставлениеФорматов()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаОтчеты Из Объект.Отчеты Цикл
		СтрокаОтчеты.Представление = Строка(СтрокаОтчеты.Отчет);
		СтрокаОтчеты.Форматы = "";
		Найденные = Объект.ФорматыОтчетов.НайтиСтроки(Новый Структура("Отчет", СтрокаОтчеты.Отчет));
		Для Каждого СтрокаФормат Из Найденные Цикл
			СтрокаОтчеты.Форматы = СтрокаОтчеты.Форматы + ?(СтрокаОтчеты.Форматы = "", "", ", ") + Строка(СтрокаФормат.Формат);
		КонецЦикла;
		Если СтрокаОтчеты.Форматы = "" Тогда
			СтрокаОтчеты.Форматы = ПредставлениеФорматовПоУмолчанию();
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРасписаниеРегламентногоЗадания()
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторЗадания = ?(СозданКопированием, ЗначениеКопирования.РегламентноеЗадание, Объект.РегламентноеЗадание);
	Если ТипЗнч(ИдентификаторЗадания) = Тип("УникальныйИдентификатор") Тогда
		Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
		Если Задание <> Неопределено Тогда
			Расписание = Задание.Расписание;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиОтчетовОбъектаКопирования()
	КоличествоСтрок = Объект.Отчеты.Количество();
	Для ОбратныйИндекс = 1 По КоличествоСтрок Цикл
		Индекс = КоличествоСтрок - ОбратныйИндекс;
		СтрокаОтчеты = Объект.Отчеты.Получить(Индекс);
		СтрокаОтчетыОбъектаКопирования = ЗначениеКопирования.Отчеты.Получить(Индекс);
		
		НастройкиКД = СтрокаОтчетыОбъектаКопирования.Настройки.Получить();
		
		СтрокаОтчеты.ВнесеныИзменения = Истина;
		
		ИдентификаторСтроки = СтрокаОтчеты.ПолучитьИдентификатор();
		СтрокаПредупреждения = ОтчетыПриАктивизацииСтрокиНаСервере(ИдентификаторСтроки, Истина, НастройкиКД);
		Если СтрокаПредупреждения <> "" Тогда
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаПредупреждения, , "Объект.Отчеты["+ Индекс +"].Представление");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПустыеШаблоныСтандартными(ТекущийОбъект)
	// Данные объекта
	Если ПустаяСтрока(ТекущийОбъект.ТемаПисьма) Тогда
		ТекущийОбъект.ТемаПисьма = Кэш.Шаблоны.Тема;
	КонецЕсли;
	Если ПустаяСтрока(ТекущийОбъект.ТекстПисьма) Тогда
		ТекущийОбъект.ТекстПисьма = Кэш.Шаблоны.Текст;
	КонецЕсли;
	Если ПустаяСтрока(ТекущийОбъект.ИмяАрхива) Тогда
		ТекущийОбъект.ИмяАрхива = Кэш.Шаблоны.ИмяАрхива;
	КонецЕсли;
	// Данные формы
	Если ПустаяСтрока(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст()) Тогда
		ТекстПисьмаФорматированныйДокумент.Добавить(Кэш.Шаблоны.Текст, ТипЭлементаФорматированногоДокумента.Текст);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиСтрокиОтчеты(ИдентификаторСтроки)
	
	СтрокаОтчеты = Объект.Отчеты.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтрокаОтчеты.Инициализирован Тогда
		СохраняемоеЗначение = Неопределено;
	ИначеЕсли СтрокаОтчеты.СКД Тогда
		СтрокаОтчеты.ВариантЗагрузкиПериода = ТекущийВариантПериода;
		СтрокаОтчеты.ПолеОтбора     = ПолеОтбора;
		
		Если ЕстьДатаНачала Тогда
			
			ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаНачала"));
			Если НЕ ДоступныйПараметр = Неопределено Тогда
				КомпоновщикНастроекКД.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала", ДатаНачала);
			КонецЕсли;
			
			ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("НачалоПериода"));
			Если НЕ ДоступныйПараметр = Неопределено Тогда
				КомпоновщикНастроекКД.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериода", ДатаНачала);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЕстьДатаОкончания Тогда
			
			// Конец периода
			ЗначениеПериода = ?(ДатаОкончания = Дата(1, 1, 1), КонецДня(ТекущаяДата()), КонецДня(ДатаОкончания));
			
			ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаОкончания"));
			Если НЕ ДоступныйПараметр = Неопределено Тогда
				КомпоновщикНастроекКД.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", ЗначениеПериода);
			КонецЕсли;
			
			// Конец периода с границей
			ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаОкончанияГраница"));
			Если НЕ ДоступныйПараметр = Неопределено Тогда
				КомпоновщикНастроекКД.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончанияГраница", Новый Граница(ЗначениеПериода, ВидГраницы.Включая));
			КонецЕсли;
			
			// Период
			ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("Период"));
			Если НЕ ДоступныйПараметр = Неопределено Тогда
				КомпоновщикНастроекКД.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Период", ЗначениеПериода);
			КонецЕсли;
			
			ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("КонецПериода"));
			Если НЕ ДоступныйПараметр = Неопределено Тогда
				КомпоновщикНастроекКД.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КонецПериода", ЗначениеПериода);
			КонецЕсли;
			
		КонецЕсли;
		
		СохраняемоеЗначение = КомпоновщикНастроекКД.Настройки;
	Иначе
		СохраняемоеЗначение = Неопределено;
	КонецЕсли;
	
	СтрокаОтчеты.ПредставлениеСтрок   = ПредставлениеСтрок;
	СтрокаОтчеты.ПредставлениеКолонок = ПредставлениеКолонок;
	
	Адрес = ?(ЭтоАдресВременногоХранилища(СтрокаОтчеты.АдресНастроек), СтрокаОтчеты.АдресНастроек, УникальныйИдентификатор);
	
	СтрокаОтчеты.АдресНастроек = ПоместитьВоВременноеХранилище(СохраняемоеЗначение, Адрес);
	
КонецПроцедуры

&НаСервере
Функция ОтчетыПриАктивизацииСтрокиНаСервере(ИдентификаторСтроки, ДобавитьДежурнуюФразу = Истина, Настройки = Неопределено)
	
	Если ИдентификаторСтроки = ИдентификаторТекущейСтрокиТаблицыОтчетов Тогда
		Возврат "";
	КонецЕсли;
	
	// Сохранение настроек предыдущего отчета
	Если ИдентификаторТекущейСтрокиТаблицыОтчетов <> -1 Тогда
		ЗаписатьНастройкиСтрокиОтчеты(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	КонецЕсли;
	
	ИдентификаторТекущейСтрокиТаблицыОтчетов = ИдентификаторСтроки;
	
	// Поиск строки
	СтрокаОтчеты = Объект.Отчеты.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаОтчеты = Неопределено Тогда
		ИдентификаторТекущейСтрокиТаблицыОтчетов = -1;
		Возврат "";
	КонецЕсли;
	
	Если Настройки = Неопределено Тогда
		// Чтение настроек текущей строки из временного хранилища или из табличной части по ссылке.
		Если ЭтоАдресВременногоХранилища(СтрокаОтчеты.АдресНастроек) Тогда
			Настройки = ПолучитьИзВременногоХранилища(СтрокаОтчеты.АдресНастроек);
		Иначе
			ИндексСтроки = Объект.Отчеты.Индекс(СтрокаОтчеты);
			ОбъектСтрокаОтчеты = РеквизитФормыВЗначение("Объект").Отчеты.Получить(ИндексСтроки);
			Настройки = ?(ОбъектСтрокаОтчеты = Неопределено, Неопределено, ОбъектСтрокаОтчеты.Настройки.Получить());
		КонецЕсли;
	КонецЕсли;
	
	// Заполним 
	СтрокаОтчеты.СКД = Истина;
	
	Если СтрокаОтчеты.Инициализирован Тогда
		
		Если ((НЕ ИмяОтчета = СтрокаОтчеты.ИмяОтчета) ИЛИ КомпоновщикНастроекКД.Настройки.ДоступныеПоляВыбора.Элементы.Количество() = 0) И СтруктураАдресовСхемКД.Свойство(СтрокаОтчеты.ИмяОтчета) Тогда
			СхемаКомпоновки = СтруктураАдресовСхемКД[СтрокаОтчеты.ИмяОтчета];
			КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
			ИмяОтчета = СтрокаОтчеты.ИмяОтчета;
		КонецЕсли;
		КомпоновщикНастроекКД.ЗагрузитьНастройки(Настройки);
		
	Иначе
		СтрокаОтчеты.Инициализирован = Истина;
		
		Если СтрокаОтчеты.ИмяОтчета = "" Тогда
			СтрокаОтчеты.ИмяОтчета = СтрокаОтчеты.Отчет.ИмяОтчета;
		КонецЕсли;
		
		ОтчетОбъект = Неопределено;
		Попытка
			Если НЕ СтруктураАдресовСхемКД.Свойство(СтрокаОтчеты.ИмяОтчета) Тогда
				ОтчетОбъект = Отчеты[СтрокаОтчеты.ИмяОтчета].Создать();
				Адрес = ПоместитьВоВременноеХранилище(ОтчетОбъект.СхемаКомпоновкиДанных, УникальныйИдентификатор);
				СтруктураАдресовСхемКД.Вставить(СтрокаОтчеты.ИмяОтчета, Адрес);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		СтруктураОграничений = Неопределено;
		Попытка
			// Получим ограниченние на отображение Элементов структуры отчета
			Если ОграниченияОтчетов.Свойство(СтрокаОтчеты.ИмяОтчета) Тогда
				СтруктураОграничений = ОграниченияОтчетов[СтрокаОтчеты.ИмяОтчета];
			Иначе
				Если ОтчетОбъект = Неопределено Тогда
					ОтчетОбъект = Отчеты[СтрокаОтчеты.ИмяОтчета].Создать();
				КонецЕсли;
				СтруктураОграничений = ОтчетОбъект.ПолучениеОграниченийНастроек();
			КонецЕсли;
			
			Если ТипЗнч(СтруктураОграничений) = Тип("Структура") Тогда
				Для Каждого ТекЭлемент Из СтруктураОграничений Цикл
					СтрокаОтчеты.ОграниченияНастроек.Добавить(ТекЭлемент.Ключ);
				КонецЦикла;
				
				Если СтруктураОграничений.Свойство("РежимВыводаОтчета") Тогда
					Элементы.ГруппаРежимВыводаОтчета.Видимость = Ложь;
				КонецЕсли;
				
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ОграниченияОтчетов.Вставить(СтрокаОтчеты.ИмяОтчета, СтруктураОграничений);
		
		Если (НЕ ИмяОтчета = СтрокаОтчеты.ИмяОтчета) И СтруктураАдресовСхемКД.Свойство(СтрокаОтчеты.ИмяОтчета) Тогда
			СхемаКомпоновки = СтруктураАдресовСхемКД[СтрокаОтчеты.ИмяОтчета];
			КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
			//КомпоновщикНастроекКД.Восстановить();
			ИмяОтчета = СтрокаОтчеты.ИмяОтчета;
		КонецЕсли;
		
		КомпоновщикНастроекКД.ЗагрузитьНастройки(Настройки);
		
		ТипКонтрагенты = Тип("СправочникСсылка.Контрагенты");
		ТипСотрудники  = Тип("СправочникСсылка.Сотрудники");
		Для Каждого ТекОтбор Из КомпоновщикНастроекКД.Настройки.ДоступныеПоляОтбора.Элементы Цикл
			
			Если ТекОтбор.Тип.СодержитТип(ТипКонтрагенты) Тогда
				Представление = ТекОтбор.Заголовок + "_Истина";
				СтрокаОтчеты.СписокВыбораПоляОтбора.Добавить(СокрЛП(ТекОтбор.Поле), Представление);
			ИначеЕсли ТекОтбор.Тип.СодержитТип(ТипСотрудники) Тогда
				Представление = ТекОтбор.Заголовок + "_Ложь";
				СтрокаОтчеты.СписокВыбораПоляОтбора.Добавить(СокрЛП(ТекОтбор.Поле), Представление);
			КонецЕсли;
			
		КонецЦикла;
		
		// Определим режим вывода отчета
		СтрокаОтчеты.РежимВыводаОтчета = 1;
		//Если (НЕ Настройки = Неопределено) И Настройки.Структура.Количество() > 0 Тогда
		//	СтруктураОтчета = Настройки.Структура[0];
		//	
		//	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
		//	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
		//	
		//	ТипСтруктуры = ТипЗнч(СтруктураОтчета);
		//	Если ТипСтруктуры = ТипСтруктурыТаблицы Тогда
		//		СтрокаОтчеты.РежимВыводаОтчета = 2;
		//	ИначеЕсли ТипСтруктуры = ТипСтруктурыДиаграммы Тогда
		//		СтрокаОтчеты.РежимВыводаОтчета = 3;
		//	Иначе
		//		СтрокаОтчеты.РежимВыводаОтчета = 1;
		//	КонецЕсли;
		//	
		//	//ЗаполнитьСтруктуруОтчета(СтрокаОтчеты);
		//КонецЕсли;
		
	КонецЕсли;
	
	//ПредставлениеСтрок   = СтрокаОтчеты.ПредставлениеСтрок;
	//ПредставлениеКолонок = СтрокаОтчеты.ПредставлениеКолонок;
	
	СписокВыбора = Элементы.ПолеОтбора.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого ТекЭлемент Из СтрокаОтчеты.СписокВыбораПоляОтбора Цикл
		СписокВыбора.Добавить(ТекЭлемент.Значение, ТекЭлемент.Представление);
	КонецЦикла;
	
	//ОбновитьОписаниеГруппировок();
	//
	//ОпределитьРежимВыводаОтчета(СтрокаОтчеты);
	
	ТекущийВариантПериода = СтрокаОтчеты.ВариантЗагрузкиПериода;
	ПолеОтбора            = СтрокаОтчеты.ПолеОтбора;
	Если ТекущийВариантПериода = "" Тогда
		ТекущийВариантПериода = "ВариантСтандартногоПериода.ПроизвольныйПериод";
	КонецЕсли;
	
	ЕстьДатаНачала    = Ложь;
	ЕстьДатаОкончания = Ложь;
	
	Счетчик = 0;
	Для Каждого ТекущийПараметр Из КомпоновщикНастроекКД.Настройки.ПараметрыДанных.Элементы Цикл
		
		ИмяПараметра = СокрЛП(ТекущийПараметр.Параметр);
		Если ИмяПараметра = "ДатаНачала" ИЛИ ИмяПараметра = "НачалоПериода" Тогда
			ЕстьДатаНачала = (ТекущийПараметр.Использование ИЛИ ЕстьДатаНачала);
			Продолжить;
		КонецЕсли;
		
		Если ИмяПараметра = "ДатаОкончания" ИЛИ ИмяПараметра = "ДатаОкончанияГраница" ИЛИ ИмяПараметра = "Период" ИЛИ ИмяПараметра = "КонецПериода" Тогда
			// Начальная дата не бывает без конечной
			ЕстьДатаОкончания = (ТекущийПараметр.Использование ИЛИ ЕстьДатаОкончания ИЛИ ЕстьДатаНачала);
			Продолжить;
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	Элементы.ПараметрыДанных.Видимость = (Счетчик>0);
	
	Если ТекущийВариантПериода = "ВариантСтандартногоПериода.ПроизвольныйПериод" Тогда
		Элементы.ДатаНачала.Видимость    = ЕстьДатаНачала;
		Элементы.ДатаОкончания.Видимость = ЕстьДатаОкончания;
	Иначе
		Элементы.ДатаНачала.Видимость    = Ложь;
		Элементы.ДатаОкончания.Видимость = Ложь;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Функция ИнициализироватьОтчет(СтрокаОтчеты, ДобавитьДежурнуюФразу, ПользовательскиеНастройки, Интерактивно = Истина)
	
	// Инициализация отчета
	ПараметрыОтчета = Новый Структура("Отчет, Настройки", СтрокаОтчеты.Отчет, ПользовательскиеНастройки);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРасписаниеПоВарианту(Вариант, ОбновитьВидимость = Ложь)
	// Список вариантов - см. РассылкаОтчетовКлиентСервер.СписокВариантовЗаполненияРасписаний()
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	
	// в 7:30 утра
	Расписание.ВремяНачала = '00010101073000';
	
	// каждый день
	Расписание.ПериодПовтораДней = 1;
	
	// по дням недели
	ДеньНеделиМин = 1;
	ДеньНеделиМакс = 7;
	
	// по всем месяцам
	ВсеМесяцы = Новый Массив;
	Для н = 1 По 12 Цикл
		ВсеМесяцы.Добавить(н);
	КонецЦикла;
	Расписание.Месяцы = ВсеМесяцы;
	
	Если Вариант = 1 Тогда // "Каждый день"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежедневно;
		
	ИначеЕсли Вариант = 2 Тогда // "Каждый второй день"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежедневно;
		Расписание.ПериодПовтораДней = 2;
		
	ИначеЕсли Вариант = 3 Тогда // "Каждый четвертый день"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежедневно;
		Расписание.ПериодПовтораДней = 4;
		
	ИначеЕсли Вариант = 4 Тогда // "По будням"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Еженедельно;
		ДеньНеделиМин = 1;
		ДеньНеделиМакс = 5;
		
	ИначеЕсли Вариант = 5 Тогда // "По выходным"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Еженедельно;
		Расписание.ВремяНачала = '00010101220000'; // в 10:00 вечера
		ДеньНеделиМин = 6;
		ДеньНеделиМакс = 7;
		
	ИначеЕсли Вариант = 6 Тогда // "По понедельникам"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Еженедельно;
		ДеньНеделиМин = 1;
		ДеньНеделиМакс = 1;
		
	ИначеЕсли Вариант = 7 Тогда // "По пятницам"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Еженедельно;
		ДеньНеделиМин = 5;
		ДеньНеделиМакс = 5;
		
	ИначеЕсли Вариант = 8 Тогда // "По воскресеньям"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Еженедельно;
		Расписание.ВремяНачала = '00010101220000'; // в 10:00 вечера
		ДеньНеделиМин = 7;
		ДеньНеделиМакс = 7;
		
	ИначеЕсли Вариант = 9 Тогда // "В первый день месяца"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежемесячно;
		Расписание.ДеньВМесяце = 1;
		
	ИначеЕсли Вариант = 10 Тогда // "В последний день месяца"
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежемесячно;
		Расписание.ДеньВМесяце = -1;
		
	ИначеЕсли Вариант = 11 Тогда // "Каждый квартал десятого числа"
		ВсеМесяцы = Новый Массив;
		ВсеМесяцы.Добавить(1);
		ВсеМесяцы.Добавить(4);
		ВсеМесяцы.Добавить(7);
		ВсеМесяцы.Добавить(10);
		Расписание.Месяцы = ВсеМесяцы;
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежемесячно;
		Расписание.ДеньВМесяце = 10;
		
	ИначеЕсли Вариант = 12 Тогда // "Другое..."
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Произвольное;
	
	Иначе
		Объект.ПериодичностьРасписания = Перечисления.ПериодичностиРасписанийРассылокОтчетов.Ежедневно;
		
	КонецЕсли;
	
	// по дням недели
	ВыбранныеДниНедели = Новый Массив;
	Для н = ДеньНеделиМин По ДеньНеделиМакс Цикл
		ВыбранныеДниНедели.Добавить(н);
	КонецЦикла;
	Расписание.ДниНедели = ВыбранныеДниНедели;
	
	Если ОбновитьВидимость Тогда
		ВидимостьДоступностьКорректность(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьСпособДоставки(Рассылка, Знач ПараметрыДоставки)
	Если Модифицированность И НЕ Записать() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыДоставки.Вставить("ДатаВыполнения", ТекущаяДатаСеанса());
	
	// Инициализация параметров записи в журнал регистрации
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыЖурнала = Новый Структура;
	ПараметрыЖурнала.Вставить("ИмяСобытия", НСтр("ru = 'Рассылка отчетов. Проверка способа доставки'"));
	ПараметрыЖурнала.Вставить("Данные", Рассылка);
	ПараметрыЖурнала.Вставить("Метаданные", Метаданные.Справочники.РассылкиОтчетов);
	ПараметрыЖурнала.Вставить("МассивОшибок", Новый Массив);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	// Добавление параметров доставки для записи результатов выполнения
	ПараметрыДоставки.Вставить("БылиОшибки",                  Ложь);
	ПараметрыДоставки.Вставить("БылиПредупреждения",          Ложь);
	ПараметрыДоставки.Вставить("ВыполненаВСетевойКаталог",    Ложь);
	ПараметрыДоставки.Вставить("ВыполненаНаFTP",              Ложь);
	ПараметрыДоставки.Вставить("ВыполненаПоЭлектроннойПочте", Ложь);
	
	// Запись пустого табличного документа в html 4
	ПолноеИмяФайла = ПолучитьИмяВременногоФайла(".html");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.HTML4);
	
	// Формирование вложений
	Файл = Новый Файл(ПолноеИмяФайла);
	
	Вложения = Новый Соответствие;
	Вложения.Вставить(Файл.Имя, Файл.ПолноеИмя);
	
	Выполнена = РассылкаОтчетов.ВыполнитьДоставку(ПараметрыЖурнала, ПараметрыДоставки, Вложения);
	
	// Очистка вложений
	Для Каждого Вложение Из Вложения Цикл
		УдалитьФайлы(Вложение.Значение);
	КонецЦикла;
	
	Результат = Новый Структура("Оповестить, Заголовок, Текст, ТекстОшибок");
	Результат.Оповестить = Ложь;
	Результат.ТекстОшибок = РассылкаОтчетовКлиентСервер.СтрокаСообщенийПользователю(ПараметрыЖурнала.МассивОшибок);
	Если Выполнена Тогда
		Результат.Текст = НСтр("ru = 'Тест способа доставки успешно пройден.'");
	Иначе
		Результат.Текст = НСтр("ru = 'Тест способа доставки не пройден.'");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ВыбранныеПолучателиРассылки()
	
	ВыбранныеПользователи = Новый ТаблицаЗначений;
	ВыбранныеПользователи.Колонки.Добавить("Пользователь");
	ВыбранныеПользователи.Колонки.Добавить("НомерКартинки");
	
	ПолучателиРассылки = Объект.Получатели.Выгрузить(, "Получатель");
	
	Для каждого Элемент Из ПолучателиРассылки Цикл
		
		СтрокаВыбранныеПользователи = ВыбранныеПользователи.Добавить();
		СтрокаВыбранныеПользователи.Пользователь = Элемент.Получатель;
		
	КонецЦикла;
	
	ЗаголовокФормыПодбора = НСтр("ru = 'Подбор получателей рассылки'");
	ПараметрыРасширеннойФормыПодбора = Новый Структура("ЗаголовокФормыПодбора, ВыбранныеПользователи",
	                                                   ЗаголовокФормыПодбора, ВыбранныеПользователи);
	АдресХранилища = ПоместитьВоВременноеХранилище(ПараметрыРасширеннойФормыПодбора);
	Возврат АдресХранилища;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Неконтекстные вызовы сервера

&НаСервереБезКонтекста
Функция ПолучитьКэш()
	// Преобразование наименований в значения
	ДниНедели = Новый Соответствие;
	ДниНедели.Вставить("Понедельник", 1);
	ДниНедели.Вставить("Вторник",     2);
	ДниНедели.Вставить("Среда",       3);
	ДниНедели.Вставить("Четверг",     4);
	ДниНедели.Вставить("Пятница",     5);
	ДниНедели.Вставить("Суббота",     6);
	ДниНедели.Вставить("Воскресенье", 7);
	ДниНедели = Новый ФиксированноеСоответствие(ДниНедели);
	
	Месяцы = Новый Соответствие;
	Месяцы.Вставить("Январь",   1);
	Месяцы.Вставить("Февраль",  2);
	Месяцы.Вставить("Март",     3);
	Месяцы.Вставить("Апрель",   4);
	Месяцы.Вставить("Май",      5);
	Месяцы.Вставить("Июнь",     6);
	Месяцы.Вставить("Июль",     7);
	Месяцы.Вставить("Август",   8);
	Месяцы.Вставить("Сентябрь", 9);
	Месяцы.Вставить("Октябрь",  10);
	Месяцы.Вставить("Ноябрь",   11);
	Месяцы.Вставить("Декабрь",  12);
	Месяцы = Новый ФиксированноеСоответствие(Месяцы);
	
	Периодичности = Новый Соответствие;
	Периодичности.Вставить(Перечисления.ПериодичностиРасписанийРассылокОтчетов.ПустаяСсылка(), "НеВыбрано");
	Для Каждого ОбъектМД Из Метаданные.Перечисления.ПериодичностиРасписанийРассылокОтчетов.ЗначенияПеречисления Цикл
		Периодичности.Вставить(Перечисления.ПериодичностиРасписанийРассылокОтчетов[ОбъектМД.Имя], ОбъектМД.Имя);
	КонецЦикла;
	Периодичности = Новый ФиксированноеСоответствие(Периодичности);
	
	Шаблоны = Новый ФиксированнаяСтруктура("Тема, Текст, ИмяАрхива",
		РассылкаОтчетовКлиентСервер.ШаблонТемы(),
		РассылкаОтчетовКлиентСервер.ШаблонТекста(),
		РассылкаОтчетовКлиентСервер.ШаблонИмениАрхива()
	);
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПустоеЗначениеОтчета", Справочники.ВариантыОтчетов.ПустаяСсылка());
	Кэш.Вставить("ГруппаЛичныхРассылок", Справочники.РассылкиОтчетов.ЛичныеРассылки);
	Кэш.Вставить("ЗаголовокСистемы", РассылкаОтчетовПовтИсп.ИмяЭтойИнформационнойБазы());
	Кэш.Вставить("Соответствия", Новый ФиксированнаяСтруктура("ДниНедели, Месяцы", ДниНедели, Месяцы));
	Кэш.Вставить("Перечисления", Новый ФиксированнаяСтруктура("Периодичности", Периодичности));
	Кэш.Вставить("Шаблоны", Шаблоны);
	Кэш = Новый ФиксированнаяСтруктура(Кэш);
	
	Возврат Кэш;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокПочтовыхАдресов(Получатель, СписокЗначений = Неопределено)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	тчКонтактнаяИнформация.Представление КАК Представление,
	|	тчКонтактнаяИнформация.Вид КАК Значение
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК тчКонтактнаяИнформация
	|ГДЕ
	|	тчКонтактнаяИнформация.Объект = &Получатель И 
	|	тчКонтактнаяИнформация.Тип = &ТипКИ";
	
	Если СписокЗначений = Неопределено Тогда
		СписокЗначений = Новый СписокЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Если ТипЗнч(Получатель) = Тип("СправочникСсылка.Пользователи") Тогда
		Запрос.УстановитьПараметр("Получатель", Получатель.Сотрудник);
	Иначе
		Запрос.УстановитьПараметр("Получатель", Получатель);
	КонецЕсли;
	Запрос.УстановитьПараметр("ТипКИ",      Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.Текст = ТекстЗапроса;
	
	ТЗИтог = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрИтог Из ТЗИтог Цикл
		Если ЗначениеЗаполнено(СтрИтог.Представление) Тогда
			СписокЗначений.Добавить(СтрИтог.Значение, СтрИтог.Представление +" ("+ Строка(СтрИтог.Значение) +")");
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокЗначений;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Вызовы клиента и сервера

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьСтраницу(Элементы, ИмяНабораСтраниц, СуффиксСтраниц)
	Элементы[ИмяНабораСтраниц].ТекущаяСтраница = Элементы[ИмяНабораСтраниц + СуффиксСтраниц];
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьДоступностьКорректность(ЭтаФорма, Изменения = "")
	Объект = ЭтаФорма.Объект;
	Элементы = ЭтаФорма.Элементы;
	
	Если Изменения = "" ИЛИ Изменения = "FTPСерверИКаталог" Тогда
		ЭтаФорма.FTPСерверИКаталог = "ftp://"+ Объект.FTPСервер + Объект.FTPКаталог;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "ВидРассылки" Тогда
		// Корректность
		Если Объект.Личная И Объект.Персонализирована Тогда
			Объект.Личная = Ложь;
		КонецЕсли;
		
		ИспользуетсяГруппаЛичныхРассылок = (Объект.Родитель = ЭтаФорма.Кэш.ГруппаЛичныхРассылок);
		Если Объект.Личная <> ИспользуетсяГруппаЛичныхРассылок Тогда
			УстановитьМодифицированностьФормы(ЭтаФорма, "Родитель", , 
				НСтр("ru = 'Группа установлена в соответствии с видом рассылки'")
			);
			Объект.Родитель = ?(Объект.Личная, ЭтаФорма.Кэш.ГруппаЛичныхРассылок, Неопределено);
		КонецЕсли;
		
		Если Объект.Личная Тогда
			РассылкаОбщая = Ложь;
			ЭтаФорма.ВидРассылки = "Личная";
		ИначеЕсли Объект.Персонализирована Тогда
			РассылкаОбщая = Ложь;
			ЭтаФорма.ВидРассылки = "Персонализирована";
		Иначе
			РассылкаОбщая = Истина;
			ЭтаФорма.ВидРассылки = "Общая";
		КонецЕсли;
		
		Если НЕ РассылкаОбщая Тогда
			Объект.ИспользоватьСетевойКаталог   = Ложь;
			Объект.ИспользоватьFTPРесурс        = Ложь;
			Объект.ИспользоватьЭлектроннуюПочту = Истина;
		КонецЕсли;
		
		// Видимость & Доступность
		Элементы.Родитель.Доступность = НЕ Объект.Личная;
		Элементы.НастройкаПолучателей.Видимость = НЕ Объект.Личная;
		ПереключитьСтраницу(Элементы, "ВидыРассылки", ?(Объект.Личная, "Личная", "ДляПолучателей"));
		Элементы.ДругиеСпособыДоставки.Видимость = РассылкаОбщая;
		Элементы.ГруппаИспользоватьЭлектроннуюПочту.Видимость = РассылкаОбщая;
		
		// Восстановление параметров
		Если Объект.ИспользоватьСетевойКаталог Тогда
			ЭтаФорма.ДругойСпособДоставки = "ИспользоватьСетевойКаталог";
			ЭтаФорма.Публиковать = Истина;
		ИначеЕсли Объект.ИспользоватьFTPРесурс Тогда
			ЭтаФорма.ДругойСпособДоставки = "ИспользоватьFTPРесурс";
			ЭтаФорма.Публиковать = Истина;
		Иначе
			ЭтаФорма.ДругойСпособДоставки = Элементы.ДругойСпособДоставки.СписокВыбора[0].Значение;
			ЭтаФорма.Публиковать = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "Отчеты" Тогда
		КоличествоОтчетов = ЭтаФорма.Объект.Отчеты.Количество();
		Если КоличествоОтчетов > 0 Тогда
			Элементы.СтраницаОтчеты.Заголовок = НСтр("ru = 'Отчеты ("+Формат(КоличествоОтчетов, "ЧН=0; ЧГ=")+")'");
		Иначе
			Элементы.СтраницаОтчеты.Заголовок = НСтр("ru = 'Отчеты'") ;
		КонецЕсли;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "ДругойСпособДоставки" ИЛИ Изменения = "Публиковать" ИЛИ Изменения = "ВидРассылки" Тогда
		Элементы.ДругойСпособДоставки.Доступность = ЭтаФорма.Публиковать;
		Элементы.ПараметрыДоставки.Доступность = ЭтаФорма.Публиковать;
		Элементы.ПараметрыДоставки.ТекущаяСтраница = Элементы[ЭтаФорма.ДругойСпособДоставки];
		Элементы.ПроверитьПубликацию.Доступность = ЭтаФорма.Публиковать;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "ИспользоватьЭлектроннуюПочту" ИЛИ Изменения = "ВидРассылки" Тогда
		Элементы.ГруппаУчетнаяЗапись.Доступность = Объект.ИспользоватьЭлектроннуюПочту;
		Элементы.ПараметрыПисьма.Доступность = Объект.ИспользоватьЭлектроннуюПочту;
		Элементы.ДополнительныеПараметрыРассылкиПоЭлектроннойПочте.Доступность = Объект.ИспользоватьЭлектроннуюПочту;
	КонецЕсли;
	
	Если Изменения = "" 
			ИЛИ Изменения = "ТолькоУведомить" 
			ИЛИ Изменения = "ИспользоватьЭлектроннуюПочту" 
			ИЛИ Изменения = "ДругойСпособДоставки" 
			ИЛИ Изменения = "Публиковать" 
			ИЛИ Изменения = "ВидРассылки" 
		Тогда 
		
		ВидимостьОпции = (Объект.ИспользоватьЭлектроннуюПочту И ЭтаФорма.Публиковать);
		ПереключитьСтраницу(Элементы, "ТолькоУведомления", ?(ВидимостьОпции, "Показать", "Скрыть"));
		Если НЕ ВидимостьОпции Тогда
			Объект.ТолькоУведомить = Ложь;
		КонецЕсли;
		
		СпособыДоставки = "";
		Если Объект.ИспользоватьСетевойКаталог Тогда
			СпособыДоставки = НСтр("ru = 'сетевой каталог'");
		КонецЕсли;
		Если Объект.ИспользоватьFTPРесурс Тогда
			СпособыДоставки = НСтр("ru = 'FTP'");
		КонецЕсли;
		Если Объект.ИспользоватьЭлектроннуюПочту И НЕ Объект.ТолькоУведомить Тогда
			СпособыДоставки = СпособыДоставки + ?(СпособыДоставки = "", НСтр("ru = 'эл. почта'"), " "+ НСтр("ru = 'и эл. почта'"));
		КонецЕсли;
		
		Элементы.СтраницаДоставка.Заголовок = НСтр("ru = 'Доставка ("+СпособыДоставки+")'");
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "ПисьмоВФорматеHTML" Тогда
		ПереключитьСтраницу(Элементы, "СтраницыТекстПисьма", ?(Объект.ПисьмоВФорматеHTML, "HTML", "ОбычныйТекст"));
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "Архивировать" Тогда
		Элементы.ИмяАрхива.Доступность    = Объект.Архивировать;
		Элементы.ПарольАрхива.Доступность = Объект.Архивировать;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "ВыполнятьПоРасписанию" Тогда
		Если Объект.ВыполнятьПоРасписанию Тогда
			Элементы.СтраницаРасписание.Заголовок = НСтр("ru = 'Расписание (активно)'");
		Иначе
			Элементы.СтраницаРасписание.Заголовок = НСтр("ru = 'Расписание (не активно)'");
		КонецЕсли;
		Элементы.ПараметрыВыполненияПоРасписанию.Доступность = Объект.ВыполнятьПоРасписанию;
		Элементы.СтраницыПериодичности.Доступность           = Объект.ВыполнятьПоРасписанию;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "ПериодичностьРасписания" Тогда
		ИмяПеречисления = ЭтаФорма.Кэш.Перечисления.Периодичности[Объект.ПериодичностьРасписания];
		
		Элементы.СтраницыПериодичности.ТекущаяСтраница = Элементы["Страница"+ИмяПеречисления];
		Если ИмяПеречисления = "Произвольное" Тогда
			Элементы.СтраницыВремяИлиИзменить.ТекущаяСтраница = Элементы.СтраницаИзменитьРасписание;
		Иначе
			Элементы.СтраницыВремяИлиИзменить.ТекущаяСтраница = Элементы.СтраницаВремяНачала;
		КонецЕсли;
		
		// Сброс параметров, которые не соответствуют закладкам упрощенного редактирования
		Если Изменения = "ПериодичностьРасписания"
			И (ИмяПеречисления = "Ежедневно" 
			ИЛИ ИмяПеречисления = "Еженедельно"
			ИЛИ ИмяПеречисления = "Ежемесячно") Тогда
			
			// Общие параметры
			ЭтаФорма.Расписание.ДатаНачала = '00010101';
			ЭтаФорма.Расписание.ДатаКонца  = '00010101';
			ЭтаФорма.Расписание.ВремяКонца = '00010101';
			ЭтаФорма.Расписание.ВремяЗавершения = '00010101';
			ЭтаФорма.Расписание.ДеньНеделиВМесяце = 0;
			ЭтаФорма.Расписание.ДетальныеРасписанияДня = Новый Массив;
			ЭтаФорма.Расписание.ИнтервалЗавершения = 0;
			ЭтаФорма.Расписание.ПаузаПовтора = 0;
			ЭтаФорма.Расписание.ПериодНедель = 0;
			ЭтаФорма.Расписание.ПериодПовтораВТечениеДня = 0;
			
			Если ИмяПеречисления <> "Ежедневно" Тогда
				ЭтаФорма.Расписание.ПериодПовтораДней = 1;
			КонецЕсли;
			
			Если ИмяПеречисления <> "Еженедельно" Тогда
				ВыбранныеДниНедели = Новый Массив;
				Для н = 1 По 7 Цикл
					ВыбранныеДниНедели.Добавить(н);
				КонецЦикла;
				ЭтаФорма.Расписание.ДниНедели = ВыбранныеДниНедели;
			КонецЕсли;
			
			Если ИмяПеречисления <> "Ежемесячно" Тогда
				ВсеМесяцы = Новый Массив;
				Для н = 1 По 12 Цикл
					ВсеМесяцы.Добавить(н);
				КонецЦикла;
				ЭтаФорма.Расписание.Месяцы = ВсеМесяцы;
				ЭтаФорма.Расписание.ДеньВМесяце = 0;
			КонецЕсли;
		КонецЕсли;
		
		// Восстанавливаем параметры на текущей закладке в соответствии с параметрами расписания
		Если ИмяПеречисления = "Ежедневно" Тогда
			ЭтаФорма.ВремяНачала = ЭтаФорма.Расписание.ВремяНачала;
			ЭтаФорма.ПериодПовтораДней = ЭтаФорма.Расписание.ПериодПовтораДней;
		ИначеЕсли ИмяПеречисления = "Еженедельно" Тогда
			ЭтаФорма.ВремяНачала = ЭтаФорма.Расписание.ВремяНачала;
			Для Каждого КлючИЗначение Из ЭтаФорма.Кэш.Соответствия.ДниНедели Цикл
				ЭтаФорма[КлючИЗначение.Ключ] = (ЭтаФорма.Расписание.ДниНедели.Найти(КлючИЗначение.Значение) <> Неопределено);
			КонецЦикла;
		ИначеЕсли ИмяПеречисления = "Ежемесячно" Тогда
			ЭтаФорма.ВремяНачала = ЭтаФорма.Расписание.ВремяНачала;
			Если ЭтаФорма.Расписание.ДеньВМесяце >= 0 Тогда
				ЭтаФорма.ДеньВМесяце = ЭтаФорма.Расписание.ДеньВМесяце;
				Элементы.ГиперссылкаНачалоКонецМесяца.Заголовок = НСтр("ru = 'начала'");
			Иначе	
				ЭтаФорма.ДеньВМесяце = -ЭтаФорма.Расписание.ДеньВМесяце;
				Элементы.ГиперссылкаНачалоКонецМесяца.Заголовок = НСтр("ru = 'конца'");
			КонецЕсли;
			Для Каждого КлючИЗначение Из ЭтаФорма.Кэш.Соответствия.Месяцы Цикл
				ЭтаФорма[КлючИЗначение.Ключ] = (ЭтаФорма.Расписание.Месяцы.Найти(КлючИЗначение.Значение) <> Неопределено);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли; // Изменения = "" ИЛИ Изменения = "ПериодичностьРасписания"
	
	Если Изменения = "" ИЛИ Изменения = "НачалоКонецМесяца" Тогда
		Элементы.ГиперссылкаНачалоКонецМесяца.Заголовок = ?(ЭтаФорма.Расписание.ДеньВМесяце >= 0, "начала", "конца");
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРасписание Тогда
		ЭтаФорма.ПредставлениеРасписания = Строка(ЭтаФорма.Расписание);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьМодифицированностьФормы(ЭтаФорма, Поле = "", ПутьКДанным = "", Текст = "")
	Если НЕ ЭтаФорма.Модифицированность Тогда
		ЭтаФорма.ФормаБылаМодифицированаНаСервере = Истина;
		Если ЗначениеЗаполнено(Текст) Тогда
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , Поле, ПутьКДанным);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеФорматовПоУмолчанию()
	Возврат НСтр("ru = 'по умолчанию'");
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызовы клиента

&НаКлиенте
Процедура ДобавитьПеретащитьПолучателя(ПолучательИлиНаборПолучателей)
	
	Если ПодборПользователейИлиГрупп(ПолучательИлиНаборПолучателей) Тогда
		
		КоличествоПолучателей = Объект.Получатели.Количество();
		Для н = 1 По КоличествоПолучателей Цикл
			ИндексПолучателя = КоличествоПолучателей - н;
			
			СтрокаПолучатель = Объект.Получатели.Получить(ИндексПолучателя);
			
			ИнексВМассиве = ПолучательИлиНаборПолучателей.Найти(СтрокаПолучатель.Получатель);
			Если ИнексВМассиве = Неопределено Тогда
				Объект.Получатели.Удалить(СтрокаПолучатель);
			Иначе
				ПолучательИлиНаборПолучателей.Удалить(ИнексВМассиве);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	МассивНовыхСтрок = ВыборПодборПеретаскиваниеВТабличнуюЧасть(
		ПолучательИлиНаборПолучателей,
		Объект.Получатели,
		"Получатель",
		Новый Структура("Исключен", Ложь)
	);
	
	Если МассивНовыхСтрок.Количество() > 0 Тогда
		Если МассивНовыхСтрок.Количество() = 1 Тогда
			ЗаголовокОповещения = НСтр("ru = 'Получатель добавлен в рассылку'");
		Иначе
			ЗаголовокОповещения = НСтр("ru = 'Получатели добавлены в рассылку'");
		КонецЕсли;
		
		ТекстОповещения = "";
		Для Каждого СтрокаПолучатель Из МассивНовыхСтрок Цикл
			ТекстОповещения = ТекстОповещения + ?(ТекстОповещения = "", "", ", ") + Строка(СтрокаПолучатель.Получатель);
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВыборПодборПеретаскиваниеЭлементаВТабличнуюЧасть(ЭлементПодбора, ТабличнаяЧасть, ИмяРеквизита, СтруктураЗаполнения, Уникальность = Истина)
	ПереданаСтруктура = (ТипЗнч(ЭлементПодбора) = Тип("Структура"));
	Если ПереданаСтруктура ИЛИ ТипЗнч(ЭлементПодбора) = Тип("ДанныеФормыЭлементКоллекции") Тогда
		Если ЭлементПодбора.Свойство(ИмяРеквизита) Тогда
			ЗначениеРеквизита = ЭлементПодбора[ИмяРеквизита];
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе // (СправочникСсылка.*) перетаскивание из формы выбора или подбора
		ЗначениеРеквизита = ЭлементПодбора;
	КонецЕсли;
	
	// Требуется уникальность реквизита в рамках таблицы
	Найденные = ТабличнаяЧасть.НайтиСтроки(Новый Структура(ИмяРеквизита, ЗначениеРеквизита));
	
	Если Уникальность И Найденные.Количество() > 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтрокаТаблицы = ТабличнаяЧасть.Добавить();
	СтрокаТаблицы[ИмяРеквизита] = ЗначениеРеквизита;
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураЗаполнения);
	Если ПереданаСтруктура Тогда
		Для Каждого КлючИЗначение Из ЭлементПодбора Цикл
			Если СтруктураЗаполнения.Свойство(КлючИЗначение.Ключ) Тогда
				ЭлементПодбора.Удалить(ЭлементПодбора.Ключ);
			КонецЕсли;
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ЭлементПодбора);
	КонецЕсли;
	
	Возврат СтрокаТаблицы;
КонецФункции

&НаКлиенте
Функция ВыборПодборПеретаскиваниеВТабличнуюЧасть(ВыбранноеЗначение, ТабличнаяЧасть, ИмяРеквизита, СтруктураЗаполнения, Идентификаторы = Ложь)
	Модифицированность = Истина;
	МассивНовыхСтрок = Новый Массив;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для Каждого ЭлементПодбора Из ВыбранноеЗначение Цикл
			Результат = ВыборПодборПеретаскиваниеЭлементаВТабличнуюЧасть(ЭлементПодбора, ТабличнаяЧасть, ИмяРеквизита, СтруктураЗаполнения);
			Если Результат <> Неопределено Тогда
				МассивНовыхСтрок.Добавить(?(Идентификаторы, Результат.ПолучитьИдентификатор(), Результат));
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = ВыборПодборПеретаскиваниеЭлементаВТабличнуюЧасть(ВыбранноеЗначение, ТабличнаяЧасть, ИмяРеквизита, СтруктураЗаполнения);
		Если Результат <> Неопределено Тогда
			МассивНовыхСтрок.Добавить(?(Идентификаторы, Результат.ПолучитьИдентификатор(), Результат));
		КонецЕсли;
	КонецЕсли;
	Возврат МассивНовыхСтрок;
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуВыбораДляПодбораПолучателей(ФормаИлиЭлементФормы)
	Отказ = Ложь;
	ПроверитьТипПолучателей(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Найденные = ТаблицаТиповПолучателей.НайтиСтроки(Новый Структура("ТипПолучателей", ТипПолучателейРассылки));
	
	Если Найденные.Количество() = 1 Тогда
		ПараметрыРасширеннойФормыПодбора = ВыбранныеПолучателиРассылки();
		ПараметрыФормыВыбора = Новый Структура;
		// Стандартные реквизиты формы выбора (см. Расширение управляемой формы для динамического списка)
		ПараметрыФормыВыбора.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
		ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Ложь);
		ПараметрыФормыВыбора.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
		ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Истина);
		ПараметрыФормыВыбора.Вставить("РежимВыбора", Истина);
		// Предполагаемые реквизиты
		ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.Независимый);
		ПараметрыФормыВыбора.Вставить("ВыборГрупп", Истина);
		ПараметрыФормыВыбора.Вставить("ВыборГруппПользователей", Истина);
		ПараметрыФормыВыбора.Вставить("РазрешитьНачалоПеретаскивания", Истина);
		// Параметры открытия расширенной формы подбора
		// (описание реквизитов см. в форме списка справочника Пользователи)
		ПараметрыФормыВыбора.Вставить("РасширенныйПодбор", Истина);
		ПараметрыФормыВыбора.Вставить("ПараметрыРасширеннойФормыПодбора", ПараметрыРасширеннойФормыПодбора);
		
		ОткрытьФорму(Найденные[0].ПутьФормыВыбора, ПараметрыФормыВыбора, ФормаИлиЭлементФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВыбратьФормат(СсылкаНаОтчет)
	ЗаполняетсяФорматПоУмолчанию = НЕ ЗначениеЗаполнено(СсылкаНаОтчет);
	
	Найденные = Объект.ФорматыОтчетов.НайтиСтроки(Новый Структура("Отчет", СсылкаНаОтчет));
	Если Найденные.Количество() > 0 Тогда
		СписокФорматов.ЗаполнитьПометки(Ложь);
		Для Каждого СтрокаФормат Из Найденные Цикл
			СписокФорматов.НайтиПоЗначению(СтрокаФормат.Формат).Пометка = Истина;
		КонецЦикла;
	Иначе
		СписокФорматов = СписокФорматовПоУмолчанию.Скопировать();
		Если НЕ ЗаполняетсяФорматПоУмолчанию Тогда
			Найденные = Объект.ФорматыОтчетов.НайтиСтроки(Новый Структура("Отчет", Кэш.ПустоеЗначениеОтчета));
			Если Найденные.Количество() > 0 Тогда
				СписокФорматов.ЗаполнитьПометки(Ложь);
				Для Каждого СтрокаФормат Из Найденные Цикл
					СписокФорматов.НайтиПоЗначению(СтрокаФормат.Формат).Пометка = Истина;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполняетсяФорматПоУмолчанию Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выберите форматы по умолчанию'");
	Иначе
		ЗаголовокФормыВыбора = НСтр("ru = 'Выберите форматы для отчета '"+ Строка(СсылкаНаОтчет) +"''");
	КонецЕсли;
	
	ДублирующийСписокФорматов = СписокФорматов.Скопировать();
	Если НЕ СписокФорматов.ОтметитьЭлементы(ЗаголовокФормыВыбора) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Проверка изменений
	ФорматыСовпадают = Истина;
	Для н = 1 По СписокФорматов.Количество() Цикл
		Если СписокФорматов[н-1].Пометка <> ДублирующийСписокФорматов[н-1].Пометка Тогда
			ФорматыСовпадают = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ФорматыСовпадают Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = "";
	
	// Очистка существующих записей
	ОчиститьФормат(СсылкаНаОтчет);
	
	// Добавление отмеченных форматов
	Для Каждого ЭлементСписка Из СписокФорматов Цикл
		Если ЭлементСписка.Пометка Тогда
			СтрокаФормат = Объект.ФорматыОтчетов.Добавить();
			СтрокаФормат.Отчет = СсылкаНаОтчет;
			СтрокаФормат.Формат = ЭлементСписка.Значение;
			Результат = Результат + ?(Результат = "", "", ", ") + Строка(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗаполняетсяФорматПоУмолчанию И Результат = "" Тогда
		Результат = СписокФорматовПоУмолчаниюПредставление;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОчиститьФормат(СсылкаНаОтчет)
	Модифицированность = Истина;
	Найденные = Объект.ФорматыОтчетов.НайтиСтроки(Новый Структура("Отчет", СсылкаНаОтчет));
	Для Каждого СтрокаФормат Из Найденные Цикл
		Объект.ФорматыОтчетов.Удалить(СтрокаФормат);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьШаблон(ТекстовыйШаблон = Неопределено, ИсключитьТемуПисьма = Ложь)
	// Проверяем и устаналиваем фокус в нужный элемент
	Если ИсключитьТемуПисьма ИЛИ НЕ (ТекущийЭлемент = Элементы.ТемаПисьма ИЛИ ТекущийЭлемент = Элементы.ИмяАрхива) Тогда
		Если Объект.ПисьмоВФорматеHTML Тогда
			Если ТекущийЭлемент <> Элементы.ТекстПисьмаФорматированныйДокумент Тогда
				ТекущийЭлемент = Элементы.ТекстПисьмаФорматированныйДокумент;
			КонецЕсли;
		Иначе
			Если ТекущийЭлемент <> Элементы.ТекстПисьма Тогда
				ТекущийЭлемент = Элементы.ТекстПисьма;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекстовыйШаблон = Неопределено Тогда
		// Ограничимся подготовкой к добавлению шаблона (переключением текущего элемента)
		Возврат;
	КонецЕсли;
	
	Если ТекущийЭлемент.ВыделенныйТекст = "" Тогда
		// Форматированный документ некорректно отрабатывает изменения свойства
		//  ВыделенныйТекст, в случае, если ничего не выделено,
		//  поэтому используется альтернативный метод добавления текста.
		Если ТекущийЭлемент = Элементы.ТекстПисьмаФорматированныйДокумент Тогда
			ТекстПисьмаФорматированныйДокумент.Добавить(ТекстовыйШаблон, ТипЭлементаФорматированногоДокумента.Текст);
		Иначе
			ТекущийЭлемент.ВыделенныйТекст = ТекстовыйШаблон;
		КонецЕсли;
	Иначе
		ТекущийЭлемент.ВыделенныйТекст = ТекущийЭлемент.ВыделенныйТекст + ТекстовыйШаблон;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ИзменитьИРазадресоватьМассив(Добавить, Элемент, Знач Массив)
	Индекс = Массив.Найти(Элемент);
	Если Добавить И Индекс = Неопределено Тогда
		ВГраницаПлюс1 = ?(Массив.Количество() >= Элемент, Элемент, Массив.Количество());
		Для н = 1 По ВГраницаПлюс1 Цикл
			Если Массив[ВГраницаПлюс1-н] < Элемент Тогда
				Массив.Вставить(ВГраницаПлюс1-н+1, Элемент);
				Возврат Массив;
			КонецЕсли;
		КонецЦикла;
		Массив.Вставить(0, Элемент);
	ИначеЕсли НЕ Добавить И Индекс <> Неопределено Тогда
		Массив.Удалить(Индекс);
	КонецЕсли;
	Возврат Массив;
КонецФункции

&НаКлиенте
Процедура ИзменитьРасписаниеВДиалоге()
	ДиалогРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Если ДиалогРасписания.ОткрытьМодально() Тогда
		Модифицированность = Истина;
		Расписание = ДиалогРасписания.Расписание;
		ВидимостьДоступностьКорректность(ЭтаФорма, "Расписание");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораНастройкиОтчета(Элемент, ДанныеВыбора, СтандартнаяОбработка, СКД = Неопределено)
	// Сброс ограничителя
	Элемент.ОграничениеТипа = Новый ОписаниеТипов;
	
	//
	ЭлементСвязиПоТипу = Неопределено;
	
	// Возможность персонализации рассылки
	Если НЕ Объект.Персонализирована Тогда
		Возврат;
	КонецЕсли;
	
	// Основной тип получателей
	КоличествоТипов = ТипПолучателейРассылки.Типы().Количество();
	Если КоличествоТипов <> 1 И КоличествоТипов <> 2 Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Поле ""Получатели"" не заполнено'"),
			,
			"ТипПолучателейРассылки"
		);
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	// 
	Если СКД Тогда
		Инициатор = Элементы.КомпоновщикНастроекПользовательскиеНастройки;
	Иначе
		//Инициатор = Элементы.НастройкиТекущегоОтчета;
		Инициатор = Неопределено;
	КонецЕсли;
	
	НайденныеИОМД = ТаблицаТиповПолучателей.НайтиСтроки(Новый Структура("ТипПолучателей", ТипПолучателейРассылки));
	Если НайденныеИОМД.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	МассивТипов = НайденныеИОМД[0].ОсновнойТип.Типы();
	Если МассивТипов.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	ОсновнойТипПолучателей = МассивТипов[0];
	
	//  - Получения описания типов, доступных для выбора
	Если СКД Тогда
		
		//  - Идентификатор пользовательской настройки КД
		ИдентификаторНастройки = Инициатор.ТекущаяСтрока;
		Если ИдентификаторНастройки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		//  - Строка в составе настроек компоновки данных
		СтрокаНастроек = КомпоновщикНастроекКД.ПользовательскиеНастройки.ПолучитьОбъектПоИдентификатору(ИдентификаторНастройки);
		Если СтрокаНастроек = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		//  - Тип настройки
		Если ТипЗнч(СтрокаНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			ЭтоЭлементОтбора = Истина;
		ИначеЕсли ТипЗнч(СтрокаНастроек) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ЭтоЭлементОтбора = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
		
		//  - Поле компоновки данных
		Если ЭтоЭлементОтбора Тогда
			
			Если ЗначениеЗаполнено(СтрокаНастроек.ЛевоеЗначение) Тогда
				
				ПолеКД = СтрокаНастроек.ЛевоеЗначение;
				
			Иначе
				
				//  - Нахождение поля в составе элементов отбора КД по идентификатору
				ТекущийЭлементОтбораКД = Неопределено;
				Идентификатор = Строка(ИдентификаторНастройки);
				
				ГруппыЭлементовОтбора = Новый Массив;
				ГруппыЭлементовОтбора.Добавить(КомпоновщикНастроекКД.Настройки.Отбор.Элементы);
				Пока ГруппыЭлементовОтбора.Количество() > 0 И ТекущийЭлементОтбораКД = Неопределено Цикл
					ГруппаЭлементовОтбора = ГруппыЭлементовОтбора[0];
					ГруппыЭлементовОтбора.Удалить(0);
					
					Для Каждого ЭлементОтбораКД Из ГруппаЭлементовОтбора Цикл
						Если ТипЗнч(ЭлементОтбораКД) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
							ГруппыЭлементовОтбора.Добавить(ЭлементОтбораКД.Элементы);
						ИначеЕсли ЭлементОтбораКД.ИдентификаторПользовательскойНастройки = Идентификатор Тогда
							ТекущийЭлементОтбораКД = ЭлементОтбораКД;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
				КонецЦикла;
				
				Если ТекущийЭлементОтбораКД = Неопределено Тогда
					Возврат;
				КонецЕсли;
				
				ПолеКД = ТекущийЭлементОтбораКД.ЛевоеЗначение;
				
			КонецЕсли;
			
			//  - Нахождение поля в составе доступных полей выбора
			ДоступноеПолеКД = КомпоновщикНастроекКД.Настройки.Выбор.ДоступныеПоляВыбора.НайтиПоле(ПолеКД);
			Если ДоступноеПолеКД = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			ОписаниеДоступныхТипов = ДоступноеПолеКД.Тип;
			
		Иначе
			
			ДоступныйПараметрКД = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(СтрокаНастроек.Параметр);
			ОписаниеДоступныхТипов = ДоступныйПараметрКД.Тип;
			
		КонецЕсли;
		
	Иначе
		
		// Массив типов для произвольных отчетов
		СтрокаНастроек = Инициатор.ТекущиеДанные;
		Если СтрокаНастроек = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ОписаниеДоступныхТипов = СтрокаНастроек.Тип;
		
	КонецЕсли;
	
	//  - Проверка на содержание типа получателей
	Если НЕ ОписаниеДоступныхТипов.СодержитТип(ОсновнойТипПолучателей) Тогда
		Возврат;
	КонецЕсли;
	
	//  - Подмена списка выбора типов на список, содержащий получателя
	СписокТипов = Новый СписокЗначений;
	СписокТипов.Добавить("[Получатель]", НСтр("ru = 'Получатель отчета'"));
	МассивТипов = ОписаниеДоступныхТипов.Типы();
	Для Каждого Тип Из МассивТипов Цикл
		МассивДляОписанияТипов = Новый Массив;
		МассивДляОписанияТипов.Добавить(Тип);
		ОписаниеТипов = Новый ОписаниеТипов(МассивДляОписанияТипов);
		ОписаниеТипов.ПривестиЗначение();
		СписокТипов.Добавить(ОписаниеТипов, Строка(Тип));
	КонецЦикла;
	
	//  - Выбор
	РезультатВыбора = ВыбратьИзСписка(СписокТипов);
	Если РезультатВыбора = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		Инициатор.ЗакончитьРедактированиеСтроки(Истина);
		Возврат;
	КонецЕсли;
	
	//  - Обработка результатов выбора
	Если ТипЗнч(РезультатВыбора.Значение) = Тип("ОписаниеТипов") Тогда
		
		//  - Выбран тип получателей
		Элемент.ОграничениеТипа = РезультатВыбора.Значение;
		
	Иначе
		
		//  - Выбран шаблон получателя
		СтандартнаяОбработка = Ложь;
		СтрокаНастроек.Использование = Истина;
		Инициатор.ЗакончитьРедактированиеСтроки(Ложь);
		РассылкаБылаПерсонализирована = Истина;
		Элементы.Отчеты.ТекущиеДанные.ВнесеныИзменения = Истина;
		
		Если СКД Тогда
			Если ЭтоЭлементОтбора Тогда
				СтрокаНастроек.ПравоеЗначение = РезультатВыбора.Значение;
			Иначе
				СтрокаНастроек.Значение = РезультатВыбора.Значение;
			КонецЕсли;
		Иначе
			СтрокаНастроек.Значение = РезультатВыбора.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычислитьФлажкиДополнительныхСпособовДоставки()
	
	Объект.ИспользоватьСетевойКаталог = Публиковать И (ДругойСпособДоставки = "ИспользоватьСетевойКаталог");
	Объект.ИспользоватьFTPРесурс    = Публиковать И (ДругойСпособДоставки = "ИспользоватьFTPРесурс");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРассылку(ПараметрыДоставки)
	// Очистка окна сообщений
	ОчиститьСообщения();
	
	//
	Если НЕ Объект.Подготовлена ИЛИ Объект.Ссылка.Пустая() Тогда
		Если НЕ Объект.Подготовлена Тогда
			ТекстВопроса = НСтр("ru = 'Перед проверкой рассылка должна быть подготовлена.
                                 |Нажмите ""Продолжить"", чтобы установить флажок ""Подготовлена"" и записать рассылку.'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Перед проверкой рассылка должна быть записана.
                                 |Нажмите ""Продолжить"", чтобы записать рассылку.'");
		КонецЕсли;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(1, НСтр("ru = 'Продолжить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		Ответ = Вопрос(ТекстВопроса, Кнопки, 60, 1, НСтр("ru = 'Проверка способа доставки'"));
		Если Ответ = 1 Тогда
			Объект.Подготовлена = Истина;
			Если НЕ Записать() Тогда
				Возврат;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыДоставки.Вставить("Рассылка", Объект.Наименование);
	
	Если ПараметрыДоставки.ИспользоватьСетевойКаталог Тогда
		ПараметрыДоставки.Вставить("СетевойКаталогWindows", Объект.СетевойКаталогWindows);
		ПараметрыДоставки.Вставить("СетевойКаталогLinux",   Объект.СетевойКаталогLinux);
	КонецЕсли;
	
	Если ПараметрыДоставки.ИспользоватьFTPРесурс Тогда
		ПараметрыДоставки.Вставить("Сервер",              Объект.FTPСервер);
		ПараметрыДоставки.Вставить("Порт",                Объект.FTPПорт);
		ПараметрыДоставки.Вставить("Логин",               Объект.FTPЛогин);
		ПараметрыДоставки.Вставить("Пароль",              Объект.FTPПароль);
		ПараметрыДоставки.Вставить("Каталог",             Объект.FTPКаталог);
		ПараметрыДоставки.Вставить("ПассивноеСоединение", Объект.FTPПассивноеСоединение);
	КонецЕсли;
	
	Если ПараметрыДоставки.ИспользоватьЭлектроннуюПочту Тогда
		ПараметрыДоставки.Вставить("УчетнаяЗапись", Объект.УчетнаяЗапись);
		ПараметрыДоставки.Вставить("СкрытыеКопии",  Объект.СкрытыеКопии);
		ПараметрыДоставки.Вставить("ШаблонТемы",    НСтр("ru = 'Тестовое сообщение Альфа-Авто'"));
		ПараметрыДоставки.Вставить("ШаблонТекста",  НСтр("ru = 'Это сообщение отправлено системой рассылок Альфа-Авто.'")
			+ Символы.ПС + Кэш.ЗаголовокСистемы
		);
		
		//РезультатВыбора = Неопределено; //РассылкаОтчетовКлиент.ВыбратьПолучателя(Объект, Ложь, Истина); // Получатель и E-Mail
		РезультатВыбора = РассылкаОтчетовКлиент.ВыбратьПолучателя(Объект, Ложь, Истина); // Получатель и E-Mail
		Если РезультатВыбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ПараметрыДоставки.Вставить("Получатели", РезультатВыбора);
		
		Если Объект.ПисьмоВФорматеHTML Тогда
			ПараметрыДоставки.Вставить("ФорматТекста", ПредопределенноеЗначение("Перечисление.ФорматТекста.HTML"));
		Иначе
			ПараметрыДоставки.Вставить("ФорматТекста", ПредопределенноеЗначение("Перечисление.ФорматТекста.ПростойТекст"));
		КонецЕсли;
		ПараметрыДоставки.Вставить("ТолькоУведомить", Ложь);
		ПараметрыДоставки.Вставить("ЗаполнитьСпособДоставкиВШаблонеСообщения", Ложь);
		ПараметрыДоставки.Вставить("ЗаполнитьСформированныеОтчетыВШаблонеСообщения", Ложь);
		ПараметрыДоставки.Вставить("ЗаполнитьПолучателяВШаблонеТемы", Ложь);
		ПараметрыДоставки.Вставить("ЗаполнитьПолучателяВШаблонеСообщения", Ложь);
		ПараметрыДоставки.Вставить("ПараметрыПисьма", Новый Структура);
		ПараметрыДоставки.Вставить("Соединение", Неопределено);
	КонецЕсли; // ПараметрыДоставки.ИспользоватьЭлектроннуюПочту
	
	ПараметрыДоставки.Вставить("ДобавлятьСсылки", "");
	
	Результат = ПроверитьСпособДоставки(Объект.Ссылка, ПараметрыДоставки);
	
	//РассылкаОтчетовКлиент.ПоказатьРезультат(Результат);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТипПолучателей(Отказ)
	Если НЕ ЗначениеЗаполнено(ТипПолучателейРассылки) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Получатели"" не заполнено'");
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ТипПолучателейРассылки");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКоличествоПолучателей(Отказ)
	Если Объект.Получатели.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки в список ""Получатели"".'");
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Получатели");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПодборПользователейИлиГрупп(ПолучательИлиНаборПолучателей)
	Если ТипЗнч(ПолучательИлиНаборПолучателей) = Тип("Массив")
		И ПолучательИлиНаборПолучателей.Количество() > 0
		И ТипЗнч(ПолучательИлиНаборПолучателей[0]) = Тип("СправочникСсылка.Пользователи") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ЗагрузитьГруппировки(Знач ВыбранныеПоля, ИмяТаблицы)
	
	Если ИдентификаторТекущейСтрокиТаблицыОтчетов = -1 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтчеты = Объект.Отчеты.НайтиПоИдентификатору(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаГруппировок = СтрокаОтчеты[ИмяТаблицы];
	
	ИндекУдаления = ТаблицаГруппировок.Количество()-1;
	Для Каждого ТекГруппировка Из ВыбранныеПоля Цикл
		НайденныеГруппировки  = ТаблицаГруппировок.НайтиСтроки(Новый Структура("Поле", ТекГруппировка.Поле));
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Поле           = ТекГруппировка.Поле;
		НоваяСтрока.Заголовок      = ТекГруппировка.Заголовок;
		НоваяСтрока.Использование  = ТекГруппировка.Использование;
		НоваяСтрока.ТипГруппировки = ТекГруппировка.ТипГруппировки;
		Если НайденныеГруппировки.Количество() = 0 Тогда
			НоваяСтрока.Использование  = Истина;
			//УстановитьПорядокПоУмолчанию(НоваяСтрока);
			ОтчетыСервер.УстановитьПорядокПоУмолчанию(КомпоновщикНастроекКД.Настройки, НоваяСтрока);
		Иначе
			НайденнаяГруппировка = НайденныеГруппировки[0];
			НоваяСтрока.Порядок.Очистить();
			Для Каждого СтрокаПорядка Из НайденнаяГруппировка.Порядок Цикл
				ЗаполнитьЗначенияСвойств(НоваяСтрока.Порядок.Добавить(), СтрокаПорядка);
			КонецЦикла;
			
			НоваяСтрока.ДопПоля.Очистить();
			Для Каждого СтрокаПолей Из НайденнаяГруппировка.ДопПоля Цикл
				ЗаполнитьЗначенияСвойств(НоваяСтрока.ДопПоля.Добавить(), СтрокаПолей);
			КонецЦикла;
			
			НоваяСтрока.ДопГруппировки.Очистить();
			Для Каждого СтрокаПолей Из НайденнаяГруппировка.ДопГруппировки Цикл
				ЗаполнитьЗначенияСвойств(НоваяСтрока.ДопГруппировки.Добавить(), СтрокаПолей);
			КонецЦикла;
			
			ТаблицаГруппировок.Удалить(НайденнаяГруппировка);
			ИндекУдаления = ИндекУдаления - 1;
		КонецЕсли;
	КонецЦикла;
	
	Пока ИндекУдаления>=0 Цикл
		ТаблицаГруппировок.Удалить(ИндекУдаления);
		ИндекУдаления = ИндекУдаления - 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокПолей(Поля)
	
	СписокПолей = Новый Массив;
	Для Каждого ТекПоле Из Поля Цикл
		
		НоваяСтрока = Новый Структура("Использование, Поле, Заголовок, ТипГруппировки");
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекПоле);
		СписокПолей.Добавить(НоваяСтрока);
	КонецЦикла;
	
	Возврат СписокПолей;
	
КонецФункции

&НаКлиенте
Процедура НастройкиКомпоновщикаНажатие(Элемент, СтандартнаяОбработка)
	
	Если ИдентификаторТекущейСтрокиТаблицыОтчетов = -1 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтчеты = Объект.Отчеты.НайтиПоИдентификатору(ИдентификаторТекущейСтрокиТаблицыОтчетов);
	Если СтрокаОтчеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//ИмяПоля = СтрЗаменить(Элемент.Имя, "СтруктураСтроки", "НастройкаСтруктуры");
	//ИмяПоля = СтрЗаменить(ИмяПоля,     "СтруктураКолонки", "НастройкаСтруктуры");
	//ИмяПоля = СтрЗаменить(ИмяПоля,     "Наименование", "");
	//
	//СтандартнаяОбработка = Ложь;
	//СтруктураПараметров = Новый Структура();
	//СтруктураПараметров.Вставить("СхемаКомпоновки", СхемаКомпоновки);
	//СтруктураПараметров.Вставить("НастройкиОтчета", ОтчетыСервер.КомпоновщикПолучитьНастройки(КомпоновщикНастроекКД));
	//
	//СтруктураПараметров.Вставить("ВидПолей",        ИмяПоля);
	//Если ИмяПоля = "НастройкаСтруктуры" Тогда
	//	Если Найти(Элемент.Имя, "СтруктураСтроки") > 0 Тогда
	//		СтруктураПараметров.Вставить("Измерения",     ПолучитьСписокПолей(СтрокаОтчеты.ИзмеренияСтроки));
	//	ИначеЕсли Найти(Элемент.Имя, "СтруктураКолонки") > 0 Тогда
	//		СтруктураПараметров.Вставить("Измерения",     ПолучитьСписокПолей(СтрокаОтчеты.ИзмеренияКолонки));
	//	КонецЕсли;
	//Иначе
	//	СтруктураПараметров.Вставить("ТекущаяСтрока", КомпоновщикНастроекКД.Настройки.ПолучитьИдентификаторПоОбъекту(КомпоновщикНастроекКД.Настройки));
	//КонецЕсли;
	//
	//Если ИмяПоля = "Порядок" ИЛИ ИмяПоля = "ДопПоля" Тогда
	//	СтруктураПараметров.Вставить("ТолькоРесурсы", Истина);
	//КонецЕсли;
	//
	//ФормаВыбора = ПолучитьФорму("ОбщаяФорма.ОтчетРедактированиеВыбранныхПолей", СтруктураПараметров, Элемент);
	//ФормаВыбора.Заголовок = "Редактирование группировок";
	//
	//Рез = ФормаВыбора.ОткрытьМодально();
	//Если Рез = КодВозвратаДиалога.ОК Тогда
	//	
	//	Модифицированность = Истина;
	//	
	//	Если Найти(Элемент.Имя, "СтруктураСтроки") > 0 Тогда
	//		ЗагрузитьГруппировки(ФормаВыбора.Группировки, "ИзмеренияСтроки");
	//		
	//		ТекстСтрок = "";
	//		Для Каждого ТекСтрока Из СтрокаОтчеты.ИзмеренияСтроки Цикл
	//			Если ТекСтрока.Использование Тогда
	//				ТекстСтрок = ТекстСтрок + ?(ТекстСтрок = "", "", ", ") + ТекСтрока.Заголовок;
	//			КонецЕсли;
	//		КонецЦикла;
	//		
	//		ПредставлениеСтрок   = ТекстСтрок;
	//	ИначеЕсли Найти(Элемент.Имя, "СтруктураКолонки") > 0 Тогда
	//		ЗагрузитьГруппировки(ФормаВыбора.Группировки, "ИзмеренияКолонки");
	//		
	//		ТекстКолонок = "";
	//		Для Каждого ТекСтрока Из СтрокаОтчеты.ИзмеренияКолонки Цикл
	//			Если ТекСтрока.Использование Тогда
	//				ТекстКолонок = ТекстКолонок + ?(ТекстКолонок = "", "", ", ") + ТекСтрока.Заголовок;
	//			КонецЕсли;
	//		КонецЦикла;
	//		
	//		ПредставлениеКолонок = ТекстКолонок;
	//	Иначе
	//		КомпоновщикНастроекКД.ЗагрузитьНастройки(ФормаВыбора.КомпоновщикНастроек.Настройки);
	//		//ВзвестиМодифицированностьОтчета();
	//		//Если Элемент.Имя = "Отбор" ИЛИ Элемент.Имя = "ОтборНаименование" Тогда
	//		//	ОбновитьДанныеФормы();
	//		//КонецЕсли;
	//	КонецЕсли;
	//	
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПериодаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Если ТекущийВариантПериода = "" Тогда
		ТекущийВариантПериода = "ВариантСтандартногоПериода.ПроизвольныйПериод";
	КонецЕсли;
	
	Если ТекущийВариантПериода = "ВариантСтандартногоПериода.ПроизвольныйПериод" Тогда
		Элементы.ДатаНачала.Видимость    = ЕстьДатаНачала;
		Элементы.ДатаОкончания.Видимость = ЕстьДатаОкончания;
	Иначе
		Элементы.ДатаНачала.Видимость    = Ложь;
		Элементы.ДатаОкончания.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОтбораНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Если ТипЗнч(ТипПолучателейРассылки) = Тип("ОписаниеТипов") Тогда
		
		Если ТипПолучателейРассылки.Типы().Количество() = 0 Тогда
			ВыбиратьКонтрагентов = Ложь;
		ИначеЕсли ТипПолучателейРассылки.Типы()[0] = Тип("СправочникСсылка.Контрагенты") Тогда
			ВыбиратьКонтрагентов = Истина;
		Иначе
			ВыбиратьКонтрагентов = Ложь;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		Если ВыбиратьКонтрагентов Тогда
			ЗначениеПометки = "_Истина";
		Иначе
			ЗначениеПометки = "_Ложь";
		КонецЕсли;
		
		ЭлементСписка = Неопределено;
		СписокВыбора = Новый СписокЗначений;
		Для Каждого ТекЭлемент Из Элемент.СписокВыбора Цикл
			Позиция = Найти(ТекЭлемент.Представление, ЗначениеПометки);
			Если Позиция = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Представление = Лев(ТекЭлемент.Представление, Позиция-1);
			
			НовыйЭлемент = СписокВыбора.Добавить(ТекЭлемент.Значение, Представление);
			Если ПолеОтбора = ТекЭлемент.Значение Тогда
				ЭлементСписка = НовыйЭлемент;
			КонецЕсли;
				
		КонецЦикла;
	
		Результат = ВыбратьИзСписка(СписокВыбора, Элемент, ЭлементСписка);
		Если НЕ Результат = Неопределено Тогда
			ПолеОтбора = Результат.Значение;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолеОтбораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(ТипПолучателейРассылки) = Тип("ОписаниеТипов") Тогда
		
		Если ТипПолучателейРассылки.Типы().Количество() = 0 Тогда
			ВыбиратьКонтрагентов = Ложь;
		ИначеЕсли ТипПолучателейРассылки.Типы()[0] = Тип("СправочникСсылка.Контрагенты") Тогда
			ВыбиратьКонтрагентов = Истина;
		Иначе
			ВыбиратьКонтрагентов = Ложь;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		Если ВыбиратьКонтрагентов Тогда
			ЗначениеПометки = "_Истина";
		Иначе
			ЗначениеПометки = "_Ложь";
		КонецЕсли;
		
		ЭлементСписка = Неопределено;
		СписокВыбора = Новый СписокЗначений;
		Для Каждого ТекЭлемент Из Элемент.СписокВыбора Цикл
			Позиция = Найти(ТекЭлемент.Представление, ЗначениеПометки);
			Если Позиция = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Представление = Лев(ТекЭлемент.Представление, Позиция-1);
			
			НовыйЭлемент = СписокВыбора.Добавить(ТекЭлемент.Значение, Представление);
			Если ПолеОтбора = ТекЭлемент.Значение Тогда
				ЭлементСписка = НовыйЭлемент;
			КонецЕсли;
				
		КонецЦикла;
	
		Результат = ВыбратьИзСписка(СписокВыбора, Элемент, ЭлементСписка);
		Если НЕ Результат = Неопределено Тогда
			ПолеОтбора = Результат.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияКомпоновщикНастроекНастройкиОтборПриАктивизацииСтроки",0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборЛевоеЗначениеПриИзменении(Элемент)
	
	ОбработчикОжиданияКомпоновщикНастроекНастройкиОтборПриАктивизацииСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияКомпоновщикНастроекНастройкиОтборПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ЛевоеЗначение = Неопределено ИЛИ СокрЛП(ТекущиеДанные.ЛевоеЗначение) = "" Тогда
		Возврат;
	КонецЕсли;
	
	Если ИдентификаторОтбора = Строка(Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОтбора = Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока;
	
	ДоступноеПолеОтбора = КомпоновщикНастроекКД.Настройки.Отбор.ДоступныеПоляОтбора.НайтиПоле(ТекущиеДанные.ЛевоеЗначение);
	
	Если ДоступноеПолеОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДоступноеПолеОтбора.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойств")) Тогда
		
		ЗначениеВладельца = ОтчетыСервер.ПолучитьВладельцаЗначенияСвойств(ТекущиеДанные.ЛевоеЗначение);
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", ЗначениеВладельца);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		Элементы.КомпоновщикНастроекКДНастройкиОтборПравоеЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
		
	Иначе
		
		ТипЗначенияОтбора = ДоступноеПолеОтбора.ТипЗначения.Типы()[0];
		СписокВладельцев  = ОтчетыСервер.ПолучитьСписокВладельцев(КомпоновщикНастроекКД.ПолучитьНастройки(), ТипЗначенияОтбора);
		
		Если СписокВладельцев.Количество()=0 Тогда
			Возврат;
		КонецЕсли;
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", СписокВладельцев[0].Значение);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		Элементы.КомпоновщикНастроекКДНастройкиОтборПравоеЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = КомпоновщикНастроекКД.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	
	Если ОтчетыСервер.ОтборПоСписку(ТекущиеДанные.ВидСравнения) И Элемент.ПараметрыВыбора.Количество()>0 Тогда
		
		ЗначениеВладельца = Неопределено;
		Для Каждого ЭлементМассива Из Элемент.ПараметрыВыбора Цикл
			Если ЭлементМассива.Имя = "Отбор.Владелец" Тогда
				ЗначениеВладельца = ЭлементМассива.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеВладельца = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		Если ТекущиеДанные.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии ИЛИ
			ТекущиеДанные.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
			ТолькоГруппы = Истина;
		Иначе
			ТолькоГруппы = Ложь;
		КонецЕсли;
		
		ДоступноеПолеОтбора = КомпоновщикНастроекКД.Настройки.Отбор.ДоступныеПоляОтбора.НайтиПоле(ТекущиеДанные.ЛевоеЗначение);
		
		СтруктураПараметров = Новый Структура("СписокВыбора, ТипЗначения, ДоступныеЗначения, ТолькоГруппы, ПараметрОтборПоВладельцу", ТекущиеДанные.ПравоеЗначение, ДоступноеПолеОтбора.ТипЗначения, ДоступноеПолеОтбора.ДоступныеЗначения, ТолькоГруппы, ЗначениеВладельца);
		
		ФормаВыбора = ПолучитьФорму("ОбщаяФорма.ОтчетВыборЗначенияОтбораИзСписка", СтруктураПараметров, Элемент);
		Рез = ФормаВыбора.ОткрытьМодально();
		
		Если НЕ Рез = Неопределено Тогда
			ТекущиеДанные.ПравоеЗначение = Рез;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Событие = "ПорядокОтчета" Тогда
		
		Если НЕ РезультатЗакрытия.Папка Тогда
			ТекущиеДанные = КомпоновщикНастроекКД.Настройки.Порядок.ПолучитьОбъектПоИдентификатору(ДополнительныеПараметры.ТекущаяСтрока);
			ТекущиеДанные.Поле          = РезультатЗакрытия.Поле;
			ТекущиеДанные.Использование = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // Подключаемый_ОбработкаРезультатаОповещения()

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиПорядокПолеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.КомпоновщикНастроекКДНастройкиПорядок.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("СхемаКомпоновки", СхемаКомпоновки);
	ПараметрыВыбора.Вставить("Событие",         "Ресурсы");
	ПараметрыВыбора.Вставить("ТекущееПоле",     ТекущиеДанные.Поле);
	ПараметрыВыбора.Вставить("НастройкиОтчета", КомпоновщикНастроекКД.ПолучитьНастройки());
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока", Элементы.КомпоновщикНастроекКДНастройкиПорядок.ТекущаяСтрока);
	ДополнительныеПараметры.Вставить("Событие",       "ПорядокОтчета");
	
	ОписаниеОповещенияВыбораПоля = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтаФорма, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораПолейОтчета", ПараметрыВыбора, ЭтаФорма,,,,ОписаниеОповещенияВыбораПоля, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры


Перем Права Экспорт; // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	
	ОбязательныеРеквизиты = Новый Структура();
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение непроверяемых реквизитов
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Проверка условий
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПериодичностьРасписания,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Отчеты,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьСетевойКаталог = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СетевойКаталогWindows,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьСетевойКаталог = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СетевойКаталогLinux,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьFTPРесурс = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК FTPСервер,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьFTPРесурс = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК FTPПорт,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьFTPРесурс = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК FTPКаталог,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьЭлектроннуюПочту = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УчетнаяЗапись,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьЭлектроннуюПочту = ИСТИНА
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВидПочтовогоАдресаПолучателей,
	|	ВЫБОР
	|		КОГДА &Подготовлена = ИСТИНА
	|				И &ИспользоватьЭлектроннуюПочту = ИСТИНА
	|				И &Личная = ЛОЖЬ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Получатели";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подготовлена",                 Подготовлена);
	Запрос.УстановитьПараметр("ИспользоватьСетевойКаталог",   ИспользоватьСетевойКаталог);
	Запрос.УстановитьПараметр("ИспользоватьFTPРесурс",        ИспользоватьFTPРесурс);
	Запрос.УстановитьПараметр("ИспользоватьЭлектроннуюПочту", ИспользоватьЭлектроннуюПочту);
	Запрос.УстановитьПараметр("Личная",                       Личная);
	Запрос.Текст = ТекстЗапроса;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() > 0 Тогда
		
		Для Каждого Реквизит Из Таблица.Колонки Цикл
			
			Если Таблица[0][Реквизит.Имя] = Истина Тогда
				МассивНепроверяемыхРеквизитов.Добавить(Реквизит.Имя);
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	// Удаление непроверяемых реквизитов
	//ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	Для Каждого ЭлементМассива Из МассивНепроверяемыхРеквизитов Цикл
	
		ПорядковыйНомер = ПроверяемыеРеквизиты.Найти(ЭлементМассива);
		Если ПорядковыйНомер <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПорядковыйНомер);
		КонецЕсли;
	
	КонецЦикла;
	
	// Дополнительный анализ
	Если НЕ Подготовлена И ИспользоватьЭлектроннуюПочту И НЕ Личная Тогда
		Если НЕ ЗначениеЗаполнено(ТипПолучателейРассылки) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Поле ""Получатели"" не заполнено'");
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , "ТипПолучателейРассылки");
		КонецЕсли;
		Если Получатели.Количество() > 0 Тогда
			ВключенныеПолучатели = Получатели.НайтиСтроки(Новый Структура("Исключен", Ложь));
			Если ВключенныеПолучатели.Количество() = 0 Тогда
				Отказ = Истина;
				ТекстСообщения = НСтр("ru = 'Необходимо включить хотя бы одного получателя.'");
				ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Получатели[0].Исключен");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	// Вызывается непосредственно до записи объекта в базу данных
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Создание регламентное задание (получение уникального идентификатора)
	УстановитьПривилегированныйРежим(Истина);
	Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(РегламентноеЗадание);
	Если Задание = Неопределено Тогда
		Задание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.РассылкаОтчетов);
		
		Задание.ИмяПользователя = Пользователь;
		
		Задание.Использование = ВыполнятьПоРасписанию;
		Задание.Наименование = ПредставлениеЗаданияПоРассылке(Наименование);
		Задание.Расписание = ДополнительныеСвойства.Расписание;
		Задание.Записать();
		
		РегламентноеЗадание = Задание.УникальныйИдентификатор;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	// Соответствие флага подготовленности рассылки и задания пометке удаления рассылки
	Если ПометкаУдаления И Подготовлена Тогда
		Подготовлена = Ложь;
	КонецЕсли;
	
	// Соответствие группы признаку личной рассылки по электронной почте
	// Пользовательские проверки расположены в форме элемента
	// Эти проверки обеспечивают жесткие привязки
	ВыбранаГруппаЛичныхРассылок = (Родитель = Справочники.РассылкиОтчетов.ЛичныеРассылки);
	Если Личная <> ВыбранаГруппаЛичныхРассылок Тогда
		Родитель = ?(Личная, Справочники.РассылкиОтчетов.ЛичныеРассылки, Справочники.РассылкиОтчетов.ПустаяСсылка());
	КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(РегламентноеЗадание);
	Если Задание <> Неопределено Тогда
		Задание.Удалить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	РегламентноеЗадание = Неопределено;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Вызывается непосредственно после записи объекта в базу данных
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(РегламентноеЗадание);
	Если Задание <> Неопределено Тогда
		ЗаданиеИзменено = Ложь;
		
		Если Задание.Использование <> ВыполнятьПоРасписанию Тогда
			Задание.Использование = ВыполнятьПоРасписанию;
			ЗаданиеИзменено = Истина;
		КонецЕсли;
		
		// Расписание устанавливается в форме элемента
		Если ДополнительныеСвойства.Свойство("Расписание") 
				И ТипЗнч(ДополнительныеСвойства.Расписание) = Тип("РасписаниеРегламентногоЗадания")
				И Строка(ДополнительныеСвойства.Расписание) <> Строка(Задание.Расписание)
			Тогда
			Задание.Расписание = ДополнительныеСвойства.Расписание;
			ЗаданиеИзменено = Истина;
		КонецЕсли;
		
		Если Задание.ИмяПользователя <> Пользователь Тогда
			Задание.ИмяПользователя = Пользователь;
			ЗаданиеИзменено = Истина;
		КонецЕсли;
		
		НаименованиеЗадания = ПредставлениеЗаданияПоРассылке(Наименование);
		Если Задание.Наименование <> НаименованиеЗадания Тогда
			Задание.Наименование = НаименованиеЗадания;
			ЗаданиеИзменено = Истина;
		КонецЕсли;
		
		Перезаполнить = Ложь;
		Перезаполнить = Перезаполнить ИЛИ (Задание.Параметры.Количество() <> 1);
		Перезаполнить = Перезаполнить ИЛИ (НЕ ТипЗнч(Задание.Параметры[0]) = Тип("Структура"));
		Перезаполнить = Перезаполнить ИЛИ (НЕ Задание.Параметры[0].Свойство("Рассылка"));
		Перезаполнить = Перезаполнить ИЛИ (НЕ Задание.Параметры[0].Рассылка = Ссылка);
		
		Если Перезаполнить Тогда
			СтруктураПараметров = Новый Структура("Рассылка", Ссылка);
			
			ПараметрыЗадания = Новый Массив;
			ПараметрыЗадания.Добавить(СтруктураПараметров);
			
			Задание.Параметры = ПараметрыЗадания;
			
			ЗаданиеИзменено = Истина;
		КонецЕсли;
		
		//Если Задание.Параметры.Количество() <> 1 ИЛИ Задание.Параметры[0] <> Ссылка Тогда
		//	ПараметрыЗадания = Новый Массив;
		//	ПараметрыЗадания.Добавить(Ссылка);
		//	Задание.Параметры = ПараметрыЗадания;
		//	ЗаданиеИзменено = Истина;
		//КонецЕсли;
			
		Если ЗаданиеИзменено Тогда
			Задание.Записать();
		КонецЕсли; 
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПредставлениеЗаданияПоРассылке(НаименованиеРассылки)
	
	Возврат НСтр("ru = 'Рассылка отчетов: "+СокрЛП(НаименованиеРассылки)+"'");
	
КонецФункции

#КонецЕсли
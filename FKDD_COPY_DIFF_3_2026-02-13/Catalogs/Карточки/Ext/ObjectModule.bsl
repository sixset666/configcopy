// Модуль справочника Карточки

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Функция, проверка корректности родителя карточки
//
// Параметры:
//  Родитель	– СправочникСсылка	– Элемент справочника.
//
// Возвращаемое значение:
//   Булево   	– Наличие родителя.
//
Функция ПроверитьРодителя(Родитель) Экспорт
	Если Родитель.Пустая() Тогда
		Возврат Ложь;
	ИначеЕсли Родитель = Справочники.Карточки.ДисконтнаяКарта Тогда
		Возврат Истина;
	ИначеЕсли Родитель = Справочники.Карточки.КарточкаПользователя Тогда
		Возврат Истина;
	ИначеЕсли Родитель = Справочники.Карточки.СлужебнаяОперация Тогда
		Возврат Истина;
	ИначеЕсли Родитель = Справочники.Карточки.Чистая Тогда
		Возврат Истина;
	ИначеЕсли Родитель = Справочники.Карточки.КлубнаяКарта Тогда
		Возврат Истина;
	ИначеЕсли Родитель = Справочники.Карточки.Талон Тогда
		Возврат Истина;
	Иначе
		Возврат ПроверитьРодителя(Родитель.Родитель);
	КонецЕсли;
КонецФункции // ПроверитьРодителя()

//Функция, получение родителя самого верхнего уровня
//
// Параметры:
//  Родитель	– СправочникСсылка	– Элемент справочника.
//
// Возвращаемое значение:
//   СправочникСсылка			   	– Вид карточки.
//
Функция ПолучитьВидКарточки(Родитель) Экспорт
	Если Родитель.Родитель.Пустая() Тогда
		Возврат Родитель;
	Иначе
		Возврат ПолучитьВидКарточки(Родитель.Родитель);
	КонецЕсли;
КонецФункции // ПолучитьВидКарточки()

//Функция, получение родителя самого верхнего уровня с заданным видом карточки
//
// Параметры:
//  ВидКарточки	– СправочникСсылка	– Элемент справочника.
//
// Возвращаемое значение:
//   СправочникСсылка   			– Родитель.
//
Функция ПолучитьРодителя(ВидКарточки) Экспорт
	ВыборкаКарточки = Справочники.Карточки.Выбрать(Неопределено,,Новый Структура("ВидКарточки",ВидКарточки));
	Если ВыборкаКарточки.Следующий() Тогда
		Возврат ВыборкаКарточки.Ссылка;
	КонецЕсли;
КонецФункции // ПолучитьРодителя()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	ОбязательныеРеквизиты.Вставить("ВидКарточки",1);
	Если ДляЭлемента И (ВидКарточки=Перечисления.ВидыКарточек.Талон ИЛИ ВидКарточки=Перечисления.ВидыКарточек.КлубнаяКарта) Тогда
		ОбязательныеРеквизиты.Вставить("Объект",1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Регистрирует карточку в регистре сведений
//
// Параметры:
//  ШтрихКод	– Строка	– Строка штрих-кода.
//
Процедура Зарегистрировать(ШтрихКод) Экспорт
	НоваяЗапись=РегистрыСведений.ШтрихКоды.СоздатьМенеджерЗаписи(); 
	НоваяЗапись.ШтрихКод = ШтрихКод;
	НоваяЗапись.Объект = ЭтотОбъект.Ссылка;
	НоваяЗапись.Запрет = Ложь;
	НоваяЗапись.Записать(Истина);
КонецПроцедуры // Зарегистрировать()

// Формирует стандартное наименование и представление карточки
// вида ВидКарточки : Объект карточки
// Используется как при интерактивном, так и при программном создании
//
// Параметры:
//  ШтрихКод	– Строка	– Строка штрих-кода.
//
Процедура СформироватьНаименование(ШтрихКод) Экспорт
	Наименование = ШтрихКод;
КонецПроцедуры // СформироватьНаименование()

// Формирует текстовые поля карточки указанного вида
Процедура СформироватьПолноеНаименование() Экспорт
	врВидКарточки = ПолучитьВидКарточки(Родитель);
	ПолноеНаименование = СокрЛП(врВидКарточки.Наименование) + " : ";
	
	Если врВидКарточки = Справочники.Карточки.ДисконтнаяКарта Тогда
		Если ТипЗнч(Объект) <> Тип("СправочникСсылка.Контрагенты") Тогда
			Возврат;
		КонецЕсли;
		ПолноеНаименование = ПолноеНаименование+Объект.Наименование;
		Комментарий   = "Размер скидки определяется магазином";
		
	ИначеЕсли врВидКарточки = Справочники.Карточки.КарточкаПользователя Тогда
		Если ТипЗнч(Объект) <> Тип("СправочникСсылка.Пользователи") Тогда
			Возврат;
		КонецЕсли;
		ПолноеНаименование = ПолноеНаименование+Объект.Наименование;
		
	ИначеЕсли врВидКарточки = Справочники.Карточки.СлужебнаяОперация Тогда
		Если ТипЗнч(Объект) <> Тип("Строка") Тогда
			Возврат;
		КонецЕсли;
		ПолноеНаименование = ПолноеНаименование+Объект;
		
	ИначеЕсли врВидКарточки = Справочники.Карточки.Чистая Тогда
		ПолноеНаименование = "Не используется";
		
	ИначеЕсли врВидКарточки = Справочники.Карточки.КлубнаяКарта Тогда
		Если ТипЗнч(Объект) <> Тип("СправочникСсылка.ДоговорыВзаиморасчетов") Тогда
			Возврат;
		КонецЕсли;
		ПолноеНаименование = ПолноеНаименование+Объект.Владелец.Наименование;
		
	ИначеЕсли врВидКарточки = Справочники.Карточки.Талон Тогда
		Если ТипЗнч(Объект) <> Тип("СправочникСсылка.ДоговорыВзаиморасчетов") Тогда
			Возврат;
		КонецЕсли;
		ВладелецНаименование = СокрЛП(Объект.Владелец.Наименование);
		Если НЕ ПустаяСтрока(ВладелецНаименование) Тогда
			ВладелецНаименование = ВладелецНаименование+" ";
		КонецЕсли;
		ПолноеНаименование = ПолноеНаименование+ВладелецНаименование+?(НЕ обЗначениеНеЗаполнено(Номинал),Номинал,"0.00")+" руб.";
		
	КонецЕсли;
КонецПроцедуры // СформироватьПолноеНаименование()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если Основание = Неопределено Тогда
		ВидКарточки = ПолучитьВидКарточки(Родитель).ВидКарточки;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	спПриКопировании(ЭтотОбъект, ОбъектКопирования);
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	спПередЗаписью(ЭтотОбъект, Отказ);
	Если ЭтоНовый() И НЕ ЭтоГруппа Тогда
		ДатаСоздания=НачалоДня(ТекущаяДата());
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	спПриЗаписи(ЭтотОбъект, Отказ);
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	спПередУдалением(ЭтотОбъект, Отказ);
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

&НаКлиенте
Процедура УстановитьОграничениеТипа()
	
	Если Объект.ПользовательскаяНастройка = Истина Тогда
		Элементы.НазначениеДоступа.Доступность       = Ложь;
		Элементы.ОбъектДоступа.Доступность           = Ложь;
		Элементы.ОбъектДоступа.ОтметкаНезаполненного = Ложь;
	Иначе
		Элементы.НазначениеДоступа.Доступность       = Истина;
		Элементы.ОбъектДоступа.Доступность           = Истина;
		Элементы.ОбъектДоступа.ОтметкаНезаполненного = Истина;
		Если Объект.НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Организация") Тогда
			ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Организации");
			Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ИначеЕсли Объект.НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Подразделение") Тогда
			ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании");
			Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ИначеЕсли Объект.НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Пользователь") Тогда
			ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
			Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		Иначе
			Элементы.ОбъектДоступа.Доступность           = Ложь;
			Элементы.ОбъектДоступа.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам") Тогда
		СправочникИмя = Метаданные.Справочники.ВариантыОтчетов.Имя;
		ПравоДоступа = обПраво("Право доступа "+СправочникИмя);
		Если ПравоДоступа=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
			Отказ=Истина;
			Возврат;
		ИначеЕсли ПравоДоступа=Перечисления.ВидыПравДляСправочников.Чтение Тогда
			ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ПользовательскаяНастройка = Истина;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		ОшибкаОткрытия = НСтр("ru = 'Новый вариант отчета можно создать только из формы отчета'");
		Возврат;
	КонецЕсли;
	
	Если обПраво("РазрешитьРедактироватьОбщиеВариантыОтчетов") Тогда
		Элементы.НазначениеДоступа.Доступность         = Истина;
		Элементы.ОбъектДоступа.Доступность             = Истина;
		Элементы.ПользовательскаяНастройка.Доступность = Истина;
	ИначеЕсли Объект.Автор = ПараметрыСеанса.Пользователь И Объект.ПользовательскаяНастройка Тогда
		Элементы.НазначениеДоступа.Доступность         = Ложь;
		Элементы.ОбъектДоступа.Доступность             = Ложь;
		Элементы.ПользовательскаяНастройка.Доступность = Ложь;
	Иначе
		ЭтаФорма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Объект.ЭтоПредопределенный Тогда
		Элементы.ГруппаДоступности.Доступность = Ложь;
		Элементы.ПользовательскаяНастройка.Доступность = Ложь;
	КонецЕсли;
	
	СписокВыбора = Элементы.ВариантЗагрузкиПериода.СписокВыбора;
	Для Каждого ТекЭлемент Из ВариантСтандартногоПериода Цикл
		
		ИмяЭлемента = ПолучитьПолноеИмяПредопределенногоЗначения(ТекЭлемент);
		Если ИмяЭлемента = "ВариантСтандартногоПериода.ПроизвольныйПериод" Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВыбора.Добавить(ПолучитьПолноеИмяПредопределенногоЗначения(ТекЭлемент), Строка(ТекЭлемент));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ПустаяСтрока(ОшибкаОткрытия) Тогда
		Если Объект.Ссылка.Пустая() Тогда
			Отказ = Истина;
		Иначе
			ТолькоПросмотр = Истина;
		КонецЕсли;
		Предупреждение(ОшибкаОткрытия);
	КонецЕсли;
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МногострочныйТекст   = Объект.Описание;
	
	ТекстВведен = ВвестиСтроку(МногострочныйТекст, НСтр("ru = 'Описание'"),, Истина);
	
	Если ТекстВведен Тогда
		Объект.Описание = МногострочныйТекст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеДоступаПриИзменении(Элемент)
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскаяНастройкаПриИзменении(Элемент)
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры

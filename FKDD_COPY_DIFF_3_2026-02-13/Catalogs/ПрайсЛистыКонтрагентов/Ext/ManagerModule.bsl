
//Формирует представление интервала срока поставки
//
// Параметры:
//  СрокПоставкиМинЧисло	- Число	- минимальный срок поставки в секундах.
//  СрокПоставкиМаксЧисло	- Число	- максимальный срок поставки в секундах.
//
// Возвращаемое значение:
//   Строка   – Сформированное представление интервала.
//
Функция ПредставлениеСрокаПоставкиИнтервал(СрокПоставкиМинЧисло, СрокПоставкиМаксЧисло) Экспорт
	СрокПоставки = "";
	ПостфиксМин  = "";
	ПостфиксМакс = "";
	Если (СрокПоставкиМинЧисло + СрокПоставкиМаксЧисло) > 0 Тогда
		Если СрокПоставкиМинЧисло <> СрокПоставкиМаксЧисло Тогда
			СрокПоставкиМин  = ПредставлениеСрокаПоставки(СрокПоставкиМинЧисло, ПостфиксМин);
			СрокПоставкиМакс = ПредставлениеСрокаПоставки(СрокПоставкиМаксЧисло, ПостфиксМакс);
			Если ПостфиксМин = ПостфиксМакс Тогда
				СрокПоставки = ?(СрокПоставкиМинЧисло > 0, СтрЗаменить(СрокПоставкиМин, ПостфиксМин, "") + " - "," до ") + СрокПоставкиМакс;
			Иначе
				СрокПоставки = ?(СрокПоставкиМинЧисло > 0, "от " + СрокПоставкиМин, "") + " до " + СрокПоставкиМакс;
			КонецЕсли;
		Иначе
			СрокПоставкиМин = ПредставлениеСрокаПоставки(СрокПоставкиМинЧисло, ПостфиксМин);
			СрокПоставки = СрокПоставкиМин;
		КонецЕсли;
	Иначе
		СрокПоставки = "0 дн.(наличие)";
	КонецЕсли;
	Возврат СрокПоставки;
КонецФункции //ПредставлениеСрокаПоставкиИнтервал()

//Формирует представление срока поставки
//
// Параметры:
//  СрокПоставкиЧисло - Число	- сроок поставки в секундах.
//  Постфикс          - Строка	- Постфикс представления срока поставки.
//
// Возвращаемое значение:
//   Строка   – Сформированное представление.
//
Функция ПредставлениеСрокаПоставки(СрокПоставкиЧисло, Постфикс = "") Экспорт
	Если СрокПоставкиЧисло < 180 Тогда // Меньше 6 минут - это косяк?
		pragDeliveryTimeInSecond = Ложь;
	КонецЕсли;
	СрокПоставки = "";
	// Менее одного дня - отображаем в часах, менее 2-х часов отображаем в часах с минутами, менее часа отобразаем в 15 минутном интервале
	Дни = Цел(СрокПоставкиЧисло/(24*60*60));
	Часы = Цел((СрокПоставкиЧисло%(24*60*60))/3600);
	Минуты = Цел((СрокПоставкиЧисло%(60*60))/60);
	Если Дни >= 1 Тогда
		// Указываем только дни!
		СрокПоставки = Формат(Дни, "ЧГ=0") + " дн.";
		Постфикс = " дн.";
	Иначе
		СрокПоставки = ?(Дни > 0, Формат(Дни, "ЧГ=0")+" дн.", "") + ?(Часы > 0, Формат(Часы, "ЧГ=0")+" ч.", "") + ?(Минуты > 0, Формат(Минуты, "ЧГ=0")+" мин.", "");
		Постфикс = ?(Дни > 0, " дн.", ?(Часы > 0, " ч.", ?(Минуты > 0, " мин.", "")));
	КонецЕсли;
	Возврат СрокПоставки;
КонецФункции //ПредставлениеСрокаПоставки()

//Расчитывает срок поставки 
//
// Параметры:
//  БазоваяДата       - Дата	- Стартовая дата от которой расчитывается срок поставки.
//  СрокПоставкиЧисло - Число	- максимальная продолжительность поставки.
//  ТолькоРабочиеДни  - Булево  - Признак необходимости учитывать рабочие дни
//
// Возвращаемое значение:
//   Дата   – Расчитанный срок поставки.
//
Функция РассчитатьСрокПоставки(БазоваяДата, СрокПоставкиЧисло, ТолькоРабочиеДни = Ложь) Экспорт
	
	Если обЗначениеНеЗаполнено(БазоваяДата) Тогда
		БазоваяДата = ТекущаяДата();
	КонецЕсли;
	
	Если СрокПоставкиЧисло < 180 Тогда // Меньше 6 минут - это косяк?
		pragDeliveryTimeInSecond = Ложь;
	КонецЕсли;
	СрокПоставки = "";
	// Менее одного дня - отображаем в часах, менее 2-х часов отображаем в часах с минутами, менее часа отобразаем в 15 минутном интервале
	Дни = Цел(СрокПоставкиЧисло/(24*60*60));
	Часы = Цел((СрокПоставкиЧисло%(24*60*60))/3600);
	Минуты = Цел((СрокПоставкиЧисло%(60*60))/60);
	
	Если ТолькоРабочиеДни Тогда
		ОдинДень = 60*60*24;
		ДеньПоставки = 0;
		Пока ДеньПоставки < Дни Цикл
			Если ДеньНедели(БазоваяДата + ДеньПоставки * ОдинДень) > 5 Тогда
				// Попали на выходной, поэтому удлиняем дни
				Дни = Дни + 1;
			КонецЕсли;
			ДеньПоставки = ДеньПоставки + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если Дни >= 1 Тогда
		// Смещаем и приводим к концу дня
		СрокПоставки = КонецДня(БазоваяДата + Дни*(24*60*60) + Часы*(60*60) + Минуты*60);
	Иначе
		// Считаем простое смещение
		СрокПоставки = БазоваяДата + Дни*(24*60*60) + Часы*(60*60) + Минуты*60;
	КонецЕсли;
	
	Возврат СрокПоставки;
КонецФункции //РассчитатьСрокПоставки()

Функция ПолучитьСценарийАвтозагрузки(ПрайсЛист) Экспорт
	
	СтруктураАвтозагрузки = СформироватьСтруктуруСценарияАвтозагрузки(ПрайсЛист);
	
	Менеджер = РегистрыСведений.ПрайсЛистыКонтрагентовАвтообновление.СоздатьМенеджерЗаписи();
	Менеджер.ПрайсЛист = ПрайсЛист;
	Менеджер.Прочитать();
	
	Если Менеджер.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураАвтозагрузки, Менеджер); // ПериодичностьПроверкиНовыхДанных, ДатаСледующейПроверки, ИнтервалПроверкиНачало, ИнтервалПроверкиКонец, ДатаФайла, ХешФайла, ДатаФайлаЗагруженного, ХешФайлаЗагруженного
		НастройкаКаталогов = Менеджер.НастройкаКаталогов.Получить(); // Каталоги и маски файлов
		Если ТипЗнч(НастройкаКаталогов) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(СтруктураАвтозагрузки, НастройкаКаталогов);
		КонецЕсли;
		НастройкаТранспорта = Менеджер.НастройкаТранспорта.Получить(); // Настройки транспорта
		Если ТипЗнч(НастройкаТранспорта) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(СтруктураАвтозагрузки, НастройкаТранспорта);
		КонецЕсли;
	КонецЕсли;
		
	Возврат СтруктураАвтозагрузки;
	
КонецФункции

Функция СформироватьСтруктуруСценарияАвтозагрузки(ПрайсЛист, ПроверятьФайл = Истина) Экспорт
	
	Если ПроверятьФайл И ПрайсЛист.ФайлИсточникДанных И ЗначениеЗаполнено(ПрайсЛист.СтрокаПодключения) Тогда
		СтруктураФайла = ПолучитьСтруктуруФайла(ПрайсЛист.СтрокаПодключения);
	Иначе
		СтруктураФайла = ПолучитьСтруктуруФайла();
	КонецЕсли;
	
	СтруктураАвтозагрузки = Новый Структура;
	
	СтруктураАвтозагрузки.Вставить("ИспользоватьАвтозагрузку", Ложь);
	СтруктураАвтозагрузки.Вставить("ИспользоватьАвтообновление", Ложь);
	СтруктураАвтозагрузки.Вставить("КаталогФайлаПрайсЛиста", СтруктураФайла.КаталогФайла);
	
	СтруктураАвтозагрузки.Вставить("МаскаФайлаПрайсЛиста", СтруктураФайла.ИмяФайла); // Может быть именем файла при загрузках из источников данных типа dbf, mdb
	СтруктураАвтозагрузки.Вставить("ФорматМаскиФайла", ?(ЗначениеЗаполнено(СтруктураФайла.РасширениеФайла), СтруктураФайла.РасширениеФайла,  ".*"));	
	
	СтруктураАвтозагрузки.Вставить("флФайлВАрхиве", Ложь);
	СтруктураАвтозагрузки.Вставить("МаскаАрхива", СтруктураФайла.ИмяФайлаБезРасширения);
	СтруктураАвтозагрузки.Вставить("ФорматМаскиАрхива", ".*");
	
	СтруктураАвтозагрузки.Вставить("ДействиеСОбработаннымиФайлами", ""); // Ничего не делать
	СтруктураАвтозагрузки.Вставить("ПрайсИзИнтернета", Ложь);
	СтруктураАвтозагрузки.Вставить("СохранятьВКаталог", Истина);
	
	СтруктураАвтозагрузки.Вставить("ТранспортПротокол", "FILE"); // FTP,FTPS, HTTP, HTTPS, POP3, IMAP
	СтруктураАвтозагрузки.Вставить("URL", "");
	СтруктураАвтозагрузки.Вставить("ТранспортСервер", "");
	СтруктураАвтозагрузки.Вставить("ТранспортПорт", "80");
	СтруктураАвтозагрузки.Вставить("Таймаут", 60);
	
	СтруктураАвтозагрузки.Вставить("ТребуетсяАвторизация", Ложь);
	СтруктураАвтозагрузки.Вставить("ТранспортЛогин", "");
	СтруктураАвтозагрузки.Вставить("ТранспортПароль", "");
	
	СтруктураАвтозагрузки.Вставить("FtpКаталог", "");
	СтруктураАвтозагрузки.Вставить("FtpПассивныйРежим", Истина);
	
	СтруктураАвтозагрузки.Вставить("SSL", Истина);
	СтруктураАвтозагрузки.Вставить("EMailАутентификация", Перечисления.СпособАутентификацииPOP3.Обычная);
	
	СтруктураАвтозагрузки.Вставить("ПочтовыйЯщик", "");
	
	СтруктураАвтозагрузки.Вставить("EmailАдресОтправителя");	// Обязательное поле, так как Имена файлов часто бывают одинаковые "Price.xls" ...
	СтруктураАвтозагрузки.Вставить("EmailТемаПисьма");			// Содержит
	СтруктураАвтозагрузки.Вставить("EmailТекстПисьма");			// Содержит
	
	СтруктураАвтозагрузки.Вставить("ПериодичностьПроверкиНовыхДанных", 1);
	СтруктураАвтозагрузки.Вставить("ДатаСледующейПроверки" , '00010101000000');
	СтруктураАвтозагрузки.Вставить("ИнтервалПроверкиНачало" , 0);  // Время начала проверки обновлений
	СтруктураАвтозагрузки.Вставить("ИнтервалПроверкиКонец" , 24); // Время конца проверки обновлений
	
	СтруктураАвтозагрузки.Вставить("ДатаФайла", '00010101000000'); // Это дата проверяемого файла или дата письма
	СтруктураАвтозагрузки.Вставить("ХешФайла", ""); // Это хеш проверяемого файла или идентификатор письма!
	
	Возврат СтруктураАвтозагрузки;
	
КонецФункции

Функция ПолучитьСтруктуруФайла(ТекИмяФайла = Неопределено) Экспорт
	СтруктураФайла = Новый Структура;
	СтруктураФайла.Вставить("КаталогФайла", "");
	СтруктураФайла.Вставить("ИмяФайла", "");
	СтруктураФайла.Вставить("РасширениеФайла", "");
	СтруктураФайла.Вставить("ИмяФайлаБезРасширения","");
	СтруктураФайла.Вставить("ДатаФайла", '00010101000000');
	СтруктураФайла.Вставить("ХешФайла", "");
	
	Если ЗначениеЗаполнено(ТекИмяФайла) Тогда
		Файл = Новый Файл(ТекИмяФайла);
		СтруктураФайла.КаталогФайла = Файл.Путь;
		СтруктураФайла.ИмяФайла = Файл.Имя;
		СтруктураФайла.РасширениеФайла = Файл.Расширение;
		СтруктураФайла.ИмяФайлаБезРасширения = Файл.ИмяБезРасширения;
	КонецЕсли;
	
	Возврат СтруктураФайла;
КонецФункции

Процедура СохранитьСценарийАвтозагрузки(ПрайсЛист, СтруктураАвтозагрузки) Экспорт
	
	Если ТранзакцияАктивна() Тогда
		БылаТранзакция = Истина;
	Иначе
		БылаТранзакция = Ложь;
		НачатьТранзакцию();
	КонецЕсли;
	
	//Заблокируем Данные записи регистра "ПрайсЛистыКонтрагентовАвтообновление"
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентовАвтообновление");
	ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛист);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	//Сохраним сценарий
	Менеджер = РегистрыСведений.ПрайсЛистыКонтрагентовАвтообновление.СоздатьМенеджерЗаписи();
	Менеджер.ПрайсЛист = ПрайсЛист;
	Менеджер.Прочитать();
	
	Если Не Менеджер.Выбран() Тогда
		Менеджер.ПрайсЛист = ПрайсЛист;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Менеджер, СтруктураАвтозагрузки);
	
	НастройкаКаталогов  = СформироватьСтруктуруНастройкаКаталогов();
	ЗаполнитьЗначенияСвойств(НастройкаКаталогов, СтруктураАвтозагрузки);
	
	НастройкаТранспорта = СформироватьСтруктуруНастройкаТранспорта();
	ЗаполнитьЗначенияСвойств(НастройкаТранспорта, СтруктураАвтозагрузки);
	
	Менеджер.НастройкаКаталогов  = Новый ХранилищеЗначения(НастройкаКаталогов);
	Менеджер.НастройкаТранспорта = Новый ХранилищеЗначения(НастройкаТранспорта);
	
	Менеджер.Записать();
	
	Если Не БылаТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры //СохранитьСценарийАвтозагрузки()

Функция СформироватьСтруктуруНастройкаКаталогов() Экспорт
	НастройкаКаталогов = Новый Структура;
	НастройкаКаталогов.Вставить("КаталогФайлаПрайсЛиста", "");
	НастройкаКаталогов.Вставить("МаскаФайлаПрайсЛиста", "");
	НастройкаКаталогов.Вставить("МаскаАрхива", "");
	НастройкаКаталогов.Вставить("ФорматМаскиАрхива", "");
	НастройкаКаталогов.Вставить("ФорматМаскиФайла", "");
	НастройкаКаталогов.Вставить("флФайлВАрхиве", Ложь);
	НастройкаКаталогов.Вставить("ДействиеСОбработаннымиФайлами", ""); // Ничего не делать
	НастройкаКаталогов.Вставить("ПрайсИзИнтернета", Ложь);
	НастройкаКаталогов.Вставить("СохранятьВКаталог", Ложь);
	Возврат НастройкаКаталогов;
КонецФункции //СформироватьСтруктуруНастройкаКаталогов()

Функция СформироватьСтруктуруНастройкаТранспорта() Экспорт
	НастройкаТранспорта = Новый Структура;
	НастройкаТранспорта.Вставить("ТранспортСервер", "");
	НастройкаТранспорта.Вставить("ТранспортПротокол", "");
	НастройкаТранспорта.Вставить("ТранспортПорт", "");
	НастройкаТранспорта.Вставить("ТранспортЛогин", "");
	НастройкаТранспорта.Вставить("ТранспортПароль", "");
	
	НастройкаТранспорта.Вставить("FtpКаталог", "");
	НастройкаТранспорта.Вставить("FtpПассивныйРежим", Истина);
	
	НастройкаТранспорта.Вставить("URL", "");
	
	НастройкаТранспорта.Вставить("Таймаут", 60);
	НастройкаТранспорта.Вставить("EMailАутентификация", Перечисления.СпособАутентификацииPOP3.Обычная);
	НастройкаТранспорта.Вставить("ПочтовыйЯщик", "");
	НастройкаТранспорта.Вставить("EmailАдресОтправителя");	// Обязательное поле, так как Имена файлов часто бывают одинаковые "Price.xls" ...
	НастройкаТранспорта.Вставить("EmailТемаПисьма");		// Содержит
	НастройкаТранспорта.Вставить("EmailТекстПисьма");		// Содержит
	
	НастройкаТранспорта.Вставить("ТребуетсяАвторизация", Ложь);
	НастройкаТранспорта.Вставить("SSL", Истина);
	
	Возврат НастройкаТранспорта;
КонецФункции

Функция РассчитатьЦеныПрайсЛиста(ПрайсЛист, Покупка, Продажа, ТипЦен, ПодразделениеКомпании, Дата, Цена, Номенклатура=Неопределено, ТегПозиции=Неопределено, Производитель=Неопределено) Экспорт
	
	Если Покупка И Продажа Тогда
		Возврат РассчитатьЦенуПокупкиИПродажи(ПрайсЛист, ТипЦен, ПодразделениеКомпании, Дата, Цена, Номенклатура, ТегПозиции, Производитель);
	ИначеЕсли Продажа Тогда
		Возврат РассчитатьЦенуПродажи(ПрайсЛист, ТипЦен, ПодразделениеКомпании, Дата, Цена, Номенклатура, ТегПозиции, Производитель);
	Иначе // Покупка
		Возврат РассчитатьЦенуПокупки(ПрайсЛист, Дата, Цена, ТегПозиции, Производитель);
	КонецЕсли;
	
КонецФункции

//Получает скидку, определяет ее размер и цену с ее учетом
// Параметры:
//  ПрайсЛист - Ссылка - Ссылка на прайс-лист по которому получается скидка;
//  Дата - Дата - Время, на которое получается скидка;
//  Цена - Число - Цена, к которой применяется скидка.
//
// Возвращаемое значение:
//  Структура - Структура данных по скидке. Содержит процент скидки. 
//  При существовании скидки и переданной цене возвращает также размер скидки и цену с ее учетом
Функция РассчитатьЦенуПокупки(ПрайсЛист, Дата, Цена, ТегПозиции, Производитель)
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата",          Дата);
	Запрос.УстановитьПараметр("ПрайсЛист",     ПрайсЛист);
	Запрос.УстановитьПараметр("ТегПозиции",    ?(ТегПозиции = Неопределено, "", ТегПозиции));
	Запрос.УстановитьПараметр("Производитель", ?(Производитель = Неопределено, Справочники.Производители.ПустаяСсылка(), Производитель));
	
	Если ТегПозиции = Неопределено И Производитель = Неопределено Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СкидкиБазовые.Скидка, 0) КАК Скидка
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
		|ГДЕ
		|	НЕ СкидкиБазовые.Отменена";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0)))) КАК Скидка
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист = &ПрайсЛист
		|					И ТегПозиции = &ТегПозиции
		|					И Производитель = &Производитель) КАК Скидки
		|		ПО (НЕ Скидки.Отменена)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист = &ПрайсЛист
		|					И ТегПозиции = &ТегПозиции
		|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиТегПозиции
		|		ПО (НЕ СкидкиТегПозиции.Отменена)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист = &ПрайсЛист
		|					И ТегПозиции = """"
		|					И Производитель = &Производитель) КАК СкидкиПроизводитель
		|		ПО (НЕ СкидкиПроизводитель.Отменена)
		|";
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураДанных = Новый Структура("ЦенаВПрайсЛисте, Скидка, ЦенаПокупки", Цена, 0, Цена);
	
	Если Выборка.Следующий() Тогда
		СтруктураДанных.Скидка = Мин(100, Выборка.Скидка);
		СтруктураДанных.ЦенаПокупки = Окр(СтруктураДанных.ЦенаВПрайсЛисте * (100 - СтруктураДанных.Скидка) / 100, 2);
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ПолучитьТекстЗапросаНачало(Запрос, ПодразделениеКомпании, ТегПозиции, Производитель)
	
	ТекстЗапросаНачало = "ВЫБОР";
	
	МассивПодразделений = Новый Массив;
	ТекущееПодразделение = ПодразделениеКомпании;
	ПеременнаяЦикла = 0;
	
	Пока Истина Цикл
		// Отбор подразделений
		МассивПодразделений.Добавить(ТекущееПодразделение);
		
		// Сортировка подразделений
		Если ТегПозиции = Неопределено И Производитель = Неопределено Тогда
			// Ничего не указано
			ТекстЗапросаНачало = ТекстЗапросаНачало + "
			|	КОГДА НаценкиБазовые.ПодразделениеКомпании = &Подразделение"+ПеременнаяЦикла+"
			|		ТОГДА "+ПеременнаяЦикла;
		Иначе
			// Будет полный набор и Тег позиции и Производитель
			ТекстЗапросаНачало = ТекстЗапросаНачало + "
			|	КОГДА ЕСТЬNULL(Наценки.ПодразделениеКомпании, ЕСТЬNULL(НаценкиТегПозиции.ПодразделениеКомпании, ЕСТЬNULL(НаценкиПроизводитель.ПодразделениеКомпании, НаценкиБазовые.ПодразделениеКомпании))) = &Подразделение"+ПеременнаяЦикла+"
			|		ТОГДА "+ПеременнаяЦикла;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Подразделение"+ПеременнаяЦикла, ТекущееПодразделение);
		
		// Получаем родителя подразделения
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		Если обЗначениеНеЗаполнено(ТекущееПодразделение) Тогда
			// Добавим пустое подразделение
			ПеременнаяЦикла = ПеременнаяЦикла + 1;
			ТекущееПодразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
			МассивПодразделений.Добавить(ТекущееПодразделение);
			
			Если ТегПозиции = Неопределено И Производитель = Неопределено Тогда
				// Ничего не указано
				ТекстЗапросаНачало = ТекстЗапросаНачало + "
				|	КОГДА НаценкиБазовые.ПодразделениеКомпании = &Подразделение"+ПеременнаяЦикла+"
				|		ТОГДА "+ПеременнаяЦикла;
			Иначе
				// Будет полный набор и Тег позиции и Производитель
				ТекстЗапросаНачало = ТекстЗапросаНачало + "
				|	КОГДА ЕСТЬNULL(Наценки.ПодразделениеКомпании, ЕСТЬNULL(НаценкиТегПозиции.ПодразделениеКомпании, ЕСТЬNULL(НаценкиПроизводитель.ПодразделениеКомпании, НаценкиБазовые.ПодразделениеКомпании))) = &Подразделение"+ПеременнаяЦикла+"
				|		ТОГДА "+ПеременнаяЦикла;
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Подразделение"+ПеременнаяЦикла,ТекущееПодразделение);
			
			Прервать;
		КонецЕсли;
		
		ПеременнаяЦикла = ПеременнаяЦикла + 1;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	
	ТекстЗапросаНачало = ТекстЗапросаНачало + " ИНАЧЕ 999 КОНЕЦ";
	
	Возврат ТекстЗапросаНачало;
	
КонецФункции

//Получает скидку и наценку, расчитывает цены с их учетом
// Параметры:
//  ПрайсЛист - Ссылка - Ссылка на прайс-лист по которому получается наценка;
//  ТипЦен    - Ссылка - Тип цен на который назначается наценка;
//  ПодразделениеКомпании - Ссылка - Подразделение на которое назначается наценка;
//  Дата - Дата - Время, на которое получается наценка;
//  ЦенаПокупки - Число - Цена, к которой применяется наценка с учетом НДС, как в прайс-листе.
//
// Возвращаемое значение:
//  Структура - Структура данных по наценке. Содержит процент наценки. 
//  При существовании наценки и переданной цене возвращает также размер наценки и цену с ее учетом
Функция РассчитатьЦенуПродажи(ПрайсЛист, ТипЦен, ПодразделениеКомпании, Дата, ЦенаПокупки, Номенклатура, ТегПозиции, Производитель)
	
	СтруктураДанных = Новый Структура("Наценка, ОкруглятьДо, ЦенаПродажи", Неопределено, 1, 0);
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапросаНачало = ПолучитьТекстЗапросаНачало(Запрос, ПодразделениеКомпании, ТегПозиции, Производитель);
	
	Если ТегПозиции = Неопределено И Производитель = Неопределено Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НаценкиБазовые.АлгоритмРасчетаЦены КАК АлгоритмРасчетаЦены,
		|	НаценкиБазовые.Наценка КАК Наценка,
		|	НаценкиБазовые.ОкруглятьДо КАК ОкруглятьДо,
		|" +  ТекстЗапросаНачало + "
		|	КАК Порядок
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиБазовые
		|ГДЕ НЕ НаценкиБазовые.Отменена";
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(Наценки.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиТегПозиции.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиПроизводитель.АлгоритмРасчетаЦены, НаценкиБазовые.АлгоритмРасчетаЦены))) КАК АлгоритмРасчетаЦены,
		|	ЕСТЬNULL(Наценки.Наценка, ЕСТЬNULL(НаценкиТегПозиции.Наценка, ЕСТЬNULL(НаценкиПроизводитель.Наценка, НаценкиБазовые.Наценка))) КАК Наценка,
		|	ЕСТЬNULL(Наценки.ОкруглятьДо, ЕСТЬNULL(НаценкиТегПозиции.ОкруглятьДо, ЕСТЬNULL(НаценкиПроизводитель.ОкруглятьДо, НаценкиБазовые.ОкруглятьДо))) КАК ОкруглятьДо,
		|" + ТекстЗапросаНачало + "
		|	КАК Порядок
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиБазовые
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = &ТегПозиции
		|				И Производитель = &Производитель) КАК Наценки
		|		ПО (НЕ Наценки.Отменена)
		|			И Наценки.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = &ТегПозиции
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиТегПозиции
		|		ПО (НЕ НаценкиТегПозиции.Отменена)
		|			И НаценкиТегПозиции.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = """"
		|				И Производитель = &Производитель) КАК НаценкиПроизводитель
		|		ПО (НЕ НаценкиПроизводитель.Отменена)
		|			И НаценкиПроизводитель.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании
		|ГДЕ НЕ НаценкиБазовые.Отменена";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + " И " + ТекстЗапросаНачало + " < 999
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
		
	Запрос.УстановитьПараметр("Дата",          Дата);
	Запрос.УстановитьПараметр("ПрайсЛист",     ПрайсЛист);
	Запрос.УстановитьПараметр("ТипЦен",        ТипЦен);
	Запрос.УстановитьПараметр("ТегПозиции",    ?(ТегПозиции = Неопределено, "", ТегПозиции));
	Запрос.УстановитьПараметр("Производитель", ?(Производитель = Неопределено, Справочники.Производители.ПустаяСсылка(), Производитель));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И Выборка.Наценка <> Null Тогда
		
		СтруктураДанных.ОкруглятьДо = ?(ЗначениеЗаполнено(Выборка.ОкруглятьДо), Выборка.ОкруглятьДо, 1);
		
		ЦенаВПрайсЛисте = 0;
		Цена = ЦенаПокупки;
		
		Если ЗначениеЗаполнено(Выборка.АлгоритмРасчетаЦены) Тогда
			СтруктураДанных.Наценка = Выборка.АлгоритмРасчетаЦены;
			Если Найти(Выборка.АлгоритмРасчетаЦены, "ЦенаНоменклатуры") > 0 Тогда
				ЦенаНоменклатуры = обПолучитьЦену(ТипЦен, Номенклатура, Дата,,,,,, ПодразделениеКомпании);
				Если ЦенаНоменклатуры > 0 И ПрайсЛист.ЦенаВключаетНДС <> ТипЦен.ЦенаВключаетНДС Тогда
					Если ТипЦен.ЦенаВключаетНДС Тогда
						// Что бы цены были как в прайс-листе уберем НДС
						ЦенаНоменклатуры = Окр(ЦенаНоменклатуры * 100 / (100 + Номенклатура.СтавкаНДС.Ставка), 2);
					Иначе
						// Что бы цены были как в прайс-листе добавим НДС
						ЦенаНоменклатуры = Окр(ЦенаНоменклатуры * (100 + Номенклатура.СтавкаНДС.Ставка) / 100, 2);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Найти(Выборка.АлгоритмРасчетаЦены, "#Процедура") > 0 Тогда
				Попытка
					Выполнить(СтрЗаменить(Выборка.АлгоритмРасчетаЦены, "#Процедура", ""));
				Исключение
					Цена = 0;
				КонецПопытки;
			Иначе
				Попытка
					Выполнить("Цена = Макс(0, " + Выборка.АлгоритмРасчетаЦены + ")");
				Исключение
					Цена = 0;
				КонецПопытки;
			КонецЕсли;
		Иначе
			СтруктураДанных.Наценка = Выборка.Наценка;
			Цена = Макс(0, Окр(ЦенаПокупки * (100 + СтруктураДанных.Наценка) / 100, 2));
		КонецЕсли;
		
		ДельтаОкругления = Цена / СтруктураДанных.ОкруглятьДо;
		Если ДельтаОкругления > Цел(ДельтаОкругления) Тогда // Требуется округлить
			СтруктураДанных.ЦенаПродажи = (Цел(ДельтаОкругления) + 1) * СтруктураДанных.ОкруглятьДо;
		Иначе
			СтруктураДанных.ЦенаПродажи = Цена;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

//Получает скидку и наценку, расчитывает цены с их учетом
// Параметры:
//  ПрайсЛист - Ссылка - Ссылка на прайс-лист по которому получается наценка;
//  ТипЦен    - Ссылка - Тип цен на который назначается наценка;
//  ПодразделениеКомпании - Ссылка - Подразделение на которое назначается наценка;
//  Дата - Дата - Время, на которое получается наценка;
//  Цена - Число - Цена, к которой применяется наценка.
//
// Возвращаемое значение:
//  Структура - Структура данных по наценке. Содержит процент наценки. 
//  При существовании наценки и переданной цене возвращает также размер наценки и цену с ее учетом
Функция РассчитатьЦенуПокупкиИПродажи(ПрайсЛист, ТипЦен, ПодразделениеКомпании, Дата, Цена, Номенклатура, ТегПозиции, Производитель)
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапросаНачало = ПолучитьТекстЗапросаНачало(Запрос, ПодразделениеКомпании, ТегПозиции, Производитель);
	
	Если ТегПозиции = Неопределено И Производитель = Неопределено Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(СкидкиБазовые.Скидка, 0) КАК Скидка,
		|	НаценкиБазовые.АлгоритмРасчетаЦены КАК АлгоритмРасчетаЦены,
		|	НаценкиБазовые.Наценка КАК Наценка,
		|	НаценкиБазовые.ОкруглятьДо КАК ОкруглятьДо,
		|" +  ТекстЗапросаНачало + "
		|	КАК Порядок
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиБазовые
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист = &ПрайсЛист
		|					И ТегПозиции = """"
		|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|			) КАК СкидкиБазовые
		|		ПО (НЕ СкидкиБазовые.Отменена)
		|ГДЕ
		|	НЕ НаценкиБазовые.Отменена";
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0)))) КАК Скидка,
		|	ЕСТЬNULL(Наценки.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиТегПозиции.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиПроизводитель.АлгоритмРасчетаЦены, НаценкиБазовые.АлгоритмРасчетаЦены))) КАК АлгоритмРасчетаЦены,
		|	ЕСТЬNULL(Наценки.Наценка, ЕСТЬNULL(НаценкиТегПозиции.Наценка, ЕСТЬNULL(НаценкиПроизводитель.Наценка, НаценкиБазовые.Наценка))) КАК Наценка,
		|	ЕСТЬNULL(Наценки.ОкруглятьДо, ЕСТЬNULL(НаценкиТегПозиции.ОкруглятьДо, ЕСТЬNULL(НаценкиПроизводитель.ОкруглятьДо, НаценкиБазовые.ОкруглятьДо))) КАК ОкруглятьДо,
		|" + ТекстЗапросаНачало + "
		|	КАК Порядок
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|				) КАК НаценкиБазовые
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = &ТегПозиции
		|				И Производитель = &Производитель
		|				) КАК Наценки
		|		ПО НЕ Наценки.Отменена
		|			И Наценки.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = &ТегПозиции
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|				) КАК НаценкиТегПозиции
		|		ПО НЕ НаценкиТегПозиции.Отменена
		|			И НаценкиТегПозиции.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТипЦен = &ТипЦен
		|				И ПодразделениеКомпании В (&МассивПодразделений)
		|				И ТегПозиции = """"
		|				И Производитель = &Производитель
		|				) КАК НаценкиПроизводитель
		|		ПО НЕ НаценкиПроизводитель.Отменена
		|			И НаценкиПроизводитель.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТегПозиции = &ТегПозиции
		|				И Производитель = &Производитель
		|				) КАК Скидки
		|		ПО НЕ Скидки.Отменена
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТегПозиции = &ТегПозиции
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|				) КАК СкидкиТегПозиции
		|		ПО НЕ СкидкиТегПозиции.Отменена
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТегПозиции = """"
		|				И Производитель = &Производитель
		|				) КАК СкидкиПроизводитель
		|		ПО НЕ СкидкиПроизводитель.Отменена
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист = &ПрайсЛист
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|				) КАК СкидкиБазовые
		|		ПО НЕ СкидкиБазовые.Отменена
		|ГДЕ НЕ НаценкиБазовые.Отменена";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + " И " + ТекстЗапросаНачало + " < 999
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
		
	Запрос.УстановитьПараметр("Дата",          Дата);
	Запрос.УстановитьПараметр("ПрайсЛист",     ПрайсЛист);
	Запрос.УстановитьПараметр("ТипЦен",        ТипЦен);
	Запрос.УстановитьПараметр("ТегПозиции",    ?(ТегПозиции = Неопределено, "", ТегПозиции));
	Запрос.УстановитьПараметр("Производитель", ?(Производитель = Неопределено, Справочники.Производители.ПустаяСсылка(), Производитель));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураДанных = Новый Структура("ЦенаВПрайсЛисте, Скидка, ЦенаПокупки, Наценка, ОкруглятьДо, ЦенаПродажи", Цена, 0, Цена, Неопределено, 1, 0);
	
	Если Выборка.Следующий() Тогда
		
		СтруктураДанных.Скидка = Выборка.Скидка;
		СтруктураДанных.ЦенаПокупки = Макс(0, Окр(СтруктураДанных.ЦенаВПрайсЛисте * (100 - СтруктураДанных.Скидка) / 100, 2));
		
		СтруктураДанных.ОкруглятьДо = ?(ЗначениеЗаполнено(Выборка.ОкруглятьДо), Выборка.ОкруглятьДо, 1);
		
		Если Выборка.Наценка <> Null Тогда
			Если ЗначениеЗаполнено(Выборка.АлгоритмРасчетаЦены) Тогда
				СтруктураДанных.Наценка = Выборка.АлгоритмРасчетаЦены;
				
				ЦенаВПрайсЛисте = СтруктураДанных.ЦенаВПрайсЛисте;
				ЦенаПокупки = СтруктураДанных.ЦенаПокупки;
				Цена = СтруктураДанных.ЦенаПокупки;
				
				Если Найти(Выборка.АлгоритмРасчетаЦены, "ЦенаНоменклатуры") > 0 Тогда
					ЦенаНоменклатуры = обПолучитьЦену(ТипЦен, Номенклатура, Дата,,,,,, ПодразделениеКомпании);
					Если ЦенаНоменклатуры > 0 И ПрайсЛист.ЦенаВключаетНДС <> ТипЦен.ЦенаВключаетНДС Тогда
						Если ТипЦен.ЦенаВключаетНДС Тогда
							// Что бы цены были как в прайс-листе уберем НДС
							ЦенаНоменклатуры = Окр(ЦенаНоменклатуры * 100 / (100 + Номенклатура.СтавкаНДС.Ставка), 2);
						Иначе
							// Что бы цены были как в прайс-листе добавим НДС
							ЦенаНоменклатуры = Окр(ЦенаНоменклатуры * (100 + Номенклатура.СтавкаНДС.Ставка) / 100, 2);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если Найти(Выборка.АлгоритмРасчетаЦены, "#Процедура") > 0 Тогда
					Попытка
						Выполнить(СтрЗаменить(Выборка.АлгоритмРасчетаЦены, "#Процедура", ""));
					Исключение
						Цена = 0;
					КонецПопытки;
				Иначе
					Попытка
						Выполнить("Цена = Макс(0, " + Выборка.АлгоритмРасчетаЦены + ")");
					Исключение
						Цена = 0;
					КонецПопытки;
				КонецЕсли;
				СтруктураДанных.ЦенаПродажи = Цена;
			Иначе
				СтруктураДанных.Наценка = Выборка.Наценка;
				СтруктураДанных.ЦенаПродажи = Макс(0, Окр(СтруктураДанных.ЦенаПокупки * (100 + СтруктураДанных.Наценка) / 100, 2));
			КонецЕсли;
			
			ДельтаОкругления = СтруктураДанных.ЦенаПродажи / СтруктураДанных.ОкруглятьДо;
			Если ДельтаОкругления > Цел(ДельтаОкругления) Тогда // Требуется округлить
				СтруктураДанных.ЦенаПродажи = (Цел(ДельтаОкругления) + 1) * СтруктураДанных.ОкруглятьДо;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		СтруктураЦеныПокупки = РассчитатьЦенуПокупки(ПрайсЛист, Дата, Цена, ТегПозиции, Производитель);
		СтруктураДанных.Скидка = СтруктураЦеныПокупки.Скидка;
		СтруктураДанных.ЦенаПокупки = СтруктураЦеныПокупки.ЦенаПокупки;
		
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции


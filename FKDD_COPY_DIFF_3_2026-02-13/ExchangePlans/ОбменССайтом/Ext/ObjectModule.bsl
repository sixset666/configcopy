////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;			// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ТекущееРегламентноеЗадание Экспорт; // Текущее регламентное задание

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция возвращает объект регламентного задания
Функция ПолучитьОбъектРегламентногоЗадания() Экспорт
		
	ТекущееРегламентноеЗадание = НайтиРеглЗаданиеПоПараметру(РегламентноеЗадание);
	Возврат ТекущееРегламентноеЗадание;
			
КонецФункции

// Найти регламентное задание по параметру
Функция НайтиРеглЗаданиеПоПараметру(УникальныйНомерЗадания)
	
	Попытка		
		Если ( НЕ ПустаяСтрока(УникальныйНомерЗадания) ) Тогда		
			УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор(УникальныйНомерЗадания);
			ТекущееРегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(УникальныйИдентификаторЗадания);	
		Иначе		
			ТекущееРегламентноеЗадание = Неопределено;	
		КонецЕсли;
		
	Исключение	
		ТекущееРегламентноеЗадание = Неопределено;	
    КонецПопытки;
	
	Возврат ТекущееРегламентноеЗадание;
	
КонецФункции

// Процедура устанавливает значение переменных регламентного задания
Процедура УстановитьЗначенияПеременныхРегламентныхНастроек() Экспорт
	
	Если (ТекущееРегламентноеЗадание = Неопределено) Тогда
		ТекущееРегламентноеЗадание = ПолучитьОбъектРегламентногоЗадания();			
	КонецЕсли;	
		
КонецПроцедуры

// Процедура устанавливает параметры регламентного задания
Процедура УстановитьПараметрыРегламентногоЗадания(РеквизитЗадания, ПараметрЗадания, КлючРегламентногоЗадания, Постфикс = "")
	
	Если ( ПараметрЗадания = Неопределено ) Тогда
		РеквизитЗадания = "";
	Иначе		
		РеквизитЗадания					= Строка(ПараметрЗадания.УникальныйИдентификатор);
		ПараметрЗадания.Наименование	= Наименование + Постфикс;
		
		// генерируем уникальный ключ, что бы в один момент времени 2 регламентных задания не выполнялись
		Если ( ПустаяСтрока(ПараметрЗадания.Ключ)) Тогда
			ПараметрЗадания.Ключ = КлючРегламентногоЗадания;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("КодНастройки", Код);
		Массив = Новый Массив();
		Массив.Добавить(СтруктураПараметров);
		
		ПараметрЗадания.Параметры.Очистить();
		ПараметрЗадания.Параметры = Массив;
		ПараметрЗадания.Записать();	
					
	КонецЕсли;	
	
КонецПроцедуры

// Процедура установить режим регламентных задач
Процедура УстановитьРежимРегламентныхЗадач() Экспорт
	
	УстановитьЗначенияПеременныхРегламентныхНастроек();
	
	Если (ТекущееРегламентноеЗадание = Неопределено) Тогда
		Возврат;	
	КонецЕсли;
	
	Если (ТекущееРегламентноеЗадание.Использование = Истина) Тогда	
		Возврат;		
	КонецЕсли;
	
	ТекущееРегламентноеЗадание.Использование = Ложь;	
	ТекущееРегламентноеЗадание.Записать();
		
КонецПроцедуры

// Функция производит поиск регламентного задания по настройке
Функция НайтиРегламентноеЗаданиеПоНастройке() Экспорт
	
	ТекРегламентноеЗадание = НайтиРеглЗаданиеПоПараметру(РегламентноеЗадание);	
	Возврат ТекРегламентноеЗадание;
	
КонецФункции

// Процедура заполняет настройки значениями по умолчанию
Процедура ЗаполнитьНастройкуОбменаWEBЗначениямиПоУмолчанию(НастройкаОбмена) Экспорт
	
	НастройкаОбмена.ВыгружатьТолькоИзменения		= Истина;
	НастройкаОбмена.ВыгружатьНаСайт		 			= Истина;
	
	НастройкаОбмена.Организация = ПараметрыСеанса.Организация;
	Если (обЗначениеНеЗаполнено(НастройкаОбмена.Организация)) Тогда 
		Выборка = Справочники.Организации.Выбрать();
		Если (Выборка.Следующий()) Тогда
			НастройкаОбмена.Организация = Выборка.Ссылка;
		КонецЕсли;	
	КонецЕсли;	
	НастройкаОбмена.Подразделение					= ПараметрыСеанса.ПодразделениеКомпании;
	НастройкаОбмена.Менеджер						= ПараметрыСеанса.Пользователь;
	НастройкаОбмена.СпособИдентификацииКонтрагентов	= "Наименование";
	НастройкаОбмена.ТипЦены							= обПраво("ОсновнойТипЦенПродажи");
	КаталогОбменаHTTP								= КаталогВременныхФайлов();
	
КонецПроцедуры

// Проверка заполнения
Функция ПроверитьКорректность(ЭтаФорма) Экспорт
	Отказать	= Ложь;
	Для Каждого ЭУ Из ЭтаФорма.ЭлементыФормы Цикл
		Если ТипЗнч(ЭУ)=Тип("ПолеВвода") И ЭУ.АвтоотметкаНезаполненного И обЗначениеНеЗаполнено(ЭУ.Значение) Тогда
			Сообщить("Не заполнен обязательный реквизит "+ЭУ.Имя, СтатусСообщения.Важное);
			Отказать	= Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ВидЦенКонтрагентаДляВыборкиТоваров=3 И обЗначениеНеЗаполнено(КонтрагентДляВыборкиТоваров) Тогда
		Сообщить("Не заполнен контрагент для определения цены товара.");
		Отказать	= Истина;
	КонецЕсли;
	
	Возврат Отказать;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Обработчик копирования
Процедура ПриКопировании(ОбъектКопирования)
	ТекущееРегламентноеЗадание	= Неопределено;
	РегламентноеЗадание			= "";
КонецПроцедуры

// обработчик перед записью формы
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если ( ВыгружатьНаСайт ) Тогда	
		Если обЗначениеНеЗаполнено(СерверHTTP) ИЛИ обЗначениеНеЗаполнено(Наименование)
			ИЛИ обЗначениеНеЗаполнено(Код) ИЛИ обЗначениеНеЗаполнено(ПортHTTP) 
			ИЛИ обЗначениеНеЗаполнено(ПользовательHTTP) ИЛИ обЗначениеНеЗаполнено(ПарольHTTP)
			ИЛИ обЗначениеНеЗаполнено(Подразделение) ИЛИ обЗначениеНеЗаполнено(Организация)
			ИЛИ обЗначениеНеЗаполнено(Менеджер) ИЛИ обЗначениеНеЗаполнено(ТипЦены) Тогда
			#Если Клиент Тогда
			Сообщить("Не заполнены обязательные реквизиты объекта!");
			#КонецЕсли
			Отказ	= Истина;
		ИначеЕсли обЗначениеНеЗаполнено(СпособИдентификацииКонтрагентов) Тогда
			#Если Клиент Тогда
			Сообщить("Не заполнены обязательные реквизиты объекта: Способ идентификации контрагентов");
			#КонецЕсли
			Отказ	= Истина;			
		КонецЕсли;	
	Иначе		
		Если (обЗначениеНеЗаполнено(КаталогОбменаHTTP)) Тогда
			Сообщить("Не указан каталог для обмена данными!");
			Отказ	= Истина;
		КонецЕсли;	
	КонецЕсли;
	
	УстановитьЗначенияПеременныхРегламентныхНастроек();
	
	Если ( ПометкаУдаления ИЛИ НЕ ИспользоватьРегламентныеЗадания) Тогда
		
		Если (ТекущееРегламентноеЗадание <> Неопределено ) Тогда
			ТекущееРегламентноеЗадание.Использование = Ложь;			
		КонецЕсли;		
					
	Иначе
		
		// если оба выключены регл задания - то включаем основное регл задание
		Если (ТекущееРегламентноеЗадание = Неопределено) Тогда	
			Сообщить("Не выбрано регламентное задание для настройки обмена.");		
		КонецЕсли;			
				
		Если (ТекущееРегламентноеЗадание <> Неопределено) Тогда	
			ТекущееРегламентноеЗадание.Использование = Истина;				
		КонецЕсли;
			
	КонецЕсли;
	
	Если (Отказ) Тогда Возврат; КонецЕсли;
	
	Если (ТекущееРегламентноеЗадание <> Неопределено 
		И НЕ ПустаяСтрока(ТекущееРегламентноеЗадание.Ключ)) Тогда
		
		КлючРегламентногоЗадания = ТекущееРегламентноеЗадание.Ключ;		
	Иначе	
		КлючРегламентногоЗадания = Строка(Новый УникальныйИдентификатор);		
	КонецЕсли;
	
	УстановитьПараметрыРегламентногоЗадания(РегламентноеЗадание, ТекущееРегламентноеЗадание, КлючРегламентногоЗадания);
	
КонецПроцедуры

// Обработчик перед удалением
Процедура ПередУдалением(Отказ)
	УстановитьЗначенияПеременныхРегламентныхНастроек();
	Если (ТекущееРегламентноеЗадание <> Неопределено) Тогда
		ТекущееРегламентноеЗадание.Удалить();
	КонецЕсли;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
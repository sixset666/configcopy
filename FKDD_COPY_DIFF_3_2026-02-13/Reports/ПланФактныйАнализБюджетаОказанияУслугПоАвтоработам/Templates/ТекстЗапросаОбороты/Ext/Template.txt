
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	СУММА(ОбъединенныйЗапрос.КоличествоПланОборот)       КАК КоличествоПлановое,
	СУММА(ОбъединенныйЗапрос.СуммаРегПланОборот)         КАК СуммаРегПлановое,
	СУММА(ОбъединенныйЗапрос.СуммаУпрПланОборот)         КАК СуммаУпрПлановое,
	СУММА(ОбъединенныйЗапрос.СуммаНДСПланОборот)         КАК СуммаНДСПлановое,
	
	СУММА(ОбъединенныйЗапрос.КоличествоФактОборот)       КАК КоличествоФактическое,
	СУММА(ОбъединенныйЗапрос.СуммаРегФактОборот)         КАК СуммаРегФактическое,
	СУММА(ОбъединенныйЗапрос.СуммаУпрФактОборот)         КАК СуммаУпрФактическое,
	СУММА(ОбъединенныйЗапрос.СуммаНДСФактОборот)         КАК СуммаНДСФактическое,
	
	СУММА(ОбъединенныйЗапрос.КоличествоФактОборот       - ОбъединенныйЗапрос.КоличествоПланОборот)       КАК КоличествоОтклонение,
	СУММА(ОбъединенныйЗапрос.СуммаРегФактОборот         - ОбъединенныйЗапрос.СуммаРегПланОборот)         КАК СуммаРегОтклонение,
	СУММА(ОбъединенныйЗапрос.СуммаУпрФактОборот         - ОбъединенныйЗапрос.СуммаУпрПланОборот)         КАК СуммаУпрОтклонение,
	СУММА(ОбъединенныйЗапрос.СуммаНДСФактОборот         - ОбъединенныйЗапрос.СуммаНДСПланОборот)         КАК СуммаНДСОтклонение,
	
	0 КАК КоличествоПроцентОтклонения,
	0 КАК СуммаРегПроцентОтклонения,
	0 КАК СуммаУпрПроцентОтклонения,
	0 КАК СуммаНДСПроцентОтклонения

	//СВОЙСТВА

{ВЫБРАТЬ 
#Область ИзмеренияПостроителяДляСвойств
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Авторабота.* КАК Авторабота,
#КонецОбласти	
	Период КАК Период
	//СВОЙСТВА}
    
ИЗ 
	(
	 ВЫБРАТЬ
	 	БюджетРаботОбороты.ПодразделениеКомпании КАК ПодразделениеКомпании,
	 	БюджетРаботОбороты.Авторабота КАК Авторабота,
	 	БюджетРаботОбороты.Период КАК Период,
	 	
	 	БюджетРаботОбороты.КоличествоОборот       КАК КоличествоПланОборот, 
	 	БюджетРаботОбороты.СуммаРегОборот         КАК СуммаРегПланОборот,   
	 	БюджетРаботОбороты.СуммаУпрОборот         КАК СуммаУпрПланОборот,   
	 	БюджетРаботОбороты.СуммаНДСОборот         КАК СуммаНДСПланОборот,   
	 	
	 	0 КАК КоличествоФактОборот, 
	 	0 КАК СуммаРегФактОборот,  
	 	0 КАК СуммаУпрФактОборот,  
	 	0 КАК СуммаНДСФактОборот
	
	 ИЗ
	 РегистрНакопления.БюджетОказанияУслугПоАвтоработам.Обороты(&ДатаНач, &ДатаКон, Месяц, СценарийПланирования = &СценарийПланирования{
		ПодразделениеКомпании.Организация.* КАК Организация, 
		ПодразделениеКомпании.* КАК ПодразделениеКомпании, 
		Авторабота.* КАК Авторабота
		}) КАК БюджетРаботОбороты
	 ОБЪЕДИНИТЬ ВСЕ
	 
	 ВЫБРАТЬ
	  	ПродажиОбороты.ПодразделениеКомпании,
	  	ПродажиОбороты.Авторабота,
		ПродажиОбороты.Период,
		
	 	0,
	 	0,
	 	0,
	 	0,
	 	
	 	ПродажиОбороты.КоличествоОборот,                  
	 	ПродажиОбороты.СуммаОборот,                                           
	 	ПродажиОбороты.СуммаУпрОборот,
	 	ПродажиОбороты.СуммаНДСОборот                          
	 	
	 ИЗ
	 РегистрНакопления.Продажи.Обороты(&ДатаНач, &ДатаКон, Месяц, {
		ПодразделениеКомпании.Организация.* КАК Организация, 
		ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	 	Авторабота.* КАК Авторабота
	 	}) КАК ПродажиОбороты
	 	ГДЕ
	 	ПродажиОбороты.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказНаряд) И 
		ПродажиОбороты.Авторабота <> ЗНАЧЕНИЕ(Справочник.Автоработы.ПустаяСсылка) И 
	 	ПродажиОбороты.КоличествоОборот > 0
	 	
	) КАК ОбъединенныйЗапрос
	//СОЕДИНЕНИЯ
	СГРУППИРОВАТЬ ПО
		ПодразделениеКомпании,
		Авторабота,
		Период
	//СГРУППИРОВАТЬПО
 ИМЕЮЩИЕ
	&ВыводитьВсеЗаписи ИЛИ СУММА(ОбъединенныйЗапрос.КоличествоПланОборот)>0
	     	    
{ГДЕ 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Авторабота.* КАК Авторабота
	 //СВОЙСТВА}
    
{УПОРЯДОЧИТЬ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Авторабота.* КАК Авторабота,
	Период КАК Период
	 //СВОЙСТВА}
    
{ИТОГИ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Авторабота.* КАК Авторабота,
	Период КАК Период
    //СВОЙСТВА}
    
ИТОГИ  
	СУММА(КоличествоПлановое), 
	СУММА(СуммаРегПлановое), 
	СУММА(СуммаУпрПлановое), 
	СУММА(СуммаНДСПлановое), 
	
	СУММА(КоличествоФактическое), 
	СУММА(СуммаРегФактическое), 
	СУММА(СуммаУпрФактическое), 
	СУММА(СуммаНДСФактическое), 
	
	СУММА(КоличествоОтклонение),
	СУММА(СуммаРегОтклонение), 
	СУММА(СуммаУпрОтклонение), 
	СУММА(СуммаНДСОтклонение), 
	
	ВЫБОР КОГДА СУММА(КоличествоПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(КоличествоОтклонение) / СУММА(КоличествоПлановое) * 100 КОНЕЦ КАК КоличествоПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаРегПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаРегОтклонение) / СУММА(СуммаРегПлановое) * 100 КОНЕЦ КАК СуммаРегПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаУпрПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаУпрОтклонение) / СУММА(СуммаУпрПлановое) * 100 КОНЕЦ КАК СуммаУпрПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаНДСПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаНДСОтклонение) / СУММА(СуммаНДСПлановое) * 100 КОНЕЦ КАК СуммаНДСПроцентОтклонения
	//ИТОГИСВОЙСТВА
	
ПО ОБЩИЕ
				
АВТОУПОРЯДОЧИВАНИЕ

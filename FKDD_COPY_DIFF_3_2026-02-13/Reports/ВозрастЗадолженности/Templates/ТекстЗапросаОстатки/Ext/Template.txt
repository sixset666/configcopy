ВЫБРАТЬ РАЗРЕШЕННЫЕ
	СУММА(ВозрастЗадолженностиЗапрос.РазницаДней) КАК РазницаДней, //ПОКАЗАТЕЛЬ_
	СУММА(ВозрастЗадолженностиЗапрос.СуммаМы) КАК СуммаМы, //ПОКАЗАТЕЛЬ
	СУММА(ВозрастЗадолженностиЗапрос.СуммаНам) КАК СуммаНам, //ПОКАЗАТЕЛЬ
	СУММА(ВозрастЗадолженностиЗапрос.СуммаУпрМы) КАК СуммаУпрМы, //ПОКАЗАТЕЛЬ
	СУММА(ВозрастЗадолженностиЗапрос.СуммаУпрНам) КАК СуммаУпрНам, //ПОКАЗАТЕЛЬ
	СУММА(ВозрастЗадолженностиЗапрос.СуммаМаксимальногоКредита) КАК СуммаМаксимальногоКредита, //ПОКАЗАТЕЛЬ
	СУММА(ВозрастЗадолженностиЗапрос.ПревышениеМаксимальногоКредитаНам) КАК ПревышениеМаксимальногоКредитаНам, //ПОКАЗАТЕЛЬ_
	СУММА(ВозрастЗадолженностиЗапрос.ПревышениеМаксимальногоКредитаМы) КАК ПревышениеМаксимальногоКредитаМы, //ПОКАЗАТЕЛЬ_
	СУММА(ВозрастЗадолженностиЗапрос.СрокОплатыЗадолженности) КАК СрокОплатыЗадолженности, //ПОКАЗАТЕЛЬ
	СУММА(ВозрастЗадолженностиЗапрос.ПросроченноДней) КАК ПросроченноДней, //ПОКАЗАТЕЛЬ_
	"" КАК ВалютаДоговораПревышениеМаксимальногоКредитаНам,
	"" КАК ВалютаДоговораПревышениеМаксимальногоКредитаМы
    //СВОЙСТВА

{ВЫБРАТЬ
	ВалютаДоговораПревышениеМаксимальногоКредитаНам КАК ВалютаДоговораПревышениеМаксимальногоКредитаНам,
	ВалютаДоговораПревышениеМаксимальногоКредитаМы КАК ВалютаДоговораПревышениеМаксимальногоКредитаМы,
	ВозрастЗадолженностиЗапрос.ИнтервалДнейПросрочки КАК ИнтервалДнейПросрочки,
	ВозрастЗадолженностиЗапрос.ИнтервалСуммыДолга КАК ИнтервалСуммыДолга,
	ВозрастЗадолженностиЗапрос.ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
#Область ИзмеренияПостроителяДляСвойств
	ВозрастЗадолженностиЗапрос.Контрагент.* КАК Контрагент,
	ВозрастЗадолженностиЗапрос.Сделка.* КАК Сделка,
	ВозрастЗадолженностиЗапрос.Сделка.Проект.* КАК Проект,
#КонецОбласти
	ВозрастЗадолженностиЗапрос.ПревышениеМаксимальногоКредитаМы КАК ПревышениеМаксимальногоКредитаМы,
	ВозрастЗадолженностиЗапрос.ПревышениеМаксимальногоКредитаНам КАК ПревышениеМаксимальногоКредитаНам,
	ВозрастЗадолженностиЗапрос.ПросроченноДней КАК ПросроченноДней
	//СВОЙСТВА
}

ИЗ

	(ВЫБРАТЬ
		ВложенныйЗапрос.Контрагент КАК Контрагент,
		ВложенныйЗапрос.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		ВложенныйЗапрос.Сделка КАК Сделка,
		ВложенныйЗапрос.РазницаДней КАК РазницаДней,
		"" КАК ИнтервалДнейПросрочки,
		"" КАК ИнтервалСуммыДолга,
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.СуммаОстаток<0 ТОГДА -ВложенныйЗапрос.СуммаОстаток ИНАЧЕ 0 КОНЕЦ) КАК СуммаМы,
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.СуммаОстаток>0 ТОГДА ВложенныйЗапрос.СуммаОстаток ИНАЧЕ 0 КОНЕЦ) КАК СуммаНам, 
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.СуммаУпрОстаток<0 ТОГДА -ВложенныйЗапрос.СуммаУпрОстаток ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпрМы, 
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.СуммаУпрОстаток>0 ТОГДА ВложенныйЗапрос.СуммаУпрОстаток ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпрНам,
		СУММА(ВложенныйЗапрос.СуммаМаксимальногоКредита) КАК СуммаМаксимальногоКредита, 
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.СуммаОстаток>0 ТОГДА ВЫБОР КОГДА ВложенныйЗапрос.СуммаОстаток-ВложенныйЗапрос.СуммаМаксимальногоКредита<=0 ТОГДА 0 ИНАЧЕ ВложенныйЗапрос.СуммаОстаток-ВложенныйЗапрос.СуммаМаксимальногоКредита КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК ПревышениеМаксимальногоКредитаНам, 
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.СуммаОстаток<0 ТОГДА ВЫБОР КОГДА (-ВложенныйЗапрос.СуммаОстаток)-ВложенныйЗапрос.СуммаМаксимальногоКредита<=0 ТОГДА 0 ИНАЧЕ (-ВложенныйЗапрос.СуммаОстаток)-ВложенныйЗапрос.СуммаМаксимальногоКредита КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК ПревышениеМаксимальногоКредитаМы, 
		СУММА(ВложенныйЗапрос.СрокОплатыЗадолженности) КАК СрокОплатыЗадолженности, 
		СУММА(ВЫБОР КОГДА ВложенныйЗапрос.РазницаДней-ВложенныйЗапрос.СрокОплатыЗадолженности<=0 ТОГДА 0 ИНАЧЕ ВложенныйЗапрос.РазницаДней-ВложенныйЗапрос.СрокОплатыЗадолженности КОНЕЦ) КАК ПросроченноДней 
	ИЗ
		(ВЫБРАТЬ
			ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
			ВзаиморасчетыОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
			ВзаиморасчетыОстатки.Сделка КАК Сделка,
			ВзаиморасчетыОстатки.ДоговорВзаиморасчетов.СрокОплатыЗадолженности КАК СрокОплатыЗадолженности,
			ВзаиморасчетыОстатки.ДоговорВзаиморасчетов.МаксимальныйКредит КАК СуммаМаксимальногоКредита,
			РАЗНОСТЬДАТ(ВзаиморасчетыОстатки.Сделка.Дата, &ДатаКон, ДЕНЬ) КАК РазницаДней,
			ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток,
			ВзаиморасчетыОстатки.СуммаУпрОстаток КАК СуммаУпрОстаток
		ИЗ
			РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&ДатаКон, Сделка<>Неопределено) КАК ВзаиморасчетыОстатки
			) КАК ВложенныйЗапрос

		//РазницаДнейГДЕ

	СГРУППИРОВАТЬ ПО
		ВложенныйЗапрос.Контрагент,
		ВложенныйЗапрос.ДоговорВзаиморасчетов,
		ВложенныйЗапрос.Сделка,
		ВложенныйЗапрос.РазницаДней
		//ИнтервалДнейПросрочкиГруппировка
			
	ИМЕЮЩИЕ
	   (СУММА(ВложенныйЗапрос.СуммаОстаток)>0 ИЛИ &ВыводитьНашДолг)
	И (СУММА(ВложенныйЗапрос.СуммаОстаток)<0 ИЛИ &ВыводитьИхДолг)

	) КАК ВозрастЗадолженностиЗапрос
	//СОЕДИНЕНИЯ

СГРУППИРОВАТЬ ПО
	ВозрастЗадолженностиЗапрос.Контрагент,
	ВозрастЗадолженностиЗапрос.ДоговорВзаиморасчетов,
	ВозрастЗадолженностиЗапрос.Сделка,
	ВозрастЗадолженностиЗапрос.ИнтервалДнейПросрочки,
	ВозрастЗадолженностиЗапрос.ИнтервалСуммыДолга
	//СГРУППИРОВАТЬПО

{ГДЕ
	ВозрастЗадолженностиЗапрос.Контрагент.* КАК Контрагент,
	ВозрастЗадолженностиЗапрос.ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	ВозрастЗадолженностиЗапрос.Сделка.* КАК Сделка,
	ВозрастЗадолженностиЗапрос.Сделка.Проект.* КАК Проект
	//СВОЙСТВА
}
		
{УПОРЯДОЧИТЬ ПО
	ВозрастЗадолженностиЗапрос.ИнтервалДнейПросрочки,
	ВозрастЗадолженностиЗапрос.ИнтервалСуммыДолга,
	ВозрастЗадолженностиЗапрос.Контрагент.* КАК Контрагент,
	ВозрастЗадолженностиЗапрос.ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	ВозрастЗадолженностиЗапрос.Сделка.* КАК Сделка,
	ВозрастЗадолженностиЗапрос.Сделка.Проект.* КАК Проект
	//СВОЙСТВА
	}
			
ИТОГИ
	МАКСИМУМ(РазницаДней), //ПОКАЗАТЕЛЬ_
	СУММА(СуммаМы), //ПОКАЗАТЕЛЬ
	СУММА(СуммаНам), //ПОКАЗАТЕЛЬ
	СУММА(СуммаУпрМы), //ПОКАЗАТЕЛЬ
	СУММА(СуммаУпрНам), //ПОКАЗАТЕЛЬ
	СУММА(СуммаМаксимальногоКредита), //ПОКАЗАТЕЛЬ
	СУММА(ПревышениеМаксимальногоКредитаНам), //ПОКАЗАТЕЛЬ_
	СУММА(ПревышениеМаксимальногоКредитаМы), //ПОКАЗАТЕЛЬ_
	МАКСИМУМ(СрокОплатыЗадолженности), //ПОКАЗАТЕЛЬ
	МАКСИМУМ(ПросроченноДней) //ПОКАЗАТЕЛЬ_
	//ИТОГИСВОЙСТВА
ПО
	ОБЩИЕ
			
{ИТОГИ ПО
	ВозрастЗадолженностиЗапрос.ИнтервалДнейПросрочки,
	ВозрастЗадолженностиЗапрос.ИнтервалСуммыДолга,
	ВозрастЗадолженностиЗапрос.Контрагент.* КАК Контрагент,
	ВозрастЗадолженностиЗапрос.ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	ВозрастЗадолженностиЗапрос.Сделка.* КАК Сделка,
	ВозрастЗадолженностиЗапрос.Сделка.Проект.* КАК Проект
	//СВОЙСТВА
	}
	
АВТОУПОРЯДОЧИВАНИЕ
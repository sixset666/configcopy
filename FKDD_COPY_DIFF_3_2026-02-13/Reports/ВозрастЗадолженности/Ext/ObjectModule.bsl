#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем ПараметрыРесурсов Экспорт;                       // хранит параметры ресурсов
Перем ПоследовательностиОтчета Экспорт;                // хранит последовательности отчета
Перем ЕстьДоговор Экспорт;                             // флаг наличия договора

// устанавливает параметры построителя
Процедура УстановитьПараметры(ТекПостроительОтчета) Экспорт
	
	Если ВРеглВалюте Тогда
		ВалютаОтчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	Иначе
		ВалютаОтчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
	ПостроительОтчета.УсловноеОформление.Очистить();

	Если ЕстьДоговор Тогда
		Если Показатели.Найти("ПревышениеМаксимальногоКредитаМы").Использование Тогда
			ЭлементОформления = ПостроительОтчета.УсловноеОформление.Добавить("ПревышениеМаксимальногоКредитаМы");
			
			ОбластьОформления = ЭлементОформления.Область.Добавить("ПревышениеМаксимальногоКредитаМы");
			ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
			ОбластьОформления = ЭлементОформления.Область.Добавить("ВалютаДоговораПревышениеМаксимальногоКредитаМы");
			ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
			
			ОтборОформления = ЭлементОформления.Отбор.Добавить("ПревышениеМаксимальногоКредитаМы");
			ОтборОформления.ВидСравнения = ВидСравнения.Больше;
			ОтборОформления.Значение = 0;
			ОтборОформления.Использование = Истина;
			
			ЭлементОформления.Оформление.ЦветТекста.Значение = WebЦвета.Красный;
			ЭлементОформления.Оформление.ЦветТекста.Использование = Истина;	
			ЭлементОформления.Использование = Истина;  
		КонецЕсли;
		
		Если Показатели.Найти("ПревышениеМаксимальногоКредитаНам").Использование Тогда
			ЭлементОформления = ПостроительОтчета.УсловноеОформление.Добавить("ПревышениеМаксимальногоКредитаНам");
			
			ОбластьОформления = ЭлементОформления.Область.Добавить("ПревышениеМаксимальногоКредитаНам");
			ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
			ОбластьОформления = ЭлементОформления.Область.Добавить("ВалютаДоговораПревышениеМаксимальногоКредитаНам");
			ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
			
			ОтборОформления = ЭлементОформления.Отбор.Добавить("ПревышениеМаксимальногоКредитаНам");
			ОтборОформления.ВидСравнения = ВидСравнения.Больше;
			ОтборОформления.Значение = 0;
			ОтборОформления.Использование = Истина;
			
			ЭлементОформления.Оформление.ЦветТекста.Значение = WebЦвета.Красный;
			ЭлементОформления.Оформление.ЦветТекста.Использование = Истина;	
			ЭлементОформления.Использование = Истина;  
		КонецЕсли;
		
		Если Показатели.Найти("ПросроченоДней").Использование Тогда
			ЭлементОформления = ПостроительОтчета.УсловноеОформление.Добавить("ПросроченоДней");
			
			ОбластьОформления = ЭлементОформления.Область.Добавить("ПросроченоДней");
			ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
			
			ОтборОформления = ЭлементОформления.Отбор.Добавить("ПросроченоДней");
			ОтборОформления.ВидСравнения = ВидСравнения.Больше;
			ОтборОформления.Значение = 0;
			ОтборОформления.Использование = Истина;
			
			ЭлементОформления.Оформление.ЦветТекста.Значение = WebЦвета.Красный;
			ЭлементОформления.Оформление.ЦветТекста.Использование = Истина;	
			ЭлементОформления.Использование = Истина;  
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// устанавливает реквизит использование табличного поля показатели
Процедура УстановитьИспользованиеПоказателей(ДопПредставление="") Экспорт 
	
	Показатели.Очистить();
	Для Каждого ТекПоказатель Из ОбщиеПоказатели Цикл
		НовыйПоказатель = Показатели.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйПоказатель, ТекПоказатель);
		НовыйПоказатель.Представление = НовыйПоказатель.Представление + ДопПредставление;
	КонецЦикла;

	Если ЕстьДоговор Тогда
		Для Каждого ТекПоказатель Из ПоказателиДоговора Цикл
			ЗаполнитьЗначенияСвойств(Показатели.Добавить(), ТекПоказатель);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// проверка заполнения
Функция КорректностьЗаполнения(ПродолжитьФормирование) Экспорт 
	
	ДопПредставление = " ("+ВалютаОтчета.Наименование+")";
	УстановитьИспользованиеПоказателей(ДопПредставление);
	Возврат Ложь;
	
КонецФункции

// вставляет дополните показатели
Процедура ЗаполнитьДополнительныеПоказатели() Экспорт 
	
	ОбщиеПоказатели.Очистить();
	ПоказателиДоговора.Очистить();
	
	СтруктураПоказателейДоговора = Новый Структура("РазницаДней, СрокОплатыЗадолженности, ПросроченоДней, СуммаПоДоговоруДЗ, СуммаПоДоговоруКЗ, СуммаМаксимальногоКредита, ПревышениеМаксимальногоКредитаМы, ПревышениеМаксимальногоКредитаНам, ВалютаДоговора");
	Для Каждого ТекПоказатель Из Показатели Цикл
		Если СтруктураПоказателейДоговора.Свойство(ТекПоказатель.Имя) Тогда
			ЗаполнитьЗначенияСвойств(ПоказателиДоговора.Добавить(), ТекПоказатель);
		Иначе
			ЗаполнитьЗначенияСвойств(ОбщиеПоказатели.Добавить(), ТекПоказатель);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// возвращает текст запроса
Функция ПолучитьТекстЗапроса() Экспорт
	
	//Определим перечень свойств
	СтрЗапросаСвойств		= "";
	СтрЗапросаИтогиСвойств	= "";
	СтрСоединенийСвойств	= "";
	Для Каждого ТекСтруктура Из СтруктураСвязиСвойств Цикл
		ИмяСвойства = СтрЗаменить(ТекСтруктура.Ключ, "Значение", "");
		Свойство = ТекСтруктура.Значение.Свойство;
		ПутьКДаннымИзмерения = "ТаблицаОстатков."+ТекСтруктура.Значение.ИмяИзмерения;
		ИмяПараметра = ТекСтруктура.Значение.ИмяПараметра;
		ЗапросСвойстваПоля = ", ЕСТЬNULL(" + ИмяСвойства + ".Значение, """ + Свойство.Наименование + ": <Пусто>"") КАК " + ИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС;
		ЗапросСвойстваИтоги = ",МАКСИМУМ(" + ИмяСвойства + "Значение)	//СВОЙСТВО" + Символы.ПС;
		ЗапросСвойстваСоединение = "{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ПутьКДаннымИзмерения + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + "}	//СВОЙСТВО" + Символы.ПС;
		
		СтрЗапросаСвойств      = СтрЗапросаСвойств      + ЗапросСвойстваПоля;
		СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств + ЗапросСвойстваИтоги;
		СтрСоединенийСвойств   = СтрСоединенийСвойств   + ЗапросСвойстваСоединение;
	КонецЦикла;
	ТекстОтбораСвойств = "";
	Если Не ПустаяСтрока(СтрЗапросаСвойств) Тогда
		ТекстОтбораСвойств = "{ГДЕ
		|	ТаблицаОстатков.Контрагент КАК Контрагент
		|	"+СтрЗапросаСвойств+Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА}";
	КонецЕсли;
	СтрЗапросаСвойств      = СтрЗапросаСвойств+Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА";
	СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств+Символы.ПС+"//ВЫБРАННЫЕИТОГИСВОЙСТВА";
	СтрСоединенийСвойств   = СтрСоединенийСвойств+Символы.ПС+"//ВЫБРАННЫЕСОЕДИНЕНИЯ";
	
	
	ТекстЗапросаРезультат = "";
	ТекстСоединения = "";
	НаходитьРазницуДней = (ЕстьДоговор=Истина) И (Показатели.Найти("РазницаДней", "Имя").Использование);
	ЕстьДЗ = Показатели.Найти("СуммаДЗ", "Имя").Использование;
	ЕстьКЗ = Показатели.Найти("СуммаКЗ", "Имя").Использование;
	ЕстьПроцентДЗ = Ложь;
	ЕстьПроцентКЗ = Ложь;
	Если Показатели.Найти("ПроцентДЗ", "Имя").Использование Тогда
		ЕстьПроцентДЗ = Истина;
		СуммаДЗ       = Истина;
	КонецЕсли;
	Если Показатели.Найти("ПроцентКЗ", "Имя").Использование Тогда
		ЕстьПроцентКЗ = Истина;
		СуммаКЗ       = Истина;
	КонецЕсли;
	ТекстРасчетаСумм = ",
	|		ТаблицаОстатков.СуммаКЗ КАК СуммаКЗ,
	|		ТаблицаОстатков.СуммаДЗ КАК СуммаДЗ";
	ИзменилиРасчетСумм = Ложь;
	ТекстВыборкиИнтервалов = "";
	
	//Определим таблицы интервалов сумм задолженностей
	Если ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалСуммыДолга")).Количество()>0 ИЛИ ИзмеренияКолонки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалСуммыДолга")).Количество()>0 Тогда
		НаименованиеВалюты = ВалютаОтчета.Наименование;
		Если СуммовыеГруппы.Количество()=0 Тогда
			НовыйПериод = СуммовыеГруппы.Добавить();
			НовыйПериод.СуммаДолга		= 9999999999999;
			НовыйПериод.Представление	= "Все";
		КонецЕсли;
		СуммовыеГруппы.Сортировать("СуммаДолга ВОЗР");
		ПревСумма = 0;
		ТекстИнтервалов="";
		Порядок = 0;
		Для Каждого ТекИнтервал Из СуммовыеГруппы Цикл
			ТекстИнтервалов = ТекстИнтервалов + ?(ТекстИнтервалов="","", "ОБЪЕДИНИТЬ ВСЕ ")+"
			|
			|ВЫБРАТЬ
			|	"+Формат(ПревСумма, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МинПредел,
			|	"+Формат(ТекИнтервал.СуммаДолга, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МаксПредел,
			|	"""+ТекИнтервал.Представление+" ("+НаименованиеВалюты+" )"" КАК Представление,
			|	"+Формат(Порядок, "ЧН=0; ЧГ=")+" КАК Порядок
			|";
			ПревСумма = ТекИнтервал.СуммаДолга;
			Порядок = Порядок+1;
		КонецЦикла;
		
		ТекстЗапросаРезультат = ТекстЗапросаРезультат+"
		|ВЫБРАТЬ
		|	ОбъединеннаяТаблица.МинПредел КАК МинПредел,
		|	ОбъединеннаяТаблица.МаксПредел КАК МаксПредел,
		|	ОбъединеннаяТаблица.Представление КАК Представление,
		|	ОбъединеннаяТаблица.Порядок КАК Порядок
		|ПОМЕСТИТЬ
		|	ИнтервалыСумм
		|ИЗ("+ТекстИнтервалов+") КАК ОбъединеннаяТаблица
		|;
		|";
		
		ТекстУсловия = "";
		Если ЕстьДЗ И ЕстьКЗ Тогда
			ТекстУсловия = "
			|	(ТаблицаОстатков.СуммаДЗ > ИнтервалыСумм.МинПредел И
			|	ТаблицаОстатков.СуммаДЗ <= ИнтервалыСумм.МаксПредел) ИЛИ 
			|	(ТаблицаОстатков.СуммаКЗ > ИнтервалыСумм.МинПредел И
			|	ТаблицаОстатков.СуммаКЗ <= ИнтервалыСумм.МаксПредел)";
			ТекстРасчетаСумм = ",
			|	ВЫБОР 
			|		КОГДА ТаблицаОстатков.СуммаКЗ > ИнтервалыСумм.МинПредел И
			|			ТаблицаОстатков.СуммаКЗ <= ИнтервалыСумм.МаксПредел ТОГДА
			|				ТаблицаОстатков.СуммаКЗ
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК СуммаКЗ,
			|	ВЫБОР 
			|		КОГДА ТаблицаОстатков.СуммаДЗ > ИнтервалыСумм.МинПредел И
			|			ТаблицаОстатков.СуммаДЗ <= ИнтервалыСумм.МаксПредел ТОГДА
			|				ТаблицаОстатков.СуммаДЗ
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК СуммаДЗ";
			ИзменилиРасчетСумм = Истина;
		ИначеЕсли ЕстьДЗ Тогда
			ТекстУсловия = "
			|	ТаблицаОстатков.СуммаДЗ > ИнтервалыСумм.МинПредел И
			|	ТаблицаОстатков.СуммаДЗ <= ИнтервалыСумм.МаксПредел";
		ИначеЕсли ЕстьКЗ Тогда
			ТекстУсловия = "
			|	ТаблицаОстатков.СуммаКЗ > ИнтервалыСумм.МинПредел И
			|	ТаблицаОстатков.СуммаКЗ <= ИнтервалыСумм.МаксПредел";
		Иначе
			ТекстУсловия = "
			|	(ТаблицаОстатков.СуммаДЗ > ИнтервалыСумм.МинПредел И
			|	ТаблицаОстатков.СуммаДЗ <= ИнтервалыСумм.МаксПредел) ИЛИ 
			|	(ТаблицаОстатков.СуммаКЗ > ИнтервалыСумм.МинПредел И
			|	ТаблицаОстатков.СуммаКЗ <= ИнтервалыСумм.МаксПредел)";
		КонецЕсли;
		
		ТекстВыборкиИнтервалов = ",
		|	ИнтервалыСумм.Представление КАК ИнтервалСуммыДолга,
		|	ИнтервалыСумм.Порядок КАК ПорядокИнтервалаСуммыДолга";
		
		ТекстСоединения = "
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнтервалыСумм КАК ИнтервалыСумм
		|ПО "+ТекстУсловия;
	Иначе
		ТекстВыборкиИнтервалов = ",
		|	0 КАК ИнтервалСуммыДолга,
		|	0 КАК ПорядокИнтервалаСуммыДолга";
	КонецЕсли;
	
	//Определим таблицы интервалов просроченных дней
	Если ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалДнейПросрочки")).Количество()>0 ИЛИ ИзмеренияКолонки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалДнейПросрочки")).Количество()>0 Тогда
		НаходитьРазницуДней = Истина;
		ДобавлятьИнтервалыДней = Истина;
		Если ВозрастныеГруппы.Количество()=0 Тогда
			НовыйПериод = ВозрастныеГруппы.Добавить();
			НовыйПериод.ЧислоДней		= 50000;
			НовыйПериод.Представление	= "Все";
		КонецЕсли;
		ВозрастныеГруппы.Сортировать("ЧислоДней ВОЗР");
		ПревЧислоДней = 0;
		ТекстИнтервалов = "";
		
		Если ВыводитьНепросроченныйДолг Тогда
			ТекстИнтервалов = ТекстИнтервалов + ?(ТекстИнтервалов="","", "ОБЪЕДИНИТЬ ВСЕ ")+"
			|
			|ВЫБРАТЬ
			|	-1 КАК МинПредел,
			|	0 КАК МаксПредел,
			|	""Не просроченный долг"" КАК Представление,
			|	-1 КАК Порядок
			|";
		КонецЕсли;
		
		Порядок = 0;
		Для Каждого ТекИнтервал Из ВозрастныеГруппы Цикл
			ТекстИнтервалов = ТекстИнтервалов + ?(ТекстИнтервалов="","", "ОБЪЕДИНИТЬ ВСЕ ")+"
			|
			|ВЫБРАТЬ
			|	"+Формат(ПревЧислоДней, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МинПредел,
			|	"+Формат(ТекИнтервал.ЧислоДней, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МаксПредел,
			|	"""+ТекИнтервал.Представление+""" КАК Представление,
			|	"+Формат(Порядок, "ЧН=0; ЧГ=")+" КАК Порядок
			|";
			ПревЧислоДней = ТекИнтервал.ЧислоДней;
			Порядок = Порядок+1;
		КонецЦикла;
		ТекстЗапросаРезультат = ТекстЗапросаРезультат+"
		|ВЫБРАТЬ
		|	ОбъединеннаяТаблица.МинПредел КАК МинПредел,
		|	ОбъединеннаяТаблица.МаксПредел КАК МаксПредел,
		|	ОбъединеннаяТаблица.Представление КАК Представление,
		|	ОбъединеннаяТаблица.Порядок КАК Порядок
		|ПОМЕСТИТЬ
		|	ИнтервалыДней
		|ИЗ("+ТекстИнтервалов+") КАК ОбъединеннаяТаблица
		|;
		|";
		
		ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов+",
		|	ИнтервалыДней.Представление КАК ИнтервалДнейПросрочки,
		|	ИнтервалыДней.Порядок КАК ПорядокИнтервалаДнейПросрочки";

		ТекстСоединения = ТекстСоединения + "
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнтервалыДней КАК ИнтервалыДней
		|ПО 
		|	ТаблицаОстатков.ПросроченоДней > ИнтервалыДней.МинПредел И
		|	ТаблицаОстатков.ПросроченоДней <= ИнтервалыДней.МаксПредел";
	Иначе
		ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов+",
		|	0 КАК ИнтервалДнейПросрочки,
		|	0 КАК ПорядокИнтервалаДнейПросрочки";
	КонецЕсли;
	
	ТекстВалюты = ?(ВРеглВалюте,"Баз","Упр");
	ТекстОтбора = "";
	Если РежимВывода=0 Тогда
		ТекстОтбора = "ГДЕ
		|	ВзаиморасчетыОстатки.Сумма"+ТекстВалюты+"Остаток>0";
	ИначеЕсли РежимВывода=1 Тогда
		ТекстОтбора = "ГДЕ
		|	ВзаиморасчетыОстатки.Сумма"+ТекстВалюты+"Остаток<0";
	КонецЕсли;
	
	ТекстОбщихЗадолженностей = "";
	ТекстИтоговПоЗадолженностям = "";
	Если ЕстьПроцентДЗ ИЛИ ЕстьПроцентКЗ Тогда
		Если ЕстьПроцентДЗ И ЕстьПроцентКЗ Тогда
			ТекстВыборки = "
			|	ВЫБОР КОГДА СУММА(ТаблицаОстатков.СуммаКЗ)=0 ТОГДА 1 ИНАЧЕ СУММА(ТаблицаОстатков.СуммаКЗ) КОНЕЦ КАК СуммаКЗ,
			|	ВЫБОР КОГДА СУММА(ТаблицаОстатков.СуммаДЗ)=0 ТОГДА 1 ИНАЧЕ СУММА(ТаблицаОстатков.СуммаДЗ) КОНЕЦ КАК СуммаДЗ
			|";
			Если ИзменилиРасчетСумм Тогда
				ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов + ",
				|	ВЫБОР 
				|		КОГДА ТаблицаОбщихИтогов.СуммаКЗ ЕСТЬ NULL ИЛИ ТаблицаОбщихИтогов.СуммаКЗ=0 ТОГДА
				|			100
				|		КОГДА ТаблицаОстатков.СуммаКЗ > ИнтервалыСумм.МинПредел И
				|			ТаблицаОстатков.СуммаКЗ <= ИнтервалыСумм.МаксПредел ТОГДА
				|				ТаблицаОстатков.СуммаКЗ/ТаблицаОбщихИтогов.СуммаКЗ*100
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК ПроцентКЗ,
				|	ВЫБОР 
				|		КОГДА ТаблицаОбщихИтогов.СуммаДЗ ЕСТЬ NULL ИЛИ ТаблицаОбщихИтогов.СуммаДЗ=0 ТОГДА
				|			100
				|		КОГДА ТаблицаОстатков.СуммаДЗ > ИнтервалыСумм.МинПредел И
				|			ТаблицаОстатков.СуммаДЗ <= ИнтервалыСумм.МаксПредел ТОГДА
				|				ТаблицаОстатков.СуммаДЗ/ТаблицаОбщихИтогов.СуммаДЗ*100
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК ПроцентДЗ";
			Иначе
				ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов + ",
				|	ВЫБОР КОГДА ТаблицаОбщихИтогов.СуммаКЗ ЕСТЬ NULL ИЛИ ТаблицаОбщихИтогов.СуммаКЗ=0 ТОГДА 100 ИНАЧЕ ТаблицаОстатков.СуммаКЗ/ТаблицаОбщихИтогов.СуммаКЗ*100 КОНЕЦ КАК ПроцентКЗ,
				|	ВЫБОР КОГДА ТаблицаОбщихИтогов.СуммаДЗ ЕСТЬ NULL ИЛИ ТаблицаОбщихИтогов.СуммаДЗ=0 ТОГДА 100 ИНАЧЕ ТаблицаОстатков.СуммаДЗ/ТаблицаОбщихИтогов.СуммаДЗ*100 КОНЕЦ КАК ПроцентДЗ
				|";
			КонецЕсли;
			ТекстИтоговПоЗадолженностям = ТекстИтоговПоЗадолженностям + ",
			|	СУММА(ПроцентКЗ),
			|	СУММА(ПроцентДЗ)";
		ИначеЕсли ЕстьПроцентДЗ Тогда
			ТекстВыборки = "
			|	ВЫБОР КОГДА СУММА(ТаблицаОстатков.СуммаДЗ)=0 ТОГДА 1 ИНАЧЕ СУММА(ТаблицаОстатков.СуммаДЗ) КОНЕЦ КАК СуммаДЗ
			|";
			ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов + ",
			|	ВЫБОР КОГДА ТаблицаОбщихИтогов.СуммаДЗ ЕСТЬ NULL ИЛИ ТаблицаОбщихИтогов.СуммаДЗ=0 ТОГДА 100 ИНАЧЕ ТаблицаОстатков.СуммаДЗ/ТаблицаОбщихИтогов.СуммаДЗ*100 КОНЕЦ КАК ПроцентДЗ
			|";
			ТекстИтоговПоЗадолженностям = ТекстИтоговПоЗадолженностям + ",
			|	СУММА(ПроцентДЗ)";
		Иначе
			ТекстВыборки = "
			|	ВЫБОР КОГДА СУММА(ТаблицаОстатков.СуммаКЗ)=0 ТОГДА 1 ИНАЧЕ СУММА(ТаблицаОстатков.СуммаКЗ) КОНЕЦ КАК СуммаКЗ
			|";
			ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов + ",
			|	ВЫБОР КОГДА ТаблицаОбщихИтогов.СуммаКЗ ЕСТЬ NULL ИЛИ ТаблицаОбщихИтогов.СуммаКЗ=0 ТОГДА 100 ИНАЧЕ ТаблицаОстатков.СуммаКЗ/ТаблицаОбщихИтогов.СуммаКЗ*100 КОНЕЦ КАК ПроцентКЗ
			|";
			ТекстИтоговПоЗадолженностям = ТекстИтоговПоЗадолженностям + ",
			|	СУММА(ПроцентКЗ)";
		КонецЕсли;
		
		ТекстОбщихЗадолженностей = "
		|ВЫБРАТЬ
		|	"+ТекстВыборки+"
		|ПОМЕСТИТЬ
		|	ТаблицаОбщихИтогов
		|ИЗ
		|	ТаблицаОстатков КАК ТаблицаОстатков
		|;
		|";
		
		ТекстСоединения = ТекстСоединения + "
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОбщихИтогов КАК ТаблицаОбщихИтогов
		| ПО ИСТИНА";
	КонецЕсли;
	
	ТекстЗапросаРезультат = ТекстЗапросаРезультат + "
	|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъединеннаяТаблица.Контрагент КАК Контрагент,
	|	ОбъединеннаяТаблица.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ОбъединеннаяТаблица.ДоговорВзаиморасчетов.СрокОплатыЗадолженности КАК СрокОплатыЗадолженности,
	|	ОбъединеннаяТаблица.ДоговорВзаиморасчетов.МаксимальныйКредит КАК СуммаМаксимальногоКредита,
	|	ОбъединеннаяТаблица.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ОбъединеннаяТаблица.Сделка КАК Сделка,
	|	ОбъединеннаяТаблица.СуммаПоДоговоруКЗ КАК СуммаПоДоговоруКЗ,
	|	ОбъединеннаяТаблица.СуммаПоДоговоруДЗ КАК СуммаПоДоговоруДЗ,
	|	ОбъединеннаяТаблица.СуммаКЗ КАК СуммаКЗ,
	|	ОбъединеннаяТаблица.СуммаДЗ КАК СуммаДЗ"+?(НаходитьРазницуДней,",
	|	РАЗНОСТЬДАТ(ОбъединеннаяТаблица.МинПериод, &ДатаКон, ДЕНЬ) КАК РазницаДней,
	|	РАЗНОСТЬДАТ(ОбъединеннаяТаблица.МинПериод, &ДатаКон, ДЕНЬ)-ОбъединеннаяТаблица.ДоговорВзаиморасчетов.СрокОплатыЗадолженности КАК ПросроченоДней", ",
	|	0 КАК РазницаДней,
	|	0 КАК ПросроченоДней")+"
	|ПОМЕСТИТЬ
	|	ТаблицаОстатков
	|ИЗ(ВЫБРАТЬ
	|	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ВзаиморасчетыОстатки.Сделка КАК Сделка,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ВзаиморасчетыОстатки.СуммаБазОстаток < 0
	|			ТОГДА -ВзаиморасчетыОстатки.СуммаБазОстаток / (ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) * ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1))
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаПоДоговоруКЗ,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ВзаиморасчетыОстатки.СуммаБазОстаток > 0
	|			ТОГДА ВзаиморасчетыОстатки.СуммаБазОстаток / (ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) * ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1))
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаПоДоговоруДЗ,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ВзаиморасчетыОстатки.Сумма"+ТекстВалюты+"Остаток < 0 ТОГДА -ВзаиморасчетыОстатки.Сумма"+ТекстВалюты+"Остаток
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаКЗ,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ВзаиморасчетыОстатки.Сумма"+ТекстВалюты+"Остаток > 0 ТОГДА ВзаиморасчетыОстатки.Сумма"+ТекстВалюты+"Остаток
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаДЗ"+?(НаходитьРазницуДней,",
	|	МИНИМУМ(ВзаиморасчетыКомпании.Период) КАК МинПериод", "")+"
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&ДатаКон, {
	|			Контрагент.* КАК Контрагент,
	|			ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	|			Сделка.* КАК Сделка
	|			}) КАК ВзаиморасчетыОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон, ) КАК КурсыВалютСрезПоследних
	|ПО ВзаиморасчетыОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта"+?(НаходитьРазницуДней,"
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
	|ПО
	|	ВзаиморасчетыОстатки.Контрагент				= ВзаиморасчетыКомпании.Контрагент И 
	|	ВзаиморасчетыОстатки.ДоговорВзаиморасчетов	= ВзаиморасчетыКомпании.ДоговорВзаиморасчетов И 
	|	ВзаиморасчетыОстатки.Сделка					= ВзаиморасчетыКомпании.Сделка","")+"
	|"+ТекстОтбора+"
	|СГРУППИРОВАТЬ ПО
	|	ВзаиморасчетыОстатки.Контрагент,
	|	ВзаиморасчетыОстатки.ДоговорВзаиморасчетов,
	|	ВзаиморасчетыОстатки.Сделка) КАК ОбъединеннаяТаблица
	|;
	|
	|"+ТекстОбщихЗадолженностей+"
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОстатков.Контрагент КАК Контрагент,
	|	ТаблицаОстатков.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ТаблицаОстатков.ВалютаВзаиморасчетов КАК ВалютаДоговораСуммаПоДоговоруДЗ,
	|	ТаблицаОстатков.ВалютаВзаиморасчетов КАК ВалютаДоговораСуммаПоДоговоруКЗ,
	|	ТаблицаОстатков.ВалютаВзаиморасчетов КАК ВалютаДоговораСуммаМаксимальногоКредита,
	|	ТаблицаОстатков.ВалютаВзаиморасчетов КАК ВалютаДоговораПревышениеМаксимальногоКредитаМы,
	|	ТаблицаОстатков.ВалютаВзаиморасчетов КАК ВалютаДоговораПревышениеМаксимальногоКредитаНам,
	|	ТаблицаОстатков.СрокОплатыЗадолженности КАК СрокОплатыЗадолженности,
	|	ТаблицаОстатков.Сделка КАК Сделка,
	|	ТаблицаОстатков.РазницаДней КАК РазницаДней"+ТекстРасчетаСумм+",
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаОстатков.ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита
	|			ТОГДА ТаблицаОстатков.СуммаМаксимальногоКредита
	|			ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаМаксимальногоКредита,
	|	ВЫБОР
	|		КОГДА ТаблицаОстатков.СуммаМаксимальногоКредита > 0 И НЕ ТаблицаОстатков.ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита И ТаблицаОстатков.СуммаПоДоговоруДЗ > ТаблицаОстатков.СуммаМаксимальногоКредита
	|			ТОГДА ТаблицаОстатков.СуммаПоДоговоруДЗ - ТаблицаОстатков.СуммаМаксимальногоКредита
	|			ИНАЧЕ 0
	|	КОНЕЦ КАК ПревышениеМаксимальногоКредитаНам,
	|	ВЫБОР
	|		КОГДА ТаблицаОстатков.СуммаМаксимальногоКредита > 0 И НЕ ТаблицаОстатков.ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита И ТаблицаОстатков.СуммаПоДоговоруКЗ > ТаблицаОстатков.СуммаМаксимальногоКредита
	|			ТОГДА ТаблицаОстатков.СуммаПоДоговоруКЗ - ТаблицаОстатков.СуммаМаксимальногоКредита
	|			ИНАЧЕ 0
	|	КОНЕЦ КАК ПревышениеМаксимальногоКредитаМы,
	|	ТаблицаОстатков.СуммаПоДоговоруКЗ КАК СуммаПоДоговоруКЗ,
	|	ТаблицаОстатков.СуммаПоДоговоруДЗ КАК СуммаПоДоговоруДЗ,
	|	ТаблицаОстатков.ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита КАК ДоговорВзаиморасчетовОтменаКонтроляСуммыКредита,
	|	ТаблицаОстатков.ПросроченоДней КАК ПросроченоДней"+ТекстВыборкиИнтервалов+"
	|	"+СтрЗапросаСвойств+"
	|{ВЫБРАТЬ
	|	Контрагент.* КАК Контрагент,
	|	ВалютаДоговораСуммаПоДоговоруДЗ КАК ВалютаДоговораСуммаПоДоговоруДЗ,
	|	ВалютаДоговораСуммаПоДоговоруКЗ КАК ВалютаДоговораСуммаПоДоговоруКЗ,
	|	ВалютаДоговораСуммаМаксимальногоКредита КАК ВалютаДоговораСуммаМаксимальногоКредита,
	|	ВалютаДоговораПревышениеМаксимальногоКредитаМы КАК ВалютаДоговораПревышениеМаксимальногоКредитаМы,
	|	ВалютаДоговораПревышениеМаксимальногоКредитаНам КАК ВалютаДоговораПревышениеМаксимальногоКредитаНам,
	|	ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	|	Сделка.* КАК Сделка,
	|	РазницаДней КАК РазницаДней,
	|	СуммаКЗ КАК СуммаКЗ,
	|	СуммаДЗ КАК СуммаДЗ,
	|	СрокОплатыЗадолженности КАК СрокОплатыЗадолженности,
	|	СуммаМаксимальногоКредита КАК СуммаМаксимальногоКредита,
	|	ПревышениеМаксимальногоКредитаМы КАК ПревышениеМаксимальногоКредитаМы,
	|	ПревышениеМаксимальногоКредитаНам КАК ПревышениеМаксимальногоКредитаНам,
	|	ПросроченоДней КАК ПросроченоДней,
	|	ИнтервалСуммыДолга КАК ИнтервалСуммыДолга,
	|	ПорядокИнтервалаСуммыДолга КАК ПорядокИнтервалаСуммыДолга,
	|	ИнтервалДнейПросрочки КАК ИнтервалДнейПросрочки,
	|	ПорядокИнтервалаДнейПросрочки КАК ПорядокИнтервалаДнейПросрочки
	|	"+СтрЗапросаСвойств+"}
	|ИЗ
	|(ВЫБРАТЬ
	|	ПредТаблицаОстатков.Контрагент КАК Контрагент,
	|	ПредТаблицаОстатков.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	МАКСИМУМ(ПредТаблицаОстатков.СрокОплатыЗадолженности) КАК СрокОплатыЗадолженности,
	|	МАКСИМУМ(ПредТаблицаОстатков.ВалютаВзаиморасчетов) КАК ВалютаВзаиморасчетов,
	|	ПредТаблицаОстатков.Сделка КАК Сделка,
	|	ВЫБОР
	|		КОГДА ПредТаблицаОстатков.ПросроченоДней <= 0
	|			ТОГДА 0
	|		ИНАЧЕ ПредТаблицаОстатков.ПросроченоДней
	|	КОНЕЦ КАК ПросроченоДней,
	|	МАКСИМУМ(ПредТаблицаОстатков.РазницаДней) КАК РазницаДней,
	|	МАКСИМУМ(ПредТаблицаОстатков.СуммаМаксимальногоКредита) КАК СуммаМаксимальногоКредита,
	|	СУММА(ПредТаблицаОстатков.СуммаПоДоговоруКЗ) КАК СуммаПоДоговоруКЗ,
	|	СУММА(ПредТаблицаОстатков.СуммаПоДоговоруДЗ) КАК СуммаПоДоговоруДЗ,
	|	СУММА(ПредТаблицаОстатков.СуммаКЗ) КАК СуммаКЗ,
	|	СУММА(ПредТаблицаОстатков.СуммаДЗ) КАК СуммаДЗ
	|{ВЫБРАТЬ
	|	Контрагент КАК Контрагент,
	|	ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Сделка КАК Сделка}
	|ИЗ ТаблицаОстатков КАК ПредТаблицаОстатков
	|СГРУППИРОВАТЬ ПО
	|	ПредТаблицаОстатков.Контрагент,
	|	ПредТаблицаОстатков.ДоговорВзаиморасчетов,
	|	ПредТаблицаОстатков.Сделка,
	|	ВЫБОР
	|		КОГДА ПредТаблицаОстатков.ПросроченоДней <= 0
	|			ТОГДА 0
	|		ИНАЧЕ ПредТаблицаОстатков.ПросроченоДней
	|	КОНЕЦ) КАК ТаблицаОстатков "+ТекстСоединения+"
	|	"+СтрСоединенийСвойств+"
	|	"+ТекстОтбораСвойств+"
	|{УПОРЯДОЧИТЬ ПО
	|	Контрагент.* КАК Контрагент,
	|	ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	|	Сделка.* КАК Сделка,
	|	РазницаДней.* КАК РазницаДней,
	|	ИнтервалСуммыДолга КАК ИнтервалСуммыДолга,
	|	ПорядокИнтервалаСуммыДолга КАК ПорядокИнтервалаСуммыДолга,
	|	ИнтервалДнейПросрочки КАК ИнтервалДнейПросрочки,
	|	ПорядокИнтервалаДнейПросрочки КАК ПорядокИнтервалаДнейПросрочки
	|	"+СтрЗапросаСвойств+"}
	|{ИТОГИ ПО
	|	Контрагент.* КАК Контрагент,
	|	Сделка.* КАК Сделка,
	|	ДоговорВзаиморасчетов.* КАК ДоговорВзаиморасчетов,
	|	РазницаДней.* КАК РазницаДней,
	|	ИнтервалСуммыДолга КАК ИнтервалСуммыДолга,
	|	ИнтервалДнейПросрочки КАК ИнтервалДнейПросрочки
	|	"+СтрЗапросаСвойств+"}
	|ИТОГИ
	|	СУММА(СуммаКЗ),
	|	СУММА(СуммаДЗ),
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(СрокОплатыЗадолженности)
	|	КОНЕЦ КАК СрокОплатыЗадолженности,
	|	ВЫБОР
	|		КОГДА ИнтервалСуммыДолга ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(ПорядокИнтервалаСуммыДолга)
	|	КОНЕЦ КАК ПорядокИнтервалаСуммыДолга,
	|	ВЫБОР
	|		КОГДА ИнтервалДнейПросрочки ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(ПорядокИнтервалаДнейПросрочки)
	|	КОНЕЦ КАК ПорядокИнтервалаДнейПросрочки,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ СУММА(СуммаПоДоговоруДЗ)=0 ТОГДА NULL
	|		ИНАЧЕ МАКСИМУМ(ВалютаДоговораСуммаПоДоговоруДЗ)
	|	КОНЕЦ КАК ВалютаДоговораСуммаПоДоговоруДЗ,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ СУММА(СуммаПоДоговоруКЗ)=0 ТОГДА NULL
	|		ИНАЧЕ МАКСИМУМ(ВалютаДоговораСуммаПоДоговоруКЗ)
	|	КОНЕЦ КАК ВалютаДоговораСуммаПоДоговоруКЗ,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ СУММА(СуммаМаксимальногоКредита)=0 ТОГДА NULL
	|		ИНАЧЕ МАКСИМУМ(ВалютаДоговораСуммаМаксимальногоКредита)
	|	КОНЕЦ КАК ВалютаДоговораСуммаМаксимальногоКредита,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ СУММА(ПревышениеМаксимальногоКредитаМы)=0 ТОГДА NULL
	|		ИНАЧЕ МАКСИМУМ(ВалютаДоговораПревышениеМаксимальногоКредитаМы)
	|	КОНЕЦ КАК ВалютаДоговораПревышениеМаксимальногоКредитаМы,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ СУММА(ПревышениеМаксимальногоКредитаНам)=0 ТОГДА NULL
	|		ИНАЧЕ МАКСИМУМ(ВалютаДоговораПревышениеМаксимальногоКредитаНам)
	|	КОНЕЦ КАК ВалютаДоговораПревышениеМаксимальногоКредитаНам,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(РазницаДней)
	|	КОНЕЦ КАК РазницаДней,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(ПросроченоДней)
	|	КОНЕЦ КАК ПросроченоДней,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(СуммаМаксимальногоКредита)
	|	КОНЕЦ КАК СуммаМаксимальногоКредита,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ (СУММА(СуммаПоДоговоруДЗ)-МАКСИМУМ(СуммаМаксимальногоКредита)) < 0 ИЛИ МАКСИМУМ(ДоговорВзаиморасчетовОтменаКонтроляСуммыКредита) ТОГДА 0
	|		ИНАЧЕ СУММА(СуммаПоДоговоруДЗ)-МАКСИМУМ(СуммаМаксимальногоКредита)
	|	КОНЕЦ КАК ПревышениеМаксимальногоКредитаНам,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ИЛИ (СУММА(СуммаПоДоговоруКЗ)-МАКСИМУМ(СуммаМаксимальногоКредита)) < 0 ИЛИ МАКСИМУМ(ДоговорВзаиморасчетовОтменаКонтроляСуммыКредита) ТОГДА 0
	|		ИНАЧЕ СУММА(СуммаПоДоговоруКЗ)-МАКСИМУМ(СуммаМаксимальногоКредита)
	|	КОНЕЦ КАК ПревышениеМаксимальногоКредитаМы,
	|	МАКСИМУМ(ДоговорВзаиморасчетовОтменаКонтроляСуммыКредита),
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ СУММА(СуммаПоДоговоруКЗ)
	|	КОНЕЦ КАК СуммаПоДоговоруКЗ,
	|	ВЫБОР
	|		КОГДА ДоговорВзаиморасчетов ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ СУММА(СуммаПоДоговоруДЗ)
	|	КОНЕЦ КАК СуммаПоДоговоруДЗ
	|	"+ТекстИтоговПоЗадолженностям+"
	|	"+СтрЗапросаИтогиСвойств+"
	|ПО
	|	ОБЩИЕ,
	|	ИнтервалСуммыДолга,
	|	ИнтервалДнейПросрочки,
	|	Сделка,
	|	ДоговорВзаиморасчетов
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат ТекстЗапросаРезультат;
	
КонецФункции

// Стандартная процедура настройки построителя отчета 
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	СтруктураДополненияПоказателей = Новый Структура;
	СтруктураДополненияПоказателей.Вставить("СуммаПоДоговоруДЗ", Новый Массив);
	СтруктураДополненияПоказателей.Вставить("СуммаПоДоговоруКЗ", Новый Массив);
	СтруктураДополненияПоказателей.Вставить("СуммаМаксимальногоКредита", Новый Массив);
	СтруктураДополненияПоказателей.Вставить("ПревышениеМаксимальногоКредитаМы", Новый Массив);
	СтруктураДополненияПоказателей.Вставить("ПревышениеМаксимальногоКредитаНам", Новый Массив);
	
	СтруктураВалюты = Новый Структура("Имя, ПутьКДанным, ИмяСвязанногоПоля, ШиринаКолонки, Формат, ГоризонтальноеПоложение, ВертикальноеПоложение",
					"ВалютаДоговораСуммаПоДоговоруДЗ", "ВалютаДоговораСуммаПоДоговоруДЗ", "ДоговорВзаиморасчетов", 5, "", ГоризонтальноеПоложение.Центр, ВертикальноеПоложение.Центр);
	СтруктураДополненияПоказателей.СуммаПоДоговоруДЗ.Добавить(СтруктураВалюты);
	
	СтруктураВалюты = Новый Структура("Имя, ПутьКДанным, ИмяСвязанногоПоля, ШиринаКолонки, Формат, ГоризонтальноеПоложение, ВертикальноеПоложение",
					"ВалютаДоговораСуммаПоДоговоруКЗ", "ВалютаДоговораСуммаПоДоговоруКЗ", "ДоговорВзаиморасчетов", 5, "", ГоризонтальноеПоложение.Центр, ВертикальноеПоложение.Центр);
	СтруктураДополненияПоказателей.СуммаПоДоговоруКЗ.Добавить(СтруктураВалюты);
	
	СтруктураВалюты = Новый Структура("Имя, ПутьКДанным, ИмяСвязанногоПоля, ШиринаКолонки, Формат, ГоризонтальноеПоложение, ВертикальноеПоложение",
					"ВалютаДоговораСуммаМаксимальногоКредита", "ВалютаДоговораСуммаМаксимальногоКредита", "ДоговорВзаиморасчетов", 5, "", ГоризонтальноеПоложение.Центр, ВертикальноеПоложение.Центр);
	СтруктураДополненияПоказателей.СуммаМаксимальногоКредита.Добавить(СтруктураВалюты);
	
	СтруктураВалюты = Новый Структура("Имя, ПутьКДанным, ИмяСвязанногоПоля, ШиринаКолонки, Формат, ГоризонтальноеПоложение, ВертикальноеПоложение",
					"ВалютаДоговораПревышениеМаксимальногоКредитаМы", "ВалютаДоговораПревышениеМаксимальногоКредитаМы", "ДоговорВзаиморасчетов", 5, "", ГоризонтальноеПоложение.Центр, ВертикальноеПоложение.Центр);
	СтруктураДополненияПоказателей.ПревышениеМаксимальногоКредитаМы.Добавить(СтруктураВалюты);
	
	СтруктураВалюты = Новый Структура("Имя, ПутьКДанным, ИмяСвязанногоПоля, ШиринаКолонки, Формат, ГоризонтальноеПоложение, ВертикальноеПоложение",
					"ВалютаДоговораПревышениеМаксимальногоКредитаНам", "ВалютаДоговораПревышениеМаксимальногоКредитаНам", "ДоговорВзаиморасчетов", 5, "", ГоризонтальноеПоложение.Центр, ВертикальноеПоложение.Центр);
	СтруктураДополненияПоказателей.ПревышениеМаксимальногоКредитаНам.Добавить(СтруктураВалюты);
					
	ПараметрыРесурсов.Вставить("СтруктураДополненияПоказателей", СтруктураДополненияПоказателей);
	
	ЗаполнитьДополнительныеПоказатели();
	ТекРежимВывода = ВосстановитьЗначение("ВозрастЗадолженности_РежимВывода");
	РежимВывода = ?(ТекРежимВывода=Неопределено, 0, ТекРежимВывода);
	
	Если ВозрастныеГруппы.Количество()=0 Тогда
		НовыйПериод = ВозрастныеГруппы.Добавить();
		НовыйПериод.ЧислоДней		= 30;
		НовыйПериод.Представление	= "До 30 дней.";
		НовыйПериод = ВозрастныеГруппы.Добавить();
		НовыйПериод.ЧислоДней		= 60;
		НовыйПериод.Представление	= "От 31 до 60 дней.";
		НовыйПериод = ВозрастныеГруппы.Добавить();
		НовыйПериод.ЧислоДней		= 90;
		НовыйПериод.Представление	= "От 61 до 90 дней.";
		НовыйПериод = ВозрастныеГруппы.Добавить();
		НовыйПериод.ЧислоДней		= 120;
		НовыйПериод.Представление	= "От 91 до 120 дней.";
		НовыйПериод = ВозрастныеГруппы.Добавить();
		НовыйПериод.ЧислоДней		= 180;
		НовыйПериод.Представление	= "От 121 до 180 дней.";
		НовыйПериод = ВозрастныеГруппы.Добавить();
		НовыйПериод.ЧислоДней		= 50000;
		НовыйПериод.Представление	= "Более 180 дней.";
	КонецЕсли;
	
	Если СуммовыеГруппы.Количество()=0 Тогда
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 10000;
		НовыйПериод.Представление	= "До 10 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 30000;
		НовыйПериод.Представление	= "От 10 до 30 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 50000;
		НовыйПериод.Представление	= "От 30 до 50 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 100000;
		НовыйПериод.Представление	= "От 50 до 100 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 150000;
		НовыйПериод.Представление	= "От 100 до 150 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 300000;
		НовыйПериод.Представление	= "От 150 до 300 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 500000;
		НовыйПериод.Представление	= "От 300 до 500 тыс.";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаДолга		= 9999999999999;
		НовыйПериод.Представление	= "Более 500 тыс.";
	КонецЕсли;
	
	ЕстьДоговор = (ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ДоговорВзаиморасчетов")).Количество()>0 ИЛИ
				ИзмеренияКолонки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ДоговорВзаиморасчетов")).Количество()>0);
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Если ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ДоговорВзаиморасчетов")).Количество()>0 ИЛИ
		ИзмеренияКолонки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ДоговорВзаиморасчетов")).Количество()>0 Тогда
		ЕстьДоговор = Истина;
	Иначе
		ЕстьДоговор = Ложь;
	КонецЕсли;
	
	ДопПредставление = " ("+ВалютаОтчета.Наименование+")";
	УстановитьИспользованиеПоказателей(ДопПредставление);
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает структуру форматирования полей
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// изменяет макет построителя
// Параметры:
//	ТекПостроитель - построитель отчета
//	МакетОтчета - макет в который нужно внести изменения
//	ПараметрыОформления - параметры оформления нового макета
//
Функция ИзменитьМакетПостроителя(ТекПостроитель, МакетОтчета, ПараметрыОформления) Экспорт
	
	Если ЕстьДоговор Тогда
		СтруктураДополненияПоказателей = ПараметрыРесурсов.СтруктураДополненияПоказателей;
		Для Каждого ТекРесурс Из СтруктураДополненияПоказателей Цикл
			ТекПоказатель = Показатели.Найти(ТекРесурс.Ключ, "Имя");
			Если ТекПоказатель=Неопределено ИЛИ ТекПоказатель.Использование=Ложь Тогда Продолжить; КонецЕсли;
			Для Каждого ЭлементРесурса Из ТекРесурс.Значение Цикл
				Если ТекПостроитель.ДоступныеПоля.Найти(ЭлементРесурса.ПутьКДанным)<>Неопределено И ТекПостроитель.ВыбранныеПоля.Найти(ЭлементРесурса.Имя)=Неопределено Тогда
					ТекПостроитель.ВыбранныеПоля.Добавить(ЭлементРесурса.Имя, ЭлементРесурса.Имя);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	// Сортировка интервалов
	ТекИзмерение = ТекПостроитель.Порядок.Найти("ИнтервалСуммыДолга");
	Если Не ТекИзмерение = Неопределено Тогда
		Инд = ТекПостроитель.Порядок.Индекс(ТекИзмерение);
		ТекПостроитель.Порядок.Удалить(Инд);
		ТекПостроитель.Порядок.Добавить("ПорядокИнтервалаСуммыДолга", "ПорядокИнтервалаСуммыДолга",,НаправлениеСортировки.Возр);
		Смещение = Инд-(ТекПостроитель.Порядок.Количество()-1);
		ТекПостроитель.Порядок.Сдвинуть(ТекПостроитель.Порядок.Количество()-1, Смещение);
		ТекПостроитель.ВыбранныеПоля.Добавить("ПорядокИнтервалаСуммыДолга", "ПорядокИнтервалаСуммыДолга");
	КонецЕсли;
	
	ТекИзмерение = ТекПостроитель.Порядок.Найти("ИнтервалДнейПросрочки");
	Если Не ТекИзмерение = Неопределено Тогда
		Инд = ТекПостроитель.Порядок.Индекс(ТекИзмерение);
		ТекПостроитель.Порядок.Удалить(Инд);
		ТекПостроитель.Порядок.Добавить("ПорядокИнтервалаДнейПросрочки", "ПорядокИнтервалаДнейПросрочки",,НаправлениеСортировки.Возр);
		Смещение = Инд-(ТекПостроитель.Порядок.Количество()-1);
		ТекПостроитель.Порядок.Сдвинуть(ТекПостроитель.Порядок.Количество()-1, Смещение);
		ТекПостроитель.ВыбранныеПоля.Добавить("ПорядокИнтервалаДнейПросрочки", "ПорядокИнтервалаДнейПросрочки");
	КонецЕсли;
	
	УстановитьИспользованиеПоказателей();
	
	Возврат Ложь;
	
КонецФункции


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
ПоследовательностиОтчета = Новый Структура();
ПараметрыРесурсов = Новый Структура();
ПоследовательностиОтчета.Вставить("ГраницыВзаиморасчетов");

#КонецЕсли
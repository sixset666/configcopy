#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем СтруктураДоступныхДеревьев Экспорт;              // хранит структуру доступных деревьев
Перем ПримененныеПоля Экспорт;                         // хранит примененные поля
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;   // хранит структуру итоговых выражений показателей
Перем ШаблонИсточникаДанных Экспорт;                   // хранит шаблон источника данных
Перем ВалютаПоУмолчанию Экспорт;                       // хранит валюту отчета по умолчанию


// Проверяет заполнение валюты отчета, возвращает "истина", если корректно, "ложь" - если нет
//
// Без параметров
// 
Функция КорректностьЗаполнения(ПродолжитьФормирование) Экспорт
		
	Если ВыбиратьВалюту И обЗначениеНеЗаполнено(ВалютаОтчета) Тогда
		Предупреждение("Не указана валюта отчета!", 10);
		ПродолжитьФормирование = Ложь;
	КонецЕсли;
		
	Возврат Ложь;
	
КонецФункции // КорректностьЗаполнения()

// Функция определяет, с помощью каких средств будет загружаться файл в формате XLS,XLSX
// доступна загрузка с использованием Microsoft Office либо Open Office
//
// Параметры:
//
// Возвращаемое значение:
//  Булево			- Истина, если файл прочитан, Ложь - иначе
//
Функция ПрочитатьXLS(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены)
	
	MSOfficeДоступен = Ложь;
	Попытка
	 	Excel = Новый COMОбъект("Excel.Application");
		MSOfficeДоступен = Истина;
	Исключение
		MSOfficeДоступен = Ложь;
	КонецПопытки;
	
	Если MSOfficeДоступен Тогда
		Возврат ПрочитатьEXCEL(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены);
	КонецЕсли;
	
	OpenOfficeДоступен = Ложь;
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params = MakePropertyValue(ServiceManager,"Hidden",Истина);
		Args = Новый COMSafeArray("VT_DISPATCH", 1); Args.SetValue(0,Params);
		OpenOfficeДоступен = Истина;
	Исключение
		OpenOfficeДоступен = Ложь;	
	КонецПопытки;

	Если OpenOfficeДоступен Тогда
		ТекстВопроса = "На компьютере не установлен Microsoft Excel. Загрузить файл средствами Open Office?";
		ОтветНаВопрос = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Возврат ПрочитатьOpenOffice(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены);	
		КонецЕсли;
	КонецЕсли;
	
	Сообщить("Ошибка: для загрузки файла требуется установить Microsoft Office или Open Office.");
	Возврат Ложь;	
	
КонецФункции // ПрочитатьXLS()

// Процедура чтения прайс-листа из файла excel
//
// Параметры:
// ТЗПрайсЛистов - ТаблицаЗначений - Таблица значений прайс-листов
//
Функция ПрочитатьEXCEL(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены)
	
	КодоваяСтраница = ВыборкаПрайсЛистов.КодоваяСтраница;
	ПрайсЛист  = ВыборкаПрайсЛистов.ПрайсЛист;
	Контрагент = ВыборкаПрайсЛистов.Контрагент;
	
	ИмяОбрФайла = СокрЛП(ВыборкаПрайсЛистов.СтрокаПодключения);
	ПроверкаПути = Новый Файл(ИмяОбрФайла);
	Если НЕ ПроверкаПути.Существует() Тогда
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: файл прайс-листа <"+ИмяОбрФайла+"> не найден.", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;

	Попытка
		Приложение = Новый COMОбъект("Excel.Application");
		Приложение.Application.Workbooks.Open(ИмяОбрФайла);
	Исключение
		Сообщить("Ошибка: возможно не установлен Microsoft Excel или установленная версия не поддерживается.");
		Возврат Ложь;
	КонецПопытки;
	
	Если Приложение=Неопределено Тогда
		Предупреждение("Ошибка: возможно не установлен Microsoft Excel или установленная версия не поддерживается.");
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьДанные = Истина;
	ОбрабатываемаяСтрока = 1;
	ЕстьОграничениеНаСтроки = Ложь;
	АктивнаяСтраничка = Приложение.ActiveSheet;
	ЕстьПоляПоиска = (НЕ КлючевоеПоле = Неопределено);
	Пока ЕстьДанные Цикл
		
		Если ЕстьОграничениеНаСтроки И ОбрабатываемаяСтрока>ВыборкаПрайсЛистов.СтрокаКонец Тогда
			Прервать;
		КонецЕсли;
		
		ОбработкаПрерыванияПользователя();
		Состояние("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> обрабатывается строка " + ОбрабатываемаяСтрока);
		
		Если ЕстьПоляПоиска Тогда
			ЗначениеПоля = АктивнаяСтраничка.Cells(ОбрабатываемаяСтрока, КлючевоеПоле).Value;
			Если НЕ ЗначениеЗаполнено(ЗначениеПоля) Тогда
				ЕстьДанные = Ложь;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НоваяПозицияПрайсЛиста = ТЗПрайсЛистов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПозицияПрайсЛиста, ВыборкаПрайсЛистов);
		
		Для Каждого ПолеЗагрузки Из СтруктураТаблицы Цикл
			ЗначениеПоля = АктивнаяСтраничка.Cells(ОбрабатываемаяСтрока, ПолеЗагрузки.Значение).Value;
			Если ТипЗнч(ЗначениеПоля) = Тип("Строка") И КодоваяСтраница <> "cpCP1251" Тогда
				ЗначениеПоля = глРарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, КодоваяСтраница, "cpCP1251");
			КонецЕсли;
			Если ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ]) = Тип("Строка") И
				ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
				ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
			КонецЕсли;
			НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ] = ЗначениеПоля;
		КонецЦикла;
		
		Если ПересчитыватьЦены Тогда
			НоваяПозицияПрайсЛиста.Цена = НоваяПозицияПрайсЛиста.Цена * КоэффициентПересчета;
		КонецЕсли;
		
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока + 1;
		
	КонецЦикла;
	
	Для Каждого Book Из Приложение.WorkBooks Цикл
		Book.close(Ложь, "", Истина);
	КонецЦикла;
	
	Приложение.Quit();
	Приложение = Неопределено;

	Возврат Истина;
	
КонецФункции // ПрочитатьEXCEL()

// процедура разбора строки символов на массив символов
//
// Параметры:
// СтрокаДляРазбора - Строка - Строка для разбора
//
// Возвращаемое значение:
// Массив - Массив символов строки
//
Функция РазобратьСтрокуTXT(Знач СтрокаДляРазбора, Разделитель)
	
	МассивСтрокиФайлаПрайсЛиста = Новый Массив();
	
	Пока СтрДлина(СтрокаДляРазбора)>0 Цикл
		ПозицияРазделителя=Найти(СтрокаДляРазбора, Разделитель);
		Если ПозицияРазделителя=0 Тогда
			ПозицияРазделителя=СтрДлина(СтрокаДляРазбора)+1;
		КонецЕсли; 	
		СтрокаПараметр=Лев(СтрокаДляРазбора,ПозицияРазделителя-1);
		СтрокаДляРазбора=Сред(СтрокаДляРазбора,ПозицияРазделителя+1);
		МассивСтрокиФайлаПрайсЛиста.Добавить(СтрокаПараметр);
	КонецЦикла; 
	
	Возврат МассивСтрокиФайлаПрайсЛиста;
	
КонецФункции // РазобратьСтрокуTXT()

// функция чтения прайс-листа из текстового файла
//
// Параметры:
// ВыборкаПрайсЛистов - ТаблицаЗначений - Таблица значений прайс-листов
//
Процедура ПрочитатьTXT(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены)
	
	ИмяОбрФайла = СокрЛП(ВыборкаПрайсЛистов.СтрокаПодключения);

	//Проверим наличие файла
	Файл=Новый Файл(ИмяОбрФайла);
	Если НЕ Файл.Существует() Тогда
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: файл прайс-листа <"+ИмяОбрФайла+"> не найден.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	КодоваяСтраница = ВыборкаПрайсЛистов.КодоваяСтраница;
	Разделитель     = ВыборкаПрайсЛистов.Разделитель;
	
	ЕстьПоляПоиска  = (НЕ КлючевоеПоле = Неопределено);
	ФайлОбменаОбъект = Новый ЧтениеТекста(ИмяОбрФайла);
	ЕстьДанные = Истина;
	СтрокаНачалоЗначение = 1;
	ЕстьОграничениеНаСтроки = Ложь;
	ОбрабатываемаяСтрока=0;
	Пока ЕстьДанные Цикл
		
		Если ЕстьОграничениеНаСтроки И ОбрабатываемаяСтрока>ВыборкаПрайсЛистов.СтрокаКонец Тогда
			Прервать;
		КонецЕсли;
		
		ОбработкаПрерыванияПользователя();
		
		СтрокаФайлаПрайсЛиста = ФайлОбменаОбъект.ПрочитатьСтроку();
		Если СтрокаФайлаПрайсЛиста = Неопределено Тогда
			Прервать;
		КонецЕсли; 
		
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока+1;
		Если ОбрабатываемаяСтрока < СтрокаНачалоЗначение Тогда
			Продолжить;
		КонецЕсли; 
		
		Состояние("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> обрабатывается строка " + ОбрабатываемаяСтрока);
		
		СтрокаФайлаПрайсЛиста = СокрЛП(СтрокаФайлаПрайсЛиста);
		МассивСтрокиФайлаПрайсЛиста = РазобратьСтрокуTXT(СтрокаФайлаПрайсЛиста, Разделитель);
		КоличествоПолей = МассивСтрокиФайлаПрайсЛиста.ВГраница();
		
		Если ЕстьПоляПоиска Тогда
			
			Если КлючевоеПоле <= КоличествоПолей Тогда
				ЗначениеПоля = МассивСтрокиФайлаПрайсЛиста[КлючевоеПоле];
				Если НЕ ЗначениеЗаполнено(ЗначениеПоля) Тогда
					ЕстьДанные = Ложь;
					Продолжить;
				КонецЕсли;
			Иначе
				Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: Для поля <"+КлючевоеПоле+"> заданы неверные данные", СтатусСообщения.Важное);
				ФайлОбменаОбъект.Закрыть();
				Возврат;
			КонецЕсли;
		
		КонецЕсли;
		
		НоваяПозицияПрайсЛиста=ТЗПрайсЛистов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПозицияПрайсЛиста, ВыборкаПрайсЛистов);
		
		Для Каждого ПолеЗагрузки Из СтруктураТаблицы Цикл
			Если ПолеЗагрузки.Значение <= КоличествоПолей Тогда
				ЗначениеПоля = МассивСтрокиФайлаПрайсЛиста[ПолеЗагрузки.Значение];
			Иначе
				Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: Для реквизита <"+ПолеЗагрузки.Ключ+"> заданы неверные данные", СтатусСообщения.Важное);
				ФайлОбменаОбъект.Закрыть();
				Возврат;
			КонецЕсли;
			Если глРарус_Компонента<>Неопределено Тогда
				Если ТипЗнч(ЗначениеПоля)=Тип("Строка") И КодоваяСтраница<>"cpCP1251" Тогда
					ЗначениеПоля=глРарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля,КодоваяСтраница,"cpCP1251");
				КонецЕсли;
			КонецЕсли;
			НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ] = ЗначениеПоля;
		КонецЦикла;
		
		Если ПересчитыватьЦены Тогда
			НоваяПозицияПрайсЛиста.Цена = НоваяПозицияПрайсЛиста.Цена * КоэффициентПересчета;
		КонецЕсли;
		
	КонецЦикла;

	ФайлОбменаОбъект.Закрыть();

	Возврат;
	
КонецПроцедуры // ПрочитатьTXT()

// функция чтения прайс-листа из файла
//
// Параметры:
// ТЗПрайсЛистов - ТаблицаЗначений - Таблица значений прайс-листа
//
Процедура ПрочитатьADO(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КоэффициентПересчета, ПересчитыватьЦены)
	
	Состояние("Инициализация ADODB.Connection ...");
	Попытка
		Приложение = Новый COMОбъект("ADODB.Connection");
		Коннект = Приложение.Open(ВыборкаПрайсЛистов.СтрокаПодключения);
	Исключение
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> ошибка подключения к файлу: "+ОписаниеОшибки(), СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	
	Команда = Новый  COMОбъект("ADODB.Command");
	Recordset = Новый  COMОбъект("ADODB.Recordset");
	Recordset.ActiveConnection = Приложение;
	Recordset.CursorType=3;
	Recordset.Open(СокрЛП(ВыборкаПрайсЛистов.ИмяТаблицы));
	КоличествоЗаписей = Recordset.RecordCount;
	ТекстЗап="
	|SELECT "+СокрЛП(ВыборкаПрайсЛистов.ИмяТаблицы)+".*
	|FROM "+СокрЛП(ВыборкаПрайсЛистов.ИмяТаблицы);
	Команда.CommandText = ТекстЗап;
	Команда.ActiveConnection = Приложение;
	Попытка
		РезультатЗапроса = Команда.Execute();
	Исключение
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> ошибка выборки из прайс-листа: "+ОписаниеОшибки(), СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	КолПолей = Recordset.Fields.Count;
	
	ОбрабатываемаяСтрока = 0;
	
	КодоваяСтраница = ВыборкаПрайсЛистов.КодоваяСтраница;
	
	Состояние("Загрузка прайс-листа ...");
	Пока НЕ Recordset.EOF Цикл
		ОбработкаПрерыванияПользователя();
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока + 1;
		Состояние("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> обрабатывается строка " + ОбрабатываемаяСтрока);
		
		НоваяПозицияПрайсЛиста=ТЗПрайсЛистов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПозицияПрайсЛиста, ВыборкаПрайсЛистов);
		
		Для Каждого ПолеЗагрузки Из СтруктураТаблицы Цикл
		
			Попытка
				ЗначениеПоля=Recordset.Fields(ПолеЗагрузки.Значение).Value;
			Исключение
				Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: Для поля <"+ПолеЗагрузки.Ключ+"> заданы неверные данные", СтатусСообщения.Важное);
				Возврат;
			КонецПопытки;
			Если глРарус_Компонента<>Неопределено Тогда
				Если ТипЗнч(ЗначениеПоля)=Тип("Строка") И КодоваяСтраница<>"cpCP1251" Тогда
					ЗначениеПоля=глРарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля,КодоваяСтраница,"cpCP1251");
				КонецЕсли;
			КонецЕсли;
			Если ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ])=Тип("Строка") И ТипЗнч(ЗначениеПоля)=Тип("Число") Тогда
				ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
			КонецЕсли; 
			НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ] = ЗначениеПоля;
		КонецЦикла;
		
		Если ПересчитыватьЦены Тогда
			НоваяПозицияПрайсЛиста.Цена = НоваяПозицияПрайсЛиста.Цена * КоэффициентПересчета;
		КонецЕсли;
		
		Recordset.MoveNext();
	КонецЦикла;

	Возврат;
	
КонецПроцедуры // ПрочитатьADO()

// "open office" заполняет массив свойств
Функция MakePropertyValue(ServiceManager,Name, Value)
	Struct = ServiceManager.Bridge_GetStruct("com.sun.star.beans.PropertyValue");
	Struct.Name = Name;
	Struct.Value = Value;
	Возврат Struct;
КонецФункции

// "open office" Функция преобразует Windows имя файла в URL OpenOffice
Функция ПреобразоватьВURL(ИмяФайла)
	Возврат "file:///" + СтрЗаменить(ИмяФайла, "\", "/");
КонецФункции

// функция чтения файла OpenOffice в прайс-лист
//
// Параметры:
// ТЗПрайсЛиста - ТаблицаЗначений - Таблица значений прайс-листа
//
Функция ПрочитатьOpenOffice(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены)
	
	КодоваяСтраница = ВыборкаПрайсЛистов.КодоваяСтраница;
	ПрайсЛист       = ВыборкаПрайсЛистов.ПрайсЛист;
	Контрагент      = ВыборкаПрайсЛистов.Контрагент;
	ИмяОбрФайла     = СокрЛП(ВыборкаПрайсЛистов.СтрокаПодключения);
	
	ПроверкаПути = Новый Файл(ИмяОбрФайла);
	Если НЕ ПроверкаПути.Существует() Тогда
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: файл прайс-листа <"+ИмяОбрФайла+"> не найден.", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params  = MakePropertyValue(ServiceManager,"Hidden", Истина);
		Args    = Новый COMSafeArray("VT_DISPATCH", 1); Args.SetValue(0,Params);
		OODoc   = Desktop.loadComponentFromURL(ПреобразоватьВURL(ИмяОбрФайла), "_blank", 0, Args);
		Sheets  = OODoc.getSheets();
		Sheet   = Sheets.getByIndex(0);
	Исключение
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: на компьютере должен быть установлен Open Office версии не ниже 2.0", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецПопытки;

	Если НЕ ТипЗнч(Sheet) = Тип("COMОбъект") Тогда
		Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: Open Office не загружен", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;

	ЕстьДанные = Истина;
	ОбрабатываемаяСтрока = 1;
	ЕстьОграничениеНаСтроки = Ложь;
	ЕстьПоляПоиска = (НЕ КлючевоеПоле = Неопределено);
	Пока ЕстьДанные Цикл
		
		Если ЕстьОграничениеНаСтроки И ОбрабатываемаяСтрока>ВыборкаПрайсЛистов.СтрокаКонец Тогда
			Прервать;
		КонецЕсли;
		
		ОбработкаПрерыванияПользователя();
		Состояние("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> обрабатывается строка " + ОбрабатываемаяСтрока);
		
		Если ЕстьПоляПоиска Тогда
			
			Cell = Sheet.getCellByPosition(КлючевоеПоле, ОбрабатываемаяСтрока-1);
			ТипЯчейки = Cell.getType();
			
			Если ТипЯчейки = 1 Тогда
				ЗначениеПоля = Cell.getValue();
			ИначеЕсли ТипЯчейки = 2 Тогда
				ЗначениеПоля = Cell.getString();
			ИначеЕсли ТипЯчейки = 3 Тогда
				ЗначениеПоля = Cell.getFormula();
			Иначе
				ЗначениеПоля = "";
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ЗначениеПоля) Тогда
				ЕстьДанные = Ложь;
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяПозицияПрайсЛиста=Неопределено;
		НоваяПозицияПрайсЛиста = ТЗПрайсЛистов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПозицияПрайсЛиста, ВыборкаПрайсЛистов);
		
		Для Каждого ПолеЗагрузки Из СтруктураТаблицы Цикл
			
			Cell = Sheet.getCellByPosition(ПолеЗагрузки.Значение, ОбрабатываемаяСтрока-1);
			ТипЯчейки = Cell.getType();
			
			Если ТипЯчейки = 1 Тогда
				ЗначениеПоля = Cell.getValue();
			ИначеЕсли ТипЯчейки = 2 Тогда
				ЗначениеПоля = Cell.getString();
			ИначеЕсли ТипЯчейки = 3 Тогда
				ЗначениеПоля = Cell.getFormula();
			Иначе
				ЗначениеПоля = "";
			КонецЕсли;
			
			Если ТипЗнч(ЗначениеПоля) = Тип("Строка") И КодоваяСтраница <> "cpCP1251" Тогда
				ЗначениеПоля=глРарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, КодоваяСтраница, "cpCP1251");
			КонецЕсли;
			
			Если ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ]) = Тип("Строка") И
				ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
				ЗначениеПоля         = Формат(ЗначениеПоля, "ЧГ=0");
			КонецЕсли;
			
			НоваяПозицияПрайсЛиста[ПолеЗагрузки.Ключ] = ЗначениеПоля;
			
		КонецЦикла;
		
		Если ПересчитыватьЦены Тогда
			НоваяПозицияПрайсЛиста.Цена = НоваяПозицияПрайсЛиста.Цена * КоэффициентПересчета;
		КонецЕсли;
		
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока + 1;
		
	КонецЦикла;
	
	ServiceManager = Неопределено;
	
	Возврат Истина;

КонецФункции

// Формирует таблицу источника данных построителя
//
// Параметры
//   ТекПостроитель      - ПостроительОтчета
//	 ТЗПрайсЛистов       - ТаблицаЗначений
//   ПоляНастройкиОтчета - структура
//
// Возвращаемое значение:
//   НоваяТЗПрайсЛистов  - ТаблицаЗначений
//
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТЗПрайсЛистов, ПоляНастройкиОтчета) Экспорт
	
	ТекстОтбора = "";
	Запрос = Новый Запрос;
	СтруктураДоступныхОтборов = Новый Структура();
	СтруктураДоступныхОтборов.Вставить("Контрагент", "ПрайсЛистыКонтрагентов.Владелец");
	СтруктураДоступныхОтборов.Вставить("Производитель", "ПрайсЛистыКонтрагентов.Производитель");
	СтруктураДоступныхОтборов.Вставить("ТипЦен", "ПрайсЛистыКонтрагентов.ТипЦен");
	СтруктураДоступныхОтборов.Вставить("ПрайсЛист", "ПрайсЛистыКонтрагентов.Ссылка");
	Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
		Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		ПозицияТочка = Найти(ТекОтбор.ПутьКДанным, ".");
		Если ПозицияТочка = 0 Тогда
			НачалоПути    = ТекОтбор.ПутьКДанным;
			ОкончаниеПути = "";
		Иначе
			НачалоПути    = Лев(ТекОтбор.ПутьКДанным, ПозицияТочка-1);
			ОкончаниеПути = Сред(ТекОтбор.ПутьКДанным, ПозицияТочка);
		КонецЕсли;
		
		Если СтруктураДоступныхОтборов.Свойство(НачалоПути) Тогда
			ТекстОтбора = ТекстОтбора + " И 
			|	"+отСформироватьСтрокуОтбора(ТекОтбор, Запрос.Параметры, СтруктураДоступныхОтборов[НачалоПути]+ОкончаниеПути);
			//ТекстОтбора = ТекстОтбора + 
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВыбиратьВалюту Тогда
		
		//Заполним параметры
		КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКонца, Новый Структура("Валюта", ВалютаОтчета));
		КурсВалютыОтчета = КурсВалюты.Курс/?(КурсВалюты.Кратность=0,1,КурсВалюты.Кратность);
		КурсВалютыОтчета = ?(КурсВалютыОтчета = 0, 1, КурсВалютыОтчета);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
		|	ВЫБОР
		|		КОГДА КурсыВалютСрезПоследних.Кратность=0 ТОГДА
		|			КурсыВалютСрезПоследних.Курс/&КурсВалютыОтчета
		|		ИНАЧЕ
		|			КурсыВалютСрезПоследних.Курс/(КурсыВалютСрезПоследних.Кратность*&КурсВалютыОтчета)
		|	КОНЕЦ КАК Коэффициент
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&НаДату,) КАК КурсыВалютСрезПоследних";
		
		Запрос.УстановитьПараметр("НаДату",           ДатаКонца);
		Запрос.УстановитьПараметр("КурсВалютыОтчета", КурсВалютыОтчета);
		ПерерасчетЦены = Новый Соответствие;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПерерасчетЦены.Вставить(Выборка.Валюта, Выборка.Коэффициент);
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.Ссылка КАК ПрайсЛист,
	|	ВЫБОР
	|		КОГДА ПрайсЛистыКонтрагентов.ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка) ИЛИ 
	|			ПрайсЛистыКонтрагентов.ТипЦен.ВалютаЦены = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА ПрайсЛистыКонтрагентов.Валюта
	|		ИНАЧЕ
	|			ПрайсЛистыКонтрагентов.ТипЦен.ВалютаЦены 
	|	КОНЕЦ КАК Валюта,
	|	ПрайсЛистыКонтрагентов.ТипЦен,
	|	ПрайсЛистыКонтрагентов.НоменклатурнаяГруппа,
	|	ПрайсЛистыКонтрагентов.КодоваяСтраница,
	|	ПрайсЛистыКонтрагентов.ИмяТаблицы,
	|	ПрайсЛистыКонтрагентов.Разделитель,
	|	ПрайсЛистыКонтрагентов.СтрокаПодключения,
	|	ПрайсЛистыКонтрагентов.ФайлИсточникДанных,
	|	ПрайсЛистыКонтрагентов.Производитель,
	|	ПрайсЛистыКонтрагентов.Наименование КАК Наименование,
	|	ПрайсЛистыКонтрагентов.Владелец КАК Контрагент,
	|	ПрайсЛистыКонтрагентов.СтруктураФайлаПрайсЛиста.(
	|		НомерСтроки КАК НомерСтроки,
	|		ИмяРеквизитаПрайсЛиста,
	|		ИмяПоляФайла,
	|		Ключевое
	|	)
	|ИЗ
	|	Справочник.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|ГДЕ
	|	(НЕ ПрайсЛистыКонтрагентов.ПометкаУдаления)" + ТекстОтбора + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование,
	|	НомерСтроки";
	
	ВыборкаПрайсЛистов = Запрос.Выполнить().Выбрать();
	Пока ВыборкаПрайсЛистов.Следующий() Цикл
		
		ТипФайла = "";
		ТипФайла = СокрЛП(ВыборкаПрайсЛистов.СтрокаПодключения);
		ПозицияТочки = Найти(ТипФайла, ".");
		Пока ПозицияТочки>0 Цикл
			ТипФайла = Сред(ТипФайла, ПозицияТочки+1);
			ПозицияТочки = Найти(ТипФайла, ".");
		КонецЦикла;
		ТипФайла = "."+ВРег(ТипФайла);
		
		//КлючевыеПоля     = Новый Структура; // Имя ключевого поля регистра
		СтруктураТаблицы = Новый Структура; // Имя поля регистра, номер ячейки
		КлючевоеПоле     = Неопределено;
		СтруктураПолейТаблицы = ВыборкаПрайсЛистов.СтруктураФайлаПрайсЛиста.Выбрать();
		Пока СтруктураПолейТаблицы.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(СтруктураПолейТаблицы.ИмяПоляФайла) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТЗПрайсЛистов.Колонки.Найти(СтруктураПолейТаблицы.ИмяРеквизитаПрайсЛиста) = Неопределено Тогда
				Если СтруктураПолейТаблицы.Ключевое Тогда
					КолонкаПоиска = ШаблонИсточникаДанных.Колонки.Найти(СтруктураПолейТаблицы.ИмяРеквизитаПрайсЛиста);
					//ТЗПрайсЛистов.Колонки.Добавить(КолонкаПоиска.Имя, Новый ОписаниеТипов(КолонкаПоиска.ТипЗначения));
					Если КолонкаПоиска <> Неопределено Тогда
						ТЗПрайсЛистов.Колонки.Добавить(КолонкаПоиска.Имя, КолонкаПоиска.ТипЗначения);
					Иначе
						ТЗПрайсЛистов.Колонки.Добавить(СтруктураПолейТаблицы.ИмяРеквизитаПрайсЛиста);
					КонецЕсли;
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ВыборкаПрайсЛистов.ФайлИсточникДанных Тогда
				Попытка
					Колонка_Источник = Число(СтруктураПолейТаблицы.ИмяПоляФайла);
				Исключение
					Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: Для реквизита <"+СтруктураПолейТаблицы.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные", СтатусСообщения.Важное);
					Продолжить;
				КонецПопытки;
				Если ТипФайла=".ODS" ИЛИ ТипФайла = ".TXT" Тогда
					Колонка_Источник = Колонка_Источник - 1;
				КонецЕсли;
			Иначе
				Колонка_Источник = СтруктураПолейТаблицы.ИмяПоляФайла;
			КонецЕсли;
			
			СтруктураТаблицы.Вставить(СтруктураПолейТаблицы.ИмяРеквизитаПрайсЛиста, Колонка_Источник);
			
			Если СтруктураПолейТаблицы.Ключевое Тогда
				КлючевоеПоле = Колонка_Источник;
			КонецЕсли;
			
		КонецЦикла;
		
		Если СтруктураТаблицы.Количество()=0 Тогда
			Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: У прайс-листа не настроены поля для загрузки файла", СтатусСообщения.Важное);
			Продолжить;
		ИначеЕсли КлючевоеПоле = Неопределено Тогда
			Сообщить("Прайс-лист: <"+ВыборкаПрайсЛистов.Наименование+"> Ошибка: У прайс-листа нет ключевого поля для загрузки файла", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;
		
		КоэффициентПересчета = 0;
		ПересчитыватьЦены    = Ложь;
		Если ВыбиратьВалюту И ЗначениеЗаполнено(ВыборкаПрайсЛистов.Валюта) Тогда
			КоэффициентПересчета = ПерерасчетЦены[ВыборкаПрайсЛистов.Валюта];
			ПересчитыватьЦены    = (ЗначениеЗаполнено(КоэффициентПересчета) И СтруктураТаблицы.Свойство("Цена"));
		КонецЕсли;
		
		Если ВыборкаПрайсЛистов.ФайлИсточникДанных Тогда
			//Грузим Exсel
			Если ТипФайла = ".XLS" ИЛИ ТипФайла = ".XLSX" Тогда 
				ПрочитатьXLS(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены);
			ИначеЕсли ТипФайла = ".TXT" Тогда
				ПрочитатьTXT(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены);
			ИначеЕсли ТипФайла=".ODS" Тогда
				ПрочитатьOpenOffice(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КлючевоеПоле, КоэффициентПересчета, ПересчитыватьЦены);
			Иначе
				Сообщить("Недопустимый тип файла."); Возврат ТЗПрайсЛистов;
			КонецЕсли;
		Иначе //Грузим из ADO
			ПрочитатьADO(ВыборкаПрайсЛистов, ТЗПрайсЛистов, СтруктураТаблицы, КоэффициентПересчета, ПересчитыватьЦены);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТЗПрайсЛистов;
	
КонецФункции // ПолучитьТаблицуИсточникаДанных()


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	ВыводитьДетальныеЗаписи = Истина;
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройкиОтчетаСИсточникомДанных(СтруктураПараметрыЗаполнения, глПрава);
	
	ВалютаПоУмолчанию = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Показатели.Найти("Валюта", "Имя").Использование = Не ВыбиратьВалюту;
	Если ВыбиратьВалюту Тогда
		ВидОтчета = Перечисления.ВидыОтчетов.Остатки;
	Иначе
		ВидОтчета = Перечисления.ВидыОтчетов.Прочий;
	КонецЕсли;
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат, 2, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт
	
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
	
	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;
	
КонецФункции	//	СтруктураФорматированияПолей()

// корректировка таблицы заголовка
Процедура ОбработкаТаблицыЗаголовка(ТаблицаЗаголовка) Экспорт
	
	Если НЕ ВыбиратьВалюту Тогда
		СтрокаПоиска = ТаблицаЗаголовка.Найти("ВалютаОтчета", "ИмяСтроки");
		Если НЕ СтрокаПоиска = Неопределено Тогда
			ТаблицаЗаголовка.Удалить(СтрокаПоиска);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();
#КонецЕсли


ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ЗаказНарядТовары.Ссылка КАК ЗаказНаряд,
	ЗаказНарядТовары.Номенклатура КАК НоменклатураРабота,
	СУММА(ЗаказНарядТовары.СуммаВсего) КАК СуммаНоменклатурыОборот
ПОМЕСТИТЬ
	ТаблицаТоваров
ИЗ
	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
ГДЕ
	(ЗаказНарядТовары.Ссылка.ДатаСоздания МЕЖДУ &ДатаНач И &ДатаКон ИЛИ 
	ЗаказНарядТовары.Ссылка.ДатаОкончания МЕЖДУ &ДатаНач И &ДатаКон) И 
	НЕ ЗаказНарядТовары.Ссылка.ПометкаУдаления
{ГДЕ
	ЗаказНарядТовары.Ссылка.Цех.* КАК ЦехЗаказа,
	ЗаказНарядТовары.Ссылка.Организация.* КАК Организация,
	ЗаказНарядТовары.Ссылка.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ЗаказНарядТовары.Ссылка.ВидОплаты.* КАК ВидОплаты,
	ЗаказНарядТовары.Ссылка.ВидРемонта.* КАК ВидРемонта,
	ЗаказНарядТовары.Ссылка.* КАК ЗаказНаряд,
	ЗаказНарядТовары.Номенклатура.* КАК Номенклатура,
	(ЗНАЧЕНИЕ(Справочник.Автоработы.ПустаяСсылка)).* КАК Работа,
	ЗаказНарядТовары.Ссылка.Автомобиль.* КАК Автомобиль,
	ЗаказНарядТовары.Ссылка.Автомобиль.ГодВыпуска КАК ГодВыпуска
}
СГРУППИРОВАТЬ ПО
	ЗаказНарядТовары.Ссылка,
	ЗаказНарядТовары.Номенклатура
ИНДЕКСИРОВАТЬ ПО
	ЗаказНаряд,
	НоменклатураРабота
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ЗаказНарядРаботы.Ссылка КАК ЗаказНаряд,
	ЗаказНарядРаботы.Ссылка.Цех КАК ПроизводственныйЦех,
	ЗаказНарядИсполнители.Цех КАК РабочийЦех,
	ЗаказНарядРаботы.Работа КАК НоменклатураРабота,
	ЗаказНарядИсполнители.Исполнитель КАК Исполнитель,
	ЗаказНарядРаботы.Количество*(ЗаказНарядИсполнители.Процент/100) КАК КоличествоОборот,
	ВЫБОР
		КОГДА ЗаказНарядРаботы.Нормочас.Цена = 1 ТОГДА
			0
		ИНАЧЕ
			ЗаказНарядРаботы.Количество*ЗаказНарядРаботы.Коэффициент*(ЗаказНарядИсполнители.Процент/100)
	КОНЕЦ КАК КоличествоНормочасовОборот,
	ВЫБОР
		КОГДА ЗаказНарядРаботы.Ссылка.КурсВалютыУпр = 0 ТОГДА
			0
		ИНАЧЕ
			(ЗаказНарядРаботы.СуммаВсего * ЗаказНарядРаботы.Ссылка.КурсДокумента/ЗаказНарядРаботы.Ссылка.КурсВалютыУпр)*(ЗаказНарядИсполнители.Процент/100)
	КОНЕЦ КАК СуммаРаботОборот
ПОМЕСТИТЬ
	ТаблицаРабот
ИЗ
	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	Документ.ЗаказНаряд.Исполнители КАК ЗаказНарядИсполнители
ПО
	ЗаказНарядРаботы.Ссылка = ЗаказНарядИсполнители.Ссылка И 
	ЗаказНарядРаботы.ИдентификаторРаботы = ЗаказНарядИсполнители.ИдентификаторРаботы
ГДЕ
	(ЗаказНарядРаботы.Ссылка.ДатаСоздания МЕЖДУ &ДатаНач И &ДатаКон ИЛИ 
	ЗаказНарядРаботы.Ссылка.ДатаОкончания МЕЖДУ &ДатаНач И &ДатаКон) И 
	НЕ ЗаказНарядРаботы.Ссылка.ПометкаУдаления И 
	НЕ ЗаказНарядРаботы.Ссылка.Проведен
{ГДЕ
	ЗаказНарядРаботы.Ссылка.Цех.* КАК ЦехЗаказа,
	ЗаказНарядРаботы.Ссылка.Организация.* КАК Организация,
	ЗаказНарядРаботы.Ссылка.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ЗаказНарядРаботы.Ссылка.ВидОплаты.* КАК ВидОплаты,
	ЗаказНарядРаботы.Ссылка.ВидРемонта.* КАК ВидРемонта,
	ЗаказНарядРаботы.Ссылка.* КАК ЗаказНаряд,
	ЗаказНарядРаботы.Работа.* КАК Работа,
	(ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)).* КАК Номенклатура,
	ЗаказНарядРаботы.Ссылка.Автомобиль.* КАК Автомобиль,
	ЗаказНарядРаботы.Ссылка.Автомобиль.ГодВыпуска КАК ГодВыпуска,
	ЗаказНарядИсполнители.Исполнитель.* КАК Исполнитель
}
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
#Область ИзмеренияПостроителяДляСвойств
	ТаблицаЗаказНаряд.Ссылка КАК ЗаказНаряд,
	ТаблицаЗаказНаряд.Автомобиль КАК Автомобиль,
	ТаблицаЗаказНаряд.Организация КАК Организация,
	ТаблицаЗаказНаряд.ПодразделениеКомпании КАК ПодразделениеКомпании,
	ТаблицаЗаказНаряд.Цех КАК ЦехЗаказа,
	ЕСТЬNULL(ТаблицыЗаказНарядов.РабочийЦех, ТаблицаЗаказНаряд.Цех) КАК РабочийЦех,
	ЕСТЬNULL(ТаблицыЗаказНарядов.ПроизводственныйЦех, ТаблицаЗаказНаряд.Цех) КАК ПроизводственныйЦех,
	ТаблицыЗаказНарядов.НоменклатураРабота КАК НоменклатураРабота,
	ТаблицыЗаказНарядов.Исполнитель КАК Исполнитель,
#КонецОбласти
	ТаблицыЗаказНарядов.ТабличнаяЧасть КАК ТабличнаяЧасть,
	ТаблицаЗаказНаряд.Автомобиль.ГодВыпуска КАК ГодВыпуска,
	ТаблицаЗаказНаряд.ВидРемонта КАК ВидРемонта,
	ТаблицаЗаказНаряд.ВидОплаты КАК ВидОплаты,
	ТаблицыЗаказНарядов.КоличествоОборот КАК КоличествоОборот,
	ТаблицыЗаказНарядов.КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
	ТаблицыЗаказНарядов.СуммаРаботОборот КАК СуммаРаботОборот,
	ТаблицыЗаказНарядов.СуммаНоменклатурыОборот  КАК СуммаНоменклатурыОборот,
	
	НачалоПериода(ТаблицаЗаказНаряд.Дата, День) КАК ПериодДень,
	НачалоПериода(ТаблицаЗаказНаряд.Дата, Неделя) КАК ПериодНеделя,
	НачалоПериода(ТаблицаЗаказНаряд.Дата, Месяц) КАК ПериодМесяц,
	НачалоПериода(ТаблицаЗаказНаряд.Дата, Квартал) КАК ПериодКвартал,
	НачалоПериода(ТаблицаЗаказНаряд.Дата, Год) КАК ПериодГод
	
//СВОЙСТВА

{ВЫБРАТЬ
	ЗаказНаряд.* КАК ЗаказНаряд,
	Автомобиль.* КАК Автомобиль,
	ГодВыпуска КАК ГодВыпуска,
	Организация.* КАК Организация,
	Исполнитель.* КАК Исполнитель,
	ТабличнаяЧасть КАК ТабличнаяЧасть,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ПроизводственныйЦех.* КАК ПроизводственныйЦех,
	РабочийЦех.* КАК РабочийЦех,
	ЦехЗаказа.* КАК ЦехЗаказа,
	НоменклатураРабота.* КАК НоменклатураРабота,
	ВидРемонта.* КАК ВидРемонта,
	ВидОплаты.* КАК ВидОплаты,
	КоличествоОборот КАК КоличествоОборот,
	КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
	СуммаРаботОборот КАК СуммаРаботОборот,
	СуммаНоменклатурыОборот КАК СуммаНоменклатурыОборот,
	ПериодДень КАК ПериодДень,
	ПериодНеделя КАК ПериодНеделя,
	ПериодМесяц КАК ПериодМесяц,
	ПериодКвартал КАК ПериодКвартал,
	ПериодГод КАК ПериодГод
	//СВОЙСТВА
}
ИЗ
	Документ.ЗаказНаряд КАК ТаблицаЗаказНаряд
ЛЕВОЕ СОЕДИНЕНИЕ
	(ВЫБРАТЬ
		ТаблицаТоваров.ЗаказНаряд КАК ЗаказНаряд,
		ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыВПроизводстве.СкладКомпании КАК Справочник.Цеха), ТаблицаТоваров.ЗаказНаряд.Цех) КАК ПроизводственныйЦех,
		ТаблицаТоваров.ЗаказНаряд.Цех КАК РабочийЦех,
		"Детали" КАК ТабличнаяЧасть,
		ЕСТЬNULL(ТоварыВПроизводстве.Номенклатура, ТаблицаТоваров.НоменклатураРабота) КАК НоменклатураРабота,
		ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Исполнитель,
		0 КАК КоличествоОборот,
		0 КАК КоличествоНормочасовОборот,
		0 КАК СуммаРаботОборот,
		ЕСТЬNULL(ТоварыВПроизводстве.СуммаУпрОборот, ВЫБОР КОГДА ТаблицаТоваров.ЗаказНаряд.КурсВалютыУпр = 0 ТОГДА 0 ИНАЧЕ ТаблицаТоваров.СуммаНоменклатурыОборот * ТаблицаТоваров.ЗаказНаряд.КурсДокумента/ТаблицаТоваров.ЗаказНаряд.КурсВалютыУпр КОНЕЦ) КАК СуммаНоменклатурыОборот,
		ЗНАЧЕНИЕ(Справочник.Автоработы.ПустаяСсылка) КАК Работа,
		ЕСТЬNULL(ТоварыВПроизводстве.Номенклатура, ТаблицаТоваров.НоменклатураРабота) КАК Номенклатура
	{ВЫБРАТЬ
		ПроизводственныйЦех КАК ПроизводственныйЦех,
		РабочийЦех КАК РабочийЦех,
		Исполнитель КАК Исполнитель,
		КоличествоОборот КАК КоличествоОборот,
		КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
		СуммаРаботОборот КАК СуммаРаботОборот,
		СуммаНоменклатурыОборот КАК СуммаНоменклатурыОборот
	}
	ИЗ
		ТаблицаТоваров КАК ТаблицаТоваров
	ЛЕВОЕ СОЕДИНЕНИЕ
		РегистрНакопления.Продажи.Обороты(&ДатаНач, &ДатаКон, Регистратор, Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		{Номенклатура.* КАК Номенклатура, (ЗНАЧЕНИЕ(Справочник.Автоработы.ПустаяСсылка)).* КАК Работа}) КАК ТоварыВПроизводстве
	ПО
		ТаблицаТоваров.ЗаказНаряд = ТоварыВПроизводстве.ДокументПродажи И 
		ТаблицаТоваров.НоменклатураРабота = ТоварыВПроизводстве.Номенклатура
		
	ОБЪЕДИНИТЬ ВСЕ
		
	ВЫБРАТЬ
		ВыработкаСотрудников.ДокументПродажи КАК ЗаказНаряд,
		ВыработкаСотрудников.ДокументПродажи.Цех КАК ПроизводственныйЦех,
		ВыработкаСотрудников.Цех КАК РабочийЦех,
		"Автоработы" КАК ТабличнаяЧасть,
		ВыработкаСотрудников.Работа КАК НоменклатураРабота,
		ВыработкаСотрудников.Сотрудник КАК Исполнитель,
		ВыработкаСотрудников.КоличествоОборот КАК КоличествоОборот,
		ВыработкаСотрудников.КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
		ВыработкаСотрудников.СуммаУпрСоСкидкойОборот КАК СуммаРаботОборот,
		0 КАК СуммаНоменклатурыОборот,
		ВыработкаСотрудников.Работа КАК Работа,
		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура
	{ВЫБРАТЬ
		ПроизводственныйЦех КАК ПроизводственныйЦех,
		РабочийЦех КАК РабочийЦех,
		Исполнитель КАК Исполнитель,
		КоличествоОборот КАК КоличествоОборот,
		КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
		СуммаРаботОборот КАК СуммаРаботОборот,
		СуммаНоменклатурыОборот КАК СуммаНоменклатурыОборот
	}
	ИЗ
		РегистрНакопления.ВыработкаСотрудников.Обороты(&ДатаНач, &ДатаКон, Регистратор, 
		{Работа.* КАК Работа,
		(ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)).* КАК Номенклатура,
		 Сотрудник.* КАК Исполнитель,
		 ВидРемонта.* КАК ВидРемонта}) КАК ВыработкаСотрудников
	{ГДЕ
		ВыработкаСотрудников.ДокументПродажи.Организация.* КАК Организация,
		ВыработкаСотрудников.ДокументПродажи.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
		ВыработкаСотрудников.ДокументПродажи.ВидОплаты.* КАК ВидОплаты,
		ВыработкаСотрудников.ДокументПродажи.Цех.* КАК ЦехЗаказа,
		ВЫРАЗИТЬ(ВыработкаСотрудников.ДокументПродажи КАК Документ.ЗаказНаряд).* КАК ЗаказНаряд,
		ВыработкаСотрудников.ВидРемонта.* КАК ВидРемонта,
		ВыработкаСотрудников.Сотрудник.* КАК Исполнитель,
		ВыработкаСотрудников.Работа КАК Работа,
		ВыработкаСотрудников.Автомобиль.* КАК Автомобиль,
		ВыработкаСотрудников.Автомобиль.ГодВыпуска КАК ГодВыпуска
	}
		
	ОБЪЕДИНИТЬ ВСЕ
		
	ВЫБРАТЬ
		ТаблицаРабот.ЗаказНаряд КАК ЗаказНаряд,
		ТаблицаРабот.ПроизводственныйЦех КАК ПроизводственныйЦех,
		ТаблицаРабот.РабочийЦех КАК РабочийЦех,
		"Автоработы" КАК ТабличнаяЧасть,
		ТаблицаРабот.НоменклатураРабота КАК НоменклатураРабота,
		ТаблицаРабот.Исполнитель КАК Исполнитель,
		ТаблицаРабот.КоличествоОборот КАК КоличествоОборот,
		ТаблицаРабот.КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
		ТаблицаРабот.СуммаРаботОборот КАК СуммаРаботОборот,
		0 КАК СуммаНоменклатурыОборот,
		ТаблицаРабот.НоменклатураРабота КАК Работа,
		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура
		
	{ВЫБРАТЬ
		ПроизводственныйЦех КАК ПроизводственныйЦех,
		РабочийЦех КАК РабочийЦех,
		Исполнитель КАК Исполнитель,
		КоличествоОборот КАК КоличествоОборот,
		КоличествоНормочасовОборот КАК КоличествоНормочасовОборот,
		СуммаРаботОборот КАК СуммаРаботОборот,
		СуммаНоменклатурыОборот КАК СуммаНоменклатурыОборот
	}
		
	ИЗ
		ТаблицаРабот КАК ТаблицаРабот) КАК ТаблицыЗаказНарядов
ПО
	ТаблицаЗаказНаряд.Ссылка = ТаблицыЗаказНарядов.ЗаказНаряд
	//СОЕДИНЕНИЯ
	
ГДЕ
	(ТаблицаЗаказНаряд.ДатаСоздания МЕЖДУ &ДатаНач И &ДатаКон
	ИЛИ ТаблицаЗаказНаряд.ДатаОкончания МЕЖДУ &ДатаНач И &ДатаКон)
	И НЕ ТаблицаЗаказНаряд.ПометкаУдаления
	
{ГДЕ
	ТаблицаЗаказНаряд.Организация.* КАК Организация,
	ТаблицаЗаказНаряд.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ТаблицаЗаказНаряд.ВидОплаты.* КАК ВидОплаты,
	ТаблицаЗаказНаряд.ВидРемонта.* КАК ВидРемонта,
	ТаблицаЗаказНаряд.Ссылка.* КАК ЗаказНаряд,
	ТаблицыЗаказНарядов.Исполнитель.* КАК Исполнитель,
	ТаблицаЗаказНаряд.Цех.* КАК ЦехЗаказа,
	ТаблицаЗаказНаряд.Автомобиль.* КАК Автомобиль,
	ТаблицыЗаказНарядов.Номенклатура.* КАК Номенклатура,
	ТаблицыЗаказНарядов.Работа.* КАК Работа,
	ТаблицаЗаказНаряд.Автомобиль.ГодВыпуска КАК ГодВыпуска
	//СВОЙСТВА
	
}
{УПОРЯДОЧИТЬ ПО 
	Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ВидРемонта.* КАК ВидРемонта,
	НоменклатураРабота.* КАК НоменклатураРабота,
	ЦехЗаказа.* КАК ЦехЗаказа,
	ТабличнаяЧасть КАК ТабличнаяЧасть,
	ПроизводственныйЦех.* КАК ПроизводственныйЦех,
	РабочийЦех.* КАК РабочийЦех,
	ЗаказНаряд.ВидОплаты.* КАК ВидОплаты,
	ЗаказНаряд.* КАК ЗаказНаряд,
	Исполнитель.* КАК Исполнитель,
	ГодВыпуска КАК ГодВыпуска,
	ПериодДень КАК ПериодДень,
	ПериодНеделя КАК ПериодНеделя,
	ПериодМесяц КАК ПериодМесяц,
	ПериодКвартал КАК ПериодКвартал,
	ПериодГод КАК ПериодГод
	
	//СВОЙСТВА
}
{ИТОГИ ПО 
	Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ВидРемонта.* КАК ВидРемонта,
	НоменклатураРабота.* КАК НоменклатураРабота,
	ЦехЗаказа.* КАК ЦехЗаказа,
	ТабличнаяЧасть КАК ТабличнаяЧасть,
	ПроизводственныйЦех.* КАК ПроизводственныйЦех,
	РабочийЦех.* КАК РабочийЦех,
	ВидОплаты.* КАК ВидОплаты,
	ЗаказНаряд.* КАК ЗаказНаряд,
	Автомобиль.* КАК Автомобиль,
	Исполнитель.* КАК Исполнитель,
	
	ГодВыпуска КАК ГодВыпуска,
	ПериодДень КАК ПериодДень,
	ПериодНеделя КАК ПериодНеделя,
	ПериодМесяц КАК ПериодМесяц,
	ПериодКвартал КАК ПериодКвартал,
	ПериодГод КАК ПериодГод
	
	//СВОЙСТВА
}
ИТОГИ 
	СУММА(КоличествоНормочасовОборот),
	СУММА(КоличествоОборот),
	СУММА(СуммаРаботОборот),
	СУММА(СуммаНоменклатурыОборот)
	
	//ИТОГИСВОЙСТВА
	
ПО
	ОБЩИЕ
	
АВТОУПОРЯДОЧИВАНИЕ


ВЫБРАТЬ РАЗРЕШЕННЫЕ
	СУММА(ОбъединенныйЗапрос.КоличествоПланОборот)       КАК КоличествоПлановое,
	СУММА(ОбъединенныйЗапрос.СуммаРегПланОборот)         КАК СуммаРегПлановое,
	СУММА(ОбъединенныйЗапрос.СуммаУпрПланОборот)         КАК СуммаУпрПлановое,
	СУММА(ОбъединенныйЗапрос.СуммаНДСПланОборот)         КАК СуммаНДСПлановое,
	
	СУММА(ОбъединенныйЗапрос.КоличествоФактОборот)       КАК КоличествоФактическое,
	СУММА(ОбъединенныйЗапрос.СуммаРегФактОборот)         КАК СуммаРегФактическое,
	СУММА(ОбъединенныйЗапрос.СуммаУпрФактОборот)         КАК СуммаУпрФактическое,
	СУММА(ОбъединенныйЗапрос.СуммаНДСФактОборот)         КАК СуммаНДСФактическое,
	
	СУММА(ОбъединенныйЗапрос.КоличествоФактОборот       - ОбъединенныйЗапрос.КоличествоПланОборот)       КАК КоличествоОтклонение,
	СУММА(ОбъединенныйЗапрос.СуммаРегФактОборот         - ОбъединенныйЗапрос.СуммаРегПланОборот)         КАК СуммаРегОтклонение,
	СУММА(ОбъединенныйЗапрос.СуммаУпрФактОборот         - ОбъединенныйЗапрос.СуммаУпрПланОборот)         КАК СуммаУпрОтклонение,
	СУММА(ОбъединенныйЗапрос.СуммаНДСФактОборот         - ОбъединенныйЗапрос.СуммаНДСПланОборот)         КАК СуммаНДСОтклонение,
	
	0 КАК КоличествоПроцентОтклонения,
	0 КАК СуммаРегПроцентОтклонения,
	0 КАК СуммаУпрПроцентОтклонения,
	0 КАК СуммаНДСПроцентОтклонения

	//СВОЙСТВА

{ВЫБРАТЬ 
#Область ИзмеренияПостроителяДляСвойств
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ВариантКомплектации.* КАК ВариантКомплектации,
	Модель.* КАК Модель,
#КонецОбласти	
	Период КАК Период
	//СВОЙСТВА}
    
ИЗ 
	(
	 ВЫБРАТЬ
	 	БюджетЗакупокОбороты.ПодразделениеКомпании КАК ПодразделениеКомпании,
	 	БюджетЗакупокОбороты.Модель КАК Модель,
	 	БюджетЗакупокОбороты.ВариантКомплектации КАК ВариантКомплектации,
	 	БюджетЗакупокОбороты.Период КАК Период,
	 	
	 	БюджетЗакупокОбороты.КоличествоОборот       КАК КоличествоПланОборот, 
	 	БюджетЗакупокОбороты.СуммаРегОборот         КАК СуммаРегПланОборот,   
	 	БюджетЗакупокОбороты.СуммаУпрОборот         КАК СуммаУпрПланОборот,   
	 	БюджетЗакупокОбороты.СуммаНДСОборот         КАК СуммаНДСПланОборот,   
	 	
	 	0 КАК КоличествоФактОборот, 
	 	0 КАК СуммаРегФактОборот,  
	 	0 КАК СуммаУпрФактОборот,  
	 	0 КАК СуммаНДСФактОборот
	
	 ИЗ
	 РегистрНакопления.БюджетЗакупокАвтомобилей.Обороты(&ДатаНач, &ДатаКон, Месяц, СценарийПланирования = &СценарийПланирования{
		ПодразделениеКомпании.Организация.* КАК Организация, 
		ПодразделениеКомпании.* КАК ПодразделениеКомпании, 
		Модель.* КАК Модель,
		ВариантКомплектации.* КАК ВариантКомплектации,
		ХозОперация.* КАК ХозОперация
		}) КАК БюджетЗакупокОбороты
	   
	 ОБЪЕДИНИТЬ ВСЕ
	 
	 ВЫБРАТЬ
	  	ОстаткиАвтомобилей.СкладКомпании.Подразделение,
	  	ОстаткиАвтомобилей.Автомобиль.Модель,
	 	ОстаткиАвтомобилей.Автомобиль.ВариантКомплектации,
		ОстаткиАвтомобилей.Период,
		
	 	0,
	 	0,
	 	0,
	 	0,
	 	
	 	ОстаткиАвтомобилей.КоличествоПриход,                  
	 	ОстаткиАвтомобилей.СуммаПриход,
	 	ОстаткиАвтомобилей.СуммаУпрПриход,
	 	ОстаткиАвтомобилей.СуммаНДСПриход
	 	
	 ИЗ
	 РегистрНакопления.ОстаткиАвтомобилей.Обороты(&ДатаНач, &ДатаКон, Месяц, {
		СкладКомпании.Подразделение.Организация.* КАК Организация, 
		СкладКомпании.Подразделение.*             КАК ПодразделениеКомпании, 
	 	Автомобиль.Модель.* КАК Модель,
	 	Автомобиль.ВариантКомплектации.* КАК ВариантКомплектации}) КАК ОстаткиАвтомобилей 
	 	
	 ГДЕ
	 	ОстаткиАвтомобилей.КоличествоПриход > 0
	 	
	) КАК ОбъединенныйЗапрос
//СОЕДИНЕНИЯ
	
СГРУППИРОВАТЬ ПО
	ОбъединенныйЗапрос.ПодразделениеКомпании,
	ОбъединенныйЗапрос.ВариантКомплектации,
	ОбъединенныйЗапрос.Модель,
	ОбъединенныйЗапрос.Период
	//СГРУППИРОВАТЬПО
	
 ИМЕЮЩИЕ
	&ВыводитьВсеЗаписи ИЛИ СУММА(ОбъединенныйЗапрос.КоличествоПланОборот)>0
	     	    
{ГДЕ 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Модель.* КАК Модель,
	ВариантКомплектации.* КАК ВариантКомплектации
	 //СВОЙСТВА}
    
{УПОРЯДОЧИТЬ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Модель.* КАК Модель,
	ВариантКомплектации.* КАК ВариантКомплектации,
	Период КАК Период
	 //СВОЙСТВА}
    
{ИТОГИ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Модель.* КАК Модель,
	ВариантКомплектации.* КАК ВариантКомплектации,
	Период КАК Период
    //СВОЙСТВА}
    
ИТОГИ  
	СУММА(КоличествоПлановое), 
	СУММА(СуммаРегПлановое), 
	СУММА(СуммаУпрПлановое), 
	СУММА(СуммаНДСПлановое), 
	
	СУММА(КоличествоФактическое), 
	СУММА(СуммаРегФактическое), 
	СУММА(СуммаУпрФактическое), 
	СУММА(СуммаНДСФактическое), 
	
	СУММА(КоличествоОтклонение),
	СУММА(СуммаРегОтклонение), 
	СУММА(СуммаУпрОтклонение), 
	СУММА(СуммаНДСОтклонение), 
	
	ВЫБОР КОГДА СУММА(КоличествоПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(КоличествоОтклонение)/СУММА(КоличествоПлановое) * 100 КОНЕЦ КАК КоличествоПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаРегПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаРегОтклонение) / СУММА(СуммаРегПлановое) * 100 КОНЕЦ КАК СуммаРегПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаУпрПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаУпрОтклонение) / СУММА(СуммаУпрПлановое) * 100 КОНЕЦ КАК СуммаУпрПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаНДСПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаНДСОтклонение) / СУММА(СуммаНДСПлановое) * 100 КОНЕЦ КАК СуммаНДСПроцентОтклонения
	//ИТОГИСВОЙСТВА
	
ПО ОБЩИЕ
				
АВТОУПОРЯДОЧИВАНИЕ

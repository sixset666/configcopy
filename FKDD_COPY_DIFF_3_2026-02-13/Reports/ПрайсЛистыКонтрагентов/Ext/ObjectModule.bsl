
Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("Производитель");
	СтруктураСвойств.Вставить("Контрагент");
	СтруктураСвойств.Вставить("Номенклатура");
	СтруктураСвойств.Вставить("ПрайсЛист");
	
	Возврат СтруктураСвойств;
	
КонецФункции

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Устанавливает основные параметры
//
Процедура УстановитьПараметры()
	
	ПоВалютеОтчета = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ПоВалютеОтчета", КомпоновщикНастроек.Настройки);
	ВалютаОтчета   = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ВалютаОтчета", КомпоновщикНастроек.Настройки);
	ДатаОкончания  = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
	
	Для Каждого Показатель Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
		Если СокрЛП(Показатель.Поле) = "Валюта" Тогда
			Показатель.Использование = (НЕ ПоВалютеОтчета);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаОкончания, Новый Структура("Валюта",ВалютаОтчета));
	КурсВалютыОтчета = КурсВалюты.Курс / ?(КурсВалюты.Кратность = 0,1,КурсВалюты.Кратность);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КурсВалютыОтчета", КурсВалютыОтчета);
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	УстановитьПараметры();
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПараметрПоВалютеОтчета = Новый ПараметрКомпоновкиДанных("ПоВалютеОтчета");
	ПоВалютеОтчета = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрПоВалютеОтчета).Значение;
	ПараметрВалютаОтчета  = Новый ПараметрКомпоновкиДанных("ВалютаОтчета");
	ВалютаОтчета          = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрВалютаОтчета).Значение;
	
	Если ПоВалютеОтчета И обЗначениеНеЗаполнено(ВалютаОтчета) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не указана валюта отчета!";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

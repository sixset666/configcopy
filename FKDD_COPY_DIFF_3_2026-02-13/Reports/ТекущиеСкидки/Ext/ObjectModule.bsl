#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета


//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//////////////////////////////////////////////////////////////////////////////////

// возвращает имя макета функции
Функция ИмяМакетаОтчета() Экспорт
	
	Если СпособФормирования = 1 Тогда
		ИмяМакетаЗапроса = "ТекстЗапросаТолькоОбщие";
	ИначеЕсли СпособФормирования=2 Тогда
		ИмяМакетаЗапроса = "ТекстЗапросаТолькоТоварные";
	Иначе
		ИмяМакетаЗапроса = "ТекстЗапросаВсеСкидки";
	КонецЕсли;
	
	Возврат ИмяМакетаЗапроса;
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ДобавитьДопПоле(ИмяПоля)
	
	Если ПостроительОтчета.ДоступныеПоля.Найти(ИмяПоля)=Неопределено Тогда
		Возврат;
	КонецЕсли;
	НовоеПоле = ДополнительныеПоля.Добавить();
	НовоеПоле.Использование = Истина;
	НовоеПоле.Имя = ИмяПоля;
	Параметры = ПараметрыИспользованияПолейИПоказателей[ИмяПоля];
	НовоеПоле.Представление = Параметры.Представление;
	НовоеПоле.ФорматнаяСтрока = Параметры.Формат;
	НовоеПоле.ШиринаКолонки = Параметры.ШиринаКолонки;
	
КонецПроцедуры

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ДополнительныеПоля.Очистить();
	ДобавитьДопПоле("ИдентификаторСкидки");
	ДобавитьДопПоле("СпособВыбора");
	ДобавитьДопПоле("СпособВычисления");
	ДобавитьДопПоле("ФлагВытеснения");
	ДобавитьДопПоле("ЭтоПодарок");
	ДобавитьДопПоле("ВидПрайсЛистаПодарка");
	ДобавитьДопПоле("ДействуетОтСуммыЧека");
	ДобавитьДопПоле("ДействуетОтСуммыСтроки");
	ДобавитьДопПоле("ПравилоРасчетаСкидкиПоКоличеству");
	ДобавитьДопПоле("ДействуетОтКоличества");
	ДобавитьДопПоле("ДействуетОтСуммыНакопленияНаКарте");
	ДобавитьДопПоле("Свойство");
	
	ПостроительОтчета.ДоступныеПоля.ЭтоПодарок.ТипЗначения=Новый ОписаниеТипов("Булево");
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку на табличный документ 
//						или поле табличного документа
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт

	ПостроительОтчета.УсловноеОформление.Очистить();
	ГруппировкаПоКарточке = ИзмеренияСтроки.Найти("ДисконтнаяКарта","ПутьКДанным");
	Если ГруппировкаПоКарточке<>Неопределено И ГруппировкаПоКарточке.Использование Тогда	
		ЭлементОформления = ПостроительОтчета.УсловноеОформление.Добавить("ДисконтнаяКарта");
			
		ОбластьОформления = ЭлементОформления.Область.Добавить("ДисконтнаяКарта");
		ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
			
		ОтборОформления = ЭлементОформления.Отбор.Добавить("ДисконтнаяКарта");
		ОтборОформления.ВидСравнения = ВидСравнения.Равно;
		ОтборОформления.Использование = Истина;
			
		ЭлементОформления.Оформление.Текст.Значение = "Без карточки";
		ЭлементОформления.Оформление.Текст.Использование = Истина;	
		ЭлементОформления.Использование = Истина;  
	КонецЕсли;
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
		
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// возвращает макет построителя отчета
//
Функция ПолучитьМакетПостроителя(ТекПостроительОтчета, МакетОтчета, ПараметрыОформления) Экспорт
	
	ТекНастройки = ТекПостроительОтчета.ПолучитьНастройки();
	
	ТекстОтбора="";                
	СтруктураОтбора = Новый Структура("ИдентификаторСкидки, СпособВычисления, ФлагВытеснения, ВидПрайсЛистаПодарка");
	Для Каждого ТекПоле Из ИзмеренияСтроки Цикл
		Если ТекПоле.Использование И НЕ СтруктураОтбора.Свойство(ТекПоле.ПутьКДанным) Тогда
			ТекстОтбора=ТекстОтбора+?(ТекстОтбора="",""," И "+Символы.ПС)+"ТаблицаСкидок."+ТекПоле.ПутьКДанным+"=ТаблицаОтбораСкидок."+ТекПоле.ПутьКДанным;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекПоле Из ДополнительныеПоля Цикл
		Если ТекПоле.Использование Тогда
			ТекстОтбора=ТекстОтбора+?(ТекстОтбора="",""," И "+Символы.ПС)+"ТаблицаСкидок."+ТекПоле.Имя+"=ТаблицаОтбораСкидок."+ТекПоле.Имя;
		КонецЕсли;
	КонецЦикла;
	
	ТекПостроительОтчета.Текст = СтрЗаменить(ТекПостроительОтчета.Текст,"//@ТекстОтбора@","ГДЕ
	|	ТаблицаСкидок.ЗначениеСкидки В (ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗначениеСкидки КАК ЗначениеСкидки
	|ИЗ
	|	ТаблицаСкидок КАК ТаблицаОтбораСкидок
	|ГДЕ
	|	"+ТекстОтбора+"
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеСкидки УБЫВ)");
	ТекПостроительОтчета.УстановитьНастройки(ТекНастройки);
	ТекПостроительОтчета.ВыводитьДетальныеЗаписи=Истина;
	
	Для Каждого ТекПоле Из ИзмеренияСтроки Цикл
		Если ТекПоле.Использование Тогда
			ИзмерениеПостроителя = ТекПостроительОтчета.ИзмеренияСтроки.Найти(СтрЗаменить(ТекПоле.ПутьКДанным,".",""));
			ИзмерениеПостроителя.Представление = ТекПоле.Представление;
		КонецЕсли;
		ТекстОтбора=ТекстОтбора+?(ТекстОтбора="",""," И "+Символы.ПС)+"ТаблицаСкидок."+ТекПоле.ПутьКДанным+"=ТаблицаОтбораСкидок."+ТекПоле.ПутьКДанным;
	КонецЦикла;
	
	СтруктураНеСвязанныхПоказателей.Очистить();
	СтруктураПоказателей = Новый Структура;
	Для Каждого ТекПоле Из ДополнительныеПоля Цикл
		Если НЕ ТекПоле.Использование Тогда Продолжить; КонецЕсли;
		ИмяТекПоля = ТекПоле.Имя;
		СтруктураПоказателей.Вставить(ИмяТекПоля);
		ТекПостроительОтчета.ВыбранныеПоля.Добавить(ИмяТекПоля, ИмяТекПоля);
		ТекВыбранноеПоле = ДополнительныеПоляОтчета.Добавить();
		ТекВыбранноеПоле.Имя = ИмяТекПоля;
		ТекВыбранноеПоле.ПутьКДанным = ИмяТекПоля;
		ТекВыбранноеПоле.ПолноеПредставление = ТекПоле.Представление;
		ТекВыбранноеПоле.ШиринаКолонки = ТекПоле.ШиринаКолонки;
		ТекВыбранноеПоле.Формат = ТекПоле.ФорматнаяСтрока;
		ТекВыбранноеПоле.НеСвязанные = Истина;
		ТекВыбранноеПоле.ВыводитьИтогПоИерархии = Ложь;
		ТекВыбранноеПоле.ВыводитьИтогДляВсехУровней = Ложь;
		ТекВыбранноеПоле.ВыводитьОбщийИтог = Ложь;
	КонецЦикла;
	СтруктураНеСвязанныхПоказателей.Вставить(ТекПостроительОтчета.ИзмеренияСтроки[ТекПостроительОтчета.ИзмеренияСтроки.Количество()-1].Имя,Новый Структура("Показатели", СтруктураПоказателей));
	
	Возврат Ложь;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

//ИмяРегистра = "ОстаткиТоваровКомпании";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
#КонецЕсли
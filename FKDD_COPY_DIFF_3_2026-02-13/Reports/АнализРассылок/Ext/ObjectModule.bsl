#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранти текст запроса
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // содержит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // структура не связанных полей
Перем ДополнительныеПоляОтчета Экспорт;                // дополнительные поля отчета

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Формирует список допустимых значений вида сравнения
Функция ПолучитьСписокВидовСравнения(ТекТипЗначения) Экспорт
	
	ВидыСравнения = Новый ТаблицаЗначений;
	ВидыСравнения.Колонки.Добавить("ВидСравнения");
	ВидыСравнения.Колонки.Добавить("ЧислоВида");
	
	Для Каждого ТекТип Из ТекТипЗначения.Типы() Цикл
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.Равно;
		ТекСтрока.ЧислоВида    = 1;
		
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.НеРавно;
		ТекСтрока.ЧислоВида    = 1;
		
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.ВСписке;
		ТекСтрока.ЧислоВида    = 1;
		
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.НеВСписке;
		ТекСтрока.ЧислоВида    = 1;
		
		Если Справочники.ТипВсеСсылки().СодержитТип(ТекТип) И Метаданные.НайтиПоТипу(ТекТип).Иерархический Тогда
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ВСпискеПоИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ВИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.НеВИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
		ИначеЕсли ТекТип = Тип("Число") ИЛИ ТекТип = Тип("Строка") ИЛИ ТекТип = Тип("Дата") Тогда
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Больше;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.БольшеИлиРавно;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Меньше;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.МеньшеИлиРавно;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Интервал;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ИнтервалВключаяНачало;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ИнтервалВключаяОкончание;
			ТекСтрока.ЧислоВида    = 1;
		КонецЕсли;
		
		Если ТекТип = Тип("Строка") Тогда
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Содержит;
			ТекСтрока.ЧислоВида    = 1;
		КонецЕсли;
	КонецЦикла;
	
	ВидыСравнения.Свернуть("ВидСравнения", "ЧислоВида");
	
	КоличествоТипов = ТекТипЗначения.Типы().Количество();
	
	СписокВидовСравнения = Новый СписокЗначений;
	Для Каждого ТекСтрока Из ВидыСравнения Цикл
		Если ТекСтрока.ЧислоВида = КоличествоТипов Тогда
			СписокВидовСравнения.Добавить(ТекСтрока.ВидСравнения);
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат СписокВидовСравнения;
	
КонецФункции // ПолучитьСписокВидовСравнения()

// Стандартная процедура настройки построителя отчета 
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	
	ДатаНачалаОбращения	= ДатаНачала;
	ДатаКонцаОбращения	=  ДобавитьМесяц(ДатаКонца,+1);
	
	ПостроительОтчета.Параметры.Вставить("ДатаНачалаОбращения",	ДатаНачалаОбращения);
	ПостроительОтчета.Параметры.Вставить("ДатаКонцаОбращения",	ДатаКонцаОбращения);
	
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	//Для Каждого ТекИзмерение Из ИзмеренияСтроки Цикл
	//	Если ТекИзмерение.ПутьКДанным="Клиент" Тогда
	//		ТекИзмерение.Использование= Ложь;	
	//	КонецЕсли;
	//КонецЦикла;
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт

	ПостроительОтчета.Параметры.Вставить("ДатаНачалаОбращения",	ДатаНачалаОбращения);
	ПостроительОтчета.Параметры.Вставить("ДатаКонцаОбращения",	ДатаКонцаОбращения);
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	
	Возврат Результат;
	
КонецФункции

// формирует отчет в виде сводной таблицы
Функция СформироватьСводнуюТаблицу(ДокументРезультат=Неопределено) Экспорт
	
	ПостроительОтчета.Параметры.Вставить("ДатаНачалаОбращения",	ДатаНачалаОбращения);
	ПостроительОтчета.Параметры.Вставить("ДатаКонцаОбращения",	ДатаКонцаОбращения);
	отСформироватьСводнуюТаблицу(ЭтотОбъект, ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции

// формирует диаграмму
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	ПостроительОтчета.Параметры.Вставить("ДатаНачалаОбращения",	ДатаНачалаОбращения);
	ПостроительОтчета.Параметры.Вставить("ДатаКонцаОбращения",	ДатаКонцаОбращения);
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров);
	
	Возврат ПолеДиаграммы;
	
КонецФункции

// формирует отчет в виде сводной диаграммы
Функция СформироватьСводнуюДиаграмму(ПолеСводнойДиаграммы=Неопределено) Экспорт
	
	ПостроительОтчета.Параметры.Вставить("ДатаНачалаОбращения",	ДатаНачалаОбращения);
	ПостроительОтчета.Параметры.Вставить("ДатаКонцаОбращения",	ДатаКонцаОбращения);
	отСформироватьСводнуюДиаграмму(ЭтотОбъект, ПолеСводнойДиаграммы);
	
	Возврат ПолеСводнойДиаграммы;
	
КонецФункции

// Функция возвращает структуру форматирования полей
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// обработка расшифровки отчета
//
Функция ТабличныйДокументОбработкаРасшифровки(ЭтаФорма, Элемент, Расшифровка, СтандартнаяОбработка) Экспорт
	ОтчетОбъект	= ЭтотОбъект;
	НомерСтроки            = Элемент.ТекущаяОбласть.Низ;
	РасшифровкаГруппировки = Элемент.Область(НомерСтроки, 1, НомерСтроки, 1).Расшифровка;
	ПараметрыГруппировки   = Элемент.Область(НомерСтроки, 2, НомерСтроки, 2).Расшифровка;
	ИмяГруппировки         = Неопределено;
	ЗначениеГруппировки    = Неопределено;
	ИмяПоля                = Неопределено;
	ЗначениеПоля           = Неопределено;
	ПутьТекущейРасшифровки = Неопределено;
	ЭтоНовыйСтиль          = Ложь;
	
	Если ТипЗнч(РасшифровкаГруппировки) = Тип("Структура") И ТипЗнч(ПараметрыГруппировки ) = Тип("Структура") Тогда
		
		ИмяГруппировки         = ПараметрыГруппировки.ИмяГруппировки;
		ЗначениеГруппировки    = Элемент.Область(НомерСтроки, ПараметрыГруппировки.Колонка, НомерСтроки, ПараметрыГруппировки.Колонка).Расшифровка;
		// если текущая группировка это детальная запись, то она не попадет в структуру расшифровок
		// добавим ее в расшифровку.
		Если ПараметрыГруппировки.ДетальнаяЗапись Тогда
			РасшифровкаГруппировки.Вставить(ПараметрыГруппировки.ИмяГруппировки, ЗначениеГруппировки);
		КонецЕсли;
		
		РасшифровкаШапки = Элемент.Область(Элемент.ФиксацияСверху, Элемент.ТекущаяОбласть.Лево).Расшифровка;
		ЭтоГруппировка = (РасшифровкаШапки = Неопределено);
		Если ЭтоГруппировка Тогда
			ПутьТекущейРасшифровки = ПараметрыГруппировки.ПутьКДанным;
			ИмяПоля                = ИмяГруппировки;
			ЗначениеПоля           = ЗначениеГруппировки;
		Иначе
			ПутьТекущейРасшифровки = РасшифровкаШапки.ПутьКДанным;
			ИмяПоля                = РасшифровкаШапки.ИмяПоля;
			ЗначениеПоля           = Расшифровка;
		КонецЕсли;
		
		ЭтоНовыйСтиль = Истина;
		
		Расшифровка                = РасшифровкаГруппировки;
		
	КонецЕсли;
		
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Расшифровка)) Тогда
		СтандартнаяОбработка=Ложь;
		Варианты=Новый СписокЗначений();
		Варианты.Добавить(1,"Открыть документ");
		Варианты.Добавить(2,"Найти в журнале");
		Варианты.Добавить(10,"Распечатать документ");
		Варианты.Добавить(11,"Показать дерево связей документа");
		Варианты.Добавить(12,"Ввести на основании");
		Выб=ЭтаФорма.ВыбратьИзМеню(Варианты);
		Если Выб=Неопределено Тогда
		Иначе
			Если Выб.Значение=10 Тогда
				ПечатныеФормы = дкПолучитьСписокПечатныхФорм(Расшифровка, Расшифровка.Метаданные().Имя, Ложь);
				СписокФорм=Новый СписокЗначений();
				
				Для Каждого ТекМакет Из ПечатныеФормы Цикл
					СписокФорм.Добавить(ТекМакет.Ключ, ТекМакет.Значение);
				КонецЦикла;
				
				// пользователь выбирает
				Форма = ЭтаФорма.ВыбратьИзМеню(СписокФорм);
				
				Если НЕ Форма=Неопределено Тогда
					СохранитьЗначение(СокрЛП(Расшифровка.Метаданные().Имя)+"ПечатнаяФорма",Форма.Представление);
					Расшифровка.ПолучитьОбъект().Печать(Форма.Представление);
				КонецЕсли;
			ИначеЕсли Выб.Значение=1 Тогда
				СтандартнаяОбработка=Истина;
			ИначеЕсли Выб.Значение=2 Тогда
				СписокДокументов=Документы[Расшифровка.Метаданные().Имя].ПолучитьФормуСписка();
				СписокДокументов.ПараметрТекущаяСтрока=Расшифровка;
				СписокДокументов.Открыть();
			ИначеЕсли Выб.Значение=11 Тогда
			
				дкДействияФормыДеревоДокумента(ЭтаФорма, Расшифровка.Ссылка);
				
			ИначеЕсли Выб.Значение=12 Тогда
				МетаданныеРасшифровки = Расшифровка.Метаданные();
				СписокПодчиненных = Новый СписокЗначений();
				Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
					Если МетаданныеДокумента.ВводитсяНаОсновании.Содержит(МетаданныеРасшифровки) Тогда
						СписокПодчиненных.Добавить(МетаданныеДокумента.Имя, МетаданныеДокумента.Синоним);
					КонецЕсли;
				КонецЦикла;
				
				Подчиненный = ЭтаФорма.ВыбратьИзМеню(СписокПодчиненных);
				Если НЕ Подчиненный = Неопределено Тогда
					//НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
					НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
					НовыйДокумент.Заполнить(Расшифровка.Ссылка);
					ФормаДокумента = НовыйДокумент.ПолучитьФорму();
					ФормаДокумента.Открыть();
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Расшифровка)) Тогда
		СтандартнаяОбработка=Ложь;
		Варианты=Новый СписокЗначений();
		Варианты.Добавить(1,"Открыть элемент");
		Варианты.Добавить(2,"Найти в справочнике");
		Выб=ЭтаФорма.ВыбратьИзМеню(Варианты);
		Если Выб=Неопределено Тогда
		Иначе
			Если Выб.Значение=1 Тогда
				СтандартнаяОбработка=Истина;
			ИначеЕсли Выб.Значение=2 Тогда
				СписокДокументов=Справочники[Расшифровка.Метаданные().Имя].ПолучитьФормуСписка();
				СписокДокументов.ПараметрТекущаяСтрока=Расшифровка;
				СписокДокументов.Открыть();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		
		СтандартнаяОбработка = Ложь;
		Попытка
			// Это расшифровка по выбранной функции или показателю
			Если Расшифровка.Свойство("ПутьКДанным") И Расшифровка.Свойство("ИмяПоля") И (Расшифровка.Свойство("Показатель") ИЛИ Расшифровка.Свойство("Функция")) Тогда
				
				ФормаРасшифровки = Отчеты[ОтчетОбъект.Метаданные().Имя].ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек,);
				Для Каждого ТекРеквизит Из ОтчетОбъект.Метаданные().Реквизиты Цикл
					Если ТекРеквизит.Имя = "ПостроительОтчета" Тогда Продолжить; КонецЕсли;
					ФормаРасшифровки[ТекРеквизит.Имя] = ОтчетОбъект[ТекРеквизит.Имя];
				Конеццикла;
				
				РасшифровкаПоКолонкам = Элемент.Область(Элемент.ВысотаТаблицы-1, Элемент.ТекущаяОбласть.Лево, Элемент.ВысотаТаблицы-1, Элемент.ТекущаяОбласть.Лево).Расшифровка;
				
				РасшифровкаОтчета = Новый Структура;
				Если НЕ РасшифровкаПоКолонкам = Неопределено Тогда
					Для Каждого ТекЭлемент Из РасшифровкаПоКолонкам Цикл
						РасшифровкаОтчета.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
					КонецЦикла;
				КонецЕсли;
				
				ФормаРасшифровки.ЗаполнитьНачальныеНастройки(ФормаРасшифровки.ИмяМакета);
				ФормаРасшифровки.ИзмеренияСтроки.Загрузить(ОтчетОбъект.ИзмеренияСтроки.Выгрузить());
				ФормаРасшифровки.ИзмеренияКолонки.Очистить();
				ФормаРасшифровки.ПостроительОтчета.УстановитьНастройки(ОтчетОбъект.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь), Истина, Ложь, Ложь, Ложь, Ложь);
				ФормаРасшифровки.ПостроительОтчета.НастроитьРасшифровку(ФормаРасшифровки.ПостроительОтчета, РасшифровкаОтчета);
				ФормаРасшифровки.ЗаполнитьНастройки = Ложь;
				
				Попытка // некоторые отчеты могут не содержать в заголовке таблицы расшифровки показателя
					Попытка
						Для Каждого ТекФункция Из ФормаРасшифровки.ДеревоПоказателей.Строки Цикл
							Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
								ТекПоказатель.Использование = ТекПоказатель.ПутьКДанным = Расшифровка.ПутьКДанным;
							КонецЦикла;
						КонецЦикла;
					Исключение
						
						Если Расшифровка.Свойство("Показатель") Тогда
							Для Каждого ТекПоказатель Из ФормаРасшифровки.Показатели Цикл
								ТекПоказатель.Использование = (Расшифровка.Показатель.Имя = ТекПоказатель.Имя);
							КонецЦикла;
						Иначе
							ФормаРасшифровки.Показатели.Загрузить(ОтчетОбъект.Показатели.Выгрузить());
						КонецЕсли;
						
						ИмяФункции = "";
						Если Расшифровка.Свойство("Функция") Тогда
							ИмяФункции = Расшифровка.Функция.Имя;
						КонецЕсли;
						
						Для Каждого ТекФункция Из ФормаРасшифровки.Функции Цикл
							ТекФункция.Использование = (ТекФункция.Имя = ИмяФункции);
						КонецЦикла;
						
					КонецПопытки;
				Исключение КонецПопытки;
				
				к = 0;
				Пока к <= ФормаРасшифровки.ПостроительОтчета.Отбор.Количество() - 1 Цикл
					Если НЕ ФормаРасшифровки.ПостроительОтчета.Отбор[к].Использование Тогда
						ФормаРасшифровки.ПостроительОтчета.Отбор.Удалить(к);
					Иначе
						Попытка
							Если ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.ВИерархии И 
								обЗначениеНеЗаполнено(ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение) Тогда
								ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.Равно;
							ИначеЕсли ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение.ЭтоГруппа Тогда
								ВыбраннаяСтрока = ФормаРасшифровки.ПостроительОтчета.Отбор[к];
								СтрокаПоиска = ФормаРасшифровки.ДеревоДоступныхПолей.Строки.Найти(ВыбраннаяСтрока.ПутьКДанным+".Родитель","ПутьКДанным", Истина);
								Если СтрокаПоиска = Неопределено Тогда 
									к = к + 1;
									Продолжить; 
								КонецЕсли;
								ПутьКДанным = СтрокаПоиска.ПутьКДанным;
								Представление = СтрокаПоиска.Представление;
								Использование = ВыбраннаяСтрока.Использование;
								Значение = ВыбраннаяСтрока.Значение;
								Отборы = ФормаРасшифровки.ПостроительОтчета.Отбор;
								ТекИндекс = Отборы.Индекс(ВыбраннаяСтрока);
								Отборы.Удалить(ТекИндекс);
								
								Попытка
									ТекОтбор = Отборы.Добавить(ПутьКДанным);
									ТекОтбор.Представление = Представление;
									ТекОтбор.Использование = Использование;
									ТекОтбор.ВидСравнения = ВидСравнения.ВИерархии;;
									ТекОтбор.Значение = Значение;
								Исключение 
								КонецПопытки;
								Если Отборы.Количество() - 1 - ТекИндекс > 0 Тогда
									Отборы.Сдвинуть(Отборы.Количество() - 1, -(Отборы.Количество() - 1 - ТекИндекс));
								КонецЕсли;
							КонецЕсли;
						Исключение
						КонецПопытки;
						к = к + 1;
					КонецЕсли;
				КонецЦикла;
				
				отФорматированиеПостроителяОтчета(ФормаРасшифровки);
				отДействияФормыСформировать(ФормаРасшифровки, ФормаРасшифровки.ЭлементыФормы.ДействияФормы.Кнопки.Сформировать);
				
			Иначе
				РасшифровкаОтчета = Новый Структура();
				// Добавим расшифровку строки/колонки
				Попытка // если есть искуственно сделанная иерархия, то она описана в ТаблицаРасшифровокИерархии
					ТаблицаРасшифровокИерархии = ОтчетОбъект.ТаблицаРасшифровокИерархии;
					Для Каждого ТекЭлемент Из Расшифровка Цикл
						ОписаниеРасшифровки = ТаблицаРасшифровокИерархии.Найти(ТекЭлемент.Ключ, "ИмяИерархии");
						ИмяКлюча = ?(ОписаниеРасшифровки=Неопределено, ТекЭлемент.Ключ, ОписаниеРасшифровки.ИмяИзмерения);
						Если Не РасшифровкаОтчета.Свойство(ИмяКлюча) Тогда
							РасшифровкаОтчета.Вставить(ИмяКлюча, ТекЭлемент.Значение);
						КонецЕсли;
					КонецЦикла;
				Исключение
					Для Каждого ТекЭлемент Из Расшифровка Цикл
						РасшифровкаОтчета.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
					КонецЦикла;
				КонецПопытки;
				
				ОтборДетализации = Новый Структура();
				Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияСтроки Цикл
					Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
					ПозицияТочки = Найти(ТекСтрока.ПутьКДанным,".");
					ТекПутьКДанным = ?(ПозицияТочки>0, Лев(ТекСтрока.ПутьКДанным, ПозицияТочки-1), ТекСтрока.ПутьКДанным);
					ОтборДетализации.Вставить(ТекПутьКДанным);
				КонецЦикла;
				
				Для Каждого ТекКолонка Из ОтчетОбъект.ИзмеренияКолонки Цикл
					Если не ТекКолонка.Использование Тогда Продолжить; КонецЕсли;
					ПозицияТочки = Найти(ТекКолонка.ПутьКДанным,".");
					ТекПутьКДанным = ?(ПозицияТочки>0, Лев(ТекКолонка.ПутьКДанным, ПозицияТочки-1), ТекКолонка.ПутьКДанным);
					ОтборДетализации.Вставить(ТекПутьКДанным);
				КонецЦикла;
				
				// Инициализируем список выбора варианта детализации расшифровки
				ВыбДетализацияРасшифровки = "";
				ДетализацияРасшифровки = Новый СписокЗначений();
				
				ЗначениеТекущейГруппировки = Неопределено;
				ТекущийОтбор = Неопределено;
				МетаданныеОтчета = ОтчетОбъект.Метаданные();
				МетаданныеРасшифровки = Неопределено;
				ИмяМетаданныхОбъекта = Неопределено;
				Если ЭтоНовыйСтиль Тогда
					ЗначениеТекущейГруппировки = ЗначениеПоля;
				Иначе
					Попытка
						Если Элемент.Область(Элемент.ФиксацияСверху, Элемент.ТекущаяОбласть.Лево).Расшифровка = Неопределено Тогда
							МассивВключенныхСтрок  = ОтчетОбъект.ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование", Истина));
							ПутьТекущейРасшифровки = МассивВключенныхСтрок[Мин(РасшифровкаОтчета.Количество()-1, МассивВключенныхСтрок.Количество()-1)].ПутьКДанным;
							ИмяТекущейГруппировки  = СтрЗаменить(ПутьТекущейРасшифровки, ".", "");
							Если РасшифровкаОтчета.Свойство(ИмяТекущейГруппировки) Тогда
								ЗначениеТекущейГруппировки = РасшифровкаОтчета[ИмяТекущейГруппировки];
							КонецЕсли;
						КонецЕсли;
					Исключение
					КонецПопытки;
				КонецЕсли;
				
				ТекущаяВетвь = ОтчетОбъект.ДеревоДоступныхПолей.Строки.Найти(ПутьТекущейРасшифровки, "ПутьКДанным", Истина);
				Если (НЕ ТекущаяВетвь = Неопределено) И ТекущаяВетвь.Отбор = Истина И МетаданныеОтчета.Реквизиты.Найти("ПостроительОтчета") <> Неопределено Тогда
					ДетализацияРасшифровки.Добавить(1, "Установить отбор");
					Для Каждого ТекСтрокаОтбора Из ПостроительОтчета.Отбор Цикл
						Если ТекСтрокаОтбора.ПутьКДанным = ПутьТекущейРасшифровки Тогда
							ТекущийОтбор = ТекСтрокаОтбора;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если ТекущийОтбор<>Неопределено И Не обЗначениеНеЗаполнено(ТекущийОтбор.Значение) Тогда
						ДетализацияРасшифровки.Добавить(2, "Добавить в отбор");
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеТекущейГруппировки) Тогда
					Попытка
						МетаданныеРасшифровки = ЗначениеТекущейГруппировки.Метаданные();
						ИмяОбъектаМетаданных = СтрЗаменить(МетаданныеРасшифровки.ПолноеИмя(), "."+МетаданныеРасшифровки.Имя,"");
						Если ИмяОбъектаМетаданных="Задача"
							ИЛИ ИмяОбъектаМетаданных="Справочник"
							ИЛИ ИмяОбъектаМетаданных="ПланСчетов"
							ИЛИ ИмяОбъектаМетаданных="БизнесПроцесс"
							ИЛИ ИмяОбъектаМетаданных="ПланВидовРасчета"
							ИЛИ ИмяОбъектаМетаданных="ПланВидовХарактеристик" Тогда
							ДетализацияРасшифровки.Добавить(3,"Открыть элемент");
							ДетализацияРасшифровки.Добавить(4,"Найти в списке");
						ИначеЕсли ИмяОбъектаМетаданных="Документ" Тогда
							ДетализацияРасшифровки.Добавить(3,"Открыть документ");
							ДетализацияРасшифровки.Добавить(4,"Найти в журнале");
							ДетализацияРасшифровки.Добавить(5,"Распечатать документ");
							ДетализацияРасшифровки.Добавить(6,"Показать дерево связей документа");
							ДетализацияРасшифровки.Добавить(7,"Ввести на основании");
						КонецЕсли;
					Исключение
					КонецПопытки;
					
				КонецЕсли;
				
				Для Каждого ТекПоле Из ОтчетОбъект.ДеревоДоступныхПолей.Строки Цикл
					Если (ОтборДетализации.Свойство(ТекПоле.ПутьКДанным)) ИЛИ (НЕ ТекПоле.Измерение) ИЛИ (НЕ ТекПоле.Отбор) Тогда Продолжить; КонецЕсли;
					Попытка
						Если ОтчетОбъект.СтруктураСвязиСвойств.Свойство(ТекПоле.Имя) Тогда Продолжить; КонецЕсли;
					Исключение КонецПопытки;
					ТекЭлемент = ДетализацияРасшифровки.Добавить(ТекПоле, ТекПоле.Представление);
					Если ТекПоле.Имя = "ПериодРегистратор" Тогда
						ВыбДетализацияРасшифровки = ТекЭлемент;
					КонецЕсли;
				КонецЦикла;
				Если ТипЗнч(ВыбДетализацияРасшифровки) = Тип("ЭлементСпискаЗначений") Тогда
					ДетализацияРасшифровки.Сдвинуть(ВыбДетализацияРасшифровки, ДетализацияРасшифровки.Количество() - ДетализацияРасшифровки.Индекс(ВыбДетализацияРасшифровки) - 1);
				КонецЕсли;
				
				// если необходимо, добавим возможность расшифровки по движениям с остатками
				Попытка // переменная ИмяОтчетаДвиженийСОстатками есть не у всех отчетов
					СтрокаРегистратора = ОтчетОбъект.ДеревоДоступныхПолей.Строки.Найти("ПериодРегистратор", "ПутьКДанным");
					Если СтрокаРегистратора <> Неопределено Тогда
						ИмяОтчетаДвиженийСОстатками = ОтчетОбъект.ИмяОтчетаДвиженийСОстатками;
						ТекЭлемент = ДетализацияРасшифровки.Добавить(СтрокаРегистратора, "Документ движения (с остатками)");
						ВыбДетализацияРасшифровки = ТекЭлемент;
					КонецЕсли;
				Исключение КонецПопытки;
				
				ВыбиратьИзМеню	= Истина;
				
				// Сформируем пункт меню для расшифровки количества событий
				Если ПутьТекущейРасшифровки="КоличествоСобытий" Тогда
					ТекЭлемент					= ДетализацияРасшифровки.Добавить("События", "Расшифровка количества событий");
					ВыбДетализацияРасшифровки	= ТекЭлемент;
					ВыбиратьИзМеню	= Ложь;
				ИначеЕсли ПутьТекущейРасшифровки="СуммаРеализацийТоваров" ИЛИ ПутьТекущейРасшифровки="СуммаЗаказНарядов" Тогда
					ТекЭлемент					= ДетализацияРасшифровки.Добавить("АнализПродаж", "Расшифровка сумм продажи");
					ВыбДетализацияРасшифровки	= ТекЭлемент;
					ВыбиратьИзМеню	= Ложь;
				ИначеЕсли ПутьТекущейРасшифровки="СуммаЗаказаНаАвтомобиль" Тогда
					ТекЭлемент					= ДетализацияРасшифровки.Добавить("ОстаткиИОборотыЗаказовНаАвтомобили", "Расшифровка сумм заказов");
					ВыбДетализацияРасшифровки	= ТекЭлемент;
					ВыбиратьИзМеню	= Ложь;					
				КонецЕсли;
				
				Если ВыбиратьИзМеню Тогда
					ВыбДетализацияРасшифровки = ДетализацияРасшифровки.ВыбратьЭлемент("Выберите вариант расшифровки ...", ВыбДетализацияРасшифровки);
				КонецЕсли;
				
				Если ВыбДетализацияРасшифровки = Неопределено Тогда Возврат Ложь; КонецЕсли;
				
				Если ВыбДетализацияРасшифровки.Значение=1 ИЛИ ВыбДетализацияРасшифровки.Значение=2 Тогда
					Отбор = ОтчетОбъект.ПостроительОтчета.Отбор;
					СчетчикОтбора = 0;
					КоличествоОтборов = Мин(Отбор.Количество() - 1, 2);
					ЕстьМестоДляОтбора = Ложь;
					Пока СчетчикОтбора <= КоличествоОтборов Цикл
						ПолеОтбора = Отбор[СчетчикОтбора];
						Если Не ПолеОтбора.Использование Тогда ЕстьМестоДляОтбора = Истина; Прервать; КонецЕсли;
						СчетчикОтбора = СчетчикОтбора+1;
					КонецЦикла;
					
					Если ТекущийОтбор=Неопределено Тогда
						ТекущийОтбор = Отбор.Добавить(ТекущаяВетвь.ПутьКДанным);
						Представление = ТекущаяВетвь.Представление;
						Если ТекущаяВетвь.Родитель <> Неопределено Тогда
							Если ТекущаяВетвь.Родитель.Имя = "Свойства" Тогда
								Представление = Представление + " (" + ТекущаяВетвь.Родитель.Родитель.Представление + ")";
							Иначе Представление = Представление + " (" + ТекущаяВетвь.Родитель.Представление + ")";
							КонецЕсли;
						КонецЕсли;
						ТекущийОтбор.Представление = Представление;
					КонецЕсли;
					
					ТекущийОтбор.Использование = Истина;
					Если ВыбДетализацияРасшифровки.Значение=1 Тогда
						ТекущийОтбор.ВидСравнения = ВидСравнения.Равно;
						ТекущийОтбор.Значение = ЗначениеТекущейГруппировки;
					Иначе
						Если ТипЗнч(ТекущийОтбор.Значение) = Тип("СписокЗначений") Тогда
							ТекущийОтбор.Значение.Добавить(ЗначениеТекущейГруппировки);
						Иначе
							СписокОтбора = Новый СписокЗначений;
							СписокОтбора.Добавить(ТекущийОтбор.Значение);
							СписокОтбора.Добавить(ЗначениеТекущейГруппировки);
							ТекущийОтбор.ВидСравнения = ВидСравнения.ВСписке;
							ТекущийОтбор.Значение = СписокОтбора;
						КонецЕсли;
					КонецЕсли;
					
					ИндексТекущегоОтбора = Отбор.Индекс(ТекущийОтбор);
					Если ЕстьМестоДляОтбора И ИндексТекущегоОтбора>=СчетчикОтбора Тогда
						Отбор.Сдвинуть(ИндексТекущегоОтбора, -(ИндексТекущегоОтбора-СчетчикОтбора));
					КонецЕсли;
					
					ВысотаПанели = 5;
					Для СчетчикОтбора = 0 По КоличествоОтборов Цикл
						ЭтаФорма.ЭлементыФормы["ПанельФильтра" + (СчетчикОтбора+1)].Свертка = РежимСверткиЭлементаУправления.Нет;	
						ПолеОтбора = Отбор[СчетчикОтбора];
						ПутьОтбора = "ПостроительОтчета.Отбор." + ПолеОтбора.Имя;
						ЭтаФорма.ЭлементыФормы["ИспользованиеОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".Использование";
						ЭтаФорма.ЭлементыФормы["ИспользованиеОтбор" + (СчетчикОтбора + 1)].Заголовок = ПолеОтбора.Представление + ":";
						ЭтаФорма.ЭлементыФормы["ВидСравненияОтбор" + (СчетчикОтбора + 1)].СписокВыбора = ПолучитьСписокВидовСравнения(ПолеОтбора.ТипЗначения);
						ЭтаФорма.ЭлементыФормы["ВидСравненияОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".ВидСравнения";
						ЭтаФорма.ЭлементыФормы["ЗначениеОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".Значение";
						ЭтаФорма.ЭлементыФормы["ДатаНачалаОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".ЗначениеС";
						ЭтаФорма.ЭлементыФормы["ДатаКонцаОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".ЗначениеПо";
						//ВидСравненияОтборПриИзменении(ЭтаФорма.ЭлементыФормы["ВидСравненияОтбор" + (СчетчикОтбора + 1)]);
						ВысотаПанели = ВысотаПанели + 26;
					КонецЦикла;
					
					ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Отбор.Доступность = Истина;
					ЭтаФорма.ЭлементыФормы.ПанельФильтра.Свертка = РежимСверткиЭлементаУправления.Нет;
					ЭтаФорма.ЭлементыФормы.ПанельФильтра.Высота = ВысотаПанели;
					ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Отбор.Пометка = Истина;
					
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=3 Тогда
					ОткрытьЗначение(ЗначениеТекущейГруппировки);
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=4 Тогда
					ФормаСпискаРасшифровки = Неопределено;
					Если ИмяОбъектаМетаданных="Задача" Тогда
						ФормаСпискаРасшифровки=Задачи[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="Справочник" Тогда
						ФормаСпискаРасшифровки=Справочники[ЗначениеТекущейГруппировки.Метаданные().Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="ПланСчетов" Тогда
						ФормаСпискаРасшифровки=ПланыСчетов[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="БизнесПроцесс" Тогда
						ФормаСпискаРасшифровки=БизнесПроцессы[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="ПланВидовРасчета" Тогда
						ФормаСпискаРасшифровки=ПланыВидовРасчета[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="ПланВидовХарактеристик" Тогда
						ФормаСпискаРасшифровки=ПланыВидовХарактеристик[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="Документ" Тогда
						ФормаСпискаРасшифровки=Документы[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					КонецЕсли;
					Если ФормаСпискаРасшифровки<>Неопределено Тогда
						ФормаСпискаРасшифровки.ПараметрТекущаяСтрока=ЗначениеТекущейГруппировки;
						ФормаСпискаРасшифровки.Открыть();
					КонецЕсли;
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=5 Тогда
					ПечатныеФормы = дкПолучитьСписокПечатныхФорм(ЗначениеТекущейГруппировки, МетаданныеРасшифровки.Имя, Ложь);
					СписокФорм = Новый СписокЗначений();
					
					Для Каждого ТекМакет Из ПечатныеФормы Цикл
						СписокФорм.Добавить(ТекМакет.Ключ, ТекМакет.Значение);
					КонецЦикла;
					
					// пользователь выбирает
					Форма = ЭтаФорма.ВыбратьИзМеню(СписокФорм);
					
					Если НЕ Форма=Неопределено Тогда
						СохранитьЗначение(СокрЛП(МетаданныеРасшифровки.Имя)+"ПечатнаяФорма",Форма.Представление);
						ЗначениеТекущейГруппировки.ПолучитьОбъект().Печать(Форма.Представление);
					КонецЕсли;
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=6 Тогда
					
					дкДействияФормыДеревоДокумента(ЭтаФорма, ЗначениеТекущейГруппировки);
					
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=7 Тогда
					//Метаданные.КритерииОтбора.ПодчиненныеДокументы.
					СписокПодчиненных = Новый СписокЗначений();
					Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
						Если МетаданныеДокумента.ВводитсяНаОсновании.Содержит(МетаданныеРасшифровки) Тогда
							СписокПодчиненных.Добавить(МетаданныеДокумента.Имя, МетаданныеДокумента.Синоним);
						КонецЕсли;
					КонецЦикла;
					
					Подчиненный = ЭтаФорма.ВыбратьИзМеню(СписокПодчиненных);
					Если НЕ Подчиненный = Неопределено Тогда
						//НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
						НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
						НовыйДокумент.Заполнить(ЗначениеТекущейГруппировки);
						ФормаДокумента = НовыйДокумент.ПолучитьФорму();
						ФормаДокумента.Открыть();
					КонецЕсли;
					
				Иначе
					
					Если ВыбДетализацияРасшифровки.Представление = "Документ движения (с остатками)" Тогда
						ФормаРасшифровки			= Отчеты[ИмяОтчетаДвиженийСОстатками].ПолучитьФорму("НастройкиДвиженияСОстатками",);
						ФормаРасшифровки.ДатаНачала	= ОтчетОбъект.ДатаНачала;
						ФормаРасшифровки.ДатаКонца	= ОтчетОбъект.ДатаКонца;
						ФормаРасшифровки.ИспользоватьАвтоотступ = ОтчетОбъект.ИспользоватьАвтоотступ;
					ИначеЕсли ВыбДетализацияРасшифровки.Представление = "Расшифровка количества событий" Тогда
						ФормаРасшифровки			= Отчеты.События.ПолучитьФорму("НастройкиОборотыПродаж");
						ФормаРасшифровки.ДатаНачала	= ДатаНачала;
						ФормаРасшифровки.ДатаКонца	= ДатаКонца;
					ИначеЕсли ВыбДетализацияРасшифровки.Представление = "Расшифровка сумм продажи" Тогда
						ФормаРасшифровки			= Отчеты.АнализПродаж.ПолучитьФорму("НастройкиОборотыПродаж");
						ФормаРасшифровки.ДатаНачала	= ДатаНачалаОбращения;
						ФормаРасшифровки.ДатаКонца	= ДатаКонцаОбращения;
					ИначеЕсли ВыбДетализацияРасшифровки.Представление = "Расшифровка сумм заказов" Тогда
						ФормаРасшифровки			= Отчеты.ОстаткиИОборотыЗаказовНаАвтомобили.ПолучитьФорму("НастройкиОстаткиИОбороты");
						ФормаРасшифровки.ДатаНачала	= ДатаНачалаОбращения;
						ФормаРасшифровки.ДатаКонца	= ДатаКонцаОбращения;						
					Иначе
						ФормаРасшифровки = Отчеты[ОтчетОбъект.Метаданные().Имя].ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек,);
						Для Каждого ТекРеквизит Из ОтчетОбъект.Метаданные().Реквизиты Цикл
							Если ТекРеквизит.Имя = "ПостроительОтчета" Тогда Продолжить; КонецЕсли;
							ФормаРасшифровки[ТекРеквизит.Имя] = ОтчетОбъект[ТекРеквизит.Имя];
						Конеццикла;
					КонецЕсли;
					
					ФормаРасшифровки.ЗаполнитьНачальныеНастройки(ФормаРасшифровки.ИмяМакета);
					Если ВыбДетализацияРасшифровки.Представление = "Расшифровка количества событий" ИЛИ
						ВыбДетализацияРасшифровки.Представление = "Расшифровка сумм продажи" ИЛИ
						ВыбДетализацияРасшифровки.Представление = "Расшифровка сумм заказов" Тогда
					Иначе
						Попытка
							ФормаРасшифровки.ИзмеренияСтроки.Очистить();
						Исключение КонецПопытки;
						
						ФормаРасшифровки.Показатели.Загрузить(ОтчетОбъект.Показатели.Выгрузить());
						ФормаРасшифровки.Функции.Загрузить(ОтчетОбъект.Функции.Выгрузить());
						
						Попытка
							ФормаРасшифровки.ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей.Скопировать();
						Исключение КонецПопытки;						
					КонецЕсли;
					
					ФормаРасшифровки.ПостроительОтчета.УстановитьНастройки(ОтчетОбъект.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь), Истина, Ложь, Ложь, Ложь, Ложь);
					ФормаРасшифровки.ПостроительОтчета.НастроитьРасшифровку(ФормаРасшифровки.ПостроительОтчета, РасшифровкаОтчета);
					ФормаРасшифровки.ЗаполнитьНастройки = Ложь;
					
					Попытка // некоторые отчеты могут не содержать в заголовке таблицы расшифровки показателя
						РасшифровкаПоказателя = Элемент.Область(Элемент.ФиксацияСверху, Элемент.ТекущаяОбласть.Лево).Расшифровка.Показатель;
						Попытка
							Для Каждого ТекФункция Из ФормаРасшифровки.ДеревоПоказателей.Строки Цикл
								Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
									ТекПоказатель.Использование = ТекПоказатель.ПутьКДанным = РасшифровкаПоказателя.ИмяРесурса;
								КонецЦикла;
							КонецЦикла;
						Исключение
							Для Каждого ТекПоказатель Из ФормаРасшифровки.Показатели Цикл
								ТекПоказатель.Использование = ТекПоказатель.Имя = РасшифровкаПоказателя.Имя;
							КонецЦикла;
						КонецПопытки;
					Исключение КонецПопытки;	
					
					к = 0;
					Пока к <= ФормаРасшифровки.ПостроительОтчета.Отбор.Количество() - 1 Цикл
						Если НЕ ФормаРасшифровки.ПостроительОтчета.Отбор[к].Использование Тогда
							ФормаРасшифровки.ПостроительОтчета.Отбор.Удалить(к);
						Иначе
							Попытка
								Если ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.ВИерархии И 
									обЗначениеНеЗаполнено(ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение) Тогда
									ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.Равно;
								ИначеЕсли ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение.ЭтоГруппа Тогда
									ВыбраннаяСтрока = ФормаРасшифровки.ПостроительОтчета.Отбор[к];
									СтрокаПоиска = ФормаРасшифровки.ДеревоДоступныхПолей.Строки.Найти(ВыбраннаяСтрока.ПутьКДанным+".Родитель","ПутьКДанным", Истина);
									Если СтрокаПоиска = Неопределено Тогда 
										к = к + 1;
										Продолжить; 
									КонецЕсли;
									ПутьКДанным = СтрокаПоиска.ПутьКДанным;
									Представление = СтрокаПоиска.Представление;
									Использование = ВыбраннаяСтрока.Использование;
									Значение = ВыбраннаяСтрока.Значение;
									Отборы = ФормаРасшифровки.ПостроительОтчета.Отбор;
									ТекИндекс = Отборы.Индекс(ВыбраннаяСтрока);
									Отборы.Удалить(ТекИндекс);
									
									Попытка
										ТекОтбор = Отборы.Добавить(ПутьКДанным);
										ТекОтбор.Представление = Представление;
										ТекОтбор.Использование = Использование;
										ТекОтбор.ВидСравнения = ВидСравнения.ВИерархии;;
										ТекОтбор.Значение = Значение;
									Исключение 
									КонецПопытки;
									Если Отборы.Количество() - 1 - ТекИндекс > 0 Тогда
										Отборы.Сдвинуть(Отборы.Количество() - 1, -(Отборы.Количество() - 1 - ТекИндекс));
									КонецЕсли;
								КонецЕсли;
							Исключение
							КонецПопытки;
							к = к + 1;
						КонецЕсли;
					КонецЦикла;
					
					Если ВыбДетализацияРасшифровки.Представление = "Расшифровка количества событий" ИЛИ
						ВыбДетализацияРасшифровки.Представление = "Расшифровка сумм заказов" Тогда
						Если ТипЗнч(РасшифровкаГруппировки)=Тип("Структура") Тогда
							СтрокаРасшифровки= Неопределено;
							Если РасшифровкаГруппировки.Свойство("Клиент", СтрокаРасшифровки) Тогда
								ЗначениеОтбора	= СтрокаРасшифровки;
								Если НЕ обЗначениеНеЗаполнено(ЗначениеОтбора) Тогда
									НайденнаяСтрокаОтбора	= ФормаРасшифровки.ПостроительОтчета.Отбор.Найти("Контрагент");
									Если НайденнаяСтрокаОтбора=Неопределено Тогда
										ТекОтбор				= ФормаРасшифровки.ПостроительОтчета.Отбор.Добавить("Контрагент");
										ТекОтбор.Представление	= "Контагент";
										ТекОтбор.Использование	= Истина;
										ТекОтбор.ВидСравнения	= ВидСравнения.Равно;;
										ТекОтбор.Значение		= ЗначениеОтбора;									
									Иначе
										НайденнаяСтрокаОтбора.Использование	= Истина;
										НайденнаяСтрокаОтбора.ВидСравнения	= ВидСравнения.Равно;
										НайденнаяСтрокаОтбора.Значение		= ЗначениеОтбора;									
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					ИначеЕсли ВыбДетализацияРасшифровки.Представление = "Расшифровка сумм продажи" Тогда
						Если ТипЗнч(РасшифровкаГруппировки)=Тип("Структура") Тогда
							СтрокаРасшифровки= Неопределено;
							Если РасшифровкаГруппировки.Свойство("Клиент", СтрокаРасшифровки) Тогда
								ЗначениеОтбора	= СтрокаРасшифровки;
								Если НЕ обЗначениеНеЗаполнено(ЗначениеОтбора) Тогда
									НайденнаяСтрокаОтбора	= ФормаРасшифровки.ПостроительОтчета.Отбор.Найти("Контрагент");
									Если НайденнаяСтрокаОтбора=Неопределено Тогда
										ТекОтбор				= ФормаРасшифровки.ПостроительОтчета.Отбор.Добавить("Покупатель");
										ТекОтбор.Представление	= "Покупатель";
										ТекОтбор.Использование	= Истина;
										ТекОтбор.ВидСравнения	= ВидСравнения.Равно;;
										ТекОтбор.Значение		= ЗначениеОтбора;									
									Иначе
										НайденнаяСтрокаОтбора.Использование	= Истина;
										НайденнаяСтрокаОтбора.ВидСравнения	= ВидСравнения.Равно;
										НайденнаяСтрокаОтбора.Значение		= ЗначениеОтбора;									
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;						
					Иначе
						ТекИзмерение = ФормаРасшифровки.ИзмеренияСтроки.Добавить();
						ТекИзмерение.Использование = Истина;
						ТекИзмерение.ПутьКДанным   = ВыбДетализацияРасшифровки.Значение.ПутьКДанным;
						ТекИзмерение.Представление = ВыбДетализацияРасшифровки.Значение.Представление;
						ТекИзмерение.ТипИзмерения  = ТипИзмеренияПостроителяОтчета.Элементы;
						Если ВыбДетализацияРасшифровки.Представление = "Документ движения (с остатками)" Тогда
							ТекИзмерение.Фиксированное = Истина;
						Иначе
							Если ФормаРасшифровки.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
								ТекИзмерение = ФормаРасшифровки.ИзмеренияСтроки.Добавить();
								ТекИзмерение.Использование = Истина;
								ТекИзмерение.ПутьКДанным   = "ПериодРегистратор";
								ТекИзмерение.Представление = "Документ движения";
								ТекИзмерение.ТипИзмерения  = "Элементы";
								ТекИзмерение.Фиксированное = Истина;
							Иначе
								Попытка
									ФормаРасшифровки.ИзмеренияКолонки.Загрузить(ОтчетОбъект.ИзмеренияКолонки.Выгрузить());
									НайденнаяКолонка = ФормаРасшифровки.ИзмеренияКолонки.Найти(ВыбДетализацияРасшифровки.Значение.ПутьКДанным, "ПутьКДанным");
									Если НайденнаяКолонка<>Неопределено Тогда
										ФормаРасшифровки.ИзмеренияКолонки.Удалить(НайденнаяКолонка);
									КонецЕсли;
									
								Исключение КонецПопытки;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					отФорматированиеПостроителяОтчета(ФормаРасшифровки);
					отДействияФормыСформировать(ФормаРасшифровки, ФормаРасшифровки.ЭлементыФормы.ДействияФормы.Кнопки.Сформировать);
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Строка") ИЛИ ТипЗнч(Расшифровка) = Тип("Дата") ИЛИ ТипЗнч(Расшифровка) = Тип("Число") ИЛИ Расшифровка=NULL Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
		//ОбработкаРасшифровкиКомпоновкиДанных(Расшифровка, СтандартнаяОбработка, ЭтотОбъект, ЭтаФорма);
		
		// Запретим стандартную обработку расшифровки
		СтандартнаяОбработка = Ложь;
		
		// Создадим и инициализируем обработчик расшифровки
		ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ОтчетОбъект.ДанныеРасшифровки, 
		Новый ИсточникДоступныхНастроекКомпоновкиДанных(ОтчетОбъект.СхемаКомпоновкиДанных));
		
		// Осуществим выбор действия расшифровки пользователем
		ДоступныеДействияРасшифровки = Неопределено;
		Попытка
			ДоступныеДействияРасшифровки = ОтчетОбъект.ДоступныеДействияРасшифровки;
		Исключение
		КонецПопытки;
		ВыполненноеДействие = Неопределено;
		Настройки = ОбработкаРасшифровки.Выполнить(Расшифровка, ВыполненноеДействие, ДоступныеДействияРасшифровки);
		
		Если Настройки <> Неопределено Тогда
			// Пользователь выбрал действие, для которого нужно менять настройки
			
			Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Упорядочить Тогда
				// Если требуется упорядочить - упорядочим в текущем отчете
				ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
				ОтчетОбъект.СформироватьТабличныйДокумент(ЭтаФорма.ЭлементыФормы.ТабличныйДокумент);
				
			Иначе
				
				НовыйОтчет = Отчеты[ОтчетОбъект.Метаданные().Имя].Создать();
				Попытка
					НовыйОтчет.ЗаполнитьНачальныеНастройки();
				Исключение
				КонецПопытки;
				ЗаполнитьЗначенияСвойств(НовыйОтчет, ОтчетОбъект, , "СохраненныеНастройки");
				
				ФормаНовогоОтчета = НовыйОтчет.ПолучитьФорму();
				ФормаНовогоОтчета.ЗаполнитьНастройки = Ложь;
				НовыйОтчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
				НовыйОтчет.ИнициализироватьКомпоновщикНастроек(ФормаНовогоОтчета);
				отДействияФормыСформировать(ФормаНовогоОтчета, ФормаНовогоОтчета.ЭлементыФормы.ДействияФормы.Кнопки.Сформировать);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецФункции	//	ТабличныйДокументОбработкаРасшифровки()

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя	= Новый Структура();
СтруктураНеСвязанныхПоказателей		= Новый Структура();
#КонецЕсли
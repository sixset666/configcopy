
Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("Контрагент");
	СтруктураСвойств.Вставить("Заказ");
	СтруктураСвойств.Вставить("Номенклатура");
	СтруктураСвойств.Вставить("Регистратор");
	
	Возврат СтруктураСвойств;
	
КонецФункции

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

//Устанавливает параметр построителя отчета "СписокЗаказов"
//
//Без параметров
//
Процедура УстановитьПараметры() Экспорт
	
	//РежимВывода
	//Выводить все
	//Не выводить закрытые заказы
	//Выводить заказы поступившие но не отгруженные
	
	Запрос = Новый Запрос;
	
	ПоляОграничения = Новый Массив;
	ПоляОграничения.Добавить("Контрагент");
	ПоляОграничения.Добавить("Заказ");
	ПоляОграничения.Добавить("Номенклатура");
	ПоляОграничения.Добавить("ХарактеристикаНоменклатуры");
	
	ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор;
	
	ТекстОтбора = ОтчетыСервер.КомпоновщикПолучитьТекстОтбора(ОтборКомпоновки.Элементы, ОтборКомпоновки.ДоступныеПоляОтбора, "ЗаказыПокупателей", Запрос.Параметры,, ПоляОграничения);
	ТекстОтбора = ?(ТекстОтбора = "", "", " И " + Символы.ПС + ТекстОтбора);
	
	ДатаНач = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаНачала",    КомпоновщикНастроек.Настройки);
	ДатаКон = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
	
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	
	РежимВывода = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("РежимВывода", КомпоновщикНастроек.Настройки);
	
	Если РежимВывода = 0 Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПокупателей.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора +"
		|";
	ИначеЕсли РежимВывода = 3 Тогда
		
		ТекстПараметров = ОтчетыСервер.КомпоновщикПолучитьТекстОтбора(ОтборКомпоновки.Элементы, ОтборКомпоновки.ДоступныеПоляОтбора, "", Запрос.Параметры,, ПоляОграничения);
		ТекстПараметров = ?(ТекстПараметров = "", "", " И " + Символы.ПС + ТекстПараметров);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПокупателей.Заказ КАК Заказ
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора +"
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ
		|	ВложенныЙЗапрос.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателейОстатки.Заказ КАК Заказ,
		|		ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		ВЫБОР
		|			КОГДА ЗаказыПокупателейОстатки.ЗаказаноОстаток = ЗаказыПокупателейОстатки.РезервОстаток ТОГДА
		|				1
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СовпадаетЗаказИРезерв
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей.Остатки(&ДатаКонГраница,
		|		Заказ В (ВЫБРАТЬ Заказ ИЗ ТаблицаЗаказов)" + ТекстПараметров + ") КАК ЗаказыПокупателейОстатки
		|	ГДЕ
		|		ЗаказыПокупателейОстатки.ЗаказаноОстаток>0) КАК ВложенныЙЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныЙЗапрос.Заказ
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ВложенныЙЗапрос.СовпадаетЗаказИРезерв)>0
		|";
		
		ДатаКон = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
		
	Иначе
		
		Если РежимВывода = 1 Тогда
			СтрокаОтбора = "(НЕ ЗаказыПокупателейОстатки.ЗаказаноОстаток = 0)";
		Иначе
			СтрокаОтбора = "(НЕ ЗаказыПокупателейОстатки.РезервОстаток = 0)";
		КонецЕсли;
		
		ТекстПараметров = ОтчетыСервер.КомпоновщикПолучитьТекстОтбора(ОтборКомпоновки.Элементы, ОтборКомпоновки.ДоступныеПоляОтбора, "", Запрос.Параметры,, ПоляОграничения);
		ТекстПараметров = ?(ТекстПараметров = "", "", " И " + Символы.ПС + ТекстПараметров);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПокупателей.Заказ КАК Заказ
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора +"
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&ДатаКонГраница,
		|	Заказ В (ВЫБРАТЬ Заказ ИЗ ТаблицаЗаказов)" + ТекстПараметров + ") КАК ЗаказыПокупателейОстатки
		|ГДЕ
		|	"+СтрокаОтбора+"
		|";
		
		ДатаКон = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
		
	КонецЕсли;
	
	СписокЗаказов = Новый СписокЗначений;
	СписокЗаказов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ"));
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", СписокЗаказов);
	
КонецПроцедуры // УстановитьПараметры()

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	УстановитьПараметры();
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

//ПоследовательностиОтчета = Новый ТаблицаЗначений();
//ПоследовательностиОтчета.Колонки.Добавить("ИмяГраницы");
//ПоследовательностиОтчета.Колонки.Добавить("Представление");
//ПоследовательностиОтчета.Колонки.Добавить("РегистрыНакопления");
//ПоследовательностиОтчета.Колонки.Добавить("Измерения");

//НоваяСтрока = ПоследовательностиОтчета.Добавить();
//НоваяСтрока.ИмяГраницы = "ГраницыЗаказы";
//НоваяСтрока.Представление = "Заказы";
//СтруктураРегистровНакопления = Новый Структура();
//СтруктураРегистровНакопления.Вставить("ЗаказыПокупателей",Новый Структура("Заказ","Заказ"));
//СтруктураРегистровНакопления.Вставить("ЗаказыРаспределение",Новый Структура("Заказ","ЗаказПокупателя"));
//НоваяСтрока.РегистрыНакопления = СтруктураРегистровНакопления;
//МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("Заказ");
//НоваяСтрока.Измерения = МассивИзмерений;

#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// проверка на использование группировки
Функция ИспользуетсяГруппировка(ИмяГруппировки) Экспорт
	Если ИзмеренияСтроки.Найти(ИмяГруппировки)<>Неопределено Тогда
		Возврат ИзмеренияСтроки.Найти(ИмяГруппировки).Использование;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

// Стандартная процедура настройки построителя отчета 
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ПостроительОтчета.УсловноеОформление.Очистить();
         
    ЭлементОформления = ПостроительОтчета.УсловноеОформление.Добавить("Код");
         
    ОбластьОформления = ЭлементОформления.Область.Добавить("Дата");
    ОбластьОформления = ЭлементОформления.Область.Добавить("Пользователь");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Оборудование");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Источник");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Объект");
	ОбластьОформления = ЭлементОформления.Область.Добавить("GUID");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Действие");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Код");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Описание");
	ОбластьОформления = ЭлементОформления.Область.Добавить("Тип");
         
	ОтборОформления = ЭлементОформления.Отбор.Добавить("Тип");
	ОтборОформления.ВидСравнения = ВидСравнения.Равно;
	ОтборОформления.Значение = Истина;
	ОтборОформления.Использование = Истина;
         
    ЭлементОформления.Оформление.ЦветТекста.Значение = WebЦвета.Красный;
    ЭлементОформления.Оформление.ЦветТекста.Использование = Истина;     
    ЭлементОформления.Использование = Истина;
	
	ВыводитьДетальныеЗаписи = Истина;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Если НЕ обПраво("РазрешитьДоступКЖурналуДействийКассира", глПрава) Тогда
		Предупреждение("Доступ к журналу регистрации действий кассира запрещен!");
		Возврат ДокументРезультат;
	КонецЕсли;
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат, 0, глПрава);
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает структуру форматирования полей
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// проверка заполнения
Функция КорректностьЗаполнения(ПродолжитьФормирование) Экспорт
	
	ПродолжитьФормирование = Истина;
	Возврат Истина;
	
КонецФункции

// устанавливает флаг использование у измерений
Функция УстановитьИспользованиеИзмерений(ТекПостроитель) Экспорт
	
	ТекПостроитель.ВыбранныеПоля.Очистить();
	ТекПостроитель.Порядок.Очистить();

	Для Каждого ТекСтрока Из ДеревоДоступныхПолей.Строки Цикл
		ИмяПоля = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "");
		Если ТекПостроитель.ВыбранныеПоля.Найти(ИмяПоля) = Неопределено Тогда
			ТекПостроитель.ВыбранныеПоля.Добавить(ТекСтрока.ПутьКДанным, ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	ТекПостроитель.Порядок.Добавить("Дата", "Дата", "Дата", НаправлениеСортировки.Возр);
	
	Возврат Истина;
	
КонецФункции

// возвращает макет построителя отчета
Функция ПолучитьМакетПостроителя(ТекПостроитель, МакетОтчета, ПараметрыОформления) Экспорт
	
	ГраницаМакета = ПараметрыОформления.ГраницаМакета;
	ГраницаМакета.Вставить("ГраницаКолонок", ГраницаМакета.ДопПоля);
	ГраницаМакета.Вставить("ГраницаКолонокСтрока", ГраницаМакета.ДопПоляСтрока);
	ГраницаМакета.Вставить("ГраницаТаблицы", Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа", ГраницаМакета.Измерения.ГраницаСлева, ГраницаМакета.Измерения.ГраницаСверху, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева));
	ГраницаМакета.Вставить("ГраницаТаблицыСтрока", Новый Структура("ГраницаСлева, ГраницаСправа", ГраницаМакета.ИзмерениеСтрока.ГраницаСлева, ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСправа));
	
	ОбластьЗаголовок = МакетОтчета.Область(1, 2);
	ОбластьЗаголовок.Текст = "Лог фронта кассира";
	ОбластьЗаголовок.Шрифт = Новый Шрифт(ОбластьЗаголовок.Шрифт,, 12, Истина);
	
	ОбластьЗаголовок = МакетОтчета.Область(2, 2);
	ОбластьЗаголовок.Текст = "Отбор: " + ПолучитьТекстОтбора();
	ОбластьЗаголовок.Шрифт = Новый Шрифт(ОбластьЗаголовок.Шрифт,, 10, Истина);
	ОбластьЗаголовок.ЦветТекста = ЦветаСтиля.ЦветФонаВыделенияПоля;
	
	ОбластьЗаголовок = МакетОтчета.Область(3, 2);
	
	ТаблицаКолонок = Новый ТаблицаЗначений;
	ТаблицаКолонок.Колонки.Добавить("Представление");
	ТаблицаКолонок.Колонки.Добавить("Имя");
	ТаблицаКолонок.Колонки.Добавить("ШиринаКолонки");
	ТаблицаКолонок.Колонки.Добавить("Формат");
	
	Для Каждого ТекПоле Из ТекПостроитель.ВыбранныеПоля Цикл
		НоваяСтрока = ТаблицаКолонок.Добавить();
		НоваяСтрока.Имя             = ТекПоле.Имя;
		
		ТекПараметрыПоля = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекПоле.Имя, ПараметрыИспользованияПолейИПоказателей);
		Если ТекПараметрыПоля=Неопределено Тогда
			НоваяСтрока.Представление = ТекПоле.Имя;
			НоваяСтрока.ШиринаКолонки = 20;
			НоваяСтрока.Формат            = "";
		Иначе
			НоваяСтрока.Представление = ТекПараметрыПоля.Представление;
			НоваяСтрока.ШиринаКолонки = ТекПараметрыПоля.ШиринаКолонки;
			НоваяСтрока.Формат        = ТекПараметрыПоля.Формат;
		КонецЕсли;
	КонецЦикла; 
	
	отПолучитьМакетДетальныхЗаписейПостроителя(МакетОтчета, ПараметрыОформления, ТаблицаКолонок);
	
	Возврат Истина;
	
КонецФункции 

// возвращает текст отбора
Функция ПолучитьТекстОтбора()
	// Текст отборов
	Если ПостроительОтчета.Отбор.Количество() = 0 Тогда
		Отбор = "Без отбора";
	Иначе
		Отбор = "";
		Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
			Если ТекОтбор.Использование И НЕ ТекОтбор.Имя = "Периодичность" Тогда
				ЗначОтбора="";
				Если ТипЗнч(ТекОтбор.Значение)=Тип("СписокЗначений") Тогда
					Для Каждого ТекЗначОтбор Из ТекОтбор.Значение Цикл
						ЗначОтбора = ЗначОтбора + ?(ЗначОтбора = "", "", "; ") + ТекЗначОтбор.Значение;
					КонецЦикла;
				ИначеЕсли ТекОтбор.Имя = "Дата" Тогда
					ВидСравненияВСтроку = Строка(ТекОтбор.ВидСравнения);
					ЗначОтбора = ВидСравненияВСтроку + " " + ?(Найти(ВидСравненияВСтроку, "Интервал") = 0, ТекОтбор.Значение, Строка(ТекОтбор.ЗначениеС) + " - " + Строка(ТекОтбор.ЗначениеПо));
				Иначе
					ЗначОтбора=ТекОтбор.Значение;
				КонецЕсли;
				Отбор = Отбор + ТекОтбор.Представление + ": " + ЗначОтбора + "," + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
			
		Если Отбор = "" Тогда Отбор = "Все"; КонецЕсли; 
	КонецЕсли; 
	Возврат Отбор;
КонецФункции


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
#КонецЕсли
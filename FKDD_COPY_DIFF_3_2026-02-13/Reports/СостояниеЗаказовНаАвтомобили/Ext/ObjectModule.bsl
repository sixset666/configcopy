
Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("Контрагент");
	СтруктураСвойств.Вставить("ДоговорВзаиморасчетов");
	СтруктураСвойств.Вставить("Заказ");
	СтруктураСвойств.Вставить("Автомобиль");
	СтруктураСвойств.Вставить("Модель");
	
	Возврат СтруктураСвойств;
	
КонецФункции

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

Функция ПолучитьОсновныеПараметры() Экспорт 
	
	СтруктураОсновныхПараметров = Новый Структура;
	СтруктураОсновныхПараметров.Вставить("ВыводитьЗакрытыеЗаказы");
	
	Возврат СтруктураОсновныхПараметров;
	
КонецФункции

// Установить параметры отчета
Процедура УстановитьПараметры()
	
	Запрос = Новый Запрос;
	
	ПоляОграничения = Новый Массив;
	ПоляОграничения.Добавить("Контрагент");
	ПоляОграничения.Добавить("ДоговорВзаиморасчетов");
	ПоляОграничения.Добавить("Заказ");
	ПоляОграничения.Добавить("Автомобиль");
	ПоляОграничения.Добавить("Модель");
	ПоляОграничения.Добавить("ВариантКомплектации");
	
	СоответствиеЗамен = Новый Соответствие;
	СоответствиеЗамен.Вставить("Контрагент",            "Заказ.Контрагент");
	СоответствиеЗамен.Вставить("ДоговорВзаиморасчетов", "Заказ.ДоговорВзаиморасчетов");
	СоответствиеЗамен.Вставить("Модель",                "Автомобиль.Модель");
	СоответствиеЗамен.Вставить("ВариантКомплектации",   "Автомобиль.ВариантКомплектации");
	
	ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор;
	
	ТекстОтбора = ОтчетыСервер.КомпоновщикПолучитьТекстОтбора(ОтборКомпоновки.Элементы, ОтборКомпоновки.ДоступныеПоляОтбора, "ЗаказыАвтомобилей", Запрос.Параметры, СоответствиеЗамен, ПоляОграничения);
	ТекстОтбора = ?(ТекстОтбора = "", "", " И " + Символы.ПС + ТекстОтбора);
	
	ДатаНач = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаНачала",    КомпоновщикНастроек.Настройки);
	ДатаКон = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
	
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	
	ВыводитьЗакрытыеЗаказы = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ВыводитьЗакрытыеЗаказы", КомпоновщикНастроек.Настройки);
	
	Если ВыводитьЗакрытыеЗаказы Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыАвтомобилей.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
		|ГДЕ
		|	ЗаказыАвтомобилей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора + "
		|";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыАвтомобилей.Заказ КАК Заказ
		|{ВЫБРАТЬ
		|	Заказ КАК Заказ}
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
		|ГДЕ
		|	ЗаказыАвтомобилей.Период МЕЖДУ &ДатаНач И &ДатаКон" + ТекстОтбора + "
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказыАвтомобилейОстатки.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(&ДатаКонГраница, Заказ В (ВЫБРАТЬ Заказ ИЗ ТаблицаЗаказов)) КАК ЗаказыАвтомобилейОстатки
		|ГДЕ
		|	(НЕ ЗаказыАвтомобилейОстатки.КоличествоОстаток = 0)
		|";
		
		ДатаКон = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
	КонецЕсли;
	
	СписокЗаказов = Новый СписокЗначений;
	СписокЗаказов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ"));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", СписокЗаказов);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	УстановитьПараметры();
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры
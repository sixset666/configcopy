// Получим выбору интервалов времени
ВЫБРАТЬ
	РегистрацияВремениРабот.Сотрудник,
	РегистрацияВремениРабот.ЗаказНаряд,
	РегистрацияВремениРабот.РабочееМесто,
	РегистрацияВремениРабот.ПакетРабот,
	РегистрацияВремениРабот.Период,
	РегистрацияВремениРабот.КонецРаботы,
	РегистрацияВремениРабот.ВидОтметки,
	РегистрацияВремениРабот.Продолжительность
ПОМЕСТИТЬ ТаблицаПоПакетам
ИЗ
	РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот
ГДЕ
	РегистрацияВремениРабот.Период МЕЖДУ &ДатаНач И &ДатаКон
	И РегистрацияВремениРабот.ЗаказНаряд <> ЗНАЧЕНИЕ(Документ.ЗаказНаряд.ПустаяСсылка)
СГРУППИРОВАТЬ ПО
	РегистрацияВремениРабот.РабочееМесто,
	РегистрацияВремениРабот.ЗаказНаряд,
	РегистрацияВремениРабот.Сотрудник,
	РегистрацияВремениРабот.ПакетРабот,
	РегистрацияВремениРабот.Период,
	РегистрацияВремениРабот.ВидОтметки,
	РегистрацияВремениРабот.КонецРаботы,
	РегистрацияВремениРабот.Продолжительность
;
// получим работы из ЗН
ВЫБРАТЬ
	ЗаказНарядРаботы.ПакетРабот,
	ЗаказНарядРаботы.Работа,
	ЗаказНарядРаботы.ПакетЗакрыт
ПОМЕСТИТЬ ТаблицаРабот
ИЗ
	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
ГДЕ
	ЗаказНарядРаботы.Ссылка В
	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		Табл.ЗаказНаряд
	ИЗ
		ТаблицаПоПакетам КАК Табл)
СГРУППИРОВАТЬ ПО
	ЗаказНарядРаботы.Работа,
	ЗаказНарядРаботы.ПакетРабот,
	ЗаказНарядРаботы.ПакетЗакрыт
;

// получми поледние интервалы по пакетам
ВЫБРАТЬ
	ТаблицаПоПакетам.Сотрудник,
	ТаблицаПоПакетам.ЗаказНаряд,
	ТаблицаПоПакетам.ПакетРабот,
	МАКСИМУМ(ТаблицаПоПакетам.КонецРаботы)  КАК КонецРаботы
ПОМЕСТИТЬ ТаблицаМаксимумов
ИЗ
	ТаблицаПоПакетам КАК ТаблицаПоПакетам
ГДЕ
	ТаблицаПоПакетам.ВидОтметки = ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)
СГРУППИРОВАТЬ ПО
	ТаблицаПоПакетам.Сотрудник,
	ТаблицаПоПакетам.ЗаказНаряд,
	ТаблицаПоПакетам.ПакетРабот
;

// получим количество работ в пакете
ВЫБРАТЬ
	ТаблицаРабот.ПакетРабот,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Таблицаработ.Работа) КАК КоличствоРаботВПакете
ПОМЕСТИТЬ ТаблицаДолей
ИЗ
	ТаблицаРабот КАК ТаблицаРабот
СГРУППИРОВАТЬ ПО
	ТаблицаРабот.ПакетРабот
;

ВЫБРАТЬ
#Область ИзмеренияПостроителяДляСвойств
	ТаблицаПоПакетам.Сотрудник    КАК Сотрудник,
	ТаблицаПоПакетам.ЗаказНаряд   КАК ЗаказНаряд,
	ТаблицаПоПакетам.РабочееМесто КАК РабочееМесто,
	ТаблицаРабот.Работа           КАК Работа,
	ВЫБОР
		КОГДА (ТаблицаПоПакетам.КонецРаботы = ТаблицаМаксимумов.КонецРаботы) И (ТаблицаРабот.ПакетЗакрыт = ИСТИНА) ТОГДА
			"Окончание работы"
		ИНАЧЕ
			ТаблицаПоПакетам.ВидОтметки  
	КОНЕЦ КАК ВидОтметки,
	ТаблицаПоПакетам.Период КАК Период,
#КонецОбласти
	МАКСИМУМ(ТаблицаПоПакетам.КонецРаботы)  КАК КонецРаботы,
	СУММА(ТаблицаПоПакетам.Продолжительность/ЕСТЬNULL(ТаблицаДолей.КоличствоРаботВПакете,1)) КАК Продолжительность
{
ВЫБРАТЬ
	Сотрудник.*    КАК Сотрудник,
	ЗаказНаряд.*   КАК ЗаказНаряд,
	РабочееМесто.* КАК РабочееМесто,
	Работа.*       КАК Работа,
	ВидОтметки.*   КАК ВидОтметки,
	Период КАК Период
}
ИЗ
	ТаблицаПоПакетам КАК ТаблицаПоПакетам
ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРабот КАК ТаблицаРабот
ПО
	ТаблицаПоПакетам.ПакетРабот = ТаблицаРабот.ПакетРабот
ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМаксимумов КАК ТаблицаМаксимумов
ПО
	ТаблицаПоПакетам.Сотрудник      = ТаблицаМаксимумов.Сотрудник
	И ТаблицаПоПакетам.ЗаказНаряд   = ТаблицаМаксимумов.ЗаказНаряд
	И ТаблицаПоПакетам.ПакетРабот   = ТаблицаМаксимумов.ПакетРабот
ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДолей КАК ТаблицаДолей
ПО
	ТаблицаПоПакетам.ПакетРабот = ТаблицаДолей.ПакетРабот
{
ГДЕ
	ТаблицаПоПакетам.Сотрудник.*    КАК Сотрудник,
	ТаблицаПоПакетам.ЗаказНаряд.*   КАК ЗаказНаряд,
	ТаблицаПоПакетам.РабочееМесто.* КАК РабочееМесто,
	ТаблицаРабот.Работа.*           КАК Работа,
	(ВЫБОР
				КОГДА ТаблицаПоПакетам.КонецРаботы = ТаблицаМаксимумов.КонецРаботы
						И ТаблицаРабот.ПакетЗакрыт = ИСТИНА
					ТОГДА "Окончание работы"
				ИНАЧЕ ТаблицаПоПакетам.ВидОтметки
			КОНЕЦ).* КАК ВидОтметки
}

СГРУППИРОВАТЬ ПО
	ТаблицаПоПакетам.Сотрудник,
	ТаблицаПоПакетам.ЗаказНаряд,
	ТаблицаПоПакетам.РабочееМесто,
	ТаблицаРабот.Работа,
	ТаблицаПоПакетам.Период,
	ВЫБОР
		КОГДА ТаблицаПоПакетам.КонецРаботы = ТаблицаМаксимумов.КонецРаботы
				И ТаблицаРабот.ПакетЗакрыт = ИСТИНА
			ТОГДА "Окончание работы"
		ИНАЧЕ ТаблицаПоПакетам.ВидОтметки
	КОНЕЦ
	
{УПОРЯДОЧИТЬ ПО 
	Сотрудник.*    КАК Сотрудник,
	ЗаказНаряд.*   КАК ЗаказНаряд,
	РабочееМесто.* КАК РабочееМесто,
	Работа.*       КАК Работа,
	ВидОтметки.*   КАК ВидОтметки,
	Период         КАК Период
}

ИТОГИ
	МАКСИМУМ(КонецРаботы),
	СУММА(Продолжительность)
ПО
	ОБЩИЕ,
	Сотрудник,
	ЗаказНаряд,
	РабочееМесто,
	Работа,
	ВидОтметки,
	Период
	
{ИТОГИ ПО 
	Сотрудник.*    КАК Сотрудник,
	ЗаказНаряд.*   КАК ЗаказНаряд,
	РабочееМесто.* КАК РабочееМесто,
	Работа.*       КАК Работа,
	ВидОтметки.*   КАК ВидОтметки,
	Период         КАК Период
}
	
АВТОУПОРЯДОЧИВАНИЕ
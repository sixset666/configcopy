#Если Клиент Тогда
// Модуль отчета
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                             //хранит текст запроса
Перем ПараметрыИспользованияПолейИПоказателей Экспорт;  //хранит параметры использования полей показателей
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;    //хранит структуру итоговых выражений показателей
Перем ДеревоДоступныхПолей Экспорт;                     //хранит дерево доступных полей
Перем СтруктураСвязиСвойств Экспорт;                    //хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                   //хранит приемника перетаскивания
Перем СтруктураНеСвязанныхПоказателей Экспорт;          //хранит структуру несвязанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                 //хранит дополнительные поля отчета
Перем ИзмеренияОтчета;                                  //хранит измерения отчета
Перем ПромежуточныйТекстЗапроса;                        //хранит промежуточный текст запроса
Перем ВычислятьABC;                                     //
Перем ВычислятьXYZ;                                     //
Перем ВыражениеРасчетаДоли Экспорт;                     // Варианты детализации
Перем мСортировка Экспорт;								//Возможные сортировки

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// удаляет измерение строки или колонки
//
// Параметры
//  ПутьИзмерения  – строка – путь к данным
//  ИмяТаблицы  – строка – имя таблицы
// 
Процедура УдалитьИзмерение(ПутьИзмерения, ИмяТаблицы) Экспорт
		
	Если ИмяТаблицы = "Строка" Тогда
		ТекСтрока = ИзмеренияСтроки.Найти(ПутьИзмерения, "ПутьКДанным");
		Если ТекСтрока = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		ИзмеренияСтроки.Удалить(ТекСтрока);
	Иначе
		ТекСтрока = ИзмеренияКолонки.Найти(ПутьИзмерения, "ПутьКДанным");
		Если ТекСтрока = Неопределено Тогда
			Возврат; 
		КонецЕсли;
		ИзмеренияКолонки.Удалить(ТекСтрока);
	КонецЕсли;
		
КонецПроцедуры // УдалитьИзмерение()
	
// добавляет измерение строки или колонки
//
// Параметры
//  ПутьИзмерения  – строка – путь к данным
//  ИмяТаблицы  – строка – имя таблицы
//  Позиция - число - позиция измерения
//
Процедура ДобавитьИзмерение(ПутьИзмерения, ИмяТаблицы, Позиция) Экспорт
		
	Если ИзмеренияКолонки.Найти(ПутьИзмерения, "ПутьКДанным")<>Неопределено ИЛИ ИзмеренияСтроки.Найти(ПутьИзмерения, "ПутьКДанным")<>Неопределено Тогда 
		Возврат; 
	КонецЕсли;
		
	ТекИзмерение = ДеревоДоступныхПолей.Строки.Найти(ПутьИзмерения, "ПутьКДанным", Истина);
	Если ИмяТаблицы = "Строка" Тогда
		НоваяСтрока = ИзмеренияСтроки.Добавить();
		ИзмеренияСтроки.Сдвинуть(НоваяСтрока,Позиция-(ИзмеренияСтроки.Количество()-1));
	Иначе
		НоваяСтрока = ИзмеренияКолонки.Добавить();
		ИзмеренияКолонки.Сдвинуть(НоваяСтрока,Позиция-(ИзмеренияКолонки.Количество()-1));
	КонецЕсли;
		
	НоваяСтрока.Использование	= Истина;
	НоваяСтрока.Представление	= ТекИзмерение.Представление;
	НоваяСтрока.ПутьКДанным		= ТекИзмерение.ПутьКДанным;
	НоваяСтрока.ТипИзмерения	= "Элементы"; 
		
КонецПроцедуры // ДобавитьИзмерение()

// проверяет настройки отчета на корректность заполнения, возвращает "истина", если корректно, "ложь" - если нет
//
// без параметров
// 
Функция КорректностьЗаполнения()
		
	Если ИмяФормыНастроек<>"XYZ_Анализ" Тогда
		Если ГруппаA + ГруппаB + ГруппаC <> 100 Тогда
			Предупреждение("Сумма параметров АВС не равна 100");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
		
	Если СокрЛП(Сортировка) = "" Тогда
		Предупреждение("Не задана сортировка данных отчета!");
		Возврат Ложь;
	КонецЕсли;
		
	Если ИмяФормыНастроек<>"ABC_Анализ" Тогда
		Если СокрЛП(Периодичность) = "" Тогда
			Предупреждение("Не задана периодичность данных отчета!");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
		
КонецФункции // КорректностьЗаполнения()
	

// Производит основные действия перед формированием
//
// Без параметров
//
Процедура ОсновныеДействияПередФормированием()
		
	//показатель по которому сортировка, д.б. всегда использован
	Показатель = Показатели.Найти(Сортировка, "Имя");
	Показатель.Использование = Истина;
		
	ПромежуточныйТекстЗапроса = ТекстЗапроса;
		
	Если ИмяФормыНастроек <> "ABC_Анализ" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПЕРИОДИЧНОСТЬ", ", Период"+Периодичность+" ПЕРИОДАМИ ("+Периодичность+", &ДатаНач, &ДатаКон)");
	КонецЕсли;
		
	Если ИмяФормыНастроек <> "XYZ_Анализ" Тогда
		СтруктураИтоговыхВыраженийПоказателей.Вставить("Доля", СтрЗаменить(ВыражениеРасчетаДоли, "ВыбСуммаСортировки", Сортировка));
	КонецЕсли;
	
КонецПроцедуры // ОсновныеДействияПередФормированием()

// Производит основные действия после формирования
//
// Без параметров
//
Процедура ОсновныеДействияПослеФормирования()
	
	ТекстЗапроса = ПромежуточныйТекстЗапроса;
	
КонецПроцедуры // ОсновныеДействияПослеФормирования()


// создает дерево результата в таблице результата
//
// Параметры
//  Выборка  –  выборка по запросу 
//  КоличествоИзмерений – число - количество измерений
//  Индекс - число - индекс
//  ТаблицаРезультата - таблица - таблица результатов
//  ТекАВСГруппа - текущая группа ABC
//  ТекXYZГруппа - текущая группа XYZ
//  КоэффициентВариации - число - коэффициент вариации
//
Процедура ПолучитьДеревоРезультата(Выборка, КоличествоИзмерений, Знач Индекс, ТаблицаРезультата, ТекАВСГруппа, ТекXYZГруппа, КоэффициентВариации);
		
	Инд = Индекс+1;
	Пока Выборка.Следующий() Цикл
		Если Инд < КоличествоИзмерений Тогда
			ПолучитьДеревоРезультата(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, ИзмеренияОтчета[Инд]), КоличествоИзмерений, Инд, ТаблицаРезультата, ТекАВСГруппа, ТекXYZГруппа, КоэффициентВариации);
		Иначе
			НоваяСтрока = ТаблицаРезультата.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если ВычислятьABC Тогда
				НоваяСтрока.ABCГруппа = ТекАВСГруппа;
			КонецЕсли;
			Если ВычислятьXYZ Тогда
				НоваяСтрока.XYZГруппа = ТекXYZГруппа;
				НоваяСтрока.КоэффициентВариации = КоэффициентВариации;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры // ПолучитьДеревоРезультата()

// получить таблицу источника данных
//
// Параметры
//  ТекПостроитель  – ПостроительОтчета - текущий построитель
//  ТаблицаВывода - таблица  
//  СтруктураКолонок - структура 
//
// Возвращаемое значение:
//  ТаблицаВывода
// 
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТаблицаВывода, СтруктураКолонок) Экспорт
	
	ИзмеренияОтчета = Новый Массив();
	
	Если ИмяФормыНастроек<>"ABC_Анализ" И ТаблицаВывода.Колонки.Найти("XYZГруппа")=Неопределено Тогда
		ТаблицаВывода.Колонки.Добавить("XYZГруппа", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
	Если ИмяФормыНастроек<>"XYZ_Анализ" И ТаблицаВывода.Колонки.Найти("ABCГруппа")=Неопределено Тогда
		ТаблицаВывода.Колонки.Добавить("ABCГруппа", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
	ИзмененияВПостроителе = Ложь;
	Если ИмяФормыНастроек="ABC_Анализ" Тогда
		Если ТекПостроитель.ИзмеренияСтроки.Найти("ABCГруппа") = Неопределено Тогда
			ТекПостроитель.ИзмеренияСтроки.Добавить("ABCГруппа");
			ИзмеренияОтчета.Добавить("ABCГруппа");
			ИзмененияВПостроителе = Истина;
		КонецЕсли;
	ИначеЕсли ИмяФормыНастроек="XYZ_Анализ" Тогда
		Если ТекПостроитель.ИзмеренияСтроки.Найти("XYZГруппа") = Неопределено Тогда
			ТекПостроитель.ИзмеренияСтроки.Добавить("XYZГруппа");
			ИзмеренияОтчета.Добавить("XYZГруппа");
			ИзмененияВПостроителе = Истина;
		КонецЕсли;
	ИначеЕсли ИмяФормыНастроек="ABC_XYZ_Анализ" Тогда
		Если ТекПостроитель.ИзмеренияСтроки.Найти("ABCГруппа") = Неопределено Тогда
			ТекПостроитель.ИзмеренияСтроки.Добавить("ABCГруппа");
			ИзмеренияОтчета.Добавить("ABCГруппа");
			ИзмененияВПостроителе = Истина;
		КонецЕсли;
		Если РазвернутьПоXYZ Тогда
			Если ТекПостроитель.ИзмеренияКолонки.Найти("XYZГруппа") = Неопределено Тогда
				ТекПостроитель.ИзмеренияКолонки.Добавить("XYZГруппа");
				ИзмеренияОтчета.Добавить("XYZГруппа");
				ИзмененияВПостроителе = Истина;
			КонецЕсли;
		Иначе
			Если ТекПостроитель.ИзмеренияСтроки.Найти("XYZГруппа") = Неопределено Тогда
				ТекПостроитель.ИзмеренияСтроки.Добавить("XYZГруппа");
				ИзмеренияОтчета.Добавить("XYZГруппа");
				ИзмененияВПостроителе = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ТекИзмерение Из СтруктураКолонок.ИзмеренияСтрок Цикл
		ИзмеренияОтчета.Добавить(ТекИзмерение.ПутьКДанным);
	КонецЦикла;
	
	Для Каждого ТекИзмерение Из СтруктураКолонок.ИзмеренияКолонок Цикл
		ИзмеренияОтчета.Добавить(ТекИзмерение.ПутьКДанным);
	КонецЦикла;
	
	СтруктураВычисляемыхПоказателей = Новый Структура ("КоэффициентВариации, Доля");
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,3));
	
	СтруктураНеобходимыхПоказателей = Новый Структура ("КоэффициентВариации, СуммаСкидки, СуммаРегл, СебестоимостьУпр, СуммаНаценкиУпр");
	Для Каждого ТекНужныйПоказатель Из СтруктураНеобходимыхПоказателей Цикл
		Если ТаблицаВывода.Колонки.Найти(ТекНужныйПоказатель.Ключ) = Неопределено Тогда
			ТаблицаВывода.Колонки.Добавить(ТекНужныйПоказатель.Ключ, ОписаниеТиповЧисло);
		КонецЕсли;
	КонецЦикла;
	
	КоличествоИзмерений = ИзмеренияОтчета.Количество();
	НачальнаяПозиция = ?(ИмяФормыНастроек = "ABC_XYZ_Анализ" И Не РазвернутьПоXYZ, 2, 1);
	
	Если КоличествоИзмерений = НачальнаяПозиция Тогда
		ТекПостроитель.ИзмеренияСтроки.Добавить("Номенклатура");
		ИзмеренияОтчета.Добавить("Номенклатура");
		КоличествоИзмерений = КоличествоИзмерений + 1;
		ИзмененияВПостроителе = Истина;
	Иначе
		ИмяПоиска = ?(ИмяФормыНастроек="XYZ_Анализ", "XYZГруппа", "ABCГруппа");
		Для Индекс=1 По КоличествоИзмерений-1 Цикл
			Если ИзмеренияОтчета[Индекс] = ИмяПоиска Тогда
				ИзмеренияОтчета[Индекс] = ИзмеренияОтчета[0];
				ИзмеренияОтчета[0] = ИмяПоиска;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ИзмененияВПостроителе Тогда
		ТекПостроитель.Выполнить();
	КонецЕсли;
	
	ВычислятьABC = ИмяФормыНастроек<>"XYZ_Анализ";
	ВычислятьXYZ = ИмяФормыНастроек<>"ABC_Анализ";
	
	Если ВычислятьXYZ Тогда
		ТекПостроитель.ИзмеренияСтроки.Добавить("Период"+Периодичность);
	КонецЕсли;
	
	Выборка = ТекПостроитель.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, ИзмеренияОтчета[НачальнаяПозиция], "Все");
	
	Если ВычислятьABC Тогда
		ВсегоСумма = 0;
		Пока Выборка.Следующий() Цикл
			ВсегоСумма = ВсегоСумма+Выборка[Сортировка];
		КонецЦикла;
		
		Выборка.Сбросить();
		Если ВсегоСумма<0 Тогда
			ВсегоСумма=-ВсегоСумма;
		КонецЕсли;
		ПорогА = Окр((ВсегоСумма*ГруппаA/100),2);
		ПорогВ = Окр((ВсегоСумма*ГруппаB/100),2);
		ГраницаПорогаВ = ПорогА + ПорогВ;
	КонецЕсли;
	
	НакопленнаяСумма = 0;
	ПорогХ = ГруппаX;
	ПорогУ = ГруппаY;
	
	ДетализацияВыборкиПериода = ?(НеУчитыватьДниБезПродаж, "", "Все");
	Пока Выборка.Следующий() Цикл
		
		ТекСуммаПараметра = Выборка[Сортировка];
		Если ВычислятьABC Тогда
			НакопленнаяСумма = НакопленнаяСумма + ТекСуммаПараметра;
			Если НакопленнаяСумма <= ПорогА ИЛИ ПорогА < ТекСуммаПараметра Тогда
				ТекABCГруппа = "Группа А";
			ИначеЕсли НакопленнаяСумма <= ГраницаПорогаВ ИЛИ ПорогВ < ТекСуммаПараметра Тогда
				ТекABCГруппа = "Группа В";
			Иначе
				ТекABCГруппа = "Группа С";
			КонецЕсли;
		КонецЕсли;
		
		Если ВычислятьXYZ Тогда
			Отклонение = 0;
			ВыборкаПоПериоду = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период"+Периодичность, ДетализацияВыборкиПериода);
			КоличествоВВыборке = ВыборкаПоПериоду.Количество();
			
			Среднее = ТекСуммаПараметра/КоличествоВВыборке;
			Пока ВыборкаПоПериоду.Следующий() Цикл
				Отклонение = Отклонение + Pow(?(ВыборкаПоПериоду[Сортировка]=Null, -Среднее, (ВыборкаПоПериоду[Сортировка] - Среднее)), 2);
			КонецЦикла;
			Если Среднее = 0 Тогда
				КоэффициентВариации = 0;
			Иначе
				КоэффициентВариации = ?(КоличествоВВыборке = 0,0,(SQRT(Отклонение/КоличествоВВыборке)/Среднее)*100);
			КонецЕсли;
			
			Если КоэффициентВариации < 0 И НеУчитыватьОтрицательнуюВариацию Тогда Продолжить; КонецЕсли;
			
			КоэффициентВариацииПоМодулю = обМод(КоэффициентВариации);
			
			Если КоэффициентВариацииПоМодулю <= ПорогХ Тогда
				ТекXYZГруппа = "Группа X";
			ИначеЕсли КоэффициентВариацииПоМодулю <= ПорогУ Тогда
				ТекXYZГруппа = "Группа Y";
			Иначе
				ТекXYZГруппа = "Группа Z";
			КонецЕсли;
		КонецЕсли;
		
		//Если (НачальнаяПозиция+1)<КоличествоИзмерений Тогда
		//	ПолучитьДеревоРезультата(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, ИзмеренияОтчета[НачальнаяПозиция+1]), КоличествоИзмерений, НачальнаяПозиция+1, ТаблицаВывода, ТекABCГруппа, ТекXYZГруппа, КоэффициентВариации);
		//Иначе
		//	НоваяСтрока = ТаблицаВывода.Добавить();
		//	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		//	Если ВычислятьABC Тогда
		//		НоваяСтрока.ABCГруппа = ТекABCГруппа;
		//	КонецЕсли;
		//	Если ВычислятьXYZ Тогда
		//		НоваяСтрока.XYZГруппа = ТекXYZГруппа;
		//		НоваяСтрока.КоэффициентВариации = КоэффициентВариации;
		//	КонецЕсли;
		//КонецЕсли;
		
		ВыборкаДетали = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока ВыборкаДетали.Следующий() Цикл
			Если НЕ ВыборкаДетали.Группировка() = "" Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаВывода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетали);
			Если ВычислятьABC Тогда
				НоваяСтрока.ABCГруппа = ТекABCГруппа;
			КонецЕсли;
			Если ВычислятьXYZ Тогда
				НоваяСтрока.XYZГруппа = ТекXYZГруппа;
				НоваяСтрока.КоэффициентВариации = КоэффициентВариации;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если ВычислятьABC Тогда
		ЕстьДоля = Показатели.Найти("Доля", "Имя").Использование;
		Если ЕстьДоля Тогда
			ТаблицаВывода.Колонки.Добавить("ИтоговаяСуммаСортировки", ОписаниеТиповЧисло);
			ТаблицаВывода.ЗаполнитьЗначения(ВсегоСумма, "ИтоговаяСуммаСортировки");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаВывода;
	
КонецФункции // ПолучитьТаблицуИсточникаДанных()
	
// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – имя макета
//  ИмяМакетаПараметров  – строка – имя макета параметров
//  
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
		
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
		
	ПоказательДляУдаления = Неопределено;
	СтрокаДляУдаления = Неопределено;
	Если ИмяФормыНастроек = "ABC_Анализ" Тогда
		СтрокаДляУдаления = "XYZГруппа";
		ПоказательДляУдаления = Показатели.Найти("КоэффициентВариации", "Имя");
	ИначеЕсли ИмяФормыНастроек = "XYZ_Анализ" Тогда
		СтрокаДляУдаления = "ABCГруппа";
		ПоказательДляУдаления = Показатели.Найти("Доля", "Имя");
	КонецЕсли;
		
	Если СтрокаДляУдаления<> Неопределено Тогда
		ДеревоДоступныхПолей.Строки.Найти(СтрокаДляУдаления,"ПутьКДанным").Измерение=Ложь;
		УдалитьИзмерение(СтрокаДляУдаления, "Строка");
	КонецЕсли;
		
	Если ПоказательДляУдаления <> Неопределено Тогда
		Показатели.Удалить(ПоказательДляУдаления);
	КонецЕсли;
		
	ТекСтруктураПараметров = ПараметрыИспользованияПолейИПоказателей["ABCГруппа"];
	ТекСтруктураПараметров.Использование.Ресурсы["ПроцентСкидки"]		= Ложь;
	ТекСтруктураПараметров.Использование.Ресурсы["ПроцентНаценки"]		= Ложь;
	ТекСтруктураПараметров.Использование.Ресурсы["КоэффициентВариации"]	= Ложь;
		
	ТекСтруктураПараметров = ПараметрыИспользованияПолейИПоказателей["XYZГруппа"];
	ТекСтруктураПараметров.Использование.Ресурсы["ПроцентСкидки"]		= Ложь;
	ТекСтруктураПараметров.Использование.Ресурсы["ПроцентНаценки"]		= Ложь;
	ТекСтруктураПараметров.Использование.Ресурсы["КоэффициентВариации"]	= Ложь;
		
	ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы.ПроцентСкидки		= Ложь;
	ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы.ПроцентНаценки 		= Ложь;
	ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы.КоэффициентВариации = Ложь;
	
	//Заполним группы АВС
	ГруппаA = 75;
	ГруппаB = 20;
	ГруппаC = 5;
	
	ГруппаX = 10;
	ГруппаY = 25;
	
	//установим сортировку
	Сортировка    = мСортировка[0].Значение;
	Периодичность = "Месяц";
	
	Если Показатели.Найти("Доля", "Имя") = Неопределено Тогда
		НоваяСтрока = Показатели.Добавить(); 
		НоваяСтрока.Имя = "Доля";
		НоваяСтрока.Использование = Истина;
		НоваяСтрока.Представление = "% от общей суммы";
		НоваяСтрока.ФорматнаяСтрока = "ЧЦ=15;ЧДЦ=2";
		НоваяСтрока.ШиринаКолонки = 14;
	КонецЕсли;
	
	Если Показатели.Найти("КоэффициентВариации", "Имя") = Неопределено Тогда
		НоваяСтрока = Показатели.Добавить(); 
		НоваяСтрока.Имя = "КоэффициентВариации";
		НоваяСтрока.Использование = Истина;
		НоваяСтрока.Представление = "Вариация (%)";
		НоваяСтрока.ФорматнаяСтрока = "ЧЦ=15;ЧДЦ=2";
		НоваяСтрока.ШиринаКолонки = 14;
	КонецЕсли;
	
	ДобавитьИзмерение("ABCГруппа", "Строка", 0);
	
	Если РазвернутьПоXYZ Тогда
		ДобавитьИзмерение("XYZГруппа", "Колонка", 0);
	Иначе
		ДобавитьИзмерение("XYZГруппа", "Строка", 1);
	КонецЕсли;
	
	Если ТипЗнч(СтруктураИтоговыхВыраженийПоказателей) = Тип("Структура") И СтруктураИтоговыхВыраженийПоказателей.Свойство("Доля") Тогда
		
		ВыражениеРасчетаДоли = СтруктураИтоговыхВыраженийПоказателей.Доля;
		
	КонецЕсли;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()
	
// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат - ПолеТабличногоДокумента или ТабличныйДокумент
//                 
// Возвращаемое значение:
//  ДокументРезультат
//  или
//  Результат - табличный документ
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
		
	Если Не КорректностьЗаполнения() Тогда
		Возврат ДокументРезультат;
	КонецЕсли;
	
	ОсновныеДействияПередФормированием();
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат, 1, глПрава);
	
	ОсновныеДействияПослеФормирования();
		
	Возврат Результат;
		
КонецФункции // ЗаполнитьНачальныеНастройки()
	
// формирует диаграмму и возвращает её 
//
// Параметры
//  ПолеДиаграммы – Диаграмма -  передает ссылку на поле диаграммы
//  СтруктураПараметров - структура - передает структуру параметров диаграммы
//
// Возвращаемое значение:
//  ПолеДиаграммы - Диаграмма
//
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
		
	Если Не КорректностьЗаполнения() Тогда
		Возврат ПолеДиаграммы;
	КонецЕсли;
		
	ОсновныеДействияПередФормированием();
		
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров, 1);
		
	ОсновныеДействияПослеФормирования();
		
	Возврат ПолеДиаграммы;
		
КонецФункции // СформироватьДиаграмму()
	
// Функция возвращает структуру форматирования полей
//
// Без параметров
//                 
// Возвращаемое значение:
//   СтруктураФорматаПолей - структура 
// 
Функция СтруктураФорматированияПолей() Экспорт
		
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
		
	// Если необходимо, добавим другие или переустановим строки форматирования полей
		
	Возврат СтруктураФорматаПолей;
		
КонецФункции	//	СтруктураФорматированияПолей()
	
// возвращает список сортировки
// 
// Без параметров
//                 
// Возвращаемое значение:
//  СписокСортировки - список
// 
Функция СтруктураСортировки() Экспорт
		
	Если СокрЛП(Сортировка) = "" Тогда
		СписокСортировки = Неопределено;
	Иначе
		СписокСортировки = Новый СписокЗначений();
			
		Если ИмяФормыНастроек <> "XYZ_Анализ" Тогда
			СписокСортировки.Добавить(НаправлениеСортировки.Возр, "ABCГруппа");
		КонецЕсли;
			
		Если ИмяФормыНастроек <> "ABC_Анализ" Тогда
			СписокСортировки.Добавить(НаправлениеСортировки.Возр, "XYZГруппа");
		КонецЕсли;
			
		Если ИмяФормыНастроек="XYZ_Анализ" Тогда
			СписокСортировки.Добавить(НаправлениеСортировки.Возр, "КоэффициентВариации");
		Иначе
			СписокСортировки.Добавить(НаправлениеСортировки.Убыв, Сортировка);
		КонецЕсли;
			
	КонецЕсли;
		
	Возврат СписокСортировки;
КонецФункции // СтруктураФорматированияПолей()
	
//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	
мСортировка = Новый СписокЗначений;
мСортировка.Добавить("СуммаРегл", "Сумма продаж (Регл.)");
мСортировка.Добавить("СуммаУпр", "Сумма продаж (Упр.)");
мСортировка.Добавить("СуммаНаценкиРегл", "Сумма наценки (Регл.)");
мСортировка.Добавить("СуммаНаценкиУпр", "Сумма наценки (Упр.)");
мСортировка.Добавить("ПроцентНаценки", "Процент наценки");
мСортировка.Добавить("СебестоимостьРегл", "Себестоимость (Регл.)");
мСортировка.Добавить("СебестоимостьУпр", "Себестоимость (Упр.)");
мСортировка.Добавить("Количество", "Количество (в базовых ед.)");
мСортировка.Добавить("КоличествоОсн", "Количество (в основных ед.)");
мСортировка.Добавить("СуммаСкидки", "Сумма скидки");
мСортировка.Добавить("ПроцентСкидки", "Процент скидки");
	
ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();
ВыражениеРасчетаДоли = "ВЫРАЗИТЬ(ВЫБОР КОГДА МАКСИМУМ(ИтоговаяСуммаСортировки)=0 ТОГДА 0 ИНАЧЕ (СУММА(ВыбСуммаСортировки)/МАКСИМУМ(ИтоговаяСуммаСортировки))*100 КОНЕЦ КАК Число(15,2))";

#КонецЕсли
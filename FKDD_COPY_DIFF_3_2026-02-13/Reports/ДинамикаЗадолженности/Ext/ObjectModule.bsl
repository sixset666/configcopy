#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем СписокПериодов Экспорт;                          // хранит список периодов
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;   // хранит структуру итоговых выражений показателей
Перем ШаблонИсточникаДанных Экспорт;                   // хранит шаблон источника данных
Перем СтруктураТипаКолонки;                            // хранит структуру типов колонок
Перем ПоследовательностиОтчета Экспорт;                // хранит последовательности отчета


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Устанавливает основные параметры
//
//Без параметров
//
Процедура УстановитьОсновныеПараметры()
	
	//Заполним параметры
	ТекСтрока = СписокПериодов[ИзмерениеСтроки];
	ИзмеренияСтроки.Очистить();
	НоваяСтрока = ИзмеренияСтроки.Добавить();
	НоваяСтрока.Использование	= Истина;
	НоваяСтрока.Представление	= ТекСтрока.Представление;
	НоваяСтрока.ПутьКДанным		= ТекСтрока.Значение;
	НоваяСтрока.ТипИзмерения	= "Элементы";
	
	ТекКолонка = СписокПериодов[ИзмерениеКолонки];
	ИзмеренияКолонки.Очистить();
	НоваяКолонка = ИзмеренияКолонки.Добавить();
	НоваяКолонка.Использование	= Истина;
	НоваяКолонка.Представление	= ТекКолонка.Представление;
	НоваяКолонка.ПутьКДанным	= ТекКолонка.Значение;
	НоваяКолонка.ТипИзмерения	= "Элементы";
	
	ПараметрыИспользованияПолейИПоказателей[ТекСтрока.Значение].Удалить("СоставноеПредставление");
	ТекПараметрыКолонки = ПараметрыИспользованияПолейИПоказателей[ТекКолонка.Значение];
	Если СтруктураТипаКолонки[ТекСтрока.Значение+"_"+ТекКолонка.Значение]="Дата" Тогда
		ТекПараметрыКолонки.Удалить("СоставноеПредставление");
	Иначе
		ТекПараметрыКолонки.Вставить("СоставноеПредставление", Новый Структура("Представление, Поля", "["+СтрЗаменить(ТекКолонка.Значение,".","")+"]"+СтрЗаменить(ТекКолонка.Представление, "Период",""), Новый Массив));
	КонецЕсли;
	
	Если ИзмерениеСтроки=1 Тогда
		ПараметрыИспользованияПолейИПоказателей.ПериодКвартал.Формат = "ДФ='к ""квартал"" гггг ""г.""'";
	ИначеЕсли ИзмерениеСтроки=2 Тогда
		ПараметрыИспользованияПолейИПоказателей.ПериодМесяц.Формат = "ДФ='ММММ гггг ""г.""'";
	КонецЕсли;
	
	Если ИзмерениеКолонки = 2 Тогда
		ПараметрыИспользованияПолейИПоказателей.ПериодМесяц.Формат = "ДФ='ММММ'";
	ИначеЕсли ИзмерениеКолонки = 4 Тогда
		Если ИзмерениеСтроки>1 Тогда
			ПараметрыИспользованияПолейИПоказателей.ПериодДень.Формат = "ДФ=дд";
		Иначе
			ПараметрыИспользованияПолейИПоказателей.ПериодДень.Формат = "ДФ=дд.ММ";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьОсновныеПараметры()

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройкиОтчетаСИсточникомДанных(СтруктураПараметрыЗаполнения, глПрава);
	
	Для Каждого ТекЭлемент Из СписокПериодов Цикл
		ТекЭлемент.Представление = ПараметрыИспользованияПолейИПоказателей[ТекЭлемент.Значение].Представление;
	КонецЦикла;
	   
	ИзмерениеСтроки = СписокПериодов.Индекс(СписокПериодов.НайтиПоЗначению(ИзмеренияСтроки[0].ПутьКДанным));
	ИзмерениеСтроки = Мин(ИзмерениеСтроки, 4);
	ИзмерениеКолонки = СписокПериодов.Индекс(СписокПериодов.НайтиПоЗначению(ИзмеренияКолонки[0].ПутьКДанным));
	ИзмерениеКолонки = Макс(ИзмерениеКолонки, ИзмерениеСтроки+1);
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// Формирует таблицу источника данных построителя
//
// Параметры
//   ТекПостроитель - ПостроительОтчета
//	 ТаблицаВывода - ТаблицаЗначений
//   ПоляНастройкиОтчета - структура
//
// Возвращаемое значение:
//   НоваяТаблицаВывода - ТаблицаЗначений
//
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТаблицаВывода, ПоляНастройкиОтчета) Экспорт
		
	ИмяСтроки = СписокПериодов[ИзмерениеСтроки].Значение;
	ИмяКолонки = СписокПериодов[ИзмерениеКолонки].Значение;
	
	ТаблицаПериодов = Новый ТаблицаЗначений;
	ТаблицаПериодов.Колонки.Добавить("ПериодСтроки", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("ПериодКолонки", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("ПериодНижнийПредел", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("ПериодВерхнийПредел", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодов.Колонки.Добавить("НомерКолонки", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(2));
	
	ТекДата = ДатаНачала;
	МинимальныйПределПериода = 1;
	Если ИзмерениеСтроки=0 Тогда // Год
		МинимальныйПределМесяца = 29;
		МинимальныйПределПериода = 366;
		ТекДата	= НачалоГода(ДатаНачала);
	ИначеЕсли ИзмерениеСтроки=1 Тогда // Квартал
		МинимальныйПределМесяца = 29;
		МинимальныйПределПериода = 92;
		ТекДата = НачалоКвартала(ДатаНачала);
	ИначеЕсли ИзмерениеСтроки=2 Тогда // Месяц
		МинимальныйПределМесяца = 31;
		МинимальныйПределПериода = 31;
		ТекДата = НачалоМесяца(ДатаНачала);
	ИначеЕсли ИзмерениеСтроки=3 Тогда // Неделя
		МинимальныйПределМесяца = 29;
		МинимальныйПределПериода = 7;
		ТекДата	= НачалоНедели(ДатаНачала);
	КонецЕсли;
	
	Шаг = 0;
	Если ИзмерениеКолонки = 1 Тогда
		Шаг = 3;
	ИначеЕсли ИзмерениеКолонки = 2 Тогда
		Шаг = 1;
	ИначеЕсли ИзмерениеКолонки = 3 Тогда
		Шаг = 60*60*24*7;
	ИначеЕсли ИзмерениеКолонки = 4 Тогда
		Шаг = 60*60*24;
	КонецЕсли;
	
	КолонкаДата = (СтруктураТипаКолонки[ИмяСтроки+"_"+ИмяКолонки]="Дата");
	Если ИзмерениеКолонки = 3 Тогда // Неделя
		ПустаяДата = Дата(1904, 1, 1);
		МинимальныйПределПериода = Окр(МинимальныйПределПериода/7+0.5, 0, 0)+?(ИзмерениеСтроки=2, 1, 0);
		Пока ТекДата<ДатаКонца Цикл // Период от начальной даты до конечной даты
			Если ИзмерениеСтроки=0 Тогда
				ПериодСтроки = НачалоГода(ТекДата);
				ТекКонДата = КонецГода(ТекДата);
			ИначеЕсли ИзмерениеСтроки=1 Тогда
				ПериодСтроки = НачалоКвартала(ТекДата);
				ТекКонДата = КонецКвартала(ТекДата);
			ИначеЕсли ИзмерениеСтроки=2 Тогда
				ПериодСтроки = НачалоМесяца(ТекДата);
				ТекКонДата = КонецМесяца(ТекДата);
			КонецЕсли;
			
			Счетчик = 1;
			ТекДата	= НачалоНедели(ТекДата);
			ТекКонДата = Мин(ТекКонДата, ДатаКонца);
			Пока ТекДата<ТекКонДата Цикл // период по группировке строки
				НоваяСтрока = ТаблицаПериодов.Добавить();
				НоваяСтрока.ПериодСтроки = ПериодСтроки;
				НоваяСтрока.ПериодКолонки = ТекДата;
				НоваяСтрока.НомерКолонки = Счетчик;
				НоваяСтрока.ПериодНижнийПредел = Макс(ТекДата, ПериодСтроки);
				ТекДата = ТекДата + Шаг;
				НоваяСтрока.ПериодВерхнийПредел = ТекДата;
				Счетчик = Счетчик + 1;
			КонецЦикла;
			
			// добавим пустышки до последнего дня
			Если ТекДата<ДатаКонца И Счетчик <= МинимальныйПределПериода Тогда
				ТекДатаКолонки = ТекДата - Шаг;
				Пока Счетчик <= МинимальныйПределПериода Цикл
					НоваяСтрока = ТаблицаПериодов.Добавить();
					НоваяСтрока.ПериодСтроки = ПериодСтроки;
					НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
					НоваяСтрока.НомерКолонки = Счетчик;
					НоваяСтрока.ПериодНижнийПредел = ПустаяДата;
					НоваяСтрока.ПериодВерхнийПредел = ПустаяДата;
					Счетчик = Счетчик + 1;
				КонецЦикла;
			Иначе
				НоваяСтрока.ПериодВерхнийПредел = ТекКонДата+1;
			КонецЕсли;
			ТекДата = ТекКонДата+1;
		КонецЦикла;
	
	ИначеЕсли ИзмерениеСтроки<>3 И ИзмерениеКолонки = 4 Тогда // день
		
		Пока ТекДата<ДатаКонца Цикл // Период от начальной даты до конечной даты
			Если ИзмерениеСтроки=0 Тогда
				ТекКонДата = КонецГода(ТекДата);
			ИначеЕсли ИзмерениеСтроки=1 Тогда
				ТекКонДата = КонецКвартала(ТекДата);
			ИначеЕсли ИзмерениеСтроки=2 Тогда
				ТекКонДата = КонецМесяца(ТекДата);
			КонецЕсли;
			
			Счетчик = 1;
			ПериодСтроки = ТекДата;
			НетПустышек = Истина;
			ТекКонДата = Мин(ТекКонДата, ДатаКонца);
			Пока ТекДата<ТекКонДата Цикл // период по группировке строки
				
				Если МинимальныйПределМесяца = 29 Тогда
					ТекДатаКолонки = Дата(1904, Месяц(ТекДата), День(ТекДата));
				Иначе
					ТекДатаКолонки = Дата(1904, 1, День(ТекДата));
				КонецЕсли;
				ТекДень = День(ТекДата);
				ПоследнийДень = День(КонецМесяца(ТекДата));
				Пока ТекДень <= ПоследнийДень Цикл // период по месяцу
					НоваяСтрока = ТаблицаПериодов.Добавить();
					НоваяСтрока.ПериодСтроки = ПериодСтроки;
					НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
					НоваяСтрока.НомерКолонки = Счетчик;
					НоваяСтрока.ПериодНижнийПредел = ТекДата;
					ТекДатаКолонки = ТекДатаКолонки + Шаг;
					ТекДата = ТекДата + Шаг;
					НоваяСтрока.ПериодВерхнийПредел = ТекДата;
					ТекДень = ТекДень + 1;
					Счетчик = Счетчик + 1;
					Если ТекДата>ДатаКонца Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если ТекДата>ДатаКонца Тогда
					Прервать;
				КонецЕсли;
				
				// добавим пустышки до последнего дня
				НетПустышек = Истина;
				Если ТекДень <= МинимальныйПределМесяца Тогда
					ТекДатаКолонки = Дата(1904, Месяц(ТекДатаКолонки), ПоследнийДень+1);
					Пока ТекДень <= МинимальныйПределМесяца Цикл
						НоваяСтрока = ТаблицаПериодов.Добавить();
						НоваяСтрока.ПериодСтроки = ПериодСтроки;
						НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
						НоваяСтрока.НомерКолонки = Счетчик;
						НоваяСтрока.ПериодНижнийПредел = ТекДатаКолонки;
						НоваяСтрока.ПериодВерхнийПредел = ТекДатаКолонки;
						ТекДатаКолонки = ТекДатаКолонки + Шаг;
						ТекДень = ТекДень + 1;
						Счетчик = Счетчик + 1;
					КонецЦикла;
					НетПустышек = Ложь;
				КонецЕсли;
			КонецЦикла;
			
			// добавим пустышки до последнего дня
			Если ТекДата<ДатаКонца И Счетчик <= МинимальныйПределПериода Тогда
				ТекДатаКолонки = ТекДатаКолонки-Шаг;
				Пока Счетчик <= МинимальныйПределПериода Цикл
					НоваяСтрока = ТаблицаПериодов.Добавить();
					НоваяСтрока.ПериодСтроки = ПериодСтроки;
					НоваяСтрока.ПериодКолонки = ТекДатаКолонки;
					НоваяСтрока.НомерКолонки = Счетчик;
					НоваяСтрока.ПериодНижнийПредел = ТекДатаКолонки;
					НоваяСтрока.ПериодВерхнийПредел = ТекДатаКолонки;
					Счетчик = Счетчик + 1;
				КонецЦикла;
				НетПустышек = Ложь;
			КонецЕсли;
			ТекДата = ТекКонДата+1;
			Если НетПустышек Тогда
				НоваяСтрока.ПериодВерхнийПредел = ТекДата;
			КонецЕсли;
		КонецЦикла;

	Иначе // Год, Квартал, Месяц
		
		Пока ТекДата<ДатаКонца Цикл
			Если ИзмерениеСтроки=0 Тогда
				ТекКонДата = КонецГода(ТекДата);
			ИначеЕсли ИзмерениеСтроки=1 Тогда
				ТекКонДата = КонецКвартала(ТекДата);
			ИначеЕсли ИзмерениеСтроки=3 Тогда
				ТекКонДата = КонецНедели(ТекДата);
			КонецЕсли;
			ПериодСтроки = ТекДата;
			ТекКонДата = Мин(ТекКонДата, ДатаКонца);
			Счетчик = 1;
			Пока ТекДата < ТекКонДата Цикл
				НоваяСтрока = ТаблицаПериодов.Добавить();
				НоваяСтрока.ПериодСтроки = ПериодСтроки;
				НоваяСтрока.ПериодКолонки = Дата(1904, Месяц(ТекДата), День(ТекДата));
				НоваяСтрока.НомерКолонки = Счетчик;
				НоваяСтрока.ПериодНижнийПредел = ТекДата;
				Если ИзмерениеСтроки = 3 Тогда
					ТекДата = ТекДата + Шаг;
				Иначе
					ТекДата = ДобавитьМесяц(ТекДата, Шаг);
				КонецЕсли;
				НоваяСтрока.ПериодВерхнийПредел = ТекДата;
				Счетчик = Счетчик+1;
			КонецЦикла;
			ТекДата = ТекКонДата+1;
			НоваяСтрока.ПериодВерхнийПредел = ТекДата;
		КонецЦикла;
	КонецЕсли;

	ТекстЗапроса = " ВЫБРАТЬ
	|	ТаблицаДанных.ПериодСтроки КАК ПериодСтроки,
	|	ТаблицаДанных."+?(КолонкаДата,"ПериодКолонки","НомерКолонки")+" КАК ПериодКолонки,
	|	ТаблицаДанных.ПериодНижнийПредел,
	|	ТаблицаДанных.ПериодВерхнийПредел
	|ПОМЕСТИТЬ
	|	ВременнаяТаблица
	|ИЗ
	|	&ТаблицаДанных КАК ТаблицаДанных";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ВременныйМенеджер = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = ВременныйМенеджер;
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаПериодов);
	Запрос.Выполнить();
	
	СтрокаУсловия="";
	СтрокаУсловияДвижений = "";                          
	СтрЗапросаСвойств      = "";
	СтрСоединенийСвойств   = "";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКонца);
	
	Для Каждого ЭлементОтбора Из ПостроительОтчета.Отбор Цикл
		Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
		Если СтруктураСвязиСвойств.Свойство(ЭлементОтбора.Имя) Тогда
			ТекСвойство = СтруктураСвязиСвойств[ЭлементОтбора.Имя];
			ИмяСвойства = СтрЗаменить(ЭлементОтбора.Имя, "Значение", "");
			НаименованиеСвойства = ТекСвойство.Представление;
			ИмяПараметра = ТекСвойство.ИмяПараметра;
			ПутьКДаннымПоля = "ОбъединенныйЗапрос." + ТекСвойство.ИмяИзмерения;
			ЗапросСвойстваПоля = "ЕСТЬNULL("+ИмяСвойства + ".Значение, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка))";
			ЗапросСвойстваСоединение = "ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ПутьКДаннымПоля + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + " //СВОЙСТВО" + Символы.ПС;
			СтрЗапросаСвойств      = СтрЗапросаСвойств + Символы.ПС + ?(ПустаяСтрока(СтрЗапросаСвойств), " ГДЕ	"," И	") + СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#", ЗапросСвойстваПоля),"&","&"+ЭлементОтбора.Имя);
			СтрСоединенийСвойств   = СтрСоединенийСвойств   + ЗапросСвойстваСоединение;
			Запрос.УстановитьПараметр(ТекСвойство.ИмяПараметра, ТекСвойство.Свойство);
		Иначе
			СтрокаУсловия=СтрокаУсловия+Символы.ПС+?(НЕ ПустаяСтрока(СтрокаУсловия)," И ","")+СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#", ЭлементОтбора.ПутьКДанным),"&","&"+ЭлементОтбора.Имя);
			СтрокаУсловияДвижений=СтрокаУсловияДвижений+" И "+Символы.ПС+СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#", "ВзаиморасчетыКомпании."+ЭлементОтбора.ПутьКДанным),"&","&"+ЭлементОтбора.Имя);
		КонецЕсли;
		Запрос.УстановитьПараметр(ЭлементОтбора.Имя, ЭлементОтбора.Значение);
	КонецЦикла;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.Сумма, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.СуммаУпр, 0)) КАК СуммаУпр,
	|	ВременнаяТаблица.ПериодСтроки КАК "+ИмяСтроки+",
	|	ВременнаяТаблица.ПериодКолонки КАК "+ИмяКолонки+"
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|	ОбъединенныйЗапрос.Период КАК Период,
	|	ОбъединенныйЗапрос.Сумма КАК Сумма,
	|	ОбъединенныйЗапрос.СуммаУпр КАК СуммаУпр
	|	ИЗ (ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
	|		&ДатаНач КАК Период,
	|		ВзаиморасчетыКомпанииОстатки.СуммаОстаток КАК Сумма,
	|		ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&ДатаНач, "+СтрокаУсловия+") КАК ВзаиморасчетыКомпанииОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|	ВзаиморасчетыКомпании.Контрагент,
	|	ВзаиморасчетыКомпании.ДоговорВзаиморасчетов,
	|	ВзаиморасчетыКомпании.Сделка,
	|		ВзаиморасчетыКомпании.Период,
	|		ВЫБОР КОГДА ВзаиморасчетыКомпании.ВидДвижения = Значение(ВидДвиженияНакопления.Приход) ТОГДА
	|			ВзаиморасчетыКомпании.Сумма
	|		ИНАЧЕ
	|			-ВзаиморасчетыКомпании.Сумма
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР КОГДА ВзаиморасчетыКомпании.ВидДвижения = Значение(ВидДвиженияНакопления.Приход) ТОГДА
	|			ВзаиморасчетыКомпании.СуммаУпр
	|		ИНАЧЕ
	|			-ВзаиморасчетыКомпании.СуммаУпр
	|		КОНЕЦ КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
	|	ГДЕ
	|		ВзаиморасчетыКомпании.Период МЕЖДУ &ДатаНач И &ДатаКон
	|		"+СтрокаУсловияДвижений+") КАК ОбъединенныйЗапрос
	|	//СОЕДИНЕНИЯ
	|	//СВОЙСТВА
	|) КАК ВложенныйЗапрос
	|	ПО ВременнаяТаблица.ПериодНижнийПредел <= ВложенныйЗапрос.Период
	|	И  ВременнаяТаблица.ПериодВерхнийПредел > ВложенныйЗапрос.Период
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.ПериодСтроки,
	|	ВременнаяТаблица.ПериодКолонки
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ВременнаяТаблица.ПериодСтроки,
	|	ВременнаяТаблица.ПериодКолонки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//СВОЙСТВА",        СтрЗапросаСвойств);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//СОЕДИНЕНИЯ",      СтрСоединенийСвойств);
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.МенеджерВременныхТаблиц = ВременныйМенеджер;
	Выборка = Запрос.Выполнить().Выгрузить();
	ВременныйМенеджер.Закрыть();
	
	КоличествоСтрок = Выборка.Количество()-1;
	Для Счетчик=1 По КоличествоСтрок Цикл
		Выборка[Счетчик].Сумма = Выборка[Счетчик].Сумма+Выборка[Счетчик-1].Сумма;
		Выборка[Счетчик].СуммаУпр = Выборка[Счетчик].СуммаУпр+Выборка[Счетчик-1].СуммаУпр;
	КонецЦикла;
	
	ТекПостроитель.ВыводитьОбщиеИтоги = Ложь;

	Возврат Выборка;
		
КонецФункции // ПолучитьТаблицуИсточникаДанных()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт

	УстановитьОсновныеПараметры();
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат, 2, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// формирует диаграмму
//
// Параметры
//  ПолеДиаграммы – Диаграмма
//  СтруктураПараметров - структура
//
// Возвращаемое значение:
//  ПолеДиаграммы - Диаграмма
//
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	Если ПолеДиаграммы.ТипДиаграммы = ТипДиаграммы.Круговая ИЛИ ПолеДиаграммы.ТипДиаграммы = ТипДиаграммы.КруговаяОбъемная Тогда
		Предупреждение("Отчет динамика задолженности не может использоваться с типом диаграммы """+СокрЛП(ПолеДиаграммы.ТипДиаграммы)+"""");
		ПолеДиаграммы.ТипДиаграммы = ТипДиаграммы.График;
		ПолеДиаграммы.ПропускатьБазовоеЗначение = Ложь;
		Возврат ПолеДиаграммы;
	КонецЕсли;
		
	ИзмерениеСтроки = СписокПериодов.Индекс(СписокПериодов.НайтиПоЗначению(СтруктураПараметров.Серии));
	ИзмерениеКолонки = ?(СтруктураПараметров.Точки = Неопределено, 4, СписокПериодов.Индекс(СписокПериодов.НайтиПоЗначению(СтруктураПараметров.Точки)));
	
	Если ИзмерениеСтроки>ИзмерениеКолонки Тогда
		Предупреждение("Ошибочная комбинация Серии: """+СписокПериодов[ИзмерениеСтроки].Представление+ """, Точки: """+СписокПериодов[ИзмерениеКолонки].Представление+""".");
		ТекИзм = ИзмерениеСтроки;
		ИзмерениеКолонки = ИзмерениеСтроки;
		ИзмерениеСтроки = ТекИзм;
		Возврат ПолеДиаграммы;
	КонецЕсли;
	
	УстановитьОсновныеПараметры();
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров, 2);
	
	Возврат ПолеДиаграммы;
	
КонецФункции // СформироватьДиаграмму()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

//Процедура индикации отоборов в заголовке отчета
// Параметры
//  ТаблицаЗаголовка – Табличное поле заголовка
Процедура ОбработкаТаблицыЗаголовка(ТаблицаЗаголовка) Экспорт
	
	СтруктураЗаголовка = отПолучитьСтруктуруЗаголовкаОтчета(ЭтотОбъект);
	
	СтрокаОтбора = ТаблицаЗаголовка.Найти("Отбор", "ИмяСтроки");
	Если НЕ СтрокаОтбора = Неопределено Тогда
		СтрокаОтбора.Текст = "Отбор: " + СтруктураЗаголовка.Отбор;
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяОтчетаДвиженийСОстатками = "ДвиженияПартийСОстатками";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();

СписокПериодов = Новый СписокЗначений();
СписокПериодов.Добавить("ПериодГод");
СписокПериодов.Добавить("ПериодКвартал");
СписокПериодов.Добавить("ПериодМесяц");
СписокПериодов.Добавить("ПериодНеделя");
СписокПериодов.Добавить("ПериодДень");

СтруктураТипаКолонки = Новый Структура;
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодКвартал",	"Число");
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодМесяц",		"Дата");
//СтруктураТипаКолонки.Вставить("ПериодГод_ПериодНеделя",		"Дата");
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодНеделя",		"Число");
СтруктураТипаКолонки.Вставить("ПериодГод_ПериодДень",		"Дата");
СтруктураТипаКолонки.Вставить("ПериодКвартал_ПериодМесяц",  "Число");
СтруктураТипаКолонки.Вставить("ПериодКвартал_ПериодНеделя", "Число");
СтруктураТипаКолонки.Вставить("ПериодКвартал_ПериодДень",  	"Число");
СтруктураТипаКолонки.Вставить("ПериодМесяц_ПериодНеделя",  	"Число");
СтруктураТипаКолонки.Вставить("ПериодМесяц_ПериодДень",  	"Число");
СтруктураТипаКолонки.Вставить("ПериодНеделя_ПериодДень",  	"Число");

ПоследовательностиОтчета = Новый Структура();
ПоследовательностиОтчета.Вставить("ГраницыВзаиморасчетов");

#КонецЕсли
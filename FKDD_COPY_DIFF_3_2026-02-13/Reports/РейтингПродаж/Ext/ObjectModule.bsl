#Если Клиент Тогда
// Модуль отчета
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверяет сортировку на корректность
//
// Без параметров
// 
Процедура КорректностьСортировки()
		
	Сортировка = "";
	Сортировка2 = "";
	
	Если РежимСортировки = 1 Тогда	
		Сортировка = "СуммаТорговойНаценкиУпр";
		Сортировка2 = "СуммаТорговойНаценки";
	ИначеЕсли РежимСортировки = 2 Тогда	
		Сортировка = "СуммаПродажСоСкидкойУпр";
		Сортировка2 = "СуммаПродажСоСкидкой";
	ИначеЕсли РежимСортировки = 3 Тогда	
		Сортировка = "СебестоимостьУпр";
		Сортировка2 = "Себестоимость";
	ИначеЕсли РежимСортировки = 4 Тогда	
		Сортировка = "КоличествоОсн";
		Сортировка2 = "Количество";
	ИначеЕсли РежимСортировки = 5 Тогда	
		Сортировка = "СуммаСкидки";		
		Сортировка2 = "СуммаСкидки";
	ИначеЕсли РежимСортировки = 6 Тогда	
		Сортировка = "ПроцентСкидки";
		Сортировка2 = "ПроцентСкидки";
	Иначе
		Возврат;
	КонецЕсли;
	
	Показатель = Показатели.Найти(Сортировка, "Имя");
	Показатель2 = Показатели.Найти(Сортировка2, "Имя");
	
	Если НЕ Показатель.Использование И НЕ Показатель2.Использование Тогда
		Показатель.Использование = Истина;
	Конецесли;
	
КонецПроцедуры // КорректностьСортировки()

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
		
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	//ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Менеджер","Менеджер","Менеджер");
	//ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()
	
// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	КорректностьСортировки();
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()
	
// формирует отчет в виде сводной таблицы
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  ДокументРезультат - ТабличныйДокумент 
//
Функция СформироватьСводнуюТаблицу(ДокументРезультат=Неопределено) Экспорт
	
	КорректностьСортировки();
	отСформироватьСводнуюТаблицу(ЭтотОбъект, ДокументРезультат);
		
	Возврат ДокументРезультат;
		
КонецФункции // СформироватьСводнуюТаблицу()
	
// формирует диаграмму
//
// Параметры
//  ПолеДиаграммы – Диаграмма
//  СтруктураПараметров - структура
//
// Возвращаемое значение:
//  ПолеДиаграммы - Диаграмма
//
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	КорректностьСортировки();
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров);
		
	Возврат ПолеДиаграммы;
		
КонецФункции // СформироватьДиаграмму()
	
// формирует отчет в виде сводной диаграммы
//
// Параметры
//  ПолеСводнойДиаграммы – СводнаяДиаграмма
//
// Возвращаемое значение:
//  ПолеСводнойДиаграммы - СводнаяДиаграмма
//
Функция СформироватьСводнуюДиаграмму(ПолеСводнойДиаграммы=Неопределено) Экспорт
	
	КорректностьСортировки();
	отСформироватьСводнуюДиаграмму(ЭтотОбъект, ПолеСводнойДиаграммы);
		
	Возврат ПолеСводнойДиаграммы;
		
КонецФункции // СформироватьСводнуюДиаграмму()
	
// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт
		
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
		
	// Если необходимо, добавим другие или переустановим строки форматирования полей
		
	Возврат СтруктураФорматаПолей;
		
КонецФункции	//	СтруктураФорматированияПолей()

// Функция возвращает структуру сортировки поля
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураСортировкиПоля - структура
//
Функция СтруктураСортировки() Экспорт
	
	СтруктураСортировкиПоля = Новый Структура("Описание, НаправлениеСортировки");
	Если НаправлениеСорт = 0 Тогда
		СтруктураСортировкиПоля.НаправлениеСортировки = НаправлениеСортировки.Убыв;
	Иначе
		СтруктураСортировкиПоля.НаправлениеСортировки = НаправлениеСортировки.Возр;
	КонецЕсли;
	
	Сортировка = "";
	Сортировка2 = "";
	Если РежимСортировки = 1 Тогда	
		Сортировка = "СуммаТорговойНаценкиУпрОборот";	
		Сортировка2 = "СуммаТорговойНаценкиОборот";
	ИначеЕсли РежимСортировки = 2 Тогда	
		Сортировка = "СуммаПродажСоСкидкойУпрОборот";
		Сортировка2 = "СуммаПродажСоСкидкойОборот";
	ИначеЕсли РежимСортировки = 3 Тогда	
		Сортировка = "СебестоимостьУпрОборот";
		Сортировка2 = "СебестоимостьОборот";
	ИначеЕсли РежимСортировки = 4 Тогда	
		Сортировка = "КоличествоОборот";
		Сортировка2 = "КоличествоОснОборот";
	ИначеЕсли РежимСортировки = 5 Тогда	
		Сортировка = "СуммаСкидкиОборот";
	ИначеЕсли РежимСортировки = 6 Тогда	
		Сортировка = "ПроцентСкидкиОборот";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Сортировка) Тогда
		Для Каждого ТекПоказатель Из Показатели Цикл
			Если Сортировка = ТекПоказатель.Имя И (не ТекПоказатель.Использование) Тогда
				Сортировка = "";
			КонецЕсли;
			Если Сортировка2 = ТекПоказатель.Имя И (не ТекПоказатель.Использование) Тогда
				Сортировка2 = "";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Сортировка) Тогда
		СтруктураСортировкиПоля.Описание = Сортировка;
	ИначеЕсли НЕ ПустаяСтрока(Сортировка2) Тогда
		СтруктураСортировкиПоля.Описание = Сортировка2;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтруктураСортировкиПоля;
	
КонецФункции // СтруктураФорматированияПолей()
	
//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	
ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
#КонецЕсли
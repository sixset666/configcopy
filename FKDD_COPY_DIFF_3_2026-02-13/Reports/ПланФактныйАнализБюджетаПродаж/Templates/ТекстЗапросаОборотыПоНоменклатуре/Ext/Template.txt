ВЫБРАТЬ РАЗРЕШЕННЫЕ
	СУММА(ВнутреннийЗапрос.КоличествоПланОборот) КАК КоличествоПлановое,
	СУММА(ВнутреннийЗапрос.СуммаРегПланОборот) КАК СуммаРегПлановое,
	СУММА(ВнутреннийЗапрос.СуммаУпрПланОборот) КАК СуммаУпрПлановое,
	СУММА(ВнутреннийЗапрос.СуммаНДСПланОборот) КАК СуммаНДСПлановое,
	СУММА(ВнутреннийЗапрос.СебестоимостьУпрПланОборот) КАК СебестоимостьУпрПлановое,
	
	СУММА(ВнутреннийЗапрос.КоличествоФактОборот) КАК КоличествоФактическое,
	СУММА(ВнутреннийЗапрос.СуммаРегФактОборот) КАК СуммаРегФактическое,
	СУММА(ВнутреннийЗапрос.СуммаУпрФактОборот) КАК СуммаУпрФактическое,
	СУММА(ВнутреннийЗапрос.СуммаНДСФактОборот) КАК СуммаНДСФактическое,
	СУММА(ВнутреннийЗапрос.СебестоимостьУпрФактОборот) КАК СебестоимостьУпрФактическое,
	
	СУММА(ВнутреннийЗапрос.КоличествоФактОборот) - СУММА(ВнутреннийЗапрос.КоличествоПланОборот) КАК КоличествоОтклонение,
	СУММА(ВнутреннийЗапрос.СуммаРегФактОборот) - СУММА(ВнутреннийЗапрос.СуммаРегПланОборот) КАК СуммаРегОтклонение,
	СУММА(ВнутреннийЗапрос.СуммаУпрФактОборот) - СУММА(ВнутреннийЗапрос.СуммаУпрПланОборот) КАК СуммаУпрОтклонение,
	СУММА(ВнутреннийЗапрос.СуммаНДСФактОборот) - СУММА(ВнутреннийЗапрос.СуммаНДСПланОборот) КАК СуммаНДСОтклонение,
	СУММА(ВнутреннийЗапрос.СебестоимостьУпрФактОборот) - СУММА(ВнутреннийЗапрос.СебестоимостьУпрПланОборот) КАК СебестоимостьУпрОтклонение,
	
	0 КАК КоличествоПроцентОтклонения,
	0 КАК СуммаРегПроцентОтклонения,
	0 КАК СуммаУпрПроцентОтклонения,
	0 КАК СуммаНДСПроцентОтклонения,
	0 КАК СебестоимостьУпрПроцентОтклонения
	
	//СВОЙСТВА

{ВЫБРАТЬ 
#Область ИзмеренияПостроителяДляСвойств
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Номенклатура.ТипНоменклатуры.* КАК ТипНоменклатуры,
	Номенклатура.* КАК Номенклатура, 	
#КонецОбласти
	Период КАК Период
	//СВОЙСТВА}
    
ИЗ 
	(
	 ВЫБРАТЬ
	 	ЕСТЬNULL(БюджетПродажОбороты.КоличествоОборот      , 0) КАК КоличествоПланОборот,
	 	ЕСТЬNULL(БюджетПродажОбороты.СуммаРегОборот        , 0) КАК СуммаРегПланОборот,
	 	ЕСТЬNULL(БюджетПродажОбороты.СуммаУпрОборот        , 0) КАК СуммаУпрПланОборот,
	 	ЕСТЬNULL(БюджетПродажОбороты.СуммаНДСОборот        , 0) КАК СуммаНДСПланОборот,
	 	ЕСТЬNULL(БюджетПродажОбороты.СебестоимостьУпрОборот, 0) КАК СебестоимостьУпрПланОборот,
	 	
	 	ЕСТЬNULL(ПродажиОбороты.КоличествоОборот      , 0) КАК КоличествоФактОборот,
	 	ЕСТЬNULL(ПродажиОбороты.СуммаОборот           , 0) КАК СуммаРегФактОборот,
	 	ЕСТЬNULL(ПродажиОбороты.СуммаУпрОборот        , 0) КАК СуммаУпрФактОборот,
	 	ЕСТЬNULL(ПродажиОбороты.СуммаНДСОборот        , 0) КАК СуммаНДСФактОборот,
	 	ЕСТЬNULL(ПродажиОбороты.СебестоимостьУпрОборот, 0) КАК СебестоимостьУпрФактОборот,
		
	 	ЕСТЬNULL(БюджетПродажОбороты.ПодразделениеКомпании, ПродажиОбороты.ПодразделениеКомпании) КАК ПодразделениеКомпании,
	 	ЕСТЬNULL(БюджетПродажОбороты.Номенклатура, ПродажиОбороты.Номенклатура) КАК Номенклатура,
	 	ЕСТЬNULL(БюджетПродажОбороты.Период, НачалоПериода(ПродажиОбороты.Период, Месяц)) КАК Период
	
	 ИЗ
	 РегистрНакопления.БюджетПродаж.Обороты(&ДатаНач, &ДатаКон, Месяц, Номенклатура ССЫЛКА Справочник.Номенклатура и СценарийПланирования = &СценарийПланирования{
		ПодразделениеКомпании.Организация.* КАК Организация, 
		ПодразделениеКомпании.* КАК ПодразделениеКомпании, 
		Номенклатура.ТипНоменклатуры.* КАК ТипНоменклатуры,
		Номенклатура.* КАК Номенклатура
		}) КАК БюджетПродажОбороты
	   
	 ПОЛНОЕ СОЕДИНЕНИЕ
		
	 РегистрНакопления.Продажи.Обороты(&ДатаНач, &ДатаКон, Месяц, {
		ПодразделениеКомпании.Организация.* КАК Организация, 
		ПодразделениеКомпании.* КАК ПодразделениеКомпании, 
		Номенклатура.ТипНоменклатуры.* КАК ТипНоменклатуры,
		Номенклатура.* КАК Номенклатура}) КАК ПродажиОбороты 
	    
	 ПО БюджетПродажОбороты.ПодразделениеКомпании = ПродажиОбороты.ПодразделениеКомпании И БюджетПродажОбороты.Номенклатура = ПродажиОбороты.Номенклатура И БюджетПродажОбороты.Период = НачалоПериода(ПродажиОбороты.Период, Месяц)
	) КАК ВнутреннийЗапрос
	//СОЕДИНЕНИЯ
	     	    
СГРУППИРОВАТЬ ПО
	ПодразделениеКомпании, 
	Номенклатура
	//СГРУППИРОВАТЬПО
	
{ГДЕ 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Номенклатура.ТипНоменклатуры.* КАК ТипНоменклатуры,
	Номенклатура.* КАК Номенклатура
	 //СВОЙСТВА}
    
{УПОРЯДОЧИТЬ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Номенклатура.ТипНоменклатуры.* КАК ТипНоменклатуры,
	Номенклатура.* КАК Номенклатура,
	Период КАК Период
 //СВОЙСТВА}
    
{ИТОГИ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	Номенклатура.ТипНоменклатуры.* КАК ТипНоменклатуры,
	Номенклатура.* КАК Номенклатура,
	Период КАК Период
    //СВОЙСТВА}
    
ИТОГИ  
	СУММА(КоличествоПлановое),
	СУММА(СуммаРегПлановое),
	СУММА(СуммаУпрПлановое),
	СУММА(СуммаНДСПлановое),
	СУММА(СебестоимостьУпрПлановое),

	СУММА(КоличествоФактическое),
	СУММА(СуммаРегФактическое),
	СУММА(СуммаУпрФактическое),
	СУММА(СуммаНДСФактическое),
	СУММА(СебестоимостьУпрФактическое),
	
	СУММА(КоличествоОтклонение),
	СУММА(СуммаРегОтклонение),
	СУММА(СуммаУпрОтклонение),
	СУММА(СуммаНДСОтклонение),
	СУММА(СебестоимостьУпрОтклонение),
	
	ВЫБОР КОГДА СУММА(КоличествоПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(КоличествоОтклонение) / СУММА(КоличествоПлановое) * 100 КОНЕЦ КАК КоличествоПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаРегПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаРегОтклонение) / СУММА(СуммаРегПлановое) * 100 КОНЕЦ КАК СуммаРегПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаУпрПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаУпрОтклонение) / СУММА(СуммаУпрПлановое) * 100 КОНЕЦ КАК СуммаУпрПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаНДСПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаНДСОтклонение) / СУММА(СуммаНДСПлановое) * 100 КОНЕЦ КАК СуммаНДСПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СебестоимостьУпрПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СебестоимостьУпрОтклонение) / СУММА(СебестоимостьУпрПлановое) * 100 КОНЕЦ КАК СебестоимостьУпрПроцентОтклонения

	//ИТОГИСВОЙСТВА
ПО 
	ОБЩИЕ
			
АВТОУПОРЯДОЧИВАНИЕ
# Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ОформлениеШапки, ОформлениеСтроки; // оформления

// раскрашивает область согласно шаблону
// Параметры:
//	Область - ТабличныйДокумент - область табличного документа, которая раскрашивается;
//	Шаблон - Область - содержит шаблон цвета текста и фона.
Процедура РаскраситьОбластьПоШаблону(Область, Шаблон, ПервыйСтолбец = 2)	
	Область.Область(1, ПервыйСтолбец, 1, 12).ЦветТекста = Шаблон.ЦветТекста;
	Область.Область(1, ПервыйСтолбец, 1, 12).ЦветФона   = Шаблон.ЦветФона;
КонецПроцедуры// РаскраситьОбластьПоШаблону(Область, Шаблон)	

// выбирает данные по разделу информации
// Параметры:
//  Раздел - Строка - НазваниеРаздела
//  Контрагент - СправочникСсылка.Контрагент - контрагент, по которому собирается информация;
//  КонтактноеЛицо - СправочникСсылка.КонтактноеЛицо - контактное лицо, по которому собирается информация;
// Возвращаемое значение
//  ТаблицаЗначений - таблица, в которую выгружены результаты запроса
Функция ТаблицаДанныхРаздела(Раздел, Контрагент, КонтактноеЛицо = Неопределено)
	ВозвращаемоеЗначение = Новый ТаблицаЗначений();
	Если Раздел = "РегистрационнаяИнформация" Тогда
		НетСтрок = Истина;
		ВозвращаемоеЗначение.Колонки.Добавить("ИННКПП");
		ВозвращаемоеЗначение.Колонки.Добавить("КодИМНС");
		ВозвращаемоеЗначение.Колонки.Добавить("КодПоОКПО");
		СтрокаВозвращаемоеЗначение = ВозвращаемоеЗначение.Добавить();
		Если НЕ ОбЗначениеНеЗаполнено(Контрагент.ИНН) Тогда
			ИННКПП = СокрЛП(Контрагент.ИНН);
			Если НЕ ОбЗначениеНеЗаполнено(Контрагент.КПП) Тогда
				ИННКПП = ИННКПП + " / "+ СокрЛП(Контрагент.КПП);
			КонецЕсли;
			НетСтрок = Ложь;
		КонецЕсли;
		Если НЕ ОбЗначениеНеЗаполнено(Контрагент.КодИМНС) Тогда
			НетСтрок = Ложь;
		КонецЕсли;
		Если НЕ ОбЗначениеНеЗаполнено(Контрагент.КодПоОКПО) Тогда
			НетСтрок = Ложь;
		КонецЕсли;
		Если НетСтрок Тогда
			ВозвращаемоеЗначение.Очистить();
		Иначе
			СтрокаВозвращаемоеЗначение.ИННКПП = ИННКПП;
			СтрокаВозвращаемоеЗначение.КодИМНС = Контрагент.КодИМНС;
			СтрокаВозвращаемоеЗначение.КодПоОКПО = Контрагент.КодПоОКПО;
		КонецЕсли;
	ИначеЕсли Раздел = "РегистрационнаяИнформацияЧастноеЛицо" Тогда
		НетСтрок = Истина;
		ВозвращаемоеЗначение.Колонки.Добавить("ДатаРождения");
		Если НЕ ОбЗначениеНеЗаполнено(Контрагент.ДатаРождения) Тогда
        	СтрокаВозвращаемоеЗначение = ВозвращаемоеЗначение.Добавить();
			СтрокаВозвращаемоеЗначение.ДатаРождения = Формат(Контрагент.ДатаРождения, "ДЛФ = Д");
		КонецЕсли;
	ИначеЕсли Раздел = "ДополнительнаяИнформация" Тогда
		ВозвращаемоеЗначение.Колонки.Добавить("Комментарий");			
		Если КонтактноеЛицо = Неопределено Тогда
			Комментарий = Контрагент.Комментарий;
		Иначе
			Комментарий = КонтактноеЛицо.Комментарий;
		КонецЕсли;
		Если НЕ ОбЗначениеНеЗаполнено(Комментарий) Тогда
//			СтруктураПараметров.Вставить("Комментарий", Комментарий);
			СтрокаВозвращаемоеЗначение = ВозвращаемоеЗначение.Добавить();
			СтрокаВозвращаемоеЗначение.Комментарий = Комментарий;
		КонецЕсли;
	Иначе	
		Запрос = Новый Запрос;
		// создадим текст запроса
		Если Раздел = "КонтактнаяИнформация" Тогда
			ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Тип,
			|	КонтактнаяИнформация.Вид,
			|	КонтактнаяИнформация.Представление,
			|	КонтактнаяИнформация.Комментарий,
			|	КонтактнаяИнформация.ЗначениеПоУмолчанию
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Объект = &Объект
			|	ОтборПоОсновномуРеквизиту";
		ИначеЕсли Раздел = "КонтактныеЛица" Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КонтактныеЛицаКонтрагентов.Ведомый КАК Ссылка,
			|	ВЫБОР
			|		КОГДА КонтактныеЛицаКонтрагентов.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
			|			ТОГДА КонтактныеЛицаКонтрагентов.Должность
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Должности.Неопределена)
			|	КОНЕЦ КАК Должность,
			|	КонтактныеЛицаКонтрагентов.Ведомый.ДеньРождения КАК ДатаРождения
			|ИЗ
			|	РегистрСведений.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
			|ГДЕ
			|	КонтактныеЛицаКонтрагентов.Ведущий = &Контрагент
			|	И КонтактныеЛицаКонтрагентов.Ведомый ССЫЛКА Справочник.Контрагенты";
		ИначеЕсли Раздел = "Кураторы" Тогда
			ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Кураторы.Куратор,
			|	Кураторы.Договор
			|ИЗ
			|	РегистрСведений.Кураторы КАК Кураторы
			|ГДЕ
			|	Кураторы.Контрагент = &Контрагент";
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
		ИначеЕсли Раздел = "БанковскиеСчета" Тогда
			ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БанковскиеСчета.Ссылка,
			|	БанковскиеСчета.ВалютаДенежныхСредств,
			|	БанковскиеСчета.НомерСчета,
			|	БанковскиеСчета.Банк.Ссылка КАК БанкСсылка,
			|	БанковскиеСчета.Банк.КоррСчет КАК КоррСчет,
			|	БанковскиеСчета.Банк.Код КАК БИК,
			|	ВЫБОР
			|		КОГДА БанковскиеСчета.Ссылка = &ОсновнойРеквизит
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЗначениеПоУмолчанию
			|ИЗ
			|	Справочник.БанковскиеСчета КАК БанковскиеСчета
			|ГДЕ
			|	БанковскиеСчета.Владелец = &Контрагент
			|	ОтборПоОсновномуРеквизиту
			|УПОРЯДОЧИТЬ ПО
			|	ЗначениеПоУмолчанию УБЫВ";
			Запрос.УстановитьПараметр("ОсновнойРеквизит", Контрагент.ОсновнойБанковскийСчет);
		ИначеЕсли Раздел = "ДоговорыКонтрагента" Тогда
			ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	ДоговорыВзаиморасчетов.Ссылка КАК Ссылка,
			               |	ДоговорыВзаиморасчетов.Наименование КАК Наименование,
			               |	ДоговорыВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
			               |	ДоговорыВзаиморасчетов.ВидОплаты КАК ВидОплаты,
			               |	ДоговорыВзаиморасчетов.ПроцентПредоплаты КАК ПроцентПредоплаты,
			               |	ДоговорыВзаиморасчетов.Основной КАК ЗначениеПоУмолчанию
			               |ИЗ
			               |	Справочник.ДоговорыВзаиморасчетов КАК ДоговорыВзаиморасчетов
			               |ГДЕ
			               |	ДоговорыВзаиморасчетов.Владелец = &Контрагент
			               |	И ДоговорыВзаиморасчетов.ЭтоГруппа = ЛОЖЬ
			               |	ОтборПоОсновномуРеквизиту
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	ЗначениеПоУмолчанию УБЫВ";
		ИначеЕсли Раздел = "ПодтверждающиеДокументы" Тогда
			ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	ПодтверждающиеДокументы.Ссылка,
			               |	ПодтверждающиеДокументы.Наименование,
			               |	ПодтверждающиеДокументы.Серия,
			               |	ПодтверждающиеДокументы.Номер,
			               |	ПодтверждающиеДокументы.КемВыдан,
			               |	ПодтверждающиеДокументы.ДатаВыдачи
			               |ИЗ
			               |	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
			               |ГДЕ
			               |	ПодтверждающиеДокументы.Владелец = &Объект";
		ИначеЕсли Раздел = "ДисконтныеКарты" Тогда
			ТекстЗапроса = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Карточки.Ссылка КАК Карточка,
			|	СУММА(ЕСТЬNULL(НакоплениеСумм.Сумма, 0)) КАК Сумма
			|ИЗ
			|   Справочник.Карточки КАК Карточки
			|ЛЕВОЕ ВНЕШНЕЕ СОЕДИНЕНИЕ
			|   РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
			|ПО
			|   НакоплениеСумм.Карточка = Карточки.Ссылка
			|ГДЕ
			|   Карточки.Объект = &Контрагент
			|СГРУППИРОВАТЬ ПО
			|	Карточки.Ссылка
			|";
		КонецЕсли;
		
		// установим параметры запроса и при необходимости корректируем его текст
		
		// если есть отбор по основным значениям раздела
		Если ВыводитьТолькоОсновные Тогда
			Если Найти(ТекстЗапроса, "ОтборПоОсновномуРеквизиту")>0 Тогда
				Если Раздел = "БанковскиеСчета" Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОтборПоОсновномуРеквизиту", "И БанковскиеСчета.Ссылка = &ОсновнойРеквизит");
				ИначеЕсли Раздел = "КонтактнаяИнформация" Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОтборПоОсновномуРеквизиту", "И КонтактнаяИнформация.ЗначениеПоУмолчанию = &ОсновнойРеквизит");
					Запрос.УстановитьПараметр("ОсновнойРеквизит", Истина);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОтборПоОсновномуРеквизиту", "");
		КонецЕсли;
		
		// у контактной информации и подтверждающих документов
		// может быть отбор или по контагенту, или по контактной информации
		Если (Раздел = "КонтактнаяИнформация") ИЛИ (Раздел = "ПодтверждающиеДокументы") Тогда
			Если КонтактноеЛицо = Неопределено Тогда
				Запрос.УстановитьПараметр("Объект", Контрагент);
			Иначе
				Запрос.УстановитьПараметр("Объект", КонтактноеЛицо);
			КонецЕсли;	
		Иначе
			Запрос.УстановитьПараметр("Контрагент", Контрагент);					   
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		ВозвращаемоеЗначение = Запрос.Выполнить().Выгрузить();
		
		// дополнительные преобразования 
		Если ВозвращаемоеЗначение.Количество() > 0 Тогда
			// контактную информацию надо отсортировать по номеру типа и умолчанию			
			Если Раздел = "КонтактнаяИнформация" Тогда
				НаборЗаписейКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
				НаборЗаписейКИ.Загрузить(ВозвращаемоеЗначение);
				киСортироватьНаборЗаписей(НаборЗаписейКИ);
				ВозвращаемоеЗначение = НаборЗаписейКИ.Выгрузить();
				Для Каждого СтрокаТаблицы Из ВозвращаемоеЗначение Цикл
					Если Не обЗначениеНеЗаполнено(СтрокаТаблицы.Комментарий) Тогда
						СтрокаТаблицы.Представление = СтрокаТаблицы.Представление + " (" + СтрокаТаблицы.Комментарий + ")";
					КонецЕсли;
				КонецЦикла;
			// для подтверждающих документов сформируем дополнительную колонку "Строка", куда будет добавлено представление 
			ИначеЕсли Раздел = "ПодтверждающиеДокументы" Тогда
				ВозвращаемоеЗначение.Колонки.Добавить("Строка");
				Для Каждого СтрокаТаблицы Из ВозвращаемоеЗначение Цикл
					СтрокаТаблицы.Строка = " " + (ВозвращаемоеЗначение.Индекс(СтрокаТаблицы)+1) + ". " + СокрЛП(СтрокаТаблицы.Наименование)
															  + ?(обЗначениеНеЗаполнено(СтрокаТаблицы.Серия),""," серия "+СокрЛП(СтрокаТаблицы.Серия))
															  + ?(обЗначениеНеЗаполнено(СтрокаТаблицы.Номер),""," №"+СокрЛП(СтрокаТаблицы.Номер))
															  + ?(обЗначениеНеЗаполнено(СтрокаТаблицы.КемВыдан),""," выдан "+СокрЛП(СтрокаТаблицы.КемВыдан))
															  + ?(обЗначениеНеЗаполнено(СтрокаТаблицы.ДатаВыдачи),""," дата выдачи "+Формат(СтрокаТаблицы.ДатаВыдачи,"ДЛФ = Д"));
				КонецЦикла;
			// для контактных лиц соберем кураторов
			ИначеЕсли Раздел = "КонтактныеЛица" Тогда
				ВозвращаемоеЗначение.Колонки.Добавить("СтрокаКураторы");
				Для Каждого СтрокаТаблицы Из ВозвращаемоеЗначение Цикл
					ТаблицаКураторов = ТаблицаДанныхРаздела("Кураторы", Контрагент, СтрокаТаблицы.Ссылка);
	    			СтрокаКураторов = "";
	    			Для Каждого СтрокаТаблицыКураторов Из ТаблицаКураторов Цикл
    					Если Не СтрокаКураторов = "" Тогда
    						СтрокаКураторов = СтрокаКураторов + ", ";
    					КонецЕсли;
    					СтрокаКураторов = СтрокаКураторов + СокрЛП(СтрокаТаблицыКураторов.Куратор.Наименование);
					КонецЦикла;
					СтрокаТаблицы.СтрокаКураторы = СтрокаКураторов;
				КонецЦикла;										  
			КонецЕсли;		
		КонецЕсли;
	КонецЕсли;
	Возврат ВозвращаемоеЗначение;
КонецФункции// ТаблицаДанныхРаздела()	

// формирует на основе текста запроса таблицу и выводит её в табличный документ
// Параметры:
//	ТаблицаДанных - ТаблицаЗначений - таблица значений, на основании которой формируется табличный документ;
//	ТабличныйДокумент - табличный документ, в который выводится таблица;
//	Макет - макет, с помощью которого формируются строки таблицы;
//	Раздел - Строка - название раздела информации о контрагенте, в который выводится таблица;
//	Уровень - Число - уровень группировки таблицы в документе;
//	ЕстьШапка - Булево - указывает, есть ли в таблице шапка;
//  РаскраситьТаблицу - Число - указывает, какие части таблицы раскрасить. Имеет значения:
//                    "0" - не раскрашивать ни шапку, ни строки таблицы;
//                    "1" - раскрасить шапку таблицы;
//                    "2" - раскрасить строки таблицы;
//                    "3" - раскрасить и шапку, и строки таблицы;
Процедура ВывестиТаблицей(ТаблицаДанных, ТабличныйДокумент, Макет, Раздел, Уровень, РаскраситьТаблицу = 3, ЕстьШапка = Истина)
	Если ЕстьШапка Тогда
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка"+Раздел);
		Если (РаскраситьТаблицу = 1) Или (РаскраситьТаблицу = 3) Тогда
			РаскраситьОбластьПоШаблону(ОбластьШапка, ОформлениеШапки, Уровень + 1);	
		КонецЕсли;	
		ТабличныйДокумент.Вывести(ОбластьШапка, Уровень);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицыДанных Из ТаблицаДанных Цикл
		Попытка
			ПоУмолчанию = СтрокаТаблицыДанных.ЗначениеПоУмолчанию;
		Исключение
			ПоУмолчанию = Ложь;
		КонецПопытки;

		ОбластьСтроки = Макет.ПолучитьОбласть("Строка" + Раздел + ?(ПоУмолчанию, "ПоУмолчанию", ""));
		ОбластьСтроки.Параметры.Заполнить(СтрокаТаблицыДанных);
		Если РаскраситьТаблицу > 1 Тогда
			РаскраситьОбластьПоШаблону(ОбластьСтроки, ОформлениеСтроки, Уровень + 1);	
		КонецЕсли;	
		ТабличныйДокумент.Вывести(ОбластьСтроки, Уровень);
	КонецЦикла;	
КонецПроцедуры// ВывестиТаблицей(Объект, ТабличныйДокумент, Макет, Раздел)	

// Формирует и восстанавливает настройки отчета
//
// Без параметров
//
Процедура ЗаполнитьНастойкиОтчета() Экспорт
	СписокНастроек.Очистить();
	СписокНастроек.Добавить("РегистрационнаяИнформация","Регистрационная информация"               , Истина);
	СписокНастроек.Добавить("КонтактнаяИнформация","Контактная информация"                         , Истина);
	СписокНастроек.Добавить("КонтактныеЛица","Контактные лица"                                     , Истина);
	СписокНастроек.Добавить("КонтактнаяИнформацияКЛ","Контактная информация контактных лиц"        , Истина);
	СписокНастроек.Добавить("ПодтверждающиеДокументыКЛ","Подтверждающие документы контактных лиц"  , Истина);
	СписокНастроек.Добавить("ДополнительнаяИнформацияКЛ","Дополнительная информация контактных лиц", Истина);
	СписокНастроек.Добавить("Кураторы","Кураторы"                                                  , Истина);
	СписокНастроек.Добавить("БанковскиеСчета","Банковские счета"                                   , Истина);	
	СписокНастроек.Добавить("ДоговорыКонтрагента","Договоры контрагента"                           , Истина);	
	СписокНастроек.Добавить("ПодтверждающиеДокументы","Подтверждающие документы"                   , Истина);	
	СписокНастроек.Добавить("ДисконтныеКарты","Дисконтные карты"                                   , Истина);	
	СписокНастроек.Добавить("ДополнительнаяИнформация","Дополнительная информация"                 , Истина);	
	
	ВыводитьДопКонтИнф = Истина;
	ВыводитьТолькоОсновные = Ложь;
	
	СтруктураНастроекОтчета = ВосстановитьЗначение("Отчет_КарточкаКлиента_Настройки");
	Если Не СтруктураНастроекОтчета = Неопределено Тогда
		ЗначениеНастройки = Неопределено;
		Для Каждого ЭлементСписка Из СписокНастроек Цикл
			Если СтруктураНастроекОтчета.Свойство(ЭлементСписка.Значение, ЗначениеНастройки) Тогда
				ЭлементСписка.Пометка = ЗначениеНастройки;
			КонецЕсли;	
		КонецЦикла;	
		Если СтруктураНастроекОтчета.Свойство("ВыводитьТолькоОсновные", ЗначениеНастройки) Тогда
			ВыводитьТолькоОсновные = ЗначениеНастройки;
		КонецЕсли;	
	КонецЕсли;			
КонецПроцедуры// ЗаполнитьНастойкиОтчета() Экспорт

// Сохраняет настройки отчета
//
// Без параметров
//
Процедура СохранитьНастойкиОтчета() Экспорт
	
	СтруктураНастроекОтчета = Новый Структура();
	Для Каждого ЭлементСписка Из СписокНастроек Цикл
		СтруктураНастроекОтчета.Вставить(ЭлементСписка.Значение, ЭлементСписка.Пометка);
	КонецЦикла;
	СтруктураНастроекОтчета.Вставить("ВыводитьТолькоОсновные", ВыводитьТолькоОсновные);
	СохранитьЗначение("Отчет_КарточкаКлиента_Настройки", СтруктураНастроекОтчета);
КонецПроцедуры// СохранитьНастойкиОтчета() Экспорт

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// формирует отчет в виде табличного документа
Функция СформироватьТабличныйДокумент (ДокРезультат=Неопределено) Экспорт
    Если ТипЗнч(ДокРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокРезультат) <> Тип("ТабличныйДокумент") Тогда
		  ДокРезультат = Новый ТабличныйДокумент();
	Иначе ДокРезультат.Очистить();
	КонецЕсли;
	
	Если Контрагент.Пустая() Тогда
		Предупреждение("Поле ""Контрагент"" не заполнено!");
		Возврат ДокРезультат;
	КонецЕсли;
	
	// получим макеты для результата и оформления
	МакетОтчета = ПолучитьМакет("Макет");
	МакетПараметровОтчета = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	ОформлениеШапки  = МакетПараметровОтчета.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеСтроки = МакетПараметровОтчета.ПолучитьОбласть("ОформлениеИзмерений").Область(7,1);
	
	ОбластьОтступа0 = МакетОтчета.ПолучитьОбласть("СтрокаОтступа0");
	ОбластьОтступа1 = МакетОтчета.ПолучитьОбласть("СтрокаОтступа1");
	ОбластьЗаголовкаРаздела = МакетОтчета.ПолучитьОбласть("НазваниеРаздела");
	
	//Построим заголовок
	ОбластьЗаголовка = МакетОтчета.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовка.Параметры.Контрагент = Контрагент;
	МояОбласть = ДокРезультат.Вывести(ОбластьЗаголовка);
	
	ДокРезультат.Вывести(ОбластьОтступа0);
		
	//Основные сведения
	СписокОсновныхСведений = Новый СписокЗначений();
	СписокОсновныхСведений.Добавить(Контрагент.НаименованиеПолное, "Полное наименование");
	СписокОсновныхСведений.Добавить(Контрагент.ВидКонтрагента, "Вид контрагента");
	СписокОсновныхСведений.Добавить(Контрагент.ФормаСобственности, "Форма собственности");
	СписокОсновныхСведений.Добавить(Контрагент.Сотрудник, "Сотрудник");
	СписокОсновныхСведений.Добавить(Контрагент.Филиал, "Филиал");
	
	ОбластьЗаголовка = МакетОтчета.ПолучитьОбласть("СтрокаОсновныеСведения");
	Для Каждого ЭлементСпискаОсновныхСведений Из СписокОсновныхСведений Цикл
		Если Не обЗначениеНеЗаполнено(ЭлементСпискаОсновныхСведений.Значение) Тогда
			Если ЭлементСпискаОсновныхСведений.Представление = "Полное наименование" Тогда
				ОбластьЗаголовка.Параметры.ПараметрРасшифровки = Контрагент;	
			Иначе
				ОбластьЗаголовка.Параметры.ПараметрРасшифровки = Неопределено;	
			КонецЕсли;	
			ОбластьЗаголовка.Параметры.НазваниеОсновногоПараметра = ЭлементСпискаОсновныхСведений.Представление;
			ОбластьЗаголовка.Параметры.ЗначениеОсновногоПараметра = ЭлементСпискаОсновныхСведений.Значение;
			ДокРезультат.Вывести(ОбластьЗаголовка);
		КонецЕсли;
	КонецЦикла;
	
	// Дополнительные сведения
	
   	// Получим таблицу кураторов контрагента
	ВыводитьКонтактнуюИнформациюКонтактныхЛиц = СписокНастроек.НайтиПоЗначению("КонтактнаяИнформацияКЛ").Пометка;
	ВыводитьПодтверждающиеДокументыКонтактныхЛиц = СписокНастроек.НайтиПоЗначению("ПодтверждающиеДокументыКЛ").Пометка;
	ВыводитьДополнительнуюИнформациюКонтактныхЛиц = СписокНастроек.НайтиПоЗначению("ДополнительнаяИнформацияКЛ").Пометка;
	
	ДокРезультат.НачатьАвтогруппировкуСтрок();	
	Для Каждого ЭлементСпискаНастроек Из СписокНастроек Цикл
		ТекущийРаздел = ЭлементСпискаНастроек.Значение;
		Если (ТекущийРаздел = "КонтактнаяИнформацияКЛ")
		 Или (ТекущийРаздел = "ПодтверждающиеДокументыКЛ")
	 	 Или (ТекущийРаздел = "ДополнительнаяИнформацияКЛ") Тогда		 
			Продолжить;
		КонецЕсли;	
		Если ЭлементСпискаНастроек.Пометка Тогда
			
			Если ((ТекущийРаздел = "РегистрационнаяИнформация") И (Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо)) Тогда
				ТекущийРаздел = "РегистрационнаяИнформацияЧастноеЛицо";
			КонецЕсли;	
			ТаблицаДанных = ТаблицаДанныхРаздела(ТекущийРаздел, Контрагент);
			Если ТаблицаДанных.Количество() > 0 Тогда
		    	ДокРезультат.Вывести(ОбластьОтступа0, 0);
		    	
		    	ОбластьЗаголовкаРаздела.Параметры.НаименованиеРаздела = ЭлементСпискаНастроек.Представление;
		    	ДокРезультат.Вывести(ОбластьЗаголовкаРаздела, 0);
				
				ДокРезультат.Вывести(ОбластьОтступа1, 1);
				Если (Найти(ТекущийРаздел, "РегистрационнаяИнформация") = 1) Или (ТекущийРаздел = "ДополнительнаяИнформация") Тогда
					ВывестиТаблицей(ТаблицаДанных, ДокРезультат, МакетОтчета, ТекущийРаздел, 1, 0, Ложь);
				ИначеЕсли ТекущийРаздел = "КонтактнаяИнформация" Тогда
					ВывестиТаблицей(ТаблицаДанных, ДокРезультат, МакетОтчета, ТекущийРаздел, 1, 2, Ложь);
				ИначеЕсли ТекущийРаздел = "КонтактныеЛица" Тогда
		    		ОбластьШапкаКонтактногоЛица = МакетОтчета.ПолучитьОбласть("ШапкаКонтактныеЛица");
		    		ОбластьОсновныеДанныеКонтактногоЛица = МакетОтчета.ПолучитьОбласть("ОсновныеДанныеКонтактныеЛица");
		    		ВывестиОбластьПробела = Ложь;
					Для Каждого КонтактныеЛицаКонтрагента Из ТаблицаДанных Цикл
		    			Если ВывестиОбластьПробела Тогда
		    				ДокРезультат.Вывести(ОбластьОтступа1, 2);	
		    			Иначе
		    				ВывестиОбластьПробела = Истина;
		    			КонецЕсли;
		    			ОбластьШапкаКонтактногоЛица.Параметры.Ссылка = КонтактныеЛицаКонтрагента.Ссылка;
		    			ДокРезультат.Вывести(ОбластьШапкаКонтактногоЛица, 1);	
		    			
		    			ОбластьОсновныеДанныеКонтактногоЛица.Параметры.Заполнить(КонтактныеЛицаКонтрагента);
		    			ДокРезультат.Вывести(ОбластьОсновныеДанныеКонтактногоЛица, 2);
						
						Если ВыводитьКонтактнуюИнформациюКонтактныхЛиц Тогда
							ТаблицаДанныхКонтактногоЛица = ТаблицаДанныхРаздела("КонтактнаяИнформация", Контрагент, КонтактныеЛицаКонтрагента.Ссылка);
							Если ТаблицаДанныхКонтактногоЛица.Количество() > 0 Тогда 
								ДокРезультат.Вывести(ОбластьОтступа1, 2);
								ВывестиТаблицей(ТаблицаДанныхКонтактногоЛица, ДокРезультат, МакетОтчета, "КонтактнаяИнформацияКЛ", 2, 2);
							КонецЕсли;
							БылаВыведенаКонтактнаяИнформация = (ТаблицаДанныхКонтактногоЛица.Количество() > 0);
		    			КонецЕсли;
		    			Если ВыводитьПодтверждающиеДокументыКонтактныхЛиц Тогда
							ТаблицаДанныхКонтактногоЛица = ТаблицаДанныхРаздела("ПодтверждающиеДокументы", Контрагент, КонтактныеЛицаКонтрагента.Ссылка);
							Если ТаблицаДанныхКонтактногоЛица.Количество() > 0 Тогда 
								Если (Не ВыводитьКонтактнуюИнформациюКонтактныхЛиц) Или (БылаВыведенаКонтактнаяИнформация) Тогда
									ДокРезультат.Вывести(ОбластьОтступа1, 2);						
								КонецЕсли;
								ВывестиТаблицей(ТаблицаДанныхКонтактногоЛица, ДокРезультат, МакетОтчета, "ПодтверждающиеДокументыКЛ", 2, 0);
							КонецЕсли;	
		    			КонецЕсли;
		    			Если ВыводитьДополнительнуюИнформациюКонтактныхЛиц Тогда
		    				ТаблицаДанныхКонтактногоЛица = ТаблицаДанныхРаздела("ДополнительнаяИнформация", Контрагент, КонтактныеЛицаКонтрагента.Ссылка);
							Если ТаблицаДанныхКонтактногоЛица.Количество() > 0 Тогда 
								ДокРезультат.Вывести(ОбластьОтступа1, 2);
								ВывестиТаблицей(ТаблицаДанныхКонтактногоЛица, ДокРезультат, МакетОтчета, "ДополнительнаяИнформацияКЛ", 2, 0);
							КонецЕсли;	
		    			КонецЕсли;	
		    		КонецЦикла;
				ИначеЕсли ТекущийРаздел = "ПодтверждающиеДокументы"	Тогда
					ВывестиТаблицей(ТаблицаДанных, ДокРезультат, МакетОтчета, ТекущийРаздел, 1, 0, Ложь);
				Иначе
					ВывестиТаблицей(ТаблицаДанных, ДокРезультат, МакетОтчета, ТекущийРаздел, 1, 3);
				КонецЕсли;
	 		КонецЕсли;
	    КонецЕсли;	
	КонецЦикла;
	ДокРезультат.ОтображатьСетку = Ложь;
	ДокРезультат.ПолеСверху = 10;
	ДокРезультат.ПолеСлева = 20;
	ДокРезультат.ПолеСнизу = 10;
	ДокРезультат.ПолеСправа = 10;
	ДокРезультат.ОтображатьЗаголовки = Ложь;
	ДокРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	Возврат ДокРезультат;
КонецФункции // РаскраситьОбластьПоШаблону()
#КонецЕсли

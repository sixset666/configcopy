#Если Клиент Тогда
	
Перем НеПоказыватьТаблДокумент Экспорт; // Признак отображения табличного документа.
Перем ТонкаяСплошнаяЛиния;
Перем ТонкаяПунктирнаяЛиния;
Перем ТолстаяСплошнаяЛиния;

// Заполняет представление полей
//
// Параметры
//   СинонимыПолей - структура - содержит синонимы полей
//   ПостроительОтчета - ПостроительОтчеты
//
Процедура ЗаполнитьПредставленияПолей(СинонимыПолей, ПостроительОтчета) Экспорт
	ИменаКоллекцийПостроителяОтчета = Новый Структура("ДоступныеПоля, ВыбранныеПоля, ИзмеренияКолонки, ИзмеренияСтроки, Отбор");
	Для Каждого ТекКоллекция Из ИменаКоллекцийПостроителяОтчета Цикл
		Для Каждого ТекОбъект Из ПостроительОтчета[ТекКоллекция.Ключ] Цикл
			Если (НЕ ПустаяСтрока(ТекОбъект.Имя)) И (СинонимыПолей.Свойство(ТекОбъект.Имя)) Тогда
				ТекОбъект.Представление = СинонимыПолей[ТекОбъект.Имя];
			КонецЕсли;
		КонецЦикла;
	КонецЦикла; 
КонецПроцедуры // ЗаполнитьПредставленияПолей()

//Формирует отчет
//
// Параметры
//  ТабДокумент - ТабличныйДокумент
//
Процедура СформироватьОтчет(ТабДокумент = Неопределено) Экспорт
	
	СформироватьТабличныйДокумент(ТабДокумент);
	
КонецПроцедуры // СформироватьОтчет()

//Формирует отчет
//
// Параметры
//  ТабДокумент - ТабличныйДокумент
//
Процедура СформироватьТабличныйДокумент(ТабДокумент = Неопределено) Экспорт
	
	Если обЗначениеНеЗаполнено(Документ) Тогда
		Предупреждение("Запишите и проведите документ, по которому формируется отчет");
		Возврат;
	КонецЕсли;
	
	// Создадим и проинициализируем основные объекты
	Если ТабДокумент = Неопределено Тогда
		ТабДокумент = Новый ТабличныйДокумент;
	КонецЕсли;	
	ПостроительОтчета = Новый ПостроительОтчета;
	
	// Сформируем заголовок макета
	Макет = ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.Документ = Строка(Документ);
	ТабДокумент.Вывести(ОбластьЗаголовок);
	
	// Используются для отображения % выполненной работы
	НомерРегистра = -1;
	КоличествоРегистров = Документ.Метаданные().Движения.Количество();
	
	// Шагаем по регистраторам
	Для Каждого ТекРегистр Из Документ.Метаданные().Движения Цикл
		НомерРегистра = НомерРегистра + 1;
		Состояние("Обрабатывается регистр ("+Цел(НомерРегистра/КоличествоРегистров*100)+"%): "+ТекРегистр.Синоним);
		
		// Проверим а валяется ли текущее движение, движением по регистру сведений или накопления
		Если (Метаданные.РегистрыНакопления.Индекс(ТекРегистр) < 0) И (Метаданные.РегистрыСведений.Индекс(ТекРегистр) < 0) Тогда
			Продолжить;
		КонецЕсли;
		
		// Это регистр накопления или сведений?
		ВидРегистра = ?(Метаданные.РегистрыНакопления.Индекс(ТекРегистр) >= 0, "Накопления", "Сведений");
		
		// Вспомогательные переменные
		ЕстьВидДвижения	= ((ВидРегистра = "Накопления") И (ТекРегистр.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки));
		ЕстьРеквизиты	= (ТекРегистр.Реквизиты.Количество() > 0);
		
		// Получаем области макета
		ОбластьИмяРегистра = Макет.ПолучитьОбласть("ИмяРегистра");
		ОбластьИмяРегистра.Область(1, 2, 1, ?(ЕстьРеквизиты,5,4)).Объединить();
		ОбластьИмяРегистра.Параметры.ВидРегистра = ВидРегистра;
		ОбластьИмяРегистра.Параметры.ИмяРегистра = ТекРегистр.Имя;
		
		Суффикс = ?(ЕстьВидДвижения,"В","") + ?(ЕстьРеквизиты,"Р","");
		ОбластьШапка	= Макет.ПолучитьОбласть("ШапкаТаблицы"+Суффикс);
		ОбластьДетали	= Макет.ПолучитьОбласть("Детали"+Суффикс);
		
		// Очистим от имен
		ОбластьИмяРегистра.Область("ИмяРегистра"			).Имя = "";
		ОбластьШапка 	  .Область("ШапкаТаблицы"	+Суффикс).Имя = "";
		ОбластьДетали	  .Область("Детали"			+Суффикс).Имя = "";
		
		// Приступаем к формированию списка полей и макета детальных записей
		Поля			= "";
		СинонимыПолей	= Новый Структура;
		
		Если ЕстьВидДвижения Тогда
			Поля = "ВЫБОР
			|		КОГДА ВидДвижения = &Приход
			|			ТОГДА ""+""
			|		КОГДА ВидДвижения = &Расход
			|			ТОГДА ""-""
			|		ИНАЧЕ ""+-""
			|	КОНЕЦ КАК ВидДвижения";
			СинонимыПолей.Вставить("ВидДвижения", "+/-");
		КонецЕсли;
		
		ИменаКоллекций = Новый Структура("Измерения,Ресурсы,Реквизиты");	Колонка = ?(ЕстьВидДвижения,3,2);
		КоличествоКолонокНеВыводить = 0;
		Для каждого ТекКоллекция Из ИменаКоллекций Цикл
			Строка = 1;
			Для каждого ТекОбъект Из ТекРегистр[ТекКоллекция.Ключ] Цикл
				
				// Поставим заглушку на вывод поле, которые не надо выводить
				Если ТекОбъект.Имя = "КодМаркировкиBase64" Тогда
					КоличествоКолонокНеВыводить = КоличествоКолонокНеВыводить + 1;
					Продолжить;
				КонецЕсли;
				
				Поля = Поля + "," + Символы.ПС + Символы.Таб + ТекОбъект.Имя;
				СинонимыПолей.Вставить(ТекОбъект.Имя, ТекОбъект.Синоним);
				
				ТекОбласть = ОбластьДетали.Область(Строка,Колонка,Строка,Колонка + ?((НЕ ЕстьВидДвижения)И(ТекКоллекция.Ключ = "Измерения"),1,0));
				ТекОбласть.Объединить();
				ТекОбласть.Заполнение				= ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ТекОбласть.Параметр					= ТекОбъект.Имя;
				ТекОбласть.ПараметрРасшифровки		= ТекОбъект.Имя;
				ТекОбласть.РазмещениеТекста			= ТипРазмещенияТекстаТабличногоДокумента.Обрезать;
				ТекОбласть.ГоризонтальноеПоложение	= ГоризонтальноеПоложение.Лево;
				ТекОбласть.Обвести(ТонкаяСплошнаяЛиния,ТонкаяСплошнаяЛиния,ТонкаяСплошнаяЛиния,ТонкаяСплошнаяЛиния);
				Строка = Строка + 1;
			КонецЦикла;
			Колонка = ?((Колонка = 2)И(НЕ ЕстьВидДвижения),4,Колонка + 1);
		КонецЦикла;
		Поля = ?(ЕстьВидДвижения,Поля,Сред(Поля,4));
		
		// Заполним область деталей синонимами полей и "допишем" ее к шапке
		ВысотаМакета = МАКС(ТекРегистр.Измерения.Количество() - КоличествоКолонокНеВыводить, ТекРегистр.Ресурсы.Количество(), ТекРегистр.Реквизиты.Количество());
		ОбластьДетали = ОбластьДетали.ПолучитьОбласть(1, , ВысотаМакета, );
		ОбластьДетали.Параметры.Заполнить(СинонимыПолей);
		ОбластьШапка.Вывести(ОбластьДетали);
		ОбластьШапка = ОбластьШапка.ПолучитьОбласть(1, , ВысотаМакета + 1, );
		
		// Обработаем макеты - объединим все что нужно объединить и обведем ячейки
		Для Кол = 2 По ?(ЕстьРеквизиты,5,4) Цикл
			Для Стр = 1 По ВысотаМакета Цикл
				ТекОбласть = ОбластьДетали.Область(Стр, Кол, Стр, Кол);
				ТекОбласть.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				ТекОбласть.Обвести(ТонкаяСплошнаяЛиния, ТонкаяПунктирнаяЛиния, ТонкаяСплошнаяЛиния, ТонкаяПунктирнаяЛиния);
			КонецЦикла;
		КонецЦикла;
		Если ЕстьВидДвижения Тогда
			ОбластьШапка.Область(1, 2, ВысотаМакета + 1, 2).Объединить();
			ОбластьШапка.Область(1, 2, ВысотаМакета + 1, 2).Обвести(ТонкаяСплошнаяЛиния, ТонкаяСплошнаяЛиния, ТонкаяСплошнаяЛиния, ТонкаяСплошнаяЛиния);
			
			ОбластьДетали.Область(1, 2, ВысотаМакета, 2).Объединить();
			ОбластьДетали.Область(1, 2, ВысотаМакета, 2).Обвести(ТонкаяСплошнаяЛиния, ТонкаяСплошнаяЛиния, ТонкаяСплошнаяЛиния, ТонкаяСплошнаяЛиния);
		КонецЕсли;
		
		// Обведем области "итоговой" линией
		ОбластьИмяРегистра.Область(1, 2, 1, ?(ЕстьРеквизиты,5,4)).Обвести(ТолстаяСплошнаяЛиния,ТолстаяСплошнаяЛиния,ТолстаяСплошнаяЛиния,ТолстаяСплошнаяЛиния);
		ОбластьШапка.Область(1, 2, ВысотаМакета + 1, ?(ЕстьРеквизиты,5,4)).Обвести(ТолстаяСплошнаяЛиния,ТолстаяСплошнаяЛиния,ТолстаяСплошнаяЛиния,ТолстаяСплошнаяЛиния);
		ОбластьДетали.Область(1, 2, ВысотаМакета, ?(ЕстьРеквизиты,5,4)).Обвести(ТонкаяСплошнаяЛиния,ТонкаяСплошнаяЛиния,ТонкаяСплошнаяЛиния,ТонкаяСплошнаяЛиния);
		
		// Сделаем шрифт шапки жирным
		ОбластьШапка.Область(1, 2, ВысотаМакета + 1, ?(ЕстьРеквизиты,5,4)).Шрифт = Новый Шрифт(ОбластьШапка.Область(1, 2, ВысотаМакета + 1, ?(ЕстьРеквизиты,5,4)).Шрифт, , ИСТИНА);
		
		// Проинициализируем построитель отчета и установим параметры
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	" + Поля + "
		|{ВЫБРАТЬ
		|	" + Поля + "}
		|ИЗ
		|	Регистр" + ВидРегистра + "." + ТекРегистр.Имя + "
		|ГДЕ
		|	Регистратор = &ПоДокументу";
		
		//ПостроительОтчета = Новый ПостроительОтчета(ТекстЗапроса);
		ПостроительОтчета.Текст = ТекстЗапроса;
		ПостроительОтчета.Параметры.Вставить("ПоДокументу", Документ);
		ПостроительОтчета.Параметры.Вставить("Приход", ВиддвиженияНакопления.Приход);
		ПостроительОтчета.Параметры.Вставить("Расход", ВиддвиженияНакопления.Расход);
		
		// Заполним представления всех полей построителя отчета синонимами измерений, ресурсов или реквизитов
		ЗаполнитьПредставленияПолей(СинонимыПолей, ПостроительОтчета);
		
		// Настроим построитель отчета
		ПостроительОтчета.МакетШапкиТаблицы			= ОбластьШапка;
		ПостроительОтчета.МакетДетальныхЗаписей		= ОбластьДетали;
		ПостроительОтчета.ВыводитьШапкуТаблицы		= ИСТИНА;
		ПостроительОтчета.ВыводитьПодвалОтчета		= ИСТИНА;
		ПостроительОтчета.ВыводитьОбщиеИтоги		= ЛОЖЬ;
		ПостроительОтчета.ВыводитьПодвалТаблицы		= ЛОЖЬ;
		ПостроительОтчета.ВыводитьЗаголовокОтчета	= ЛОЖЬ;
		
		Если ПостроительОтчета.ПолучитьЗапрос().Выполнить().Пустой() Тогда
			Продолжить;
		Иначе
			ТабДокумент.Вывести(ОбластьИмяРегистра);
		КонецЕсли;
		ТабДокумент.НачатьГруппуСтрок();
		ПостроительОтчета.Вывести(ТабДокумент);
		ТабДокумент.ЗакончитьГруппуСтрок();
	КонецЦикла;
	
	ТабДокумент.ОтображатьГруппировки= ИСТИНА;
	ТабДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабДокумент.ТолькоПросмотр		= ИСТИНА;
	ТабДокумент.Автомасштаб			= ЛОЖЬ;
	ТабДокумент.ПолеСлева			= 9;
	ТабДокумент.ПолеСправа			= 6;
	Если Не НеПоказыватьТаблДокумент Тогда
		ТабДокумент.Показать("Движения документа "+ Строка(Документ));
	КонецЕсли;
	Состояние("");
КонецПроцедуры // СформироватьТабличныйДокумент()

ТонкаяСплошнаяЛиния		 = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
ТонкаяПунктирнаяЛиния	 = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.ЧастыйПунктир, 1);
ТолстаяСплошнаяЛиния	 = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2);
НеПоказыватьТаблДокумент = Ложь;
#КонецЕсли

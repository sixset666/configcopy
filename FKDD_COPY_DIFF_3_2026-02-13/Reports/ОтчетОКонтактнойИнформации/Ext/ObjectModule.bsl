#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ОформлениеШапки, ОформлениеСтроки; // оформления
Перем ВидКонтрагентаВОтчете Экспорт; // отображение контрагента

// Раскрашивает область согласно шаблону
//
// Параметры:
//	Область - ТабличныйДокумент - область табличного документа, которая раскрашивается;
//	Шаблон - Область - содержит шаблон цвета текста и фона.
//
Процедура РаскраситьОбластьПоШаблону(Область, Шаблон, Столбец = 1)	
	Область.Область(1, Столбец, 1, Столбец).ЦветТекста = Шаблон.ЦветТекста;
	Область.Область(1, Столбец, 1, Столбец).ЦветФона   = Шаблон.ЦветФона;
КонецПроцедуры// РаскраситьОбластьПоШаблону(Область, Шаблон)	

// Выводит контактную информацию в табличный документ
//
// Параметры:
//	Объект - СправочникСсылка - Ссылка на контрагента или контактное лицо, контактная информация которого выводится;
//	ТабличныйДокумент - табличный документ, в который выводятся строки контактной информации;
//	Макет - макет, с помощью которого формируются строки с контактной информацией;
//
// Возвращаемое значение
//	Булево - "Ложь", если не было выведено ни одной строки контактной информации
//
Процедура ВывестиКонтактнуюИнформациюОбъекта(Объект, ТабличныйДокумент, Макет,ЭтоКонтакт = Ложь)
	ИмяМетаданныхОбъекта = Объект.Метаданные().Имя;
	ОбластьОбъектаОсновная = Макет.ПолучитьОбласть(ИмяМетаданныхОбъекта+"|Основная");
	ОбластьОбъектаОсновная.Параметры.Объект = Объект;
	Если ВыводитьКонтактнуюИнформациюКонтактныхЛиц И НЕ ЭтоКонтакт Тогда
		РаскраситьОбластьПоШаблону(ОбластьОбъектаОсновная, ОформлениеСтроки, 2);
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьОбъектаОсновная);
	
	ОбластьОбъектаКонтактнаяИнформация = Макет.ПолучитьОбласть(ИмяМетаданныхОбъекта+"|КонтактнаяИнформация");
   	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Вид,
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Комментарий,
	               |	NULL КАК ПредставлениеПД,
	               |	NULL КАК Ссылка
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента,
	               |	NULL,
	               |	NULL,
	               |	ПодтверждающиеДокументы.Представление,
	               |	ПодтверждающиеДокументы.Ссылка
	               |ИЗ
	               |	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
	               |ГДЕ
	               |	ПодтверждающиеДокументы.Владелец = &Объект";
	Запрос.УстановитьПараметр("Объект", Объект);			   
	Запрос.Текст = ТекстЗапроса;			   
    ТаблицаЗначенийКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаКонтактнойИнформации Из ВидыКонтактнойИнформации Цикл
		Если СтрокаКонтактнойИнформации.Пометка Тогда
			СтрокаТаблицыЗначений = ТаблицаЗначенийКонтактнойИнформации.Найти(СтрокаКонтактнойИнформации.ВидыКонтактнойИнформации
																			  , "Вид");
			Если СтрокаТаблицыЗначений = Неопределено Тогда
				КонтактнаяИнформация = "";	
			Иначе
				КонтактнаяИнформация = СтрокаТаблицыЗначений.Представление;
				Если Не обЗначениеНеЗаполнено(СтрокаТаблицыЗначений.Комментарий) Тогда
					КонтактнаяИнформация = КонтактнаяИнформация + " (" + СтрокаТаблицыЗначений.Комментарий + ")";
				КонецЕсли;	
			КонецЕсли;
			ОбластьОбъектаКонтактнаяИнформация.Параметры.КонтактнаяИнформация = КонтактнаяИнформация;
			Если ВыводитьКонтактнуюИнформациюКонтактныхЛиц И НЕ ЭтоКонтакт Тогда
				РаскраситьОбластьПоШаблону(ОбластьОбъектаКонтактнаяИнформация, ОформлениеСтроки);
			КонецЕсли;
			ТабличныйДокумент.Присоединить(ОбластьОбъектаКонтактнаяИнформация);
		КонецЕсли;	
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента КАК ВидПодтверждающегоДокумента,
	|	ПодтверждающиеДокументы.Владелец,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПодтверждающиеДокументы.Ссылка) КАК КоличествоПД
	|ИЗ
	|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
	|ГДЕ
	|	ПодтверждающиеДокументы.Владелец В(&ПереченьВладельцев)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждающиеДокументы.Владелец,
	|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента
	|ИТОГИ
	|	МАКСИМУМ(КоличествоПД)
	|ПО
	|	ВидПодтверждающегоДокумента";
	Запрос.УстановитьПараметр("ПереченьВладельцев",Объекты.ВыгрузитьКолонку("Контрагент"));
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
    		
	Для Каждого СтрокаПодтверждающийДокумент Из ВидыПодтверждающихДокументов Цикл
		Если СтрокаПодтверждающийДокумент.Пометка Тогда
			
			Отбор = Новый Структура("Вид",СтрокаПодтверждающийДокумент.ВидыПодтверждающихДокументов);
			СтрокаТаблицыЗначений = ТаблицаЗначенийКонтактнойИнформации.НайтиСтроки(Отбор);
			
			Отбор = Новый Структура("ВидПодтверждающегоДокумента",СтрокаПодтверждающийДокумент.ВидыПодтверждающихДокументов);
			Выборка.Сбросить();
			КоличествоПД = Выборка.НайтиСледующий(Отбор);
			
			Если КоличествоПД Тогда
				Для х = 0 по Выборка.КоличествоПД-1 Цикл
					Если х<СтрокаТаблицыЗначений.Количество() Тогда
						СтрокаТаблицы = СтрокаТаблицыЗначений.Получить(х);
					Иначе
						СтрокаТаблицы = Неопределено
					КонецЕсли;
					
					Если СтрокаТаблицы = Неопределено Тогда
						КонтактнаяИнформация = "";	
					Иначе
						КонтактнаяИнформация = СтрокаТаблицы.ПредставлениеПД;
						Если Не обЗначениеНеЗаполнено(СтрокаТаблицы.Комментарий) Тогда
							КонтактнаяИнформация = КонтактнаяИнформация + " (" + СтрокаТаблицы.Комментарий + ")";
						КонецЕсли;	
					КонецЕсли;
                	ОбластьОбъектаКонтактнаяИнформация.Параметры.КонтактнаяИнформация = КонтактнаяИнформация;
					Если ВыводитьКонтактнуюИнформациюКонтактныхЛиц И НЕ ЭтоКонтакт Тогда
						РаскраситьОбластьПоШаблону(ОбластьОбъектаКонтактнаяИнформация, ОформлениеСтроки);
					КонецЕсли;
					ТабличныйДокумент.Присоединить(ОбластьОбъектаКонтактнаяИнформация);			
				КонецЦикла;
			Иначе
				//ОбластьОбъектаКонтактнаяИнформация.Параметры.КонтактнаяИнформация = КонтактнаяИнформация;
				Если ВыводитьКонтактнуюИнформациюКонтактныхЛиц И НЕ ЭтоКонтакт Тогда
					РаскраситьОбластьПоШаблону(ОбластьОбъектаКонтактнаяИнформация, ОформлениеСтроки);
				КонецЕсли;
				ТабличныйДокумент.Присоединить(ОбластьОбъектаКонтактнаяИнформация);			
			КонецЕсли;			
		КонецЕсли;	
		ОбластьОбъектаКонтактнаяИнформация.Параметры.КонтактнаяИнформация = "";
	КонецЦикла;	
	КонецПроцедуры// ВывестиКонтактнуюИнформацию()

// Формирует и восстанавливает настройки отчета
//
// Без параметров
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	Запрос = Новый Запрос;
	ЗапросПД = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СправочникВидыКонтактнойИнформации.Ссылка КАК ВидыКонтактнойИнформации,
	               |	СправочникВидыКонтактнойИнформации.Тип.Порядок КАК Порядок,
	               |	Ложь КАК Пометка
	               |ИЗ
	               |	Справочник.ВидыКонтактнойИнформации КАК СправочникВидыКонтактнойИнформации";
				   
				   
	ТекстЗапросаПД ="ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВидыДокументов.Ссылка КАК ВидыПодтверждающихДокументов,
	                |	ВидыДокументов.Порядок,
	                |	Ложь КАК Пометка
	                |ИЗ
	                |	Перечисление.ВидыДокументов КАК ВидыДокументов";
	
	 ВидыПодтверждающихДокументов.Очистить();
	СтруктураНастроекОтчета = ВосстановитьЗначение("Отчет_ОтчетОКонтактнойИнформации_Настройки");
	
	Если СтруктураНастроекОтчета = Неопределено Тогда
		Запрос.Текст = ТекстЗапроса;
		ВидыКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
		ВидыКонтактнойИнформации.Сортировать("Порядок, ВидыКонтактнойИнформации");
		
		ЗапросПД.Текст = ТекстЗапросаПД;
		ВидыПодтверждающихДокументов = Запрос.Выполнить().Выгрузить();
		ВидыПодтверждающихДокументов.Сортировать("Порядок, ВидыКонтактнойИнформации");
	Иначе
		Попытка
			СтруктураНастроекОтчета.Свойство("ВидыКонтактнойИнформации", ВидыКонтактнойИнформации);
			
			ТекстЗапросаСУсловием = ТекстЗапроса + "
										  |ГДЕ
										  |	(НЕ СправочникВидыКонтактнойИнформации.Ссылка В (&СписокСсылок))";
			СписокСсылок = Новый СписокЗначений();
			СписокСсылок.ЗагрузитьЗначения(ВидыКонтактнойИнформации.ВыгрузитьКолонку("ВидыКонтактнойИнформации"));
			Запрос.УстановитьПараметр("СписокСсылок", СписокСсылок);
			Запрос.Текст = ТекстЗапросаСУсловием;
			Результат = Запрос.Выполнить();
			Если Не Результат.Пустой() Тогда
				ТаблицаНовыхВидовКонтактнойИнформации = Результат.Выгрузить();
				Для Каждого НовыйВидКонтактнойИнформации Из ТаблицаНовыхВидовКонтактнойИнформации Цикл
					СтрокаВидыКонтактнойИнформации = ВидыКонтактнойИнформации.Добавить();
					СтрокаВидыКонтактнойИнформации.ВидыКонтактнойИнформации = НовыйВидКонтактнойИнформации.ВидыКонтактнойИнформации;
					СтрокаВидыКонтактнойИнформации.Пометка = НовыйВидКонтактнойИнформации.Пометка;
					СтрокаВидыКонтактнойИнформации.Порядок = НовыйВидКонтактнойИнформации.Порядок;
				КонецЦикла;	
			КонецЕсли;
			
			СтруктураНастроекОтчета.Свойство("ВыводитьКонтактнуюИнформациюКонтактныхЛиц", ВыводитьКонтактнуюИнформациюКонтактныхЛиц);
		Исключение
			Запрос.Текст = ТекстЗапроса;
			ВидыКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
			ВидыКонтактнойИнформации.Сортировать("Порядок, ВидыКонтактнойИнформации");
		КонецПопытки;
		
		Попытка 
			СтруктураНастроекОтчета.Свойство("ВидыПодтверждающихДокументов",ВидыПодтверждающихДокументов);
			
			
			ТекстЗапросаСУсловием = ТекстЗапросаПД + "
										  |ГДЕ
										  |	(НЕ ВидыДокументов.Ссылка В (&СписокСсылок))";
			СписокСсылок = Новый СписокЗначений();
			СписокСсылок.ЗагрузитьЗначения(ВидыПодтверждающихДокументов.ВыгрузитьКолонку("ВидыПодтверждающихДокументов"));
			Запрос.УстановитьПараметр("СписокСсылок", СписокСсылок);
			Запрос.Текст = ТекстЗапросаСУсловием;
			Результат = Запрос.Выполнить();
			Если Не Результат.Пустой() Тогда
				ТаблицаНовыхВидовПодтверждающихДокументов = Результат.Выгрузить();
				Для Каждого НовыйВидПодтверждающегоДокумента Из ТаблицаНовыхВидовПодтверждающихДокументов Цикл
					СтрокаВидыПодтверждающихДокументов = ВидыПодтверждающихДокументов.Добавить();
					СтрокаВидыПодтверждающихДокументов.ВидыКонтактнойИнформации = НовыйВидПодтверждающегоДокумента.ВидыПодтверждающихДокументов;
					СтрокаВидыПодтверждающихДокументов.Пометка = НовыйВидПодтверждающегоДокумента.Пометка;
					СтрокаВидыПодтверждающихДокументов.Порядок = НовыйВидПодтверждающегоДокумента.Порядок;
				КонецЦикла;	
			КонецЕсли;
			
		Исключение
			ЗапросПД.Текст = ТекстЗапросаПД;
			ВидыПодтверждающихДокументов = ЗапросПД.Выполнить().Выгрузить();
			ВидыПодтверждающихДокументов.Сортировать("Порядок, ВидыПодтверждающихДокументов");
		КонецПопытки;
	КонецЕсли;
	
	Если ВидКонтрагентаВОтчете = Неопределено Тогда
		ВидКонтрагентаВОтчете = "Контрагенты";
	КонецЕсли;
	
КонецПроцедуры// ЗаполнитьНачальныеНастройки() Экспорт

// Сохраняет настройки отчета
//
// Без параметров
//
Процедура СохранитьНастойкиОтчета() Экспорт
	
	СтруктураНастроекОтчета = Новый Структура();
	СтруктураНастроекОтчета.Вставить("ВидыКонтактнойИнформации", ВидыКонтактнойИнформации);
	СтруктураНастроекОтчета.Вставить("ВыводитьКонтактнуюИнформациюКонтактныхЛиц", ВыводитьКонтактнуюИнформациюКонтактныхЛиц);
	СтруктураНастроекОтчета.Вставить("ВидыПодтверждающихДокументов", ВидыПодтверждающихДокументов);
	СохранитьЗначение("Отчет_ОтчетОКонтактнойИнформации_Настройки", СтруктураНастроекОтчета);
	
КонецПроцедуры// СохранитьНастойкиОтчета() Экспорт

//устновка параметров отбора
//
Процедура ЗаполнениеФильтраОтбора() Экспорт
	ДоступныеПоляОтбора.Очистить();
	
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Контрагент","Контрагент",Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Контрагент.Наименование","Наименование",Новый ОписаниеТипов("Строка"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Контрагент.ФормаСобственности","Форма собственности",Новый ОписаниеТипов("ПеречислениеСсылка.ФормыСобственности"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Контрагент.Сотрудник","Сотрудник",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Контрагент.Филиал","Филиал",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Контрагент.ВидОтношений","Вид отношений",Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКонтрагентов"));
	ПолеНастройки.Отбор=Истина;
	
	ФильтрыОтчета.УстановитьДоступныеПоля(ДоступныеПоляОтбора);
	ФильтрыОтчета.Добавить("Контрагент.Наименование","Наименование","Наименование");
	ФильтрыОтчета.Добавить("Контрагент.ФормаСобственности","ФормаСобственности","Тип клиента");
	ФильтрыОтчета.Добавить("Контрагент.ВидКонтрагента", "ВидОтношений", "Основной вид отношений");
	ФильтрыОтчета.Добавить("Контрагент.Сотрудник", "Сотрудник", "Исполнитель");
	ФильтрыОтчета.Добавить("Контрагент.Филиал", "Филиал", "Филиал");
	
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Сотрудник","Сотрудник",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Сотрудник.Наименование","Наименование",Новый ОписаниеТипов("Строка"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Сотрудник.Должность","Должность",Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Сотрудник.Подразделение","Подразделение",Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Сотрудник.Организация","Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Сотрудник.Цех","Цех",Новый ОписаниеТипов("СправочникСсылка.Цеха"));
	ПолеНастройки.Отбор=Истина;
	
	ФильтрыОтчетаСотрудников.УстановитьДоступныеПоля(ДоступныеПоляОтбора);
	ФильтрыОтчетаСотрудников.Добавить("Сотрудник.Наименование","Наименование","Наименование");
	ФильтрыОтчетаСотрудников.Добавить("Сотрудник.Должность","Должность","Должность");
	ФильтрыОтчетаСотрудников.Добавить("Сотрудник.Подразделение", "Подразделение", "Подразделение");
	ФильтрыОтчетаСотрудников.Добавить("Сотрудник.Организация", "Организация", "Организация");
	ФильтрыОтчетаСотрудников.Добавить("Сотрудник.Цех","Цех","Цех");
	
	КвалификаторыСтроки = Новый КвалификаторыСтроки(100);
	КвалификаторыСтроки1 = Новый КвалификаторыСтроки(12);
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Организация","Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Организация.Наименование","Наименование",Новый ОписаниеТипов("Строка"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Организация.ФормаСобственности","Форма собственности",Новый ОписаниеТипов("ПеречислениеСсылка.ФормыСобственности"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки= ДоступныеПоляОтбора.Добавить("Организация.ИНН","ИНН",Новый ОписаниеТипов("Строка", , КвалификаторыСтроки1));
	ПолеНастройки.Отбор=Истина;

	ФильтрыОтчетаОрганизаций.УстановитьДоступныеПоля(ДоступныеПоляОтбора);
	ФильтрыОтчетаОрганизаций.Добавить("Организация.Наименование","Наименование","Наименование");
	ФильтрыОтчетаОрганизаций.Добавить("Организация.ФормаСобственности", "ФормаСобственности", "Форма собственности");
	ФильтрыОтчетаОрганизаций.Добавить("Организация.ИНН", "ИНН", "ИНН");
	
КонецПроцедуры

//При изменении отбора
//
Процедура ЗаполнениеТаблицыОбъектов(ВидКонтрагентаВОтчете) Экспорт
	СтрокаУсловия="";
	
	Если ВидКонтрагентаВОтчете = "Контрагенты" Тогда
		Для Каждого ЭлементОтбора Из ФильтрыОтчета Цикл
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			СтрокаУсловия=СтрокаУсловия+?(СтрокаУсловия="",""," И ")+СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#","#ИмяТаблицы#"+ЭлементОтбора.ПутьКДанным),"&","&"+ЭлементОтбора.Имя);
		КонецЦикла;
		СтрокаУсловия = ?(СтрокаУсловия="","", "	И "+СтрокаУсловия);
		//СтрокаУсловия = Прав(СтрокаУсловия, СтрДлина(СтрокаУсловия)-2);
		СтрокаУсловия = СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "");
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Контрагент.Ссылка
		               |ИЗ
		               |	Справочник.Контрагенты КАК Контрагент
		               |ГДЕ
		               |	Контрагент.ЭтоГруппа = ЛОЖЬ
					   |"+СтрокаУсловия+"";
		Для Каждого ЭлементОтбора Из ФильтрыОтчета Цикл
			// не используем элемент отбора
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			Если ТипЗнч(ЭлементОтбора.Значение) = Тип("Строка") Тогда
				Запрос.УстановитьПараметр(ЭлементОтбора.Имя, "%"+ЭлементОтбора.Значение+"%");
			Иначе
				Запрос.УстановитьПараметр(ЭлементОтбора.Имя,ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйОбъект = Объекты.Добавить();
			НовыйОбъект.Контрагент = Выборка.Ссылка;
		КонецЦикла;
	ИначеЕсли  ВидКонтрагентаВОтчете = "Сотрудники" Тогда
		Для Каждого ЭлементОтбора Из ФильтрыОтчетаСотрудников Цикл
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			СтрокаУсловия=СтрокаУсловия+?(СтрокаУсловия="",""," И ")+СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#","#ИмяТаблицы#"+ЭлементОтбора.ПутьКДанным),"&","&"+ЭлементОтбора.Имя);
		КонецЦикла;
		СтрокаУсловия = ?(СтрокаУсловия="","", "	И "+СтрокаУсловия);
		//СтрокаУсловия = Прав(СтрокаУсловия, СтрДлина(СтрокаУсловия)-2);
		СтрокаУсловия = СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "");
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Сотрудник.Ссылка
		               |ИЗ
		               |	Справочник.Сотрудники КАК Сотрудник
		               |ГДЕ
		               |	Сотрудник.ЭтоГруппа = ЛОЖЬ
					   |"+СтрокаУсловия+"";
		Для Каждого ЭлементОтбора Из ФильтрыОтчетаСотрудников Цикл
			// не используем элемент отбора
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			Если ТипЗнч(ЭлементОтбора.Значение) = Тип("Строка") Тогда
				Запрос.УстановитьПараметр(ЭлементОтбора.Имя, "%"+ЭлементОтбора.Значение+"%");
			Иначе
				Запрос.УстановитьПараметр(ЭлементОтбора.Имя,ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйОбъект = Объекты.Добавить();
			НовыйОбъект.Контрагент = Выборка.Ссылка;
		КонецЦикла;
		
	Иначе
		Для Каждого ЭлементОтбора Из ФильтрыОтчетаОрганизаций Цикл
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			СтрокаУсловия=СтрокаУсловия+?(СтрокаУсловия="",""," И ")+СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#","#ИмяТаблицы#"+ЭлементОтбора.ПутьКДанным),"&","&"+ЭлементОтбора.Имя);
		КонецЦикла;
		СтрокаУсловия = ?(СтрокаУсловия="","", "	И "+СтрокаУсловия);
		СтрокаУсловия = Прав(СтрокаУсловия, СтрДлина(СтрокаУсловия)-2);
		СтрокаУсловия = СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "");
		Если СтрокаУсловия <> "" Тогда
			СтрокаУсловия = "ГДЕ "+ СтрокаУсловия;
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Организация.Ссылка
		               |ИЗ
		               |	Справочник.Организации КАК Организация
					   |"+СтрокаУсловия+"";
		Для Каждого ЭлементОтбора Из ФильтрыОтчетаОрганизаций Цикл
			// не используем элемент отбора
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			Если ТипЗнч(ЭлементОтбора.Значение) = Тип("Строка") Тогда
				Запрос.УстановитьПараметр(ЭлементОтбора.Имя, "%"+ЭлементОтбора.Значение+"%");
			Иначе
				Запрос.УстановитьПараметр(ЭлементОтбора.Имя,ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйОбъект = Объекты.Добавить();
			НовыйОбъект.Контрагент = Выборка.Ссылка;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент (ДокРезультат=Неопределено) Экспорт
    Если ТипЗнч(ДокРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокРезультат) <> Тип("ТабличныйДокумент") Тогда
		  ДокРезультат = Новый ТабличныйДокумент();
	Иначе ДокРезультат.Очистить();
	КонецЕсли;
	
	
	// получим макеты для результата и оформления
	МакетОтчета = ПолучитьМакет("Макет");
	МакетПараметровОтчета = ПолучитьОбщийМакет(Метаданные.ОбщиеМакеты.МакетОформлениеОтчета);
	ОформлениеШапки  = МакетПараметровОтчета.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеСтроки = МакетПараметровОтчета.ПолучитьОбласть("ОформлениеИзмерений").Область(6,1);
	

	ОбластьЗаголовокОсновная = МакетОтчета.ПолучитьОбласть("Заголовок|Основная");
	МояОбласть = ДокРезультат.Вывести(ОбластьЗаголовокОсновная);
	
	КоличествоФиксированныхСтрок = 4;
	Если Не Куратор.Пустая() Тогда
		ОбластьКураторОсновная = МакетОтчета.ПолучитьОбласть("Куратор|Основная");
		ОбластьКураторОсновная.Параметры.Куратор = Куратор;
		МояОбласть = ДокРезультат.Вывести(ОбластьКураторОсновная);
		КоличествоФиксированныхСтрок = 6;
	КонецЕсли;	
	//Построим заголовок
	ОбластьШапкаОсновная = МакетОтчета.ПолучитьОбласть("Шапка|Основная");
	ОбластьШапкаОсновная.Параметры.Объект = ВидКонтрагентаВОтчете;
	РаскраситьОбластьПоШаблону(ОбластьШапкаОсновная, ОформлениеШапки, 2);	
	МояОбласть = ДокРезультат.Вывести(ОбластьШапкаОсновная);
	
	Если Объекты.Количество() = 0 Тогда
		Предупреждение(СокрЛП(ВидКонтрагентаВОтчете)+": Список не заполнен!");
		Возврат ДокРезультат;
	КонецЕсли;
	
	Если ВидыКонтактнойИнформации.Итог("Пометка") = 0 Тогда
		Предупреждение("Не выбран ни один вид контактной информации!");
		Возврат ДокРезультат;
	КонецЕсли;
	
	НомерОбласти = 3;
	ОбластьШапкаКонтактнаяИнформация = МакетОтчета.ПолучитьОбласть("Шапка|КонтактнаяИнформация");
	Для Каждого СтрокаКонтактнойИнформации Из ВидыКонтактнойИнформации Цикл
		Если СтрокаКонтактнойИнформации.Пометка Тогда
			ОбластьШапкаКонтактнаяИнформация.Параметры.Заполнить(СтрокаКонтактнойИнформации);
			РаскраситьОбластьПоШаблону(ОбластьШапкаКонтактнаяИнформация, ОформлениеШапки);
			ДокРезультат.Присоединить(ОбластьШапкаКонтактнаяИнформация);
			НомерОбласти = НомерОбласти + 1;
		КонецЕсли;	
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента КАК ВидПодтверждающегоДокумента,
	|	ПодтверждающиеДокументы.Владелец,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПодтверждающиеДокументы.Ссылка) КАК КоличествоПД	
	|ИЗ
	|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
	|ГДЕ
	|	ПодтверждающиеДокументы.Владелец В(&ПереченьВладельцев)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждающиеДокументы.Владелец,
	|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента	
	|ИТОГИ
	|	МАКСИМУМ(КоличествоПД)
	|ПО
	|	ВидПодтверждающегоДокумента";
	
	Запрос.УстановитьПараметр("ПереченьВладельцев",Объекты.ВыгрузитьКолонку("Контрагент"));
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ОбластьШапкаПодтверждающиеДокументы = МакетОтчета.ПолучитьОбласть("Шапка|ПодтверждающиеДокументы");
	
	Для Каждого СтрокаПодтверждающиеДокументы Из ВидыПодтверждающихДокументов Цикл
		Если СтрокаПодтверждающиеДокументы.Пометка Тогда
			ОбластьШапкаПодтверждающиеДокументы.Параметры.Заполнить(СтрокаПодтверждающиеДокументы);
			РаскраситьОбластьПоШаблону(ОбластьШапкаПодтверждающиеДокументы, ОформлениеШапки);
			
			Отбор = Новый Структура("ВидПодтверждающегоДокумента",СтрокаПодтверждающиеДокументы.ВидыПодтверждающихДокументов);
			Выборка.Сбросить();
			КоличествоПД = Выборка.НайтиСледующий(Отбор);
			ЧислоДокументов = ?(КоличествоПД,Выборка.КоличествоПД,1);
			Начало = НомерОбласти;
			Для х = 1 по ЧислоДокументов Цикл
				ДокРезультат.Присоединить(ОбластьШапкаПодтверждающиеДокументы);
				НомерОбласти = НомерОбласти + 1; 
			КонецЦикла;        
			Окончание = НомерОбласти - 1;
			Область = ДокРезультат.Область(4,Начало,4,Окончание);
			Область.Объединить();
		КонецЕсли;	
	КонецЦикла;	  
	
	Для Каждого СтрокаКонтрагентов Из Объекты Цикл
		ВывестиКонтактнуюИнформациюОбъекта(СтрокаКонтрагентов.Контрагент, ДокРезультат, МакетОтчета);
		Если ВыводитьКонтактнуюИнформациюКонтактныхЛиц Тогда
			ТаблицаКонтактныхЛиц = Неопределено;
			Если РаботаСКонтактнымиЛицами.ПолучитьВедомых(СтрокаКонтрагентов.Контрагент, ТаблицаКонтактныхЛиц) Тогда
				ДокРезультат.НачатьГруппуСтрок(, Ложь);
				
				Для Каждого СтрокаВедущего Из ТаблицаКонтактныхЛиц Цикл
					ВывестиКонтактнуюИнформациюОбъекта(СтрокаВедущего.Ведомый, ДокРезультат, МакетОтчета, Истина);
				КонецЦикла;
				
				ДокРезультат.ЗакончитьГруппуСтрок();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	ДокРезультат.ОтображатьСетку = Ложь;
	ДокРезультат.ПолеСверху = 10;
	ДокРезультат.ПолеСлева = 20;
	ДокРезультат.ПолеСнизу = 10;
	ДокРезультат.ПолеСправа = 10;
	ДокРезультат.ОтображатьЗаголовки = Ложь;
	ДокРезультат.ФиксацияСлева = 2;
	ДокРезультат.ФиксацияСверху = КоличествоФиксированныхСтрок;
	
	Возврат ДокРезультат;
КонецФункции // РаскраситьОбластьПоШаблону()
#КонецЕсли

#Область ПрограмныйИнтерфейс

// Главный метод
//
Функция ORM_Request(Request, Responce) Экспорт
	
	Попытка
		ЗаголовокЗапроса = Неопределено; ИнформацияПользователя = Неопределено;
		
		// разбираем строку сообщения
		СтруктураЗапроса = РазобратьСтрокуСообщения(Request);
		
		// выполним проверку на обязательные реквизиты
		Если НЕ СтруктураЗапроса.Свойство("request", ЗаголовокЗапроса) ИЛИ НЕ СтруктураЗапроса.Свойство("authorizationinfo", ИнформацияПользователя) Тогда
			Responce = ОтветПоШаблону("02", "Не указана секция request или authorizationinfo.");
			Возврат Ложь;
		КонецЕсли;
		
		// получим вид запроса
		Команда = Неопределено;
		Если ЗаголовокЗапроса.Свойство("command",Команда) Тогда
			id = ""; session_id = ""; client_id = ""; УзелОбмена = Неопределено; // объявим переменные для дальнейшего использования
			
			// Проверка сервера, заполним идентифицирующие параметры
			ЗаголовокЗапроса.Свойство("id", id); ЗаголовокЗапроса.Свойство("session_id", session_id);
			
			// получим узел обмена с устройством
			Если Команда <> "checksrv" И Команда <> "GetLicense" Тогда
				Если ИнформацияПользователя.Свойство("client_id", client_id) Тогда
					Если НЕ ПланыОбмена.ОбменСМобильнымУстройством.СоздатьНовыйУзелДляУстройтва(client_id, УзелОбмена) Тогда
						Responce = ОтветПоШаблону("01", "Не удалось создать узел обмена для данного устройства.", id, session_id);
						Возврат Ложь;
					КонецЕсли;
				Иначе
					Responce = ОтветПоШаблону("01", "Не указан id устройства.", id, session_id);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если Команда <> "get_attach" И Команда <> "get_logo" И Команда <> "ticket" Тогда
				ЗаписатьСобытиеВЛог(УзелОбмена, Request,, Команда);
			КонецЕсли;
			
			// выполняем команды
			Если Команда = "checksrv" Тогда // проверка сервера
				Responce = ОтветПоШаблону("0", "OK", id, session_id);
				ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
				Возврат Истина;
			ИначеЕсли Команда = "GetLicense" Тогда // получение лицензии
				Токен = Неопределено; Лицензия = Неопределено; Ошибки = "";
				
				Если НЕ СтруктураЗапроса.Свойство("token", Токен) ИЛИ Токен = Неопределено Тогда
					Responce = ОтветПоШаблону("07", "Не указан токен.", id, session_id);
					Возврат Ложь;
				КонецЕсли;
				
				Если ПолучитьЛицензиюМобильногоУстройства(Токен, Лицензия, Ошибки) Тогда
					Лицензия = XMLСтрока(Лицензия);
					Responce = "<?xml version=""1.0"" encoding=""UTF-8""?>
							   |<response cmd=""get_license"" id=""0003""><resultinfo><code>0</code><description>ОК</description></resultinfo><license>"+
							   Лицензия +"</license></response>";
					Возврат Истина;
				Иначе
					Responce = ОтветПоШаблону("08", Ошибки, id, session_id);
					Возврат Ложь;
				КонецЕсли;
				
			ИначеЕсли Команда = "firststart" Тогда // первоначальная загрузка
				ОберткаОтвета = "<?xml version=""1.0"" encoding=""UTF-8""?>
								|<response cmd=""firststart"" id="""+ id +
								"""><resultinfo><code>0</code><description>ОК</description></resultinfo>#TEXT</response>";
				
				// формируем строку с информацией выгрузки
				Responce = СтрЗаменить(ОберткаОтвета, "#TEXT", СоздатьПервоначальныйОбраз(УзелОбмена));
				
				// Удалим все изменения по узлу
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена);
				
				// очистим изменения по данному узлу
				ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
				
				Возврат Истина;
			ИначеЕсли Команда = "syncdata" Тогда // синхронизация объекта
				// выполним авторизацию
				ПользовательИБ        = Справочники.Пользователи.НайтиПоРеквизиту("ЛогинНаМобильномУстройстве", ИнформацияПользователя.login);
				УдалосьАвторизоваться = (ЗначениеЗаполнено(ПользовательИБ) И ПользовательИБ.ПарольНаМобильномУстройстве = ИнформацияПользователя.pass);
				
				Если НЕ УдалосьАвторизоваться Тогда
					Responce = ОтветПоШаблону("05", "Не удалось авторизоваться. Проверти правильность логина и пароля.", id, session_id);
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					
					Возврат Ложь;
				КонецЕсли;
				
				// получим изменения по Узлу
				ОбновитьИнформационнуюБазу(СтруктураЗапроса, ПользовательИБ, УзелОбмена);
				Изменения = ПланыОбмена.ОбменСМобильнымУстройством.ПолучитьИзменения(УзелОбмена);
				
				// Формируем файл выгрузки
				ОберткаОтвета = "<?xml version=""1.0"" encoding=""UTF-8""?>
								|<response cmd=""syncdata"" id=""" + Изменения.НомерСообщения +
								"""><resultinfo><code>0</code><description>ОК</description></resultinfo>#TEXT</response>";
				
				// формируем строку с информацией выгрузки
				Responce  = СтрЗаменить(ОберткаОтвета, "#TEXT", СформироватьXMLизменений(Изменения, УзелОбмена));
				
				ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
				
				Возврат Истина;
			ИначеЕсли Команда = "partialsync" Тогда
				
				// выполним авторизацию
				ПользовательИБ        = Справочники.Пользователи.НайтиПоРеквизиту("ЛогинНаМобильномУстройстве", ИнформацияПользователя.login);
				УдалосьАвторизоваться = (ЗначениеЗаполнено(ПользовательИБ) И ПользовательИБ.ПарольНаМобильномУстройстве = ИнформацияПользователя.pass);
				
				Если НЕ УдалосьАвторизоваться Тогда
					Responce = ОтветПоШаблону("05", "Не удалось авторизоваться. Проверти правильность логина и пароля.", id, session_id);
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					
					Возврат Ложь;
				КонецЕсли;
				
				// получим изменения по Узлу
				ОбъектыОбмена = ОбновитьИнформационнуюБазу(СтруктураЗапроса, ПользовательИБ, УзелОбмена, Истина);
				Изменения = ПланыОбмена.ОбменСМобильнымУстройством.ПолучитьИзменения(УзелОбмена, ОбъектыОбмена.ПринудительноВыгрузить);
				
				// Формируем файл выгрузки
				ОберткаОтвета = "<?xml version=""1.0"" encoding=""UTF-8""?>
								|<response cmd=""partialsync"" id=""" + Изменения.НомерСообщения +
								"""><resultinfo><code>0</code><description>ОК</description></resultinfo>#WARNING#TEXT</response>";
				ОберткаОтвета = СтрЗаменить(ОберткаОтвета,"#WARNING",?(ОбъектыОбмена.ОбъектыОбмена.Количество() = 0, "", "<warning>Need update</warning>"));
				Responce  = СтрЗаменить(ОберткаОтвета, "#TEXT", СформироватьXMLизменений(Изменения, УзелОбмена));
				ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
				Возврат Истина;
			ИначеЕсли Команда = "ticket" Тогда
				ОписаниеРезультата = Неопределено;
				Если СтруктураЗапроса.Свойство("resultinfo", ОписаниеРезультата) Тогда
					КодСообщения = ОписаниеРезультата.code;
					Описание     = ОписаниеРезультата.description;
					// если нет ошибок и прищел номер сообщения, то почистим регистрацию этих изменений
					Если КодСообщения = "0" И ЗаголовокЗапроса.Свойство("id", id) Тогда
						ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, Число(id));
						ПланыОбмена.ОбменСМобильнымУстройством.УстановитьНомерПринятого(УзелОбмена, Число(id));
					КонецЕсли;
				КонецЕсли;
				
				Responce = ОтветПоШаблону("0", "OK", id, session_id);
				
				Возврат Истина;
			ИначеЕсли Команда = "get_backorder" Тогда
				СписокID = Неопределено;
				Если СтруктураЗапроса.Свойство("СписокID", СписокID) Тогда
					Если СписокID <> Неопределено И СписокID.Количество() > 0 Тогда
						Responce = "<?xml version=""1.0"" encoding=""UTF-8""?><response cmd=""get_backorder"" id="""+id+
						"""><resultinfo><code>0</code><description>ОК</description></resultinfo><references name=""backorder"">"+
						СформироватьТаблицуПоЗапросу("backorder", СписокID)+"</references></response>";
						
						ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					Иначе
						// ответим что не передан параметр запроса
						Responce = ОтветПоШаблону("05", "Не удалось обнаружить завку по указанному адресу.", id, session_id);
						
						ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
						Возврат Ложь;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли Команда = "get_servicecampaign" ИЛИ Команда = "get_autohistory" Тогда
				Auto_VIN = Неопределено;
				Если СтруктураЗапроса.Свойство("Auto_VIN", Auto_VIN) И НЕ ПустаяСтрока(Auto_VIN) Тогда
					Если Команда = "get_servicecampaign" Тогда
						АвтомобильСсылка = Новый Структура("VIN, ОригинальныйVIN", Auto_VIN, Auto_VIN);
						Responce = "<?xml version=""1.0"" encoding=""UTF-8""?><response cmd=""get_servicecampaign"" id="""+id+"""><resultinfo><code>0</code><description>ОК</description></resultinfo><references name=""servicecampaign"">"+ СформироватьТаблицуПоЗапросу("servicecampaign", АвтомобильСсылка) +"</references></response>";
						ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					ИначеЕсли Команда = "get_autohistory" Тогда
						Responce = "<?xml version=""1.0"" encoding=""UTF-8""?><response cmd=""get_autohistory"" id="""+id+"""><resultinfo><code>0</code><description>ОК</description></resultinfo><references name=""autohistory"">"+ СформироватьТаблицуПоЗапросу("autohistory", Auto_VIN) +"</references></response>";
						ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли Команда = "get_logo" Тогда
				Если ЗначениеЗаполнено(УзелОбмена) Тогда
					Картинка = ПланыОбмена.ОбменСМобильнымУстройством.ПолучитьКартинкуУзлаОбмена(УзелОбмена);
					
					Если Картинка <> Неопределено Тогда
						Responce = "<?xml version=""1.0"" encoding=""UTF-8""?><response cmd=""get_logo"" id="""+id+
						"""><resultinfo><code>0</code><description>ОК</description></resultinfo><references name=""logo"">"+Base64Строка(Картинка)+"</references></response>";
						
						ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
						Возврат Истина;
					КонецЕсли;
					
					// ответим что не передан параметр запроса
					Responce = ОтветПоШаблону("05", "Не удалось получить картинку.", id, session_id);
					
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					Возврат Ложь;
				КонецЕсли;
			ИначеЕсли Команда = "сustom_аctions" Тогда
				
				ПользовательИБ        = Справочники.Пользователи.НайтиПоРеквизиту("ЛогинНаМобильномУстройстве", ИнформацияПользователя.login);
				УдалосьАвторизоваться = (ЗначениеЗаполнено(ПользовательИБ) И ПользовательИБ.ПарольНаМобильномУстройстве = ИнформацияПользователя.pass);
				
				Если НЕ УдалосьАвторизоваться Тогда
					Responce = ОтветПоШаблону("05", "Не удалось авторизоваться. Проверти правильность логина и пароля.", id, session_id);
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					
					Возврат Ложь;
				КонецЕсли;
				
				КомандаПользователя = Справочники.КомандыМобильногоПриложения.ПолучитьСсылку(Новый УникальныйИдентификатор(СтруктураЗапроса.CustomActions[0].ID));
				
				Документ = Неопределено;
				Если НЕ ПустаяСтрока(СтруктураЗапроса.CustomActions[0].CurrentEvent) Тогда
					Если СтруктураЗапроса.CustomActions[0].EventType = "1" Тогда
						Документ = Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(СтруктураЗапроса.CustomActions[0].CurrentEvent));
					Иначе
						Документ = Документы.Событие.ПолучитьСсылку(Новый УникальныйИдентификатор(СтруктураЗапроса.CustomActions[0].CurrentEvent));
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(КомандаПользователя) Тогда
					Responce = ОтветПоШаблону("11", "Не удалось идентифицировать команду.", id, session_id);
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					
					Возврат Ложь;
				ИначеЕсли НЕ КомандаПользователя.Активность Тогда
					Responce = ОтветПоШаблону("11", "Команда не активна.", id, session_id);
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					
					Возврат Ложь;
				КонецЕсли;
				
				Попытка
					КомандаПользователя(УзелОбмена, ПользовательИБ, КомандаПользователя, Документ);
				Исключение
					Responce = ОтветПоШаблону("11", "Не удалось поставить команду в очередь.", id, session_id);
					ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
					
					Возврат Ложь;
				КонецПопытки;
				
				Responce = ОтветПоШаблону("0", "OK", id, session_id);
				
			ИначеЕсли Команда = "print" Тогда
				ПечатныеФормы = Неопределено;
				Если СтруктураЗапроса.Свойство("print", ПечатныеФормы) Тогда
					Для Каждого ЗапросПечати Из ПечатныеФормы Цикл
						Документ = Неопределено;
						Если ЗапросПечати.Свойство("Документ", Документ) И ЗначениеЗаполнено(Документ) Тогда
							Если ЗапросПечати.ТипДокумента = "1" И ЗапросПечати.ПечатнаяФорма = "АктОсмотра" Тогда
								Попытка
									Документ = Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(Документ)).ПолучитьОбъект();
								Исключение
									Responce = ОтветПоШаблону("20", "Не удалось получить документ.", id, session_id);
									Возврат Ложь;
								КонецПопытки;
								Ошибки = Неопределено;
								Если Печать(Документ, УзелОбмена, Справочники.Пользователи.ПолучитьСсылку(Новый УникальныйИдентификатор(ЗапросПечати.Пользователь)), Ошибки) Тогда
									Responce = ОтветПоШаблону("0", "OK", id, session_id);
									Возврат Истина;
								Иначе
									Responce = ОтветПоШаблону("21", Ошибки, id, session_id);
									Возврат Ложь;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				Responce = ОтветПоШаблону("0", "OK", id, session_id);
			ИначеЕсли Команда = "get_attach" Тогда
				СписокID = Неопределено;
				Если СтруктураЗапроса.Свойство("СписокID", СписокID) И СписокID <> Неопределено Тогда
					Если СписокID.Количество() > 0 Тогда
						Результат = ВыгрузитьФайлыДополнительногоПредложения(СписокID, Responce, id, session_id);
						
						Возврат Результат;
					КонецЕсли;
				КонецЕсли;
			Иначе
				// если не смоги опознать команду
				Responce = ОтветПоШаблону("05", "Не опознанный запрос "+Команда, id, session_id);
				ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
	Исключение
		ТекстСообщения = ИнформацияОбОшибке();
		Responce = ОтветПоШаблону("01", обЗаменитьСпециальныеСимволыXML(ОписаниеОшибки()), id, session_id);
		ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, Команда);
		Возврат Ложь;
	КонецПопытки;
КонецФункции

// получение остаков по допродажам
Функция ORM_GetBalance(GUID, Model, ClientID, Responce) Экспорт
	
	Попытка
		УзелОбмена = Неопределено; Responce = "";
		Допродажа  = Справочники.Автоработы.ПолучитьСсылку(Новый УникальныйИдентификатор(GUID));
		Модель     = Справочники.Модели.ПолучитьСсылку(Новый УникальныйИдентификатор(Model));
		
		Если Допродажа.Пустая() Тогда
			Responce = ОтветПоШаблону("020", "Не указана допродажа.");
			Возврат Ложь;
		КонецЕсли;
		
		Если НЕ ПланыОбмена.ОбменСМобильнымУстройством.СоздатьНовыйУзелДляУстройтва(ClientID, УзелОбмена) Тогда
			Responce = ОтветПоШаблону("01", "Не удалось создать узел обмена для данного устройства.");
			Возврат Ложь;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		
		// получим список подразделений
		Если УзелОбмена.Подразделения.Количество() > 0 Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ВложенныйЗапрос.Ссылка КАК ПодразделениеКомпании
			|ИЗ
			|	(";
			
			Сч = 0; ТекстВложенногоЗапроса = "";
			Для Каждого Подразделение Из УзелОбмена.Подразделения Цикл
				ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + ?(ПустаяСтрока(ТекстВложенногоЗапроса), "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) +
				СтрЗаменить("ВЫБРАТЬ
				|	ПодразделенияКомпании.Ссылка
				|ИЗ
				|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
				|ГДЕ
				|	ПодразделенияКомпании.Ссылка В ИЕРАРХИИ(&Ссылка#)", "#", Сч);
				
				Запрос.УстановитьПараметр("Ссылка"+Сч, Подразделение.ПодразделениеКомпании);
				Сч = Сч + 1;
			КонецЦикла;
			ТекстЗапроса = ТекстЗапроса + ТекстВложенногоЗапроса + ") КАК ВложенныйЗапрос";
			Запрос.Текст = ТекстЗапроса;
			МассивПодразделений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПодразделениеКомпании");
			Запрос.УстановитьПараметр("Подразделения", МассивПодразделений);
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.СкладКомпании, ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)) КАК СкладКомпании,
		|	МИНИМУМ(ВЫРАЗИТЬ(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) / ВЫБОР КОГДА СвязанныеРаботы.Количество = 0 ТОГДА 1 ИНАЧЕ СвязанныеРаботы.Количество КОНЕЦ КАК ЧИСЛО(12, 0))) КАК RemainTotal,
		|	МИНИМУМ(ВЫРАЗИТЬ(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0) / ВЫБОР КОГДА СвязанныеРаботы.Количество = 0 ТОГДА 1 ИНАЧЕ СвязанныеРаботы.Количество КОНЕЦ КАК ЧИСЛО(12, 0))) КАК RemainReserved,
		|	МИНИМУМ(ВЫРАЗИТЬ((ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) / ВЫБОР КОГДА СвязанныеРаботы.Количество = 0 ТОГДА 1 ИНАЧЕ СвязанныеРаботы.Количество КОНЕЦ КАК ЧИСЛО(12, 0))) КАК RemainFree
		|ИЗ
		|	РегистрСведений.СвязанныеРаботы КАК СвязанныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, "+?(МассивПодразделений = Неопределено, "", "СкладКомпании.Подразделение В (&Подразделения)")+") КАК ОстаткиТоваровКомпанииОстатки
		|		ПО СвязанныеРаботы.СвязаннаяРабота = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|ГДЕ
		|	СвязанныеРаботы.Модель = &Модель
		|	И СвязанныеРаботы.Авторабота = &Авторабота
		|	И СвязанныеРаботы.СвязаннаяРабота ССЫЛКА Справочник.Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.СкладКомпании, ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка))";
		
		Запрос.УстановитьПараметр("Авторабота", Допродажа);
		Запрос.УстановитьПараметр("Модель"    , Модель);
		Выборка = Запрос.Выполнить().Выбрать();
		
		// начало блока событий
		
		Responce = Responce + "<?xml version=""1.0"" encoding=""UTF-8""?><response cmd=""RemainAdditionalSales"" id=""1""><resultinfo><code>0</code><description>ОК</description></resultinfo><references name=""RemainAdditionalSales"" command=""clear"">";
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.RemainTotal = 0 Тогда Продолжить; КонецЕсли;
			Responce = Responce + "<row>";
			Responce = Responce + "<field name=""ID"">"             + Новый УникальныйИдентификатор() + "</field>";
			Responce = Responce + "<field name=""Warehouse"">"      + обЗаменитьСпециальныеСимволыXML(спПолучитьНаименование(Выборка.СкладКомпании)) + "</field>";
			Responce = Responce + "<field name=""RemainTotal"">"    + XMLСтрока(Выборка.RemainTotal) + "</field>";
			Responce = Responce + "<field name=""RemainReserved"">" + XMLСтрока(Выборка.RemainReserved) + "</field>";
			Responce = Responce + "<field name=""RemainFree"">"     + XMLСтрока(Выборка.RemainFree) + "</field>";
			Responce = Responce + "</row>";
		КонецЦикла;
		
		// закроем блок
		Responce = Responce + "</references></response>";
		
		Возврат Истина;
	Исключение
		ТекстСообщения = ИнформацияОбОшибке();
		
		Responce = ОтветПоШаблону("01", обЗаменитьСпециальныеСимволыXML(ОписаниеОшибки()));
		ЗаписатьСобытиеВЛог(УзелОбмена, Responce, Истина, "ORM_GetBalance");
		Возврат Ложь;
	КонецПопытки;
КонецФункции

// проверка совместимости клиента и сервера
Функция CheckClientServerCompatibility(ClientProtocolVersion, ServerProtocolVersion, Code, Message)
	
	ServerProtocolVersion = 1;
	Code                  = 0;
	Message               = "Ok";
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыРаботыСЗащитой

// Получение лицензии и номера для мобильного устройства
//
Функция ПолучитьЛицензиюМобильногоУстройства(Токен, Лицензия, ОписаниеОшибки) 
	СписокРешений = ЛицензированиеПоддержка.СписокРешений();
	РезультатыЗапуска = Новый Массив;
	РезультатыЗапускаПоВсемСерверам = Новый Массив;
	Ошибка = Ложь;
	
	АдресаСерверовЛицензирования = ЛицензированиеСервер.СписокАдресовСервераЛицензирования();
	
	КоличествоСерверов = АдресаСерверовЛицензирования.Количество();
	Сч = 1;
	Для Каждого АдресСервера Из АдресаСерверовЛицензирования Цикл
		
		ЛицензированиеСервер.УстановитьПараметрСеансаТекущийАдресСервераЛицензирования(АдресСервера);
		
		Для Каждого Решение Из СписокРешений Цикл
			ОписаниеОшибки = "";
			КодОшибки = 0;
			ЛицензированиеСервер.НачалоРаботыСистемыЛицензирования(Решение.Ключ, ОписаниеОшибки, КодОшибки);
			Если ОписаниеОшибки<>"" Тогда
				Ошибка = Истина;
			КонецЕсли;
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ОписаниеОшибки",ОписаниеОшибки);
			ПараметрыФормы.Вставить("КодОшибки",КодОшибки);
			ПараметрыФормы.Вставить("ПрограммноеОткрытиеФормы",Истина);
			ПараметрыФормы.Вставить("ИмяОбработки", Решение.Ключ);
			ПараметрыФормы.Вставить("ИмяРешения", Решение.Значение);
			РезультатыЗапуска.Добавить(ПараметрыФормы); 
			РезультатыЗапускаПоВсемСерверам.Добавить(ПараметрыФормы);
		КонецЦикла;
		
	КонецЦикла;
	
	////Подключение к системе лицензирования
	//СписокРешений = ЛицензированиеПоддержка.ПолучитьСписокРешений();
	//Ошибка = Ложь;
	//
	//Для Каждого Решение Из СписокРешений Цикл
	//	КодОшибки = 0;
	//	
	//	// Проверка и разбор длинного имени
	//	ИмяОбработки = Решение.Ключ; ИмяМакетаХранилища = ""; ИмяМакетаПараметровЛицензирования = ""; КороткоеИмяОбработки = "";
	//	Если НЕ ЛицензированиеСервер.РазделитьИмяОбработки(ИмяОбработки, ИмяМакетаХранилища, ИмяМакетаПараметровЛицензирования, КороткоеИмяОбработки) Тогда
	//		ОписаниеОшибки = "Неверный формат имени обработки:"+Символы.ПС+ИмяОбработки;
	//		КодОшибки = 1;
	//		Возврат Ложь;
	//	КонецЕсли;	
	//	КлючПродукта = ИмяМакетаХранилища+"."+ИмяМакетаПараметровЛицензирования;
	//	
	//	ОписаниеОшибки = ""; КодОшибки = 0;
	//	
	//	ЛицензированиеСервер.НачалоРаботыСистемыЛицензирования(Решение.Ключ, ОписаниеОшибки, КодОшибки);
	//	
	//	Если НЕ ПустаяСтрока(ОписаниеОшибки) Тогда Ошибка = Истина; КонецЕсли;
	//КонецЦикла;
	//
	Если Ошибка Тогда	
		ЛицензированиеСервер.ЗавершениеРаботыСистемыЛицензирования(ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	
	// получение номера лицензии по токену
	Результат = зфЗащищенныеФункцииСервер.ПолучитьЛицензиюМобильногоУстройства(Токен, Лицензия, ОписаниеОшибки);
	ЛицензированиеСервер.ЗавершениеРаботыСистемыЛицензирования(ОписаниеОшибки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФормированиеСообщений

// Составляет структуру данных из присланного запроса
//
// Параметры:
//	ТекстСообщения - Строка - Разбираемое сообщение
//
// Возвращаемое значение - Структура - Структура с данными
//
Функция РазобратьСтрокуСообщения(ТекстСообщения)
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("СписокID", Новый Массив);
	
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстСообщения);
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ЧтениеXML.Имя = "request" Тогда
				ЗаголовокЗапроса = Новый Структура;
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					ЗаголовокЗапроса.Вставить(СокрЛП(ЧтениеXML.Имя), СокрЛП(ЧтениеXML.Значение));
				КонецЦикла;
				
				СтруктураДанных.Вставить("request", ЗаголовокЗапроса);
			ИначеЕсли ЧтениеXML.Имя = "authorizationinfo" ИЛИ ЧтениеXML.Имя = "resultinfo" Тогда
				ИнформацияПользователя = Новый Структура;
				ИмяСекции = ЧтениеXML.Имя;
				
				Пока ЧтениеXML.Прочитать() Цикл
					Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И (ЧтениеXML.Имя = "authorizationinfo" ИЛИ ЧтениеXML.Имя = "resultinfo") Тогда
						Прервать;
					КонецЕсли;
					
					ИмяУзла  = "";
					ЗначУзла = "";
					
					Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						ИмяУзла = ЧтениеXML.Имя;
						ЧтениеXML.Прочитать();
						ЗначУзла = ЧтениеXML.Значение;
						ИнформацияПользователя.Вставить(ИмяУзла, ЗначУзла);
					КонецЕсли;
				КонецЦикла;
				СтруктураДанных.Вставить(ИмяСекции, ИнформацияПользователя);
			ИначеЕсли ЧтениеXML.Имя = "id" Тогда
				ЧтениеXML.Прочитать();
				
				Значение = ЧтениеXML.Значение;
				
				СтруктураДанных.СписокID.Добавить(Значение);
			ИначеЕсли ЧтениеXML.Имя = "token" Тогда
				ЧтениеXML.Прочитать();
				СтруктураДанных.Вставить("token", Число(ЧтениеXML.Значение));
			ИначеЕсли ЧтениеXML.Имя = "VIN" Тогда
				ЧтениеXML.Прочитать();
				СтруктураДанных.Вставить("Auto_VIN", ЧтениеXML.Значение);
			ИначеЕсли ЧтениеXML.Имя = "references" Тогда
				ОписаниеОбъектов = Неопределено; ТипОбъекта = ЧтениеXML.ПолучитьАтрибут("name");
				СтруктураДанных.Свойство(ТипОбъекта, ОписаниеОбъектов);
				Если ОписаниеОбъектов = Неопределено Тогда
					ОписаниеОбъектов = Новый Массив;
				КонецЕсли;
				Пока ЧтениеXML.Прочитать() Цикл
					Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "row" Тогда
						Объект = Новый Структура;
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "deleted" Тогда
						УдаляемыеОбъекты = Новый Массив;
						Пока Истина Цикл
							ЧтениеXML.Прочитать();
							Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "deleted" Тогда
								Прервать;
							КонецЕсли;
							
							Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "item" Тогда
								Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
									Если ЧтениеXML.ЛокальноеИмя = "id" Тогда
										УдаляемыеОбъекты.Добавить(ЧтениеXML.Значение);
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
						КонецЦикла;
						
						СтруктураДанных.Вставить(ТипОбъекта+"Deleted", УдаляемыеОбъекты);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "field" Тогда
						Поле = ЧтениеXML.ПолучитьАтрибут("name");
						ЧтениеXML.Прочитать();
						Объект.Вставить(Поле, ЧтениеXML.Значение);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "id" И ТипОбъекта = "print" Тогда
						ЧтениеXML.Прочитать();
						Объект.Вставить("Документ", ЧтениеXML.Значение);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "userid" И ТипОбъекта = "print" Тогда
						ЧтениеXML.Прочитать();
						Объект.Вставить("Пользователь", ЧтениеXML.Значение);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "event_type" И ТипОбъекта = "print" Тогда
						ЧтениеXML.Прочитать();
						Объект.Вставить("ТипДокумента", ЧтениеXML.Значение);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "print_form" И ТипОбъекта = "print" Тогда
						ЧтениеXML.Прочитать();
						Объект.Вставить("ПечатнаяФорма", ЧтениеXML.Значение);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "row" Тогда
						ОписаниеОбъектов.Добавить(Объект);
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "references" Тогда
						СтруктураДанных.Вставить(ТипОбъекта, ОписаниеОбъектов);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "request" Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураДанных;
	
КонецФункции

// Формирует отмет по шаблону
//
Функция ОтветПоШаблону(КодОшибки, ОписаниеОшибки, id = "", idСесии = "")
	// заполним шаблоны ответов
	ШаблонОтвета = "<?xml version=""1.0"" encoding=""UTF-8""?><response id=""#ID#""><resultinfo><code>#CODE#</code><description>#DESCRIPTION#</description></resultinfo><session_id>#SESSION_ID#</session_id></response>";
	
	// Подменяем значения
	ТекстОтвета = СтрЗаменить(ШаблонОтвета, "#CODE#"       , КодОшибки);
	ТекстОтвета = СтрЗаменить(ТекстОтвета , "#ID#"         , id);
	ТекстОтвета = СтрЗаменить(ТекстОтвета , "#SESSION_ID#" , idСесии);
	ТекстОтвета = СтрЗаменить(ТекстОтвета , "#DESCRIPTION#", ОписаниеОшибки);
	
	Возврат ТекстОтвета;
КонецФункции

// Создание ответа на запрос первой загрузки
//
Функция СоздатьПервоначальныйОбраз(Узел)
	ВыгруженныеЗаявки = Новый Массив; ВыгруженныеСобытия = Новый Массив;
	Образ = "";
	
	Запрос = Новый Запрос;
	
	ЕстьОтборПоПодразделениюКомпании = (Узел.Подразделения.Количество() <> 0);
	
	// выгружаем документ событий
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявкаНаРемонт.Ссылка КАК Заявка
	|ПОМЕСТИТЬ ЗаявкиНаРемонт
	|ИЗ
	|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	|ГДЕ
	|	ЗаявкаНаРемонт.ДатаНачала МЕЖДУ &ДатаНач И &ДатаКон
	|	И ЗаявкаНаРемонт.ПометкаУдаления = ЛОЖЬ
	|	"+?(ЕстьОтборПоПодразделениюКомпании,"И ЗаявкаНаРемонт.ПодразделениеКомпании В(&ПодразделениеКомпании)","")+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Событие.Ссылка КАК Заявка
	|ПОМЕСТИТЬ События
	|ИЗ
	|	Документ.Событие КАК Событие
	|ГДЕ
	|	Событие.ДатаНачала МЕЖДУ &ДатаНач И &ДатаКон
	|	И Событие.ВидСобытия В(&СписокДоступныхСтатусов)
	|	И Событие.ПометкаУдаления = ЛОЖЬ
	|	"+?(ЕстьОтборПоПодразделениюКомпании,"И Событие.ПодразделениеКомпании В(&ПодразделениеКомпании)", "")+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаРемонт.Заявка
	|ИЗ
	|	ЗаявкиНаРемонт КАК ЗаявкиНаРемонт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	События.Заявка
	|ИЗ
	|	События КАК События";
	
	ДоступныеСтатусы = Новый Массив;
	ДоступныеСтатусы.Добавить(Перечисления.ВидыСобытий.ТелефонныйЗвонок);
	ДоступныеСтатусы.Добавить(Перечисления.ВидыСобытий.SMS);
	ДоступныеСтатусы.Добавить(Перечисления.ВидыСобытий.ЭлектронноеПисьмо);
	
	Запрос.УстановитьПараметр("СписокДоступныхСтатусов", ДоступныеСтатусы);
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(НачалоДня(ТекущаяДата())-1));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(КонецДня(ТекущаяДата())+1));
	
	Если ЕстьОтборПоПодразделениюКомпании Тогда
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Узел.Подразделения.ВыгрузитьКолонку("ПодразделениеКомпании"));
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Количество() > 0 Тогда
		// начало блока событий
		Образ = Образ + "<references name=""EventItem"" command=""clear"">";
		
		Пока РезультатЗапроса.Следующий() Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(РезультатЗапроса.Заявка, "EventItem");
			Если ТипЗнч(РезультатЗапроса.Заявка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
				ВыгруженныеЗаявки.Добавить(РезультатЗапроса.Заявка);
			ИначеЕсли ТипЗнч(РезультатЗапроса.Заявка) = Тип("ДокументСсылка.Событие") Тогда
				ВыгруженныеСобытия.Добавить(РезультатЗапроса.Заявка);
			КонецЕсли;
		КонецЦикла;
		
		// закроем блок
		Образ = Образ + "</references>";
	КонецЕсли;
	
	// Выгружаем чек-лис по заявкам
	Если ВыгруженныеЗаявки.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗначениеCheckList.Объект                          КАК Заявка,
		|	ЗначениеCheckList.CheckList                       КАК CheckList,
		|	ЗначениеCheckList.GUID                            КАК guid,
		|	ЗначениеCheckList.Значение                        КАК Значение,
		|	ЗначениеCheckList.Checked                         КАК Checked,
		|	ЗначениеCheckList.Порядок                         КАК Порядок,
		|	ЗначениеCheckList.CheckList.Описание              КАК CheckListОписание,
		|	ЗначениеCheckList.CheckList.ЗначенияВыбора        КАК CheckListЗначенияВыбора,
		|	ЗначениеCheckList.CheckList.ТипЗначения           КАК CheckListТипЗначения,
		|	ЗначениеCheckList.CheckList.Родитель.Наименование КАК CheckListGroup
		|ИЗ
		|	РегистрСведений.ЗначениеCheckList КАК ЗначениеCheckList
		|ГДЕ
		|	ЗначениеCheckList.Объект В(&Объекты)";
		Запрос.УстановитьПараметр("Объекты", ВыгруженныеЗаявки);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		// начало блока событий
		Образ = Образ + "<references name=""CheckList"" command=""clear"">";
		Пока Выборка.Следующий() Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, "CheckList");
		КонецЦикла;
		
		// закроем блок
		Образ = Образ + "</references>";
	КонецЕсли;
	
	// Выгружаем участников событий
	Если ВыгруженныеСобытия.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СобытиеПользователи.Ссылка       КАК Событие,
		|	СобытиеПользователи.Пользователь КАК Пользователь
		|ИЗ
		|	Документ.Событие.Пользователи КАК СобытиеПользователи
		|ГДЕ
		|	СобытиеПользователи.Ссылка В(&СписокСобытий)";
		Запрос.УстановитьПараметр("СписокСобытий", ВыгруженныеСобытия);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		// начало блока событий
		Образ = Образ + "<references name=""Participants"" command=""clear"">";
		Пока Выборка.Следующий() Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, "Participants");
		КонецЦикла;
		
		// закроем блок
		Образ = Образ + "</references>"; 
	КонецЕсли;
	
	// выгружаем дополнительные предложения и их премменяемость
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Автоработы.Ссылка,
	|	Автоработы.Родитель,
	|	Автоработы.Наименование,
	|	Автоработы.НаименованиеПолное,
	|	Автоработы.Комментарий
	|ИЗ
	|	Справочник.Автоработы КАК Автоработы
	|ГДЕ
	|	Автоработы.ЭтоДополнительноеПредложение = ИСТИНА
	|	И Автоработы.ЭтоГруппа = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыгруженныеДопПредложения = Новый Массив;
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		// начало блока событий
		Образ = Образ + "<references name=""DirectoryAdditionalSales"" command=""clear"">";
		
		Пока Выборка.Следующий() Цикл
			ВыгруженныеДопПредложения.Добавить(Выборка.Родитель);
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, "DirectoryAdditionalSales");
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Автоработы.Ссылка,
	|	Автоработы.Родитель,
	|	Автоработы.Наименование,
	|	ВЫРАЗИТЬ(Автоработы.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеПолное
	|ИЗ
	|	Справочник.Автоработы КАК Автоработы
	|ГДЕ
	|	Автоработы.Ссылка В(&Ссылки)
	|	И Автоработы.ЭтоГруппа = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	Автоработы.Ссылка,
	|	Автоработы.Родитель,
	|	Автоработы.Наименование,
	|	ВЫРАЗИТЬ(Автоработы.НаименованиеПолное КАК СТРОКА(1000))";
	Запрос.УстановитьПараметр("Ссылки", ВыгруженныеДопПредложения);
	
	РезультатЗапросаГруппы = Запрос.Выполнить();
	
	Если НЕ РезультатЗапросаГруппы.Пустой() Тогда
		Выборка = РезультатЗапросаГруппы.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, "DirectoryAdditionalSalesGroup");
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		// закроем блок
		Образ = Образ + "</references>";
	КонецЕсли;
	
	// выгрузим команды МП
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КомандыМобильногоПриложения.Ссылка,
	|	КомандыМобильногоПриложения.Наименование,
	|	КомандыМобильногоПриложения.ПредставлениеНаМобильномУстройстве,
	|	КомандыМобильногоПриложения.Порядок,
	|	КомандыМобильногоПриложения.Синхронизация,
	|	КомандыМобильногоПриложения.ПометкаУдаления,
	|	КомандыМобильногоПриложения.Активность
	|ИЗ
	|	Справочник.КомандыМобильногоПриложения КАК КомандыМобильногоПриложения";
	Выборка = Запрос.Выполнить().Выбрать();
	
	// начало блока событий
	Образ = Образ + "<references name=""CustomActions"" command=""clear"">";
	Пока Выборка.Следующий() Цикл
		Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, "CustomActions");
	КонецЦикла;
	
	// закроем блок
	Образ = Образ + "</references>";
	
	// Подготовим данные для расчета цены
	// получим базовый тип цен
	БазовыйТипЦен = Узел.ТипЦенРабот; МножительТипаЦен = 1;
	Пока БазовыйТипЦен.Рассчитывается Цикл
		МножительТипаЦен = МножительТипаЦен*(1+БазовыйТипЦен.ПроцентСкидкиНаценки/100);
		БазовыйТипЦен = БазовыйТипЦен.БазовыйТипЦен;
	КонецЦикла;
	
	// Получим таблицу цехов
	ТаблицаЦехов = Новый ТаблицаЗначений();
	ТаблицаЦехов.Колонки.Добавить("Цех", Новый ОписаниеТипов("СправочникСсылка.Цеха"));
	ТаблицаЦехов.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
	
	Приоритет = 0; ЦехРодитель = Узел.Цех;
	Пока Истина Цикл
		НоваяСтрока = ТаблицаЦехов.Добавить();
		НоваяСтрока.Цех       = ЦехРодитель;
		НоваяСтрока.Приоритет = Приоритет;
		
		НовыйЦехРодитель = ЦехРодитель.Родитель;
		Если НовыйЦехРодитель.Пустая() Тогда
			Прервать;
		КонецЕсли;
		
		Приоритет = Приоритет + 1;
		ЦехРодитель = НовыйЦехРодитель;
	КонецЦикла;
	
	// добавим пустой цех
	НоваяСтрока = ТаблицаЦехов.Добавить();
	НоваяСтрока.Цех       = Справочники.Цеха.ПустаяСсылка();
	НоваяСтрока.Приоритет = Приоритет;

	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Автоработы.Ссылка КАК Авторабота,
	|	Автоработы.Родитель КАК Родитель
	|ПОМЕСТИТЬ Допродажи
	|ИЗ
	|	Справочник.Автоработы КАК Автоработы
	|ГДЕ
	|	Автоработы.ЭтоДополнительноеПредложение = ИСТИНА
	|	И Автоработы.ЭтоГруппа = ЛОЖЬ
	|ИНДЕКСИРОВАТЬ ПО
	|	Авторабота,
	|	Родитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗЦеха.Цех КАК Цех,
	|	ТЗЦеха.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ Цеха
	|ИЗ
	|	&Цеха КАК ТЗЦеха
	|ИНДЕКСИРОВАТЬ ПО
	|	Цех
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныАвтоработСрезПоследних.Цех КАК Цех,
	|	ЦеныАвтоработСрезПоследних.Модель КАК Модель,
	|	ЦеныАвтоработСрезПоследних.Авторабота КАК Авторабота,
	|	ЦеныАвтоработСрезПоследних.Нормочас,
	|	ЦеныАвтоработСрезПоследних.Цена,
	|	ЦеныАвтоработСрезПоследних.Валюта,
	|	ЕСТЬNULL(Цеха.Приоритет, 99) КАК Приоритет
	|ПОМЕСТИТЬ Цены
	|ИЗ
	|	РегистрСведений.ЦеныАвторабот.СрезПоследних(
	|			&Момент,
	|			(Авторабота В
	|					(ВЫБРАТЬ
	|						Допродажи.Авторабота
	|					ИЗ
	|						Допродажи КАК Допродажи)
	|				ИЛИ Авторабота В
	|					(ВЫБРАТЬ
	|						Допродажи.Родитель
	|					ИЗ
	|						Допродажи КАК Допродажи))
	|				И Цех В
	|					(ВЫБРАТЬ
	|						Цеха.Цех
	|					ИЗ
	|						Цеха КАК Цеха)
	|				И ВидРемонта = &ВидРемонта
	|				И ТипЦен = &ТипЦен) КАК ЦеныАвтоработСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цеха КАК Цеха
	|		ПО ЦеныАвтоработСрезПоследних.Цех = Цеха.Цех
	|ИНДЕКСИРОВАТЬ ПО
	|	Цех,
	|	Модель,
	|	Авторабота
	|;
	|
	|ВЫБРАТЬ
	|	АвтоработыНормыВремени.Ссылка КАК Авторабота,
	|	АвтоработыНормыВремени.Модель КАК Модель,
	|	МАКСИМУМ(АвтоработыНормыВремени.ВремяВыполнения) КАК ВремяВыполнения
	|ПОМЕСТИТЬ ВремяВыполненияАвторабот
	|ИЗ
	|	Справочник.Автоработы.НормыВремени КАК АвтоработыНормыВремени
	|ГДЕ
	|	АвтоработыНормыВремени.Ссылка В (ВЫБРАТЬ Допродажи.Авторабота ИЗ Допродажи КАК Допродажи)
	|СГРУППИРОВАТЬ ПО
	|АвтоработыНормыВремени.Модель,
	|АвтоработыНормыВремени.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Автоработы.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Модели.ПустаяСсылка),
	|	Автоработы.ВремяВыполнения
	|ИЗ
	|	Справочник.Автоработы КАК Автоработы
	|ГДЕ
	|	Автоработы.Ссылка В (ВЫБРАТЬ Допродажи.Авторабота ИЗ Допродажи КАК Допродажи)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Авторабота,
	|	Модель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Допродажи.Авторабота КАК Авторабота,
	|	Допродажи.Родитель КАК Родитель,
	|	ПрименяемостьДополнительныхПредложенийПоМоделям.GUID,
	|	ЕСТЬNULL(ПрименяемостьДополнительныхПредложенийПоМоделям.Модель, ЗНАЧЕНИЕ(Справочник.Модели.ПустаяСсылка)) КАК Модель
	|ПОМЕСТИТЬ ДопродажиСПрименяемостью
	|ИЗ
	|	Допродажи КАК Допродажи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрименяемостьДополнительныхПредложенийПоМоделям КАК ПрименяемостьДополнительныхПредложенийПоМоделям
	|		ПО Допродажи.Авторабота = ПрименяемостьДополнительныхПредложенийПоМоделям.ДополнительноеПредложение
	|
	|;
	|
	|УНИЧТОЖИТЬ Допродажи;
	|
	|УНИЧТОЖИТЬ Цеха;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Допродажи.Авторабота КАК Авторабота,
	|	Допродажи.Модель КАК Модель,
	|	Допродажи.GUID,
	|	ЕСТЬNULL(Цены.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(Цены.Нормочас, ЗНАЧЕНИЕ(Справочник.НормоЧасы.ПустаяСсылка)) КАК Нормочас,
	|	Цены.Валюта,
	|	ЕСТЬNULL(Цены.Приоритет, 99) КАК Приоритет,
	|	ЕСТЬNULL(ЦеныРодителей.Цена, 0) КАК ЦенаРодителя,
	|	ЕСТЬNULL(ЦеныРодителей.Нормочас, ЗНАЧЕНИЕ(Справочник.НормоЧасы.ПустаяСсылка))  КАК НормочасРодителя,
	|	ЦеныРодителей.Валюта КАК ВалютаРодителя,
	|	ЕСТЬNULL(ЦеныРодителей.Приоритет, 0) КАК ПриоритетРодителя,
	|	ВремяВыполненияАвторабот.ВремяВыполнения
	|ИЗ
	|	ДопродажиСПрименяемостью КАК Допродажи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремяВыполненияАвторабот КАК ВремяВыполненияАвторабот
	|		ПО Допродажи.Авторабота = ВремяВыполненияАвторабот.Авторабота И
	|			Допродажи.Модель = ВремяВыполненияАвторабот.Модель
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цены КАК ЦеныРодителей
	|		ПО Допродажи.Родитель = ЦеныРодителей.Авторабота И
	|			Допродажи.Модель = ЦеныРодителей.Модель
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цены КАК Цены
	|		ПО Допродажи.Авторабота = Цены.Авторабота И
	|			Допродажи.Модель = Цены.Модель
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ПриоритетРодителя
	|ИТОГИ
	|	СУММА(Цена),
	|	СУММА(ЦенаРодителя)
	|ПО
	|	Авторабота,
	|	Модель";
	
	Запрос.УстановитьПараметр("Момент"    , ТекущаяДата());
	Запрос.УстановитьПараметр("ВидРемонта", Узел.ВидРемонта);
	Запрос.УстановитьПараметр("ТипЦен"    , БазовыйТипЦен);
	Запрос.УстановитьПараметр("Цеха"      , ТаблицаЦехов);
	
	// создадим таблицу получивщихся авторабот
	ТЗАвторабот = Новый ТаблицаЗначений;
	ТЗАвторабот.Колонки.Добавить("Авторабота", Новый ОписаниеТипов("СправочникСсылка.Автоработы"));
	ТЗАвторабот.Колонки.Добавить("Модель", Новый ОписаниеТипов("СправочникСсылка.Модели"));
	ТЗАвторабот.Колонки.Добавить("GUID", Новый ОписаниеТипов("Строка"));
	ТЗАвторабот.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаАвтоработы = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаАвтоработы.Следующий() Цикл
			ВыборкаМоделей = ВыборкаАвтоработы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ЦенаНайдена = Ложь;
			Пока ВыборкаМоделей.Следующий() Цикл
				НоваяСтрока = ТЗАвторабот.Добавить();
				
				НоваяСтрока.Модель     = ВыборкаМоделей.Модель;
				НоваяСтрока.Авторабота = ВыборкаМоделей.Авторабота;
				
				Выборка = ВыборкаМоделей.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					Если НЕ ЗначениеЗаполнено(НоваяСтрока.GUID) Тогда
						Если ЗначениеЗаполнено(Выборка.GUID) Тогда
							НоваяСтрока.GUID = Выборка.GUID;
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(Выборка.Цена) Тогда
						НоваяСтрока.Цена = Выборка.Цена*МножительТипаЦен;
						ЦенаНайдена = Истина;
						Прервать;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(Выборка.Нормочас) Тогда
						НоваяСтрока.Цена = Выборка.Нормочас.Цена*Выборка.ВремяВыполнения*МножительТипаЦен;
						ЦенаНайдена = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ ЦенаНайдена Тогда
					Выборка.Сбросить();
					
					Пока Выборка.Следующий() Цикл
						Если ЗначениеЗаполнено(Выборка.ЦенаРодителя) Тогда
							НоваяСтрока.Цена = Выборка.ЦенаРодителя*МножительТипаЦен;
							ЦенаНайдена = Истина;
							Прервать;
						КонецЕсли;
						
						Если ЗначениеЗаполнено(Выборка.НормочасРодителя) Тогда
							НоваяСтрока.Цена = Выборка.НормочасРодителя.Цена*Выборка.ВремяВыполнения*МножительТипаЦен;
							ЦенаНайдена = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		// начало блока событий
		Образ = Образ + "<references name=""Applicability"" command=""clear"">";
		
		Для Каждого Строка Из ТЗАвторабот Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Строка, "Applicability");
		КонецЦикла;
		
		// закроем блок
		Образ = Образ + "</references>";
	КонецЕсли;
	
	ЗапросПоСправочникам = Новый Запрос;
	
	// установим параметры
	ЗапросПоСправочникам.УстановитьПараметр("ПустаяСтрока", "");
	ЗапросПоСправочникам.УстановитьПараметр("ВыгруженныеЗаявки", ВыгруженныеЗаявки);
	ЗапросПоСправочникам.УстановитьПараметр("ВыгруженныеСобытия", ВыгруженныеСобытия);
	
	// создадим менеджер
	ЗапросПоСправочникам.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗапросПоСправочникам.Текст =
	"ВЫБРАТЬ
	|	ЗаявкаНаРемонт.Организация,
	|	ЗаявкаНаРемонт.ПодразделениеКомпании,
	|	ЗаявкаНаРемонт.Заказчик,
	|	ЗаявкаНаРемонт.Контрагент,
	|	ЗаявкаНаРемонт.Автомобиль,
	|	ЗаявкаНаРемонт.СервиснаяКампания
	|ПОМЕСТИТЬ ВыгружаемыеПоляЗаявки
	|ИЗ
	|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	|ГДЕ
	|	ЗаявкаНаРемонт.Ссылка В(&ВыгруженныеЗаявки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Событие.Организация,
	|	Событие.ПодразделениеКомпании,
	|	Событие.КонтактноеЛицо,
	|	Событие.Контрагент,
	|	Событие.Автомобиль,
	|	NULL
	|ИЗ
	|	Документ.Событие КАК Событие
	|ГДЕ
	|	Событие.Ссылка В(&ВыгруженныеСобытия)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	СобытиеАвтомобили.Автомобиль,
	|	NULL
	|ИЗ
	|	Документ.Событие.Автомобили КАК СобытиеАвтомобили
	|ГДЕ
	|	СобытиеАвтомобили.Ссылка В(&ВыгруженныеСобытия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пользователи.Ссылка,
	|	Пользователи.Наименование,
	|	Пользователи.Сотрудник,
	|	Пользователи.ЛогинНаМобильномУстройстве,
	|	Пользователи.ПарольНаМобильномУстройстве,
	|	""Users"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ЛогинНаМобильномУстройстве <> &ПустаяСтрока
	|	И Пользователи.ПарольНаМобильномУстройстве <> &ПустаяСтрока
	|	И Пользователи.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Модели.Ссылка,
	|	Модели.Родитель,
	|	Модели.ЭтоГруппа,
	|	Модели.НаименованиеПолное,
	|	""Model"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.Модели КАК Модели
	|ГДЕ
	|	Модели.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Автомобили.Ссылка,
	|	Автомобили.Родитель,
	|	Автомобили.НаименованиеПолное,
	|	Автомобили.VIN,
	|	Автомобили.Модель,
	|	Автомобили.Цвет,
	|	ЕСТЬNULL(АвтомобилиСрезПоследних.Значение, """") КАК ГосНомер,
	|	""Auto"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.Автомобили КАК Автомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследних
	|		ПО (АвтомобилиСрезПоследних.Автомобиль = Автомобили.Ссылка)
	|ГДЕ
	|	Автомобили.ЭтоГруппа = ЛОЖЬ
	|	И Автомобили.ПометкаУдаления = ЛОЖЬ
	|	И Автомобили.Ссылка В
	|			(ВЫБРАТЬ
	|				ВыгружаемыеПоляЗаявки.Автомобиль
	|			ИЗ
	|				ВыгружаемыеПоляЗаявки КАК ВыгружаемыеПоляЗаявки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	Организации.НаименованиеПолное,
	|	""Company"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	Контрагенты.Родитель,
	|	Контрагенты.ЭтоГруппа,
	|	Контрагенты.НаименованиеПолное,
	|	Контрагенты.Имя,
	|	Контрагенты.Фамилия,
	|	Контрагенты.Отчество,
	|	Контрагенты.ФормаСобственности,
	|	""Client"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ПометкаУдаления = ЛОЖЬ
	|	И Контрагенты.Ссылка В
	|			(ВЫБРАТЬ
	|				ВыгружаемыеПоляЗаявки.Контрагент КАК Контрагент
	|			ИЗ
	|				ВыгружаемыеПоляЗаявки КАК ВыгружаемыеПоляЗаявки
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				ВыгружаемыеПоляЗаявки.Заказчик
	|			ИЗ
	|				ВыгружаемыеПоляЗаявки КАК ВыгружаемыеПоляЗаявки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Цвета.Ссылка,
	|	Цвета.Наименование,
	|	""Colors"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.Цвета КАК Цвета
	|ГДЕ
	|	Цвета.ПометкаУдаления = ЛОЖЬ
	|	И Цвета.Ссылка В
	|			(ВЫБРАТЬ
	|				ВЫБОР
	|					КОГДА ВыгружаемыеПоляЗаявки.Автомобиль ССЫЛКА Справочник.Автомобили
	|						ТОГДА ВыгружаемыеПоляЗаявки.Автомобиль.Цвет
	|					ИНАЧЕ NULL
	|				КОНЕЦ КАК Цвет
	|			ИЗ
	|				ВыгружаемыеПоляЗаявки КАК ВыгружаемыеПоляЗаявки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисныеКампании.Ссылка,
	|	СервисныеКампании.Наименование,
	|	""DirectoryServiceCampaign"" КАК ТипВыгрузки
	|ИЗ
	|	Справочник.СервисныеКампании КАК СервисныеКампании
	|ГДЕ
	|	СервисныеКампании.Ссылка В
	|			(ВЫБРАТЬ
	|				ВыгружаемыеПоляЗаявки.СервиснаяКампания
	|			ИЗ
	|				ВыгружаемыеПоляЗаявки КАК ВыгружаемыеПоляЗаявки)
	|	И СервисныеКампании.ПометкаУдаления = ЛОЖЬ
	|	И СервисныеКампании.ЭтоГруппа = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыгружаемыеПоляЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставCheckList.Модель,
	|	СоставCheckList.CheckList,
	|	СоставCheckList.GUID,
	|	СоставCheckList.Порядок,
	|	СоставCheckList.CheckList.Описание,
	|	СоставCheckList.CheckList.ТипЗначения,
	|	СоставCheckList.CheckList.ЗначенияВыбора,
	|	""DirectoryCheckList"" КАК ТипВыгрузки
	|ИЗ
	|	РегистрСведений.СоставCheckList КАК СоставCheckList";
	
	РезультатЗапроса = ЗапросПоСправочникам.ВыполнитьПакет();
	
	Для Каждого Результат Из РезультатЗапроса Цикл
		Если Результат = Неопределено ИЛИ Результат.Колонки.Найти("ТипВыгрузки") = Неопределено ИЛИ Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Образ = Образ + СтрЗаменить("<references name=""%1"" command=""clear"">", "%1", Выборка.ТипВыгрузки);
		
		Выборка.Сбросить();
		Пока Выборка.Следующий() Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, Выборка.ТипВыгрузки);
		КонецЦикла;
		
		// закроем блок
		Образ = Образ + "</references>";
	КонецЦикла;
	
	Возврат Образ;
КонецФункции

// Формирует ответ на запрос обмена данными
//
Функция СформироватьXMLизменений(Изменения, Узел)
	
	//Стр_СтрокиСообщения = Новый Структура;
	УдаленныеАвтомобили = ""; УдаленныеМодели = ""; УдаленныеКонтрагенты = ""; УдаленныеКоманды = "";
	УдаленныеОрганизации = ""; УдаленныеПользователи = ""; УдаленныеСобытия = ""; УдаленныеДопродажи = "";
	УдаленныеСервисныеКомпании = "";
	
	// пройдемся по удаленным
	Для Каждого УдаленныйЭлемент Из Изменения.УдаляемыеОбъекты Цикл
		
		ТипУдаленного = УдаленныйЭлемент.ТипОбъекта; // получим тип удаленного элемента
		
		Если ТипУдаленного = Тип("СправочникСсылка.Автомобили") Тогда
			
			УдаленныеАвтомобили = УдаленныеАвтомобили + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.Модели") Тогда
			
			УдаленныеМодели = УдаленныеМодели + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.Организации") Тогда
			
			УдаленныеОрганизации = УдаленныеОрганизации + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.Контрагенты") Тогда
			
			УдаленныеКонтрагенты = УдаленныеКонтрагенты + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.Пользователи") Тогда
			
			УдаленныеПользователи = УдаленныеПользователи + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
			
			УдаленныеСобытия = УдаленныеСобытия + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("ДокументСсылка.Событие") Тогда
			
			УдаленныеСобытия = УдаленныеСобытия + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.Автоработы") Тогда
			
			УдаленныеДопродажи = УдаленныеДопродажи + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.СервисныеКампании") Тогда
			
			УдаленныеСервисныеКомпании = УдаленныеСервисныеКомпании + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		ИначеЕсли ТипУдаленного = Тип("СправочникСсылка.КомандыМобильногоПриложения") Тогда
			
			УдаленныеКоманды = УдаленныеКоманды + "<item id="""+ УдаленныйЭлемент.Значение.УникальныйИдентификатор() +"""/>";
			
		КонецЕсли;
		
	КонецЦикла;
	
	ИзмененныеАвтомобили = ""; ИзмененныеМодели = ""; ИзмененныеКонтрагенты = ""; ИзмененныеКоманды = "";
	ИзмененныеОрганизации = ""; ИзмененныеПользователи = ""; ИзмененныеСобытия = ""; ИзмененныеДопродажи = "";
	ИзмененныеСервисныеКомпании = "";
	
	ВыгруженныеСобытия = Новый Массив; ВыгруженныеАвтоработы = Новый Массив;
	
	// пройдемся по измененным объектам
	Для Каждого ИзмененныйЭлемент Из Изменения.ИзмененныеОбъекты Цикл
		ТипИзмененного = ИзмененныйЭлемент.ТипОбъекта;
		Если ТипИзмененного = Тип("СправочникСсылка.Пользователи") Тогда
			ИзмененныеПользователи = ИзмененныеПользователи + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "Users");
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.Модели") Тогда
			ИзмененныеМодели = ИзмененныеМодели + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "Model");
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.Автомобили") Тогда
			// откроем блок записи
			ИзмененныеАвтомобили = ИзмененныеАвтомобили + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "Auto");
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.Организации") Тогда
			// откроем блок записи
			ИзмененныеОрганизации = ИзмененныеОрганизации + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "Company");
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.Контрагенты") Тогда
			// откроем блок записи
			ИзмененныеКонтрагенты = ИзмененныеКонтрагенты + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "Client");
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.Автоработы") Тогда
			// откроем блок записи
			Если НЕ ИзмененныйЭлемент.Значение.ЭтоГруппа Тогда
				ИзмененныеДопродажи = ИзмененныеДопродажи + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "DirectoryAdditionalSales");
				ВыгруженныеАвтоработы.Добавить(ИзмененныйЭлемент.Значение);
			Иначе
				ИзмененныеДопродажи = ИзмененныеДопродажи + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "DirectoryAdditionalSalesGroup");
			КонецЕсли;
		ИначеЕсли ТипИзмененного = Тип("ДокументСсылка.ЗаявкаНаРемонт") ИЛИ ТипИзмененного = Тип("ДокументСсылка.Событие") Тогда
			// откроем блок записи
			ИзмененныеСобытия = ИзмененныеСобытия + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "EventItem");
			Если ТипЗнч(ИзмененныйЭлемент.Значение) = Тип("ДокументСсылка.Событие") Тогда
				ВыгруженныеСобытия.Добавить(ИзмененныйЭлемент.Значение);
			КонецЕсли;
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.СервисныеКампании") Тогда
			ИзмененныеСервисныеКомпании = ИзмененныеСервисныеКомпании + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "DirectoryServiceCampaign");
		ИначеЕсли ТипИзмененного = Тип("СправочникСсылка.КомандыМобильногоПриложения") Тогда
			ИзмененныеКоманды = ИзмененныеКоманды + ПолучитьПредставлениеВыгружаемогоОбъекта(ИзмененныйЭлемент.Значение, "CustomActions");
		КонецЕсли;
	КонецЦикла;
	
	// собираем образ из кусочков
	Образ = "";
	Если НЕ ПустаяСтрока(УдаленныеПользователи) ИЛИ НЕ ПустаяСтрока(ИзмененныеПользователи) Тогда
		Образ = Образ + "<references name=""Users""><deleted>" + УдаленныеПользователи + "</deleted>" + ИзмененныеПользователи + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеМодели) ИЛИ НЕ ПустаяСтрока(ИзмененныеМодели) Тогда
		Образ = Образ + "<references name=""Model""><deleted>" + УдаленныеМодели + "</deleted>" + ИзмененныеМодели + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеАвтомобили) ИЛИ НЕ ПустаяСтрока(ИзмененныеАвтомобили) Тогда
		Образ = Образ + "<references name=""Auto""><deleted>" + УдаленныеАвтомобили + "</deleted>" + ИзмененныеАвтомобили + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеОрганизации) ИЛИ НЕ ПустаяСтрока(ИзмененныеОрганизации) Тогда
		Образ = Образ + "<references name=""Company""><deleted>" + УдаленныеОрганизации + "</deleted>" + ИзмененныеОрганизации + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеКонтрагенты) ИЛИ НЕ ПустаяСтрока(ИзмененныеКонтрагенты) Тогда
		Образ = Образ + "<references name=""Client""><deleted>" + УдаленныеКонтрагенты + "</deleted>" + ИзмененныеКонтрагенты + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеДопродажи) ИЛИ НЕ ПустаяСтрока(ИзмененныеДопродажи) Тогда
		Образ = Образ + "<references name=""DirectoryAdditionalSales""><deleted>" + УдаленныеДопродажи + "</deleted>" + ИзмененныеДопродажи + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИзмененныеСервисныеКомпании) ИЛИ НЕ ПустаяСтрока(УдаленныеСервисныеКомпании) Тогда
		Образ = Образ + "<references name=""DirectoryServiceCampaign""><deleted>" + УдаленныеСервисныеКомпании + "</deleted>" + ИзмененныеСервисныеКомпании + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеСобытия) ИЛИ НЕ ПустаяСтрока(ИзмененныеСобытия) Тогда
		Образ = Образ + "<references name=""EventItem""><deleted>" + УдаленныеСобытия + "</deleted>" + ИзмененныеСобытия + "</references>";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(УдаленныеКоманды) ИЛИ НЕ ПустаяСтрока(ИзмененныеКоманды) Тогда
		Образ = Образ + "<references name=""CustomActions""><deleted>" + УдаленныеКоманды + "</deleted>" + ИзмененныеКоманды + "</references>";
	КонецЕсли;
	
	Если ВыгруженныеСобытия.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СобытиеПользователи.Ссылка       КАК Событие,
		|	СобытиеПользователи.Пользователь КАК Пользователь
		|ИЗ
		|	Документ.Событие.Пользователи КАК СобытиеПользователи
		|ГДЕ
		|	СобытиеПользователи.Ссылка В(&СписокСобытий)";
		Запрос.УстановитьПараметр("СписокСобытий", ВыгруженныеСобытия);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		// начало блока событий
		Образ = Образ + "<references name=""Participants"">";
		Пока Выборка.Следующий() Цикл
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Выборка, "Participants");
		КонецЦикла;
		
		// закроем блок
		Образ = Образ + "</references>"; 
	КонецЕсли;
	
	// выгрузим чек листы
	СоставCheckList = Изменения.ИзмененныеОбъекты.Найти(Тип("РегистрСведенийНаборЗаписей.СоставCheckList"), "ТипОбъекта");
	Если СоставCheckList <> Неопределено И СоставCheckList.Значение.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТИзменений.Модель КАК Модель,
		|	ТИзменений.CheckList КАК CheckList,
		|	ТИзменений.guid КАК guid
		|ПОМЕСТИТЬ ТИзменений
		|ИЗ
		|	&ТИзменений КАК ТИзменений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТИзменений.Модель,
		|	ТИзменений.CheckList,
		|	СоставCheckList.Порядок,
		|	ТИзменений.guid,
		|	СоставCheckList.guid                 КАК guidЗаписи,
		|	ТИзменений.CheckList.Описание        КАК CheckListОписание,
		|	ТИзменений.CheckList.ЗначенияВыбора  КАК CheckListЗначенияВыбора,
		|	ТИзменений.CheckList.ТипЗначения     КАК CheckListТипЗначения
		|ИЗ
		|	ТИзменений КАК ТИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставCheckList КАК СоставCheckList
		|		ПО ТИзменений.Модель = СоставCheckList.Модель
		|			И ТИзменений.CheckList = СоставCheckList.CheckList
		|			И ТИзменений.guid = СоставCheckList.guid";
		Запрос.УстановитьПараметр("ТИзменений", СоставCheckList.Значение);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗЗнаяения = РезультатЗапроса.Выгрузить();
		
		Образ = Образ + "<references name=""DirectoryCheckList"">";
		
		УдаленныеСтроки = ТЗЗнаяения.НайтиСтроки(Новый Структура("guidЗаписи,Порядок", null, null));
		
		Если УдаленныеСтроки.Количество() > 0 Тогда
			Образ = Образ + "<deleted>";
			
			Для Каждого Строка Из УдаленныеСтроки Цикл
				Образ = Образ + "<item id="""+ Строка.guid +"""/>";
			КонецЦикла;
			
			Образ = Образ + "</deleted>";
		КонецЕсли;
		
		Для Каждого Строка Из ТЗЗнаяения Цикл
			Если Строка.guidЗаписи = null Тогда Продолжить; КонецЕсли;
			
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Строка, "DirectoryCheckList");
		КонецЦикла;
		
		Образ = Образ + "</references>";
		
		Запрос.МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
	ЗначенияCheckList = Изменения.ИзмененныеОбъекты.Найти(Тип("РегистрСведенийНаборЗаписей.ЗначениеCheckList"), "ТипОбъекта");
	Если ЗначенияCheckList <> Неопределено И ЗначенияCheckList.Значение.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТИзменений.Заявка КАК Заявка,
		|	ТИзменений.CheckList КАК CheckList,
		|	ТИзменений.guid КАК guid
		|ПОМЕСТИТЬ ТИзменений
		|ИЗ
		|	&ТИзменений КАК ТИзменений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТИзменений.Заявка                КАК Заявка,
		|	ТИзменений.CheckList             КАК CheckList,
		|	ЗначениеCheckList.Значение       КАК Значение,
		|	ТИзменений.guid                  КАК guid,
		|	ЗначениеCheckList.guid           КАК guidЗаписи,
		|	ЗначениеCheckList.Checked        КАК Checked,
		|	ЗначениеCheckList.Порядок        КАК Порядок,
		|	ТИзменений.CheckList.Описание    КАК CheckListОписание,
		|	ТИзменений.CheckList.ТипЗначения КАК CheckListТипЗначения,
		|	ТИзменений.CheckList.Родитель.Наименование КАК CheckListGroup
		|ИЗ
		|	ТИзменений КАК ТИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначениеCheckList КАК ЗначениеCheckList
		|		ПО ТИзменений.Заявка = ЗначениеCheckList.Объект
		|			И ТИзменений.CheckList = ЗначениеCheckList.CheckList
		|			И ТИзменений.guid = ЗначениеCheckList.guid";
		Запрос.УстановитьПараметр("ТИзменений", ЗначенияCheckList.Значение);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗЗнаяения = РезультатЗапроса.Выгрузить();
		
		Образ = Образ + "<references name=""CheckList"">";
		
		УдаленныеСтроки = ТЗЗнаяения.НайтиСтроки(Новый Структура("guidЗаписи,Значение", null, null));
		
		Если УдаленныеСтроки.Количество() > 0 Тогда
			Образ = Образ + "<deleted>";
			
			Для Каждого Строка Из УдаленныеСтроки Цикл
				Образ = Образ + "<item id="""+ Строка.guid +"""/>";
			КонецЦикла;
			
			Образ = Образ + "</deleted>";
		КонецЕсли;
		
		Для Каждого Строка Из ТЗЗнаяения Цикл
			Если Строка.guidЗаписи = null Тогда Продолжить; КонецЕсли;
			
			Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Строка, "CheckList");
		КонецЦикла;
		
		Образ = Образ + "</references>";
		
		Запрос.МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
	// выгружаем применяемость допродаж
	Применяемость = Изменения.ИзмененныеОбъекты.Найти(Тип("РегистрСведенийНаборЗаписей.ПрименяемостьДополнительныхПредложенийПоМоделям"), "ТипОбъекта");
	Если ВыгруженныеАвтоработы.Количество() > 0 ИЛИ (Применяемость <> Неопределено И Применяемость.Значение.Количество() > 0) ИЛИ НЕ ПустаяСтрока(УдаленныеДопродажи) Тогда
		// получим удаленные записи
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Применяемость.ДополнительноеПредложение,
		|	Применяемость.Модель,
		|	Применяемость.GUID
		|ПОМЕСТИТЬ ТЗПрименяемость
		|ИЗ
		|	&Применяемость КАК Применяемость
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЗПрименяемость.ДополнительноеПредложение,
		|	ТЗПрименяемость.Модель,
		|	ТЗПрименяемость.GUID,
		|	ПрименяемостьДополнительныхПредложенийПоМоделям.GUID КАК GUIDЗаписи
		|ИЗ
		|	ТЗПрименяемость КАК ТЗПрименяемость
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрименяемостьДополнительныхПредложенийПоМоделям КАК ПрименяемостьДополнительныхПредложенийПоМоделям
		|		ПО ТЗПрименяемость.ДополнительноеПредложение = ПрименяемостьДополнительныхПредложенийПоМоделям.ДополнительноеПредложение
		|			И ТЗПрименяемость.Модель = ПрименяемостьДополнительныхПредложенийПоМоделям.Модель
		|			И ТЗПрименяемость.GUID = ПрименяемостьДополнительныхПредложенийПоМоделям.GUID";
		Запрос.УстановитьПараметр("Применяемость"         , Применяемость.Значение);
		Запрос.УстановитьПараметр("ВыгруженныеАвтоработы" , ВыгруженныеАвтоработы);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Образ = Образ + "<references name=""Applicability"">";
		
		Образ = Образ + "<deleted>";
		
		Образ = Образ + УдаленныеДопродажи;
		
		Пока Выборка.Следующий() Цикл
			Образ = Образ + "<item id="""+ Выборка.ДополнительноеПредложение.УникальныйИдентификатор() +"""/>";
			Если Выборка.guidЗаписи = null Тогда
				Образ = Образ + "<item id="""+ Выборка.guid +"""/>";
			КонецЕсли;
		КонецЦикла;
		
		//Для Каждого ЭлементМассива Из ВыгруженныеАвтоработы Цикл
		//	Выборка.Сбросить();
		//	УникальныйИдентификатор = Строка(ЭлементМассива.УникальныйИдентификатор());
		//	
		//	Если НЕ Выборка.НайтиСледующий(Новый Структура("GUID", УникальныйИдентификатор)) Тогда
		//		Образ = Образ + "<item id="""+ УникальныйИдентификатор +"""/>";
		//	КонецЕсли;
		//КонецЦикла;
		
		// выгрузим новые записи
		// выгружаем применяемость
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Автоработы.Ссылка КАК Авторабота
		|ПОМЕСТИТЬ ТабДопродажи
		|ИЗ
		|	Справочник.Автоработы КАК Автоработы
		|ГДЕ
		|	Автоработы.ЭтоДополнительноеПредложение = ИСТИНА
		|	И Автоработы.ЭтоГруппа = ЛОЖЬ
		|	И (Автоработы.Ссылка В (&ВыгруженныеАвтоработы)
		|		ИЛИ Автоработы.Ссылка В
		|			(ВЫБРАТЬ
		|				ТЗПрименяемость.ДополнительноеПредложение
		|			ИЗ
		|				ТЗПрименяемость КАК ТЗПрименяемость))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабДопродажи.Авторабота,
		|	ЕСТЬNULL(ПрименяемостьДополнительныхПредложенийПоМоделям.Модель, ЗНАЧЕНИЕ(Справочник.Модели.ПустаяСсылка)) КАК Модель,
		|	ПрименяемостьДополнительныхПредложенийПоМоделям.GUID,
		|	0 КАК Цена
		|ИЗ
		|	ТабДопродажи КАК ТабДопродажи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрименяемостьДополнительныхПредложенийПоМоделям КАК ПрименяемостьДополнительныхПредложенийПоМоделям
		|		ПО ТабДопродажи.Авторабота = ПрименяемостьДополнительныхПредложенийПоМоделям.ДополнительноеПредложение";
	
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ТЗ_Результат = РезультатЗапроса.Выгрузить();
			
			Для Каждого Строка Из ТЗ_Результат Цикл
				Если НЕ ЗначениеЗаполнено(Строка.GUID) Тогда
					Строка.GUID = Строка(Строка.Авторабота.УникальныйИдентификатор());
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого ЭлементМассива Из ВыгруженныеАвтоработы Цикл
				Выборка.Сбросить();
				УникальныйИдентификатор = Строка(ЭлементМассива.УникальныйИдентификатор());
				Если НЕ Выборка.НайтиСледующий(Новый Структура("GUID", УникальныйИдентификатор)) И ТЗ_Результат.Найти(УникальныйИдентификатор, "GUID") = Неопределено Тогда
					
					Образ = Образ + "<item id="""+ ЭлементМассива.УникальныйИдентификатор() +"""/>";
					
				КонецЕсли;
			КонецЦикла;
			
			Образ = Образ + "</deleted>";
			
			Для Каждого Строка Из ТЗ_Результат Цикл
				ЦенаРаботы  = орПолучитьЦенуАвтоработы(Узел.ТипЦенРабот, Строка.Авторабота, Строка.Модель,, Узел.Цех, Узел.ВидРемонта);
				Строка.Цена = ?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени), 1, ЦенаРаботы.НормаВремени)*ЦенаРаботы.Цена;
				
				Образ = Образ + ПолучитьПредставлениеВыгружаемогоОбъекта(Строка, "Applicability");
			КонецЦикла;
		Иначе
			
			Образ = Образ + "</deleted>";
			
		КонецЕсли;
		
		Образ = Образ + "</references>";
	КонецЕсли;
	
	Возврат Образ;
	
КонецФункции

// Точечные запросы
//
Функция СформироватьТаблицуПоЗапросу(Команда, Параметр)
	Результат = "";
	
	Если Команда = "backorder" Тогда
		ДокументыОснования = Новый Массив;
		Для Каждого IDзаявки Из Параметр Цикл
			ДокументыОснования.Добавить(Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(IDзаявки)));
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка,
		|	ЗаказНаряд.Номер,
		|	ЗаказНаряд.ДатаНачала КАК ДатаНачала,
		|	ЗаказНаряд.ПлановаяДатаВыдачи КАК ПлановаяДатаВыдачи,
		|	ЗаказНаряд.ДокументОснование КАК ДокументОснование,
		|	ЗаказНаряд.Состояние
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.ДокументОснование В(&ДокументыОснования)
		|	И ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
		|	И ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
		|ИТОГИ
		|	МИНИМУМ(ДатаНачала),
		|	МАКСИМУМ(ПлановаяДатаВыдачи)
		|ПО
		|	ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументыОснования", ДокументыОснования);
		
		ВыборкаОснования = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		// посчитаем смещение часового пояса
		Смещение = СмещениеСтандартногоВремени(ЧасовойПояс());
		РезЧасовойПояс = ?(Смещение > 0, "+", "") + Формат(цел(Смещение/3600)+(Смещение%3600)/6000, "ЧЦ=4; ЧДЦ=2; ЧРД=:; ЧН=-00:00; ЧВН=");
		
		Пока ВыборкаОснования.Следующий() Цикл
			СписокНомеров = ""; СписокСостояний = "";
			Выборка = ВыборкаОснования.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока Выборка.Следующий() Цикл
				СписокНомеров = СписокНомеров + ?(ПустаяСтрока(СписокНомеров), "", ";") + Выборка.Номер;
				СписокСостояний = СписокСостояний + ?(ПустаяСтрока(СписокСостояний), "", ";") + Выборка.Состояние;
			КонецЦикла;
			// открываем строку
			Результат = Результат + "<row>";
			
			Ид = Новый УникальныйИдентификатор();
			Результат = Результат + "<field name = ""ID"">"          + Ид + "</field>";
			Результат = Результат + "<field name = ""OrderNumber"">" + СписокНомеров + "</field>";
			Результат = Результат + "<field name = ""State"">"       + СписокСостояний + "</field>";
			Результат = Результат + "<field name = ""StartDate"">"   + Формат(ВыборкаОснования.ДатаНачала, "ДФ=yyyy-MM-ddTHH:mm:ss.999; ДП=1899-12-30T04:00:00.000") + РезЧасовойПояс  + "</field>";
			Результат = Результат + "<field name = ""EndDate"">"     + Формат(ВыборкаОснования.ПлановаяДатаВыдачи, "ДФ=yyyy-MM-ddTHH:mm:ss.999; ДП=1899-12-30T04:00:00.000") + РезЧасовойПояс  + "</field>";
			Результат = Результат + "<field name = ""BackOrder_EventItem_S_ID"">" + ВыборкаОснования.ДокументОснование.УникальныйИдентификатор() + "</field>";
			
			// закрываем строку
			Результат = Результат + "</row>";
		КонецЦикла;
		
		Возврат Результат;
	ИначеЕсли Команда = "servicecampaign" Тогда
		
		Результат = "";
		
		ТЗ_СервисныхКомпаний = орПроверитьСервиснуюКампанию(Параметр, ТекущаяДата(), Истина);
		
		Для Каждого СТР_СервиснойКомпании Из ТЗ_СервисныхКомпаний Цикл
			// открываем строку
			Результат = Результат + "<row>";
			
			Результат = Результат + "<field name = ""ID"">"   + СТР_СервиснойКомпании.СервиснаяКампания.УникальныйИдентификатор() + "</field>";
			Результат = Результат + "<field name = ""Name"">" + обЗаменитьСпециальныеСимволыXML(спПолучитьНаименование(СТР_СервиснойКомпании.СервиснаяКампания)) + "</field>";
			
			// закрываем строку
			Результат = Результат + "</row>";
		КонецЦикла;
		
		Возврат Результат;

	ИначеЕсли Команда = "autohistory" Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Автомобили.Ссылка КАК Автомобиль
		|ПОМЕСТИТЬ Автомобили
		|ИЗ
		|	Справочник.Автомобили КАК Автомобили
		|ГДЕ
		|	Автомобили.VIN = &VIN
		|	ИЛИ Автомобили.ОригинальныйVIN = &VIN
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка,
		|	ЗаказНаряд.Номер,
		|	ЗаказНаряд.ДатаСоздания,
		|	ЗаказНаряд.ДатаЗакрытия,
		|	ЗаказНаряд.ПричинаОбращения,
		|	ЗаказНаряд.Мастер,
		|	ЗаказНаряд.Пробег,
		|	ЗаказНаряд.Рекомендации,
		|	ЗаказНаряд.СуммаДокумента,
		|	ЗаказНаряд.СуммаРаботДокумента,
		|	ЗаказНаряд.СуммаДокумента - ЗаказНаряд.СуммаРаботДокумента КАК СуммаДеталей
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Автомобиль В
		|			(ВЫБРАТЬ
		|				Автомобили.Автомобиль
		|			ИЗ
		|				Автомобили КАК Автомобили)";
		Запрос.УстановитьПараметр("VIN", Параметр);
		Выборка = Запрос.Выполнить().Выбрать();
		
		// посчитаем смещение часового пояса
		Смещение = СмещениеСтандартногоВремени(ЧасовойПояс());
		РезЧасовойПояс = ?(Смещение > 0, "+", "") + Формат(цел(Смещение/3600)+(Смещение%3600)/6000, "ЧЦ=4; ЧДЦ=2; ЧРД=:; ЧН=-00:00; ЧВН=");
		
		Пока Выборка.Следующий() Цикл
			// открываем строку
			Результат = Результат + "<row>";
			
			Результат = Результат + "<field name = ""ID"">" + Выборка.Ссылка.УникальныйИдентификатор() + "</field>";
			Результат = Результат + "<field name = ""OrderNumber"">" + Выборка.Номер + "</field>";
			Результат = Результат + "<field name = ""CreateDate"">"  + Формат(Выборка.ДатаСоздания, "ДФ=yyyy-MM-ddTHH:mm:ss.999") + РезЧасовойПояс + "</field>";
			Если ЗначениеЗаполнено(Выборка.ДатаЗакрытия) Тогда
				Результат = Результат + "<field name = ""CloseDate"">" + Формат(Выборка.ДатаЗакрытия, "ДФ=yyyy-MM-ddTHH:mm:ss.999") + РезЧасовойПояс + "</field>";
			КонецЕсли;
			Результат = Результат + "<field name = ""MasterName"">"    + обЗаменитьСпециальныеСимволыXML(спПолучитьНаименование(Выборка.Мастер)) + "</field>";
			Результат = Результат + "<field name = ""OrderReason"">"   + обЗаменитьСпециальныеСимволыXML(Выборка.ПричинаОбращения) + "</field>";
			Результат = Результат + "<field name = ""Recomendation"">" + обЗаменитьСпециальныеСимволыXML(Выборка.Рекомендации) + "</field>";
			Результат = Результат + "<field name = ""Mileage"">"       + Выборка.Пробег + "</field>";
			Результат = Результат + "<field name = ""TotalAmount"">"   + XMLСтрока(Выборка.СуммаДокумента) + "</field>";
			Результат = Результат + "<field name = ""WorkAmount"">"    + XMLСтрока(Выборка.СуммаРаботДокумента) + "</field>";
			Результат = Результат + "<field name = ""PartsAmount"">"   + XMLСтрока(Выборка.СуммаДеталей) + "</field>";
			Результат = Результат + "<field name = ""VIN"">"           + Параметр + "</field>";
			
			Результат = Результат + "<field name = ""IsGroup"">Нет</field>";
			Результат = Результат + "<field name = ""ParentID ""></field>";
			
			// закрываем строку
			Результат = Результат + "</row>";
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//получает схему ТС из регистра КартинкиИФайлы по модели автомобиля
//Модель - макет печ. формы
//ИдентификаторОбъекта - имя картинки в регистре, где содержится схема
//Возвращает найденную картинку или неопределено
//
Функция ПолучитьДанныеСхемыТС(Модель, ИдентификаторОбъекта) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КартинкиИФайлы.ИмяФайла,
	|	КартинкиИФайлы.Данные,
	|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
	|	0 КАК ПорядокСортировки
	|ИЗ
	|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	|ГДЕ
	|	КартинкиИФайлы.Объект = &Модель
	|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта
	|
	| //ОбъединениеГруппы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокСортировки");
	Запрос.УстановитьПараметр("Модель",Модель.Ссылка);
	Запрос.УстановитьПараметр("ИдентификаторОбъекта",ИдентификаторОбъекта);
	ДанныеРегистра = Запрос.Выполнить().Выбрать();
	
	Если ДанныеРегистра.Количество() > 0 Тогда
		ДанныеРегистра.Следующий();
		Попытка
			Если ДанныеРегистра.ДанныеВоВнешнемФайле Тогда
				СхемаТС = Новый ДвоичныеДанные(ДанныеРегистра.ИмяФайла);
			Иначе
				СхемаТС = ДанныеРегистра.Данные.Получить().ПолучитьДвоичныеДанные();
			КонецЕсли;
		Исключение
		КонецПопытки;
		Если СхемаТС <> Неопределено И (ТипЗнч(СхемаТС)=Тип("ДвоичныеДанные")) Тогда
			Возврат СхемаТС;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // орПолучитьСхемуТС()

// получает логотип компании из регистра КартинкиИФайлы
//
Функция ПолучитьЛоготипКомпании(Объект, ИдентификаторОбъекта = "Логотип") Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КартинкиИФайлы.ИмяФайла,
	|	КартинкиИФайлы.Данные,
	|	КартинкиИФайлы.ДанныеВоВнешнемФайле
	|ИЗ
	|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	|ГДЕ
	|	КартинкиИФайлы.Объект = &Модель
	|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта");
	Запрос.УстановитьПараметр("Модель", Объект);
	Запрос.УстановитьПараметр("ИдентификаторОбъекта", ИдентификаторОбъекта);
	
	ДанныеРегистра = Запрос.Выполнить().Выбрать();
	
	Если ДанныеРегистра.Количество() > 0 Тогда
		ДанныеРегистра.Следующий();
		Попытка
			Если ДанныеРегистра.ДанныеВоВнешнемФайле Тогда
				Логотип = Новый ДвоичныеДанные(ДанныеРегистра.ИмяФайла);
			Иначе
				Логотип = ДанныеРегистра.Данные.Получить().ПолучитьДвоичныеДанные();
			КонецЕсли;
		Исключение
		КонецПопытки;
		Если Логотип <> Неопределено И (ТипЗнч(Логотип)=Тип("ДвоичныеДанные")) Тогда
			Возврат Логотип;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Выгружаем описание дополнительного предложения на мобильое устройство
// 
Функция ВыгрузитьФайлыДополнительногоПредложения(ДополнительныеПредложения, Responce, id, session_id)
	Отбор = Новый Массив;
	Для Каждого ДополнительноеПредложение Из ДополнительныеПредложения Цикл
		Отбор.Добавить(Справочники.Автоработы.ПолучитьСсылку(Новый УникальныйИдентификатор(ДополнительноеПредложение)));
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
	|	КартинкиИФайлы.Данные,
	|	КартинкиИФайлы.РазмерФайла,
	|	КартинкиИФайлы.ИмяФайла
	|ИЗ
	|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	|ГДЕ
	|	КартинкиИФайлы.Объект В(&Отбор)
	|	И (КартинкиИФайлы.Идентификатор = ""ОсновноеИзображение"" ИЛИ КартинкиИФайлы.Идентификатор = ""ПолноеОписание"")";
	Запрос.УстановитьПараметр("Отбор", Отбор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Responce = ОтветПоШаблону("0", "OK", id, session_id);
		Возврат Ложь;
	Иначе
		Responce = "<?xml version=""1.0"" encoding=""UTF-8""?><response cmd=""get_attach"" id=""" + id +
		"""><resultinfo><code>0</code><description>ОК</description></resultinfo><references name=""attach"">";
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Responce = Responce + "<row>";
			Если Выборка.ДанныеВоВнешнемФайле Тогда
				Данные = Новый ДвоичныеДанные(Выборка.ИмяФайла);
			Иначе
				Данные = Выборка.Данные.Получить();
				Если ТипЗнч(Данные) <> Тип("ДвоичныеДанные") Тогда
					Данные = Данные.ПолучитьДвоичныеДанные();
				КонецЕсли;
			КонецЕсли;
			
			Файл = Новый Файл(Выборка.ИмяФайла);
			
			Responce = Responce + "<extension>" + Файл.Расширение      + "</extension>";
			Responce = Responce + "<file>"      + Base64Строка(Данные) + "</file></row>";
		КонецЦикла;
		
		Responce = Responce + "</references></response>";
		Возврат Истина;
	КонецЕсли;
КонецФункции

// Получить представление выгружаемого формата строкой XML
//
// Параметры:
//	ЭлементДанных - элемент базы данных
//	Тип - Строка - Тип выгружаемого объекта
//
// Возвращаемое значение строка серилизованного объекта
//
Функция ПолучитьПредставлениеВыгружаемогоОбъекта(ЭлементДанных, Тип)
	
	Если НЕ ЗначениеЗаполнено(Тип) Тогда
		Возврат "";
	КонецЕсли;
	
	// откроем блок записи
	ТекстПредставления = "<row>";
	
	Если Тип = "Users" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"       + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Login"">"    + ЭлементДанных.ЛогинНаМобильномУстройстве + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Password"">" + ЭлементДанных.ПарольНаМобильномУстройстве + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""FIO"">"      + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Наименование) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Employee"">" + ЭлементДанных.Сотрудник.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Workshop"">" + ?(ЭлементДанных.Сотрудник.Пустая(), "", ЭлементДанных.Сотрудник.Цех.УникальныйИдентификатор()) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""photo""></field>"; // FOTO
	ИначеЕсли Тип = "Colors" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"       + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ColorName"">"+ обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Наименование) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";
	ИначеЕсли Тип = "Model" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"        + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">"   + ЭлементДанных.ЭтоГруппа+"</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ParentID"">"  + ЭлементДанных.Родитель.УникальныйИдентификатор() +"</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ModelName"">" + обЗаменитьСпециальныеСимволыXML(?(ЭлементДанных.ЭтоГруппа, ЭлементДанных.Ссылка.Наименование, ЭлементДанных.НаименованиеПолное)) + "</field>";
		
		СхемаТС = ПолучитьДанныеСхемыТС(ЭлементДанных.Ссылка,"СхемаТС");
		Если СхемаТС = Неопределено Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""Plan""></field>"; // FOTO
		Иначе
			ТекстКартинки      = Base64Строка(СхемаТС);
			ТекстПредставления = ТекстПредставления + "<field name=""Plan"">"+ ТекстКартинки +"</field>"; // FOTO
		КонецЕсли;
	ИначеЕсли Тип = "Auto" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ParentID"">"  + ЭлементДанных.Родитель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ModelName"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.НаименованиеПолное) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""VIN"">"       + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.VIN) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Color"">"     + ЭлементДанных.Цвет.УникальныйИдентификатор() + "</field>";
		
		ТекстПредставления = ТекстПредставления + "<field name=""Model"">" + ?(ЗначениеЗаполнено(ЭлементДанных.Модель), ЭлементДанных.Модель.УникальныйИдентификатор(), "") + "</field>";
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""Number"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.ГосНомер) +"</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""Number"">" + обЗаменитьСпециальныеСимволыXML(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ЭлементДанных, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер))+"</field>";
		КонецПопытки;
	ИначеЕсли Тип = "Company" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"   + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Name"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.НаименованиеПолное) + "</field>";
		
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";
		
		Логотип = ПолучитьЛоготипКомпании(ЭлементДанных.Ссылка);
		
		Если Логотип = Неопределено Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""Logo""></field>"; // FOTO
		Иначе
			ТекстПредставления = ТекстПредставления + "<field name=""Logo"">"+ Base64Строка(Логотип) +"</field>"; // FOTO
		КонецЕсли;
		
		ТекстПредставления = ТекстПредставления + "<field name=""Phone"">"  + обЗаменитьСпециальныеСимволыXML(киПолучитьПредставлениеКИ(ЭлементДанных.Ссылка, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий)) +"</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Adress"">" + обЗаменитьСпециальныеСимволыXML(киПолучитьПредставлениеКИ(ЭлементДанных.Ссылка, Справочники.ВидыКонтактнойИнформации.АдресФактический)) +"</field>";
	ИначеЕсли Тип = "Client" И ТипЗнч(ЭлементДанных) = Тип("Строка") Тогда
		IDClient = Новый УникальныйИдентификатор;
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"        + IDClient + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">"   + ?(ЭлементДанных.ЭтоГруппа, "Да", "Нет")          + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ParentID"">"  + ЭлементДанных.Родитель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""FullName"">"  + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.НаименованиеПолное) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ObjectType"">3</field>";
	ИначеЕсли Тип = "Client" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"        + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">"   + ?(ЭлементДанных.ЭтоГруппа, "Да", "Нет") + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ParentID"">"  + ЭлементДанных.Родитель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""FullName"">"  + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.НаименованиеПолное) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Adress"">"    + обЗаменитьСпециальныеСимволыXML(киПолучитьПредставлениеКИ(ЭлементДанных.Ссылка, Справочники.ВидыКонтактнойИнформации.АдресФактический)) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Email"">"     + обЗаменитьСпециальныеСимволыXML(киПолучитьПредставлениеКИ(ЭлементДанных.Ссылка, Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий)) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Name"">"      + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Имя) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Surname"">"   + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Фамилия) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""SecondName"">"+ обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Отчество) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsCompany"">" + ?(ЭлементДанных.ФормаСобственности <> Перечисления.ФормыСобственности.ЧастноеЛицо, "Да", "Нет") + "</field>";
		
		// выгрузим телефон
		Телефон = ПолучитьОсновнойТелефонКонтрагента(ЭлементДанных.Ссылка);
		
		Если ЗначениеЗаполнено(Телефон.CountryCode) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""CountryCode"">" + Телефон.CountryCode + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Телефон.CityCode) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""CityCode"">" + Телефон.CityCode + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Телефон.PhoneNumber) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""PhoneNumber"">" + Телефон.PhoneNumber + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Телефон.PhoneRepresentation) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""PhoneRepresentation"">" + Телефон.PhoneRepresentation + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Телефон.ExtensionTelephone) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""ExtensionTelephone"">" + Телефон.ExtensionTelephone + "</field>";
		КонецЕсли;
	ИначеЕсли Тип = "DirectoryCheckList" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ЭлементДанных.guid + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""CheckListID"">" + ЭлементДанных.CheckList.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""DirectoryCheckList_Model_S_ID"">" + ЭлементДанных.Модель.УникальныйИдентификатор() + "</field>";
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""Text"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckListОписание) + "</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""Text"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckList.Описание) + "</field>";
		КонецПопытки;
		
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""Type"">" + ЭлементДанных.CheckListТипЗначения + "</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""Type"">" + ЭлементДанных.CheckList.ТипЗначения + "</field>";
		КонецПопытки;
		
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""ValuesOfChoice"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckListЗначенияВыбора) + "</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""ValuesOfChoice"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckList.ЗначенияВыбора) + "</field>";
		КонецПопытки;

		ТекстПредставления = ТекстПредставления + "<field name=""Position"">" + ЭлементДанных.Порядок + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name =""S_ParentID"">" + ЭлементДанных.CheckList.Родитель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">"+ ?(ЭлементДанных.CheckList.ЭтоГруппа,"Да", "Нет") +"</field>";
	ИначеЕсли Тип = "CheckList" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ЭлементДанных.guid + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Checked"">" + ЭлементДанных.Checked + "</field>";
		
		Если ЭлементДанных.Значение <> null И ЭлементДанных.Значение <> Неопределено Тогда
			Если ТипЗнч(ЭлементДанных.Значение) = Тип("ПеречислениеСсылка.ВидыЗначенийСветофора") Тогда
				ТекстПредставления = ТекстПредставления + "<field name=""Value"">" +
					XMLСтрока(Перечисления.ВидыЗначенийСветофора.ПреобразоватьКЧислу(ЭлементДанных.Значение)) + "</field>";
			ИначеЕсли ТипЗнч(ЭлементДанных.Значение) <> Тип("Булево") Тогда
				ТекстПредставления = ТекстПредставления + "<field name=""Value"">" + XMLСтрока(ЭлементДанных.Значение) + "</field>";
			Иначе
				ТекстПредставления = ТекстПредставления + "<field name=""Value"">" + ?(ЭлементДанных.Значение, "Да", "Нет") + "</field>";
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""Text"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckListОписание) + "</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""Text"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckList.Описание) + "</field>";
		КонецПопытки;
		
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""Type"">" + ЭлементДанных.CheckListТипЗначения + "</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""Type"">" + ЭлементДанных.CheckList.ТипЗначения + "</field>";
		КонецПопытки;
		
		Попытка
			ТекстПредставления = ТекстПредставления + "<field name=""ValuesOfChoice"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckListЗначенияВыбора) + "</field>";
		Исключение
			ТекстПредставления = ТекстПредставления + "<field name=""ValuesOfChoice"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.CheckList.ЗначенияВыбора) + "</field>";
		КонецПопытки;
		
		ТекстПредставления = ТекстПредставления + "<field name=""CheckList_EventItem_S_ID"">" + ЭлементДанных.Заявка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""DirectoryID"">" + ЭлементДанных.CheckList.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""position"">" + ЭлементДанных.Порядок + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""CheckListGroup"">" + ЭлементДанных.CheckListGroup + "</field>";
	ИначеЕсли Тип = "DirectoryAdditionalSales" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""S_ParentID"">" + ЭлементДанных.Родитель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""name"">" + обЗаменитьСпециальныеСимволыXML(?(ПустаяСтрока(ЭлементДанных.НаименованиеПолное), ЭлементДанных.Наименование, ЭлементДанных.НаименованиеПолное)) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""desc"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Комментарий) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";
	ИначеЕсли Тип = "DirectoryAdditionalSalesGroup" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""ParentID"">" + ЭлементДанных.Родитель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""name"">" + обЗаменитьСпециальныеСимволыXML(?(ПустаяСтрока(ЭлементДанных.НаименованиеПолное), ЭлементДанных.Наименование, ЭлементДанных.НаименованиеПолное)) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""desc""></field>";
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Да</field>";
	ИначеЕсли Тип = "Applicability" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ?(ПустаяСтрока(ЭлементДанных.guid), ЭлементДанных.Авторабота.УникальныйИдентификатор(), ЭлементДанных.guid) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""Model"">" + ЭлементДанных.Модель.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""DirectoryAdditionalSales"">" + ЭлементДанных.Авторабота.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""price"">" + XMLСтрока(ЭлементДанных.Цена) + "</field>";
	ИначеЕсли Тип = "DirectoryServiceCampaign" Тогда
		ТекстПредставления = ТекстПредставления + "<field name = ""ID"">"   + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name = ""Name"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Наименование) + "</field>";
	ИначеЕсли Тип = "Participants" Тогда
		ТекстПредставления = ТекстПредставления + "<field name = ""ID"">"   + Новый УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name = ""User"">" + ЭлементДанных.Пользователь.УникальныйИдентификатор() + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name = ""Participants_EventItem_S_ID"">" + ЭлементДанных.Событие.УникальныйИдентификатор() + "</field>";
		
		Сотрудник = ЭлементДанных.Пользователь.Сотрудник;
		ТекстПредставления = ТекстПредставления + "<field name = ""Workshop"">" + ?(Сотрудник.Пустая() ИЛИ Сотрудник.Цех.Пустая(), "", Сотрудник.Цех.УникальныйИдентификатор()) + "</field>";
		ТекстПредставления = ТекстПредставления + "<field name = ""Name"">" + обЗаменитьСпециальныеСимволыXML(?(Сотрудник.Пустая(), ЭлементДанных.Пользователь.наименование, Сотрудник.наименование)) + "</field>";
	ИначеЕсли Тип = "EventItem" Тогда
		// посчитаем смещение часового пояса
		Смещение = СмещениеСтандартногоВремени(ЧасовойПояс());
		РезЧасовойПояс = ?(Смещение > 0, "+", "") + Формат(цел(Смещение/3600)+(Смещение%3600)/6000, "ЧЦ=4; ЧДЦ=2; ЧРД=:; ЧН=-00:00; ЧВН=");
		
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">" + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";
		
		Если ЗначениеЗаполнено(ЭлементДанных.ДатаНачала) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""StartDate"">" + Формат(ЭлементДанных.ДатаНачала, "ДФ=yyyy-MM-ddTHH:mm:ss.999") + РезЧасовойПояс + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.ДатаОкончания) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""EndDate"">"   + Формат(ЭлементДанных.ДатаОкончания, "ДФ=yyyy-MM-ddTHH:mm:ss.999") + РезЧасовойПояс + "</field>";
		КонецЕсли;
		
		Если ТипЗнч(ЭлементДанных) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""Text"">"         + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.ПричинаОбращения) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""CompaignID"">"   + ЭлементДанных.СервиснаяКампания.УникальныйИдентификатор() +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""DocumentSumm"">" + ЭлементДанных.СуммаДокумента +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""FeedbackMessage"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Комментарий) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""DispatcherName"">"  + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Диспетчер.Наименование) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Workshop"">" +
								 ?(ЭлементДанных.Диспетчер.Пустая() ИЛИ ЭлементДанных.Диспетчер.Цех.Пустая(),
								 "", ЭлементДанных.Диспетчер.Цех.УникальныйИдентификатор()) +"</field>";
			
			//получим пользователя связанного с диспетчером
			Если ЗначениеЗаполнено(ЭлементДанных.Диспетчер) Тогда
				НайденныйПользователь = Справочники.Пользователи.НайтиПоРеквизиту("Сотрудник", ЭлементДанных.Диспетчер); 
				Если  НЕ НайденныйПользователь.Пустая() Тогда
					ТекстПредставления = ТекстПредставления + "<field name=""Dispatcher"">" + НайденныйПользователь.УникальныйИдентификатор() +"</field>";
				КонецЕсли;
			КонецЕсли;
			
			// выгрузка InteractiveStatus И InteractiveID
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 2
			|	ЗаказНаряд.Состояние КАК InteractiveStatus,
			|	ЗаказНаряд.Номер
			|ИЗ
			|	Документ.ЗаказНаряд КАК ЗаказНаряд
			|ГДЕ
			|	ЗаказНаряд.ДокументОснование = &Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЗаказНаряд.Состояние.Порядок";
			Запрос.УстановитьПараметр("Ссылка", ЭлементДанных.Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			InteractiveID = "ЗР" + ?(РезультатЗапроса.Пустой(), "", "\ЗН") + " №"+ЭлементДанных.Номер;
			Выборка = РезультатЗапроса.Выбрать();
			Если Выборка.Следующий() Тогда
				InteractiveID = InteractiveID + "\№"+Выборка.Номер + ?(Выборка.Количество() > 1, "...","");
				ТекстПредставления = ТекстПредставления + "<field name=""InteractiveStatus"">"+ Выборка.InteractiveStatus+"</field>";
			КонецЕсли;
			
			ТекстПредставления = ТекстПредставления + "<field name=""InteractiveID"">" + InteractiveID +"</field>";
			
			// выгрузим ТЧ документа
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗаявкаНаРемонтТовары.Номенклатура.Артикул КАК Артикул,
			|	ЗаявкаНаРемонтТовары.Номенклатура.НаименованиеПолное КАК Наименование,
			|	ЗаявкаНаРемонтТовары.Количество,
			|	ЗаявкаНаРемонтТовары.СуммаВсего,
			|	0 КАК Тип,
			|	ЗаявкаНаРемонтТовары.Ссылка
			|ИЗ
			|	Документ.ЗаявкаНаРемонт.Товары КАК ЗаявкаНаРемонтТовары
			|ГДЕ
			|	ЗаявкаНаРемонтТовары.Ссылка В(&Ссылка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗаявкаНаРемонтРаботы.Работа.Артикул,
			|	ЗаявкаНаРемонтРаботы.Работа.НаименованиеПолное,
			|	ЗаявкаНаРемонтРаботы.Коэффициент,
			|	ЗаявкаНаРемонтРаботы.СуммаВсего,
			|	1,
			|	ЗаявкаНаРемонтРаботы.Ссылка
			|ИЗ
			|	Документ.ЗаявкаНаРемонт.Работы КАК ЗаявкаНаРемонтРаботы
			|ГДЕ
			|	ЗаявкаНаРемонтРаботы.Ссылка = &Ссылка
			|ИТОГИ ПО
			|	Тип";
			Запрос.УстановитьПараметр("Ссылка", ЭлементДанных.Ссылка);
			ВыборкаТипы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			ТабличныеЧасти = "";
			Пока ВыборкаТипы.Следующий() Цикл
				ТабличныеЧасти = ТабличныеЧасти + ?(ПустаяСтрока(ТабличныеЧасти), "", Символы.ПС) + ?(ВыборкаТипы.Тип = 0, "Товары:", "Работы:");
				Выборка = ВыборкаТипы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					ТабличныеЧасти = ТабличныеЧасти + Символы.ПС + "    ";
					ТабличныеЧасти = ТабличныеЧасти + обЗаменитьСпециальныеСимволыXML(Выборка.Артикул);
					ТабличныеЧасти = ТабличныеЧасти +?(ЗначениеЗаполнено(Выборка.Артикул), "; ", "") + обЗаменитьСпециальныеСимволыXML(Выборка.Наименование);
					ТабличныеЧасти = ТабличныеЧасти +?(ЗначениеЗаполнено(Выборка.Наименование), "; ", "") + Выборка.Количество;
					ТабличныеЧасти = ТабличныеЧасти +?(ЗначениеЗаполнено(Выборка.Количество), "; ", "") + Выборка.СуммаВсего;
				КонецЦикла;
			КонецЦикла;
			
			ТекстПредставления = ТекстПредставления + "<field name=""DeclaredWorkText"">" + ТабличныеЧасти +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Email"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.АдресЭлектронойПочты) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""VIN"">"   + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.VIN) +"</field>";
		Иначе
			ТекстПредставления = ТекстПредставления + "<field name=""Text"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Содержание) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""FeedbackMessage"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Результат) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""MessageType"">" + ЭлементДанных.ТипСообщения +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""EventTopic"">" + обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Тема) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Email"">" + ЭлементДанных.Mob_EMail +"</field>";
		КонецЕсли;
		
		ТекстПредставления = ТекстПредставления + "<field name=""Author"">" + ЭлементДанных.Автор.УникальныйИдентификатор() +"</field>";
		ТекстПредставления = ТекстПредставления + "<field name=""DocumentID"">"   + ЭлементДанных.Номер +"</field>";
		
		РеквизитКонтрагент = ?(ТипЗнч(ЭлементДанных) = Тип("ДокументСсылка.ЗаявкаНаРемонт"), "Заказчик", "Контрагент");
		Если ЗначениеЗаполнено(ЭлементДанных[РеквизитКонтрагент]) И ТипЗнч(ЭлементДанных[РеквизитКонтрагент]) = Тип("СправочникСсылка.Контрагенты") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""Client"">"+ ЭлементДанных[РеквизитКонтрагент].УникальныйИдентификатор() +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""ClientName""></field>";
		ИначеЕсли ЗначениеЗаполнено(ЭлементДанных[РеквизитКонтрагент]) И ТипЗнч(ЭлементДанных[РеквизитКонтрагент]) = Тип("Строка") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""ClientName"">"+ ЭлементДанных[РеквизитКонтрагент] +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Client""></field>";
		КонецЕсли;
		
		Если ТипЗнч(ЭлементДанных) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""AutoNumber"">"+ обЗаменитьСпециальныеСимволыXML(ЭлементДанных.ГосНомер)+"</field>";
			
			// выгрузим ТЧ
			ТекстПредставления = ТекстПредставления + "<field name=""DeclaredWorkSumm"">" + Формат(ЭлементДанных.Работы.Итог("СуммаВсего"), "ЧН=0") + "</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""DeclaredPartsSumm"">" + Формат(ЭлементДанных.Товары.Итог("СуммаВсего"), "ЧН=0") + "</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Mileage"">" + XMLСтрока(ЭлементДанных.Пробег) + "</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""IsLocked"">" + ВозможноРедактироватьЗаявкуНаРемонт(ЭлементДанных) + "</field>";
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.КодРегиона) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""CountryCode"">" + ЭлементДанных.КодРегиона + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.КодГорода) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""CityCode"">" + ЭлементДанных.КодГорода + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.НомерТелефона) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""PhoneNumber"">" + ЭлементДанных.НомерТелефона + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.ПредставлениеТелефона) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""PhoneRepresentation"">" + ЭлементДанных.ПредставлениеТелефона + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.ВнутреннийНомер) Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""ExtensionTelephone"">" + ЭлементДанных.ВнутреннийНомер + "</field>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементДанных.Автомобиль) И ТипЗнч(ЭлементДанных.Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""ReceptionAuto"">"+ ЭлементДанных.Автомобиль.УникальныйИдентификатор() +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Model""></field>";
			ТекстПредставления = ТекстПредставления + "<field name=""AutoName""></field>";
		ИначеЕсли ЗначениеЗаполнено(ЭлементДанных.Автомобиль) И ТипЗнч(ЭлементДанных.Автомобиль) = Тип("СправочникСсылка.Модели") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""ReceptionAuto""></field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Model"">"+ ЭлементДанных.Автомобиль.УникальныйИдентификатор() +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""AutoName""></field>";
		ИначеЕсли ЗначениеЗаполнено(ЭлементДанных.Автомобиль) И ТипЗнч(ЭлементДанных.Автомобиль) = Тип("Строка") Тогда
			ТекстПредставления = ТекстПредставления + "<field name=""AutoName"">"+ обЗаменитьСпециальныеСимволыXML(ЭлементДанных.Автомобиль) +"</field>";
			ТекстПредставления = ТекстПредставления + "<field name=""ReceptionAuto""></field>";
			ТекстПредставления = ТекстПредставления + "<field name=""Model""></field>";
		КонецЕсли;
		
		ТекстПредставления = ТекстПредставления + "<field name=""EType"">" + ПолучитьТипСобытия(ЭлементДанных) +"</field>";
		
		СтатусДокумента = ПолучитьСтатусСобытия(ЭлементДанных);
		ТекстПредставления = ТекстПредставления + "<field name=""Status"">" + СтатусДокумента.Состояние +"</field>";
		
		Если СтатусДокумента.Состояние = 4 ИЛИ СтатусДокумента.Состояние = 5 Тогда
			
			ТекстПредставления = ТекстПредставления + "<field name=""ExecutionDate"">" + ?(СтатусДокумента.Состояние = 4, Формат(СтатусДокумента.ДатаОкончания, "ДФ=yyyy-MM-ddTHH:mm:ss.999; ДП=1899-12-30T04:00:00.000"), Формат(СтатусДокумента.ДатаЗакрытия, "ДФ=yyyy-MM-ddTHH:mm:ss.999; ДП=1899-12-30T04:00:00.000")) + РезЧасовойПояс + "</field>";
			
		КонецЕсли;
	ИначеЕсли Тип = "CustomActions" Тогда
		ТекстПредставления = ТекстПредставления + "<field name=""ID"">"          + ЭлементДанных.Ссылка.УникальныйИдентификатор() + "</field>";   // ID
		ТекстПредставления = ТекстПредставления + "<field name=""Name"">"        + ЭлементДанных.ПредставлениеНаМобильномУстройстве + "</field>"; // Наименование на мобильном устройстве
		ТекстПредставления = ТекстПредставления + "<field name=""SyncType"">"    + ЭлементДанных.Синхронизация + "</field>";                      // Тип синхронизации
		ТекстПредставления = ТекстПредставления + "<field name=""Position"">"    + ЭлементДанных.Порядок + "</field>";                            // Порядок
		ТекстПредставления = ТекстПредставления + "<field name=""Enabled"">"     + ЭлементДанных.Активность + "</field>";                         // Активность
		ТекстПредставления = ТекстПредставления + "<field name=""IsGroup"">Нет</field>";                                                          // Признак группы
	КонецЕсли;
	
	// закроем блок
	ТекстПредставления = ТекстПредставления + "</row>";
	
	Возврат ТекстПредставления;
	
КонецФункции

// Получение изначального ID документа из документа основания
//
Функция ПолучитьИзначальныйID(Документ, ДокументОснование)
	ТипДокумента          = ТипЗнч(Документ);
	ТипДокументаОснования = ТипЗнч(ДокументОснование);
	
	Если ТипДокумента = Тип("ДокументСсылка.ЗаказНаряд") И ТипДокументаОснования = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		
		Возврат ДокументОснование.УникальныйИдентификатор();
		
	Иначе
		
		Возврат Документ.УникальныйИдентификатор();
		
	КонецЕсли;
	
КонецФункции

// Преобразования вида события для выгрузки на МП
//
Функция ПолучитьТипСобытия(Документ)
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.Событие") Тогда
		
		Если Документ.ВидСобытия = Перечисления.ВидыСобытий.ТелефонныйЗвонок Тогда
			Возврат "0";
		ИначеЕсли Документ.ВидСобытия = Перечисления.ВидыСобытий.SMS Тогда
			Возврат "2";
		ИначеЕсли Документ.ВидСобытия = Перечисления.ВидыСобытий.ЭлектронноеПисьмо Тогда
			Возврат "3";
		КонецЕсли;
		
	Иначе
		
		Возврат "1";
		
	КонецЕсли;
	
КонецФункции

// Получить статус собылия для выгрузки на МП
//
Функция ПолучитьСтатусСобытия(Документ)
	
	ТипДокумента = ТипЗнч(Документ);
	
	Если ТипДокумента = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ВЫБОР
		|			КОГДА ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
		|				ТОГДА 4
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
		|						ТОГДА 5
		|					ИНАЧЕ 3
		|				КОНЕЦ
		|		КОНЕЦ) КАК Состояние,
		|	МАКСИМУМ(ЗаказНаряд.ДатаОкончания) КАК ДатаОкончания,
		|	МАКСИМУМ(ЗаказНаряд.ДатаЗакрытия) КАК ДатаЗакрытия
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.ДокументОснование = &ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументОснование", Документ);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.Состояние <> null Тогда
			Возврат Новый Структура("Состояние,ДатаОкончания,ДатаЗакрытия", Выборка.Состояние, Выборка.ДатаОкончания, Выборка.ДатаЗакрытия);
		Иначе
			Возврат Новый Структура("Состояние", 1);
		КонецЕсли;
	Иначе
		Если Документ.Состояние = Перечисления.СостоянияСобытий.Запланировано ИЛИ Документ.Состояние = Перечисления.СостоянияСобытий.Утверждено Тогда
			Возврат Новый Структура("Состояние", 1);
		ИначеЕсли Документ.Состояние = Перечисления.СостоянияСобытий.Выполняется Тогда
			Возврат Новый Структура("Состояние", 3);
		Иначе
			Возврат Новый Структура("Состояние,ДатаОкончания", 4, Документ.ДатаОкончания);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Получение телефона контрагента для отпраки на МП
//
Функция ПолучитьОсновнойТелефонКонтрагента(Контрагент)
	
	Телефон = Новый Структура("CountryCode,CityCode,PhoneRepresentation,ExtensionTelephone,PhoneNumber");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Поле1 КАК CountryCode,
	|	КонтактнаяИнформация.Поле2 КАК CityCode,
	|	КонтактнаяИнформация.Поле3 КАК PhoneNumber ,
	|	КонтактнаяИнформация.Поле4 КАК ExtensionTelephone,
	|	КонтактнаяИнформация.Представление КАК PhoneRepresentation
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.ЗначениеПоУмолчанию = &ЗначениеПоУмолчанию
	|	И КонтактнаяИнформация.Тип = &ТипКИ
	|	И КонтактнаяИнформация.Вид = &ВидКИ
	|	И КонтактнаяИнформация.Объект = &Контрагент";
	
	Запрос.УстановитьПараметр("ЗначениеПоУмолчанию", Истина);
	Запрос.УстановитьПараметр("ТипКИ"      , Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("ВидКИ"      , Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("Контрагент" , Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Телефон, Выборка);
	КонецЕсли;
	
	Возврат Телефон;
	
КонецФункции

// Преведение типа повреждений из типов МП к типу
Функция ПолучитьТипПовреждения(Тип) Экспорт
	Если Тип = "0" Тогда
		Возврат Перечисления.ТипыПовреждений.Царапина;
	ИначеЕсли Тип = "1" Тогда
		Возврат Перечисления.ТипыПовреждений.Вмятина;
	ИначеЕсли Тип = "2" Тогда
		Возврат Перечисления.ТипыПовреждений.Ржавчина;
	ИначеЕсли Тип = "3" Тогда
		Возврат Перечисления.ТипыПовреждений.Трещина;
	ИначеЕсли Тип = "4" Тогда
		Возврат Перечисления.ТипыПовреждений.Пробой;
	ИначеЕсли Тип = "5" Тогда
		Возврат Перечисления.ТипыПовреждений.Скол;
	ИначеЕсли Тип = "6" Тогда
		Возврат Перечисления.ТипыПовреждений.Прочее;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция ВозможноРедактироватьЗаявкуНаРемонт(Основание)
	
	МассивВидов = Новый Массив(1); 
	МассивВидов[0] = "ЗаказНаряд";
	МассивПодчиненных = дкПолучитьМассивПодчиненных(Основание, МассивВидов, Истина);
	
	Если МассивПодчиненных.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВсеЗакрыты = Истина;
	ЗакрытыеСостояния = Новый Массив;
	ЗакрытыеСостояния.Добавить(Справочники.ВидыСостоянийЗаказНарядов.Выполнен);
	ЗакрытыеСостояния.Добавить(Справочники.ВидыСостоянийЗаказНарядов.Закрыт);
	
	Для Каждого ПодчиненныйДокумент Из МассивПодчиненных Цикл
		Если ЗакрытыеСостояния.Найти(ПодчиненныйДокумент.Состояние) = Неопределено Тогда
			ВсеЗакрыты = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВсеЗакрыты;
	
КонецФункции

#КонецОбласти

#Область РаботаСБазой

// Обновление информационной базы данными с мобильного устройства
//
Функция ОбновитьИнформационнуюБазу(СтруктураЗапроса, ТекущийПользователь, Узел, ЧастичнаяЗагрузка = Ложь)
	
	ДанныеКВыгрузке = ТребуетьсяВыгрузитьОбъекты(Узел);
	ОбъектыОбмена = Новый Массив; ПринудительноВыгрузить = Новый Массив;
	
	УдаляемыеПовреждения = Неопределено; УдаляемыеКартинки = Неопределено; УдаляемыеДополнительныеПредложенияДокументов = Неопределено;
	
	Если СтруктураЗапроса.Свойство("DamageDeleted") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОписаниеПовреждений.Объект,
		|	ОписаниеПовреждений.GUIDПовреждения
		|ИЗ
		|	РегистрСведений.ОписаниеПовреждений КАК ОписаниеПовреждений
		|ГДЕ
		|	ОписаниеПовреждений.GUIDПовреждения В(&GUIDПовреждения)";
		Запрос.УстановитьПараметр("GUIDПовреждения", СтруктураЗапроса.DamageDeleted);
		УдаляемыеПовреждения = Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
	Если СтруктураЗапроса.Свойство("DirectoryPhotoDeleted") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КартинкиИФайлы.Объект,
		|	КартинкиИФайлы.Идентификатор
		|ИЗ
		|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		|ГДЕ
		|	КартинкиИФайлы.Идентификатор В(&Идентификатор)";
		Запрос.УстановитьПараметр("Идентификатор", СтруктураЗапроса.DirectoryPhotoDeleted);
		УдаляемыеКартинки = Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
	Если СтруктураЗапроса.Свойство("AdditionalSalesDeleted") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеПредложенияДокументов.Документ,
		|	ДополнительныеПредложенияДокументов.ДополнительноеПредложение,
		|	ДополнительныеПредложенияДокументов.ИдентификаторМобильногоУстройства
		|ИЗ
		|	РегистрСведений.ДополнительныеПредложенияДокументов КАК ДополнительныеПредложенияДокументов
		|ГДЕ
		|	ДополнительныеПредложенияДокументов.ИдентификаторМобильногоУстройства В(&ИдентификаторМобильногоУстройства)";
		Запрос.УстановитьПараметр("ИдентификаторМобильногоУстройства", СтруктураЗапроса.AdditionalSalesDeleted);
		УдаляемыеДополнительныеПредложенияДокументов = Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
	Для Каждого Элемент Из СтруктураЗапроса Цикл
		
		ЗаписьЖурналаРегистрации(Элемент.Ключ,УровеньЖурналаРегистрации.Информация,,,"Общий цикл");
		
		Если Элемент.Ключ = "Client" Тогда
			// получим ссылку
			Для Каждого Client Из Элемент.Значение Цикл
				// проверим возможность изменения объекта ИБ
				УникальныйИдентификатор = Неопределено; СсылкаНаЭлементИБ = Неопределено;
				
				// получим объект данных
				Если Client.Свойство("ID", УникальныйИдентификатор) Тогда
					СсылкаНаЭлементИБ = Справочники.Контрагенты.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификатор));
					
					// проверим на изменения на строне ИБ
					Если ДанныеКВыгрузке.Найти(СсылкаНаЭлементИБ, "Объект") <> Неопределено Тогда
						Если ОбъектыОбмена.Найти(СсылкаНаЭлементИБ) = Неопределено Тогда
							ОбъектыОбмена.Добавить(СсылкаНаЭлементИБ);
						КонецЕсли;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				Если СсылкаНаЭлементИБ <> Неопределено Тогда
					Свойство = Неопределено; // запишем адрес контрагента
					
					СоответствиеРевизитов = Новый Соответствие;
					СоответствиеРевизитов.Вставить("Name"      , "Имя");
					СоответствиеРевизитов.Вставить("Surname"   , "Фамилия");
					СоответствиеРевизитов.Вставить("SecondName", "Отчество");
					СоответствиеРевизитов.Вставить("FullName"  , "НаименованиеПолное");
					СоответствиеРевизитов.Вставить("FullName"  , "Наименование");
					
					ОбъектИБ = СсылкаНаЭлементИБ.ПолучитьОбъект();
					
					Для Каждого СоответствиеРевизитовЭлемент Из СоответствиеРевизитов Цикл
						Если Client.Свойство(СоответствиеРевизитовЭлемент.Ключ, Свойство) И ЗначениеЗаполнено(Свойство) Тогда
							ОбъектИБ[СоответствиеРевизитовЭлемент.Значение] = Свойство;
						КонецЕсли;
					КонецЦикла;
					
					Если ОбъектИБ.Модифицированность() Тогда
						ОбъектИБ.ОбменДанными.Загрузка = Истина;
						ОбъектИБ.Записать();
					КонецЕсли;
					
					Если Client.Свойство("Adress", Свойство) И ЗначениеЗаполнено(Свойство) Тогда
						// запищем мыло
						КлючЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
						КлючЗаписи.Объект = СсылкаНаЭлементИБ;
						КлючЗаписи.Тип    = Перечисления.ТипыКонтактнойИнформации.Адрес;
						КлючЗаписи.Вид    = Справочники.ВидыКонтактнойИнформации.АдресФактический;
						
						КлючЗаписи.Прочитать();
						Если НЕ КлючЗаписи.Выбран() Тогда
							КлючЗаписи.Объект = СсылкаНаЭлементИБ;
							КлючЗаписи.Тип    = Перечисления.ТипыКонтактнойИнформации.Адрес;
							КлючЗаписи.Вид    = Справочники.ВидыКонтактнойИнформации.АдресФактический;
						КонецЕсли;
						
						КлючЗаписи.Представление = Свойство;
						
						КлючЗаписи.Записать(Истина);
					КонецЕсли;
					
					Если Client.Свойство("PhoneRepresentation", Свойство) И ЗначениеЗаполнено(Свойство) Тогда
						// запищем мыло
						КлючЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
						КлючЗаписи.Объект = СсылкаНаЭлементИБ;
						КлючЗаписи.Тип    = Перечисления.ТипыКонтактнойИнформации.Телефон;
						КлючЗаписи.Вид    = Справочники.ВидыКонтактнойИнформации.ТелефонСотовый;
						
						КлючЗаписи.Прочитать();
						Если НЕ КлючЗаписи.Выбран() Тогда
							КлючЗаписи.Объект = СсылкаНаЭлементИБ;
							КлючЗаписи.Тип    = Перечисления.ТипыКонтактнойИнформации.Телефон;
							КлючЗаписи.Вид    = Справочники.ВидыКонтактнойИнформации.ТелефонСотовый;
						КонецЕсли;
						
						Client.Свойство("PhoneRepresentation" , КлючЗаписи.Представление);
						Client.Свойство("CountryCode"         , КлючЗаписи.Поле1);
						Client.Свойство("CityCode"            , КлючЗаписи.Поле2);
						Client.Свойство("PhoneNumber"         , КлючЗаписи.Поле3);
						Client.Свойство("ExtensionTelephone"  , КлючЗаписи.Поле4);
						
						КлючЗаписи.ЗначениеПоУмолчанию = Истина;
						
						КлючЗаписи.Записать(Истина);
					КонецЕсли;
					
					Если Client.Свойство("Email", Свойство) И ЗначениеЗаполнено(Свойство) Тогда
						// запищем мыло
						КлючЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
						КлючЗаписи.Объект = СсылкаНаЭлементИБ;
						КлючЗаписи.Тип    = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
						КлючЗаписи.Вид    = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий;
						
						КлючЗаписи.Прочитать();
						Если НЕ КлючЗаписи.Выбран() Тогда
							КлючЗаписи.Объект = СсылкаНаЭлементИБ;
							КлючЗаписи.Тип    = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
							КлючЗаписи.Вид    = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий;
						КонецЕсли;
						
						КлючЗаписи.Представление = Свойство;
						
						КлючЗаписи.Записать(Истина);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "Auto" Тогда
			// получим ссылку
			Для Каждого Auto Из Элемент.Значение Цикл
				// проверим возможность изменения объекта ИБ
				УникальныйИдентификатор = Неопределено;
				
				СсылкаНаЭлементИБ = Неопределено;
				
				// получим объект данных
				Если Auto.Свойство("ID", УникальныйИдентификатор) Тогда
					СсылкаНаЭлементИБ = Справочники.Автомобили.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификатор));
					
					// проверим на изменения на строне ИБ
					Если ДанныеКВыгрузке.Найти(СсылкаНаЭлементИБ, "Объект") <> Неопределено Тогда
						Если ОбъектыОбмена.Найти(СсылкаНаЭлементИБ) = Неопределено Тогда
							ОбъектыОбмена.Добавить(СсылкаНаЭлементИБ);
						КонецЕсли;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				Если СсылкаНаЭлементИБ <> Неопределено Тогда
					Свойство = Неопределено; // запишем адрес контрагента
					Если Auto.Свойство("Number", Свойство) И ЗначениеЗаполнено(Свойство) Тогда
						Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(СсылкаНаЭлементИБ, Свойство, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
					КонецЕсли;
					
					Если Auto.Свойство("VIN", Свойство) И ЗначениеЗаполнено(Свойство) Тогда
						ОбъектАвтомобиль = СсылкаНаЭлементИБ.ПолучитьОбъект();
						
						ОбъектАвтомобиль.VIN = Свойство;
						
						ОбъектАвтомобиль.ОбменДанными.Загрузка = Истина;
						
						ОбъектАвтомобиль.Записать();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "EventItem" Тогда
			Для Каждого EventItem Из Элемент.Значение Цикл
				Status = Неопределено; Type = Неопределено; ID = Неопределено;
				
				Если EventItem.Свойство("Status", Status) И
					EventItem.Свойство("EType", Type) И
					EventItem.Свойство("ID", ID) И
					ЗначениеЗаполнено(Status) И
					ЗначениеЗаполнено(Type) И
					ЗначениеЗаполнено(ID) Тогда
					
					Если Type = "1" Тогда // ЗнР и ЗН
						СсылкаНаДокумент = Документы["ЗаявкаНаРемонт"].ПолучитьСсылку(Новый УникальныйИдентификатор(ID));
						
						// проверим на изменения на строне ИБ
						Если ДанныеКВыгрузке.Найти(СсылкаНаДокумент, "Объект") <> Неопределено Тогда
							Если ОбъектыОбмена.Найти(СсылкаНаДокумент) = Неопределено Тогда
								ОбъектыОбмена.Добавить(СсылкаНаДокумент);
							КонецЕсли;
							Продолжить;
						КонецЕсли;
						
						// обновим заявку и ее данные
						ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
						
						EventItem.Свойство("Text"               , ДокументОбъект.ПричинаОбращения);
						EventItem.Свойство("FeedbackMessage"    , ДокументОбъект.Комментарий);
						EventItem.Свойство("CountryCode"        , ДокументОбъект.КодРегиона);
						EventItem.Свойство("CityCode"           , ДокументОбъект.КодГорода);
						EventItem.Свойство("PhoneRepresentation", ДокументОбъект.ПредставлениеТелефона);
						EventItem.Свойство("ExtensionTelephone" , ДокументОбъект.ВнутреннийНомер);
						EventItem.Свойство("PhoneNumber"        , ДокументОбъект.НомерТелефона);
						EventItem.Свойство("AutoNumber"         , ДокументОбъект.ГосНомер);
						EventItem.Свойство("Mileage"            , ДокументОбъект.Пробег);
						EventItem.Свойство("Email"              , ДокументОбъект.АдресЭлектронойПочты);
						EventItem.Свойство("VIN"                , ДокументОбъект.VIN);
						
						
						CompaignID = Неопределено;
						Если EventItem.Свойство("CompaignID", CompaignID) И ЗначениеЗаполнено(CompaignID) Тогда
							ДокументОбъект.СервиснаяКампания = Справочники.СервисныеКампании.ПолучитьСсылку(Новый УникальныйИдентификатор(CompaignID));
						КонецЕсли;
						
						ДокументОбъект.ОбменДанными.Загрузка = Истина;
						
						ДокументОбъект.Записать();
						
						СсылкаНаДокумент = ДокументОбъект.Ссылка;
						
						Если ЧастичнаяЗагрузка Тогда
							ВыполнитьОчисткуСвязанныхРеквизитов(СсылкаНаДокумент);
						КонецЕсли;
						
						Если Status = "3" Тогда
							// проверим наличие ЗН если нет создадим
							Запрос = Новый Запрос;
							Запрос.Текст =
							"ВЫБРАТЬ
							|	ЗаказНаряд.Ссылка
							|ИЗ
							|	Документ.ЗаказНаряд КАК ЗаказНаряд
							|ГДЕ
							|	ЗаказНаряд.ДокументОснование = &ДокументОснование
							|	И (ЗаказНаряд.Состояние <> &СостояниеЗакрыт
							|	И ЗаказНаряд.Состояние <> &СостояниеВыполнен)
							|
							|УПОРЯДОЧИТЬ ПО
							|	ЗаказНаряд.ДатаСоздания УБЫВ";
							Запрос.УстановитьПараметр("ДокументОснование", СсылкаНаДокумент);
							Запрос.УстановитьПараметр("СостояниеЗакрыт"  , Справочники.ВидыСостоянийЗаказНарядов.Закрыт);
							Запрос.УстановитьПараметр("СостояниеВыполнен", Справочники.ВидыСостоянийЗаказНарядов.Выполнен);
							
							РезультатЗапроса = Запрос.Выполнить();
							Если РезультатЗапроса.Пустой() Тогда
								
								ДокументОбъект = Документы.ЗаказНаряд.СоздатьДокумент();
								ДокументОбъект.Заполнить(СсылкаНаДокумент);
								
								ДокументОбъект.УстановитьНовыйНомер();
								
								ДокументОбъект.Автор                 = ТекущийПользователь;
								ДокументОбъект.ПодразделениеКомпании = ТекущийПользователь.Подразделение;
								ДокументОбъект.Организация           = ТекущийПользователь.Организация;
								
								ДокументОбъект.Состояние  = Справочники.ВидыСостоянийЗаказНарядов.НачатьРаботу;
								ДокументОбъект.ДатаНачала = ТекущаяДата();
								
								Если НЕ ЗначениеЗаполнено(ДокументОбъект.Автомобиль) Тогда
									VIN = Неопределено;
									// сначала попробуем поискать автомобиль
									Если EventItem.Свойство("VIN", VIN) И ЗначениеЗаполнено(VIN) Тогда
										СсылкаАвтомобиль = Справочники.Автомобили.НайтиПоРеквизиту("VIN", VIN);
										Если ЗначениеЗаполнено(СсылкаАвтомобиль) Тогда
											ДокументОбъект.Автомобиль = СсылкаАвтомобиль;
											ДокументОбъект.ОбработкаРеквизита("Автомобиль");
										КонецЕсли;
									КонецЕсли;
									
									// создаем автомобиль
									Модель = Неопределено;
									Если НЕ ЗначениеЗаполнено(ДокументОбъект.Автомобиль) И EventItem.Свойство("Model", Модель) И Модель <> Неопределено Тогда
										ОбъектАвтомобиль = Справочники.Автомобили.СоздатьЭлемент();
										ОбъектАвтомобиль.Модель = Справочники.Модели.ПолучитьСсылку(Новый УникальныйИдентификатор(Модель));
										
										ГосНомер = Неопределено;
										EventItem.Свойство("ГосНомер", ГосНомер);
										
										ОбъектАвтомобиль.Наименование = Справочники.Автомобили.СформироватьНаименованиеАвтомобиляПоПолям(ОбъектАвтомобиль.Модель,, ГосНомер, VIN);
										ОбъектАвтомобиль.НаименованиеПолное = ОбъектАвтомобиль.Наименование;
										ОбъектАвтомобиль.VIN = VIN;
										
										ОбъектАвтомобиль.ОбменДанными.Загрузка = Истина;
										Попытка
											ОбъектАвтомобиль.Записать();
										Исключение КонецПопытки;
										
										ДокументОбъект.Автомобиль = ОбъектАвтомобиль.Ссылка;
										ДокументОбъект.ОбработкаРеквизита("Автомобиль");
									КонецЕсли;
								КонецЕсли;
								Если НЕ ЗначениеЗаполнено(ДокументОбъект.Заказчик) ИЛИ НЕ ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
									// создаем контрагента
									ИмяКлиента = Неопределено;
									Если EventItem.Свойство("ClientName", ИмяКлиента) И ИмяКлиента <> Неопределено Тогда
										НайденныйКонтрагент = Справочники.Контрагенты.НайтиПоНаименованию(ИмяКлиента, Истина);
										Если НайденныйКонтрагент.Пустая() Тогда
											// создаем нового контрагента
											НайденныйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
											НайденныйКонтрагент.Наименование = ИмяКлиента;
											НайденныйКонтрагент.НаименованиеПолное = ИмяКлиента;
											
											НайденныйКонтрагент.ФормаСобственности = Перечисления.ФормыСобственности.Прочее;
											НайденныйКонтрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель;
											
											НайденныйКонтрагент.ОбменДанными.Загрузка = Истина;
											
											Попытка
												НайденныйКонтрагент.Записать();
											Исключение КонецПопытки;
											
											ДокументОбъект.Заказчик = НайденныйКонтрагент.Ссылка;
											ДокументОбъект.ОбработкаРеквизита("Заказчик");
										КонецЕсли;
									КонецЕсли;
								КонецЕсли;
							Иначе
								Выборка = РезультатЗапроса.Выбрать(); Выборка.Следующий();
								ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
							КонецЕсли;
							
							EventItem.Свойство("FeedbackMessage", ДокументОбъект.Комментарий);
							EventItem.Свойство("Text"           , ДокументОбъект.ПричинаОбращения);
							EventItem.Свойство("Mileage"        , ДокументОбъект.Пробег);
							
							CompaignID = Неопределено;
							Если EventItem.Свойство("CompaignID", CompaignID) И ЗначениеЗаполнено(CompaignID) Тогда
								ДокументОбъект.СервиснаяКампания = Справочники.СервисныеКампании.ПолучитьСсылку(Новый УникальныйИдентификатор(CompaignID));
							КонецЕсли;
							
							ДокументОбъект.Проведен = Истина;
							ДокументОбъект.ОбменДанными.Загрузка = Истина;
							ДокументОбъект.ДополнительныеСвойства.Вставить("РегистрироватьИзмененияДокумента", Истина);
							ДокументОбъект.Записать();
							
							ПринудительноВыгрузить.Добавить(СсылкаНаДокумент);
						КонецЕсли;
						
					ИначеЕсли Type = "0" ИЛИ Type = "2" ИЛИ Type = "3" Тогда
						
						СсылкаНаСобытие = Документы.Событие.ПолучитьСсылку(Новый УникальныйИдентификатор(ID));
						
						// проверим на изменения на строне ИБ
						Если ДанныеКВыгрузке.Найти(СсылкаНаСобытие, "Объект") <> Неопределено Тогда
							Если ОбъектыОбмена.Найти(СсылкаНаСобытие) = Неопределено Тогда
								ОбъектыОбмена.Добавить(СсылкаНаСобытие);
							Конецесли;
							Продолжить;
						КонецЕсли;
						
						// изменим статус
						Если ЗначениеЗаполнено(СсылкаНаСобытие) Тогда
							ОбъектСобытие = СсылкаНаСобытие.ПолучитьОбъект();
							
							Если Status = "1" Тогда
								ОбъектСобытие.Состояние = Перечисления.СостоянияСобытий.Запланировано;
							ИначеЕсли Status = "3" Тогда
								ОбъектСобытие.Состояние = Перечисления.СостоянияСобытий.Выполняется;
							ИначеЕсли Status = "4" ИЛИ Status = "5" Тогда
								ОбъектСобытие.Состояние = Перечисления.СостоянияСобытий.Завершено;
							КонецЕсли;
							
							EventItem.Свойство("Text", ОбъектСобытие.Содержание);
							EventItem.Свойство("FeedbackMessage", ОбъектСобытие.Результат);
							EventItem.Свойство("Email", ОбъектСобытие.Mob_EMail);
							
							ОбъектСобытие.ОбменДанными.Загрузка = Истина;
							
							ОбъектСобытие.Записать();
							
							// проверим есть ли необходимость отправить письмо или СМС
							Если Status = "3" Тогда
								Если Type = "2" Тогда // создаем SMS
									// проверим нет ли на основании этого документа уже SMS
									ВидыДокументов = Новый Массив;
									ВидыДокументов.Добавить("SMS");
									
									Если дкПолучитьМассивПодчиненных(СсылкаНаСобытие, ВидыДокументов, Истина).Количество() = 0 Тогда
										// созадем документ
										ДокументSMS = Документы.SMS.СоздатьДокумент();
										
										ДокументSMS.Заполнить(СсылкаНаСобытие);
										ДокументSMS.ОбменДанными.Загрузка = Истина;
										
										ДокументSMS.Автор                 = ТекущийПользователь;
										ДокументSMS.ПодразделениеКомпании = ТекущийПользователь.Подразделение;
										ДокументSMS.Организация           = ТекущийПользователь.Организация;
										
										EventItem.Свойство("Text", ДокументSMS.ТекстСообщения);
										
										ДокументSMS.Записать();
									КонецЕсли;
								ИначеЕсли Type = "3" Тогда // создаем электронное письмо
									// проверим нет ли на основании этого документа уже письмо
									ВидыДокументов = Новый Массив;
									ВидыДокументов.Добавить("ЭлектронноеПисьмо");
									
									Если дкПолучитьМассивПодчиненных(СсылкаНаСобытие, ВидыДокументов, Истина).Количество() = 0 Тогда
										// созадем документ
										ДокументEMail = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
										
										ДокументEMail.Автор                 = ТекущийПользователь;
										ДокументEMail.ПодразделениеКомпании = ТекущийПользователь.Подразделение;
										ДокументEMail.Организация           = ТекущийПользователь.Организация;
										
										ДокументEMail.Заполнить(СсылкаНаСобытие);
										ДокументEMail.ОбменДанными.Загрузка = Истина;
										
										Email = Неопределено;
										Если EventItem.Свойство("Email", Email) И ЗначениеЗаполнено(Email) Тогда
											НоваяСтрока = ДокументEMail.Получатели.Добавить();
											НоваяСтрока.АдресЭлектроннойПочты = Email;
											НоваяСтрока.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
											НоваяСтрока.Представление = Email;
										КонецЕсли;
										
										ДокументEMail.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие");
										
										Запрос = Новый Запрос;
										Запрос.Текст =
										"ВЫБРАТЬ
										|	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка
										|ИЗ
										|	Справочник.УчетныеЗаписиЭлектроннойПочты.Пользователи КАК УчетныеЗаписиЭлектроннойПочтыПользователи
										|ГДЕ
										|	УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь
										|	И УчетныеЗаписиЭлектроннойПочтыПользователи.УчетнаяЗаписьПоУмолчанию = ИСТИНА
										|	И УчетныеЗаписиЭлектроннойПочтыПользователи.Транспорт = ИСТИНА";
										Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
										
										РезультатЗапроса = Запрос.Выполнить();
										Если НЕ РезультатЗапроса.Пустой() Тогда
											ДокументEMail.УчетнаяЗапись = РезультатЗапроса.Выгрузить()[0].Ссылка;
											ДокументEMail.ОтправительПредставление = ДокументEMail.УчетнаяЗапись.АдресЭлектроннойПочты;
										КонецЕсли;
										
										ПолучателиПредставление = ДокументEMail.СформироватьПредставлениеПолучателейПисьма(Перечисления.КодГруппыАдресаЭлектронногоПисьма);
										ДокументEMail.Записать();
										
										Попытка
											СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
											СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(ДокументEMail.УчетнаяЗапись, СтруктураРежимовОбмена);
											Если СоединениеСПочтовымСервером <> Неопределено Тогда
												Если эпОтправитьПисьмо(ДокументEMail, СоединениеСПочтовымСервером) Тогда
													эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
												Иначе
													эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
												КонецЕсли;
											КонецЕсли;
										Исключение
										КонецПопытки;
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "CheckList" Тогда
			Для Каждого CheckList Из Элемент.Значение Цикл
				СсылкаНаДокумент =  Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(CheckList.CheckList_EventItem_S_ID));
				Если ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
					
					Checked = Неопределено;
					Если CheckList.Свойство("Checked", Checked) И ВРег(Checked) = "ДА" Тогда
						СсылкаCheckList = Справочники.CheckList.ПолучитьСсылку(Новый УникальныйИдентификатор(CheckList.DirectoryID));
						Если ЗначениеЗаполнено(СсылкаCheckList) Тогда
							ЗаписьРегистра = РегистрыСведений.ЗначениеCheckList.СоздатьМенеджерЗаписи();
							ЗаписьРегистра.Объект    = СсылкаНаДокумент;
							ЗаписьРегистра.CheckList = СсылкаCheckList;
							
							Если CheckList.Types = "1" ИЛИ CheckList.Types = "5" Тогда
								ЗаписьРегистра.Значение = CheckList.Value;
							ИначеЕсли CheckList.Types = "2" ИЛИ CheckList.Types = "3" Тогда
								ЗаписьРегистра.Значение = Число(CheckList.Value);
							ИначеЕсли CheckList.Types = "4" Тогда
								ЗаписьРегистра.Значение = (ВРег(CheckList.Value) = "ДА");
							ИначеЕсли CheckList.Types = "6" ИЛИ CheckList.Types = "7" Тогда
								ЗаписьРегистра.Значение = Перечисления.ВидыЗначенийСветофора.ПреобразоватьКПеречислению(CheckList.Value);
							КонецЕсли;
							
							ЗаписьРегистра.GUID    = CheckList.ID;
							ЗаписьРегистра.Checked = CheckList.Checked;
							ЗаписьРегистра.Порядок = CheckList.position;
							
							ЗаписьРегистра.Записать(Истина);
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "DirectoryPhoto" Тогда
			Для Каждого DirectoryPhoto Из Элемент.Значение Цикл
				
				Если НЕ ЗначениеЗаполнено(DirectoryPhoto.EventId) Тогда
					Продолжить;
				КонецЕсли;
				
				СсылкаНаДокумент =  Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(DirectoryPhoto.EventId));
				
				Если ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
					ФайлСтрокой = Неопределено;
					Если DirectoryPhoto.Свойство("Photo", ФайлСтрокой) И ФайлСтрокой <> Неопределено Тогда
						Запись = РегистрыСведений.КартинкиИФайлы.СоздатьМенеджерЗаписи();
						
						Запись.Объект         = СсылкаНаДокумент;
						Если DirectoryPhoto.Свойство("IsScrean") И ВРег(DirectoryPhoto.IsScrean) = "ДА" Тогда
							Запись.Идентификатор  = "DirectoryPhoto";
							Запись.ИмяФайла       = "DirectoryPhoto.png";
						Иначе
							Запись.Идентификатор  = DirectoryPhoto.ID;
							Запись.ИмяФайла       = DirectoryPhoto.ID+".png";
						КонецЕсли;
						
						Запись.Данные         = Новый ХранилищеЗначения(Base64Значение(ФайлСтрокой), Новый СжатиеДанных(9));
						Запись.Картинка       = Истина;
						Запись.ДатаСохранения = ТекущаяДата();
						Запись.Записать();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "Damage" Тогда
			Для Каждого Damage Из Элемент.Значение Цикл
				СсылкаНаДокумент =  Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(Damage.Damage_EventItem_S_ID));
				
				Если ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
					Запись = РегистрыСведений.ОписаниеПовреждений.СоздатьМенеджерЗаписи();
					
					Запись.Объект                = СсылкаНаДокумент;
					Запись.GUIDПовреждения       = Damage.ID;
					Запись.ТипПовреждения        = ПолучитьТипПовреждения(Damage.Types);
					Запись.ОписаниеПовреждения   = Damage.Text;
					Запись.ИдентификаторКартинки = Damage.Photo;
					
					Если Damage.Свойство("Number") Тогда
						Запись.НомерПовреждения = XMLЗначение(Тип("Число"), Damage.Number);
					КонецЕсли;
					
					Запись.Записать(Истина);
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "AdditionalSales" Тогда
			Для Каждого AdditionalSales Из Элемент.Значение Цикл
				СсылкаНаДокумент   = Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Новый УникальныйИдентификатор(AdditionalSales.AdditionalSales_EventItem_S_ID));
				СсылкаНаАвтоработу = Справочники.Автоработы.ПолучитьСсылку(Новый УникальныйИдентификатор(AdditionalSales.Directory));
				
				Если ЗначениеЗаполнено(СсылкаНаДокумент) И ЗначениеЗаполнено(СсылкаНаАвтоработу) Тогда
					Запись = РегистрыСведений.ДополнительныеПредложенияДокументов.СоздатьМенеджерЗаписи();
					
					Запись.Документ = СсылкаНаДокумент;
					Запись.ДополнительноеПредложение         = СсылкаНаАвтоработу;
					Запись.Количество                        = AdditionalSales.Amount;
					Запись.ИдентификаторМобильногоУстройства = AdditionalSales.ID;
					
					Запись.Записать();
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "DamageDeleted" Тогда
			Для Каждого DamageDeleted Из Элемент.Значение Цикл
				Запись = РегистрыСведений.ОписаниеПовреждений.СоздатьМенеджерЗаписи();
				
				Если УдаляемыеПовреждения <> Неопределено Тогда
					УдаляемыеПовреждения.Сбросить();
					Если УдаляемыеПовреждения.НайтиСледующий(Новый Структура("GUIDПовреждения", DamageDeleted)) Тогда
						Запись.Объект = УдаляемыеПовреждения.Объект;
					КонецЕсли;
				КонецЕсли;
				Запись.GUIDПовреждения = DamageDeleted;
				Запись.Прочитать();
				Если Запись.Выбран() Тогда
					Запись.Удалить();
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "DirectoryPhotoDeleted" Тогда
			Для Каждого DirectoryPhotoDeleted Из Элемент.Значение Цикл
				Запись = РегистрыСведений.КартинкиИФайлы.СоздатьМенеджерЗаписи();
				Если УдаляемыеКартинки <> Неопределено Тогда
					УдаляемыеКартинки.Сбросить();
					Если УдаляемыеКартинки.НайтиСледующий(Новый Структура("Идентификатор", DirectoryPhotoDeleted)) Тогда
						Запись.Объект = УдаляемыеКартинки.Объект;
					КонецЕсли;
				КонецЕсли;
				Запись.Идентификатор = DirectoryPhotoDeleted;
				Запись.Прочитать();
				Если Запись.Выбран() Тогда
					Запись.Удалить();
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Элемент.Ключ = "AdditionalSalesDeleted" Тогда
			Для Каждого AdditionalSalesDeleted Из Элемент.Значение Цикл
				Если УдаляемыеДополнительныеПредложенияДокументов <> Неопределено Тогда
					УдаляемыеДополнительныеПредложенияДокументов.Сбросить();
					Запись = РегистрыСведений.ДополнительныеПредложенияДокументов.СоздатьМенеджерЗаписи();
					Если УдаляемыеДополнительныеПредложенияДокументов.НайтиСледующий(Новый Структура("ИдентификаторМобильногоУстройства", AdditionalSalesDeleted)) Тогда
						Запись.Документ                  = УдаляемыеДополнительныеПредложенияДокументов.Документ;
						Запись.ДополнительноеПредложение = УдаляемыеДополнительныеПредложенияДокументов.ДополнительноеПредложение;
					КонецЕсли;
					Запись.ИдентификаторМобильногоУстройства = AdditionalSalesDeleted;
					Запись.Прочитать();
					Запись.Прочитать();
					Если Запись.Выбран() Тогда
						Запись.Удалить();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ОбменСМобильнымУстройством.ОбновитьДопродажиВДокументах();
	
	Для каждого ОбъектОбмена Из ОбъектыОбмена Цикл
		ПринудительноВыгрузить.Добавить(ОбъектОбмена);
	КонецЦикла;
	
	Возврат Новый Структура("ОбъектыОбмена,ПринудительноВыгрузить", ОбъектыОбмена, ПринудительноВыгрузить);
	
КонецФункции

Функция КомандаПользователя(Узел, Пользователь, Команда, Документ)
	
	НаборЗаписей = РегистрыСведений.ИсторияКомандМобильногоПриложения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Источник.Установить(Узел);
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Отбор.Команда.Установить(Команда);
	
	НаборЗаписей.Прочитать(); НаборЗаписей.Очистить();
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период       = ТекущаяДата();
	Запись.Источник     = Узел;
	Запись.Пользователь = Пользователь;
	Запись.Команда      = Команда;
	Запись.Объект       = Документ;
	
	НаборЗаписей.Записать(Истина);
	
КонецФункции

Функция ЗаписатьСобытиеВЛог(Узел, Текст, Ответ = Ложь, Команда = "")
	
	МенеджерЗаписи = РегистрыСведений.ЛогОбменаСМобильнымУстройством.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период  = ТекущаяДатаСеанса();
	МенеджерЗаписи.Узел    = Узел;
	МенеджерЗаписи.Ответ   = Ответ;
	МенеджерЗаписи.Текст   = Текст;
	МенеджерЗаписи.Команда = Команда;
	МенеджерЗаписи.guid    = Новый УникальныйИдентификатор;
	
	МенеджерЗаписи.Записать(Истина);
	
КонецФункции

Функция ТребуетьсяВыгрузитьОбъекты(Узел)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвтомобилиИзменения.Ссылка КАК Объект
	|ИЗ
	|	Справочник.Автомобили.Изменения КАК АвтомобилиИзменения
	|ГДЕ
	|	АвтомобилиИзменения.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтрагентыИзменения.Ссылка
	|ИЗ
	|	Справочник.Контрагенты.Изменения КАК КонтрагентыИзменения
	|ГДЕ
	|	КонтрагентыИзменения.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СобытиеИзменения.Ссылка
	|ИЗ
	|	Документ.Событие.Изменения КАК СобытиеИзменения
	|ГДЕ
	|	СобытиеИзменения.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявкаНаРемонтИзменения.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРемонт.Изменения КАК ЗаявкаНаРемонтИзменения
	|ГДЕ
	|	ЗаявкаНаРемонтИзменения.Узел = &Узел";
	
	Запрос.УстановитьПараметр("Узел", Узел);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ВыполнитьОчисткуСвязанныхРеквизитов(Документ)
	
	НаборЗаписейОписаниеПовреждений = РегистрыСведений.ОписаниеПовреждений.СоздатьНаборЗаписей();
	НаборЗаписейОписаниеПовреждений.Отбор.Объект.Установить(Документ);
	НаборЗаписейОписаниеПовреждений.Прочитать();
	
	ИдентификаторыПовреждений = НаборЗаписейОписаниеПовреждений.ВыгрузитьКолонку("ИдентификаторКартинки");
	
	НаборЗаписейКартинкиИФайлы = РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей();
	НаборЗаписейКартинкиИФайлы.Отбор.Объект.Установить(Документ);
	НаборЗаписейКартинкиИФайлы.Прочитать();
	
	УдаляемыеСтроки = Новый Массив;
	Для Каждого Картинка Из НаборЗаписейКартинкиИФайлы Цикл
		Если ИдентификаторыПовреждений.Найти(Картинка.Идентификатор) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		УдаляемыеСтроки.Добавить(Картинка);
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		НаборЗаписейКартинкиИФайлы.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	НаборЗаписейОписаниеПовреждений.Очистить();
	
	НаборЗаписейКартинкиИФайлы.Записать(Истина);
	НаборЗаписейОписаниеПовреждений.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыПечати

// Обработка запроса на печать документа
//
Функция Печать(Документ, УзелОбмена, Пользователь, Ошибки = Неопределено)
	
	НаборЗаписейРегистра = РегистрыСведений.ОчередьПечатиДокументовМобильногоКлиента.СоздатьНаборЗаписей();
	НаборЗаписейРегистра.Отбор.УзелОбмена.Установить(УзелОбмена);
	НаборЗаписейРегистра.Отбор.Документ.Установить(Документ.Ссылка);
	НаборЗаписейРегистра.Отбор.ПечатнаяФорма.Установить("АктОсмотра");
	
	НаборЗаписейРегистра.Прочитать();
	
	Если НаборЗаписейРегистра.Количество() = 0 Тогда
		
		НоваяЗапись = НаборЗаписейРегистра.Добавить();
		
		НоваяЗапись.Документ      = Документ.Ссылка;
		НоваяЗапись.Пользователь  = Пользователь;
		НоваяЗапись.УзелОбмена    = УзелОбмена;
		НоваяЗапись.ПечатнаяФорма = "АктОсмотра";
		
		Попытка
			НаборЗаписейРегистра.Записать();
		Исключение
			Ошибки = ОписаниеОшибки();
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
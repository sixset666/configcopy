// Процедура заполняет кеш выполнения работ в ЗН
Процедура урвЗаказНарядЗаполнитьКэшВыполнениеРабот(ЭтаФорма=Неопределено) Экспорт
	
	урвзфЗаказНарядЗаполнитьКэшВыполнениеРабот(ЭтаФорма);
	
КонецПроцедуры

// Процедура отображает статус в ЗН
Процедура урвЗаказНарядОтображениеСтатуса(ЭтаФорма=Неопределено,ОформленияСтрок=Неопределено) Экспорт
	
	урвзфЗаказНарядОтображениеСтатуса(ЭтаФорма,ОформленияСтрок);
	
КонецПроцедуры

// Процедура заполняет сотрудников ЗН по данным УРВ
Процедура урвЗаказНарядСотрудникиЗаполнитьПоДаннымУРВ(ДокументОбъект) Экспорт
	
	урвзфЗаказНарядСотрудникиЗаполнитьПоДаннымУРВ(ДокументОбъект);
	
КонецПроцедуры

// Функция возвращает список занятых рабочих мест
Функция урвПолучитьСписокЗанятыхРабочихМест() Экспорт
	ЗанятыеРабочиеМеста = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СостояниеРаботСрезПоследних.Период КАК Период,
	               |	СостояниеРаботСрезПоследних.РабочееМесто КАК РабочееМесто,
	               |	СостояниеРаботСрезПоследних.ПакетРабот,
	               |	СостояниеРаботСрезПоследних.Сотрудник,
	               |	СостояниеРаботСрезПоследних.Статус
	               |ИЗ
	               |	РегистрСведений.УРВ_СостояниеРабот.СрезПоследних(, ) КАК СостояниеРаботСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СостояниеРаботСрезПоследних.Период,
	               |	СостояниеРаботСрезПоследних.РабочееМесто,
	               |	СостояниеРаботСрезПоследних.Сотрудник,
	               |	СостояниеРаботСрезПоследних.ПакетРабот,
	               |	СостояниеРаботСрезПоследних.Статус
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период УБЫВ
	               |ИТОГИ ПО
	               |	РабочееМесто";
	//Запрос.УстановитьПараметр("Подразделение", ПользовательСистемы.Подразделение);			   
	ВыборкаРабочиеМеста = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРабочиеМеста.Следующий() Цикл
		ВыборкаЗаписи = ВыборкаРабочиеМеста.Выбрать();
		Если ВыборкаЗаписи.Следующий() Тогда
			Если ВыборкаЗаписи.Статус = Перечисления.УРВ_СтатусыРабот.Выполнение Тогда
				ЗанятыеРабочиеМеста.Добавить(ВыборкаЗаписи.РабочееМесто);	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат ЗанятыеРабочиеМеста;
КонецФункции

// Функция возвращает структуру события
Функция урвПолучитьСтруктуруСобытия(Данные, Рарус_Компонента, ЭтаФорма) Экспорт
	
	СтруктураРезультата = Новый Структура("СкКод,Стат,СкСимвол");
	
	Попытка
		Данные = СтрЗаменить(Данные,"|",Символы.ПС);
		Если СтрЧислоСтрок(Данные) = 1 Тогда // если был передан только сам код
			СтруктураРезультата.СкКод = Число(Данные);
			СтруктураРезультата.СкСимвол = "";
			СтруктураРезультата.Стат = СокрЛП(Рарус_Компонента.ПолучитьСостояниеПерехватаКлавиш());	
		ИначеЕсли СтрЧислоСтрок(Данные) = 3 Тогда
			СтруктураРезультата.СкКод = Число(СтрПолучитьСтроку(Данные,1));
			СтруктураРезультата.СкСимвол = СтрПолучитьСтроку(Данные,2);
			СтруктураРезультата.Стат = СокрЛП(СтрПолучитьСтроку(Данные,3));
		ИначеЕсли СтрЧислоСтрок(Данные) = 4 Тогда     // если сам символ "|"
			СтруктураРезультата.СкКод = Число(СтрПолучитьСтроку(Данные,1));
			СтруктураРезультата.СкСимвол = СтрПолучитьСтроку(Данные,3);
			СтруктураРезультата.Стат = СокрЛП(СтрПолучитьСтроку(Данные,4));
		Иначе
			СтруктураРезультата = Неопределено;	
		КонецЕсли;
	Исключение КонецПопытки;
	Возврат СтруктураРезультата;
	
КонецФункции

// Функция формирования отчета по состоянию работ
Функция урвСформироватьОтчетПоСостояниюРабот(ЭтаФорма = Неопределено, ПараметрОтбора = Неопределено, Период = Неопределено) Экспорт
	
	ОтчетОбъект = Отчеты.УРВ_СостояниеРабот.Создать();
	ОтчетОбъект.РежимВыводаОтчета=Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
	ОтчетОбъект.РежимНастройки=Перечисления.РежимыНастройкиОтчетов.Эксперт;
	ОтчетОбъект.ИмяФормыНастроек="НастройкиОбороты";
	ОтчетОбъект.ЗаполнитьНачальныеНастройки("ТекстЗапроса");
	ОтчетОбъект.ПостроительОтчета.Отбор.Сбросить();
	// покажем все колонки
	Для Каждого СтрокаПоказатель Из ОтчетОбъект.Показатели Цикл
		СтрокаПоказатель.Использование = Истина;
	КонецЦикла;
	
	Если ЭтаФорма <> Неопределено И ТипЗнч(ЭтаФорма.Ссылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Попытка
			ОтчетОбъект.ПостроительОтчета.Отбор.ЗаказНаряд.Значение=ЭтаФорма.Ссылка;
			ОтчетОбъект.ПостроительОтчета.Отбор.ЗаказНаряд.ВидСравнения = ВидСравнения.Равно;
			ОтчетОбъект.ПостроительОтчета.Отбор.ЗаказНаряд.Использование=Истина;
			Если НЕ обЗначениеНеЗаполнено(ЭтаФорма.Ссылка.ДатаСоздания) Тогда
				ОтчетОбъект.ДатаНачала = ЭтаФорма.Ссылка.ДатаСоздания;
			КонецЕсли;
			// спрячем колонку заказ-наряд
			ПолеЗаказНаряд = ОтчетОбъект.Показатели.Найти("ЗаказНаряд");
			Если ПолеЗаказНаряд <> Неопределено Тогда ПолеЗаказНаряд.Использование = Ложь; КонецЕсли;
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	// настроим отбор, если передано...
	Если ПараметрОтбора <> Неопределено Тогда
		// ... работа
		Если ТипЗнч(ПараметрОтбора) = Тип("СправочникСсылка.Автоработы") Тогда
			Попытка
				ОтчетОбъект.ПостроительОтчета.Отбор.Работа.Значение=ПараметрОтбора;
				ОтчетОбъект.ПостроительОтчета.Отбор.Работа.ВидСравнения = ВидСравнения.Равно;
				ОтчетОбъект.ПостроительОтчета.Отбор.Работа.Использование=Истина;
				// спрячем колонку работы
				ПолеРабота = ОтчетОбъект.Показатели.Найти("Работа");
				Если ПолеРабота <> Неопределено Тогда ПолеРабота.Использование = Ложь; КонецЕсли;
			Исключение
			КонецПопытки; 
		// ... сотрудник	
		ИначеЕсли ТипЗнч(ПараметрОтбора) = Тип("СправочникСсылка.Сотрудники") Тогда
			Попытка
				ОтчетОбъект.ПостроительОтчета.Отбор.Сотрудник.Значение=ПараметрОтбора;
				ОтчетОбъект.ПостроительОтчета.Отбор.Сотрудник.ВидСравнения = ВидСравнения.Равно;
				ОтчетОбъект.ПостроительОтчета.Отбор.Сотрудник.Использование=Истина;
				// спрячем колонку сотрудника
				ПолеСотрудник = ОтчетОбъект.Показатели.Найти("Сотрудник");
				Если ПолеСотрудник <> Неопределено Тогда ПолеСотрудник.Использование = Ложь; КонецЕсли;
			Исключение
			КонецПопытки; 
		// ... цех	
		ИначеЕсли ТипЗнч(ПараметрОтбора) = Тип("СправочникСсылка.Цеха") Тогда
			Попытка
				ОтчетОбъект.ПостроительОтчета.Отбор.РабочееМесто.Значение=ПараметрОтбора;
				ОтчетОбъект.ПостроительОтчета.Отбор.РабочееМесто.ВидСравнения = ВидСравнения.Равно;
				ОтчетОбъект.ПостроительОтчета.Отбор.РабочееМесто.Использование=Истина;
				// спрячем колонку
				ПолеРабочееМесто = ОтчетОбъект.Показатели.Найти("РабочееМесто");
				Если ПолеРабочееМесто <> Неопределено Тогда ПолеРабочееМесто.Использование = Ложь; КонецЕсли;
			Исключение
			КонецПопытки; 
		КонецЕсли;
	КонецЕсли;
	
	Если Период <> Неопределено Тогда
		ОтчетОбъект.ДатаНачала = НачалоДня(Период);
		ОтчетОбъект.ДатаКонца = КонецДня(Период)
	КонецЕслИ;
	
	ФормаОтчета=ПолучитьОбщуюФорму("Отчет",,ЭтаФорма);
	ФормаОтчета.ОтчетОбъект=ОтчетОбъект;
	ФормаОтчета.Заголовок="Состояние работ";
	ФормаОтчета.Открыть();
	
КонецФункции



Процедура СопоставитьЭлементНоменклатуры(Ссылка, ИмяМодуля) Экспорт

	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Номенклатура") Тогда
		СсылкаНаНоменклатуру = Ссылка;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда	
		СсылкаНаНоменклатуру = Ссылка.Владелец;
		Если ЗначениеЗаполнено(СсылкаНаНоменклатуру) И НЕ ТипЗнч(СсылкаНаНоменклатуру) = Тип("СправочникСсылка.Номенклатура") Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если СсылкаНаНоменклатуру.ЭтоГруппа Тогда
		СопоставитьГруппу(СсылкаНаНоменклатуру, СсылкаНаНоменклатуру.Родитель, ИмяМодуля);
	Иначе	
		Если СсылкаНаНоменклатуру.ПометкаУдаления Тогда
			УдалитьЭлемент(СсылкаНаНоменклатуру, ПолучитьТаблицуСопоставления(Ссылка, ИмяМодуля));
		Иначе
			СопоставитьЭлемент(СсылкаНаНоменклатуру, СсылкаНаНоменклатуру.Родитель, ПолучитьТаблицуСопоставления(Ссылка, ИмяМодуля), ИмяМодуля);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура СопоставитьВсюНоменклатуру() Экспорт
	
	КатегорииСопоставления = ns_Сопоставление.ПолучитьКатегорииСопоставления();
	Для Каждого ИмяМодуля Из КатегорииСопоставления Цикл
		ТаблицаСопоставления = ПолучитьТаблицуСопоставления(, ИмяМодуля);
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.Родитель КАК Родитель,
		|	Номенклатура.ЭтоГруппа КАК ЭтоГруппа
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоГруппа УБЫВ";
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли; 
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Попытка
			НачатьТранзакцию();
			
			Пока Выборка.Следующий() Цикл
				Если Выборка.ЭтоГруппа Тогда
					СопоставитьГруппу(Выборка.Ссылка, Выборка.Родитель, ИмяМодуля);
				Иначе	
					СопоставитьЭлемент(Выборка.Ссылка, Выборка.Родитель, ТаблицаСопоставления, ИмяМодуля)
				КонецЕсли; 
			КонецЦикла; 
			
			ЗафиксироватьТранзакцию();		
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли; 
			РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ОшибкаРаботыМодуля, ОписаниеОшибки());
			ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
		КонецПопытки; 
	КонецЦикла; 
	
КонецПроцедуры

Функция СопоставитьГруппу(Ссылка, Родитель, ИмяМодуля)
	
	СсылкаНаГруппу = Справочники[ИмяМодуля].ПолучитьСсылку(Ссылка.УникальныйИдентификатор());
	Если Найти(Строка(СсылкаНаГруппу), "не найден") > 0 Тогда
		ОбъектГруппа = Справочники[ИмяМодуля].СоздатьГруппу();
		ОбъектГруппа.УстановитьСсылкуНового(СсылкаНаГруппу);
	Иначе
		ОбъектГруппа = СсылкаНаГруппу.ПолучитьОбъект();
	КонецЕсли; 
	
	ОбъектГруппа.Номенклатура = Ссылка;
	ОбъектГруппа.Наименование = Ссылка.Наименование;
	Если ЗначениеЗаполнено(Родитель) Тогда
		ОбъектГруппа.Родитель = СопоставитьГруппу(Родитель, Родитель.Родитель, ИмяМодуля);
	КонецЕсли; 
	ОбъектГруппа.Записать();
	
	Возврат ОбъектГруппа.Ссылка;
КонецФункции

Процедура СопоставитьЭлемент(Ссылка, Родитель, ТаблицаСопоставления, ИмяМодуля)
	
	Характеристики = ПолучитьХарактеристики(Ссылка);
	
	//УстановитьБезопасныйРежим(Истина);
	//
	//Для каждого ИмяРазделителя Из ns_ОбщегоНазначенияСервер.РазделителиКонфигурации() Цикл
	//    УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
	//КонецЦикла;

	ОбщийМодуль = Вычислить(ИмяМодуля);
	Если Характеристики = Неопределено Тогда
		ОбщийМодуль.Сопоставить(Ссылка, Неопределено, Родитель, ТаблицаСопоставления);
	Иначе
		Для каждого Характеристика Из Характеристики Цикл
			ОбщийМодуль.Сопоставить(Ссылка, Характеристика, Родитель, ТаблицаСопоставления);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьЭлемент(Ссылка, ТаблицаСопоставления)
	
	Характеристики = ПолучитьХарактеристики(Ссылка);
	
	Если Характеристики = Неопределено Тогда
		Удалить(Ссылка, Неопределено, ТаблицаСопоставления);
	Иначе
		Для каждого Характеристика Из Характеристики Цикл
			Удалить(Ссылка, Характеристика, ТаблицаСопоставления);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура Удалить(Ссылка, Характеристика, ТаблицаСопоставления)
	СсылкаНаЭлемент = НайтиСопоставление(Ссылка, Характеристика, ТаблицаСопоставления);
	Если СсылкаНаЭлемент = Неопределено Тогда
		Возврат;	
	Иначе
		ОбъектЭлемент = СсылкаНаЭлемент.ПолучитьОбъект();
		ОбъектЭлемент.УстановитьПометкуУдаления(Истина);
		ОбъектЭлемент.Записать();
	КонецЕсли; 
КонецПроцедуры

Функция НайтиСопоставление(Ссылка, Характеристика, ТаблицаСопоставления) Экспорт
	
	Если ЗначениеЗаполнено(Характеристика) Тогда
		Id = Строка(Ссылка.УникальныйИдентификатор()) + "#" + Строка(Характеристика.УникальныйИдентификатор());
	Иначе
		Id = Строка(Ссылка.УникальныйИдентификатор());
	КонецЕсли; 
	
	Возврат ТаблицаСопоставления[Id];
КонецФункции
  
Функция ПолучитьТаблицуСопоставления(Ссылка = Неопределено, ИмяМодуля)
	
	ТаблицаСопоставления = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ЗапчастиИАксессуары.Ссылка КАК Ссылка,
	|	ns_ЗапчастиИАксессуары.Id КАК Id
	|ИЗ
	|	Справочник.ns_ЗапчастиИАксессуары КАК ns_ЗапчастиИАксессуары
	|ГДЕ
	|	ns_ЗапчастиИАксессуары.Id ПОДОБНО &Id
	|	И ns_ЗапчастиИАксессуары.ЭтоГруппа = ЛОЖЬ";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ns_ЗапчастиИАксессуары", ИмяМодуля);
	Если НЕ Ссылка = Неопределено Тогда
		Id = XMLСтрока(Ссылка.УникальныйИдентификатор());
		Запрос.УстановитьПараметр("Id", Id + "%");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ИмяМодуля + ".Id ПОДОБНО &Id", "ИСТИНА");	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаСопоставления;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТаблицаСопоставления.Вставить(Выборка.Id, Выборка.Ссылка);	
	КонецЦикла;
	
	Возврат ТаблицаСопоставления;
КонецФункции

Функция ПолучитьКатегорииСопоставления() Экспорт
	КатегорииСопоставления = Новый Массив;
	
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьЗапчастиИАксессуары() Тогда
		КатегорииСопоставления.Добавить("ns_ЗапчастиИАксессуары");
	КонецЕсли; 
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьБытоваяЭлектроника() Тогда
		КатегорииСопоставления.Добавить("ns_БытоваяЭлектроника");
	КонецЕсли; 
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьДляДомаИДачи() Тогда
		КатегорииСопоставления.Добавить("ns_ДляДомаИДачи");
	КонецЕсли; 
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьДляБизнеса() Тогда
		КатегорииСопоставления.Добавить("ns_ДляБизнеса");
	КонецЕсли; 
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьХоббиИОтдых() Тогда
		КатегорииСопоставления.Добавить("ns_ХоббиИОтдых");
	КонецЕсли; 
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьМотоциклыИМототехника() Тогда
		КатегорииСопоставления.Добавить("ns_МотоциклыИМототехника");
	КонецЕсли;
	Если ns_ОбщегоНазначенияПовтИсп.ИспользоватьЛичныеВещи() Тогда
		КатегорииСопоставления.Добавить("ns_ЛичныеВещи");
	КонецЕсли;
	
	Возврат КатегорииСопоставления;
КонецФункции

Функция ПолучитьХарактеристики(Ссылка)
	Характеристики = Неопределено;
	Если ns_ОбщегоНазначенияПовтИсп.ПолучитьСоздаватьОбъявлениеНаКаждуюХарактеристику() Тогда
		Характеристики = ns_Переопределяемый.ПолучитьХарактеристики(Ссылка);
	КонецЕсли;
	
	Возврат Характеристики;
КонецФункции

Процедура ЗаполнитьTitle(ОбъектЭлемент) Экспорт
	
	Заголовок = ns_ШаблонизаторСервер.СформироватьTitle(ns_ОбщегоНазначенияПовтИсп.ПолучитьШаблонЗаголовка(), ОбъектЭлемент.Title, ОбъектЭлемент.Номенклатура, ОбъектЭлемент.Характеристика);
	Если НЕ ЗначениеЗаполнено(ОбъектЭлемент.Title) Тогда
		ОбъектЭлемент.Title = Заголовок;	
		Возврат;
	КонецЕсли;

	Если НЕ ОбъектЭлемент.Title = Заголовок И ns_ОбщегоНазначенияПовтИсп.ПолучитьНеПерезаполнятьЗаголовок() = Ложь Тогда
		ОбъектЭлемент.Title = Заголовок;	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьDescription(Ссылка, ОбъектЭлемент) Экспорт
	
	Описание = ns_Переопределяемый.ПолучитьОписание(Ссылка);
	Если НЕ ЗначениеЗаполнено(ОбъектЭлемент.nsDescription) Тогда
		ОбъектЭлемент.nsDescription = Описание;	
		Возврат;
	КонецЕсли;

	Если НЕ ОбъектЭлемент.nsDescription = Описание И ns_ОбщегоНазначенияПовтИсп.ПолучитьНеПерезаполнятьОписание() = Ложь Тогда
		ОбъектЭлемент.nsDescription = Описание;	
	КонецЕсли; 
	
КонецПроцедуры


















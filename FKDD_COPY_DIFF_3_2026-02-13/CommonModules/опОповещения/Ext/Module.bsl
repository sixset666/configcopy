////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С РЕГИСТРОМ НАПОМИНАНИЯ

// Выводит окно напоминания
// запускается из общего модуля приложения как обработчик ожидания
//
// Динамически подключаемый обработчик изменения данных
//
// Параметры:
//  Нет
//
Процедура опОбработкаОповещений() Экспорт
	Запрос = Новый Запрос;
    Запрос.Текст="ВЫБРАТЬ
                 |	Напоминания.Пользователь,
                 |	Напоминания.Автор,
                 |	Напоминания.Завершено,
                 |	Напоминания.РеальнаяДатаОповещения,
                 |	Напоминания.Объект
                 |ИЗ
                 |	РегистрСведений.Напоминания КАК Напоминания
                 |ГДЕ
                 |	Напоминания.Пользователь = &Пользователь
                 |	И Напоминания.Завершено = ЛОЖЬ
                 |	И Напоминания.Редактирование = ЛОЖЬ
                 |	И Напоминания.РеальнаяДатаОповещения < &РеальнаяДатаОповещения
                 |	И (Напоминания.ДатаАктуальности = &ПустаяДата
                 |			ИЛИ Напоминания.ДатаАктуальности >= &РеальнаяДатаОповещения)";
				 
	Запрос.УстановитьПараметр("Пользователь",ПараметрыСеанса.Пользователь);
	Запрос.УстановитьПараметр("РеальнаяДатаОповещения",ТекущаяДата());
	Запрос.УстановитьПараметр("ПустаяДата",Дата("00010101"));
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		КоличествоНапоминаний = Результат.Выгрузить().Количество();
		ПоказыватьБаннер = обПраво("ПоказыватьНапоминаниеВВсплывающемОкне");
		Если КоличествоНапоминаний = 1 Тогда
			Пока Выборка.Следующий() Цикл
				Отбор = Новый Структура;
				Отбор.Вставить("Пользователь",Выборка.Пользователь);
				Отбор.Вставить("Автор",Выборка.Автор);
				Отбор.Вставить("Завершено",Выборка.Завершено);
				Отбор.Вставить("РеальнаяДатаОповещения",Выборка.РеальнаяДатаОповещения);
				Отбор.Вставить("Объект",Выборка.Объект);
				Ключ = РегистрыСведений.Напоминания.СоздатьКлючЗаписи(Отбор);
						
				Форма = РегистрыСведений.Напоминания.ПолучитьФорму("ФормаЗаписи" , ,Ключ);
				Если Не Форма.Открыта() Тогда
					Форма.Открыть();
				КонецЕсли;
				Если ПоказыватьБаннер Тогда
					РегистрыСведений.Напоминания.СоздатьНаборЗаписей().ПоказатьБаннер(Форма.РегистрСведенийМенеджерЗаписи);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Форма = РегистрыСведений.Напоминания.ПолучитьФорму("ФормаНабораЗаписей");
			Если ПоказыватьБаннер Тогда
				Пока Выборка.Следующий() Цикл
					РегистрСведенийМенеджерЗаписи = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
					РегистрСведенийМенеджерЗаписи.Пользователь = Выборка.Пользователь;
					РегистрСведенийМенеджерЗаписи.Автор = Выборка.Автор;
					РегистрСведенийМенеджерЗаписи.Завершено = Выборка.Завершено;
					РегистрСведенийМенеджерЗаписи.РеальнаяДатаОповещения = Выборка.РеальнаяДатаОповещения;
					РегистрСведенийМенеджерЗаписи.Объект = Выборка.Объект;
					РегистрСведенийМенеджерЗаписи.Прочитать();
					Если РегистрСведенийМенеджерЗаписи.Выбран() Тогда
						РегистрыСведений.Напоминания.СоздатьНаборЗаписей().ПоказатьБаннер(РегистрСведенийМенеджерЗаписи);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Выборка.Сбросить();
			Если Не Форма.Открыта() Тогда
				Форма.Выборка = Выборка;
				Форма.Открыть();
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // опОбработкаОповещений()

// Обработчик стандартной панели 
// Используется для создания напоминаний из форм списка, форм объектов
// и присоединением активного объекта к напоминанию
//
// Параметры:
//	Объект 		 - Ссылка		- ЛюбаяСсылка, ссылка на объект, который нужно присоединить к напоминанию
//	ДопПараметры - Структура	- Структура, которая может содержать поля из следующего списка (в любом сочетании):
//				Автор					Реквизит предназначен для хранения ссылки на автора напоминания - (текущего пользователя),
//										который создал напоминание
//				Завершено				Реквизит предназначен для хранения флага завершенности напоминания
//				РеальнаяДатаОповещения	Реквизит предназначен для хранения реальной даты когда будет выведено напоминание
//				Объект					Ссылка на объект конфигурации прилагаемый к напоминанию
//				ДатаАктуальности		Дата до которой напоминание считается актуальным. После этой даты даже не завершенные 
//										напоминания выводится не будут.
//				ДатаНачала				Дата оповещения как она заложена в напоминании без учета отсрочки напоминания
//										пользователем и предварительного вывода напоминания.
//				ДатаОповещения			Дата очередного напоминания, для непериодических напоминаний равняется дате начала
//										как она заложена в напоминании без учета отсрочки напоминания пользователем и
//										предварительного вывода напоминания.
//				Описание				Описание напоминания - сообщение содержащее суть напоминания.
//				Редактирование			Индикатор редактирования объекта. Предназначен для запрета открытия и вывода
//										напоминания во время редактирования
//				СрокДоНачала			Предназначен для хранения срока в секундах за который нужно предупредить о наступлении события
//				Тема					Заголовок окна, который будет выведен при отображении напоминания
//				ТипПериода				Строка хранящая тип периода напоминания (1 -8) символ –  тип периодичности дня,
//										(9  - 14) символ тип периодичности недели,  (15  - 16) символ тип периодичности месяца,
//										(17  - 18) символ тип периодичности года 
//				УдалитьПоИстеченииСрока	Признак удаления напоминания после истечения даты актуальности, или после завершения
//										(для не периодических напоминаний)
//				СписокПользователей		Тип - СписокЗначений - Содержит список пользователей-получателей напоминания
//
//Форма		 - Форма		- Форма объекта
//
Процедура опСоздатьОповещение(Объект=Неопределено, ДопПараметры=Неопределено, Форма=Неопределено) Экспорт
#Если Клиент тогда
	ЭтоНовыйОбъект = Ложь;
	Попытка ЭтоНовыйОбъект = Форма.ЭтотОбъект.ЭтоНовый(); Исключение КонецПопытки;
	Если ЭтоНовыйОбъект Тогда
		Ответ = Вопрос("Объект еще не записан, создание для него нового напоминания не возможно.
						|Записать объект?", РежимДиалогаВопрос.ДаНет);
		Отказ = Истина;
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Попытка Отказ = НЕ Форма.ЗаписатьВФорме(); Исключение КонецПопытки;
		КонецЕсли;
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Менеджер = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
	Если Объект <> Неопределено Тогда		
		Менеджер.Объект = Объект;
	КонецЕсли;
	Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
		МетаИзмерения = Метаданные.РегистрыСведений.Напоминания.Измерения;
		МетаРесурсы = Метаданные.РегистрыСведений.Напоминания.Ресурсы;
		Для Каждого Параметр Из ДопПараметры Цикл
			МетаОбъект = МетаИзмерения.Найти(Параметр.Ключ);
			Если (МетаОбъект<>Неопределено) тогда
				Менеджер[МетаОбъект.Имя] = Параметр.Значение;
			Иначе
				МетаОбъект = МетаРесурсы.Найти(Параметр.Ключ);
				Если (МетаОбъект<>Неопределено) тогда
					Менеджер[МетаОбъект.Имя] = Параметр.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если обЗначениеНеЗаполнено(Менеджер.ДатаНачала) Тогда
			Менеджер.ДатаНачала = ТекущаяДата();
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Менеджер.ДатаОповещения) Тогда
			Менеджер.ДатаОповещения = Менеджер.ДатаНачала;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Менеджер.РеальнаяДатаОповещения) Тогда
			Менеджер.РеальнаяДатаОповещения = Макс(ТекущаяДата(), Менеджер.ДатаОповещения - Менеджер.СрокДоНачала);
		КонецЕсли;
		 
		текФорма = Менеджер.ПолучитьФорму();
		// занесем список переданных пользователей
		ДопПараметры.Свойство("СписокПользователей", текФорма.Пользователи); 
		текФорма.Открыть();
	Иначе	
		текФорма = Менеджер.ПолучитьФорму();
		текФорма.Открыть();
	КонецЕсли;
#КонецЕсли
КонецПроцедуры // опСоздатьОповещение()

// Обработчик события из Всплывающего баннера.
// Обработка предопределенного события. Перенесена из модуля приложения.
//
// Параметры:
//	Источник	- ОписаниеТипов	- Источник	
//  Событие     - Событие		- Событие
//	Данные      - Строка		- Данные
//
Процедура опОбработкаВнешнегоСобытия(Источник, Событие, Данные)	Экспорт
#Если Клиент тогда
	// Ссылка Автор.
	Если Лев(Данные,1)="1" Тогда		
		ПоискИдентификатораБаннера = Данные;
		НачальныйНомер = Найти(ПоискИдентификатораБаннера, ")_");
		Пока НачальныйНомер>0 Цикл
			ПоискИдентификатораБаннера = Сред(ПоискИдентификатораБаннера, НачальныйНомер+1);
			НачальныйНомер = Найти(ПоискИдентификатораБаннера, ")_");
		КонецЦикла;
		ПользовательСсылка = Вычислить(Сред(СтрЗаменить(Данные,ПоискИдентификатораБаннера,""),2));
		Если НЕ обЗначениеНеЗаполнено(ПользовательСсылка) Тогда
			Форма = ПользовательСсылка.ПолучитьФорму("ФормаЭлемента");
			Если Форма.Открыта() Тогда
				Форма.Активизировать();
			Иначе
				Форма.Открыть();
			КонецЕсли;
		КонецЕсли;
	// Ссылка Объект.
	ИначеЕсли Лев(Данные,1)="2" Тогда
		Попытка
			ОбъектСсылка = ЗначениеИзСтрокиВнутр(Сред(Данные,2));
			Форма = ОбъектСсылка.ПолучитьФорму();
			Если Форма.Открыта() Тогда
				Форма.Активизировать();
			Иначе
				Форма.Открыть();
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
#КонецЕсли
КонецПроцедуры // опОбработкаВнешнегоСобытия()

// Процедура для открытия нужного оповещения
//
// Параметры:
//	Объект:			- Объект	- Объект для которого выбирается/создается напоминание
//	ДопПараметры	- Структура	- Структура, с параметрами нужного напоминания
//
Процедура опОткрытьОповещение(Объект=Неопределено, ДопПараметры=Неопределено) Экспорт
	Перем Значение;
	
	Менеджер = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
	Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
		МетаИзмерения = Метаданные.РегистрыСведений.Напоминания.Измерения;
		Для Каждого Измерение Из МетаИзмерения Цикл
			Если ДопПараметры.Свойство(Измерение.Имя, Значение) тогда
				Менеджер[Измерение.Имя] = Значение;
			КонецЕсли;
		КонецЦикла;
		Менеджер.Прочитать();
		Менеджер.ПолучитьФорму().Открыть();
	КонецЕсли;
КонецПроцедуры // опОткрытьОповещение()

// Процедура выбора и открытия нужного оповещения. 
// Или для создания нового, если выбран пункт меню "создать"
//
// Параметры:
//	Объект			- Объект	- Объект для которого выбирается/создается напоминание
//	ДопПараметры	- Структура	- Структура, для создания нового напоминания
//								  (см. описание реквизита в функции опСоздатьОповещение)
//	Форма			- Форма		- Форма объекта
//	Элемент			- Элемент	- Элемент формы, рядом с которым выводится меню выбора из списка
//
Процедура опВыборОповещения(Объект, ДопПараметры, Форма, Элемент) Экспорт
#Если Клиент тогда
	Перем СписокОповещений; // Список оповещений
	СписокОповещений = Новый СписокЗначений;
	Если НЕ Объект.Ссылка.Пустая() тогда
		Запрос = Новый Запрос;
		Запрос.Текст = " 
		|ВЫБРАТЬ
		|	Напоминания.Пользователь,
		|	Напоминания.Автор,
		|	Напоминания.Завершено,
		|	Напоминания.РеальнаяДатаОповещения КАК РеальнаяДатаОповещения,
		|	Напоминания.Объект,
		|	Напоминания.ДатаОповещения,
		|	Напоминания.Тема
		|ИЗ
		|	РегистрСведений.Напоминания КАК Напоминания
		|ГДЕ
		|	Напоминания.Объект = &Объект
		|	И (НЕ Напоминания.Завершено)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РеальнаяДатаОповещения
		|";
		Запрос.УстановитьПараметр("Объект", Объект);
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		Для каждого стр Из ТаблицаРезультата Цикл
			текОповещение = Новый Структура;
			Для поле=0 по ТаблицаРезультата.Колонки.Количество()-1 Цикл
				текОповещение.Вставить(ТаблицаРезультата.Колонки[поле].Имя,Стр[поле]);
			КонецЦикла; 
			текДата = Формат(текОповещение.РеальнаяДатаОповещения, "ДЛФ=D");
			текЗаголовок = текДата+" "+СокрЛП(текОповещение.Тема);
			Если НЕ обЗначениеНЕЗаполнено(текОповещение.Пользователь) Тогда
				текЗаголовок = текЗаголовок + " (Получатель: " + текОповещение.Пользователь.Наименование + ")";
			КонецЕсли;
			СписокОповещений.Добавить(текОповещение, текЗаголовок,,БиблиотекаКартинок.ОповещениеИзменить);
		КонецЦикла;
	КонецЕсли;
	
	СписокОповещений.Добавить(Неопределено, "Создать новое напоминание",,БиблиотекаКартинок.Оповещение);
	
	текДействие = "Создать";
	текДанные = ДопПараметры;
	
	Если СписокОповещений.Количество() > 1 Тогда // иначе нужно создать новое
		РезВыбора = Форма.ВыбратьИзМеню(СписокОповещений, Элемент);
		Если РезВыбора = Неопределено Тогда
			возврат;
		ИначеЕсли РезВыбора.Значение <> Неопределено тогда // выбран НЕ пункт "создать"
			текДействие = "Изменить";
			текДанные = РезВыбора.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если текДействие = "Создать" Тогда		
		Если ДопПараметры.Свойство("СписокПользователей") Тогда
			Если Вопрос("Создать напоминание для всех пользователей?",РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
				текДанные.Удалить("СписокПользователей");
			КонецЕсли;
		КонецЕсли;
		опСоздатьОповещение(Объект, текДанные, Форма);
	ИначеЕсли текДействие = "Изменить" Тогда
		опОткрытьОповещение(Объект, текДанные);
	КонецЕсли;
#КонецЕсли
КонецПроцедуры // опВыборОповещения()

// Процедура удаляет завершенные и просроченные напоминания.
//
// Параметры:
//	Условие	- Строка	- Условие удаления
//
Процедура опОчисткаНапоминаний(Условие)	Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Напоминания.Пользователь,
	               |	Напоминания.Автор,
	               |	Напоминания.Завершено,
	               |	Напоминания.РеальнаяДатаОповещения,
	               |	Напоминания.Объект
	               |ИЗ
	               |	РегистрСведений.Напоминания КАК Напоминания
	               |ГДЕ
	               |	(Напоминания.Завершено = ИСТИНА
	               |			ИЛИ Напоминания.ДатаАктуальности < &РеальнаяДатаОповещения
	               |				И Напоминания.ДатаАктуальности <> &ПустаяДата)";
	Если Условие = "Только свои" Тогда
		Запрос.Текст = Запрос.Текст +"
				   |	И Напоминания.Автор = &Автор";
		Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.Пользователь);
	ИначеЕсли ТипЗнч(Условие) = Тип("СправочникСсылка.Организации") Тогда
		Запрос.Текст = Запрос.Текст +"
				   |	И Напоминания.Пользователь.Организация = &Организация";
		Запрос.УстановитьПараметр("Организация", Условие);
	ИначеЕсли ТипЗнч(Условие) = Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		Запрос.Текст = Запрос.Текст +"
				   |	И Напоминания.Пользователь.Подразделение = &Подразделение";
		Запрос.УстановитьПараметр("Подразделение", Условие);
	ИначеЕсли ТипЗнч(Условие) = Тип("СправочникСсылка.Пользователи") Тогда
		Запрос.Текст = Запрос.Текст +"
				   |	И Напоминания.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", Условие);
	КонецЕсли;
	Запрос.УстановитьПараметр("РеальнаяДатаОповещения",ТекущаяДата());
	Запрос.УстановитьПараметр("ПустаяДата",Дата("00010101"));
	Выборка = Запрос.Выполнить().Выбрать();
	РегистрСведенийМенеджерЗаписи = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
	Пока Выборка.Следующий() Цикл
		РегистрСведенийМенеджерЗаписи.Пользователь = Выборка.Пользователь;
		РегистрСведенийМенеджерЗаписи.Автор = Выборка.Автор;
		РегистрСведенийМенеджерЗаписи.Завершено = Выборка.Завершено;
		РегистрСведенийМенеджерЗаписи.РеальнаяДатаОповещения = Выборка.РеальнаяДатаОповещения;
		РегистрСведенийМенеджерЗаписи.Объект = Выборка.Объект;
		РегистрСведенийМенеджерЗаписи.Прочитать();
		Если РегистрСведенийМенеджерЗаписи.Выбран() Тогда
			РегистрСведенийМенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // опОчисткаНапоминаний()

// возвращает строку представления объекта
//
// Параметры:
//	Объект	-  ЛюбаяСсылка	- Объект
//
Функция опСформироватьПредставление(Объект) Экспорт
	текЗаголовок = "Создать напоминание";
	Если НЕ Объект.Ссылка.Пустая() тогда
		Запрос = Новый Запрос;
		Запрос.Текст = " 
		|ВЫБРАТЬ
		|	Напоминания.Пользователь,
		|	Напоминания.Автор,
		|	Напоминания.Завершено,
		|	Напоминания.РеальнаяДатаОповещения КАК РеальнаяДатаОповещения,
		|	Напоминания.Объект,
		|	Напоминания.ДатаОповещения,
		|	Напоминания.Тема
		|ИЗ
		|	РегистрСведений.Напоминания КАК Напоминания
		|ГДЕ
		|	Напоминания.Объект = &Объект
		|	И (НЕ Напоминания.Завершено)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РеальнаяДатаОповещения
		|";
		Запрос.УстановитьПараметр("Объект", Объект);
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		Если ТаблицаРезультата.Количество() > 0 Тогда
			текДата = Формат(ТаблицаРезультата[0].РеальнаяДатаОповещения, "ДЛФ=D");
			текЗаголовок = текДата+" "+СокрЛП(ТаблицаРезультата[0].Тема);
			Если ТаблицаРезультата.Количество() >= 2 Тогда
				текЗаголовок = текЗаголовок + " (всего "+Строка(ТаблицаРезультата.Количество())+")";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат текЗаголовок;
КонецФункции // опСформироватьПредставление()

// Функция используется для поиска следующей актуальной даты напоминания
// Возвращает ложь если следующей даты найти не удалось (например напоминание не периодическое)
//
// Параметры:
//	ЭтаЗапись  	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата 	  	– Дата							- Начальная дата
//  НоваяДата 	- Дата							- Найденная дата
//
// Возвращаемое значение:
//  Булево	- Истина, если удалось перейти, иначе Ложь
//
Функция опПерейтиНаСледующуюДату(ЭтаЗапись, Знач Дата, НоваяДата) Экспорт
	Рез = ПерейтиНаСледующуюДату(ЭтаЗапись, Дата, НоваяДата);
	Пока Рез и НоваяДата<ТекущаяДата() Цикл
		Рез = ПерейтиНаСледующуюДату(ЭтаЗапись, Дата, НоваяДата);
		Дата = НоваяДата;
	КонецЦикла;
	Возврат Рез;
КонецФункции // опПерейтиНаСледующуюДату()

// Функция используется для поиска ближайшей даты напоминания
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Начальная дата
//  НоваяДата 	- Дата							- Найденная дата
//
// Возвращаемое значение:
//	Булево	- Истина, если удалось найти, иначе Ложь
//
Функция опНайтиБлижайшуюДату(ЭтаЗапись, Дата)  Экспорт
	НомерЦикла=0;
	НоваяДата = Дата;
	Пока Истина и НомерЦикла<500 Цикл
		НомерЦикла = НомерЦикла + 1;
	    Если Не ПроверитьДень(ЭтаЗапись,Дата) Тогда
			Если ПерейтиНаСледующийДень(ЭтаЗапись,Дата,НоваяДата) 
				или ПерейтиНаСледующуюНеделю(ЭтаЗапись,Дата,НоваяДата)
				или ПерейтиНаСледующийМесяц(ЭтаЗапись,Дата,НоваяДата) 
				или ПерейтиНаСледующийГод(ЭтаЗапись,Дата,НоваяДата) Тогда			
				   	Дата = НоваяДата;
					Продолжить;
			Иначе
				 Возврат ложь;
			КонецЕсли; 
		 ИначеЕсли Не ПроверитьНеделю(ЭтаЗапись,Дата) Тогда
					Если ПерейтиНаСледующуюНеделю(ЭтаЗапись,Дата,НоваяДата)
						или ПерейтиНаСледующийМесяц(ЭтаЗапись,Дата,НоваяДата) 
						или ПерейтиНаСледующийГод(ЭтаЗапись,Дата,НоваяДата) Тогда			
						   	Дата = НоваяДата;
							Продолжить;
					Иначе
					 	Возврат ложь;
					КонецЕсли; 
		 ИначеЕсли Не ПроверитьМесяц(ЭтаЗапись,Дата) Тогда
					Если ПерейтиНаСледующийМесяц(ЭтаЗапись,Дата,НоваяДата) 
						или ПерейтиНаСледующийГод(ЭтаЗапись,Дата,НоваяДата) Тогда			
					   		Дата = НоваяДата;
							Продолжить;
					Иначе
					 	Возврат ложь;
					КонецЕсли; 
		 ИначеЕсли Не ПроверитьГод(ЭтаЗапись,Дата) Тогда
					Если ПерейтиНаСледующийГод(ЭтаЗапись,Дата,НоваяДата) Тогда			
					   		Дата = НоваяДата;
							Продолжить;
					Иначе
					 	Возврат ложь;
					КонецЕсли; 
		Иначе
             Прервать;
		 КонецЕсли; 	
		 
	 КонецЦикла; 
	Если НомерЦикла=500 Тогда
	 	Возврат ложь;
	КонецЕсли; 
	Возврат Истина;
КонецФункции // опНайтиБлижайшуюДату()

// Функция используется для поиска следующей даты напоминания
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Начальная дата
//  НоваяДата 	- Дата							- Найденная дата
//
// Возвращаемое значение:
//	Булево	- Истина, если удалось найти, иначе Ложь
//
Функция ПерейтиНаСледующуюДату(ЭтаЗапись, Знач Дата, НоваяДата) Экспорт	
	Если Сред(ЭтаЗапись.ТипПериода,1,1) = "0" Тогда		
	    	Период = Число(Сред(ЭтаЗапись.ТипПериода,3)+ " ");
			Если Период > 0 Тогда
			 	НоваяДата = Дата+Период;
				Возврат Истина;	
			Иначе
				Возврат Ложь;
			КонецЕсли; 
	КонецЕсли; 
	БылПереход = Истина;	
	Если НЕ ПерейтиНаСледующийДень(ЭтаЗапись,Дата,НоваяДата) или КонецНедели(Дата)<>КонецНедели(НоваяДата) Тогда
		Если НЕ ПерейтиНаСледующуюНеделю(ЭтаЗапись,Дата,НоваяДата) или Месяц(Дата)<>Месяц(НоваяДата) Тогда
			Если НЕ ПерейтиНаСледующийМесяц(ЭтаЗапись,Дата,НоваяДата) или Год(Дата)<>Год(НоваяДата) Тогда	
					БылПереход = ПерейтиНаСледующийГод(ЭтаЗапись,Дата,НоваяДата) или Год(Дата);					
		    КонецЕсли; 
	    КонецЕсли; 
	КонецЕсли; 	
	Если БылПереход и опНайтиБлижайшуюДату(ЭтаЗапись,НоваяДата) Тогда	
		Если (ЭтаЗапись.ДатаАктуальности=Дата("00010101")) или (НоваяДата<ЭтаЗапись.ДатаАктуальности) Тогда
			Возврат Истина;
		Иначе  
			Возврат Ложь;	
		КонецЕсли; 	
	Иначе  
		Возврат Ложь;	
	КонецЕсли; 
КонецФункции // ПерейтиНаСледующуюДату()

// Функция используется для поиска следующей даты напоминания с шагом в день
//
// Параметры
// ЗтаЗапись  - РегистрСведенийМенеджерЗаписи, менеджер записи регистра Напоминания
//  Дата  	  – Дата, начальная дата
//  НоваяДата - Дата, найденная дата
//
// Возвращаемое значение:
//  Истина - удалось перейти
//  Ложь - не удалось перейти
//
Функция ПерейтиНаСледующийДень(ЭтаЗапись, Дата,НоваяДата) 
	УстановленДень = Число(Сред(ЭтаЗапись.ТипПериода,1,1)+" ");	
	НоваяДата = Дата;	
	Если УстановленДень=2 или УстановленДень=7 Тогда    	
		НоваяДата = НоваяДата + 86400;		
	ИначеЕсли УстановленДень=1 Тогда
		НоваяДата = НоваяДата + 86400;	
		Пока Сред(ЭтаЗапись.ТипПериода,1+ДеньНедели(НоваяДата),1)<>"1" Цикл
			НоваяДата = НоваяДата+86400;	
		КонецЦикла;  
	ИначеЕсли УстановленДень = 3 Тогда
		День = Число(Сред(ЭтаЗапись.ТипПериода,2,2)+" ");	
		ТекДата = НоваяДата + 86400;
		Если День(ТекДата)>День Тогда
			ТекДата = ДобавитьМесяц(ТекДата,1);
		КонецЕсли; 
		Попытка
			НовДата = Дата(Год(ТекДата), Месяц(ТекДата), День, Час(ТекДата), Минута(ТекДата), Секунда(ТекДата)); 
		Исключение
		КонецПопытки;		
		НомерЦикла = 1;
		Пока ТипЗнч(НовДата)<>Тип("Дата") и НомерЦикла<120 Цикл
			ТекДата = ДобавитьМесяц(ТекДата,1);
			НомерЦикла = НомерЦикла + 1;
			Попытка
				НовДата = Дата(Год(ТекДата), Месяц(ТекДата), День, Час(ТекДата), Минута(ТекДата), Секунда(ТекДата)); 
			Исключение
			КонецПопытки;
		КонецЦикла;		
		Если НомерЦикла = 120 Тогда
			Возврат Ложь;
		КонецЕсли;
		НоваяДата = НовДата;
	ИначеЕсли УстановленДень = 4 Тогда    		
		День = Число(Сред(ЭтаЗапись.ТипПериода,2,2)+" ");	
		ТекДата = НоваяДата + 86400;
		ДнейВМесяце = (КонецМесяца(ТекДата)-НачалоМесяца(ТекДата)+1)/86400;
		Если День(ТекДата)>ДнейВМесяце-День + 1 Тогда
			ТекДата = ДобавитьМесяц(ТекДата,1);
			ДнейВМесяце = (КонецМесяца(ТекДата)-НачалоМесяца(ТекДата)+1)/86400;			
		КонецЕсли; 
		Попытка
			НовДата = Дата(Год(ТекДата), Месяц(ТекДата), ДнейВМесяце-День + 1, Час(ТекДата), Минута(ТекДата), Секунда(ТекДата)); 
		Исключение
		КонецПопытки;
		НомерЦикла = 1;
		Пока ТипЗнч(НовДата)<>Тип("Дата") и НомерЦикла<120 Цикл
			ТекДата = ДобавитьМесяц(ТекДата,1);
			ДнейВМесяце = (КонецМесяца(ТекДата)-НачалоМесяца(ТекДата)+1)/86400;			
			НомерЦикла = НомерЦикла + 1;
			Попытка
				НовДата = Дата(Год(ТекДата), Месяц(ТекДата), ДнейВМесяце-День + 1, Час(ТекДата), Минута(ТекДата), Секунда(ТекДата)); 				
			Исключение
			КонецПопытки;
		КонецЦикла;
		Если НомерЦикла = 120 Тогда
			Возврат Ложь;
		КонецЕсли;
		НоваяДата = НовДата;
	ИначеЕсли УстановленДень = 5 Тогда    //четный
			НоваяДата = НоваяДата + 86400;
			Пока Цел(День(НоваяДата)%2)<>0 Цикл
				НоваяДата = НоваяДата + 86400;			
			КонецЦикла; 	
	ИначеЕсли УстановленДень = 6 Тогда    //не четный
			НоваяДата = НоваяДата + 86400;
			Пока Цел(День(НоваяДата)%2)<>1 Цикл
				НоваяДата = НоваяДата + 86400;			
			КонецЦикла;
	Иначе Возврат Ложь;
	КонецЕсли; 	
	Возврат Истина;	
КонецФункции  // ПерейтиНаСледующийДень()

// Функция используется для поиска следующей даты напоминания с шагом в неделю
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Начальная дата
//  НоваяДата 	- Дата							- Найденная дата
//
// Возвращаемое значение:
//	Булево	- Истина, если удалось найти, иначе Ложь
//
Функция ПерейтиНаСледующуюНеделю(ЭтаЗапись, Дата,НоваяДата)
	УстановленаНеделя = Число(Сред(ЭтаЗапись.ТипПериода,9,1)+" ");	
	НоваяДата = НачалоНедели(Дата);
	Если УстановленаНеделя = 3 Тогда
		НоваяДата = НоваяДата + 604800;		
		НомерНедели = Цел((НоваяДата-НачалоНедели(НачалоМесяца(НоваяДата)))/604800) + 1;			
		Пока Цел(НомерНедели%2) <> 0 Цикл
			НоваяДата = НоваяДата + 604800;
			НомерНедели = Цел((НоваяДата-НачалоНедели(НачалоМесяца(НоваяДата)))/604800) + 1;	
		КонецЦикла;
	ИначеЕсли УстановленаНеделя = 4 Тогда
		НоваяДата = НоваяДата + 604800;
		НомерНедели = Цел((НоваяДата-НачалоНедели(НачалоМесяца(НоваяДата)))/604800) + 1;
		Пока Цел(НомерНедели%2) <>1 Цикл
			НоваяДата = НоваяДата + 604800;
			НомерНедели = Цел((НоваяДата-НачалоНедели(НачалоМесяца(НоваяДата)))/604800) + 1;	
		КонецЦикла;
	ИначеЕсли УстановленаНеделя = 5 Тогда   //Номера  недель месяца (10,14)
		НоваяДата = НоваяДата + 604800;
		НомерНедели = Цел((НоваяДата-НачалоНедели(НачалоМесяца(НоваяДата)))/604800)+1;		
		Пока Не ((НомерНедели<5) и (Сред(ЭтаЗапись.ТипПериода,9+НомерНедели,1)="1"))
			и НЕ (Сред(ЭтаЗапись.ТипПериода,14,1)="1" и (КонецМесяца(НоваяДата) <= КонецНедели(НоваяДата))) Цикл
			НоваяДата = НоваяДата + 604800;
			НомерНедели = Цел((НоваяДата-НачалоНедели(НачалоМесяца(НоваяДата)))/604800) + 1;
		КонецЦикла;	
	ИначеЕсли УстановленаНеделя = 6 Тогда  //Все  недели
		НоваяДата = НоваяДата + 604800;	
	ИначеЕсли УстановленаНеделя = 7 Тогда  //кол-во недель		
		КолвоНедель = Число(Сред(ЭтаЗапись.ТипПериода,10,3)+" ");				
		НоваяДата = НоваяДата + 604800;
		Пока (НачалоНедели(НоваяДата) - НачалоНедели(ЭтаЗапись.ДатаНачала))%(604800*КолвоНедель) <> 0 Цикл
			НоваяДата = НоваяДата + 604800;		
		КонецЦикла;			
	Иначе		
		Возврат Ложь;	
	КонецЕсли; 			
	НоваяДата = Дата(Год(НоваяДата), Месяц(НоваяДата), День(НоваяДата), Час(Дата), Минута(Дата), Секунда(Дата)); 
	Возврат	Истина;	
КонецФункции // ПерейтиНаСледующуюНеделю()

// Функция используется для поиска следующей даты напоминания с шагом в месяц
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Начальная дата
//  НоваяДата 	- Дата							- Найденная дата
//
// Возвращаемое значение:
//	Булево	- Истина, если удалось найти, иначе Ложь
//
Функция ПерейтиНаСледующийМесяц(ЭтаЗапись, Дата,НоваяДата) 
	УстановленМесяц = Число(Сред(ЭтаЗапись.ТипПериода,15,2)+" ");
	Если УстановленМесяц > 0  Тогда
		Месяц = УстановленМесяц;
		Если Месяц<=Месяц(Дата) Тогда
			Год = Год(Дата) + 1;			
		Иначе 
			Год = Год(Дата);
		КонецЕсли;		
	ИначеЕсли  УстановленМесяц = 0 и Число(Сред(ЭтаЗапись.ТипПериода,1,1)+" ")<>8 Тогда
		Месяц = Месяц(Дата)+1;   
		Если Месяц>12 Тогда
			Год = Год(Дата) + 1;			
			Месяц = Месяц - 12;
		Иначе 
			Год = Год(Дата);
		КонецЕсли;   		
	Иначе 
		Возврат	Ложь;
	КонецЕсли; 	
	
	НоваяДата = Дата(Год, Месяц, 1, Час(Дата), Минута(Дата), Секунда(Дата)); 	
	
	Возврат	Истина;	
КонецФункции // ПерейтиНаСледующийМесяц()

// Функция используется для поиска следующей даты напоминания с шагом в год
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Начальная дата
//  НоваяДата 	- Дата							- Найденная дата
//
// Возвращаемое значение:
//	Булево	- Истина, если удалось найти, иначе Ложь
//
Функция ПерейтиНаСледующийГод(ЭтаЗапись, Дата,НоваяДата) 
	КолвоЛет = Число(Сред(ЭтаЗапись.ТипПериода,17)+" ");
	Если КолвоЛет<>0 Тогда
		Год=Год(Дата)+КолвоЛет;		
		Попытка
			НовДата = Дата(Год, Месяц(Дата), День(Дата), Час(Дата), Минута(Дата), Секунда(Дата)); 
		Исключение
		КонецПопытки; 	
		НомерЦикла = 1;
		Пока ТипЗнч(НовДата)<>Тип("Дата") Цикл		
			Попытка
			НовДата = Дата(Год, Месяц(Дата), День(Дата)-НомерЦикла, Час(Дата), Минута(Дата), Секунда(Дата)); 
			Исключение			
			КонецПопытки;
			НомерЦикла = НомерЦикла + 1;
		КонецЦикла;
		НоваяДата = НовДата;		
	Иначе   			
		Год=Год(Дата)+1;
		НоваяДата = Дата(Год, 1, 1, Час(Дата), Минута(Дата), Секунда(Дата)); 			
	КонецЕсли;
	Возврат	Истина;
КонецФункции // ПерейтиНаСледующийГод()

// Функция проверяет день передаваемой даты на соответствие заданному периоду напоминания
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Проверяемая дата
//
// Возвращаемое значение:
//	Булево	- Истина, если соответствует периоду, иначе Ложь
//
Функция ПроверитьДень(ЭтаЗапись, Дата) 
	УстановленДень = Число(Сред(ЭтаЗапись.ТипПериода,1,1)+" ");	
	Если УстановленДень = 2 Тогда    	
		Возврат Истина;
	ИначеЕсли УстановленДень=1 Тогда		
		Возврат Сред(ЭтаЗапись.ТипПериода,1+ДеньНедели(Дата),1)="1"
	ИначеЕсли УстановленДень=3 Тогда
		Возврат День(Дата) = Число(Сред(ЭтаЗапись.ТипПериода,2,2)+" ");			
	ИначеЕсли УстановленДень = 4 Тогда    				
		ДнейВМесяце = (КонецМесяца(Дата)-НачалоМесяца(Дата)+1)/86400;
		Возврат День(Дата)=ДнейВМесяце + 1 - Число(Сред(ЭтаЗапись.ТипПериода,2,2)+" ");
	ИначеЕсли УстановленДень = 5 Тогда    //четный
			Возврат Цел(День(Дата)%2)= 0;						
	ИначеЕсли УстановленДень = 6 Тогда    //не четный
			Возврат Цел(День(Дата)%2)= 1;						
	Иначе Возврат Истина;
	КонецЕсли; 	
КонецФункции // ПроверитьДень()

// Функция проверяет неделю передаваемой даты на соответствие заданному периоду напоминания
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Проверяемая дата
//
// Возвращаемое значение:
//	Булево	- Истина, если соответствует периоду, иначе Ложь
//
Функция ПроверитьНеделю(ЭтаЗапись, Дата)
	УстановленаНеделя = Число(Сред(ЭтаЗапись.ТипПериода,9,1)+" ");			
	Если УстановленаНеделя = 3 Тогда				
		Возврат Цел((Цел((Дата-НачалоНедели(НачалоМесяца(Дата)))/604800) + 1)%2) = 0;
	ИначеЕсли УстановленаНеделя = 4 Тогда
		Возврат Цел((Цел((Дата-НачалоНедели(НачалоМесяца(Дата)))/604800) + 1)%2) = 1;
	ИначеЕсли УстановленаНеделя = 5 Тогда   //Номера  недель месяца (10,14)
		НомерНедели = Цел((Дата-НачалоНедели(НачалоМесяца(Дата)))/604800)+1;
		Возврат ((НомерНедели<5) и (Сред(ЭтаЗапись.ТипПериода,9+НомерНедели,1)="1"))
			или  (Сред(ЭтаЗапись.ТипПериода,14,1)="1" и (КонецМесяца(Дата)<=КонецНедели(Дата)));
	ИначеЕсли УстановленаНеделя = 7 Тогда  //кол-во недель		
		КолвоНедель = Число(Сред(ЭтаЗапись.ТипПериода,10,3)+" ");
		Если КолвоНедель<>0 Тогда			
			Возврат (НачалоНедели(Дата) - НачалоНедели(ЭтаЗапись.ДатаНачала))%(604800*КолвоНедель) = 0;
		Иначе        
	 		Возврат	Истина;
		КонецЕсли;		
	Иначе
		Возврат	Истина;
	КонецЕсли; 			
КонецФункции // ПроверитьНеделю()

// Функция проверяет месяц передаваемой даты на соответствие заданному периоду напоминания
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Проверяемая дата
//
// Возвращаемое значение:
//	Булево	- Истина, если соответствует периоду, иначе Ложь
//
Функция ПроверитьМесяц(ЭтаЗапись, Дата) 
	УстановленМесяц = Число(Сред(ЭтаЗапись.ТипПериода,15,2)+" ");
	Если УстановленМесяц > 0  Тогда
		Возврат (Месяц(Дата) = УстановленМесяц);
	Иначе
		Возврат	Истина;	
	КонецЕсли; 	
КонецФункции // ПроверитьМесяц()

// Функция проверяет год передаваемой даты на соответствие заданному периоду напоминания
//
// Параметры:
//	ЭтаЗапись	- РегистрСведенийМенеджерЗаписи	- Менеджер записи регистра Напоминания
//  Дата	  	– Дата							- Проверяемая дата
//
// Возвращаемое значение:
//	Булево	- Истина, если соответствует периоду, иначе Ложь
//
Функция ПроверитьГод(ЭтаЗапись, Дата)
	КолвоЛет = Число(Сред(ЭтаЗапись.ТипПериода,17)+" ");
	Если КолвоЛет = 0 Тогда
		Возврат	Истина;
	Иначе  		
		Возврат (Год(Дата) - Год(ЭтаЗапись.ДатаНачала))%(КолвоЛет) = 0 ;
	КонецЕсли;
КонецФункции // ПроверитьГод()

// Область Общее

Процедура ЗаполнитьЗначенияСвойствXDTO(Объявление, Данные, Колонки) Экспорт
	
	УстановитьБезопасныйРежим(Истина);

	Для каждого ИмяРазделителя Из ns_ОбщегоНазначенияСервер.РазделителиКонфигурации() Цикл
	    УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
	КонецЦикла;

	Для каждого ТекущаяКолонка Из Колонки Цикл
		Если ЗначениеЗаполнено(Данные[ТекущаяКолонка.Имя]) И НЕ ЭтоИсключаемоеПоле(ТекущаяКолонка.Имя) Тогда
			Попытка
				Выполнить("Добавить_" + ТекущаяКолонка.Имя + "(Объявление, Данные[ТекущаяКолонка.Имя])");
			Исключение
				Сообщить("Колонка " + ТекущаяКолонка.Имя + "   " + ОписаниеОшибки());
			КонецПопытки; 
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

Функция ПолучитьURIПространстваИмен() Экспорт
	Возврат "https://nizamov.studio/avito";
КонецФункции

Функция ЭтоИсключаемоеПоле(ИмяПоля)
	Если ИмяПоля = "Владелец" ИЛИ 
		ИмяПоля = "НаименованиеПолное" ИЛИ 
		ИмяПоля = "Скидка" ИЛИ 
		ИмяПоля = "Количество" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли; 
КонецФункции

Функция ПолучитьТаблицуДляВыгрузки() Экспорт
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Номенклатура");
	Таблица.Колонки.Добавить("Title");
	Таблица.Колонки.Добавить("AdStatus");
	Таблица.Колонки.Добавить("AdType");
	Таблица.Колонки.Добавить("AllowEmail");
	Таблица.Колонки.Добавить("AvitoId");
	Таблица.Колонки.Добавить("Category");
	Таблица.Колонки.Добавить("DateBegin");
	Таблица.Колонки.Добавить("DateEnd");
	Таблица.Колонки.Добавить("Id");
	Таблица.Колонки.Добавить("ListingFee");
	Таблица.Колонки.Добавить("nsDescription");
	Таблица.Колонки.Добавить("RimBolts");
	Таблица.Колонки.Добавить("RimBoltsDiameter");
	Таблица.Колонки.Добавить("RimDiameter");
	Таблица.Колонки.Добавить("RimOffset");
	Таблица.Колонки.Добавить("RimType");
	Таблица.Колонки.Добавить("RimWidth");
	Таблица.Колонки.Добавить("TireAspectRatio");
	Таблица.Колонки.Добавить("TireSectionWidth");
	Таблица.Колонки.Добавить("TireType");
	Таблица.Колонки.Добавить("TypeId");
	Таблица.Колонки.Добавить("VideoURL");
	Таблица.Колонки.Добавить("WheelAxle");
	Таблица.Колонки.Добавить("Brand");
	Таблица.Колонки.Добавить("OEM");
	Таблица.Колонки.Добавить("Шаблон");
	Таблица.Колонки.Добавить("Price");
	Таблица.Колонки.Добавить("ContactPhone");
	Таблица.Колонки.Добавить("ManagerName");
	Таблица.Колонки.Добавить("Address");
	Таблица.Колонки.Добавить("Артикул");
	Таблица.Колонки.Добавить("Наименование");
	Таблица.Колонки.Добавить("Характеристика");
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Condition");
	Таблица.Колонки.Добавить("GoodsType");
	Таблица.Колонки.Добавить("ТекстОбъявления");
	Таблица.Колонки.Добавить("GoodsSubType");
	Таблица.Колонки.Добавить("Availability");
	Таблица.Колонки.Добавить("VehicleType");
	Таблица.Колонки.Добавить("MotoType");
	Таблица.Колонки.Добавить("ProductsType");
	Таблица.Колонки.Добавить("Homologation");
	Таблица.Колонки.Добавить("LoadIndex");
	Таблица.Колонки.Добавить("Model");
	Таблица.Колонки.Добавить("OriginalOEM");
	Таблица.Колонки.Добавить("OriginalVendor");
	Таблица.Колонки.Добавить("ResidualTread");
	Таблица.Колонки.Добавить("RunFlat");
	Таблица.Колонки.Добавить("SpeedIndex");
	Таблица.Колонки.Добавить("ЗонаПоказаОбъявления");
	Таблица.Колонки.Добавить("Apparel");
	Таблица.Колонки.Добавить("ApparelType");
	Таблица.Колонки.Добавить("Size");
	Таблица.Колонки.Добавить("TiresModels");
	Таблица.Колонки.Добавить("Quantity");
	
	Возврат Таблица;
КонецФункции

// КонецОбласти 

// Область ОбщиеТеги

Процедура Добавить_AdStatus(Объявление, Данные)
	СтрДанные = XMLстрока(Данные);
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "AdStatus");
	Объявление.AdStatus = ФабрикаXDTO.Создать(Тип, СтрДанные);
КонецПроцедуры

Процедура Добавить_AllowEmail(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "AllowEmail");
	Объявление.AllowEmail = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_AvitoId(Объявление, Данные)
	Объявление.AvitoId = Формат(Данные, "ЧРГ=; ЧГ=0");
КонецПроцедуры

Процедура Добавить_Category(Объявление, Данные)
	Category = Строка(Данные);
	Разделитель = Найти(Category, "/");
	Если Разделитель > 0 Тогда
		Category = Лев(Category, Разделитель - 1); 
	КонецЕсли;
	Объявление.Category = Category;
КонецПроцедуры

Процедура Добавить_ContactPhone(Объявление, Данные)
	Объявление.ContactPhone = Строка(Данные);
КонецПроцедуры

Процедура Добавить_DateBegin(Объявление, Данные)
	Объявление.DateBegin = Данные;
КонецПроцедуры

Процедура Добавить_DateEnd(Объявление, Данные)
	Объявление.DateEnd = Данные;
КонецПроцедуры

Процедура Добавить_ТекстОбъявления(Объявление, Данные)
	Объявление.Description = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Id(Объявление, Данные)
	Объявление.Id = Строка(Данные);
КонецПроцедуры

Процедура Добавить_ListingFee(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "ListingFee");
	Объявление.ListingFee = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_Price(Объявление, Данные)
	Объявление.Price = Данные;
КонецПроцедуры

Процедура Добавить_VideoURL(Объявление, Данные)
	Объявление.VideoURL = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Title(Объявление, Данные)
	Объявление.Title = Строка(Данные);
КонецПроцедуры

Процедура Добавить_ManagerName(Объявление, Данные)
	Объявление.ManagerName = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Address(Объявление, Данные)
	Объявление.Address = Лев(Строка(Данные), 256);
КонецПроцедуры

Процедура Добавить_AdType(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "AdType");
	Объявление.AdType = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_Condition(Объявление, Данные)
	Объявление.Condition = Строка(Данные);
КонецПроцедуры

// КонецОбласти 

// Область ЧастыеТеги

Процедура Добавить_GoodsType(Объявление, Данные)
	Объявление.GoodsType = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Images(Объявление, Данные) Экспорт
	ТипImage = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "Image");
	ТипТЧImages = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "Ad.Images");
	
	ИзображенияТЧ = ФабрикаXDTO.Создать(ТипТЧImages);
	Для каждого Строка Из Данные Цикл
		Изображение = ФабрикаXDTO.Создать(ТипImage);
		Если Строка.Url Тогда
			Изображение.url = Строка.UrlФайла;
		Иначе	
			Изображение.name = Строка.ИмяФайла;
		КонецЕсли; 
		ИзображенияТЧ.Image.Добавить(Изображение);
	КонецЦикла; 
	Объявление.Images.Добавить(ИзображенияТЧ);
КонецПроцедуры

Процедура Добавить_GoodsSubType(Объявление, Данные)
	Объявление.GoodsSubType = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Availability(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "Availability");
	Объявление.Availability = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_DisplayAreas(Объявление, ЗонаПоказа) Экспорт
	ТипТЧDisplayAreas = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "Ad.DisplayAreas");
	
	DisplayAreas = ФабрикаXDTO.Создать(ТипТЧDisplayAreas);
	Для каждого Строка Из ЗонаПоказа.Список Цикл
		DisplayAreas.Area.Добавить(Строка.ЗонаПоказа.Наименование);
	КонецЦикла; 
	Объявление.DisplayAreas.Добавить(DisplayAreas);
КонецПроцедуры

// КонецОбласти 

// Область Заглушки

Процедура Добавить_Номенклатура(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_Характеристика(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_Объявление(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_Артикул(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_Наименование(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_Ссылка(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_Шаблон(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_nsDescription(Объявление, Данные)
	// Заглушка
КонецПроцедуры

Процедура Добавить_ЗонаПоказаОбъявления(Объявление, Данные)
	// Заглушка
КонецПроцедуры

// КонецОбласти 

// Область ЗапчастиИАксессуары

Процедура Добавить_OEM(Объявление, Данные)
	Объявление.OEM = Лев(Строка(Данные), 50);
КонецПроцедуры

Процедура Добавить_TypeId(Объявление, Данные)
	Объявление.TypeId = Строка(Данные.Код);
КонецПроцедуры

Процедура Добавить_Homologation(Объявление, Данные)
	Объявление.Homologation = Строка(Данные);
КонецПроцедуры

Процедура Добавить_LoadIndex(Объявление, Данные)
	Объявление.LoadIndex = Данные;
КонецПроцедуры

Процедура Добавить_Model(Объявление, Данные)
	Объявление.Model = Строка(Данные);
КонецПроцедуры

Процедура Добавить_OriginalOEM(Объявление, Данные)
	Объявление.OriginalOEM = Строка(Данные);
КонецПроцедуры

Процедура Добавить_OriginalVendor(Объявление, Данные)
	Объявление.OriginalVendor = Строка(Данные);
КонецПроцедуры

Процедура Добавить_ResidualTread(Объявление, Данные)
	Объявление.ResidualTread = Строка(Данные);
КонецПроцедуры

Процедура Добавить_RunFlat(Объявление, Данные)
	Если Объявление.TypeId = "10-048" Тогда
		Объявление.RunFlat = Строка(Данные);
	КонецЕсли; 
КонецПроцедуры

Процедура Добавить_SpeedIndex(Объявление, Данные)
	Объявление.SpeedIndex = Строка(Данные);
КонецПроцедуры

Процедура Добавить_TiresModels(Объявление, Данные)
	Объявление.Model = Строка(Данные);
КонецПроцедуры

Процедура Добавить_RimBolts(Объявление, Данные)
	Объявление.RimBolts = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_RimBoltsDiameter(Объявление, Данные)
	Объявление.RimBoltsDiameter = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_RimDiameter(Объявление, Данные)
	Объявление.RimDiameter = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_RimOffset(Объявление, Данные)
	Объявление.RimOffset = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_RimType(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "RimType");
	Объявление.RimType = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_RimWidth(Объявление, Данные)
	Объявление.RimWidth = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_Subway(Объявление, Данные)
	Объявление.Subway = Строка(Данные);
КонецПроцедуры

Процедура Добавить_TireAspectRatio(Объявление, Данные)
	Объявление.TireAspectRatio = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_TireSectionWidth(Объявление, Данные)
	Объявление.TireSectionWidth = Число(Строка(Данные));
КонецПроцедуры

Процедура Добавить_TireType(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "TireType");
	Объявление.TireType = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_WheelAxle(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "WheelAxle");
	Объявление.WheelAxle = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_Brand(Объявление, Данные)
	Объявление.Brand = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Quantity(Объявление, Данные)
	Объявление.Quantity = Формат(Данные, "ЧРГ=''; ЧГ=0");
КонецПроцедуры

// КонецОбласти 

//Область Разные теги

Процедура Добавить_VehicleType(Объявление, Данные)
	//Если ТипЗнч(Данные) = Тип("ПеречислениеСсылка.ns_VehicleType") Тогда
	//	Тип = ФабрикаXDTO.Тип(ns_Авито.ПолучитьURIПространстваИмен(), "VehicleType");
	//ИначеЕсли ТипЗнч(Данные) = Тип("ПеречислениеСсылка.ns_VehicleTypeМотоциклы") Тогда 	
	//	Тип = ФабрикаXDTO.Тип(ns_Авито.ПолучитьURIПространстваИмен(), "VehicleTypeMoto");
	//Иначе
	//	Возврат;
	//КонецЕсли; 
	//Объявление.VehicleType = ФабрикаXDTO.Создать(Тип, Строка(Данные));
	Объявление.VehicleType = Строка(Данные);
КонецПроцедуры

Процедура Добавить_MotoType(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "MotoType");
	Объявление.MotoType = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_ProductsType(Объявление, Данные)
	Тип = ФабрикаXDTO.Тип(ПолучитьURIПространстваИмен(), "ProductsType");
	Объявление.ProductsType = ФабрикаXDTO.Создать(Тип, Строка(Данные));
КонецПроцедуры

Процедура Добавить_Apparel(Объявление, Данные)
	Объявление.Apparel = Строка(Данные);
КонецПроцедуры

Процедура Добавить_ApparelType(Объявление, Данные)
	Объявление.ApparelType = Строка(Данные);
КонецПроцедуры

Процедура Добавить_Size(Объявление, Данные)
	Объявление.Size = Строка(Данные);
КонецПроцедуры

//КонецОбласти 

// Подписки

Процедура ns_СопоставлениеПриЗаписи(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	//УстановитьБезопасныйРежим(Истина);

	//Для каждого ИмяРазделителя Из ns_ОбщегоНазначенияСервер.РазделителиКонфигурации() Цикл
	//    УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
	//КонецЦикла;
	
	Попытка
		Если ЗначениеЗаполнено(Источник.Ссылка) Тогда
			КатегорииСопоставления = ns_Сопоставление.ПолучитьКатегорииСопоставления();
			Для Каждого Категория Из КатегорииСопоставления Цикл
				Попытка
					Выполнить(Категория + ".СопоставитьЭлементНоменклатуры(Источник.Ссылка);")
				Исключение
					Ошибка = ОписаниеОшибки();
					РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ОшибкаРаботыМодуля, Ошибка);
					ns_ОбщегоНазначения.СообщениеПользователю(Ошибка);
				КонецПопытки; 
			КонецЦикла; 
		КонецЕсли; 
	Исключение
		ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

// КонецОбласти 

// Область НачальноеЗаполнение

Процедура НачальноеЗаполнение() Экспорт
	Справочники.ns_Магазины.СоздатьМагазинПоУмолчанию();
	НачальноеЗаполнениеКатегорий();
КонецПроцедуры

Процедура НачальноеЗаполнениеКатегорий()
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ЗапчастиИАксессуары") = Неопределено Тогда
		НайтиИДобавитьКатегорию("Запчасти и аксессуары");
		ns_ЗапчастиИАксессуары.ЗаполнитьСправочникTypeId();	
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.БытоваяЭлектроника") = Неопределено Тогда
		РодительскаяКатегория = НайтиИДобавитьКатегорию("Бытовая электроника");
		НайтиИДобавитьКатегорию("Телефоны",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Телефоны/Аксессуары",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Аудио и видео",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Товары для компьютера",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Товары для компьютера/Комплектующие",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Фототехника",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Игры, приставки и программы",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Оргтехника и расходники",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Оргтехника и расходники/Расходные материалы",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Планшеты и электронные книги",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Планшеты и электронные книги/Аксессуары",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Ноутбуки",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Настольные компьютеры",, РодительскаяКатегория);
		ns_БытоваяЭлектроника.ЗаполнитьСправочникGoodsType();
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ДляДомаИДачи") = Неопределено Тогда
		РодительскаяКатегория = НайтиИДобавитьКатегорию("Для дома и дачи");
		НайтиИДобавитьКатегорию("Ремонт и строительство",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Мебель и интерьер",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Бытовая техника",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Посуда и товары для кухни",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Растения",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Продукты питания",, РодительскаяКатегория);
		ns_ДляДомаИДачи.ЗаполнитьСправочникGoodsType();
	КонецЕсли; 

	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ДляБизнеса") = Неопределено Тогда
		РодительскаяКатегория = НайтиИДобавитьКатегорию("Для бизнеса");
		НайтиИДобавитьКатегорию("Готовый бизнес",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Оборудование для бизнеса",, РодительскаяКатегория);
		ns_ДляБизнеса.ЗаполнитьСправочникGoodsType();
	КонецЕсли; 

	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ХоббиИОтдых") = Неопределено Тогда
		РодительскаяКатегория = НайтиИДобавитьКатегорию("Хобби и отдых");
		НайтиИДобавитьКатегорию("Билеты и путешествия",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Велосипеды",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Книги и журналы",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Коллекционирование",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Музыкальные инструменты",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Охота и рыбалка",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Спорт и отдых",, РодительскаяКатегория);
		ns_ХоббиИОтдых.ЗаполнитьСправочникGoodsType();
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.МотоциклыИМототехника") = Неопределено Тогда
		НайтиИДобавитьКатегорию("Мотоциклы и мототехника", 20);
	КонецЕсли; 

	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ЛичныеВещи") = Неопределено Тогда
		РодительскаяКатегория = НайтиИДобавитьКатегорию("Личные вещи");
		НайтиИДобавитьКатегорию("Одежда, обувь, аксессуары",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Детская одежда и обувь",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Товары для детей и игрушки",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Часы и украшения",, РодительскаяКатегория);
		НайтиИДобавитьКатегорию("Красота и здоровье",, РодительскаяКатегория);
		ns_ЛичныеВещи.ЗаполнитьСправочникGoodsType();
		ns_ЛичныеВещи.ЗаполнитьСправочникApparel();
		ns_ЛичныеВещи.ЗаполнитьСправочникApparelType();
	КонецЕсли; 
	
КонецПроцедуры

Функция НайтиИДобавитьКатегорию(ИмяКатегории, МаксимальноеКоличествоФото = 10, РодительскаяКатегория = Неопределено)
	Категория = Справочники.ns_Category.НайтиПоНаименованию(ИмяКатегории);
	Если ЗначениеЗаполнено(Категория) Тогда
		Возврат Категория;	
	КонецЕсли;            
	
	Возврат ДобавитьКатегорию(ИмяКатегории, МаксимальноеКоличествоФото, РодительскаяКатегория);
КонецФункции

Функция ДобавитьКатегорию(ИмяКатегории, МаксимальноеКоличествоФото = 10, РодительскаяКатегория = Неопределено) Экспорт
	Категория = Справочники.ns_Category.СоздатьЭлемент();
	Если НЕ РодительскаяКатегория = Неопределено Тогда
		Категория.Родитель = РодительскаяКатегория;
	КонецЕсли;
	
	Категория.Наименование = ИмяКатегории;
	Категория.МаксимальноеКоличествоФото = МаксимальноеКоличествоФото;
	Категория.Записать();
	
	Возврат Категория.Ссылка;
КонецФункции

// КонецОбласти   

// Область Версии

Процедура ПроверитьВерсиюМодуля() Экспорт
	ВерсияССервера = ПолучитьАктуальнуюВерсию();
	Если ВерсияССервера = ТекущаяВерсия() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("У вас установлена актуальная версия: " + ТекущаяВерсия());
	Иначе
		ns_ОбщегоНазначения.СообщениеПользователю("Вышла новая версия модуля: " + ВерсияССервера + " Ваша версия: " + ТекущаяВерсия());
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьАктуальнуюВерсию()
	АктуальнаяВерсия = "";
	
	Попытка  
		SSL = Новый ЗащищенноеСоединениеOpenSSL();
		
		HTTPСоединение = Новый HTTPСоединение(ПолучитьСервер(), ПолучитьПорт(),,,, 15, SSL);
		Запрос = Новый HTTPЗапрос("/api/v1/avito/version/");
		Запрос.Заголовки.Вставить("Content-Type", "application/json");
		Запрос.УстановитьТелоИзСтроки(СформироватьДанныеПроверки(), КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
		
		Ответ = HTTPСоединение.ВызватьHTTPМетод("POST", Запрос);
		
		Если Ответ.КодСостояния = 200 Тогда
			Данные = ОбработатьJSON(Ответ.ПолучитьТелоКакСтроку());
			Возврат Данные["ver"];
		КонецЕсли;
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ОшибкаОбращенияКСерверу, ОписаниеОшибки(),,Ложь);
		Возврат АктуальнаяВерсия;
	КонецПопытки;
	
	Возврат АктуальнаяВерсия;
КонецФункции

Функция ПолучитьСписокВерсий() Экспорт
	Попытка  
		SSL = Новый ЗащищенноеСоединениеOpenSSL();
		
		HTTPСоединение = Новый HTTPСоединение(ПолучитьСервер(), ПолучитьПорт(),,,, 15, SSL);
		Запрос = Новый HTTPЗапрос("/api/v1/avito/versions/");
		Запрос.Заголовки.Вставить("Content-Type", "application/json");
		
		Ответ = HTTPСоединение.ВызватьHTTPМетод("GET", Запрос);
		
		Если Ответ.КодСостояния = 200 Тогда
			Возврат ОбработатьJSON(Ответ.ПолучитьТелоКакСтроку());
		КонецЕсли;
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ОшибкаОбращенияКСерверу, ОписаниеОшибки());
		//ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Неопределено;
КонецФункции

Функция ТекущаяВерсия()
	Возврат "2.5.1.2";
КонецФункции

Функция ПолучитьСервер()
	Возврат "post24.cloud";		
КонецФункции

Функция ПолучитьПорт()
	Возврат 443;
КонецФункции

Функция СформироватьДанныеПроверки()
	Данные = Новый Соответствие;     
	Данные.Вставить("contract", ns_ОбщегоНазначенияПовтИсп.ПолучитьНомерДоговора());
	
	Возврат СформироватьJSON(Данные);
КонецФункции

Функция СформироватьJSON(Структура)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(,Символы.Таб));
	
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON;
	НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением;
	НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;

	ЗаписатьJSON(ЗаписьJSON, Структура, НастройкиСериализацииJSON);
	
	Возврат ЗаписьJSON.Закрыть();
КонецФункции
 
Функция ОбработатьJSON(СтрокаJSON)
	
	Результат = Неопределено;
	
	Попытка            
		ИменаСвойствСоЗначениямиДата = Новый Массив;
		ИменаСвойствСоЗначениямиДата.Добавить("date");
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		Результат = ПрочитатьJSON(ЧтениеJSON, Истина, ИменаСвойствСоЗначениямиДата);
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ОшибкаОбращенияКСерверу, ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

// КонецОбласти   














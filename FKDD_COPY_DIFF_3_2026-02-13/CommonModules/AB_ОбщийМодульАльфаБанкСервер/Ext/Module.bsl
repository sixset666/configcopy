Процедура AB_ПолучениеТранзакцийБанка() Экспорт
	Обр = Обработки.AB_ОбменАльфаБанк.Создать();
	Обр.ОбменДанными();
КонецПроцедуры

Процедура НачатьРаботуСТерминалом(Терминал, Пользователь, Документ) Экспорт
	//Проверим еще раз что терминал свободен
	Если ПроверитьДоступностьТерминала(Терминал) = Истина Тогда
		НоваяЗапись = РегистрыСведений.AB_РаботаСТерминаломАльфаБанк.СоздатьМенеджерЗаписи();
		НоваяЗапись.Терминал = Терминал;
		НоваяЗапись.Организация = Терминал.Организация;
		НоваяЗапись.ПодразделениеКомпании = Терминал.ПодразделениеКомпании;
		НоваяЗапись.Пользователь = Пользователь;
		НоваяЗапись.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Занят;
		НоваяЗапись.IDСессии = ПолучитьПоследнийID() + 1;
		НоваяЗапись.Период = ТекущаяДатаСеанса(); 
		НоваяЗапись.Документ = Документ;
		НоваяЗапись.Записать();
	Иначе
		Сообщить("Терминал занят");
	КонецЕсли;

КонецПроцедуры

Процедура ЗакончитьРаботуСТерминалом(Терминал,ТекущийПользователь,Документ,Сумма) Экспорт
	отказ = Ложь;
	СтруктураТМ = ПроверитьДоступностьТерминала(Терминал);
	Если ТипЗнч(СтруктураТМ) = Тип("Структура") Тогда
		Пользователь = СтруктураТМ.Пользователь;
		IDСессии = СтруктураТМ.IDСессии;	
		Если ЗначениеЗаполненоМ(Пользователь,IDСессии) Тогда
			
			//Проверка пользователя закрывающего сессию и пользователя открывшего сессию
			Если ТекущийПользователь <> Пользователь Тогда
				Сообщить("Нельзя закрыть терминал открытый другим пользователем!");
				отказ = Истина;
			КонецЕсли;
			
			//Обработаем документ
			Если Сумма <> 0 Тогда
				Попытка
					ДокОб = Документ.ПолучитьОбъект();
					ДокОб.AB_ЧерезАльфаБанк = Истина;
					ДокОб.СуммаДокумента = Сумма;
					ДокОб.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					Инфо = ИнформацияОбОшибке();
					Сообщить("Не удалось записать документ!");
					Отказ = Истина
				КонецПопытки;
			КонецЕсли;
			
			Если НЕ отказ Тогда
				НоваяЗапись = РегистрыСведений.AB_РаботаСТерминаломАльфаБанк.СоздатьМенеджерЗаписи();
				НоваяЗапись.Терминал = Терминал;
				НоваяЗапись.Организация = Терминал.Организация;
				НоваяЗапись.ПодразделениеКомпании = Терминал.ПодразделениеКомпании;
				НоваяЗапись.Пользователь = Пользователь;
				НоваяЗапись.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Открыт;
				НоваяЗапись.IDСессии = IDСессии;
				НоваяЗапись.Период = ТекущаяДатаСеанса();
				НоваяЗапись.Документ = Документ;
				НоваяЗапись.Записать();
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьТранзакции(Терминал, Документ) Экспорт
	Если ЗначениеЗаполнено(Терминал) Тогда
		СтруктураТМ = ПроверитьДоступностьТерминала(Терминал);
		Если ТипЗнч(СтруктураТМ) = Тип("Структура") Тогда
			ДатаНачалаРаботы = СтруктураТМ.ДатаНачалаРаботы;
			Пользователь = СтруктураТМ.Пользователь;
			IDСессии = СтруктураТМ.IDСессии;
			Если ЗначениеЗаполненоМ(ДатаНачалаРаботы,Пользователь,IDСессии) Тогда
				Транзакции = ПолучитьТранзакции(Терминал,ДатаНачалаРаботы);
				Для Каждого Транзакция Из Транзакции Цикл
					ЗаписатьТранзакциюДляТерминала(Транзакция,Терминал,Пользователь,IDСессии,Документ);
					ПоставитьГалочкуНаТранзакцию(Транзакция.UUID);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПроверитьДоступностьТерминала(Терминал=Неопределено,Пользователь=Неопределено,Документ=Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Терминал КАК Терминал,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Статус КАК Статус,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Период КАК Период,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Пользователь КАК Пользователь,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.IDСессии КАК IDСессии,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.AB_РаботаСТерминаломАльфаБанк.СрезПоследних(
		|			,
		|			(Статус = ЗНАЧЕНИЕ(Перечисление.AB_СтатусыТерминаловАльфабанк.Открыт)
		|				ИЛИ Статус = ЗНАЧЕНИЕ(Перечисление.AB_СтатусыТерминаловАльфабанк.Занят))
		|				И Терминал = &Терминал) КАК AB_РаботаСТерминаломАльфаБанкСрезПоследних";
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Терминал = &Терминал","Пользователь = &Пользователь");
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Иначе
		Если ЗначениеЗаполнено(Документ) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"Терминал = &Терминал","Документ = &Документ");
			Запрос.УстановитьПараметр("Документ", Документ);
		Иначе
			Запрос.УстановитьПараметр("Терминал", Терминал);
		КонецЕсли;
	КонецЕсли;
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Занят Тогда
			Возврат Новый Структура("ДатаНачалаРаботы,Пользователь,IDСессии,Документ",
									ВыборкаДетальныеЗаписи.Период,
									ВыборкаДетальныеЗаписи.Пользователь,
									ВыборкаДетальныеЗаписи.IDСессии,
									ВыборкаДетальныеЗаписи.Документ);
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Открыт Тогда 
			Возврат Истина;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
		
КонецФункции

Функция ПолучитьПоследнийID()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(AB_РаботаСТерминаломАльфаБанк.IDСессии) КАК IDСессии
		|ИЗ
		|	РегистрСведений.AB_РаботаСТерминаломАльфаБанк КАК AB_РаботаСТерминаломАльфаБанк";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.IDСессии
	КонецЦикла;
	
КонецФункции
Функция ПолучитьТранзакции(Терминал,ДатаНачалаРаботы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_ТранзакцииАльфабанкСрезПоследних.Период КАК Период,
		|	AB_ТранзакцииАльфабанкСрезПоследних.УчетнаяЗапись КАК УчетнаяЗапись,
		|	AB_ТранзакцииАльфабанкСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
		|	AB_ТранзакцииАльфабанкСрезПоследних.Терминал КАК Терминал,
		|	AB_ТранзакцииАльфабанкСрезПоследних.UUID КАК UUID,
		|	AB_ТранзакцииАльфабанкСрезПоследних.IDОперации КАК IDОперации,
		|	AB_ТранзакцииАльфабанкСрезПоследних.ДатаОперации КАК ДатаОперации,
		|	AB_ТранзакцииАльфабанкСрезПоследних.Сумма КАК Сумма,
		|	AB_ТранзакцииАльфабанкСрезПоследних.Используется КАК Используется,
		|	AB_ТранзакцииАльфабанкСрезПоследних.JSON КАК JSON,
		|	AB_ТранзакцииАльфабанкСрезПоследних.НазначениеПлатежа КАК НазначениеПлатежа
		|ИЗ
		|	РегистрСведений.AB_ТранзакцииАльфабанк.СрезПоследних(
		|			,
		|			Терминал = &Терминал
		|				И ДатаОперации >= &ДатаНачалаРаботы
		|				И Используется = ЛОЖЬ) КАК AB_ТранзакцииАльфабанкСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаНачалаРаботы", ДатаНачалаРаботы);
	Запрос.УстановитьПараметр("Терминал", Терминал);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции
Функция ПолучитьТранзакцию(UUID)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_ТранзакцииАльфабанкСрезПоследних.Период КАК Период,
		|	AB_ТранзакцииАльфабанкСрезПоследних.УчетнаяЗапись КАК УчетнаяЗапись,
		|	AB_ТранзакцииАльфабанкСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
		|	AB_ТранзакцииАльфабанкСрезПоследних.Терминал КАК Терминал,
		|	AB_ТранзакцииАльфабанкСрезПоследних.UUID КАК UUID,
		|	AB_ТранзакцииАльфабанкСрезПоследних.IDОперации КАК IDОперации,
		|	AB_ТранзакцииАльфабанкСрезПоследних.ДатаОперации КАК ДатаОперации,
		|	AB_ТранзакцииАльфабанкСрезПоследних.Сумма КАК Сумма,
		|	AB_ТранзакцииАльфабанкСрезПоследних.Используется КАК Используется,
		|	AB_ТранзакцииАльфабанкСрезПоследних.JSON КАК JSON,
		|	AB_ТранзакцииАльфабанкСрезПоследних.НазначениеПлатежа КАК НазначениеПлатежа
		|ИЗ
		|	РегистрСведений.AB_ТранзакцииАльфабанк.СрезПоследних(, UUID = &UUID) КАК AB_ТранзакцииАльфабанкСрезПоследних";
	
	Запрос.УстановитьПараметр("UUID", UUID);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции
Функция ПоставитьГалочкуНаТранзакцию(UUID)
	Набор = РегистрыСведений.AB_ТранзакцииАльфабанк.СоздатьНаборЗаписей();
	Набор.Отбор.UUID.Установить(UUID);
	Набор.Прочитать();
	Для Каждого Строка Из Набор Цикл
		строка.Используется = Истина;
	КонецЦикла;
	Набор.Записать()
КонецФункции
Функция ЗначениеЗаполненоМ(Значение1="1",Значение2="1",Значение3="1",Значение4="1",Значение5="1",Значение6="1") Экспорт
	Если ЗначениеЗаполнено(Значение1)
		И ЗначениеЗаполнено(Значение2)
		И ЗначениеЗаполнено(Значение3)
		И ЗначениеЗаполнено(Значение4)
		И ЗначениеЗаполнено(Значение5)
		И ЗначениеЗаполнено(Значение6) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
Функция ЗаписатьТранзакциюДляТерминала(Транзакция,Терминал,Пользователь,IDСессии,Документ)
	НоваяЗапись = РегистрыСведений.AB_РаботаСТерминаломАльфаБанк.СоздатьМенеджерЗаписи();
	НоваяЗапись.Период = ТекущаяДатаСеанса();
	НоваяЗапись.Терминал = Терминал;
	НоваяЗапись.Организация = Терминал.Организация;
	НоваяЗапись.ПодразделениеКомпании = Терминал.ПодразделениеКомпании;
	НоваяЗапись.Пользователь = Пользователь;
	НоваяЗапись.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Транзакция;
	НоваяЗапись.IDСессии = IDСессии;
	НоваяЗапись.UUID = Транзакция.UUID;
	НоваяЗапись.Документ = Документ;
	НоваяЗапись.Сумма = Транзакция.Сумма;
    НоваяЗапись.Записать();
КонецФункции
Функция ПроверитьВозможностьРаботы(Организация,Подразделение) Экспорт 
	
	ОбработкаБанка = Обработки.AB_ОбменАльфаБанк.Создать();
	УчетныеЗаписиПоОрганизации = ОбработкаБанка.ПолучитьУчетныеЗаписи(Организация);
	ЕстьЛиРабочаяУчетнаяЗапись = Ложь;
	ЕстьЛиНастроенныйТерминал = Ложь;
	Для Каждого УчетнаяЗапись из УчетныеЗаписиПоОрганизации Цикл
		УчетнаяЗапись = ОбработкаБанка.ПолучитьДанныеУчетнойЗаписи(УчетнаяЗапись.Ссылка);
		Если ЗначениеЗаполненоМ(УчетнаяЗапись.ClientID,
								УчетнаяЗапись.ClientSecret,  
								УчетнаяЗапись.access_token,  
								УчетнаяЗапись.refresh_token,  
								УчетнаяЗапись.РасчетныйСчет) Тогда
			ЕстьЛиРабочаяУчетнаяЗапись = Истина;
			Прервать;
		КонецЕсли;
		//УчетнаяЗапись = УчетнаяЗапись.Ссылка;
		//Если ОбработкаБанка.ОбновитьТокен(УчетнаяЗапись) = Истина Тогда
		//	ЕстьЛиРабочаяУчетнаяЗапись = Истина;
		//	Прервать;
		//КонецЕсли;
	КонецЦикла;
	
	МассивПодразделений = Новый Массив;
	МассивПодразделений.Добавить(Подразделение);
	
	Подр = Подразделение; 
	
	Пока ЗначениеЗаполнено(Подр.Родитель) Цикл
		МассивПодразделений.Добавить(Подр.Родитель);
		Подр = Подр.Родитель;
	КонецЦикла;
	
	//Терминал
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_ТерминалыАльфабанк.Ссылка КАК Ссылка,
		|	AB_ТерминалыАльфабанк.Наименование КАК Наименование,
		|	AB_ТерминалыАльфабанк.ID КАК ID,
		|	AB_ТерминалыАльфабанк.Организация КАК Организация,
		|	AB_ТерминалыАльфабанк.ПодразделениеКомпании КАК ПодразделениеКомпании
		|ИЗ
		|	Справочник.AB_ТерминалыАльфабанк КАК AB_ТерминалыАльфабанк
		|ГДЕ
		|	AB_ТерминалыАльфабанк.ПометкаУдаления = ЛОЖЬ
		|	И AB_ТерминалыАльфабанк.Организация = &Организация
		|	И AB_ТерминалыАльфабанк.ПодразделениеКомпании В (&ПодразделениеКомпании)";
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",МассивПодразделений);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для каждого Терминал из РезультатЗапроса Цикл 
		Если ЗначениеЗаполнено(Терминал.ID) Тогда
			ЕстьЛиНастроенныйТерминал = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьЛиНастроенныйТерминал И ЕстьЛиРабочаяУчетнаяЗапись Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ПривязатьТранзакциюПринудительно(UUID,Терминал) Экспорт
	Если ЗначениеЗаполнено(Терминал) И ЗначениеЗаполнено(UUID) Тогда
		СтруктураТМ = ПроверитьДоступностьТерминала(Терминал);
		Если ТипЗнч(СтруктураТМ) = Тип("Структура") Тогда
			ДатаНачалаРаботы = СтруктураТМ.ДатаНачалаРаботы;
			Пользователь = СтруктураТМ.Пользователь;
			IDСессии = СтруктураТМ.IDСессии;
			Документ = СтруктураТМ.Документ; 
			Если ЗначениеЗаполненоМ(ДатаНачалаРаботы,Пользователь,IDСессии) Тогда
				Транзакция = ПолучитьТранзакцию(UUID);
				Если Транзакция.Количество() Тогда
					Транзакция = Транзакция[0];
					Если НЕ Транзакция.Используется Тогда
						ЗаписатьТранзакциюДляТерминала(Транзакция,Терминал,Пользователь,IDСессии,Документ);
						ПоставитьГалочкуНаТранзакцию(Транзакция.UUID);
						Возврат Истина;
					Иначе
						Сообщить("Транзакция уже используется в ПКО!");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецФункции
Функция ПолучитьСуммуТранзакцийТерминалаПоСессии(IDСессии)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(AB_РаботаСТерминаломАльфаБанк.Сумма) КАК Сумма
		|ИЗ
		|	РегистрСведений.AB_РаботаСТерминаломАльфаБанк КАК AB_РаботаСТерминаломАльфаБанк
		|ГДЕ
		|	AB_РаботаСТерминаломАльфаБанк.IDСессии = &IDСессии";
	
	Запрос.УстановитьПараметр("IDСессии", IDСессии);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() тогда
		Возврат РезультатЗапроса[0].Сумма;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции
Функция ЗакончитьРаботуСТерминаломПринудительно(Терминал) Экспорт
	отказ = Ложь;
	//Получим информацию по терминалу
	СтруктураТМ = ПроверитьДоступностьТерминала(Терминал);
	Если ТипЗнч(СтруктураТМ) = Тип("Структура") Тогда
		Пользователь = СтруктураТМ.Пользователь;
		IDСессии = СтруктураТМ.IDСессии;
		Документ = СтруктураТМ.Документ;
		Если ЗначениеЗаполненоМ(Пользователь,IDСессии,Документ) Тогда
			Сумма = ПолучитьСуммуТранзакцийТерминалаПоСессии(IDСессии);
			//Обработаем документ
			Если Сумма <> 0 Тогда
				Попытка
					ДокОб = Документ.ПолучитьОбъект();
					ДокОб.AB_ЧерезАльфаБанк = Истина;
					ДокОб.СуммаДокумента = Сумма;
					ДокОб.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					Инфо = ИнформацияОбОшибке();
					Сообщить("Не удалось записать документ!");
					Отказ = Истина
				КонецПопытки;
			КонецЕсли;
			//Закроем терминал
			Если НЕ отказ Тогда
				НоваяЗапись = РегистрыСведений.AB_РаботаСТерминаломАльфаБанк.СоздатьМенеджерЗаписи();
				НоваяЗапись.Терминал = Терминал;
				НоваяЗапись.Организация = Терминал.Организация;
				НоваяЗапись.ПодразделениеКомпании = Терминал.ПодразделениеКомпании;
				НоваяЗапись.Пользователь = Пользователь;
				НоваяЗапись.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Открыт;
				НоваяЗапись.IDСессии = IDСессии;
				НоваяЗапись.Период = ТекущаяДатаСеанса();
				НоваяЗапись.Документ = Документ;
				НоваяЗапись.Записать();
			КонецЕсли;
			Сообщить("Закрыли сессию");
			Возврат Истина;
		Иначе
			Сообщить("Нет данных");
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Сообщить("Терминал не занят");
		Возврат Ложь;
	КонецЕсли;
КонецФункции















// модуль процедур управления диалогом общих для всех объектов


// Общие действия форм при их открытии
//
// Параметры:
//  ЭтаФорма  - форма, открываемая форма
//
Процедура удФормаПриОткрытии(ЭтаФорма) Экспорт
	зфФормаПриОткрытии(ЭтаФорма);
КонецПроцедуры // удФормаПриОткрытии()

// Общие действия форм при их закрытии
//
// Параметры:
//  ЭтаФорма  - форма, закрываемая форма
//
Процедура удФормаПриЗакрытии(ЭтаФорма) Экспорт
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().УдалитьИзДереваФорм(ЭтаФорма);
КонецПроцедуры // удФормаПриЗакрытии()

// Общие действия форм перед открытием
//
// Параметры:
//  ЭтаФорма 			 - форма,  открываемая форма. 
//  Отказ      		     - Булево, отказ от открытия формы
//  СтандартнаяОбработка - Булево, признак стандартной работы обработчика
//
Процедура удФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
  // Зарезервировано на будущее  
КонецПроцедуры // удФормаПередОткрытием()

// расставляет автоотметки незаполненного для полей ввода формы
// в соответствии с переданной структурой обязательных полей
//
// Параметры:
//  ЭтаФорма                              - форма - форма в которой расставляем автоотметки. 
//  СтруктураОбязательныхРеквизитов       - структура содержит строковые идентификаторы реквизитов или вложенные
//											структуры для табличных частей
//                                          Для реквизита значение структуры содержит число 1-Обязательный,
//																							2-Уникальный,
//																							3-Уникальный и обязательный
//  СнятиеСтарыхАвтоотметокНезаполненного - признак того, снимать старые метки или нет.
//
Процедура удРасставитьАвтоотметкиНезаполненного(ЭтаФорма, СтруктураОбязательныхРеквизитов, СнятиеСтарыхАвтоотметокНезаполненного=Ложь) Экспорт
	зфРасставитьАвтоотметкиНезаполненного(ЭтаФорма, СтруктураОбязательныхРеквизитов, СнятиеСтарыхАвтоотметокНезаполненного);
КонецПроцедуры // удРасставитьАвтоотметкиНезаполненного()

//заполнение дополнительных действий форм и списков справочников
//
// Параметры:
//  ЭтаФорма - форма - форма в которой производим заполнение. 
//
Процедура удЗаполнитьДополнительныеДействияСправочников(ЭтаФорма) Экспорт
	
	// получим права пользователя и определим что за форма
	ЭтоФормаОбъекта = Ложь;
	ЭтоФормаСписка = Ложь;
	ЭтоФормаСпискаПВХ = Ложь;
	Попытка	
		ПраваПользователя = ЭтаФорма.ЭтотОбъект.Права;
		ЭтаФормаЭтотОбъект = ЭтаФорма.ЭтотОбъект;
		ЭтоФормаОбъекта = Истина;		
	Исключение
		ПраваПользователя = Неопределено;
		ЭтаФормаЭтотОбъект = Неопределено;
	КонецПопытки; 
	Попытка
		СписокДляБух = ЭтаФорма.СправочникСписок;
		ЭтоФормаСписка = Истина;
	Исключение
	КонецПопытки;
	Попытка
		СписокПВХ = ЭтаФорма.ПланВидовХарактеристикСписок;
		ЭтоФормаСпискаПВХ = Истина;
	Исключение
	КонецПопытки;
	
	// ищем подменю "Действия"
	ПодменюДействия = Неопределено;
	ДействияФормы=ЭтаФорма.ЭлементыФормы.Найти("ДействияФормы");
	Если ДействияФормы<>Неопределено Тогда
		Для НомерКнопки = 1 По ДействияФормы.Кнопки.Количество() Цикл
			ТекПодменю = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Получить(НомерКнопки-1);
			Если (ТекПодменю.ТипКнопки = ТипКнопкиКоманднойПанели.Подменю) И (ТекПодменю.Текст = "Действия") Тогда
				ПодменюДействия = ТекПодменю;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли; 
	
	// добавление кнопки заметки
	Если ДействияФормы<>Неопределено Тогда
		Если ЭтоФормаСписка ИЛИ ЭтоФормаОбъекта Тогда
			ДоступностьЗаметки = Истина;
			Если ЭтоФормаСписка Тогда
				ТипДанных = Тип("СправочникСсылка." + Метаданные.НайтиПоТипу(ТипЗнч(СписокДляБух)).Имя);
			ИначеЕсли Метаданные.Справочники.Содержит(ЭтаФормаЭтотОбъект.Метаданные()) Тогда
				ТипДанных = Тип("СправочникСсылка." + Метаданные.НайтиПоТипу(ТипЗнч(ЭтаФормаЭтотОбъект.Ссылка)).Имя);
				Если обЗначениеНеЗаполнено(ЭтаФормаЭтотОбъект.Ссылка) Тогда
					ДоступностьЗаметки = Ложь;
				КонецЕсли;
			Иначе
				ТипДанных = Тип("Строка");
			КонецЕсли;
			Если Метаданные.РегистрыСведений.ЗаметкиПользователей.Измерения.Объект.Тип.СодержитТип(ТипДанных) Тогда
				КнопкиДействийФормы = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки;
				КнопкаЗаметок = КнопкиДействийФормы.Найти("кнЗаметки");
				Если КнопкаЗаметок = Неопределено Тогда
					КнопкиДействийФормы.Добавить("ЗаметкиРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
					КнопкаЗаметок             = КнопкиДействийФормы.Добавить("кнЗаметки",ТипКнопкиКоманднойПанели.Действие,"Заметки",Новый Действие("ДополнительныеДействияФормы"));
					КнопкаЗаметок.Картинка    = БиблиотекаКартинок.ЗаметкиСоздать;
					КнопкаЗаметок.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
					КнопкаЗаметок.Доступность = ДоступностьЗаметки;
					КнопкаЗаметок.Подсказка   = "Открыть список заметок";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	Если ПодменюДействия <> Неопределено Тогда
		Попытка	// Может возникнуть ситуация что форма программно открывается повторно, в этом случае все кнопки уже созданы!
			
			// добавляем кнопку включения возможности редактирования кода справочника
			РедактированиеКодов = обПраво("РазрешитьРедактированиеКодовСправочников",ПраваПользователя,,ЭтаФормаЭтотОбъект); 
			Если НЕ ЭтаФорма.ТолькоПросмотр И РедактированиеКодов = Перечисления.ВариантыОтветов.Спрашивать Тогда
				//проверим наличие кода на форме и получим значение реквизита "СпособРедактирования" для форм списка
				РедактированиеВСписке = Ложь;
				Список = ЭтаФорма.ЭлементыФормы.Найти("Список");
				Если Список <> Неопределено И Тип(Список) = Тип("ТабличноеПоле") 
					И Список.СпособРедактирования = СпособРедактированияСписка.ВСписке Тогда
					РедактированиеВСписке = Истина;
					Код = Список.Колонки.Найти("Код");
			    КонецЕсли;
				Если Код = Неопределено Тогда
					Код = ЭтаФорма.ЭлементыФормы.Найти("Код");
				КонецЕсли;
				//если свойство у формы автонумерация="не использовать",то код редактируется всегда
				//банки, валюты, пользователи, ячейки хранения
				Попытка
					Если ЭтаФорма.Автонумерация = АвтонумерацияВФорме.НеИспользовать Тогда
						Код = Неопределено;
					КонецЕсли;
				Исключение КонецПопытки;
				//добавим разделитель и кнопку
				Если Код <> Неопределено И (ЭтоФормаОбъекта ИЛИ ((ЭтоФормаСписка ИЛИ ЭтоФормаСпискаПВХ) И РедактированиеВСписке)) Тогда
				    // разделитель
				  	КнопкиПодменюДействия = ПодменюДействия.Кнопки;
					КнопкиПодменюДействия.Вставить(0,"РедактированиеКодовРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
					// кнопка
					Кнопка = КнопкиПодменюДействия.Найти("РедактироватьКод");
					Если Кнопка = Неопределено Тогда
						Кнопка = КнопкиПодменюДействия.Вставить(0,"РедактироватьКод",ТипКнопкиКоманднойПанели.Действие,"Редактировать код",Новый Действие("ДополнительныеДействияФормы"));
						Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
						Кнопка.Подсказка   = "Включить возможность редактирования кода";
					КонецЕсли; 
				КонецЕсли;	
			КонецЕсли;	
			
			// добавляем кнопки для вызова дополнительных действий
			КнопкиПодменюДействия = ПодменюДействия.Кнопки;
			КнопкиПодменюДействия.Вставить(0,"ДопВозможностиРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
			
			// добавляем кнопку создания письма в техническую поддержку 1С-Раруса
			Кнопка = КнопкиПодменюДействия.Найти("СоздатьПисьмоВТехПоддержку");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"СоздатьПисьмоВТехПоддержку",ТипКнопкиКоманднойПанели.Действие,"Написать письмо в службу техподдержки",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.НовоеПисьмо;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Написать письмо в службу тех.поддержки ООО ""1С-Рарус Альфа-Авто""";
			КонецЕсли; 
			
			// добавляем кнопку создания напоминания
			Кнопка = КнопкиПодменюДействия.Найти("СоздатьНапоминание");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"СоздатьНапоминание",ТипКнопкиКоманднойПанели.Действие,"Создать напоминание",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.Оповещение;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Создать напоминание";
			КонецЕсли;
			
			// добавляем кнопку выгрузки в бухгалтерию
			Если удВыгружаетсяВБухгалтерию(ЭтаФорма, ЭтоФормаСписка, Ложь, СписокДляБух) Тогда
				Кнопка = КнопкиПодменюДействия.Найти("ВыгрузкаВБухгалтерию");
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0,"ВыгрузкаВБухгалтерию",ТипКнопкиКоманднойПанели.Действие,"Выгрузка в бухгалтерию",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ДебетКредит;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Выгрузка в бухгалтерию";
				КонецЕсли;
			КонецЕсли;
			
			// добавляем кнопку выгрузки в табличный документ
			Кнопка = КнопкиПодменюДействия.Найти("ВыгрузкаВТабличныйДокумент");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ВыгрузкаВТабличныйДокумент",ТипКнопкиКоманднойПанели.Действие,"Выгрузка в табличный документ",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ТабличныйДокумент;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Выгрузка в табличный документ";
			КонецЕсли;
			
			// добавляем кнопку замены объектов
			Кнопка = КнопкиПодменюДействия.Найти("ЗаменитьНаОбъект");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ЗаменитьНаОбъект",ТипКнопкиКоманднойПанели.Действие,"Заменить на другой объект",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.Заменить;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Создать запись о замене на другой объект";
			КонецЕсли;
			
			// добавляем кнопку выведения истории объекта
			Кнопка = КнопкиПодменюДействия.Найти("ИсторияОбъекта");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ИсторияОбъекта",ТипКнопкиКоманднойПанели.Действие,"История объекта",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ЖурналРегистрацииКонфигурации;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "История объекта";
			КонецЕсли;
			
			// добавляем кнопку поиска ссылок
			Кнопка = КнопкиПодменюДействия.Найти("ПоискСсылок");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ПоискСсылок",ТипКнопкиКоманднойПанели.Действие,"Поиск ссылок",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ПоискСсылок;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Поиск ссылок на объекты использующие данный объект и используемых в данном объекте";
			КонецЕсли;
			
			// добавляем кнопку добавления в избранное
			Кнопка = КнопкиПодменюДействия.Найти("ДобавитьВИзбранное");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ДобавитьВИзбранное",ТипКнопкиКоманднойПанели.Действие,"Добавить в избранное",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ДобавитьВИзбранное;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Добавить в избранное";
			КонецЕсли;
			
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	//KamikadZe begin add 21.04.2015 14:14:50
	обОбщие.обСформироватьПодменюДействий(ЭтаФорма);
	//KamikadZe end add
	
КонецПроцедуры // удЗаполнитьДополнительныеДействияСправочников()

//заполнение дополнительных действий форм и списков документов
//
// Параметры:
//  ЭтаФорма - форма - форма в которой производим заполнение. 
//
Процедура удЗаполнитьДополнительныеДействияДокументов(ЭтаФорма) Экспорт
	// определим что за форма
	ЭтоФормаСписка = Ложь;
	ЭтоФормаЖурнала = Ложь;
	Попытка
		Список = ЭтаФорма.ДокументСписок;
		ЭтоФормаСписка = Истина;
	Исключение
		Попытка
			Список = ЭтаФорма.ЖурналДокументовСписок;
			ЭтоФормаЖурнала = Истина;
		Исключение
		КонецПопытки;
	КонецПопытки;
	
	// получим права пользователя
	Попытка	
		ПраваПользователя = ЭтаФорма.ЭтотОбъект.Права;
		ЭтаФормаЭтотОбъект = ЭтаФорма.ЭтотОбъект;
		ЭтоФормаОбъекта = Истина;		
	Исключение
		ПраваПользователя = Неопределено;
		ЭтаФормаЭтотОбъект = Неопределено;
	КонецПопытки; 
	
	//!!!!! При добавлении кнопок, нажатие которых приводит к изменению данных в документе, 
	//не забывать взводить флажок "ИзменяетДанные" !!!!!
	
	// добавляем движения документа
	//найдем последнее подменю в форме
	Сч = 1;
	ПодменюПерейти = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Найти("Подменю");
	Пока Истина Цикл
		Подменю_ = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Найти("Подменю" + Строка(Сч));
		Если Подменю_ = Неопределено Тогда
			Если Сч > 1 Тогда 
				ПодменюПерейти = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Найти("Подменю" + Строка(Сч-1));
			КонецЕсли;
		    Прервать;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	//добавляем, если у документов в принципе могут быть движения
	Если (ЭтоФормаСписка И (ТипЗнч(ЭтаФорма.Отбор.Ссылка.Значение) = Тип("СписокЗначений") ИЛИ ЭтаФорма.Отбор.Ссылка.Значение.Ссылка.Метаданные().Движения.Количество() > 0))
		ИЛИ ЭтоФормаЖурнала
		ИЛИ ((НЕ ЭтоФормаСписка) И ЭтаФорма.ЭтотОбъект.Метаданные().Движения.Количество() > 0) Тогда
		Если ПодменюПерейти <> Неопределено Тогда
			Кнопка = ПодменюПерейти.Кнопки.Найти("ДвиженияДокумента");
			Если Кнопка = Неопределено Тогда
				Кнопка = ПодменюПерейти.Кнопки.Вставить(0,"ДвиженияДокумента",ТипКнопкиКоманднойПанели.Действие,"Движения документа",Новый Действие("ДополнительныеДействияФормы"));
				ПодменюПерейти.Кнопки.Вставить(1,"ДвиженияДокументаРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
				Кнопка.Подсказка = "Вывести движения документа";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// добавляем дерево документа
	ДействияФормы = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки;
	Кнопка = ДействияФормы.Найти("ДеревоДокумента");
	Если Кнопка = Неопределено Тогда
		ДействияФормы.Добавить("ДеревоДокументовРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
		Кнопка = ДействияФормы.Добавить("ДеревоДокумента",ТипКнопкиКоманднойПанели.Действие,"Дерево документов",Новый Действие("ДополнительныеДействияФормы"));
		Кнопка.Картинка    = БиблиотекаКартинок.Дерево;
		Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
		Кнопка.Подсказка   = "Показать дерево связей документа";
	КонецЕсли;
	
	// добавляем панель свойств
	Кнопка = ДействияФормы.Найти("Свойства");
	Если Кнопка = Неопределено Тогда
		ДействияФормы.Добавить("СвойстваРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
		Кнопка = ДействияФормы.Добавить("Свойства",ТипКнопкиКоманднойПанели.Действие,"Свойства",Новый Действие("ДополнительныеДействияФормы"));
		Кнопка.Картинка    = БиблиотекаКартинок.СвойстваОбъекта;
		Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
		Кнопка.Подсказка   = "Открыть панель свойств";
	КонецЕсли;
	
	// добавление кнопок расширенной сортировки (по коду и производителю)
	Попытка
		ДокументОбъект = ЭтаФорма.ДокументОбъект;
	Исключение
		ДокументОбъект = Неопределено;
	КонецПопытки;
	
	Если ДокументОбъект <> Неопределено Тогда
		
		МассивТабЧасти = Новый Массив;
		МетаДанныеТабЧасти = ДокументОбъект.Метаданные().ТабличныеЧасти;
		Для Каждого МетаТабЧасть Из МетаДанныеТабЧасти Цикл
			Если МетаТабЧасть.Реквизиты.Найти("Номенклатура")<> Неопределено Тогда
				МассивТабЧасти.Добавить(МетаТабЧасть.Имя);
			КонецЕсли;
		КонецЦикла;
		
		МассивКомандПанели = Новый Массив;
		Для Каждого ИмяТабЧасти Из МассивТабЧасти Цикл
			Попытка
				ТабПоле = ЭтаФорма.ЭлементыФормы[ИмяТабЧасти];
			Исключение
				Продолжить;
			КонецПопытки;
			
			Если ТипЗнч(ТабПоле) = Тип("ТабличноеПоле") И ТабПоле.Данные = ИмяТабЧасти Тогда
				Если ТабПоле.Колонки.Найти("Код") <> Неопределено Тогда 
					
					Попытка
						КоманднаяПанельПоля = ЭтаФорма.ЭлементыФормы["КоманднаяПанель"+ИмяТабЧасти];
						Если КоманднаяПанельПоля.ИсточникДействий = ТабПоле Тогда
							МассивКомандПанели.Добавить(КоманднаяПанельПоля);
						Иначе
							КоманднаяПанельПоля = Неопределено;
						КонецЕсли;
					Исключение
						КоманднаяПанельПоля = Неопределено;
					КонецПопытки;
					
					Если КоманднаяПанельПоля = Неопределено Тогда
						Для Каждого ТекЭлемФормы Из ЭтаФорма.ЭлементыФормы Цикл
							Если ТипЗнч(ТекЭлемФормы) <> Тип("КоманднаяПанель") Тогда
								Продолжить;
							КонецЕсли;
							Если ТекЭлемФормы.ИсточникДействий = ТабПоле Тогда
								МассивКомандПанели.Добавить(ТекЭлемФормы);
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого КоманднаяПанельКнопкиСортировки Из МассивКомандПанели Цикл
			
			ЕстьКнопкаРасширеннойСортировки = Ложь;
			Для Каждого ТекКнопка Из КоманднаяПанельКнопкиСортировки.Кнопки Цикл
				Если Найти(ТекКнопка.Имя, "_РасширеннаяСортировка_") > 0 Тогда
					ЕстьКнопкаРасширеннойСортировки = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьКнопкаРасширеннойСортировки Или Не КоманднаяПанельКнопкиСортировки.ИсточникДействий.ИзменятьПорядокСтрок Тогда
				Продолжить;
			КонецЕсли;
			
			// запоминаем все действия, а затем выключаем автозаполнение
			МассивСтандарныхКнопок = Новый Массив;
			Итератор = 0;
			ЕстьКнопкаСортировкиВозр = Ложь;
			ЕстьКнопкаСортировкиУбыв = Ложь;
			КнопкаСортировкиВозр = Неопределено;
			КнопкаСортировкиУбыв = Неопределено;
			Для Каждого ТекКнопка Из КоманднаяПанельКнопкиСортировки.Кнопки Цикл
				Если (ТекКнопка.ТипКнопки = ТипКнопкиКоманднойПанели.Разделитель Или Найти(ТекКнопка.Имя, "Действие") <> 1) Тогда
					Прервать;
				КонецЕсли;
				
				Если (ТекКнопка.Подсказка = "Упорядочить по возрастанию" Или
					ТекКнопка.Пояснение = "Упорядочить список по возрастанию" Или
					ТекКнопка.Текст = "Сортировать по возрастанию") Тогда
					ЕстьКнопкаСортировкиВозр = Истина;
					КнопкаСортировкиВозр = ТекКнопка;
				КонецЕсли;
				Если (ТекКнопка.Подсказка = "Упорядочить по убыванию" Или
					ТекКнопка.Пояснение = "Упорядочить список по убыванию" Или
					ТекКнопка.Текст = "Сортировать по убыванию") Тогда
					ЕстьКнопкаСортировкиУбыв = Истина;
					КнопкаСортировкиУбыв = ТекКнопка;
				КонецЕсли;
				
				Если КоманднаяПанельКнопкиСортировки.АвтоЗаполнение Тогда
					СтруктураКнопки = Новый Структура;
					СтруктураКнопки.Вставить("Имя", 				ТекКнопка.Имя);
					СтруктураКнопки.Вставить("ТипКнопки", 			ТекКнопка.ТипКнопки);
					СтруктураКнопки.Вставить("ИзменяетДанные", 		ТекКнопка.ИзменяетДанные);
					СтруктураКнопки.Вставить("Доступность", 		ТекКнопка.Доступность);
					СтруктураКнопки.Вставить("Пометка", 			ТекКнопка.Пометка);
					СтруктураКнопки.Вставить("Действие", 			ТекКнопка.Действие);
					СтруктураКнопки.Вставить("Текст", 				ТекКнопка.Текст);
					СтруктураКнопки.Вставить("Подсказка", 			ТекКнопка.Подсказка);
					СтруктураКнопки.Вставить("Пояснение", 			ТекКнопка.Пояснение);
					СтруктураКнопки.Вставить("Картинка", 			ТекКнопка.Картинка);
					СтруктураКнопки.Вставить("Отображение", 		ТекКнопка.Отображение);
					СтруктураКнопки.Вставить("СочетаниеКлавиш", 	ТекКнопка.СочетаниеКлавиш);
					СтруктураКнопки.Вставить("КнопкаПоУмолчанию", 	ТекКнопка.КнопкаПоУмолчанию);
					СтруктураКнопки.Вставить("ПорядокКнопок", 		ТекКнопка.ПорядокКнопок);
					СтруктураКнопки.Вставить("Кнопка", 			ТекКнопка);
					МассивСтандарныхКнопок.Добавить(СтруктураКнопки);
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьКнопкаСортировкиВозр Или ЕстьКнопкаСортировкиУбыв Тогда
				Если КоманднаяПанельКнопкиСортировки.АвтоЗаполнение Тогда
					КоманднаяПанельКнопкиСортировки.АвтоЗаполнение = Ложь;
					
					Для Каждого ТекКнопка Из МассивСтандарныхКнопок Цикл
						Если (ТекКнопка.Кнопка = КнопкаСортировкиВозр) Тогда
							Кнопка = КоманднаяПанельКнопкиСортировки.Кнопки.Вставить(Итератор, Строка(КоманднаяПанельКнопкиСортировки.ИсточникДействий.Имя)+"_РасширеннаяСортировка_Возр",ТипКнопкиКоманднойПанели.Действие,ТекКнопка.Текст,Новый Действие("ДополнительныеДействияФормы"));
							Кнопка.Картинка		= БиблиотекаКартинок.СортироватьСписокПоВозрастанию;
							
						ИначеЕсли (ТекКнопка.Кнопка = КнопкаСортировкиУбыв) Тогда
							Кнопка = КоманднаяПанельКнопкиСортировки.Кнопки.Вставить(Итератор, Строка(КоманднаяПанельКнопкиСортировки.ИсточникДействий.Имя)+"_РасширеннаяСортировка_Убыв",ТипКнопкиКоманднойПанели.Действие,ТекКнопка.Текст,Новый Действие("ДополнительныеДействияФормы"));
							Кнопка.Картинка		= БиблиотекаКартинок.СортироватьСписокПоУбыванию;
							
						Иначе
							Кнопка = КоманднаяПанельКнопкиСортировки.Кнопки.Вставить(Итератор, ТекКнопка.Имя,ТипКнопкиКоманднойПанели.Действие,ТекКнопка.Текст,ТекКнопка.Действие);
							Кнопка.Картинка		= ТекКнопка.Картинка;
							Если ТекКнопка.Подсказка="Добавить" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.ДобавитьЭлементСписка;
							ИначеЕсли ТекКнопка.Подсказка="Добавить копированием" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.СкопироватьЭлементСписка;
							ИначеЕсли ТекКнопка.Подсказка="Изменить текущий элемент" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.ИзменитьЭлементСписка;
							ИначеЕсли ТекКнопка.Подсказка="Удалить текущий" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.УдалитьЭлементСписка;
							ИначеЕсли ТекКнопка.Подсказка="Закончить редактирование" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.ЗакончитьРедактирование;
							ИначеЕсли ТекКнопка.Подсказка="Переместить вверх" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.ПереместитьВверх;
							ИначеЕсли ТекКнопка.Подсказка="Переместить вниз" Тогда
								Кнопка.Картинка    = БиблиотекаКартинок.ПереместитьВниз;
							КонецЕсли;
						КонецЕсли;
						
						Кнопка.Отображение	= ТекКнопка.Отображение;
						Кнопка.Подсказка	= ТекКнопка.Подсказка;
						Кнопка.Пояснение	= ТекКнопка.Пояснение;
						Кнопка.Доступность	= ТекКнопка.Доступность;
						
						Итератор = Итератор +1;
					КонецЦикла;
				Иначе
					Если ЕстьКнопкаСортировкиВозр Тогда
						ТекКнопка = КнопкаСортировкиВозр;
						ИндексКнопки = КоманднаяПанельКнопкиСортировки.Кнопки.Индекс(ТекКнопка);
						Кнопка = КоманднаяПанельКнопкиСортировки.Кнопки.Вставить(ИндексКнопки, Строка(КоманднаяПанельКнопкиСортировки.ИсточникДействий.Имя)+"_РасширеннаяСортировка_Возр",ТипКнопкиКоманднойПанели.Действие,ТекКнопка.Текст,Новый Действие("ДополнительныеДействияФормы"));
						Кнопка.Картинка		= БиблиотекаКартинок.СортироватьСписокПоВозрастанию;
						Кнопка.Отображение	= ТекКнопка.Отображение;
						Кнопка.Подсказка	= ТекКнопка.Подсказка;
						Кнопка.Пояснение	= ТекКнопка.Пояснение;
						Кнопка.Доступность	= ТекКнопка.Доступность;
						КоманднаяПанельКнопкиСортировки.Кнопки.Удалить(ТекКнопка);
					КонецЕсли;
					Если ЕстьКнопкаСортировкиУбыв Тогда
						ТекКнопка = КнопкаСортировкиУбыв;
						ИндексКнопки = КоманднаяПанельКнопкиСортировки.Кнопки.Индекс(ТекКнопка);
						Кнопка = КоманднаяПанельКнопкиСортировки.Кнопки.Вставить(ИндексКнопки, Строка(КоманднаяПанельКнопкиСортировки.ИсточникДействий.Имя)+"_РасширеннаяСортировка_Убыв",ТипКнопкиКоманднойПанели.Действие,ТекКнопка.Текст,Новый Действие("ДополнительныеДействияФормы"));
						Кнопка.Картинка		= БиблиотекаКартинок.СортироватьСписокПоУбыванию;
						Кнопка.Отображение	= ТекКнопка.Отображение;
						Кнопка.Подсказка	= ТекКнопка.Подсказка;
						Кнопка.Пояснение	= ТекКнопка.Пояснение;
						Кнопка.Доступность	= ТекКнопка.Доступность;
						КоманднаяПанельКнопкиСортировки.Кнопки.Удалить(ТекКнопка);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// добавление кнопки заметки
	Если ЭтоФормаСписка ИЛИ ЭтоФормаЖурнала ИЛИ ЭтоФормаОбъекта Тогда
		ДоступностьЗаметки = Истина;
		Если НЕ ЭтоФормаЖурнала Тогда
			Если ЭтоФормаСписка Тогда
				ТипДанных = Тип("ДокументСсылка." + Метаданные.НайтиПоТипу(ТипЗнч(Список)).Имя);
			ИначеЕсли Метаданные.Документы.Содержит(ЭтаФормаЭтотОбъект.Метаданные()) Тогда
				ТипДанных = Тип("ДокументСсылка." + Метаданные.НайтиПоТипу(ТипЗнч(ЭтаФормаЭтотОбъект.Ссылка)).Имя);
				Если обЗначениеНеЗаполнено(ЭтаФормаЭтотОбъект.Ссылка) Тогда
					ДоступностьЗаметки = Ложь;
				КонецЕсли;
			Иначе
				ТипДанных = Тип("Строка");
			КонецЕсли;
		Иначе
			ТипДанных = Тип("Строка");
		КонецЕсли;
		Если Метаданные.РегистрыСведений.ЗаметкиПользователей.Измерения.Объект.Тип.СодержитТип(ТипДанных) ИЛИ ЭтоФормаЖурнала Тогда
			КнопкиДействийФормы = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки;
			КнопкаЗаметок = КнопкиДействийФормы.Найти("кнЗаметки");
			Если КнопкаЗаметок = Неопределено Тогда
				КнопкиДействийФормы.Добавить("ЗаметкиРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
				КнопкаЗаметок             = КнопкиДействийФормы.Добавить("кнЗаметки",ТипКнопкиКоманднойПанели.Действие,"Заметки",Новый Действие("ДополнительныеДействияФормы"));
				КнопкаЗаметок.Картинка    = БиблиотекаКартинок.ЗаметкиСоздать;
				КнопкаЗаметок.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
				КнопкаЗаметок.Доступность = ДоступностьЗаметки;
				КнопкаЗаметок.Подсказка   = "Открыть список заметок";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// ищем подменю "Действия"
	ПодменюДействия = Неопределено;
	Для НомерКнопки = 1 По ДействияФормы.Количество() Цикл
		ТекПодменю = ДействияФормы.Получить(НомерКнопки-1);
		Если (ТекПодменю.ТипКнопки = ТипКнопкиКоманднойПанели.Подменю) И (ТекПодменю.Текст = "Действия") Тогда
			ПодменюДействия = ТекПодменю;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ПодменюДействия <> Неопределено Тогда
		Попытка	// Может возникнуть ситуация что форма программно открывается повторно, в этом случае все кнопки уже созданы!		
			
			// добавляем кнопку включения возможности редактирования кода справочника
			РедактированиеНомеров = обПраво("РазрешитьРедактированиеНомеровДокументов",ПраваПользователя,,ЭтаФормаЭтотОбъект); 
			Если НЕ ЭтаФорма.ТолькоПросмотр И РедактированиеНомеров = Перечисления.ВариантыОтветов.Спрашивать Тогда
				//проверим наличие номера на форме
				Номер = ЭтаФорма.ЭлементыФормы.Найти("Номер");
				//добавим разделитель и кнопку
				Если Номер <> Неопределено И ЭтоФормаОбъекта Тогда
				    // разделитель
				  	КнопкиПодменюДействия = ПодменюДействия.Кнопки;
					КнопкиПодменюДействия.Вставить(0,"РедактированиеНомеровРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
					// кнопка
					Кнопка = КнопкиПодменюДействия.Найти("РедактироватьНомер");
					Если Кнопка = Неопределено Тогда
						Кнопка = КнопкиПодменюДействия.Вставить(0,"РедактироватьНомер",ТипКнопкиКоманднойПанели.Действие,"Редактировать номер",Новый Действие("ДополнительныеДействияФормы"));
						Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
						Кнопка.Подсказка   = "Включить возможность редактирования номера";
					КонецЕсли; 
				КонецЕсли;	
			КонецЕсли;				
			
			// добавляем кнопки для вызова дополнительных действий
			КнопкиПодменюДействия = ПодменюДействия.Кнопки;
			КнопкиПодменюДействия.Вставить(0,"ДопВозможностиРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
			
			// добавляем кнопку создания письма в техническую поддержку 1С-Раруса Альфа-Авто
			Кнопка = КнопкиПодменюДействия.Найти("СоздатьПисьмоВТехПоддержку");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"СоздатьПисьмоВТехПоддержку",ТипКнопкиКоманднойПанели.Действие,"Написать письмо в службу техподдержки",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.НовоеПисьмо;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Написать письмо в службу тех.поддержки ООО ""1С-Рарус Альфа-Авто""";
			КонецЕсли; 
			
			// добавляем кнопку создания напоминания
			Кнопка = КнопкиПодменюДействия.Найти("СоздатьНапоминание");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"СоздатьНапоминание",ТипКнопкиКоманднойПанели.Действие,"Создать напоминание",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.Оповещение;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Создать напоминание";
			КонецЕсли;
			
			// добавляем кнопку добавления в шаблоны если есть такое право
			РазрешитьВыгрузкуВШаблон	= обПраво("РазрешитьВыгрузкуВШаблон",ПраваПользователя,,ЭтаФормаЭтотОбъект);
			РазрешитьЗагрузкуИзШаблона	= обПраво("РазрешитьЗагрузкуИзШаблона",ПраваПользователя,,ЭтаФормаЭтотОбъект);
			Если РазрешитьВыгрузкуВШаблон ИЛИ РазрешитьЗагрузкуИзШаблона Тогда
				Кнопка = КнопкиПодменюДействия.Найти("Шаблоны");
				Если Кнопка = Неопределено Тогда
					КнопкаШаблоны = КнопкиПодменюДействия.Вставить(0,"Шаблоны",ТипКнопкиКоманднойПанели.Подменю,"Шаблоны");
					КнопкаШаблоны.Картинка    = БиблиотекаКартинок.НастрИзменить;
					КнопкаШаблоны.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					
					Кнопка = КнопкаШаблоны.Кнопки.Добавить("ВыгрузитьВШаблон",ТипКнопкиКоманднойПанели.Действие,"Выгрузить в шаблон",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.НастрСохранить;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Выгрузить в шаблон";
					Кнопка.Доступность = РазрешитьВыгрузкуВШаблон;
					
					Кнопка = КнопкаШаблоны.Кнопки.Добавить("ЗагрузитьИзШаблона",ТипКнопкиКоманднойПанели.Действие,"Загрузить из шаблона",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.НастрВосстановить;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Загрузить из шаблона";
					Кнопка.Доступность = РазрешитьЗагрузкуИзШаблона;
				КонецЕсли;
			КонецЕсли;
			
			// добавляем кнопку выгрузки в табличный документ
			Кнопка = КнопкиПодменюДействия.Найти("ВыгрузкаВТабличныйДокумент");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ВыгрузкаВТабличныйДокумент",ТипКнопкиКоманднойПанели.Действие,"Выгрузка в табличный документ",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ТабличныйДокумент;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Выгрузка в табличный документ";
			КонецЕсли;
			
			// Добовляем кнопку выгрузки в буху
			// Нужно узнать добовлять ету кнопку
			
			Если удВыгружаетсяВБухгалтерию(ЭтаФорма, ЭтоФормаСписка, ЭтоФормаЖурнала, Список) Тогда
				Кнопка = КнопкиПодменюДействия.Найти("ВыгрузкаВБухгалтерию");
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0,"ВыгрузкаВБухгалтерию",ТипКнопкиКоманднойПанели.Действие,"Выгрузка в бухгалтерию",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ДебетКредит;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Выгрузка в бухгалтерию";
				КонецЕсли;
			КонецЕсли;
			
			// добавим кнопку альтернативного редактирования
			Если удЕстьВозможностьАльтернативногоРедактирования(ЭтаФорма, ЭтоФормаСписка, ЭтоФормаЖурнала, Список) Тогда
				Кнопка = КнопкиПодменюДействия.Найти("АльтернативноеРедактирование");
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0,"АльтернативноеРедактирование",ТипКнопкиКоманднойПанели.Действие,"Альтернативное редактирование",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.РедактироватьВДиалоге;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Альтернативное редактирование документа";
				КонецЕсли;
			КонецЕсли;
			
			// добавим кнопку поиска контрагентов
			Если НЕ ЭтоФормаСписка Тогда
				Кнопка = КнопкиПодменюДействия.Найти("ИскатьКонтрагентов");
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0,"ИскатьКонтрагентов",ТипКнопкиКоманднойПанели.Действие,"Поиск контрагентов",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.Контрагенты;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Поиск контрагентов";
					Кнопка.СочетаниеКлавиш=Новый СочетаниеКлавиш(Клавиша.F,Истина,Ложь,Истина);
				КонецЕсли;
			КонецЕсли;

			// добавляем кнопку выведения истории объекта
			Кнопка = КнопкиПодменюДействия.Найти("ИсторияОбъекта");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ИсторияОбъекта",ТипКнопкиКоманднойПанели.Действие,"История объекта",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ЖурналРегистрацииКонфигурации;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "История объекта";
			КонецЕсли;
			
			// добавляем кнопку поиска ссылок
			Кнопка = КнопкиПодменюДействия.Найти("ПоискСсылок");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ПоискСсылок",ТипКнопкиКоманднойПанели.Действие,"Поиск ссылок",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ПоискСсылок;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Поиск ссылок на объекты использующие данный объект и используемых в данном объекте";
			КонецЕсли;
			
			// добавляем кнопку добавления в избранное
			Кнопка = КнопкиПодменюДействия.Найти("ДобавитьВИзбранное");
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПодменюДействия.Вставить(0,"ДобавитьВИзбранное",ТипКнопкиКоманднойПанели.Действие,"Добавить в избранное",Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка    = БиблиотекаКартинок.ДобавитьВИзбранное;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка   = "Добавить в избранное";
			КонецЕсли;
			
			// добавляем кнопку поиска по номеру документа основания 
			ТипДанных = Метаданные.НайтиПоТипу(ТипЗнч(Список));
			ЕстьВводНаОсновании = Ложь;
			Если ЭтоФормаЖурнала Тогда
				Для Каждого Документ Из ТипДанных.РегистрируемыеДокументы Цикл
					Для Каждого Док Из Метаданные.Документы Цикл
						Если Док.ВводитсяНаОсновании.Содержит(Документ) Тогда
							ЕстьВводНаОсновании=Истина; Прервать;
						КонецЕсли; 
					КонецЦикла;
					Если ЕстьВводНаОсновании Тогда
						Прервать;
					КонецЕсли; 
				КонецЦикла;
			Иначе
				Если НЕ ЭтоФормаСписка Тогда
					ТипДанных = Метаданные.НайтиПоТипу(ТипЗнч(ЭтаФорма.ДокументОбъект));
				КонецЕсли; 
				Для Каждого Док Из Метаданные.Документы Цикл
					Если Док.ВводитсяНаОсновании.Содержит(ТипДанных) Тогда
						ЕстьВводНаОсновании=Истина; Прервать;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли; 
			Если ЕстьВводНаОсновании Тогда
				Кнопка = КнопкиПодменюДействия.Найти("ПоискПоНомеруДокументаОснования");
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0,"ПоискПоНомеруДокументаОснования",ТипКнопкиКоманднойПанели.Действие,"Поиск по номеру документа основания",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ПоискПоНомеру;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Поиск по номеру документа основания";
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	// добавляем картинки и файлы
	удФайлыИКартинкиОбновитьПодменю(ЭтаФорма);
	// добавляем подменю Утверждение
	дкЗаполнитьСписокУтверждение(ЭтаФорма, , ПраваПользователя, ЭтоФормаСписка ИЛИ ЭтоФормаЖурнала);
	
	//KamikadZe begin add 21.04.2015 14:14:50
	обОбщие.обСформироватьПодменюДействий(ЭтаФорма);
	//KamikadZe end add
	
КонецПроцедуры // удЗаполнитьДополнительныеДействияДокументов()

// Процедура выполняет сворачивание/разворачивание элементов форм
//
// Параметры:
//  Видимость         - Булево, признак "развернутости" элемент. 
//  ГлавныйЭлемент    - Элемент формы который сворачиваем.
//  Главный разделить - Разделитель.
//  Панель формы      - панель или главная панель формы.
//  Направление       - Строка, направление сворачивания.
//  Пропорционально   - Булево - Истина, если разделитель привязывается пропорционально
//
Процедура удУправлениеВидимостиЭлементов(Видимость, ГлавныйЭлемент, ГлавныйРазделитель, ПанельФормы, Направление, Пропорционально = Истина) Экспорт
	Если ВРег(Направление) =  "ЛЕВО" Тогда
		Если Видимость Тогда
			// откроем 
			ГлавныйРазделитель.Свертка=РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка=РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Лево);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Право,ГлавныйРазделитель,ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.Ширина = 4;
		Иначе	
			// скроем 
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйЭлемент,ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право);
			ГлавныйЭлемент.Свертка=РежимСверткиЭлементаУправления.Лево;
			ГлавныйРазделитель.Свертка=РежимСверткиЭлементаУправления.Лево;
		КонецЕсли;
	ИначеЕсли ВРег(Направление) = "ПРАВО" Тогда
		
		Если Видимость Тогда
			// Показать
			ГлавныйРазделитель.Свертка = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка = РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право,  ПанельФормы, ГраницаЭлементаУправления.Лево);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.Ширина = 4;
		Иначе 
			//скрыть 
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ГлавныйЭлемент, ГраницаЭлементаУправления.Лево);
			ГлавныйЭлемент.Свертка = РежимСверткиЭлементаУправления.Право;
			ГлавныйРазделитель.Свертка = РежимСверткиЭлементаУправления.Право;
		КонецЕсли;	
		
	ИначеЕсли ВРег(Направление) = "НИЗ" Тогда
		
		Если Видимость Тогда
			// Откроем
			ГлавныйРазделитель.Свертка  = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка		= РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Верх);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Низ);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			ГлавныйРазделитель.Высота 	= 4;
		Иначе // Скроем 
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх);
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ГлавныйЭлемент,ГраницаЭлементаУправления.Верх);
			ГлавныйЭлемент.Свертка 		= РежимСверткиЭлементаУправления.Низ;		
			ГлавныйРазделитель.Свертка	= РежимСверткиЭлементаУправления.Низ;		
		КонецЕсли;
		
	ИначеЕсли ВРег(Направление) = "ВЕРХ" Тогда
		Если Видимость Тогда
			// Откроем
			ГлавныйРазделитель.Свертка  = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка		= РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Верх);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Низ);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ГлавныйРазделитель,ГраницаЭлементаУправления.Верх);
			ГлавныйРазделитель.Высота 	= 4;
		Иначе // Скроем 
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ);
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Низ);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйЭлемент, ГраницаЭлементаУправления.Низ);
			ГлавныйЭлемент.Свертка 		= РежимСверткиЭлементаУправления.Верх;		
			ГлавныйРазделитель.Свертка	= РежимСверткиЭлементаУправления.Верх;		
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры	// обУправлениеВидимостиЭлементов()

//Растановка доступности элементов, влияющие на модифицированность формы
//
// Параметры:
//  ЭтаФорма - Форма - форма в которой производим расстановку доступности. 
//	Доступность - Булево - значение признака доступности
//	ЭлементПанели - командная панель - командная панель, в которой требуется установить доступность кнопок
//
Процедура удУправлениеДоступностьюЭлементовФормы(ЭтаФорма, Доступность, ЭлементПанели=Неопределено) Экспорт
	
	Если ЭлементПанели=Неопределено Тогда
		МетаданныеОбъекта = ЭтаФорма.Метаданные();
		ТипПолеВвода = Тип("ПолеВвода");
		ТипКоманднаяПанель = Тип("КоманднаяПанель");
		ТипТабличноеПоле = Тип("ТабличноеПоле");
		Для Каждого ТекЭлемент Из ЭтаФорма.ЭлементыФормы Цикл
			ТипЭлемента = ТипЗнч(ТекЭлемент);
			Если ТипЭлемента = ТипКоманднаяПанель Тогда
				удУправлениеДоступностьюЭлементовФормы(ЭтаФорма, Доступность, ТекЭлемент);
			ИначеЕсли ТипЭлемента = ТипТабличноеПоле И 
				(НЕ МетаданныеОбъекта.ТабличныеЧасти.Найти(ТекЭлемент.Данные)=Неопределено ИЛИ ТекЭлемент.ИзменяетДанные) Тогда
				ТекЭлемент.ТолькоПросмотр = (НЕ Доступность);
			ИначеЕсли (НЕ МетаданныеОбъекта.Реквизиты.Найти(ТекЭлемент.Данные)=Неопределено ИЛИ ТекЭлемент.ИзменяетДанные) Тогда
				Если ТипЭлемента = ТипПолеВвода Тогда
					
					//для полей код и номер доступность определяется соотв. правами
					Если (ТекЭлемент.Имя = "Код" ИЛИ ТекЭлемент.Имя = "Номер") И Доступность Тогда
						Продолжить;
					КонецЕсли;
					
					ТекЭлемент.ТолькоПросмотр = (НЕ Доступность);
				Иначе
					ТекЭлемент.Доступность = Доступность;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		//Управление доступностью командной панели
	ИначеЕсли ЭлементПанели.ИзменяетДанные Тогда
		ЭлементПанели.Доступность = Доступность;
	ИначеЕсли ТипЗнч(ЭлементПанели.Кнопки) = Тип("КнопкиКоманднойПанели") Тогда
		Для Каждого ТекЭлемент Из ЭлементПанели.Кнопки Цикл
			удУправлениеДоступностьюЭлементовФормы(ЭтаФорма, Доступность, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура изменяет признак формат текста письма (Текст, HТМЛ),
// и при этом конвертирует сам текст.
//
// Параметры:
//  ЭУHTML,ЭУТекст - Элементы управления формы - соответственно поле HTML и поле Текст
//  Кнопка         - Конпка - выбранная кнопка ее текст "Простой текст" или "HTML" новый вид текста письма
//                   либо текст кнопки если пометку кнопки менять не требуется, соответственно 
//                   вопрос о потере форматирования задаваться не будет
// Возвращаемое значение:
//  Булево - признак выполнения изменения формата
//
Функция удИзменитьФорматТекста(ЭУHTML,ЭУТекст, Кнопка) Экспорт
	Если ТипЗнч(Кнопка) = Тип("Строка") Тогда
		НовыйВидТекстаПисьма = Кнопка;
		ЕстьКнопка = Ложь;
	Иначе
		Если Кнопка.Пометка Тогда 
			Возврат Ложь; 
		КонецЕсли;
		НовыйВидТекстаПисьма = Кнопка.Текст;		
		ЕстьКнопка = Истина;
	КонецЕсли; 	
		
	Если Найти(НовыйВидТекстаПисьма,"Простой текст") > 0 Тогда  		
		ИсходныйТекст = ЭУHTML.ПолучитьТекст();
		НачалоBODY = Найти(ИсходныйТекст, "<BODY>");
		КонецBODY  = Найти(ИсходныйТекст, "</BODY>");
		Если ЕстьКнопка И (НачалоBODY > 0 И КонецBODY > 0 И (НачалоBODY + 6) < КонецBODY) Тогда
			СтрокаВопроса = "Будет потеряно форматирование текста. Продолжить?";
			ОтветНаВопрос = Вопрос(СтрокаВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда				
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		ФорматТекста = ЭУHTML.Документ.all.item(0).innerText; 			
		ЭУТекст.Значение =  СтрЗаменить(ФорматТекста, Символ(13), "");
		ЭУТекст.Видимость = Истина;
		ЭУHTML.Видимость = Ложь;
		ЭУHTML.УстановитьТекст("");						
	Иначе
		ФорматХТМЛ = обПреобразоватьТекстВHTML(ЭУТекст.Значение);
	
		ЭУHTML.УстановитьТекст(ФорматХТМЛ);
		ЭУHTML.Видимость = Истина;
		ЭУТекст.Видимость = Ложь;
	КонецЕсли; 
	
	Если ЕстьКнопка Тогда
		Кнопка.Пометка = Истина; 		
	КонецЕсли;   
	Возврат Истина;		 			

КонецФункции // обИзменитьФорматТекста()

// Начало выбора значения свойства объектов в панели свойств
//
// Параметры
//  Объект  – Объект, свойства которого редактируются (справочник, документ и т.д.)
//  СвойстваОбъекта  – ТЧ – Табличная часть свойств объекта
//  Свойство  – ПВХСсылка.СвойстваОбъектов – Ссылка на свойство объекта, значение которого редактируется
//  Элемент  – поле ввода – Поле ввода, в котором осуществляется редактирование свойства
//  СтандартнаяОбработка  – Булево – Признак стандартной обработки начала выбора значения свойства
//	глСтруктураОтбора - Глобальная переменная со структурой отбора для выбора
//
Процедура удСвойстваОбъектаЗначениеНачалоВыбора(Объект, СвойстваОбъекта, Свойство, Элемент, СтандартнаяОбработка, глСтруктураОтбора) Экспорт
	Результат=орСвойстваОбъектаЗначениеНачалоВыбора(Объект, СвойстваОбъекта, Свойство, Элемент, СтандартнаяОбработка, глСтруктураОтбора);
	Если Результат<>Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если Свойство=ПланыВидовХарактеристик.СвойстваОбъектов.Доверенность Тогда
		СвойствоДоверенноеЛицоСтрока=СвойстваОбъекта.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДоверенноеЛицо,"Свойство");
		Если СвойствоДоверенноеЛицоСтрока=Неопределено Тогда
			СвойствоДоверенноеЛицо=Неопределено;
		Иначе
			СвойствоДоверенноеЛицо=СвойствоДоверенноеЛицоСтрока.Значение;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(СвойствоДоверенноеЛицо) Тогда
			Элемент.ВыборПоВладельцу=СвойствоДоверенноеЛицо;
		Иначе
			СвойствоГрузополучательСтрока=СвойстваОбъекта.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.Грузополучатель,"Свойство");
			Если СвойствоГрузополучательСтрока=Неопределено Тогда
				СвойствоГрузополучатель=Неопределено;
			Иначе
				СвойствоГрузополучатель=СвойствоГрузополучательСтрока.Значение;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(СвойствоГрузополучатель) Тогда
				Если ТипЗнч(Объект)=Тип("ДокументОбъект.ВозвратПоставщику") Тогда
					СвойствоГрузополучатель=Объект.Контрагент;
				ИначеЕсли ТипЗнч(Объект)=Тип("ДокументОбъект.ОтчетКомиссионера") Тогда
					СвойствоГрузополучатель=Объект.Контрагент;
				ИначеЕсли ТипЗнч(Объект)=Тип("ДокументОбъект.ПоступлениеДопРасходов") Тогда
					СвойствоГрузополучатель=Объект.Организация;
				ИначеЕсли ТипЗнч(Объект)=Тип("ДокументОбъект.ПоступлениеТоваров") Тогда
					СвойствоГрузополучатель=Объект.Организация;
				ИначеЕсли ТипЗнч(Объект)=Тип("ДокументОбъект.РеализацияТоваров") Тогда
					СвойствоГрузополучатель=Объект.Контрагент;
				ИначеЕсли ТипЗнч(Объект)=Тип("ДокументОбъект.Чек") Тогда
					СвойствоГрузополучатель=Объект.Контрагент;
				Иначе
					Попытка
						СвойствоГрузополучатель=Объект.Контрагент;
					Исключение
					КонецПопытки; 
				КонецЕсли; 
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(СвойствоГрузополучатель) Тогда
				Элемент.ВыборПоВладельцу=СвойствоГрузополучатель;
			КонецЕсли;
		КонецЕсли;
		Если глСтруктураОтбора=Неопределено Тогда
			глСтруктураОтбора = Новый Структура;
		КонецЕсли; 
		глСтруктураОтбора.Вставить("ВидПодтверждающегоДокумента", Перечисления.ВидыДокументов.Доверенность);
		
	Иначе
	КонецЕсли; 
КонецПроцедуры // удСвойстваОбъектаЗначениеНачалоВыбора()

// Обработчик изменения значения свойства объекта в панели свойств
//
// Параметры
//  Объект  – Объект, свойства которого редактируются (справочник, документ и т.д.)
//  СвойстваОбъекта  – ТЧ – Табличная часть свойств объекта
//  Свойство  – ПВХСсылка.СвойстваОбъектов – Ссылка на свойство объекта, значение которого редактируется
//  Элемент  – поле ввода – Поле ввода, в котором осуществляется редактирование свойства
//
Процедура удСвойстваОбъектаЗначениеПриИзменении(Объект, СвойстваОбъекта, Свойство, Элемент) Экспорт
	Результат=орСвойстваОбъектаЗначениеПриИзменении(Объект, СвойстваОбъекта, Свойство, Элемент);
	Если Результат<>Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если Свойство=ПланыВидовХарактеристик.СвойстваОбъектов.Доверенность Тогда
		Если НЕ обЗначениеНеЗаполнено(Элемент.Значение) Тогда
			Если Элемент.Значение.ВидПодтверждающегоДокумента<>Перечисления.ВидыДокументов.Доверенность Тогда
				Элемент.Значение=Справочники.ПодтверждающиеДокументы.ПустаяСсылка();
			КонецЕсли; 
		КонецЕсли; 
		
	Иначе
	КонецЕсли; 
КонецПроцедуры // удСвойстваОбъектаЗначениеПриИзменении()

// Создание и заполнение меню на командной панели формы
//
// Параметры
//  ЭтаФорма - Форма, на командной панели которой создается кнопка (заполняется меню)
//
Процедура удФайлыИКартинкиОбновитьПодменю(ЭтаФорма) Экспорт
	
	Если НЕ обПраво("ПроверятьНаличиеФайловОткрываемыхЭлементов") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоФормыСписка = Ложь;
	Попытка
		Попытка
			Ссылка = ЭтаФорма.ЭтотОбъект.Ссылка;
		Исключение
			Ссылка = Документы[Метаданные.НайтиПоТипу(ТипЗнч(ЭтаФорма.ЭлементыФормы.Список.Значение)).Имя].ПустаяСсылка(); 
		КонецПопытки;
		
		Если НЕ РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей().Метаданные().Измерения.Объект.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
			Возврат;		
		КонецЕсли;
	Исключение
		Попытка
			// Значит открывается форма журнала
			Список         = ЭтаФорма.ЭлементыФормы.Список;
			ЭтоФормыСписка = Истина;
		Исключение
			Возврат;
		КонецПопытки;
	КонецПопытки;
	
	ДействияФормы = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки;
	
	// удалим старые кнопки и создадим новые
	Кнопка = ДействияФормы.Найти("КартинкиИФайлы");
	Если НЕ Кнопка = Неопределено Тогда
		ДействияФормы.Удалить(Кнопка);
	КонецЕсли;
	
	Кнопка = ДействияФормы.Найти("КартинкиИФайлыПодменю");
	Если НЕ Кнопка = Неопределено Тогда
		ДействияФормы.Удалить(Кнопка);
	КонецЕсли;
	
	Кнопка = ДействияФормы.Найти("КартинкиИФайлыРазделитель");
	Если НЕ Кнопка = Неопределено Тогда
		ДействияФормы.Удалить(Кнопка);
	КонецЕсли;
	
	// Добавляем в командную панель разделитель.
	ДействияФормы.Добавить("КартинкиИФайлыРазделитель",ТипКнопкиКоманднойПанели.Разделитель);
	
	// Если открыта форма документа, то проверим, если уже для данного документа какие-нибудь картинки или файлы.
	// Если есть, то создадим подменю, каждый пункт которого будет ссылаться на конкретный экземпляр картинки или файла.
	Если НЕ ЭтоФормыСписка Тогда
		// получим СписокФайловКартинок
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	КартинкиИФайлы.Идентификатор КАК Идентификатор,
		|	КартинкиИФайлы.ИмяФайла      КАК ИмяФайла
		|ИЗ
		|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		|ГДЕ
		|	КартинкиИФайлы.Объект = &ОбъектИнформации
		|АВТОУПОРЯДОЧИВАНИЕ");
		Запрос.УстановитьПараметр("ОбъектИнформации", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если НЕ Выборка.Количество()=0 Тогда
			
			Кнопка = ДействияФормы.Добавить("КартинкиИФайлыПодменю",ТипКнопкиКоманднойПанели.Подменю,"Картинки и файлы",Новый Действие("ДополнительныеДействияФормы"));
			Кнопка.Картинка    = БиблиотекаКартинок.КартинкиИФайлы;
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Подсказка   = "Открыть картинки и файлы";
			
			//Заполним подменю файлами и картинками
			Пока Выборка.Следующий() Цикл
				КнопкаПодменю = Кнопка.Кнопки.Добавить("КартинкиИФайлы_" + Выборка.Идентификатор,ТипКнопкиКоманднойПанели.Действие,рфПолучитьИмяФайлаИзПолногоИмени(Выборка.ИмяФайла),Новый Действие("ДополнительныеДействияФормы"));
				КнопкаПодменю.Подсказка   = "Открыть " + Выборка.ИмяФайла;
			КонецЦикла;
			ДействияФормы = Кнопка.Кнопки;
			ДействияФормы.Добавить("КартинкиИФайлыРазделительПодменю",ТипКнопкиКоманднойПанели.Разделитель);
		КонецЕсли;
	КонецЕсли;
	
	Кнопка = ДействияФормы.Добавить("КартинкиИФайлы",ТипКнопкиКоманднойПанели.Действие,"Картинки и файлы",Новый Действие("ДополнительныеДействияФормы"));
	Кнопка.Картинка    = БиблиотекаКартинок.КартинкиИФайлы;
	Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Авто;
	Кнопка.Подсказка   = "Открыть картинки и файлы"; 				
	
КонецПроцедуры

// Функция проверяет подлежит ли данный объект выгрузке в бухгалтерии.
// Параметры:
//	ЭтаФорма - Вызывающая форма
//	ЭтоСписок - Признак формы списка
//	ЭтоЖурнал - Признак формы журнала
//	Список    - Объект списка формы
//	ИмяОбъекта - Имя проверяемого объекта
//
//	Возвращает истину если объект может быть выгружен, иначе ложь
//
Функция удВыгружаетсяВБухгалтерию(ЭтаФорма=Неопределено, ЭтоСписок=Ложь, ЭтоЖурнал=Ложь, Список=Неопределено, ИмяОбъекта="") Экспорт
	Если ЭтаФорма = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	ОбработкаВыгрузки=Обработки.ВыгрузкаДанныхБух.Создать();
	ОбработкаВыгрузки.ИнициализироватьВидыСправочников();
	ОбработкаВыгрузки.ИнициализироватьВидыДокументов();
	ТабСправочников=ОбработкаВыгрузки.ВидыСправочников;
	ТабДокументов=ОбработкаВыгрузки.ВидыДокументов;
	
	// Составим структуру объектов
	стрОбъектов=Новый Структура;
	Если ЭтоСписок Тогда
		МетаданныеОбъекта=Метаданные.НайтиПоТипу(ТипЗнч(Список));
		стрОбъектов.Вставить(МетаданныеОбъекта.Имя);
	ИначеЕсли ЭтоЖурнал Тогда
		МетаданныеОбъекта=Метаданные.НайтиПоТипу(ТипЗнч(Список));
		Для Каждого РегДокумент Из МетаданныеОбъекта.РегистрируемыеДокументы Цикл
			стрОбъектов.Вставить(Регдокумент.Имя);
		КонецЦикла;
	ИначеЕсли НЕ ПустаяСтрока(ИмяОбъекта) Тогда
		стрОбъектов.Вставить(ИмяОбъекта);
	Иначе
		МетаданныеОбъекта=ЭтаФорма.ЭтотОбъект.Метаданные();
		стрОбъектов.Вставить(МетаданныеОбъекта.Имя);
	КонецЕсли;
	
	// определим выгружать или не выгружать
	Выгружать=Ложь;
	Для Каждого Объект Из стрОбъектов Цикл
		Если ТабСправочников.НайтиСтроки(Новый Структура("Объект",Объект.Ключ)).Количество()>0 ИЛИ ТабДокументов.НайтиСтроки(Новый Структура("Объект",Объект.Ключ)).Количество()>0 Тогда
			Выгружать=Истина;
		КонецЕсли;
		Если Выгружать Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Выгружать;
КонецФункции

// Функция проверяет подлежит доступно ли для объекта альтернативное редактирование.
// Параметры:
//	ЭтаФорма - Вызывающая форма
//	ЭтоСписок - Признак формы списка
//	ЭтоЖурнал - Признак формы журнала
//	Список    - Объект списка формы
//	ИмяОбъекта - Имя проверяемого объекта
//
//	Возвращает истину если объект может быть выгружен, иначе ложь
Функция удЕстьВозможностьАльтернативногоРедактирования(ЭтаФорма=Неопределено, ЭтоСписок=Ложь, ЭтоЖурнал=Ложь, Список=Неопределено) Экспорт
	Если ЭтаФорма = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	// Составим структуру объектов
	МассивОбъектов=Новый Массив;
	Если ЭтоСписок Тогда
		МетаданныеОбъекта=Метаданные.НайтиПоТипу(ТипЗнч(Список));
		МассивОбъектов.Добавить(МетаданныеОбъекта);
	ИначеЕсли ЭтоЖурнал Тогда
		МетаданныеОбъекта=Метаданные.НайтиПоТипу(ТипЗнч(Список));
		Для Каждого РегДокумент Из МетаданныеОбъекта.РегистрируемыеДокументы Цикл
			МассивОбъектов.Добавить(Регдокумент);
		КонецЦикла;
	Иначе
		МетаданныеОбъекта=ЭтаФорма.ЭтотОбъект.Метаданные();
		МассивОбъектов.Добавить(МетаданныеОбъекта);
	КонецЕсли;
	
	Результат = Ложь;
	Для Каждого Объект Из МассивОбъектов Цикл
		ЕстьКомментарий                  = НЕ(Объект.Реквизиты.Найти("Комментарий") = Неопределено);
		ЕстьФормаДокументаДополнительная = НЕ(Объект.Формы.Найти("ФормаДокументаДополнительная") = Неопределено);
		
		Если ЕстьКомментарий ИЛИ ЕстьФормаДокументаДополнительная Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Процедура открывает обработку ВыгрузкаДанныхБух с отбором по ЭтотОбъект
Процедура удВыгрузитьВбухгалтерию(ЭтаФорма,ЭтотОбъект) Экспорт
#Если Клиент Тогда
	Если ЭтотОбъект=Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяОбъекта=ЭтотОбъект.Метаданные().Имя;
	Если НЕ удВыгружаетсяВБухгалтерию(ЭтаФорма,,,,ИмяОбъекта) Тогда
		Предупреждение("Этот объект не выгружается в бухгалтерию.", 10);
		Возврат;
	КонецЕсли;
	
	// определим тип объекта
	ДокументыВсеСсылки=Документы.ТипВсеСсылки();
	ЭтоДокумент=ДокументыВсеСсылки.СодержитТип(ТипЗнч(ЭтотОбъект.Ссылка));
	
	// заполним параметры обработки
	ОбработкаВыгрузкиВбухгалтерию=Обработки.ВыгрузкаДанныхБух.Создать();
	ОбработкаВыгрузкиВбухгалтерию.НачалоПериода=?(ЭтоДокумент, ЭтотОбъект.Дата, РабочаяДата);
	ОбработкаВыгрузкиВбухгалтерию.КонецПериода=?(ЭтоДокумент, ЭтотОбъект.Дата, РабочаяДата);
	ФормаОбработки=ОбработкаВыгрузкиВбухгалтерию.ПолучитьФорму("Форма",ЭтаФорма,ЭтаФорма);
	ФормаОбработки.Открыть();
	НоваяСтрока=ОбработкаВыгрузкиВбухгалтерию.ОбъектыКВыгрузке.Добавить();
	НоваяСтрока.Объект=ЭтотОбъект.Ссылка;
	Для Каждого ТекСтрока Из ОбработкаВыгрузкиВбухгалтерию.ВидыСправочников Цикл
		ТекСтрока.Пометка=Ложь;
	КонецЦикла;
	Для Каждого ТекСтрока Из ОбработкаВыгрузкиВбухгалтерию.ВидыДокументов Цикл
		ТекСтрока.Пометка=Ложь;
	КонецЦикла;
	ФормаОбработки.ЭлементыФормы.ПанельОбъектов.ТекущаяСтраница=ФормаОбработки.ЭлементыФормы.ПанельОбъектов.Страницы.ОбъектыКВыгрузке;
#КонецЕсли
КонецПроцедуры

// процедура перерисовывает картиноку заметок
Процедура удОбновитьКартинкуЗаметок(Строка, ЭтаФорма, КоманднаяПанель=Неопределено) Экспорт
	Если КоманднаяПанель = Неопределено Тогда
		Попытка
			КоманднаяПанель = ЭтаФорма.ЭлементыФормы.ДействияФормы;
		Исключение
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КнопкаЗаметок = КоманднаяПанель.Кнопки.Найти("кнЗаметки");
	
	Если КнопкаЗаметок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// если получилось пройти все проверки обновим
	Если НЕ Метаданные.РегистрыСведений.ЗаметкиПользователей.Измерения.Объект.Тип.СодержитТип(ТипЗнч(Строка)) Тогда
		КнопкаЗаметок.Картинка = БиблиотекаКартинок.ЗаметкиСоздать;
		КнопкаЗаметок.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	КнопкаЗаметок.Доступность = Истина;
	// проверим наличие заметок
	НаборРегистра = РегистрыСведений.ЗаметкиПользователей.СоздатьНаборЗаписей();
	НаборРегистра.Отбор.Объект.ВидСравнения  = ВидСравнения.Равно;
	НаборРегистра.Отбор.Объект.Значение      = Строка;
	НаборРегистра.Отбор.Объект.Использование = Истина;
	НаборРегистра.Прочитать();
	
	Если НаборРегистра.Количество() > 0 Тогда
		КнопкаЗаметок.Картинка = БиблиотекаКартинок.ЗаметкиПерейти;
	Иначе
		КнопкаЗаметок.Картинка = БиблиотекаКартинок.ЗаметкиСоздать;
	КонецЕсли;
КонецПроцедуры
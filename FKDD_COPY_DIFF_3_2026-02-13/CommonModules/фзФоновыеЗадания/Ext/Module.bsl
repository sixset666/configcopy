////////////////////////////////////////////////////////////////////////////////
// ОСНОВНЫЕ ПРОЦЕДУРЫ, РЕАЛИЗУЮЩИЕ АЛГОРИТМЫ РЕГЛАМЕНТНЫХ И ФОНОВЫХ ЗАДАНИЙ
  
// Выполняет обмен с удаленными подразделениями плана обмена "Удаленные подразделения"
//
// Параметры
//  СтруктураПараметров  – Структура – Структура параметров фонового задания
//
Процедура ОбменСУдаленнымиПодразделениями(Знач СтруктураПараметров) Экспорт
	УзлыСтрока = "";
	//приводим параметры к корректности
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("ТаблицаУзлов") Тогда
		СтруктураПараметров.Вставить("ТаблицаУзлов",Новый ТаблицаЗначений);
	ИначеЕсли ТипЗнч(СтруктураПараметров.ТаблицаУзлов) <> Тип("ТаблицаЗначений") Тогда
		СтруктураПараметров.ТаблицаУзлов = Новый ТаблицаЗначений;
	КонецЕсли;		
	Если НЕ ЗначениеЗаполнено(СтруктураПараметров.ТаблицаУзлов) Тогда
		ЗаписьЖурналаРегистрации("ФоновоеЗадание.Обмен", УровеньЖурналаРегистрации.Предупреждение, , , "Не заполнена таблица узлов,
				|с которыми необходимо произвести обмен.");
		Возврат;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.Свойство("ВестиЖурналРегистрации") Тогда
		СтруктураПараметров.Вставить("ВестиЖурналРегистрации",Истина);
	ИначеЕсли ТипЗнч(СтруктураПараметров.ВестиЖурналРегистрации) <> Тип("Булево") Тогда
		СтруктураПараметров.ВестиЖурналРегистрации = Ложь;
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("ВестиЛогФайл") Тогда
		СтруктураПараметров.Вставить("ВестиЛогФайл",Ложь);
	ИначеЕсли ТипЗнч(СтруктураПараметров.ВестиЛогФайл) <> Тип("Булево") Тогда
		СтруктураПараметров.ВестиЛогФайл = Ложь;
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("КаталогЛогов") Тогда
		СтруктураПараметров.Вставить("КаталогЛогов");
	ИначеЕсли ТипЗнч(СтруктураПараметров.КаталогЛогов) <> Тип("Строка") Тогда
		СтруктураПараметров.КаталогЛогов = КаталогВременныхФайлов();
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("АктуальностьЛогов") Тогда
		СтруктураПараметров.Вставить("АктуальностьЛогов");
	ИначеЕсли ТипЗнч(СтруктураПараметров.АктуальностьЛогов) <> Тип("Число") Тогда
		СтруктураПараметров.АктуальностьЛогов = 0;
	КонецЕсли;
	ФиксироватьПриемОтправкуОбъектов = Неопределено;
	СтруктураПараметров.Свойство("ФиксироватьПриемОтправкуОбъектов",ФиксироватьПриемОтправкуОбъектов);
	Если ФиксироватьПриемОтправкуОбъектов = Неопределено Тогда
		ФиксироватьПриемОтправкуОбъектов = Ложь;
	КонецЕсли;
	
	// Собственно обмен
	ОбменУдаленныеПодразделения = Новый ТаблицаЗначений;
	ОбменУдаленныеПодразделения.Колонки.Добавить("Узел");
	ОбменУдаленныеПодразделения.Колонки.Добавить("НастройкаДоставки");
	ОбменУдаленныеПодразделения.Колонки.Добавить("Прием");
	ОбменУдаленныеПодразделения.Колонки.Добавить("Отправка");
    ОбменУдаленныеПодразделения.Колонки.Добавить("КоличествоЭлементовВТранзакции");
	ОбменУдаленныеПодразделения.Колонки.Добавить("ДопроводитьПоПартиям");

	Для Каждого ТекУзел Из СтруктураПараметров.ТаблицаУзлов Цикл
		НоваяСтрока = ОбменУдаленныеПодразделения.Добавить();
		НоваяСтрока.Узел = ТекУзел.Узел;
		НоваяСтрока.НастройкаДоставки = ТекУзел.НастройкаДоставки;
		Попытка
			НоваяСтрока.Прием 							= ТекУзел.Прием;
			НоваяСтрока.Отправка 						= ТекУзел.Отправка;
			НоваяСтрока.КоличествоЭлементовВТранзакции 	= ТекУзел.КоличествоЭлементовВТранзакции;
			НоваяСтрока.ДопроводитьПоПартиям 			= ТекУзел.ДопроводитьПоПартиям;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	Попытка
		ПрерватьЗадание = Ложь; // флаг неуспешного завершения задания (например, ошибка фтп)
		ВыполнитьОбмен(ОбменУдаленныеПодразделения,СтруктураПараметров.ВестиЖурналРегистрации,
			СтруктураПараметров.ВестиЛогФайл,СтруктураПараметров.КаталогЛогов,ФиксироватьПриемОтправкуОбъектов,,, ПрерватьЗадание);
		
		ПрефиксИмени = СокрЛП(ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Ссылка.Код) + "_";
		//логи
		Если СтруктураПараметров.ВестиЛогФайл Тогда
			//обрежем неактуальные
			Если СтруктураПараметров.АктуальностьЛогов > 0 Тогда
				мсвФайлов = НайтиФайлы(СтруктураПараметров.КаталогЛогов, ПрефиксИмени + "????-??-??.log");
				Для Каждого ТекФайл Из мсвФайлов Цикл
					Попытка
						ДатаФайла = Дата(СтрЗаменить(СтрЗаменить(Прав(ТекФайл.Имя,14),".log",""),"-",""));
						Если ДатаФайла < (НачалоДня(ТекущаяДата()) - СтруктураПараметров.АктуальностьЛогов*24*3600) Тогда
							УдалитьФайлы(ТекФайл.ПолноеИмя);
						КонецЕсли;
					Исключение
						ВызватьИсключение ОписаниеОшибки();
					КонецПопытки;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	Исключение 
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		ТекстОшибки = "Ошибка выполнения фонового задания: " + ТекстОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " 
			+ Символы.ВК + "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
		#Если Клиент Тогда
			Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		#КонецЕсли 
		ЗаписьЖурналаРегистрации("ФоновоеЗадание.Обмен", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ПрерватьЗадание = Истина; // была явная ошибка, задание надо завершить аварийно
		//Возврат;
	КонецПопытки;
	Если ПрерватьЗадание Тогда
		Попытка
			Результат = 1/0;
		Исключение
			ВызватьИсключение "Завершилось с ошибками";
		КонецПопытки;
	КонецЕсли;		

КонецПроцедуры // ОбменСУдаленнымиПодразделениями()

// Выполняет восстановление последовательностей документов
Процедура ВосстановлениеПоследовательностей(Знач СтруктураПараметров) Экспорт
	
	Если ПустаяСтрока(ИмяПользователя()) Тогда
		ИмяПользователя           = "Администратор";
		ПолноеИмяПользователя     = "Администратор информационной базы";
	Иначе
		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			ПолноеИмяПользователя = ИмяПользователя();
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
		КонецЕсли;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	
	// Уберем на время выполнения восстановления последовательностей обработку значимых событий
	ВключитьЗначимыеСобытия = Ложь;
	Если обПраво("АктивностьЗначимыхСобытий") Тогда
		МенеджерЗаписи = РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект         = Неопределено;
		МенеджерЗаписи.ПравоНастройка = обПолучитьСсылкуПВХПравИНастроек("АктивностьЗначимыхСобытий");
		МенеджерЗаписи.Значение       = Ложь;
		МенеджерЗаписи.Записать(Истина);
		ВключитьЗначимыеСобытия = Истина;
	КонецЕсли;
	
	//приводим параметры к корректности
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.Свойство("СписокПоследовательностей") Тогда
		СтруктураПараметров.Вставить("СписокПоследовательностей");
	ИначеЕсли ТипЗнч(СтруктураПараметров.СписокПоследовательностей) <> Тип("Массив") Тогда
		СтруктураПараметров.СписокПоследовательностей = Новый Массив;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.Свойство("НаДату") Тогда
		СтруктураПараметров.Вставить("НаДату");
	ИначеЕсли ТипЗнч(СтруктураПараметров.НаДату) <> Тип("Дата") Тогда
		СтруктураПараметров.НаДату = '00010101';
	КонецЕсли;
	
	СписокПоследовательностей = Новый Структура;
	Для Каждого ТекПоследовательность Из СтруктураПараметров.СписокПоследовательностей Цикл
		СписокПоследовательностей.Вставить(ТекПоследовательность, "Плановое восстановление последовательности");
	КонецЦикла;
	
	Рез = Обработки.ВосстановлениеПоследовательностей.Создать().Восстановить(СписокПоследовательностей,
	СтруктураПараметров.НаДату);
	
	//Восстановим значение права
	Если ВключитьЗначимыеСобытия Тогда
		МенеджерЗаписи = РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект         = Неопределено;
		МенеджерЗаписи.ПравоНастройка = обПолучитьСсылкуПВХПравИНастроек("АктивностьЗначимыхСобытий");
		МенеджерЗаписи.Значение       = Истина;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	
	ЕстьОшибки = Рез.Ошибка;
	Если ЕстьОшибки Тогда
		Ошибки = "Последовательности не восстановлены:";
		Для каждого ТекСтрока Из Рез.СписокПроведенныхДокументов Цикл
			Если ТекСтрока.Пометка Тогда
				Ошибки = Ошибки + Символы.ПС + " - ошибка проведения <" + ТекСтрока.Значение + ">";
			КонецЕсли; 
		КонецЦикла;
		ВызватьИсключение(Ошибки);
	КонецЕсли;
	
КонецПроцедуры // ВосстановлениеПоследовательностей()

// Обновляет индекс полнотекстового поиска
Процедура ОбновлениеИндексаПолнотекстовогоПоиска(Знач СтруктураПараметров) Экспорт
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить Тогда
 		Если Не ПолнотекстовыйПоиск.ИндексАктуален() Тогда
			ПолнотекстовыйПоиск.ОбновитьИндекс(Ложь, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбновлениеИндексаПолнотекстовогоПоиска()

// Выполняет слияние индексов полнотекстового поиска
Процедура СлияниеИндексаПолнотекстовогоПоиска(Знач СтруктураПараметров) Экспорт
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить Тогда
 		Если Не ПолнотекстовыйПоиск.ИндексАктуален() Тогда
			ПолнотекстовыйПоиск.ОбновитьИндекс(Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // СлияниеИндексаПолнотекстовогоПоиска()

// Выполняет допроведение документов по партионным регистрам
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров
//
Процедура ДопроведениеПоПартиям(СтруктураПараметров) Экспорт
	дкДопровестиПоПартиям();
КонецПроцедуры // ДопроведениеПоПартиям()

// Вызов произвольной обработки или функции
Процедура ПроизвольнаяОбработка(Знач СтруктураПараметров) Экспорт
	Если (НЕ СтруктураПараметров.Свойство("ИмяМетода")) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСтрока = "";
	Если СтруктураПараметров.Свойство("МассивПараметров") Тогда
		Сч = 0;
		Для Каждого ТекПараметр Из СтруктураПараметров.МассивПараметров Цикл
			ПараметрыСтрока = ПараметрыСтрока + "," + "СтруктураПараметров.МассивПараметров[" + Сч + "]";
			Сч = Сч + 1;
		КонецЦикла;
		Если ПараметрыСтрока <> "" Тогда
			ПараметрыСтрока = Сред(ПараметрыСтрока, 2);
		КонецЕсли;
	КонецЕсли;
	
	Выполнить(СтруктураПараметров.ИмяМетода + "(" + ПараметрыСтрока + ")");
КонецПроцедуры // ПроизвольнаяОбработка()

// Функция выделяет необходимые части из строки запуска
//
Функция ВыделитьПодстрокуСтрокиЗапуска(СтрокаЗапуска, КлючПодстроки)

	НомерПервогоСимвола = Найти(ВРег(СтрокаЗапуска), ВРег(КлючПодстроки)) + СтрДлина(КлючПодстроки);
	Для Счетчик = НомерПервогоСимвола По СтрДлина(СтрокаЗапуска) Цикл
		Если Сред(СтрокаЗапуска, Счетчик, 1) = ";" Тогда
			НомерПоследнегоСимвола = Счетчик - 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Получаем искомую подстроку
	ИскомаяПодстрока = Сред(СтрокаЗапуска, НомерПервогоСимвола, (НомерПоследнегоСимвола - НомерПервогоСимвола + 1));
	Если Сред(ИскомаяПодстрока, 1, 1) = """" Тогда
		// Если подстрока в кавычках, то выделяем их
		ИскомаяПодстрока = Сред(ИскомаяПодстрока, 2, (СтрДлина(ИскомаяПодстрока) - 2));
	КонецЕсли;
	
	Возврат ИскомаяПодстрока;

КонецФункции // ВыделитьПодстроку()

// Выполняет допроведение документов по партионным регистрам
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров
//
Процедура НапоминаниеПроСнятиеРезервов(СтруктураПараметров) Экспорт
	
	//приводим параметры к корректности
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	
	ЗаказыНаАвтомобиль = Неопределено;
	СтруктураПараметров.Свойство("НапоминаниеЗаказыНаАвтомобиль",ЗаказыНаАвтомобиль);
	ЗаказыПокупателей = Неопределено;	
	СтруктураПараметров.Свойство("НапоминаниеЗаказыПокупателей", ЗаказыПокупателей);
	
	Если ЗаказыПокупателей<>Неопределено И ЗаказыПокупателей Тогда
		Обработки.АвтоматическоеСнятиеРезервов.СформироватьНапоминанияПроСнятиеРезервов();
	КонецЕсли;
	
	Если ЗаказыНаАвтомобиль<>Неопределено И ЗаказыНаАвтомобиль  Тогда
		//Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон",Ложь) Тогда
			Обработки.АвтоматическоеСнятиеРезервовАвтомобилей.Создать().СформироватьНапоминанияПроСнятиеРезервовАвтомобилей();
		//КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ДопроведениеПоПартиям()

// Выполняет допроведение документов по партионным регистрам
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров
//
Процедура АвтоматическоеСнятиеРезервов(СтруктураПараметров) Экспорт
	
	//приводим параметры к корректности
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	ЗаказыНаАвтомобиль = Неопределено;
	Если НЕ СтруктураПараметров.Свойство("ЗаказыНаАвтомобиль",ЗаказыНаАвтомобиль) Тогда
		СтруктураПараметров.Вставить("ЗаказыНаАвтомобиль");
	КонецЕсли;
	ЗаказыПокупателей = Неопределено;	
	Если НЕ СтруктураПараметров.Свойство("ЗаказыПокупателей", ЗаказыПокупателей) Тогда
		СтруктураПараметров.Вставить("ЗаказыПокупателей");
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("КорректировкаЗаказа") Тогда
		СтруктураПараметров.Вставить("КорректировкаЗаказа");
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("ФормированиеНапоминаний") Тогда
		СтруктураПараметров.Вставить("ФормированиеНапоминаний");
	КонецЕсли;
	Если НЕ СтруктураПараметров.Свойство("СписаниеРезервовСПредоплатой") Тогда
		СтруктураПараметров.Вставить("СписаниеРезервовСПредоплатой");
	КонецЕсли;
	

	Если ЗаказыПокупателей<>Неопределено И ЗаказыПокупателей Тогда
		Обработки.АвтоматическоеСнятиеРезервов.ВыполнитьАвтоматическоеСнятиеРезервов(СтруктураПараметров);
	КонецЕсли;
	
	Если ЗаказыНаАвтомобиль<>Неопределено И ЗаказыНаАвтомобиль  Тогда
		//Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон",Ложь) Тогда
			Обработки.АвтоматическоеСнятиеРезервовАвтомобилей.Создать().ВыполнитьАвтоматическоеСнятиеРезервовАвтомобилей(СтруктураПараметров);
		//КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДопроведениеПоПартиям()

// Выполняет обмен с ЕАИС ТО
Процедура АвтоматическийОбменСЕАСИТО(СтруктураПараметров) Экспорт
	обрОбмена = Обработки.АРМОбменСЕАИСТО.Создать();
	Попытка
		Сообщить("Старт обмена с ЕАИС ТО...");
		Результат = обрОбмена.ВыполнитьОбменПоРегламентномузаданию(СтруктураПараметров);
		Если Результат.Ошибка Тогда
			ВызватьИсключение(Результат.ТекстОшибки);
		КонецЕсли;
	Исключение
		ВызватьИсключение("При выполнении обмена произошли ошибки.");
	КонецПопытки;
	Сообщить("Окончание обмена с ЕАИС ТО...");
КонецПроцедуры

// Формирует напоминания о необходимости заказа авто для тест-драйва
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров
//
Процедура РасчетЗаказаАвтомобиляПодТестДрайв(Знач СтруктураПараметров) Экспорт
	
	Если НЕ ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("СрокОповещения") Тогда
		СрокОповещения = СтруктураПараметров.СрокОповещения;
	Иначе
		СрокОповещения = 0;
	КонецЕсли;
	
	Отчет = Отчеты.ПланЗаказовАвтомобилейДляТестДрайва.Создать();
	
	Отчет.ЗаполнитьНачальныеНастройки();
	
	ВариантОтчета    = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек["Основной"];
	
	Если НЕ ВариантОтчета.Настройки.ПараметрыДанных.Элементы.Найти("ДатаОкончания") = Неопределено Тогда
		ВариантОтчета.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", ТекущаяДата());
	КонецЕсли;
	
	Показатели      = ВариантОтчета.Настройки.Выбор;
	Отбор           = ВариантОтчета.Настройки.Отбор;
	СтруктураОтчета = ВариантОтчета.Настройки.Структура;
	Порядок         = ВариантОтчета.Настройки.Порядок;
	
	СтруктураОтчета.Очистить();
	Порядок.Элементы.Очистить();
	
	Показатели.Элементы.Очистить();
	
	НовыйПоказатель = Показатели.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НовыйПоказатель.Использование = Истина;
	
	НовыйПоказатель = Показатели.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовыйПоказатель.Использование = Истина;
	НовыйПоказатель.Поле          = Новый ПолеКомпоновкиДанных("СрокТочкиЗаказа");
	
	//ТочкаЗаказа
	КоличествоПодчиненных = Отбор.Элементы.Количество()-1;
	Пока КоличествоПодчиненных >= 0 Цикл
		Отбор.Элементы.Удалить(Отбор.Элементы[КоличествоПодчиненных]);
		КоличествоПодчиненных = КоличествоПодчиненных - 1;
	КонецЦикла;
	
	ТекОтбор = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ТекОтбор.Использование  = Истина;
	ТекОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокТочкиЗаказа");
	ТекОтбор.ПравоеЗначение = СрокОповещения;
	ТекОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	
	НоваяГруппировка = СтруктураОтчета.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	НоваяГруппировка.Использование = Истина;
	
	Группировка = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("АвтоПолеГруппировкиКомпоновкиДанных"));
	Группировка.Использование  = Истина;
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование  = Истина;
	НовоеПоле.Поле           = Новый ПолеКомпоновкиДанных("Автомобиль");
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование  = Истина;
	НовоеПоле.Поле           = Новый ПолеКомпоновкиДанных("ПодразделениеКомпании.ОтветственныйЗаТД");
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование  = Истина;
	НовоеПоле.Поле           = Новый ПолеКомпоновкиДанных("Модель");
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование = Истина;
	
	НовоеПоле = НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	НовоеПоле.Использование = Истина;
	
	ДатаПробега = КонецДня(ТекущаяДата());
	ДатаКонца   = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", ВариантОтчета.Настройки);
	ДатаПробега = Мин(ДатаКонца, ДатаКонца);
	Отчет.СхемаКомпоновкиДанных.Параметры.ДатаПробега.Значение = ДатаПробега;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		Отчет.СхемаКомпоновкиДанных,
		ВариантОтчета.Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	ТаблицаРезультата = Новый ТаблицаЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультата);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
	Если ТаблицаРезультата = Неопределено ИЛИ ТаблицаРезультата.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	ДатаОповещений = ТекущаяДата();
	
	НаборЗаписейНапоминания = РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
	НаборЗаписейНапоминания.Отбор.Автор.Установить(ПараметрыСеанса.Пользователь);
	НаборЗаписейНапоминания.Отбор.Завершено.Установить(Ложь);
	НаборЗаписейНапоминания.Прочитать();
	
	Для Каждого ТекСтрока Из ТаблицаРезультата Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Автомобиль) ИЛИ ТекСтрока.СрокТочкиЗаказа = NULL Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписейНапоминания.Добавить();
		НоваяЗапись.Пользователь            = ТекСтрока.ПодразделениеКомпанииОтветственныйЗаТД;
		НоваяЗапись.Автор                   = ПараметрыСеанса.Пользователь;
		НоваяЗапись.Завершено               = Ложь;
		НоваяЗапись.РеальнаяДатаОповещения  = ДатаОповещений;
		НоваяЗапись.ДатаНачала              = ДатаОповещений;
		НоваяЗапись.ДатаОповещения          = ДатаОповещений;
		НоваяЗапись.Редактирование          = Ложь;
		
		Если ТекСтрока.СрокТочкиЗаказа<=0 Тогда
			НоваяЗапись.Объект = ТекСтрока.Автомобиль;
			НоваяЗапись.Описание = "Необходимо заказать новый автомобиль для тест-драйва, модель: <" + СокрЛП(ТекСтрока.Модель)+">";
		Иначе
			НоваяЗапись.Объект = ТекСтрока.Модель;
			НоваяЗапись.Описание = "Через " + СокрЛП(ТекСтрока.СрокТочкиЗаказа) + " " + обПолучитьПредставлениеДня(ТекСтрока.СрокТочкиЗаказа) + " необходимо заказать новый автомобиль для тест-драйва, модель: <" + СокрЛП(ТекСтрока.Модель)+">.";
		КонецЕсли;
		
		НоваяЗапись.Тема = "Заказ автомобилей для тест-драйва";
		НоваяЗапись.ТипПериода              = "00";
		НоваяЗапись.УдалитьПоИстеченииСрока = Истина;
		
	КонецЦикла;
	
	//записываем сформированные напоминания
	Попытка
		НаборЗаписейНапоминания.Записать(Истина); 
	Исключение
	КонецПопытки;
	
КонецПроцедуры

// Формирует напоминания о необходимости заказа авто для тест-драйва
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров
//
Процедура РасчетВыводаАвтомобиляИзТестДрайва(Знач СтруктураПараметров) Экспорт
	
	Если НЕ ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("СрокОповещения") Тогда
		СрокОповещения = СтруктураПараметров.СрокОповещения;
	Иначе
		СрокОповещения = 0;
	КонецЕсли;
	
	Отчет = Отчеты.ПланЗаказовАвтомобилейДляТестДрайва.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
		
	ВариантОтчета    = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек["Основной"];
		
	Если НЕ ВариантОтчета.Настройки.ПараметрыДанных.Элементы.Найти("ДатаОкончания") = Неопределено Тогда
		ВариантОтчета.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", ТекущаяДата());
	КонецЕсли;
	
	Показатели      = ВариантОтчета.Настройки.Выбор;
	Отбор           = ВариантОтчета.Настройки.Отбор;
	СтруктураОтчета = ВариантОтчета.Настройки.Структура;
	Порядок         = ВариантОтчета.Настройки.Порядок;
	
	СтруктураОтчета.Очистить();
	Порядок.Элементы.Очистить();
	
	Показатели.Элементы.Очистить();
	
	НовыйПоказатель = Показатели.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НовыйПоказатель.Использование = Истина;
	
	НовыйПоказатель = Показатели.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовыйПоказатель.Использование = Истина;
	НовыйПоказатель.Поле          = Новый ПолеКомпоновкиДанных("ОсталосьДнейДоВыбытия");
	
	//ТочкаЗаказа
	КоличествоПодчиненных = Отбор.Элементы.Количество()-1;
	Пока КоличествоПодчиненных >= 0 Цикл
		Отбор.Элементы.Удалить(Отбор.Элементы[КоличествоПодчиненных]);
		КоличествоПодчиненных = КоличествоПодчиненных - 1;
	КонецЦикла;
	
	ТекОтбор = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ТекОтбор.Использование  = Истина;
	ТекОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОсталосьДнейДоВыбытия");
	ТекОтбор.ПравоеЗначение = СрокОповещения;
	ТекОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	
	НоваяГруппировка = СтруктураОтчета.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	НоваяГруппировка.Использование = Истина;
	
	Группировка = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("АвтоПолеГруппировкиКомпоновкиДанных"));
	Группировка.Использование  = Истина;
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование  = Истина;
	НовоеПоле.Поле           = Новый ПолеКомпоновкиДанных("Автомобиль");
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование  = Истина;
	НовоеПоле.Поле           = Новый ПолеКомпоновкиДанных("ПодразделениеКомпании.ОтветственныйЗаТД");
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование = Истина;
	
	ДатаПробега = КонецДня(ТекущаяДата());
	ДатаКонца   = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", ВариантОтчета.Настройки);
	ДатаПробега = Мин(ДатаКонца, ДатаКонца);
	Отчет.СхемаКомпоновкиДанных.Параметры.ДатаПробега.Значение = ДатаПробега;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		Отчет.СхемаКомпоновкиДанных,
		ВариантОтчета.Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	ТаблицаРезультата = Новый ТаблицаЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультата);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
	Если ТаблицаРезультата = Неопределено ИЛИ ТаблицаРезультата.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	ДатаОповещений  = ТекущаяДата();
	
	НаборЗаписейНапоминания = РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
	НаборЗаписейНапоминания.Отбор.Автор.Установить(ПараметрыСеанса.Пользователь);
	НаборЗаписейНапоминания.Отбор.Завершено.Установить(Ложь);
	НаборЗаписейНапоминания.Прочитать();
	
	Для Каждого ТекСтрока Из ТаблицаРезультата Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Автомобиль) ИЛИ ТекСтрока.ОсталосьДнейДоВыбытия = NULL Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписейНапоминания.Добавить();
		НоваяЗапись.Пользователь            = ТекСтрока.ПодразделениеКомпанииОтветственныйЗаТД;
		НоваяЗапись.Автор                   = ПараметрыСеанса.Пользователь;
		НоваяЗапись.Завершено               = Ложь;
		НоваяЗапись.РеальнаяДатаОповещения  = ДатаОповещений;
		НоваяЗапись.ДатаНачала              = ДатаОповещений;
		НоваяЗапись.ДатаОповещения          = ДатаОповещений;
		НоваяЗапись.Редактирование          = Ложь;
		
		НоваяЗапись.Объект = ТекСтрока.Автомобиль;
		Если ТекСтрока.ОсталосьДнейДоВыбытия<=0 Тогда
			НоваяЗапись.Описание = "Необходимо вывести из тест-драйва автомобиль: <" + СокрЛП(ТекСтрока.Автомобиль) + ">.";
		Иначе
			НоваяЗапись.Описание = "Через " + СокрЛП(ТекСтрока.ОсталосьДнейДоВыбытия) + " " + обПолучитьПредставлениеДня(ТекСтрока.ОсталосьДнейДоВыбытия) + " необходимо вывести из тест-драйва автомобиль: <" + СокрЛП(ТекСтрока.Автомобиль)+">.";
		КонецЕсли;
		
		НоваяЗапись.Тема = "Вывод автомобилей из тест-драйва";
		НоваяЗапись.ТипПериода              = "00";
		НоваяЗапись.УдалитьПоИстеченииСрока = Истина;
		
	КонецЦикла;
	
	//записываем сформированные напоминания
	Попытка
		НаборЗаписейНапоминания.Записать(Истина); 
	Исключение
	КонецПопытки;
	
КонецПроцедуры

//Rarus Alkoko 17.06.2015 {

Функция ЗагрузитьПрайсЛист(ПрайсЛист, СтрокаПодключения = Неопределено, КлючЗадания = Неопределено) Экспорт
	
	ОбработкаЗагрузки = Обработки.ЗагрузкаПрайсЛистов.Создать();
	ОбработкаЗагрузки.ПрайсЛистКонтрагента = ПрайсЛист;
	Возврат ОбработкаЗагрузки.ЗагрузитьПрайсЛистДляРазбора(СтрокаПодключения, КлючЗадания);
	
КонецФункции

Функция ЗагрузитьПрайсЛистИзНовогоФайла(ПрайсЛист, КлючЗадания = Неопределено) Экспорт
	
	ПрайсЛистОбъект = ПрайсЛист.ПолучитьОбъект();
	Возврат ПрайсЛистОбъект.ЗагрузитьНовыйПрайсЛист(КлючЗадания);
	
КонецФункции

Процедура ПроверкаНовыхФайловПрайсЛистов(Знач СтруктураПараметров) Экспорт 
	// Собираем запросом все прайс-листы в статусах Завершена, Разрешена, Прервана
	// с признаком использования автозагрузки и датой следующей загрузки <= Текущей.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Автообновление.ПрайсЛист КАК ПрайсЛист
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовАвтообновление КАК Автообновление
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СрезПоследних(, ) КАК ЖурналЗагрузки
	|		ПО (ЖурналЗагрузки.ПрайсЛист = Автообновление.ПрайсЛист)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
	|		ПО (ЗапрещенныеПодразделения.ПрайсЛист = Автообновление.ПрайсЛист)
	|			И (ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
	|ГДЕ
	|	Автообновление.ИспользоватьАвтозагрузку
	|	И Автообновление.ДатаСледующейПроверки <= &ТекущаяДата
	|	И (Автообновление.ПериодичностьПроверкиНовыхДанных <> 1
	|			ИЛИ Автообновление.ИнтервалПроверкиНачало <= &ТекущаяДатаЧас
	|				И Автообновление.ИнтервалПроверкиКонец > &ТекущаяДатаЧас)
	|	И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
	//|	И (ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Разрешена)
	//|			ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.НовыйФайл)
	//|			ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.НовыйФайлОшибка)
	//|			ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Ожидает)
	//|			ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Прервана)
	//|			ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Завершена)
	//|			ИЛИ ЖурналЗагрузки.Статус ЕСТЬ NULL )
	|";
	
	ДатаПроверки = ТекущаяДата();
	Запрос.УстановитьПараметр("ТекущаяДата", ДатаПроверки);
	Запрос.УстановитьПараметр("ТекущаяДатаЧас", Час(ДатаПроверки));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		//Выполняем проверку наличия новых файлов, сохраняя если требуется
		//При этом если есть новые файлы устанавливаем статус "Есть новый файл" или "Ожидает загрузки"
		Выборка.ПрайсЛист.ПолучитьОбъект().ПроверитьНовыеФайлы();
	КонецЦикла;
	
КонецПроцедуры //ПроверкаНовыхФайловПрайсЛистов()

Процедура ПроверитьЗаданиеЗагрузки(ПрайсЛист, КлючЗадания)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", КлючЗадания);
	НайденныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если НайденныеЗадания.Количество() = 0 Тогда
		Причина = "Фоновое задание завершено с ошибкой";
		НовыйСтатус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Ошибка;
	Иначе
		ФоновоеЗадание = НайденныеЗадания[0];
		Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Возврат;
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
			Причина = "Завершено фоновое задание";
			НовыйСтатус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Завершена;
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			Причина = "Фоновое задание завершено аварийно";
			НовыйСтатус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Ошибка;
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
			Причина = "Фоновое задание отменено";
			НовыйСтатус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Прервана;
		КонецЕсли;
	КонецЕсли;
	
	Период = ТекущаяДата();
	
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки");
	ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛист);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛист);
	НаборЗаписей.Отбор.Период.Установить(Период);
	
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ПрайсЛист  = ПрайсЛист;
	НоваяЗапись.Период     = Период;
	НоваяЗапись.Статус     = НовыйСтатус;
	НоваяЗапись.Примечание = Причина;
	НоваяЗапись.Автор      = Справочники.Пользователи.Робот;
	
	НаборЗаписей.Записать();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Процедура ЗагрузкаПрайсЛистов(Знач СтруктураПараметров) Экспорт
	
	// Сначала проверим загружаемые прайс-листы и статусы фоновых заданий!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЖурналЗагрузки.ПрайсЛист КАК ПрайсЛист,
	|	ЖурналЗагрузки.ИдентификаторФоновогоЗадания КАК КлючЗадания
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СрезПоследних(, ) КАК ЖурналЗагрузки
	|ГДЕ
	|	ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Загружается)
	|	И НЕ ЖурналЗагрузки.ИдентификаторФоновогоЗадания = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПроверитьЗаданиеЗагрузки(Выборка.ПрайсЛист, Выборка.КлючЗадания);
	КонецЦикла;
	
	Ограничение = обПраво("КоличествоЗагружаемыхПрайсЛистов");
	Если обЗначениеНеЗаполнено(Ограничение) Тогда
		Ограничение = 1;
	КонецЕсли;
	
	//Получим количество запущенных фоновых заданий
	Отбор = Новый Структура();
	Отбор.Вставить("ИмяМетода", "фзФоновыеЗаданияСервер.ЗагрузитьПрайсЛистИзНовогоФайла");
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Выполняется = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор).Количество();
	
	//Определим количество ФЗ, которые осталось запустить
	МожноЗапустить = Ограничение - Выполняется; 
	Если МожноЗапустить <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	//Выберем загружаемые прайс-листы
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ " + Формат(МожноЗапустить, "ЧГ=0") + "
	|	ЖурналЗагрузки.ПрайсЛист КАК ПрайсЛист
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СрезПоследних(, ) КАК ЖурналЗагрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовАвтообновление КАК Автообновление
	|		ПО ЖурналЗагрузки.ПрайсЛист = Автообновление.ПрайсЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
	|		ПО (ЗапрещенныеПодразделения.ПрайсЛист = ЖурналЗагрузки.ПрайсЛист)
	|			И (ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
	|ГДЕ
	|	(ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Ожидает)
	|			ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.НовыйФайл)
	|				И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
	|				И Автообновление.ИспользоватьАвтообновление)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЖурналЗагрузки.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		//Подготовка параметров фонового задания
		КлючЗадания = Строка(Новый УникальныйИдентификатор);
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Выборка.ПрайсЛист);
		МассивПараметров.Добавить(КлючЗадания);
		
		НаименованиеЗадания = НСтр("ru='Загрузка прайс-листа: '") + Строка(Выборка.ПрайсЛист);
		
		//запуск фонового задания
		ФоновыеЗадания.Выполнить("фзФоновыеЗаданияСервер.ЗагрузитьПрайсЛистИзНовогоФайла", МассивПараметров, КлючЗадания, НаименованиеЗадания);
	КонецЦикла;
	
КонецПроцедуры //ЗагрузкаПрайсЛистов()

//Rarus Alkoko 17.06.2015 }

////////////////////////////////////////////////////////////////////////////////
// ОСНОВНЫЕ ПРОЦЕДУРЫ, РЕАЛИЗУЮЩИЕ АЛГОРИТМЫ РЕГЛАМЕНТНЫХ И ФОНОВЫХ ЗАДАНИЙ

// Пустое фоновое задание. Крутится себе бесконечно. Используется для отладки
Процедура Пустышка() Экспорт
	Пока Истина Цикл
	КонецЦикла;
КонецПроцедуры

/////////////////////////
// АРМ Коммуникатора

// Пересчитывает и записывает в регистр сведений ДеревоКоммуникатораАРМ ДеревоГрупп из АРМКоммуникатор
//
// Параметр:
//	Дерево		- ДеревоЗначений	- ДеревоГрупп (из АРМКоммуникатор)
//	Параметры	- Массив			- МассивПараметров состоящий из Пользователь, ДатаНачалаПериодаСобытий,
//									  ДатаОкончанияПериодаСобытий
//
Процедура АРМКоммуникаторПересчитатьИЗаписатьДеревоГрупп(Дерево, Параметры) Экспорт
	
	#Если Сервер Тогда
		// Если код выполняется на сервере, значит задействован механизм 
		// фоновых заданий. В таком случае, параметры сеанса могут быть не инициализированы.
		Пользователь = обОпределитьТекущегоПользователя();
		ПараметрыСеанса.Пользователь = Пользователь;
	#КонецЕсли	
		
	АРМКоммуникаторПересчитатьДеревоГрупп(Дерево, Параметры);
	АРМКоммуникаторЗаписатьДеревоГрупп(Дерево);
	
КонецПроцедуры // АРМКоммуникаторПересчитатьДеревоГрупп()

// Пересчитывает количество писем ДеревоГрупп из АРМКоммуникатор
//
// Параметр:
//	Дерево		- ДеревоЗначений	- ДеревоГрупп (из АРМКоммуникатор)
//	Параметры	- Массив			- МассивПараметров состоящий из Пользователь, ДатаНачалаПериодаСобытий,
//									  ДатаОкончанияПериодаСобытий
//
Процедура АРМКоммуникаторПересчитатьДеревоГрупп(Дерево, Параметры)
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		// если это папка учетной записи электронной почты, пересчитаем письма
		КоличествоВсего = 0;
		Если ВРег(СтрокаДерева.Раздел) = "НАПОМИНАНИЯ" ИЛИ ВРег(СтрокаДерева.Раздел) = "СОБЫТИЯ" Тогда
			СтрокаДерева.КоличествоВсего = "0";
		КонецЕсли;
		Если ТипЗнч(СтрокаДерева.Ссылка) = Тип("СправочникСсылка.ГруппыПисемЭлектроннойПочты") Тогда
			СтрокаДерева.КоличествоВсего = АРМКоммуникаторКоличествоПисем(СтрокаДерева.Ссылка);
		// если это ветка напоминаний, пересчитаем напоминания	
		ИначеЕсли Найти(ВРег(СтрокаДерева.Раздел), "НАПОМИНАНИЯВЕТКА") > 0 Тогда
		// если это ветка событий, пересчитаем события	
			КоличествоВсего = АРМКоммуникаторКоличествоНапоминаний(СтрокаДерева.Наименование);
			СтрокаДерева.КоличествоВсего = ?(КоличествоВсего > 0, Строка(КоличествоВсего), "0");
			КоличествоВсегоНапоминаний = КоличествоВсего + Число(СтрокаДерева.Родитель.КоличествоВсего);
			СтрокаДерева.Родитель.КоличествоВсего = ?(КоличествоВсегоНапоминаний > 0,Строка(КоличествоВсегоНапоминаний), "0");
		ИначеЕсли Найти(ВРег(СтрокаДерева.Раздел), "СОБЫТИЯВЕТКА") > 0 Тогда
			КоличествоВсего = АРМКоммуникаторПолучитьТаблицуСобытий(СтрокаДерева.Наименование, Параметры["ДатаНачалаПериодаСобытий"],Параметры["ДатаОкончанияПериодаСобытий"]).Количество();
			СтрокаДерева.КоличествоВсего = ?(КоличествоВсего > 0, Строка(КоличествоВсего), "0");
			КоличествоВсегоСобытий = КоличествоВсего + Число(СтрокаДерева.Родитель.КоличествоВсего);
			СтрокаДерева.Родитель.КоличествоВсего = ?(КоличествоВсегоСобытий > 0,Строка(КоличествоВсегоСобытий), "0");
		ИначеЕсли ТипЗнч(СтрокаДерева.Ссылка) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
			СтрокаДерева.КоличествоВсего = АРМКоммуникаторКоличествоПисемУчетнойЗаписи(СтрокаДерева.Ссылка);	
		КонецЕсли;
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			АРМКоммуникаторПересчитатьДеревоГрупп(СтрокаДерева, Параметры);
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры // АРМКоммуникаторПересчитатьДеревоГрупп()

// Записывает в регистр сведений ДеревоКоммуникатораАРМ ДеревоГрупп из АРМКоммуникатор
//
// Параметр:
//	Дерево		- ДеревоЗначений	- ДеревоГрупп (из АРМКоммуникатор)
//
Процедура АРМКоммуникаторЗаписатьДеревоГрупп(Дерево)
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		НоваяЗапись = РегистрыСведений.ДеревоКоммуникатораАРМ.СоздатьМенеджерЗаписи();
		НоваяЗапись.Пользователь	 = ПараметрыСеанса.Пользователь;
		НоваяЗапись.Наименование	 = СтрокаДерева.Наименование;
        НоваяЗапись.Родитель		 = ?(СтрокаДерева.Родитель = Неопределено,"",СтрокаДерева.Родитель.Наименование);
		НоваяЗапись.КоличествоВсего	 = ?(СтрокаДерева.КоличествоВсего = "", 0,СтрокаДерева.КоличествоВсего);
		НоваяЗапись.Записать();
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			АРМКоммуникаторЗаписатьДеревоГрупп(СтрокаДерева);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// функция подсчета количества напоминаний
//
// Параметр:
//	НазваниеСтроки	- Строка	- Название строки
//
// Возвращаемое значение:
//	Число	        - Количество напоминаний
//
Функция АРМКоммуникаторКоличествоНапоминаний(НазваниеСтроки)
	Запрос = Новый Запрос;
	// заполним количество
	ТекстЗапроса = "ВЫБРАТЬ
				   |	Напоминания.Пользователь
				   |ИЗ
				   |	РегистрСведений.Напоминания КАК Напоминания
				   |ГДЕ
				   |	Напоминания.Завершено = &Завершено";
	// Посчитаем количество элементов 
	Если ВРег(НазваниеСтроки) = "АКТУАЛЬНЫЕ" Тогда
		ТекстЗапроса = ТекстЗапроса +"
									 |	И Напоминания.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
		Запрос.УстановитьПараметр("Завершено", Ложь);
	ИначеЕсли ВРег(НазваниеСтроки) = "СОЗДАННЫЕ" Тогда
		ТекстЗапроса = ТекстЗапроса +"
									 |	И Напоминания.Автор = &Автор";
		Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.Пользователь);
		Запрос.УстановитьПараметр("Завершено", Ложь);
	ИначеЕсли ВРег(НазваниеСтроки) = "ЗАВЕРШЕННЫЕ" Тогда
		ТекстЗапроса = ТекстЗапроса +"
									 |	И Напоминания.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
		Запрос.УстановитьПараметр("Завершено", Истина);
	ИначеЕсли ВРег(НазваниеСтроки) = "ПРОСРОЧЕННЫЕ" Тогда
		ТекстЗапроса = ТекстЗапроса +"
									 |	И Напоминания.Пользователь = &Пользователь
									 |	И Напоминания.ДатаАктуальности > &НачалоПериода
									 |	И Напоминания.ДатаАктуальности < &ОкончаниеПериода";
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
		Запрос.УстановитьПараметр("Завершено", Ложь);
		Запрос.УстановитьПараметр("НачалоПериода", '00010101');
		Запрос.УстановитьПараметр("ОкончаниеПериода", ТекущаяДата());
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	КоличествоВсего = Запрос.Выполнить().Выгрузить().Количество();
	
	Возврат КоличествоВсего;
КонецФункции// КоличествоНапоминаний()

// функция получения таблицы событий
//
// Параметр:
//	ПараметрНаименование	- Строка	- Переданный параметр
//
// Возвращаемое значение:
//	ТаблицаЗначений         - Таблица событий
//
Функция АРМКоммуникаторПолучитьТаблицуСобытий(ПараметрНаименование,ДатаНачалаПериодаСобытий,ДатаОкончанияПериодаСобытий)
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Событие.Ссылка,
	               |	Событие.ПометкаУдаления,
	               |	Событие.Номер,
	               |	Событие.Дата,
	               |	Событие.CRM_GUIDЗвонка,
	               |	Событие.Автор,
	               |	Событие.ВидСобытия,
	               |	Событие.ДатаНачала КАК ДатаНачала,
	               |	Событие.ДатаОкончания,
	               |	Событие.ДокументОснование,
	               |	Событие.ИсточникИнформации,
	               |	Событие.Комментарий,
	               |	Событие.Организация,
	               |	Событие.ПодразделениеКомпании,
	               |	Событие.Приоритет,
	               |	Событие.Продолжительность,
	               |	Событие.Результат,
	               |	Событие.Содержание,
	               |	Событие.Состояние,
	               |	Событие.Тема,
	               |	Событие.ТипСообщения,
	               |	Событие.ФорматТекста,
	               |	Событие.Представление
	               |ИЗ
	               |	Документ.Событие КАК Событие
	               |ГДЕ
	               |	Событие.Пользователи.Пользователь = &Пользователь";
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
	Если Не(ВРег(ПараметрНаименование) = "СОБЫТИЯ") Тогда
		Если ВРег(ПараметрНаименование) = "В РАБОТЕ" Тогда
			ТекстЗапроса = ТекстЗапроса + "
										  |	И Событие.Состояние В (&Состояние)";
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
										  |	И Событие.Состояние = &Состояние";
		КонецЕсли;
		Если ВРег(ПараметрНаименование) = "ПРОСРОЧЕННЫЕ" Тогда
			ТекстЗапроса = ТекстЗапроса + "
										  |	И Событие.ПометкаУдаления = ЛОЖЬ
										  |	И Событие.ДатаНачала < &ТекущаяДата";
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
			Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияСобытий.Запланировано);
		ИначеЕсли ВРег(ПараметрНаименование) = "НЕ ОБРАБОТАНО" Тогда
			ТекстЗапроса = ТекстЗапроса + "
										  |	И Событие.ПометкаУдаления = ЛОЖЬ
										  |	И Событие.ДатаНачала <= &ДатаОкончания";
			Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончанияПериодаСобытий);
			Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияСобытий.НеОбработано);
		ИначеЕсли ВРег(ПараметрНаименование) = "В РАБОТЕ" Тогда
			ТекстЗапроса = ТекстЗапроса + "
										  |	И Событие.ПометкаУдаления = ЛОЖЬ
										  |	И Событие.ДатаНачала >= &ДатаНачалаОтбора
										  |	И Событие.ДатаНачала <= &ТекущаяДата";
			Запрос.УстановитьПараметр("ДатаНачалаОтбора", НачалоДня(ТекущаяДата()) - 30 * 24 * 3600);
			Запрос.УстановитьПараметр("ТекущаяДата", КонецДня(ТекущаяДата()));
			СписокСостояний = Новый Массив;
			СписокСостояний.Добавить(Перечисления.СостоянияСобытий.Выполняется);
			СписокСостояний.Добавить(Перечисления.СостоянияСобытий.Утверждено);
			Запрос.УстановитьПараметр("Состояние", СписокСостояний);
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
										  |	И Событие.ДатаНачала >= &ДатаНачала";
			Запрос.УстановитьПараметр("ДатаНачала", ДатаНачалаПериодаСобытий);
			Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончанияПериодаСобытий);
			Если ВРег(ПараметрНаименование) = "ЗАПЛАНИРОВАННЫЕ" Тогда
				Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияСобытий.Запланировано);
			ИначеЕсли ВРег(ПараметрНаименование) = "ОТМЕНЕННЫЕ" Тогда	
				Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияСобытий.Отменено);
			ИначеЕсли ВРег(ПараметрНаименование) = "ЗАВЕРШЕННЫЕ" Тогда					
				Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияСобытий.Завершено);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
								  |
	               				  |УПОРЯДОЧИТЬ ПО
	                              |	ДатаНачала ВОЗР";
	Запрос.Текст = ТекстЗапроса;
	ТаблицаСобытий = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаСобытий;
КонецФункции// ПолучитьТаблицуСобытий()

// Функция возвращает количество писем в учетной записи
Функция АРМКоммуникаторКоличествоПисемУчетнойЗаписи(УчетнаяЗапись)
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЭлектронноеПисьмо.Ссылка КАК Ссылка,
	               |	ВЫБОР
	               |		КОГДА ЭлектронноеПисьмо.НеРассмотрено
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК НеПрочитанное
	               |ИЗ
	               |	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
	               |ГДЕ
	               |	ЭлектронноеПисьмо.УчетнаяЗапись = &УчетнаяЗапись
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Ссылка),
	               |	СУММА(НеПрочитанное)
	               |ПО
	               |	ОБЩИЕ";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Результат = Запрос.Выполнить();
	СтрокаКоличествоВсего = "";
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		СтрокаКоличествоВсего = Строка(Выборка.Ссылка)+?(Выборка.НеПрочитанное > 0,"("+Выборка.НеПрочитанное+")","");
	КонецЕсли;	
	Возврат СтрокаКоличествоВсего;
КонецФункции// КоличествоПисем()

// Функция возвращает количество писем в группе
Функция АРМКоммуникаторКоличествоПисем(ГруппаПисем)
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЭлектронноеПисьмо.Ссылка КАК Ссылка,
	               |	ВЫБОР
	               |		КОГДА ЭлектронноеПисьмо.НеРассмотрено
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК НеПрочитанное
	               |ИЗ
	               |	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
	               |ГДЕ
	               |	ЭлектронноеПисьмо.ГруппаПисем = &ГруппаПисем
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Ссылка),
	               |	СУММА(НеПрочитанное)
	               |ПО
	               |	ОБЩИЕ";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ГруппаПисем", ГруппаПисем);
	Результат = Запрос.Выполнить();
	СтрокаКоличествоВсего = "0";
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		СтрокаКоличествоВсего = Строка(Выборка.Ссылка)+?(Выборка.НеПрочитанное > 0,"("+Выборка.НеПрочитанное+")","");
	КонецЕсли;	
	Возврат СтрокаКоличествоВсего;
КонецФункции// КоличествоПисем()

// Вызов произвольной обработки или функции
Процедура ТестированиеИИсправлениеИБ(Знач СтруктураПараметров) Экспорт
	
КонецПроцедуры // ТестированиеИИсправлениеИБ()

// Процедура создание обмена с сайтом
Процедура ЗаданиеОбменССайтом(СтруктураПараметров) Экспорт
	
	Если (обЗначениеНеЗаполнено(СтруктураПараметров.КодНастройки)) Тогда
		Возврат;
	Иначе
		КодНастройки	= СтруктураПараметров.КодНастройки;
	КонецЕсли;
	
	НастройкаОбмена = ПланыОбмена.ОбменССайтом.НайтиПоКоду(КодНастройки);
	
	Если (обЗначениеНеЗаполнено(НастройкаОбмена)) Тогда
		Возврат;
	КонецЕсли;
	
	Если ( НастройкаОбмена.ПометкаУдаления ) Тогда
		Возврат;
	КонецЕсли;
		
	ОбработкаОбмена					= Обработки.ОбменССайтом.Создать();	
	ОбработкаОбмена.УзелОбмена		= НастройкаОбмена;
	ОбработкаОбмена.ПроизвестиВыгрузкуДанных();
	
КонецПроцедуры

Процедура ЗаданиеОбменССайтомАвтосервиса(СтруктураПараметров) Экспорт

	Если (обЗначениеНеЗаполнено(СтруктураПараметров.КодНастройки)) Тогда
		Возврат;
	Иначе
		КодНастройки	= СтруктураПараметров.КодНастройки;
	КонецЕсли;

	НастройкаОбмена = ПланыОбмена.ОбменССайтомАвтосервиса.НайтиПоКоду(КодНастройки);
	
	Если (обЗначениеНеЗаполнено(НастройкаОбмена)) Тогда
		Возврат;
	КонецЕсли;
	
	Если ( НастройкаОбмена.ПометкаУдаления ) Тогда
		Возврат;
	КонецЕсли;
		
	ОбработкаОбмена					= Обработки.ОбменССайтомАвтосервиса.Создать();	
	ОбработкаОбмена.УзелОбмена		= НастройкаОбмена;
	
	ОбработкаОбмена.ВыгружатьДанныеНаСайт = НастройкаОбмена.ВыгружатьДанныеНаСайт;
	ОбработкаОбмена.ЗагружатьДанныеССайта = НастройкаОбмена.ЗагружатьДанныеССайта;
	ОбработкаОбмена.ВыгружатьТолькоИзменения = НастройкаОбмена.ВыгружатьТолькоИзменения;
	ОбработкаОбмена.ОбменМоделями = НастройкаОбмена.ОбменМоделями;
	ОбработкаОбмена.ОбменКонтрагентами = НастройкаОбмена.ОбменКонтрагентами;
	ОбработкаОбмена.ОбменАвтомобилями = НастройкаОбмена.ОбменАвтомобилями;
	ОбработкаОбмена.ОбменЦенами = НастройкаОбмена.ОбменЦенами;
	ОбработкаОбмена.ОбменТО = НастройкаОбмена.ОбменТО;
	ОбработкаОбмена.ОбменЗаявками = НастройкаОбмена.ОбменЗаявками;
	ОбработкаОбмена.ОбменЗаказНарядами = НастройкаОбмена.ОбменЗаказНарядами;
	
	ОбработкаОбмена.ПроизвестиВыгрузкуДанных();
	
КонецПроцедуры


/////////////////////////
// ОБМЕН

// Выделить из строки код настройки доставки
Функция ВыделитьКодНастройкиДоставки(Команда) Экспорт
	
	// проверка полученной настройки выполнится уже при обработке сообщения
	ПозНастройка = Найти(Команда,"(");
	СтрокаНастройка = "";
	Если ПозНастройка > 0 Тогда
		СтрокаНастройка = Сред(Команда,ПозНастройка+1);
	КонецЕсли;
	Если СтрокаНастройка <> "" Тогда
		ПозНастройка = Найти(СтрокаНастройка,")");
		Если ПозНастройка > 0 Тогда
			СтрокаНастройка = Лев(СтрокаНастройка,ПозНастройка-1);
		КонецЕсли;
	КонецЕсли;
	СтрокаНастройка = СокрЛП(СтрокаНастройка);
		
	Возврат СтрокаНастройка;
	
КонецФункции

// Выделение из строки количество элементов
Функция ВыделитьКоличествоЭлементов(Команда)

	ПозКолво = Найти(Команда,":");
	Если ПозКолво > 0 Тогда
		ТекСтрока = Сред(Команда,ПозКолво+1);
		
		СтрокаКолво = "";
		Для Инд = 1 По СтрДлина(ТекСтрока) Цикл
		  ТекСимвол = Сред(ТекСтрока,Инд,1);
		  Если Найти("0123456789",ТекСимвол) > 0 Тогда
		      СтрокаКолво = СтрокаКолво + ТекСимвол; 
		  Иначе	
		      Если СтрокаКолво <> ""  Тогда
			      Прервать;
			  КонецЕсли; 
		  КонецЕсли; 	
		
		КонецЦикла;  
		Команда = Лев(Команда,ПозКолво-1);
	Иначе
		СтрокаКолво = "0";
	КонецЕсли;
	Попытка
		КоличествоЭлементовВТранзакции = Число(СтрокаКолво);
	Исключение
		КоличествоЭлементовВТранзакции = 0;
	КонецПопытки;
	
	Возврат КоличествоЭлементовВТранзакции;

КонецФункции

// Основной метод обмена. Выполняет процедуры обмена согласно ТЧ
// Используется обработка ОбменСУдаленнымиПодразделениями. В процедуру обмена также включается и доставка
//
Процедура ВыполнитьОбмен(ОбменУдаленныеПодразделения,ВестиЖурналРегистрации = Истина,ВестиЛогФайл = Ложь,
		КаталогЛогов = "",ФиксироватьПриемОтправкуОбъектов = Ложь, ВыполнятьОбменНаКлиенте = Ложь, Рез = Ложь, ПрерватьЗадание = Ложь) Экспорт
	КонфигурацияИзменена_ = КонфигурацияИзменена();
	ОбработкаОбмен = Обработки.ОбменСУдаленнымиПодразделениями.Создать();
	ОбработкаОбмен.КомментироватьОбъекты = ФиксироватьПриемОтправкуОбъектов;
	ОбработкаОбмен.ВестиЖурналРегистрации = ВестиЖурналРегистрации;
	ОбработкаОбмен.ВестиЛогФайл = ВестиЛогФайл;
	ОбработкаОбмен.КаталогЛогов = КаталогЛогов;
	ОбработкаОбмен.Комментировать = Истина; // пока  фоновом нет параметра
	ОбработкаОбмен.ВыполнятьОбменНаКлиенте = ВыполнятьОбменНаКлиенте;
	// проверим, не выполняются ли другие фоновые задания обмена
	Если обЭтоКлиентСервер() Тогда
		ПредставлениеФоновыхЗаданий = "";
		Попытка
			ЕстьЗапущенныеЗадания = ОбработкаОбмен.ПроверитьЗапускЗаданияОбмена(Ложь,ПредставлениеФоновыхЗаданий);
			Если ЕстьЗапущенныеЗадания Тогда
				СтрСообщения = "Ошибка при запуске фонового задания. Обнаружены следующие активные фоновые задания обмена: "
					+ Символы.ПС + ПредставлениеФоновыхЗаданий;	
				ЗаписьЖурналаРегистрации("Обмен.Запуск", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
				#Если Клиент Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);	
				#КонецЕсли 
				Рез = Ложь;
				Возврат;
			КонецЕсли; 
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
			ЗаписьЖурналаРегистрации("Обмен.Запуск", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			#Если Клиент Тогда
			Сообщить("Ошибка получения активных фоновых заданий обмена: " + ТекстОшибки, СтатусСообщения.Внимание);
			#КонецЕсли
		КонецПопытки;
	КонецЕсли; 
		
	ИтогоПриемВыполнен = Истина;
	ИтогоОтправкаВыполнена = Истина;
	Для Каждого ТекНастройкаОбмена Из ОбменУдаленныеПодразделения Цикл
		
		//проверки
		Если ТекНастройкаОбмена.Узел.Пустая() ИЛИ ТекНастройкаОбмена.НастройкаДоставки.Пустая() ИЛИ
			(НЕ ТекНастройкаОбмена.Отправка
			И НЕ ТекНастройкаОбмена.Прием) Тогда
			// допроводим принудительно, независимо от обмена (отдельная строка)
			Если ТекНастройкаОбмена.ДопроводитьПоПартиям Тогда
				дкДопровестиПоПартиям();  // в строке стоит только допроведение и больше ничего - просто допроводим
			КонецЕсли; 
		    Продолжить;
		КонецЕсли;
		
		// SUIG - оставить порядок как есть + считаем что отсут-ие сообщения обмена при приеме - не ошибка
		ПриемВыполнен = Истина;
		ОтправкаВыполнена = Истина;
		
		// для подч. узла - прием - отправка ??
		Если ТекНастройкаОбмена.Прием Тогда
			ОбработкаОбмен.УзелОбмена = ТекНастройкаОбмена.Узел;
			ОбработкаОбмен.НастройкаОбмена = ТекНастройкаОбмена.НастройкаДоставки;
			ПриемВыполнен = ОбработкаОбмен.ВыполнитьЗагрузку(,,,,, ПрерватьЗадание);
		КонецЕсли;
		
		Если НЕ ТекНастройкаОбмена.Узел  = ПланыОбмена.ГлавныйУзел() Тогда
			Если НЕ ПриемВыполнен Тогда
				Продолжить; // сбой при приеме от доч. узла - просто идем дальше 
			КонецЕсли;
		Иначе
			Если НЕ ПриемВыполнен ИЛИ ((НЕ КонфигурацияИзменена_) И КонфигурацияИзменена()) Тогда
				Прервать; // сбой при приеме от род. узла - дальше идти незачем 
			КонецЕсли;
		КонецЕсли;
		
		Если ТекНастройкаОбмена.Отправка Тогда
			ОбработкаОбмен.УзелОбмена = ТекНастройкаОбмена.Узел;
			ОбработкаОбмен.НастройкаОбмена = ТекНастройкаОбмена.НастройкаДоставки;
			ОтправкаВыполнена = ОбработкаОбмен.ВыполнитьВыгрузку(,, ПрерватьЗадание);
		КонецЕсли;
		
		// после всех обмена допроведем
		// вместе с обменом имеет смысл, если  все прошло успешно
		Если ПриемВыполнен ИЛИ ОтправкаВыполнена Тогда
			Если ТекНастройкаОбмена.ДопроводитьПоПартиям Тогда
				дкДопровестиПоПартиям(); 
			КонецЕсли; 
		КонецЕсли; 
		
       		
		Если НЕ ТекНастройкаОбмена.Узел  = ПланыОбмена.ГлавныйУзел() Тогда
			Если НЕ ОтправкаВыполнена Тогда
				Продолжить; // сбой при отправке род. узлу - просто идем дальше (есть и другие узлы) 
			КонецЕсли;
		Иначе
		    Если НЕ ОтправкаВыполнена Тогда
				Прервать; // сбой при отправке род. узлу - дальше идти незачем (дочкам дочери отправлять не нужно)
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПриемВыполнен Тогда
		    ИтогоПриемВыполнен = Ложь;
		КонецЕсли; 
		
		Если НЕ ОтправкаВыполнена Тогда
		    ИтогоОтправкаВыполнена = Ложь;
		КонецЕсли; 
		
	КонецЦикла;

	Рез = ИтогоПриемВыполнен И ИтогоОтправкаВыполнена;
КонецПроцедуры // ВыполнитьОбмен()

// Преобразует строку-параметр запуска (схема обмена) в таблицу значений для запуска обмена
// через командный файл
//
Функция РазобратьСхемуОбмена(СхемаОбмена,ВестиЖурналРегистрации = Истина,ВестиЛогФайл = Ложь,
		КаталогЛогов = "",ФиксироватьПриемОтправкуОбъектов = Ложь, ВыполнятьОбменНаКлиенте = Ложь) Экспорт
	
	ОбменУдаленныеПодразделения = Новый ТаблицаЗначений;
	ОбменУдаленныеПодразделения.Колонки.Добавить("Узел");
	ОбменУдаленныеПодразделения.Колонки.Добавить("НастройкаДоставки");
	ОбменУдаленныеПодразделения.Колонки.Добавить("Прием");
	ОбменУдаленныеПодразделения.Колонки.Добавить("Отправка");
    ОбменУдаленныеПодразделения.Колонки.Добавить("КоличествоЭлементовВТранзакции");
	ОбменУдаленныеПодразделения.Колонки.Добавить("ДопроводитьПоПартиям");
	
	ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	ГлавныйУзел = ПланыОбмена.ГлавныйУзел();
	СхемаОбмена = ВРег(СхемаОбмена);
	СтрокаКоманд = Сред(СхемаОбмена, Найти(СхемаОбмена, "EXCHANGE") + 8);
	Пока СтрДлина(СтрокаКоманд) > 0 Цикл
		ЛевСимвол = Лев(СтрокаКоманд,1);
		Пока Найти(" :=/\-;|(){}[]'""",ЛевСимвол) > 0 Цикл
			СтрокаКоманд = Сред(СтрокаКоманд,2);
			ЛевСимвол = Лев(СтрокаКоманд,1);
		КонецЦикла;
		
		// Преобразуем команду в кодировку 1251 на всякий случай (если код узла на русском)
		СтрокаКоманд = одОбменДанными.ПреобразоватьТекстДос(СтрокаКоманд);

		ПозРазделителя = Найти(СтрокаКоманд,";");
		
		Если ПозРазделителя > 0 Тогда
			Команда = Лев(СтрокаКоманд,ПозРазделителя-1);
			СтрокаКоманд = Сред(СтрокаКоманд,ПозРазделителя+1);
		Иначе
			Команда = СтрокаКоманд;
			СтрокаКоманд = "";
		КонецЕсли;
		
		Если Найти(Команда,"REPOST") = 1 Тогда
		   	НовоеДействие = ОбменУдаленныеПодразделения.Добавить();
			НовоеДействие.Узел = ПланыОбмена.УдаленныеПодразделения.ПустаяСсылка();
			НовоеДействие.НастройкаДоставки = Справочники.НастройкиДоставкиСообщений.ПустаяСсылка();
			НовоеДействие.Прием = Ложь;
			НовоеДействие.Отправка = Ложь;
			НовоеДействие.КоличествоЭлементовВТранзакции = 0;
			НовоеДействие.ДопроводитьПоПартиям = Истина;
		
		// получение из главного  дочерний узел
		// EXCHANGE:FROMMAIN(ЦБ000001), где ЦБ000001 - код настройки доставки
		ИначеЕсли Найти(Команда,"FROMMAIN")  = 1 Тогда
			УзелОбмена = ПланыОбмена.ГлавныйУзел();
			Если УзелОбмена = Неопределено Тогда
				Продолжить; //текущий узел самый главный, а значит игнорируем
			КонецЕсли;
			КодУзла=" ("+ВРЕГ(СокрЛП(УзелОбмена.Код))+")"; ИмяУзла=""""+СокрП(УзелОбмена.Наименование)+"""";
			
			// определим настройку доставки (если указана)
			КодНастройкаДоставки = ВыделитьКодНастройкиДоставки(Команда);
			Если КодНастройкаДоставки = "" Тогда
				ТекНастройкаДоставки = УзелОбмена.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
			Иначе
				// исходим из того, что это код элемента справочника НастройкиДоставки
				ТекНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(КодНастройкаДоставки);
				// не нашли - выдадим ошибку, не берем настройку по умолчанию, пользователь должен знать
			КонецЕсли;
            			
			// получим количество элементов
			КоличествоЭлементовВТранзакции = ВыделитьКоличествоЭлементов(Команда);
			
			НовоеДействие = ОбменУдаленныеПодразделения.Добавить();
			НовоеДействие.Узел = УзелОбмена;
			// получаем текущую настройку доставки для главного узла
			НовоеДействие.НастройкаДоставки = ТекНастройкаДоставки;
			НовоеДействие.Прием = Истина;
			НовоеДействие.Отправка = Ложь;
			НовоеДействие.КоличествоЭлементовВТранзакции = КоличествоЭлементовВТранзакции;
			НовоеДействие.ДопроводитьПоПартиям = Ложь;
			ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен", УровеньЖурналаРегистрации.Информация, , , КодУзла);
			ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен", УровеньЖурналаРегистрации.Информация, , , "  Настройка: " + СокрЛП(НовоеДействие.НастройкаДоставки));
			
			// отправка из дочернего в главный
			// EXCHANGE:OUTPUTMAIN(ЦБ000001), где ЦБ000001 - код настройки доставки
		ИначеЕсли Найти(Команда,"OUTPUTMAIN") = 1 Тогда
			УзелОбмена = ПланыОбмена.ГлавныйУзел();
			Если УзелОбмена = Неопределено Тогда
				Продолжить; //текущий узел самый главный, а значит игнорируем
			КонецЕсли;
			КодУзла=" ("+ВРЕГ(СокрЛП(УзелОбмена.Код))+")"; ИмяУзла=""""+СокрП(УзелОбмена.Наименование)+"""";
			
			// определим настройку доставки (если указана)
			КодНастройкаДоставки = ВыделитьКодНастройкиДоставки(Команда);
			Если КодНастройкаДоставки = "" Тогда
				ТекНастройкаДоставки = УзелОбмена.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
			Иначе
				// исходим из того, что это код элемента справочника НастройкиДоставки
				ТекНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(КодНастройкаДоставки);
				// не нашли - выдадим ошибку, не берем настройку по умолчанию, пользователь должен знать
			КонецЕсли;
			
			// получим количество элементов
			КоличествоЭлементовВТранзакции = ВыделитьКоличествоЭлементов(Команда);
			
			НовоеДействие = ОбменУдаленныеПодразделения.Добавить();
			НовоеДействие.Узел = УзелОбмена;
			// получаем текущую настройку доставки для главного узла
			НовоеДействие.НастройкаДоставки = ТекНастройкаДоставки;
			НовоеДействие.Прием = Ложь;
			НовоеДействие.Отправка = Истина;
			НовоеДействие.КоличествоЭлементовВТранзакции = КоличествоЭлементовВТранзакции;
			НовоеДействие.ДопроводитьПоПартиям = Ложь;
			
			// получение из дочерних в главный 
		ИначеЕсли Найти(Команда, "FROMSUBS") = 1 Тогда
		
			// получим количество элементов
			КоличествоЭлементовВТранзакции = ВыделитьКоличествоЭлементов(Команда);

			// Получим список кодов узлов если были заданы расширенные параметры (FromSubs=U1,U2,U3)
			СтрокаСКодамиУзлов = "";
			УказаныКодыУзлов = Ложь;
			Если СокрЛП(Сред(Команда,9,1)) = "="  Тогда
				СтрокаСКодамиУзлов = ВРЕГ(СокрЛП(Сред(Команда,10)));
			    УказаныКодыУзлов = Истина;
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен.ПолучениеДанныхИзПодчУзлов", УровеньЖурналаРегистрации.Информация, , , "Начало получения данных из подчиненных узлов" + ?(СтрДлина(СтрокаСКодамиУзлов)>0,": "+СтрокаСКодамиУзлов,""));
			Если НЕ ПустаяСтрока(СтрокаСКодамиУзлов) Тогда
				СтрокаСКодамиУзлов=","+СтрокаСКодамиУзлов+","; // Обрамим запятыми, при сравнении код узла также будем брать в запятые
			КонецЕсли;
			
			МассивПодстрок = обРазложитьСтрокуВМассивПодстрок(СтрокаСКодамиУзлов,",");
			СоответствиеУзелНастройка = Новый Соответствие;
			Для Инд = 0 По МассивПодстрок.ВГраница() Цикл
				// определим настройку доставки (если указана)
				КодНастройкаДоставки = ВыделитьКодНастройкиДоставки(МассивПодстрок[Инд]);
				КодУзелОбмена =  СокрЛП(СтрЗаменить(МассивПодстрок[Инд],"(" + КодНастройкаДоставки + ")",""));
				СоответствиеУзелНастройка.Вставить(КодУзелОбмена,КодНастройкаДоставки);
			КонецЦикла;
			
			//СчОбработано = 0;
			ВыборкаУзлов = ПланыОбмена.УдаленныеПодразделения.Выбрать();
			ОбщаяНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.ПустаяСсылка();
			Если НЕ УказаныКодыУзлов Тогда
				ОбщийКодНастройкиДоставки = ВыделитьКодНастройкиДоставки(Команда);
				ОбщаяНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(ОбщийКодНастройкиДоставки);
			КонецЕсли;
			Пока ВыборкаУзлов.Следующий() Цикл
				УзелОбмена = ВыборкаУзлов.Ссылка;
				УзелОбменаКод = СокрЛП(УзелОбмена.Код);
				КодУзла=" ("+ВРЕГ(УзелОбменаКод)+")"; ИмяУзла=""""+СокрП(УзелОбмена.Наименование)+"""";
				
				Если (УзелОбмена = ЭтотУзел)
					ИЛИ (УзелОбмена = ГлавныйУзел)
					ИЛИ (НЕ ПустаяСтрока(СтрокаСКодамиУзлов) И СоответствиеУзелНастройка[УзелОбменаКод] = Неопределено) Тогда
					Продолжить; // Этот узел сейчас нам не нужен - пропускаем
				ИначеЕсли УзелОбмена.ПометкаУдаления Тогда
					ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен.ПолучениеДанныхИзПодчУзлов", УровеньЖурналаРегистрации.Предупреждение, , , "Узел " + ИмяУзла + КодУзла + " помечен на удаление - ИГНОРИРУЕТСЯ (обмен не выполняется)");
					Продолжить; // Этот узел сейчас нам не нужен - пропускаем
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОбщаяНастройкаДоставки) Тогда
					ТекНастройкаДоставки = ОбщаяНастройкаДоставки;
				Иначе
					Если НЕ ЗначениеЗаполнено(СоответствиеУзелНастройка[УзелОбменаКод]) Тогда
						ТекНастройкаДоставки = УзелОбмена.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
					Иначе
						// исходим из того, что это код элемента справочника НастройкиДоставки
						ТекНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(СоответствиеУзелНастройка[УзелОбменаКод]);
						// не нашли - выдадим ошибку, не берем настройку по умолчанию, пользователь должен знать
					КонецЕсли;
				КонецЕсли;
				НовоеДействие = ОбменУдаленныеПодразделения.Добавить();
				НовоеДействие.Узел = УзелОбмена;
				// получаем текущую настройку доставки для главного узла
				НовоеДействие.НастройкаДоставки = ТекНастройкаДоставки;
				НовоеДействие.Прием = Истина;
				НовоеДействие.Отправка = Ложь;
				НовоеДействие.КоличествоЭлементовВТранзакции = КоличествоЭлементовВТранзакции;
			    НовоеДействие.ДопроводитьПоПартиям = Ложь;
			
			КонецЦикла;				
			
			// отправка из главного в дочерние
		ИначеЕсли Найти(Команда, "OUTPUTSUBS") = 1 Тогда
			// Получим список кодов узлов если были заданы расширенные параметры (OutPutSubs=U1,U2,U3:50)
			
			// получим количество элементов
			КоличествоЭлементовВТранзакции = ВыделитьКоличествоЭлементов(Команда);
			СтрокаСКодамиУзлов = "";
			УказаныКодыУзлов = Ложь;
			Если СокрЛП(Сред(Команда,11,1)) = "="  Тогда
				СтрокаСКодамиУзлов = ВРЕГ(СокрЛП(Сред(Команда,12)));
			    УказаныКодыУзлов = Истина;
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен.ВыгрузкаДанныхВПодчУзлы", УровеньЖурналаРегистрации.Информация, , , "Начало отправки данных в подчиненные узлы" + ?(СтрДлина(СтрокаСКодамиУзлов)>0,": "+СтрокаСКодамиУзлов,""));
			
			Если НЕ ПустаяСтрока(СтрокаСКодамиУзлов) Тогда
				СтрокаСКодамиУзлов=","+СтрокаСКодамиУзлов+","; // Обрамим запятыми, при сравнении код узла также будем брать в запятые
			КонецЕсли;
			
			МассивПодстрок = обРазложитьСтрокуВМассивПодстрок(СтрокаСКодамиУзлов,",");
			СоответствиеУзелНастройка = Новый Соответствие;
			Для Инд = 0 По МассивПодстрок.ВГраница() Цикл
				// определим настройку доставки (если указана)
				КодНастройкаДоставки = ВыделитьКодНастройкиДоставки(МассивПодстрок[Инд]);
				КодУзелОбмена =  СокрЛП(СтрЗаменить(МассивПодстрок[Инд],"(" + КодНастройкаДоставки + ")",""));
				СоответствиеУзелНастройка.Вставить(КодУзелОбмена,КодНастройкаДоставки);
			КонецЦикла;
			
			ВыборкаУзлов = ПланыОбмена.УдаленныеПодразделения.Выбрать();
			ОбщаяНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.ПустаяСсылка();
			Если НЕ УказаныКодыУзлов Тогда
				ОбщийКодНастройкиДоставки = ВыделитьКодНастройкиДоставки(Команда);
				ОбщаяНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(ОбщийКодНастройкиДоставки);
			КонецЕсли;
			Пока ВыборкаУзлов.Следующий() Цикл
				УзелОбмена = ВыборкаУзлов.Ссылка;
				УзелОбменаКод = СокрЛП(УзелОбмена.Код);
				КодУзла=" ("+ВРЕГ(УзелОбменаКод)+")"; ИмяУзла=""""+СокрП(УзелОбмена.Наименование)+"""";
				Если (УзелОбмена = ЭтотУзел)
					ИЛИ (УзелОбмена = ГлавныйУзел)
					ИЛИ (НЕ ПустаяСтрока(СтрокаСКодамиУзлов) И СоответствиеУзелНастройка[УзелОбменаКод] = Неопределено) Тогда
					Продолжить; // Этот узел сейчас нам не нужен - пропускаем
				ИначеЕсли УзелОбмена.ПометкаУдаления Тогда
					ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен.ПолучениеДанныхИзПодчУзлов", УровеньЖурналаРегистрации.Предупреждение, , , "Узел " + ИмяУзла + КодУзла + " помечен на удаление - ИГНОРИРУЕТСЯ (обмен не выполняется)");
					Продолжить; // Этот узел сейчас нам не нужен - пропускаем
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОбщаяНастройкаДоставки) Тогда
					ТекНастройкаДоставки = ОбщаяНастройкаДоставки;
				Иначе
					Если НЕ ЗначениеЗаполнено(СоответствиеУзелНастройка[УзелОбменаКод]) Тогда
						ТекНастройкаДоставки = УзелОбмена.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
					Иначе
						// исходим из того, что это код элемента справочника НастройкиДоставки
						ТекНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(СоответствиеУзелНастройка[УзелОбменаКод]);
						// не нашли - выдадим ошибку, не берем настройку по умолчанию, пользователь должен знать
					КонецЕсли;
				КонецЕсли;
				НовоеДействие = ОбменУдаленныеПодразделения.Добавить();
				НовоеДействие.Узел = УзелОбмена;
				// получаем текущую настройку доставки для главного узла
				НовоеДействие.НастройкаДоставки = ТекНастройкаДоставки;
				НовоеДействие.Прием = Ложь;
				НовоеДействие.Отправка = Истина;
				НовоеДействие.КоличествоЭлементовВТранзакции = КоличествоЭлементовВТранзакции;
				НовоеДействие.ДопроводитьПоПартиям = Ложь;
				
			КонецЦикла;				
			
		ИначеЕсли НЕ ПустаяСтрока(Команда) Тогда
			ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен.Ошибка", УровеньЖурналаРегистрации.Ошибка,, , "Выделена неизвестная команда обмена: """ + Команда + """");
		КонецЕсли;
		
		// определим необходимость записи логов в журнал регистрации, в файл, каталог логов
		// и детального лога
		Если НЕ ПустаяСтрока(СхемаОбмена) Тогда
			КаталогЛогов = "";
			ВестиЖурналРегистрации = (Найти(СхемаОбмена,"/WRITELOGEVENT") > 0);
			ПозицияЛогФайл = Найти(СхемаОбмена,"/WRITELOGFILE");
			ВестиЛогФайл = (ПозицияЛогФайл > 0);
			Если ВестиЛогФайл Тогда
				ПозицияЛогФайл = ПозицияЛогФайл + 13;
				ТекСимвол = Сред(СхемаОбмена,ПозицияЛогФайл,1);
				Если ТекСимвол = "=" Тогда
					ТекКоманда = Сред(СхемаОбмена,ПозицияЛогФайл + 1);
					МассивПодстрок = обРазложитьСтрокуВМассивПодстрок(ТекКоманда,"""");
					Для Инд = 0 По МассивПодстрок.ВГраница() Цикл
						Если МассивПодстрок[Инд] = "" Тогда
							Продолжить;
						КонецЕсли; 
						КаталогЛогов = МассивПодстрок[Инд];
					КонецЦикла; 
				КонецЕсли;
				// не указан - определим каталог логов из каталога регл.операций или 
				Если КаталогЛогов = "" Тогда
					КорневойКаталог = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
					Если ПустаяСтрока(КорневойКаталог) Тогда
						КаталогЛогов = КаталогВременныхФайлов();
					Иначе
						КаталогЛогов = КорневойКаталог + "LOGs\";
					КонецЕсли;
				КонецЕсли; 
				
			КонецЕсли; 
			ФиксироватьПриемОтправкуОбъектов = (Найти(СхемаОбмена,"/DETAILLOG") > 0);
			ВыполнятьОбменНаКлиенте = (Найти(СхемаОбмена,"/CLIENT") > 0);
		КонецЕсли; 
		
	КонецЦикла; // разбора команд обмена
	
	Инд = 1;
	Для каждого Строка Из ОбменУдаленныеПодразделения Цикл
	   ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен", УровеньЖурналаРегистрации.Информация, , , Строка(Инд) + "  Узел: " + СокрЛП(Строка.Узел));
	   ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен", УровеньЖурналаРегистрации.Информация, , , Строка(Инд) + "  Настройка: " + СокрЛП(Строка.НастройкаДоставки));
	   Инд = Инд + 1;
   КонецЦикла; 
   
   Возврат ОбменУдаленныеПодразделения;
	
КонецФункции

// Выполняет процедуры обмена согласно параметру запуска обмена
//Параметры:
//	СхемаОбмена - параметр-строка запуска обмена, содержащий схему обмена
Процедура ВыполнитьОбменПоСхеме(СхемаОбмена) Экспорт
	ФиксироватьПриемОтправкуОбъектов = Истина; // для ком.файла пока Истина ???
	ВестиЖурналРегистрации = Истина;
	ВестиЛогФайл = Ложь;
	КаталогЛогов = "";
	ВыполнятьОбменНаКлиенте = Ложь;
	Попытка
		ОбменУдаленныеПодразделения = РазобратьСхемуОбмена(СхемаОбмена,ВестиЖурналРегистрации,ВестиЛогФайл,КаталогЛогов,ФиксироватьПриемОтправкуОбъектов, ВыполнятьОбменНаКлиенте);
		ВыполнитьОбмен(ОбменУдаленныеПодразделения,ВестиЖурналРегистрации,ВестиЛогФайл,КаталогЛогов,ФиксироватьПриемОтправкуОбъектов, ВыполнятьОбменНаКлиенте);
	Исключение 
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		ТекстОшибки = "Ошибка выполнения обмена через командный файл: " + ТекстОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " 
			+ Символы.ВК + "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
		#Если Клиент Тогда
			Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		#КонецЕсли 
		ЗаписьЖурналаРегистрации("ОбработкаПараметраЗапуска.Обмен", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Возврат;
	КонецПопытки;
КонецПроцедуры

// Формирует представление фонового задания
//
// Параметры
//  ФоновоеЗадание  – ФоновоеЗадание
//
// Возвращаемое значение:
//   Строка   – строковое представление
//
Функция ПредставлениеФоновогоЗадания(ФоновоеЗадание) Экспорт

	Представление =  СокрЛП(ФоновоеЗадание.Наименование) + "" + ?(ПустаяСтрока(ФоновоеЗадание.Ключ),""," (Ключ=""" + ФоновоеЗадание.Ключ + """)")
		+ ", запущено в " + СокрЛП(ФоновоеЗадание.Начало) + " на сервере " + СокрЛП(ФоновоеЗадание.Расположение);
	Возврат Представление;

КонецФункции // ПредставлениеФоновогоЗадания()

// Процедура - Напоминание о записи на ремонт
//
// Параметры:
//  СтруктураПараметров	 - Стркутура	 - Структура параметров фонового задания
//
Процедура НапоминаниеОЗаписиНаРемонт(СтруктураПараметров) Экспорт
	// Выбираем неотправленные сообщения 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НапоминаниеОЗаписиНаРемонт.ЗаявкаНаРемонт,
	|	НапоминаниеОЗаписиНаРемонт.ЗаявкаНаРемонт.ВидРемонта
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	РегистрСведений.НапоминаниеОЗаписиНаРемонт КАК НапоминаниеОЗаписиНаРемонт
	|ГДЕ
	|	НапоминаниеОЗаписиНаРемонт.ДатаЗапланированнойОтправки < &ТекДата
	|	И НапоминаниеОЗаписиНаРемонт.ДатаНачалаРемонта > &ТекДата
	|	И НапоминаниеОЗаписиНаРемонт.ДатаРеальнойОтправки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.ЗаявкаНаРемонтВидРемонта.ШаблонПриЗаписиНаРемонт,
	|	ВременнаяТаблица.ЗаявкаНаРемонт
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|ГДЕ
	|	НЕ(ВременнаяТаблица.ЗаявкаНаРемонтВидРемонта.ВремяНачалаОтправки < &ТекДата
	|				И ВременнаяТаблица.ЗаявкаНаРемонтВидРемонта.ВремяОкончанияОтправки > &ТекДата)
	|	И ВременнаяТаблица.ЗаявкаНаРемонтВидРемонта.ШаблонПриЗаписиНаРемонт <> ЗНАЧЕНИЕ(Справочник.ШаблоныТекстовыхСообщений.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда 
		Выборка = РезультатЗапроса.Выбрать();
	Иначе
		Возврат;
	КонецЕсли;
	
	// Обходим 
	Пока Выборка.Следующий() Цикл
		// Создадим документ смс
		ДокументСМС = Документы.SMS.СоздатьДокумент();
		// Заполним на основании заявки
		ДокументСМС.Заполнить(Выборка.ЗаявкаНаРемонт);
		// Получим номер отправителя
		Если Не ЗначениеЗаполнено(ДокументСМС.НомерОтправителя) Тогда
			ДокументСМС.НомерОтправителя=ПараметрыСеанса.смсПользователь;
		КонецЕсли;
		// Установим дату отправки
		ДокументСМС.НачалоОтправки=ТекущаяДата();
		
		Попытка
			// Запишем документ
			ДокументСМС.Записать();
			// Отправим
			РезультатОтправки = ДокументСМС.ОтправитьSMS();
			// Внесем запись в регистр
			РегистрыСведений.НапоминаниеОЗаписиНаРемонт.ИзменитьПослеОтправкиИзРегЗадания(Выборка.ЗаявкаНаРемонт, ДокументСМС.Ссылка);
			// Если ошибка, то запишем её в журнал
			Если Не ПустаяСтрока(РезультатОтправки) Тогда
				ЗаписьЖурналаРегистрации("Ошибка отправки документа смс:"+РезультатОтправки++Строка(ДокументСМС.Ссылка),УровеньЖурналаРегистрации.Ошибка,Метаданные.Документы.ЗаявкаНаРемонт,ДокументСМС,"Документ SMS не удалось отправить.");
			КонецЕсли;
		Исключение
			// Если документ не удалось записать
			ЗаписьЖурналаРегистрации("Ошибка записи документа смс:"+Строка(ДокументСМС.Ссылка),УровеньЖурналаРегистрации.Ошибка,Метаданные.Документы.ЗаявкаНаРемонт,ДокументСМС,"Документ SMS не удалось записать.");
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры


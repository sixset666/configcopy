// Проверяет используется ли распределенная база и удаленные подразделения
Функция ЕстьРИБ()	Экспорт
	
	ПланОбмена=Метаданные.ПланыОбмена.Найти("УдаленныеПодразделения");
	Если ПланОбмена<>Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УдаленныеПодразделения.Ссылка) КАК КоличествоУзлов
		|ИЗ
		|	ПланОбмена.УдаленныеПодразделения КАК УдаленныеПодразделения";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.КоличествоУзлов > 1;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура МеханизмВерсионированияОбъектов_ПриЗаписиОбъекта(Источник, Отказ) Экспорт
	
	Перем ЧислоВерсийОбъекта;
	
	Если ОбъектВерсионируется(Источник, ЧислоВерсийОбъекта) и 
		НЕ ПараметрыСеанса.Пользователь.Код = "Робот_ВосстПосл                                   "  //БизТех 11.04.2023 добавлено
		И Не Источник.ДополнительныеСвойства.Свойство("ПропустьВерсию") Тогда     //<<БАА 10.10.23
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла); 
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписатьXML(ЗаписьXML, Источник, НазначениеТипаXML.Явное);
		ЗаписьXML.Закрыть();
		
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		
		УдалитьФайлы(ИмяВременногоФайла);
		
		//попытка временно - на тест
		Попытка
		//БИЗТЕХ КОВАЛЬ 26.09.2024 ++
		//Решение проблемы дублирующихся версий объекта
		//Так как запись происходит через файл то подготовленый файл и надо сравнивать с предыдущей версией
		ИзмененЛи = ЕстьЛиИзмененияВновойВерсии(Источник,ДвоичныеДанные);
		Если ИзмененЛи = Ложь Тогда Возврат КонецЕсли;
		//БИЗТЕХ КОВАЛЬ 26.09.2024 --
		Исключение
		КонецПопытки;
		
		
		верс_ВерсионированиеОбъектовПривилегированный.ЗаписатьВерсиюОбъекта(Источник.Ссылка, ЧислоВерсийОбъекта, ХранилищеДанных);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет настройки версионирования по переданному объекту и
// и возвращает вариант версионирования. Если по объекту не настроено
// версионирование, то он версионируется в соответствии с правилами
// версионирования "по умолчанию".
//
Функция ОбъектВерсионируется(знач Источник, ЧислоВерсийОбъекта)
	
	Попытка
		ИспользоватьВерсионированиеОбъектов = ПараметрыСеанса.верс_ИспользоватьВерсионированиеОбъектов;
	Исключение
		ИспользоватьВерсионированиеОбъектов = Константы.верс_ИспользоватьВерсионированиеОбъектов.Получить();
		ПараметрыСеанса.верс_ИспользоватьВерсионированиеОбъектов = ИспользоватьВерсионированиеОбъектов;	
	КонецПопытки;
	// Проверяем, что подсистема версионирования включена
	Если ИспользоватьВерсионированиеОбъектов Тогда
		
		ВариантВерсионирования = ПолучитьВариантВерсионирования(Источник);
		
		ЧислоВерсийОбъекта = верс_ВерсионированиеОбъектовПривилегированный.ПолучитьКоличествоВерсийОбъекта(Источник.Ссылка);
		
		ВерсионироватьОбъект = Истина;
		
		Если ВариантВерсионирования = Перечисления.верс_ВариантыВерсионированияОбъектов.НеВерсионировать Тогда
			ВерсионироватьОбъект = Ложь;
		ИначеЕсли ВариантВерсионирования = Перечисления.верс_ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении Тогда
			Если ЧислоВерсийОбъекта = 0 И НЕ Источник.Проведен Тогда
				ВерсионироватьОбъект = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		ВерсионироватьОбъект = Ложь;
	КонецЕсли;
	
	Возврат ВерсионироватьОбъект;
	
КонецФункции

Функция ПолучитьВариантВерсионирования(Источник)
	
	ИмяОбъекта = Источник.Метаданные().Имя;
	
	НастройкаВерсионирования = РегистрыСведений.верс_НастройкаВерсионированияОбъектов.СоздатьМенеджерЗаписи();
	НастройкаВерсионирования.ТипОбъекта = ИмяОбъекта;
	НастройкаВерсионирования.Прочитать();
	
	Если ЗначениеЗаполнено(НастройкаВерсионирования.Вариант) Тогда
		Возврат НастройкаВерсионирования.Вариант;
	Иначе
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Источник.Ссылка)) Тогда
			Возврат Перечисления.верс_ВариантыВерсионированияОбъектов.НеВерсионировать;
		Иначе
			Возврат ?(ДокументПроводится(Источник.Метаданные().Имя),
			          Перечисления.верс_ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении,
			          Перечисления.верс_ВариантыВерсионированияОбъектов.ВерсионироватьВсегда);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Проверяет, что документу разрешено проведение
//
Функция ДокументПроводится(знач ИмяДокумента) Экспорт
	
	Возврат Метаданные.Документы[ИмяДокумента].Проведение = 
	            Метаданные.СвойстваОбъектов.Проведение.Разрешить;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Блок функций для работы с настройками значений

// Записывает настройку версионирования по объекту в регистр сведений
//
Процедура ЗаписатьНастройкуВерсионированияПоОбъекту(Объект, ВариантВерсионирования) Экспорт
	
	ВариантМВ = РегистрыСведений.верс_НастройкаВерсионированияОбъектов.СоздатьМенеджерЗаписи();
	ВариантМВ.ТипОбъекта = Объект;
	ВариантМВ.Вариант = ВариантВерсионирования;
	ВариантМВ.Записать();
	
КонецПроцедуры

// Получает настройку версионирования по объекту из регистра сведений
//
Функция ЗагрузитьНастройкуВерсионированияПоОбъекту(знач Объект) Экспорт
	
	ВариантМВ = РегистрыСведений.верс_НастройкаВерсионированияОбъектов.СоздатьМенеджерЗаписи();
	ВариантМВ.ТипОбъекта = Объект;
	ВариантМВ.Прочитать();
	
	Если НЕ ЗначениеЗаполнено(ВариантМВ.Вариант) Тогда
		ВариантМВ.Вариант = Перечисления.верс_ВариантыВерсионированияОбъектов.НеВерсионировать;	
	КонецЕсли;

	Возврат ВариантМВ.Вариант;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Блок функций для работы с формами

#Если Клиент Тогда
	
// Процедура добавляет кнопку открытия отчета. 
// Необходимо добавить вызов процедуры в модуль дкДокументы - дкФормаПередОткрытием() и дкСписокПередОткрытием()
// и спСправочники - спЭлементПередОткрытием() и спСписокПередОткрытием()
// ССБ Начало
// верс_ВерсионированиеОбъектов.верс_удЗаполнитьДополнительныеДействияДокументов(ЭтаФорма);	
// ССБ Конец
Процедура верс_удЗаполнитьДополнительныеДействияДокументов(ЭтаФорма) 	Экспорт
	
	Перем ЧислоВерсийОбъекта;
	
	ВерсионированиеОбъекта = Ложь;
	Попытка
		ВерсионированиеОбъекта = ОбъектВерсионируется(ЭтаФорма.Отбор.Ссылка.Значение, ЧислоВерсийОбъекта);
	Исключение
		Попытка
			ВерсионированиеОбъекта = ОбъектВерсионируется(ЭтаФорма.Ссылка, ЧислоВерсийОбъекта);
		Исключение
			Возврат;
		КонецПопытки;			
	КонецПопытки;	
	Если ВерсионированиеОбъекта Тогда
		
		Попытка
			// ищем подменю "Действия"
			ДействияФормы = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки;
			ПодменюДействия = Неопределено;
			Для НомерКнопки = 1 По ДействияФормы.Количество() Цикл
				ТекПодменю = ДействияФормы.Получить(НомерКнопки-1);
				Если (ТекПодменю.ТипКнопки = ТипКнопкиКоманднойПанели.Подменю) И (ТекПодменю.Текст = "Действия") Тогда
					ПодменюДействия = ТекПодменю;
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
			
			Если ПодменюДействия <> Неопределено Тогда
				Попытка	// Может возникнуть ситуация что форма программно открывается повторно, в этом случае все кнопки уже созданы!		
					
					// добавляем кнопки для вызова дополнительных действий
					КнопкиПодменюДействия = ПодменюДействия.Кнопки;
					КнопкиПодменюДействия.Вставить(0,"верс_Разделитель",ТипКнопкиКоманднойПанели.Разделитель);
					
					Кнопка = КнопкиПодменюДействия.Найти("верс_ИсторияИзменений");
					Если Кнопка = Неопределено Тогда
						//Кнопка = КнопкиПодменюДействия.Вставить(0,"верс_ИсторияИзменений",ТипКнопкиКоманднойПанели.Действие,"История изменений объектов",Новый Действие("верс_ВерсионированиеОбъектов.ОткрытьОтчетИсторияИзмененийОбъектов"));
						Кнопка = КнопкиПодменюДействия.Вставить(0,"верс_ИсторияИзменений",ТипКнопкиКоманднойПанели.Действие,"История изменений объектов",Новый Действие("ДополнительныеДействияФормы"));
						Кнопка.Картинка    = БиблиотекаКартинок.верс_ИсторияВерсий;
						Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
						Кнопка.Подсказка   = "Открыть отчет История изменений объектов";
					КонецЕсли;
				Исключение
				КонецПопытки; 
			КонецЕсли;
		Исключение
			
		КонецПопытки;
	КонецЕсли;	
	
КонецПроцедуры	

// Процедура открытия отчета по кнопке. 
// Необходимо добавить вызов процедуры в модуль дкДокументы - дкОбработкаРеквизита() и спСправочники - спОбработкаРеквизита()
// ССБ Начало
// Если Имя = "верс_ИсторияИзменений" Тогда
// 	верс_ВерсионированиеОбъектов.ОткрытьОтчетИсторияИзмененийОбъектов(ЭтаФорма);
// КонецЕсли;
// ССБ Конец
Процедура ОткрытьОтчетИсторияИзмененийОбъектов(ЭтаФорма)	Экспорт
	
	Попытка
		ТекДокумент = ЭтаФорма.ЭлементыФормы.Список.ТекущаяСтрока;
	Исключение
		ТекДокумент = ЭтаФорма.Ссылка;
	КонецПопытки;                                            
	
	Если НЕ ЗначениеЗаполнено(ТекДокумент) Тогда
        Возврат
    КонецЕсли;
    
    ОтчетПоИзменениям = Отчеты.верс_ИсторияИзмененийОбъектов.Создать();
    ОтчетПоИзменениям.СсылкаНаОбъект = ТекДокумент;
    
    Форма = ОтчетПоИзменениям.ПолучитьФорму();
	//Форма.ВладелецФормы = ЭтаФорма;
    Форма.Открыть();
    Форма.СформироватьТаблицуВерсий();

КонецПроцедуры

#КонецЕсли 

#Область БИЗТЕХ_КОВАЛЬ

Функция ЕстьЛиИзмененияВновойВерсии(Объект,ТекущийДокументXML) Экспорт
	//используем предзаписанный файл
	ТекущийДокументXML = ПолучитьСтрокуИзДвоичныхДанных(ТекущийДокументXML);//ОбъектВстрокуXML(Объект);
	//Получим последнюю версию
	ПоследняяВерсия_Данные = ПолучитьПоследнююВерсию(Объект.Ссылка);
	//ПРоверим что она существует
	Если ПоследняяВерсия_Данные = ложь Тогда Возврат Истина КонецЕсли; 
	//Получим XML из версии
	ПоследняяВерсия_XML = ПолучитьСтрокуИзДвоичныхДанных(ПоследняяВерсия_Данные);
	//Сравниваем
	Если СтрСравнить(ПоследняяВерсия_XML,ТекущийДокументXML) = 0 Тогда
		//Данные одинаковы, нет нужды делать еще одну версию
		Возврат Ложь;
	Иначе
		//Делаем версию
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Функция ОбъектВстрокуXML(Объект) Экспорт
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписатьXML(ЗаписьXML, Объект, НазначениеТипаXML.Явное);
	СтрокаXML = ЗаписьXML.Закрыть();
	Возврат СтрокаXML;
КонецФункции

Функция ПолучитьПоследнююВерсию(Объект) Экспорт
	КоличествоВерс = верс_ВерсионированиеОбъектовПривилегированный.ПолучитьКоличествоВерсийОбъекта(Объект);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ АвторВерсии, ДатаВерсии, ВерсияОбъекта 
	                |ИЗ РегистрСведений.верс_ВерсииОбъектов
	                |ГДЕ Объект = &Ссылка
	                |И НомерВерсии = &НомВер";
	Запрос.УстановитьПараметр("Ссылка", Объект);
	Запрос.УстановитьПараметр("НомВер", Число(КоличествоВерс));
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Если Выборка.ВерсияОбъекта = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Выборка.ВерсияОбъекта.Получить()	
	КонецЕсли; 
	
КонецФункции 

#КонецОбласти


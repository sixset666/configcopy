
#Область СлужебныеПроцедурыИФункции

Функция ВИерархии(Элемент, Группа)
	
	Если НЕ Группа.ЭтоГруппа Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекущийРодитель = Элемент.Родитель;
	
	Пока НЕ ТекущийРодитель.Пустая() Цикл
		Если ТекущийРодитель = Группа Тогда
			Возврат Истина;
		КонецЕсли;
		
		ТекущийРодитель = ТекущийРодитель.Родитель;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиПодписокНаСобытия

// обработчик подписки на соббытие
Процедура ЗаписьЗаказНарядаПриЗаписи(Источник, Отказ) Экспорт
	
	ДокументОснование = Источник.ДокументОснование;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		РегистрироватьИзмененияДокумента = Ложь;
		
		// проверим возможность регистрации заявки
		Если ДокументОснование.ХозОперация <> Справочники.ХозОперации.ПланРемонта ИЛИ (НЕ Источник.ПометкаУдаления И (НЕ Источник.ДополнительныеСвойства.Свойство("РегистрироватьИзмененияДокумента", РегистрироватьИзмененияДокумента) ИЛИ НЕ РегистрироватьИзмененияДокумента)) Тогда
			Возврат;
		КонецЕсли;
		
		ПланыОбмена.ОбменСМобильнымУстройством.ЗарегистрироватьИзмененияДляУзла(ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

// обработчик подписки на соббытие
Процедура ЗаписьЗаявкиНаРемонтПриЗаписи(Источник, Отказ) Экспорт
	
	ДанныеДляРегистрации = Новый Массив; РегистрироватьИзмененияДокумента = Ложь;
	
	// проверим возможность регистрации заявки
	Если Источник.ХозОперация <> Справочники.ХозОперации.ПланРемонта ИЛИ (НЕ Источник.ПометкаУдаления И (НЕ Источник.ДополнительныеСвойства.Свойство("РегистрироватьИзмененияДокумента", РегистрироватьИзмененияДокумента) ИЛИ НЕ РегистрироватьИзмененияДокумента)) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляРегистрации.Добавить(Источник.Ссылка);
	
	// регистрация связанных данных
	Если обЭтотТип(Источник.Контрагент, Тип("СправочникСсылка.Контрагенты")) Тогда
		ДанныеДляРегистрации.Добавить(Источник.Контрагент);
	КонецЕсли;
	
	Если обЭтотТип(Источник.Автомобиль, Тип("СправочникСсылка.Автомобили")) ИЛИ обЭтотТип(Источник.Автомобиль, Тип("СправочникСсылка.Модели")) Тогда
		ДанныеДляРегистрации.Добавить(Источник.Автомобиль);
	КонецЕсли;
	
	Если обЭтотТип(Источник.Автомобиль, "СправочникСсылка.Автомобили") Тогда
		ДанныеДляРегистрации.Добавить(Источник.Автомобиль.Модель);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник.Организация) Тогда
		ДанныеДляРегистрации.Добавить(Источник.Организация);
	КонецЕсли;
	
	Если обЭтотТип(Источник.Заказчик, Тип("СправочникСсылка.Контрагенты")) Тогда
		ДанныеДляРегистрации.Добавить(Источник.Заказчик);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник.СервиснаяКампания) Тогда
		ДанныеДляРегистрации.Добавить(Источник.СервиснаяКампания);
	КонецЕсли;
	
	ПланыОбмена.ОбменСМобильнымУстройством.ЗарегистрироватьИзмененияДляУзла(ДанныеДляРегистрации);
	
КонецПроцедуры

// обработчик подписки на соббытие
Процедура ЗаписьСобытияПриЗаписи(Источник, Отказ) Экспорт
	
	//->Diginova _Настройка базы_ 03.01.2022
	Если Источник.ОбменДанными.Загрузка = Истина тогда
		Возврат;
	КонецЕсли;
	//<-Diginova _FlaiM_

	ДанныеДляРегистрации = Новый Массив; РегистрироватьИзмененияДокумента = Ложь;
	
	// проверим возможность регистрации заявки
	Если НЕ Источник.ПометкаУдаления И (НЕ Источник.ДополнительныеСвойства.Свойство("РегистрироватьИзмененияДокумента", РегистрироватьИзмененияДокумента) ИЛИ НЕ РегистрироватьИзмененияДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляРегистрации.Добавить(Источник.Ссылка);
	
	Если обЭтотТип(Источник.Контрагент, Тип("СправочникСсылка.Контрагенты")) Тогда
		ДанныеДляРегистрации.Добавить(Источник.Контрагент);
	КонецЕсли;
	
	ДанныеДляРегистрации.Добавить(Источник.Автомобиль);
	
	Если обЭтотТип(Источник.Автомобиль, "СправочникСсылка.Автомобили") Тогда
		ДанныеДляРегистрации.Добавить(Источник.Автомобиль.Модель);
	КонецЕсли;
	
	ДанныеДляРегистрации.Добавить(Источник.Организация);
	
	Если обЭтотТип(Источник.КонтактноеЛицо, Тип("СправочникСсылка.Контрагенты")) Тогда
		ДанныеДляРегистрации.Добавить(Источник.КонтактноеЛицо);
	КонецЕсли;
	
	ПланыОбмена.ОбменСМобильнымУстройством.ЗарегистрироватьИзмененияДляУзла(ДанныеДляРегистрации);
	
КонецПроцедуры

// обработчик подписки на соббытие
Процедура ЗаписьАвтомобиляПриЗаписи(Источник, Отказ) Экспорт
	
	Если НЕ Источник.ЭтоГруппа Тогда
		Данные = Новый Массив;
		Данные.Добавить(Источник.Ссылка);
		Данные.Добавить(Источник.Модель);
		ПланыОбмена.ОбменСМобильнымУстройством.ЗарегистрироватьИзмененияДляУзла(Данные);
	КонецЕсли;
	
КонецПроцедуры

// обработчик подписки на соббытие
Процедура ИзменениеЦенАвтоработОбработкаПроведенияОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	// получим набор узлов для регистрации изменений
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТипЦенРабот", Источник.ТипЦен);
	Запрос.УстановитьПараметр("Цех"        , Источник.Цех);
	Запрос.УстановитьПараметр("ВидРемонта" , Источник.ВидРемонта);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбменСМобильнымУстройством.Ссылка
	|ИЗ
	|	ПланОбмена.ОбменСМобильнымУстройством КАК ОбменСМобильнымУстройством
	|ГДЕ
	|	ОбменСМобильнымУстройством.ТипЦенРабот = &ТипЦенРабот
	|	И ОбменСМобильнымУстройством.Цех = &Цех
	|	И ОбменСМобильнымУстройством.ВидРемонта = &ВидРемонта
	|	И ОбменСМобильнымУстройством.ЭтотУзел = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	УзлыДляРегистрации = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДокумента", Источник.Автоработы.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Авторабота КАК Авторабота,
	|	Таблица.Модель КАК Модель
	|ПОМЕСТИТЬ Документ
	|ИЗ
	|	&ТаблицаДокумента КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Авторабота,
	|	Модель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрименяемостьДополнительныхПредложенийПоМоделям.ДополнительноеПредложение,
	|	ПрименяемостьДополнительныхПредложенийПоМоделям.Модель
	|ПОМЕСТИТЬ ДоступныеДопПредложения
	|ИЗ
	|	РегистрСведений.ПрименяемостьДополнительныхПредложенийПоМоделям КАК ПрименяемостьДополнительныхПредложенийПоМоделям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Автоработы.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Модели.ПустаяСсылка)
	|ИЗ
	|	Справочник.Автоработы КАК Автоработы
	|ГДЕ
	|	Автоработы.ЭтоДополнительноеПредложение = ИСТИНА
	|	И Автоработы.ЭтоГруппа = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДоступныеДопПредложения.ДополнительноеПредложение,
	|	Документ.Авторабота,
	|	Документ.Авторабота.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	ДоступныеДопПредложения КАК ДоступныеДопПредложения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ КАК Документ
	|		ПО ДоступныеДопПредложения.Модель = Документ.Модель
	|			И (ДоступныеДопПредложения.ДополнительноеПредложение = Документ.Авторабота
	|				ИЛИ Документ.Авторабота.ЭтоГруппа)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// получим ссылки для регистрации изменений
	Выборка = РезультатЗапроса.Выбрать();
	
	АвтоработыДляРегистрации = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если АвтоработыДляРегистрации.Найти(Выборка.ДополнительноеПредложение) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Выборка.ЭтоГруппа ИЛИ ВИерархии(Выборка.ДополнительноеПредложение, Выборка.Авторабота) Тогда
			
			АвтоработыДляРегистрации.Добавить(Выборка.ДополнительноеПредложение);
			
		КонецЕсли;
	КонецЦикла;
	
	Если АвтоработыДляРегистрации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПланыОбмена.ОбменСМобильнымУстройством.ЗарегистрироватьИзмененияДляУзла(АвтоработыДляРегистрации, УзлыДляРегистрации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПодписокНаСобытия

// зачисщает устаревщие данные для обмена
Процедура ОчисткаУстаревшихРегистрацийВОбменСМобильнымУстройством(Знач СтруктураПараметров) Экспорт
	
	// получим список устареших данных
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаРемонт.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	|ГДЕ
	|	ЗаявкаНаРемонт.ДатаНачала < &ДатаОграниечение
	|	И ЗаявкаНаРемонт.Ссылка В
	|			(ВЫБРАТЬ
	|				ЗаявкаНаРемонтИзменения.Ссылка
	|			ИЗ
	|				Документ.ЗаявкаНаРемонт.Изменения КАК ЗаявкаНаРемонтИзменения)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Событие.Ссылка
	|ИЗ
	|	Документ.Событие КАК Событие
	|ГДЕ
	|	Событие.ДатаНачала < &ДатаОграниечение
	|	И Событие.Ссылка В
	|			(ВЫБРАТЬ
	|				СобытиеИзменения.Ссылка
	|			ИЗ
	|				Документ.Событие.Изменения КАК СобытиеИзменения)";
	Запрос.УстановитьПараметр("ДатаОграниечение", НачалоДня(НачалоДня(ТекущаяДата())-1));
	
	РезультатЗапроса = Запрос.Выполнить(); УзлыОбмена = ПланыОбмена.ОбменСМобильнымУстройством.ПолучитьУзлыОбмена();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		События = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		Для Каждого Событие Из События Цикл
			ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбмена, Событие);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИБ

// Обновление допродаж в Заказ-нарядах
Функция ОбновитьДопродажиВДокументах() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДополнительныеПредложенияДокументов.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ДополнительныеПредложенияДокументов КАК ДополнительныеПредложенияДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка,
	|	ЗаказНаряд.ДокументОснование
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Состояние <> &СостояниеЗакрыт
	|	И ЗаказНаряд.Состояние <> &СостояниеВыполнен
	|	И ЗаказНаряд.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ДополнительныеПредложенияДокументов.Документ
	|			ИЗ
	|				РегистрСведений.ДополнительныеПредложенияДокументов КАК ДополнительныеПредложенияДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНаряд.ДатаСоздания УБЫВ";
	
	Запрос.УстановитьПараметр("СостояниеЗакрыт"  , Справочники.ВидыСостоянийЗаказНарядов.Закрыт);
	Запрос.УстановитьПараметр("СостояниеВыполнен", Справочники.ВидыСостоянийЗаказНарядов.Выполнен);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ВыборкаЗнР = Результаты[0].Выбрать();
	ВыборкаЗН  = Результаты[1].Выбрать();
	
	СкладПоУмолчанию = обПраво(ПланыВидовХарактеристик.ПраваИНастройки.ОсновнойСкладКомпании);
	
	Пока ВыборкаЗнР.Следующий() Цикл
		ВыборкаЗН.Сбросить();
		Если НЕ ВыборкаЗН.НайтиСледующий(Новый Структура("ДокументОснование", ВыборкаЗнР.Документ)) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = ВыборкаЗН.Ссылка.ПолучитьОбъект();
		
		// заполним допродажи
		НаборЗаписей = РегистрыСведений.ДополнительныеПредложенияДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(ВыборкаЗнР.Документ);
		НаборЗаписей.Прочитать();
		
		УдаляемыеАвтоработы = Новый Массив; УдаляемыеМатериалы = Новый Массив;
		
		// удалим значения приемки
		Для Каждого Авторабота Из ДокументОбъект.Работы Цикл
			Если Авторабота.ИсточникПродажи = Перечисления.ИсточникиПродажАвтосервиса.Приемка Тогда
				УдаляемыеАвтоработы.Добавить(Авторабота);
			КонецЕсли;
			
			НайденныеМатериалы = ДокументОбъект.Материалы.НайтиСтроки(Новый Структура("ИдентификаторРаботы", Авторабота.ИдентификаторРаботы));
			Для Каждого Материал Из НайденныеМатериалы Цикл
				УдаляемыеМатериалы.Добавить(Материал);
			КонецЦикла;
		КонецЦикла;
		
		УдаляемыеТовары = ДокументОбъект.Товары.НайтиСтроки(Новый Структура("ИсточникПродажи", Перечисления.ИсточникиПродажАвтосервиса.Приемка));
		
		Для Каждого УдаляемыйТовар Из УдаляемыеТовары Цикл
			ДокументОбъект.Товары.Удалить(УдаляемыйТовар);
		КонецЦикла;
		
		Для Каждого УдаляемаяАвторабота Из УдаляемыеАвтоработы Цикл
			ДокументОбъект.Работы.Удалить(УдаляемаяАвторабота);
		КонецЦикла;
		
		Для Каждого УдаляемыйМатериал Из УдаляемыеМатериалы Цикл
			ДокументОбъект.Материалы.Удалить(УдаляемыйМатериал);
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда
			ДобавленныеРаботы = Новый Массив;
			Для Каждого Запись Из НаборЗаписей Цикл
				НоваяСтрока                     = ДокументОбъект.Работы.Добавить();
				НоваяСтрока.Работа              = Запись.ДополнительноеПредложение;
				НоваяСтрока.Количество          = Запись.Количество;
				НоваяСтрока.ИдентификаторРаботы = Новый УникальныйИдентификатор;
				НоваяСтрока.ИсточникПродажи     = Перечисления.ИсточникиПродажАвтосервиса.Приемка;
				
				ДокументОбъект.ОбработкаРеквизита("Работы.Работа", НоваяСтрока);
				ДобавленныеРаботы.Добавить(Запись.ДополнительноеПредложение);
			КонецЦикла;
			
			НаборЗаписей.Очистить(); НаборЗаписей.Записать();
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СвязанныеРаботы.СвязаннаяРабота КАК СвязаннаяРабота,
			|	СвязанныеРаботы.СвязаннаяРабота.Артикул КАК Артикул,
			|	СвязанныеРаботы.Модель КАК Модель,
			|	СвязанныеРаботы.ВариантКомплектации КАК ВариантКомплектации,
			|	СвязанныеРаботы.Количество КАК Количество,
			|	ВЫБОР
			|		КОГДА СвязанныеРаботы.СвязаннаяРабота ССЫЛКА Справочник.Автоработы
			|			ТОГДА 1
			|		ИНАЧЕ 2
			|	КОНЕЦ КАК ПОРЯДОКРАБОТЫДЕТАЛИ,
			|	ВЫБОР
			|		КОГДА СвязанныеРаботы.Модель = &Модель
			|			ТОГДА 1
			|		ИНАЧЕ 2
			|	КОНЕЦ КАК ПОРЯДОКМОДЕЛЬ,
			|	ВЫБОР
			|		КОГДА СвязанныеРаботы.ВариантКомплектации = &ВариантКомплектации
			|			ТОГДА 1
			|		ИНАЧЕ 2
			|	КОНЕЦ КАК ПОРЯДОКВАРИАНТКОМПЛЕКТАЦИИ,
			|	СвязанныеРаботы.Авторабота КАК Авторабота
			|ИЗ
			|	РегистрСведений.СвязанныеРаботы КАК СвязанныеРаботы
			|ГДЕ
			|	СвязанныеРаботы.Авторабота В(&РаботаВладелец)
			|	И (СвязанныеРаботы.Модель = &Модель
			|			ИЛИ СвязанныеРаботы.Модель = ЗНАЧЕНИЕ(Справочник.Модели.ПустаяСсылка))
			|	И (СвязанныеРаботы.ВариантКомплектации = &ВариантКомплектации
			|			ИЛИ СвязанныеРаботы.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка))
			|	И СвязанныеРаботы.СвязаннаяРабота ССЫЛКА Справочник.Номенклатура
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПОРЯДОКРАБОТЫДЕТАЛИ,
			|	СвязанныеРаботы.СвязаннаяРабота.Наименование,
			|	ПОРЯДОКМОДЕЛЬ,
			|	ПОРЯДОКВАРИАНТКОМПЛЕКТАЦИИ
			|ИТОГИ ПО
			|	Авторабота,
			|	СвязаннаяРабота";
			Запрос.УстановитьПараметр("РаботаВладелец",ДобавленныеРаботы);
			Запрос.УстановитьПараметр("Модель",ДокументОбъект.Автомобиль.Модель);
			Запрос.УстановитьПараметр("ВариантКомплектации", ДокументОбъект.Автомобиль.ВариантКомплектации);
			ВыборкаАвторабот = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаАвторабот.Следующий() Цикл
				ВыборкаСвязанныеАвтоработы = ВыборкаАвторабот.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСвязанныеАвтоработы.Следующий() Цикл
					ВыборкаДетальныеЗаписи = ВыборкаСвязанныеАвтоработы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					// если есть запись добавим в таблицу
					Если ВыборкаДетальныеЗаписи.Следующий() Тогда
						НоваяСтрока = ДокументОбъект.Материалы.Добавить();
						НоваяСтрока.ИдентификаторРаботы = ДокументОбъект.Работы.Найти(ВыборкаАвторабот.Авторабота, "Работа").ИдентификаторРаботы;
						НоваяСтрока.Номенклатура        = ВыборкаДетальныеЗаписи.СвязаннаяРабота;
						НоваяСтрока.Количество          = ВыборкаДетальныеЗаписи.Количество;
						ДокументОбъект.ОбработкаРеквизита("МатериалыЗаказчика.Номенклатура", НоваяСтрока);
						НоваяСтрока.СкладКомпании       = СкладПоУмолчанию;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			ДокументОбъект.Проведен = Истина;
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать();
		КонецЕсли;
		
		ДокументОбъект = Неопределено;
	КонецЦикла;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// В ДАННЫЙ МОДУЛЬ ВЫНЕСЕНЫ ФУНКЦИИ УПРАВЛЕНИЯ ОБОРУДОВАНИЕМ
// ВЫЗОВ КОТОРЫХ МОЖЕТ ПРОИСХОДИТЬ КАК С "КЛИЕНТА" ТАК И С "СЕРВЕРА"
////////////////////////////////////////////////////////////////////////////////

// устанавливает объекту момент, когда его надо перегрузить в оборудование
// 
// Параметры:
//  Объект: 
//		Если Оборудование, то устанавливает момент времени когда в последний раз обновлялось его содержимое
//		Если ГруппаТоваровОборудования, то устанавливает признак для всех товаров входящих в данную группу
//		о том что их надо выгрузить в оборудование
//		Если Номенклатура, то устанавливает признак конкретной номенклатуре.
//  МоментОбновления - дата/время, когда.
//
Процедура уоУстановитьМоментИзменения(Объект,МоментОбновления=Неопределено) Экспорт
	Если ТипЗнч(Объект)=Тип("СправочникСсылка.Оборудование") Тогда
		// оборудование
		Об=Объект.ПолучитьОбъект();
		Об.МоментПоследнейЗагрузки=МоментОбновления;
		Попытка Об.Записать(); Исключение Сообщить("Не удалось проставить момент последней загрузки оборудованию: "+СокрЛП(Объект.Наименование)); КонецПопытки;
		
	ИначеЕсли ТипЗнч(Объект)=Тип("СправочникСсылка.ГруппыТоваровОборудования") ИЛИ ТипЗнч(Объект)=Тип("СправочникСсылка.Номенклатура") Тогда
		// получаем выборку
		ВидОбъекта=?(ТипЗнч(Объект)=Тип("СправочникСсылка.Номенклатура"),"Номенклатура","ГруппаТоваров");
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ТоварыВОборудовании.ГруппаТоваров КАК ГруппаТоваров,
		|	ТоварыВОборудовании.Номенклатура КАК Номенклатура,
		|	ТоварыВОборудовании.НомерЯчейки КАК НомерЯчейки,
		|	&Момент КАК МоментИзменения
		|ИЗ
		|	РегистрСведений.ТоварыВОборудовании КАК ТоварыВОборудовании
		|ГДЕ
		|   ТоварыВОборудовании."+ВидОбъекта+"=&Фильтр
		|");
		Запрос.УстановитьПараметр("Фильтр",Объект);
		Запрос.УстановитьПараметр("Момент",МоментОбновления);
		
		Выборка=Запрос.Выполнить().Выбрать();
		НачатьТранзакцию();
		Пока Выборка.Следующий() Цикл
			
			НоваяЗапись=РегистрыСведений.ТоварыВОборудовании.СоздатьМенеджерЗаписи(); 
			НоваяЗапись.ГруппаТоваров=Выборка.ГруппаТоваров;
			НоваяЗапись.Номенклатура=Выборка.Номенклатура;
			НоваяЗапись.НомерЯчейки=Выборка.НомерЯчейки;
			НоваяЗапись.МоментИзменения=Выборка.МоментИзменения;
			НоваяЗапись.Записать(Истина);
			
		КонецЦикла;
		ЗафиксироватьТранзакцию();
		
		
	ИначеЕсли ТипЗнч(Объект)=Тип("СправочникСсылка.Номенклатура") Тогда
		
	КонецЕсли;
КонецПроцедуры // уоУстановитьМоментИзменения()

//Получение максимального номера ячейки регистра сведенийй Товары в оборудовании
// ГруппаТоваров - СправочникСсылка.ГруппыТоваровОборудования
Функция уоПолучитьМаксимальныйНомерЯчейки(ГруппаТоваров) Экспорт
	
	НомерЯчейки = 0;
	ТекстЗапроса ="
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТоварыВОборудовании.НомерЯчейки) КАК НомерЯчейки
	|ИЗ
	|	РегистрСведений.ТоварыВОборудовании КАК ТоварыВОборудовании
	|ГДЕ
	|	ТоварыВОборудовании.ГруппаТоваров = &ГруппаТоваров";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ГруппаТоваров", ГруппаТоваров);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		НомерЯчейки = Выборка.НомерЯчейки;
		Если обЗначениеНеЗаполнено(НомерЯчейки) Тогда НомерЯчейки = 0; КонецЕсли; 
	КонецЕсли;
	
	Возврат НомерЯчейки;
	
КонецФункции // уоПолучитьМаксимальныйНомерЯчейки

// Проверка номенклатуры на условия заполнения группы
Функция уоСоответствуетЗаполнениюГруппы(Номенклатура, ГруппаТоваров) Экспорт
	
	Результат = Ложь;
	Если ГруппаТоваров.РежимЗаполнения = 0 Тогда 
		Результат = Истина; // Все товары
	ИначеЕсли ГруппаТоваров.РежимЗаполнения = 1 Тогда
		Результат = Номенклатура.ТипНоменклатуры.Весовой; // Весовой товар
	ИначеЕсли ГруппаТоваров.РежимЗаполнения = 2 Тогда
		Результат = НЕ Номенклатура.ТипНоменклатуры.Весовой; // Штучный товар
	ИначеЕсли ГруппаТоваров.РежимЗаполнения = 3 Тогда // Типы товара из списка
		Результат = 
		(ГруппаТоваров.ОграниченияЗаполненияГруппы.Найти(Номенклатура.ТипНоменклатуры, "ТипГруппаНоменклатуры") <> Неопределено);		
	ИначеЕсли ГруппаТоваров.РежимЗаполнения = 4 Тогда // Товар в иерархии из списка
		Для Каждого СтрокаГруппа ИЗ ГруппаТоваров.ОграниченияЗаполненияГруппы Цикл
			Результат = Номенклатура.ПринадлежитЭлементу(СтрокаГруппа.ТипГруппаНоменклатуры);
			Если Результат Тогда Прервать; КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	//Если НЕ Результат Тогда
	//	Результат = (Вопрос("Товар не отвечает условиям заполнения группы. Продолжить?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Да);
	//КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Если Клиент Тогда

// Функция получения пути к локальному каталогу оборудования
//
// Возвращаемое значение:
//   <Строка>   - путь к локальному каталогу оборудования
//
Функция уоПолучитьПутьККаталогуОборудования() Экспорт
	ПутьПоУмолчанию="";
	Попытка
		WS=Новый COMОбъект("WScript.Shell");
		ПутьПоУмолчанию=WS.RegRead("HKEY_LOCAL_MACHINE\Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders\Common AppData");
		Если НЕ ПустаяСтрока(ПутьПоУмолчанию) И (Прав(ПутьПоУмолчанию,1)<>"\") Тогда
			ПутьПоУмолчанию=ПутьПоУмолчанию+"\";
		КонецЕсли;
	Исключение
		ПутьПоУмолчанию="C:\1C-Rarus\"; // уж лучше поставить это чем совсем никак
	КонецПопытки;
	ПутьПоУмолчанию=ПутьПоУмолчанию+"Protect\LocalProtect\"; // припишем нашу ветку 
	Возврат ПутьПоУмолчанию;
КонецФункции // уоПолучитьПутьККаталогуОборудования()

#КонецЕсли                              

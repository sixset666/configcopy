
#Область СписаниеИАктивацияБонусныхБаллов

Процедура АктивацияБонусныхБаллов(Знач СтруктураПараметров) Экспорт
	
	Обработка = Обработки.АктивацияБонусныхБаллов.Создать();
	
	Если СтруктураПараметров <> Неопределено Тогда
		СтруктураПараметров.Свойство("УчетнаяЗапись"      , Обработка.УчетнаяЗаписьЭлектроннойПочты);
		СтруктураПараметров.Свойство("ШаблонРассылкиSMS"  , Обработка.ШаблонРассылкиSMS);
		СтруктураПараметров.Свойство("ШаблонРассылкиEMail", Обработка.ШаблонРассылкиEMail)
	КонецЕсли;
	
	Результат = Обработка.СоздатьДокументыКорректировкиБонусов();
	Если Результат <> Неопределено И Обработка.ТаблицаРассылок.Количество() > 0 Тогда
		Обработка.ВыполнитьРассылкуУведомлений(Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура СписаниеПросроченныхБонусныхБаллов(Знач СтруктураПараметров) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БонусныеБаллыОстатки.БонуснаяКарта       КАК Карточка,
	|	БонусныеБаллыОстатки.ДатаСписанияБонусов КАК ДатаСписания,
	|	БонусныеБаллыОстатки.КоличествоОстаток   КАК Количество
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.Остатки(, ДатаСписанияБонусов < &ТекДата) КАК БонусныеБаллыОстатки";
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		// создадим документ
		#Если Клиент Тогда
		Сообщить("-------------------------------------------------------------");
		Сообщить("Списание просроченных балллов.");
		#КонецЕсли
		
		НовыйДокумент = Документы.КорректировкаБонусов.СоздатьДокумент();
		НовыйДокумент.Заполнить(Неопределено);
		НовыйДокумент.УстановитьНовыйНомер();
		
		НовыйДокумент.Комментарий = "Создан регламентным заданием ""Списание просроченных бонусных баллов""";
		
		НовыйДокумент.БонусыКСписанию.Загрузить(РезультатЗапроса.Выгрузить());
		
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
			#Если Клиент Тогда
			Сообщить(НСтр("ru = 'Сформирован документ '") + Строка(НовыйДокумент.Ссылка) + ".");
			#КонецЕсли
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Сообщить(ИнформацияОбОшибке.Описание +"|"+ ИнформацияОбОшибке.Причина);
		КонецПопытки;
		
		#Если Клиент Тогда
		Сообщить("-------------------------------------------------------------");
		#КонецЕсли
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Оповещения

Процедура ОповещениеОБонусныхБаллах(Знач СтруктураПараметров) Экспорт
	
	Обработка = Обработки.РассылкаИнформацииОБонусныхБаллах.Создать();
	Обработка.Дата = ТекущаяДата();
	
	Если СтруктураПараметров <> Неопределено Тогда
		СтруктураПараметров.Свойство("УчетнаяЗапись"      , Обработка.УчетнаяЗаписьЭлектроннойПочты);
		СтруктураПараметров.Свойство("ШаблонРассылкиSMS"  , Обработка.ШаблонРассылкиSMS);
		СтруктураПараметров.Свойство("ШаблонРассылкиEMail", Обработка.ШаблонРассылкиEMail)
	КонецЕсли;
	
	Обработка.АвтоматическаяРассылка();
	
КонецПроцедуры

#КонецОбласти
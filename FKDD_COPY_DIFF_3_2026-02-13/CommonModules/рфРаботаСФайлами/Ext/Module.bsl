////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФАЙЛАМИ

//Процедуры и функции обработки имени строки с именем файла.

// Составляет полное имя файла из имени каталога и имени файла.
//
// Параметры
//  ИмяКаталога  – Строка, содержащая путь к каталогу файла на диске.
//  ИмяФайла     – Строка, содержащая имя файла, без имени каталога.
//
// Возвращаемое значение:
//   Строка – полное имя файла с учетом каталога.
//
Функция рфПолучитьИмяФайла(ИмяКаталога, ИмяФайла) Экспорт

	Если Прав(ИмяКаталога, 1) = "\" Тогда
		ИмяКаталога = Лев(ИмяКаталога, СтрДлина(ИмяКаталога) - 1);
	КонецЕсли;

	Возврат ИмяКаталога + ?(ПустаяСтрока(ИмяФайла), "", "\" + ИмяФайла);

КонецФункции // рфПолучитьИмяФайла()

// Выделяет из имени файла его расширение (набор символов после последней точки).
//
// Параметры
//  ИмяФайла – Строка, содержащая имя файла, неважно с именем каталога или без.
//
// Возвращаемое значение:
//   Строка – расширение файла.
//
Функция рфПолучитьРасширениеФайла(ИмяФайла) Экспорт
	
	ПозицияПоследнейТочки = 0;
	РасширениеФайла = ИмяФайла;

	Пока 1 = 1 Цикл
		ПозицияПоследнейТочки = Найти(РасширениеФайла, ".");
		Если ПозицияПоследнейТочки = 0 Тогда
			Прервать;
		Иначе
			РасширениеФайла = Сред(РасширениеФайла, ПозицияПоследнейТочки + 1)
		КонецЕсли;
	КонецЦикла;

	Возврат ?(РасширениеФайла = ИмяФайла, "", РасширениеФайла);

КонецФункции // рфПолучитьРасширениеФайла()

// Проверяет наличие запрещенных в среде MS Windows символов в имени файла.
//
// Параметры
//  ИмяФайла     – Строка, содержащая имя файла, без каталога.
//
// Возвращаемое значение:
//   Булево – Истина, если есть запрещенные символы, Ложь, если нет.
//
Функция рфЕстьЗапрещенныеСимволыИмени(ИмяФайла) Экспорт

	Если Найти(ИмяФайла,  "\") > 0
	 ИЛИ Найти(ИмяФайла,  "/") > 0
	 ИЛИ Найти(ИмяФайла,  ":") > 0
	 ИЛИ Найти(ИмяФайла,  "*") > 0
	 ИЛИ Найти(ИмяФайла,  "&") > 0
	 ИЛИ Найти(ИмяФайла, """") > 0
	 ИЛИ Найти(ИмяФайла,  "<") > 0
	 ИЛИ Найти(ИмяФайла,  ">") > 0
	 ИЛИ Найти(ИмяФайла,  "|") > 0 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции // рфЕстьЗапрещенныеСимволыИмени()

// Функция возвращает имя файла из полного имени файла
//
// Параметры
//  ПолноеИмяФайла  – Строка, Полное имя файла.
//
// Возвращаемое значение:
//   Строка   – имя файла.
//
Функция рфПолучитьИмяФайлаИзПолногоИмени(Знач ПолноеИмяФайла) Экспорт

	Пока Найти(ПолноеИмяФайла,"\")>0 Цикл
		ПолноеИмяФайла = Прав(ПолноеИмяФайла,СтрДлина(ПолноеИмяФайла) - Найти(ПолноеИмяФайла,"\"));
	КонецЦикла;	
	
	Возврат ПолноеИмяФайла;	
	
КонецФункции // рфПолучитьИмяФайлаИзПолногоИмени()

// Функция возвращает каталог файла из полного имени файла
//
// Параметры
//  ПолноеИмяФайла  – Строка, Полное имя файла.
//
// Возвращаемое значение:
//   Строка   – имя файла.
//
Функция рфПолучитьКаталогФайла(Знач ПолноеИмяФайла) Экспорт
	
	ИмяФайла = рфПолучитьИмяФайлаИзПолногоИмени(ПолноеИмяФайла); 	
	ИмяКаталога = Лев(ПолноеИмяФайла,СтрДлина(ПолноеИмяФайла) - СтрДлина(ИмяФайла));
			
	Возврат ИмяКаталога;	
	
КонецФункции // рфПолучитьКаталогФайла()

// Функция корректно обрезает имя файла если оно  больше 100 символов, 
// чтобы осталось расширение. (рфОбрезатьИмяФайла("ОченьДлинноеИмяФайла.txt",20) -> ОченьДлинноеИмяФ.txt)
//
// Параметры
//  ИмяФайла 		   – Строка, содержащая имя файла, неважно с именем каталога или без.
//  КоличествоСимволов – Число, кол-во символов, до которого необходимо урезать имя файла.
//  ВыводитьСообщение  – Булево, признак необходимости вывести сообщение что имя файла обрезано.
//
// Возвращаемое значение:
//  Строка   – обрезанное имя файла.
//
Функция рфОбрезатьИмяФайла(Знач ИмяФайла, КоличествоСимволов, ВыводитьСообщение=Ложь) Экспорт
	
	Если СтрДлина(ИмяФайла) > КоличествоСимволов Тогда
		Если ВыводитьСообщение Тогда
			Сообщить("Имя файла """+ИмяФайла+""" обрезано, так как его длина больше "+КоличествоСимволов+" символов", СтатусСообщения.Информация);
		КонецЕсли; 
		РасширениеФайла = рфПолучитьРасширениеФайла(ИмяФайла);
		СамоИмяФайла = Лев(ИмяФайла, СтрДлина(ИмяФайла)-СтрДлина(РасширениеФайла)-1);
		СамоИмяФайла = Лев(СамоИмяФайла, КоличествоСимволов-СтрДлина(РасширениеФайла)-1);
		ИмяФайла = СамоИмяФайла + "." + РасширениеФайла;
	КонецЕсли;
	
	Возврат ИмяФайла;
	
КонецФункции // рфОбрезатьИмяФайла()

//Rarus Alkoko 15.07.2015 {

//Функция распаковывает архив и выбирает из него файлы по маске
// Параметры
//  ИмяФайлаАрхива 	  – Строка, содержащая полное имя файла.
//  КаталогРаспаковки – Строка, путь к каталогу, в который необходимо распаковать архив.
//  МаскаФайлов       – Строка, маска, по которой будет производиться поиск файлов в архиве.
//  Ошибки            – Строка, содержит текст ошибок.
//
// Возвращаемое значение:
//  Массив   – Массив, содержащий имена найденных по маске файлов в архиве.
//
Функция РаспаковатьZIPАрхивПрайсЛиста(ИмяФайлаАрхива, КаталогРаспаковки, Знач МаскаФайлов, Ошибки = "") Экспорт
	
	ИмяКаталога		 = рфПолучитьИмяФайла(КаталогРаспаковки, "Tmp");
	СоздатьКаталог(ИмяКаталога);             
	МассивИменФайлов = Новый Массив;
	
	РасширениеФайла = рфПолучитьРасширениеФайла(ИмяФайлаАрхива);
	Если НРег(РасширениеФайла) = "zip" Тогда
		ЧтениеZIP			= Новый ЧтениеZIPФайла(ИмяФайлаАрхива);
		ЧтениеZIP.ИзвлечьВсе(ИмяКаталога, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
		ЧтениеZIP.Закрыть();
	Иначе
		Если рфПолучитьИндексПиктограммыФайла(РасширениеФайла) = 15 Тогда // Это архив
			ПутьКАрхиватору = СокрЛП(обПраво("ПутьК7Zip"));
			ФайлАрхиватора = Новый Файл(ПутьКАрхиватору);
			Если ФайлАрхиватора.Существует() Тогда
				//Уберем из маски пробелы и заменим на спец. символ
				МаскаФайлов = СтрЗаменить(МаскаФайлов, " ", "?");
				// Будем дожидаться окончания разархивирования
				Попытка
					СтрокаКоманды = """"+ПутьКАрхиватору+""" e """ + ИмяФайлаАрхива + """ -o""" + ИмяКаталога + """ " + МаскаФайлов + " -y";
					
					//Запишем в журнал регистрации информацию о начале выполнения скрипта
					СтрокаСообщения = "Результат команды - " + СокрЛП("Запуск Wscript: "+СтрокаКоманды);   
					ИерархияСобытия = "РегламентныеПроцедуры.КомандаСистемы";
															
					ТекУровеньЖурналаРегистрации = УровеньЖурналаРегистрации.Информация;
										
					ЗаписьЖурналаРегистрации(ИерархияСобытия,ТекУровеньЖурналаРегистрации, , ,СтрокаСообщения);
					
					//Выполним скрипт
					WshShell = Новый COMОбъект("Wscript.Shell");
					sReturn = WshShell.run(СтрокаКоманды,1,True);
					
					//Запишем в журнал регистрации информацию о результате выполнения скрипта
					СтрокаСообщения = "Результат команды - " + СокрЛП(sReturn);
									
					ЗаписьЖурналаРегистрации(ИерархияСобытия,ТекУровеньЖурналаРегистрации, , ,СтрокаСообщения);
				Исключение
					Ошибки = "Не удалось выполнить команду " + """"+ПутьКАрхиватору+""" e """ + ИмяФайлаАрхива + """ -o""" + ИмяКаталога + """ " + МаскаФайлов + " -y" + Символы.ПС + ОписаниеОшибки();
					Возврат Ложь;
				КонецПопытки;
			Иначе
				Ошибки = "Не найден архиватор - "+ПутьКАрхиватору;
				Возврат МассивИменФайлов;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(МаскаФайлов) Тогда
		МаскаФайлов = "*"
	КонецЕсли;	
	РаспакованныеФайлы	= НайтиФайлы(ИмяКаталога, МаскаФайлов, Ложь);
	Для Каждого Файл Из РаспакованныеФайлы Цикл
		ИмяФайла = рфПолучитьИмяФайла(КаталогРаспаковки, Файл.Имя);
		ПереместитьФайл(Файл.ПолноеИмя, ИмяФайла);
		МассивИменФайлов.Добавить(ИмяФайла);
	КонецЦикла;
	
	Попытка
		УдалитьФайлы(ИмяКаталога);
	Исключение
	КонецПопытки;
	
	Возврат МассивИменФайлов;
	
КонецФункции //РаспаковатьZIPАрхивПрайсЛиста()
//Rarus Alkoko 15.07.2015 }

#Если Клиент Тогда
	
	
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ДИАЛОГАМИ 

// Позволяет пользователю выбрать файл на диске.
//
// Параметры
//  Файлы  				– Массив, Полное имя файла.
//  МножественныйВыбор  - Булево, Признак множественного выбора файлов.
// Возвращаемое значение:
//   Булево   – Признак выбора файла
//
Функция рфВыбратьФайлы(Файлы,МножественныйВыбор = Истина)Экспорт
	
	ПапкаМоиДокументы = рфПолучитьСистемнуюПапку("MyDocuments");	
	ПапкаМоиДокументы = ?(ПапкаМоиДокументы=Неопределено,"С:\",ПапкаМоиДокументы);
	
	ИмяКаталога=ВосстановитьЗначение("КаталогПользователя");
	
	Если  НЕ ИмяКаталога = Неопределено Тогда
		Каталог = Новый Файл(ИмяКаталога);
		Если НЕ Каталог.Существует() Тогда
			ИмяКаталога = ПапкаМоиДокументы;
		КонецЕсли;
	Иначе
		ИмяКаталога=ПапкаМоиДокументы;
	КонецЕсли;
		
	Диалог = рфПолучитьДиалогВыбораФайлов(Истина, ИмяКаталога);
	Диалог.МножественныйВыбор = МножественныйВыбор;
	
	Если НЕ Диалог.Выбрать() Тогда
		Возврат Ложь;
	Иначе
		Файлы = Диалог.ВыбранныеФайлы;
		СохранитьЗначение("КаталогПользователя", Диалог.Каталог);
		Возврат Истина;
	КонецЕсли;
		
КонецФункции // рфВыбратьФайлы()
 
// Позволяет пользователю выбрать каталог на диске.
//
// Параметры
//  ИмяКаталога  – Строка, содержащая начальный путь к каталогу на диске.
//
// Возвращаемое значение:
//   Булево – Истина, если каталог выбран, Ложь, если нет.
//
Функция рфВыбратьКаталог(ИмяКаталога) Экспорт

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Выбор каталога";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Каталог = ИмяКаталога;

	Если Диалог.Выбрать() Тогда
		ИмяКаталога = Диалог.Каталог;
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции // рфВыбратьКаталог()

// Создает и устанавливает реквизиты диалога выбора фала.
//
// Параметры
//  МножественныйВыбор – Булево, признак множественного выбора.
//  НачальныйКаталог – Строка, содержащая начальный каталог выбора файла.
//
// Возвращаемое значение:
//   ДиалогВыбораФайлов – созданный диалог.
//
Функция рфПолучитьДиалогВыбораФайлов(МножественныйВыбор, НачальныйКаталог = "") Экспорт

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Каталог = НачальныйКаталог;
	Диалог.Заголовок = "Выберите файл...";
	Диалог.Фильтр = рфПолучитьФильтрФайлов();
	Диалог.ПредварительныйПросмотр = Ложь;
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.МножественныйВыбор = МножественныйВыбор;

	Возврат Диалог;

КонецФункции // рфПолучитьДиалогВыбораФайлов()

// Создает и устанавливает реквизиты диалога выбора сохранения файла.
//
// Параметры
//  МножественныйВыбор – Булево, признак множественного выбора.
//  НачальныйКаталог – Строка, содержащая начальный каталог выбора файла.
//
// Возвращаемое значение:
//   ДиалогВыбораФайлов – созданный диалог.
//
Функция рфПолучитьДиалогСохраненияФайлов(НачальныйКаталог = "",ИмяФайла) Экспорт

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Каталог = НачальныйКаталог;
	Диалог.Заголовок = "Сохранить как...";
	Диалог.ПредварительныйПросмотр = Ложь;  
	Диалог.ПолноеИмяФайла=ИмяФайла;
	Расширение=рфПолучитьРасширениеФайла(ИмяФайла); 
	Диалог.Фильтр="*."+Расширение+"|*."+Расширение+"|";
	Возврат Диалог;

КонецФункции // рфПолучитьДиалогСохраненияФайлов()

#КонецЕсли

// Процедуры и функции чтения/записи  файлов с дисков

// Сохраняет файл на диске.
//
// Параметры
//  Файл    		– ХранилищеЗначения или ДвоичныеДанные,  данные для записи на диск.
//  ИмяФайла     	– Строка, содержащая полное имя файла.
//  ТолькоЧтение 	– Булево, признак установки записываемому файлу атрибута ТолькоЧтение.
//  СпособПерезаписи – Строка. Параметр определяет способ перезаписи существующих
//                 файлов на диске. В зависимости от пришедшего параметра выдается или
//                 не выдается запрос на перезапись файлов. Может устанавливаться в теле
//                 функции, если это необходимо. Принимаемые значения:
//                 "" (пустая строка) - это означает, что диалог еще ни разу не задавался
//                 и при наличии существующего файла будет выдан диалог запроса перезаписи.
//                 ДА - Файл нужно перезаписать
//                 НЕТ - Файл не нужно перезаписывать
// Возвращаемое значение:
//   Булево – Истина, если каталог выбран, Ложь, если нет.
//
Функция рфСохранитьФайлНаДиске(Файл, ИмяФайла, ТолькоЧтение=Ложь, СпособПерезаписи="") Экспорт
		
	ФайлНаДиске = Новый Файл(ИмяФайла);
	КаталогНаДиске = Новый Файл(ФайлНаДиске.Путь);
	
	Если НЕ КаталогНаДиске.Существует() Тогда
		СоздатьКаталог(ФайлНаДиске.Путь);
	КонецЕсли;
	
	Если ФайлНаДиске.Существует() Тогда
		Если СпособПерезаписи = ""  Тогда
			#Если Клиент Тогда
			Текст = "Переписать """+ФайлНаДиске.ПолноеИмя+""" ?";
			Ответ = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, "Внимание!");
			
			Если Ответ = КодВозвратаДиалога.Да Тогда
			#КонецЕсли	
				Если ФайлНаДиске.ПолучитьТолькоЧтение() Тогда
					ФайлНаДиске.УстановитьТолькоЧтение(Ложь);
				КонецЕсли;
			#Если Клиент Тогда
			Иначе 
			КонецЕсли;
			#КонецЕсли	
			
		ИначеЕсли СпособПерезаписи = "НЕТ" Тогда
			Возврат Ложь;
			
		ИначеЕсли СпособПерезаписи = "ДА" Тогда
			Если ФайлНаДиске.ПолучитьТолькоЧтение() Тогда
				ФайлНаДиске.УстановитьТолькоЧтение(Ложь);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// запишем файл на диск
	Если ТипЗнч(Файл) <> Тип("ДвоичныеДанные") Тогда
		ДвоичныеДанные = Файл.Получить();
	Иначе
		ДвоичныеДанные = Файл;
	КонецЕсли; 
	
	Попытка
		ДвоичныеДанные.Записать(ИмяФайла);
		ФайлНаДиске.УстановитьТолькоЧтение(ТолькоЧтение);
	Исключение
		обОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // рфСохранитьФайлНаДиске()

#Если Клиент Тогда
// Открывает переданный файл на диске с учетом типа файлов. Файлы, с которыми 
// может работать 1С:Предприятие открываются в 1С:Предприятии. Остальные файлы
// пытаются открыться зарегистрированным для них в системе приложением.
//
// Параметры
//  ИмяКаталога  – Строка, содержащая путь к каталогу файла на диске.
//  ИмяФайла     – Строка, содержащая имя файла, без имени каталога.
//
Процедура рфОткрытьФайл(ИмяКаталога, ИмяФайла) Экспорт
		
	ПолноеИмяФайла = рфПолучитьИмяФайла(ИмяКаталога, ИмяФайла);
	РасширениеФайла = Врег(рфПолучитьРасширениеФайла(ИмяФайла));

	Если РасширениеФайла="BMP" ИЛИ РасширениеФайла="DIB" ИЛИ РасширениеФайла="RLE"
		ИЛИ РасширениеФайла="JPG" ИЛИ РасширениеФайла="JPEG" ИЛИ РасширениеФайла="TIF"
		ИЛИ РасширениеФайла="GIF" ИЛИ РасширениеФайла="PNP" ИЛИ РасширениеФайла="ICO" 
		ИЛИ РасширениеФайла="WMF" ИЛИ РасширениеФайла="EMF" Тогда
		ФормаИзображения=ПолучитьОбщуюФорму( "ФормаПросмотраИзображения");
		ФормаИзображения.ФайлИзображения = ПолноеИмяФайла;
		ФормаИзображения.Открыть();	
	ИначеЕсли РасширениеФайла = "MXL" Тогда
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.Прочитать(ПолноеИмяФайла);
		ТабличныйДокумент.Показать(ИмяФайла, Лев(ИмяФайла, СтрДлина(ИмяФайла) - 4));
	ИначеЕсли РасширениеФайла = "TXT" Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ПолноеИмяФайла);
		ТекстовыйДокумент.Показать(ИмяФайла, Лев(ИмяФайла, СтрДлина(ИмяФайла) - 4));
	ИначеЕсли РасширениеФайла = "EPF" Тогда
		ВнешняяОбработка = ВнешниеОбработки.Создать(ПолноеИмяФайла);
		ВнешняяОбработка.ПолучитьФорму().Открыть();		
	Иначе
		Если Найти(",EXE,COM,BAT,CMD,VBS,PL,BAS,JS,JAVA,REG,SHS,PIF,SCR,DLL,SSH,CHM,HLP,LNK,CPL,MSI,", "," + РасширениеФайла + ",") > 0 Тогда
			Ответ = Вопрос("Файл " + ИмяФайла + " потенциально ОПАСЕН (может содержать вредоносный код)! 
						   |Уверены что хотите открыть данный файл ?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Внимание!");
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;			   
		КонецЕсли;
		ЗапуститьПриложение(ПолноеИмяФайла);
	КонецЕсли;

КонецПроцедуры // рфОткрытьФайл()

// Процедура записывает файл из хранилища во временный файл с уникальным именем и выполняет
// открытие файла с учетом расширения.
//
// Параметры:
//  Данные - ДвоичныеДанные, файл сохраненный в хранилище значений.
//  ИмяФайла - Строка, имя файла или расширение с точкой, используется для открытия файла
// с учетом расширения.
//
Процедура рфОткрытьФайлИзХранилища(Данные,ИмяФайла) Экспорт
	
	РасширениеФайла = ВРег(рфПолучитьРасширениеФайла(ИмяФайла));
	ВременныйФайл = ПолучитьИмяВременногоФайла(Нрег(РасширениеФайла));
	
	Если РасширениеФайла = "MSG" Тогда
		эпОткрытьВложенноеСообщениеИзХранилища(Данные);	
	Иначе	
	
		СпособПерезаписи = "ДА";
		
	    Состояние("Сохраняется файл: " + ИмяФайла);
		Если НЕ рфСохранитьФайлНаДиске(Данные, ВременныйФайл, , СпособПерезаписи) Тогда
			Возврат;
		КонецЕсли;
		
		Если РасширениеФайла="BMP" ИЛИ РасширениеФайла="DIB" ИЛИ РасширениеФайла="RLE"
			ИЛИ РасширениеФайла="JPG" ИЛИ РасширениеФайла="JPEG" ИЛИ РасширениеФайла="TIF"
			ИЛИ РасширениеФайла="GIF" ИЛИ РасширениеФайла="PNP" ИЛИ РасширениеФайла="ICO" 
			ИЛИ РасширениеФайла="WMF" ИЛИ РасширениеФайла="EMF" Тогда
			ФормаИзображения = ПолучитьОбщуюФорму("ФормаПросмотраИзображения");
			ФормаИзображения.ИмяФайла = ИмяФайла;
			ФормаИзображения.ФайлИзображения =ВременныйФайл;
			ФормаИзображения.Открыть();	
		ИначеЕсли РасширениеФайла = "MXL" Тогда
			ТабличныйДокумент = Новый ТабличныйДокумент;
			ТабличныйДокумент.Прочитать(ВременныйФайл);
			ТабличныйДокумент.Показать(ИмяФайла, Лев(ИмяФайла, СтрДлина(ИмяФайла) - 4));
		ИначеЕсли РасширениеФайла = "TXT" ИЛИ РасширениеФайла = "LOG" ИЛИ РасширениеФайла = "INI"  ИЛИ 
				  РасширениеФайла = "ME" Тогда
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			ТекстовыйДокумент.Прочитать(ВременныйФайл);
			ТекстовыйДокумент.Показать(ИмяФайла, Лев(ИмяФайла, СтрДлина(ИмяФайла) - 4));
		ИначеЕсли РасширениеФайла = "EPF" Тогда
			ВнешняяОбработка = ВнешниеОбработки.Создать(ВременныйФайл);
			ВнешняяОбработка.ПолучитьФорму().Открыть();		
		Иначе
			Если Найти(",EXE,COM,BAT,CMD,VBS,PL,BAS,JS,JAVA,REG,SHS,PIF,SCR,DLL,SSH,CHM,HLP,LNK,CPL,MSI,", "," + РасширениеФайла + ",") > 0 Тогда
				Ответ = Вопрос("Файл " + ИмяФайла + " потенциально ОПАСЕН (может содержать вредоносный код)! 
							   |Уверены что хотите открыть данный файл ?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Внимание!");
				Если Ответ = КодВозвратаДиалога.Нет Тогда
					Возврат;
				КонецЕсли;			   
			КонецЕсли;
			ЗапуститьПриложение(ВременныйФайл);
		КонецЕсли;
	КонецЕсли;		
	
КонецПроцедуры // рфОткрытьФайлИзХранилища()

// Проверяет наличие каталога на диске и предлагает создать, если каталога не существует.
//
// Параметры
//  ИмяКаталога  – Строка, содержащая путь к каталогу файла на диске.
//
// Возвращаемое значение:
//   Булево – Истина, если каталог существует или создан, Ложь, если каталога нет.
//
Функция рфПроверитьСуществованиеКаталога(ИмяКаталога) Экспорт

	КаталогНаДиске = Новый Файл(ИмяКаталога);
	Если КаталогНаДиске.Существует() Тогда
		Возврат Истина;
	Иначе
		Ответ = Вопрос("Указанный каталог не существует. Создать каталог?", РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.ОК, "Внимание!");
		Если Ответ = КодВозвратаДиалога.ОК Тогда
			СоздатьКаталог(ИмяКаталога);
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

КонецФункции // рфПроверитьСуществованиеКаталога()

// Сохранение на диск отмеченных файлов в табличной части объекта.
//
// Параметры
//  ВыделенныеСтроки  – ВыделенныеСтрокиТабличногоПоля , набор переданных строк для обработки
//                                                                      
Процедура рфСохранитьФайлыТЧ(ВыделенныеСтроки) Экспорт
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКаталога = рфПолучитьКаталогПользователя();    
	
	// выбран конкретный файл
	Если ВыделенныеСтроки.Количество() = 1 Тогда
		ВыделеннаяСтрока = ВыделенныеСтроки[0];
		
		РасширениеФайла = рфПолучитьРасширениеФайла(ВыделеннаяСтрока.Имя);
		Если Найти(Врег(РасширениеФайла), "MSG") > 0 Тогда
			#Если Клиент Тогда
				Предупреждение("Запись на диск почтовых сообщений невозможна!");
			#КонецЕсли
			Возврат;
		КонецЕсли;	
		Если Найти(",EXE,COM,BAT,CMD,VBS,PL,BAS,JS,JAVA,REG,SHS,PIF,SCR,DLL,SSH,CHM,HLP,LNK,CPL,MSI,", "," + РасширениеФайла + ",") > 0 Тогда
			Ответ = Вопрос("Файл " + ВыделеннаяСтрока.Имя + " потенциально ОПАСЕН (может содержать вредоносный код)! 
						   |Уверены что хотите сохранить данный файл на диск ?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Внимание!");
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;			   
		КонецЕсли;
		
		Диалог = рфПолучитьДиалогСохраненияФайлов(ИмяКаталога,ВыделеннаяСтрока.Имя);
		// если нажали "Отмена"
		Если НЕ Диалог.Выбрать() Тогда
			Возврат
		КонецЕсли;	
		ИмяФайла = Диалог.ПолноеИмяФайла;	
		ИмяКаталога = Диалог.Каталог;
		СпособПерезаписи = "ДА";
		рфСохранитьФайлНаДиске(ВыделеннаяСтрока.Файл, ИмяФайла, , СпособПерезаписи);
	КонецЕсли;
	// выбрано несколько файлов
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		Если НЕ рфВыбратьКаталог(ИмяКаталога) ТОгда
			Возврат
		КонецЕсли;
		
		Для каждого СсылкаФайл из ВыделенныеСтроки Цикл
			
			РасширениеФайла = рфПолучитьРасширениеФайла(СсылкаФайл.Имя);
			Если Найти(Врег(РасширениеФайла), "MSG") > 0 Тогда
				#Если Клиент Тогда
					Предупреждение("Запись на диск почтовых сообщений невозможна!");
				#КонецЕсли
				Продолжить;
			КонецЕсли;	
			
			Если Найти(",EXE,COM,BAT,CMD,VBS,PL,BAS,JS,JAVA,REG,SHS,PIF,SCR,DLL,SSH,CHM,HLP,LNK,CPL,MSI,", "," + РасширениеФайла + ",") > 0 Тогда
				Ответ = Вопрос("Файл " + СсылкаФайл.Имя + " потенциально ОПАСЕН (может содержать вредоносный код)! 
							   |Уверены что хотите сохранить данный файл на диск ?", РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Нет,"Внимание!");
				Если Ответ = КодВозвратаДиалога.Нет Тогда
					Продолжить;
				ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
					Возврат;	
				КонецЕсли;			   
			КонецЕсли;
			
			Состояние("Сохраняется файл: " + СсылкаФайл.Имя);
			ИмяФайла = рфПолучитьИмяФайла(ИмяКаталога, СсылкаФайл.Имя);
			Если НЕ рфСохранитьФайлНаДиске(СсылкаФайл.Файл, ИмяФайла) ТОгда
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	КонецЕСли;
	
	СохранитьЗначение("КаталогПользователя" + ПараметрыСеанса.Пользователь, ИмяКаталога);
	
КонецПроцедуры // рфСохранитьФайлыТЧ()

// Сохранение на диск выделенных файлов в табличной части объекта и их открытие.
//
// Параметры
//  ВыделенныеСтроки  – ВыделенныеСтрокиТабличногоПоля , набор переданных строк для обработки
//
Процедура рфОткрытьФайлыТЧ(ВыделенныеСтроки) Экспорт

	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для каждого Строка из ВыделенныеСтроки Цикл
		рфОткрытьФайлИзХранилища(Строка.Файл,Строка.Имя);
	КонецЦикла;

КонецПроцедуры // рфОткрытьФайлыТЧ()

// Сохранение на диск отмеченных в списке элементов.
//
// Параметры
//  ВыделенныеСтроки  – ВыделенныеСтрокиТабличногоПоля , набор переданных строк для обработки
//
Процедура рфСохранитьФайлы(ВыделенныеСтроки) Экспорт
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКаталога = рфПолучитьКаталогПользователя();    
	
	// выбран конкретный файл
	Если ВыделенныеСтроки.Количество() = 1 Тогда
		
		ВыделеннаяСтрока = ВыделенныеСтроки[0];
		
		Диалог = рфПолучитьДиалогСохраненияФайлов(ИмяКаталога,ВыделеннаяСтрока.Имя);
		
		// если нажали "Отмена"
		Если НЕ Диалог.Выбрать() Тогда
			Возврат
		КонецЕсли;	
		
		ИмяФайла = Диалог.ПолноеИмяФайла;	
		ИмяКаталога = Диалог.Каталог;
		
		СпособПерезаписи = "ДА";
		
		рфСохранитьФайлНаДиске(ВыделеннаяСтрока.ХранилищеЗначения, ИмяФайла,, СпособПерезаписи);
		
	КонецЕсли;
	
	// выбрано несколько файлов
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		
		Если НЕ рфВыбратьКаталог(ИмяКаталога) ТОгда
			Возврат
		КонецЕсли;
				
		СпособПерезаписи = "";
		
		Для каждого СсылкаФайл из ВыделенныеСтроки Цикл
			
			Состояние("Сохраняется файл: " + СсылкаФайл.Имя);
			ИмяФайла = рфПолучитьИмяФайла(ИмяКаталога, СсылкаФайл.Имя);
			Если НЕ рфСохранитьФайлНаДиске(СсылкаФайл.ХранилищеЗначения, ИмяФайла, , СпособПерезаписи) ТОгда
				Прервать;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕСли;
	
	СохранитьЗначение("КаталогПользователя" + ПараметрыСеанса.Пользователь, ИмяКаталога);
	
КонецПроцедуры // рфСохранитьФайлы()

// Универсальная функция работы с диалогом выбора файлов
// 
// Параметры:
// 	КлючУникальности - Строка, для конкретных мест вызова диалога можно указать ключ по которому
// 	будет сохранен каталог выбора
// 	ФильтрФайлов 		- Строка, фильтр файлов, по умолчанию используется стандартный.
//						Например: " Файлы MS Project (*.mpp)|*.mpp| Все файлы (*.*)|*.*".	
// 	ИндексФильтра	- Число, содержит индекс активного фильтра в списке фильтров.
// 	РежимДиалогаВыбора - Строка, содержит режим диалога для выбора файла (ВыборКаталога,Открытие,Сохранение). 
// 	Заголовок		  - Строка, заголовок окна выбора.
// 	ПредварительныйПросмотр - Булево,содержит признак показа окна предварительного просмотра файлов (изображений). 
// 	ПроверятьСуществованиеФайла - булево,содержит признак проверки существования выбираемого файла. 
// 	МножественныйВыбор -  Булево, содержит признак множественного выбора файлов.	
// 	Расширение - Строка, Содержит расширение файла по умолчанию, которое будет использоваться системой
// 	при записи файла. 
//
// Возвращаемые значения:
//  
//   ДиалогВыбораФайлов - диалог выбора файлов, если ничего не выбрано возвращается Неопределено 
//						  Пример:"Диалог.ВыбранныеФайлы.Получить(0) - получение полного имени файла выбранного в диалоге". 	
//
Функция рфДиалогОткрытияФайлов(РежимДиалогаВыбора = "Открытие", КлючУникальности = "",ФильтрФайлов = "",ИндексФильтра = 0,Заголовок = "Выберите файл...",
				ПредварительныйПросмотр = Ложь,ПроверятьСуществованиеФайла = Истина,МножественныйВыбор = Ложь,Расширение = "")  Экспорт
	
	ПапкаМоиДокументы=рфПолучитьСистемнуюПапку("MyDocuments");	
	ПапкаМоиДокументы=?(ПапкаМоиДокументы=Неопределено,"С:\",ПапкаМоиДокументы);
	
	ИмяКаталога=ВосстановитьЗначение("КаталогПользователя");
	
	Если  НЕ ИмяКаталога = Неопределено Тогда
		Каталог = Новый Файл(ИмяКаталога);
		Если НЕ Каталог.Существует() Тогда
			ИмяКаталога =  ПапкаМоиДокументы;
		КонецЕсли;
	Иначе
		ИмяКаталога=ПапкаМоиДокументы;
	КонецЕсли;
	
	ИмяУникальногоКаталога = ВосстановитьЗначение(КлючУникальности + "КаталогПользователя");
	ИмяКаталога =   ?(ИмяУникальногоКаталога = Неопределено,ИмяКаталога,ИмяУникальногоКаталога);

	Если ФильтрФайлов = "" Тогда
		ФильтрФайлов = рфПолучитьФильтрФайлов();
	КонецЕсли;	
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Каталог = ИмяКаталога;
	Диалог.Заголовок = Заголовок;
	Диалог.Фильтр = ФильтрФайлов;
	Диалог.ПредварительныйПросмотр = ПредварительныйПросмотр;
	Диалог.ИндексФильтра = ИндексФильтра;
	Диалог.ПроверятьСуществованиеФайла = ПроверятьСуществованиеФайла;
	Диалог.МножественныйВыбор = МножественныйВыбор;
	
	Если НЕ Расширение = ""  Тогда
		  Диалог.Расширение = Расширение;
	КонецЕсли;	
		
	Если  Диалог.Выбрать() Тогда
		СохранитьЗначение(КлючУникальности + "КаталогПользователя",Диалог.Каталог);
		Возврат Диалог;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // рфДиалогОткрытияФайлов()

#КонецЕсли

// ПРИКЛАДНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция возвращает индекс пиктограммы файла из коллекции пиктограмм в зависимости от расширения файла.
//
// Параметры
//  РасширениеФайла – Строка, содержащая расширение файла.
//
// Возвращаемое значение:
//   Число – индекс пиктограммы в коллекции "ккВидыФайлов".
//
Функция рфПолучитьИндексПиктограммыФайла(РасширениеФайла) Экспорт

	РасширениеФайла = Врег(РасширениеФайла);

	Если Найти(",1CD,CF,CFU,DT,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат 1;
	ИначеЕсли "MXL" = РасширениеФайла Тогда
		Возврат 2;
	ИначеЕсли Найти(",TXT,LOG,INI,ME,", "," + РасширениеФайла + ",")  > 0 Тогда
		Возврат 3;
	ИначеЕсли "EPF" = РасширениеФайла Тогда
		Возврат 4;
	ИначеЕсли Найти(",BMP,DIB,RLE,JPG,JPEG,TIF,GIF,PNG,ICO,WMF,EMF,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат 5;
	ИначеЕсли Найти(",HTM,HTML,MHT,SHTML,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат 6;
	ИначеЕсли Найти(",DOC, DOT,", "," + РасширениеФайла + ",")  > 0 Тогда
		Возврат 7;
	ИначеЕсли Найти(",XLS,XLSX,CSV,HLT,", "," + РасширениеФайла + ",")  > 0 Тогда
		Возврат 8;
	ИначеЕсли "PPT" = РасширениеФайла Тогда
		Возврат 9;
	ИначеЕсли "VSD" = РасширениеФайла Тогда
		Возврат 10;
	ИначеЕсли "MPP" = РасширениеФайла Тогда
		Возврат 11;
	ИначеЕсли "MDB" = РасширениеФайла Тогда
		Возврат 12;
	ИначеЕсли "XML" = РасширениеФайла Тогда
		Возврат 13;
	ИначеЕсли "MSG" = РасширениеФайла Тогда
		Возврат 14;
	//Rarus Alkoko 15.07.2015 {
	//ИначеЕсли Найти(",RAR,ZIP,ARJ,CAB", "," + РасширениеФайла + ",") > 0 Тогда
	ИначеЕсли Найти(",RAR,ZIP,ARJ,CAB,7Z,", "," + РасширениеФайла + ",") > 0 Тогда
	//Rarus Alkoko 15.07.2015 }	
		Возврат 15;
	ИначеЕсли Найти(",EXE,COM,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат 16;
	ИначеЕсли Найти(",BAT,CMD,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат 17;
	ИначеЕсли "PDF" = РасширениеФайла Тогда
		Возврат 18;
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции // рфПолучитьИндексПиктограммыФайла()


// Функция возвращает путь к системной папке, указанной в параметре <Имя>
//
// Параметры
//  Имя – имя системной папки. Возможные значения:
// 		AllUsersDesktop - рабочий стол (для всех пользователей);
// 		AllUsersStartMenu - меню "старт" (для всех пользователей);
// 		AllUsersPrograms - меню "программы" (для всех пользователей);
// 		AllUsersStartup - автозагрузка (для всех пользователей);
// 		Desktop - рабочий стол;
// 		Favorites - избранное;
// 		Fonts - шрифты;
// 		MyDocuments - мои документы;
//  	NetHood - папка NetHood;
// 		PrintHood - папка PrintHood;
// 		Programs - меню "программы";
// 		Recent - последние запуски;
// 		SendTo - меню "Отправить…";
// 		StartMenu - меню "старт";
//  	Startup - автозагрузка;
// 		Templates- шаблоны.
//
// Возвращаемое значение 
//  Строка, путь к системной папке
//
Функция рфПолучитьСистемнуюПапку(Имя)  Экспорт
  Попытка
    Ctrl = Новый COMОбъект("MSScriptControl.ScriptControl");
    // Указываем язык скрипта (VBS)	
    Ctrl.Language = "vbscript";
    // Добавляем код на VBS
    Ctrl.AddCode("
    |Function SpecialFolders(Name)
    |Set Shell = CreateObject(""WScript.Shell"")
    |SpecialFolders = Shell.SpecialFolders(Name)
    |End Function");
    // Запускаем функцию SpecialFolders с параметром <Имя>
    ИмяПапки = Ctrl.Run("SpecialFolders", Имя);
	
  Исключение
    // В случае неудачного выполнения возвращаем Неопределено
    ИмяПапки = Неопределено; 
  КонецПопытки;
  
  Возврат ИмяПапки;
КонецФункции // рфПолучитьСистемнуюПапку()

#Если Клиент Тогда
// Функция возвращает каталог пользователя
// Параметры
//  Нет
//
// Возвращаемое значение 
//  Строка, путь к каталогу пользователя
//
Функция рфПолучитьКаталогПользователя() Экспорт
	
	ПапкаМоиДокументы = рфПолучитьСистемнуюПапку("MyDocuments");	
	ПапкаМоиДокументы = ?(ПапкаМоиДокументы=Неопределено,"С:\",ПапкаМоиДокументы);
	
	ИмяКаталога = ВосстановитьЗначение("КаталогПользователя" + ПараметрыСеанса.Пользователь);
	ИмяКаталога = ?(ИмяКаталога = Неопределено,ПапкаМоиДокументы,ИмяКаталога);
	
	Возврат ИмяКаталога; 
	
КонецФункции // рфПолучитьКаталогПользователя()
#КонецЕсли

// Формирует строку фильтра для диалога выбора файла с типами файлов.
//
// Параметры
//  Нет.
//
// Возвращаемое значение:
//   Строка – фильтр по типам файлов для диалога выбора файла.
//
Функция рфПолучитьФильтрФайлов() Экспорт

	Возврат "Все файлы (*.*)|*.*|"
	      + "Документ Microsoft Word (*.doc,*.docx)|*.doc;*.docx|"
	      + "Документ Microsoft Excel (*.xls,*.xlsx)|*.xls;*.xlsx|"
	      + "Документ Microsoft PowerPoint (*.ppt,*.pptx)|*.ppt;*.pptx|"
	      + "Документ Microsoft Visio (*.vsd,*.vsdx)|*.vsd;*.vsd|"
	      + "Письмо электронной почты (*.msg)|*.msg|"
	      + "Картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|"
	      + "Текстовый документ (*.txt)|*.txt|"
	      + "Табличный документ (*.mxl)|*.mxl|";

КонецФункции // рфПолучитьФильтрФайлов()

// Функции работы с архивами

// Функция архивирует файл
// 
// Параметры:
//  ИмяИсходногоФайла			- Строка, Имя исходного файла, предназначенного для архивации
//  ИмяАрхивногоФайла			- Строка, Имя результирующего архивного файла
// 	УдалятьФайлПослеАрхивации	- Булево, Признак удаления исходного файла после его архивации
//
// Возвращаемое значение:
//   Булево - Результат архивации файла
//
Функция рфАрхивировать(ИмяИсходногоФайла, ИмяАрхивногоФайла, УдалятьФайлПослеАрхивации = Ложь) Экспорт
	Файл = Новый Файл(ИмяИсходногоФайла);
	Если Файл.Существует() Тогда
		#Если Клиент Тогда 
		Состояние("Архивация файла: " + ИмяИсходногоФайла);
        #КонецЕсли
		// Читаем содержимое исходного файла и пишем его в хранилище значений
		ЧтениеТекста = Новый ЧтениеТекста(ИмяИсходногоФайла);
		ХЗ = Новый ХранилищеЗначения(ЧтениеТекста.Прочитать(), Новый СжатиеДанных(9));
		ЧтениеТекста.Закрыть();
		// Записываем значение хранилища значений в архивный файл
		ЗначениеВФайл(ИмяАрхивногоФайла, ХЗ);
		
		// Удаляем исходный файл, если в этом есть необходимость
		Если УдалятьФайлПослеАрхивации Тогда
			УдалитьФайлы(ИмяИсходногоФайла);
		КонецЕсли;
		
		Возврат Истина;
	Иначе
		Сообщить("Ошибка архивации. Файл не существует: " + ИмяИсходногоФайла, СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
КонецФункции // рфАрхивировать()

// Функция разархивирует файл
// 
// Параметры:
//  ИмяАрхивногоФайла			- Строка, Имя архивного файла, предназначенного для разархивации
//  ИмяИсходногоФайла			- Строка, Имя исходного файла, полученного при разархивации
// 	УдалятьФайлПослеАрхивации	- Булево, Признак удаления архивного файла после его разархивации
//
// Возвращаемое значение:
//   Булево - Результат разархивации файла
//
Функция рфРазархивировать(ИмяАрхивногоФайла, ИмяИсходногоФайла, УдалятьФайлПослеАрхивации = Ложь) Экспорт
	Файл = Новый Файл(ИмяАрхивногоФайла);
	Если Файл.Существует() Тогда
		#Если Клиент Тогда 
		Состояние("Разархивация файла: " + ИмяАрхивногоФайла);
        #КонецЕсли
		
		// Читаем содержимое архивного файла
		ЧтениеТекста = Новый ЧтениеТекста(ИмяАрхивногоФайла);
		Строка = ЧтениеТекста.Прочитать();
		ЧтениеТекста.Закрыть();

		// Записываем содержимое архивного файла в него же, но в кодировке UTF8
		ЗаписьТекста = Новый ЗаписьТекста(ИмяАрхивногоФайла, КодировкаТекста.UTF8);
		ЗаписьТекста.Записать(Строка);
		ЗаписьТекста.Закрыть();
		
		// Расшифровываем значение архивного файла и записываем его в исходный
		ЗаписьТекста = Новый ЗаписьТекста(ИмяИсходногоФайла);
		Попытка
			ЗаписьТекста.Записать(ЗначениеИзФайла(ИмяАрхивногоФайла).Получить());
		Исключение
			обОписаниеОшибки();
		КонецПопытки;
		
		// Удаляем архивный файл, если в этом есть необходимость
		Если УдалятьФайлПослеАрхивации Тогда
			УдалитьФайлы(ИмяАрхивногоФайла);
		КонецЕсли;
		
		Возврат Истина;
	Иначе
		Сообщить("Ошибка разархивации. Файл не существует: " + ИмяАрхивногоФайла, СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
КонецФункции // рфРазархивировать()


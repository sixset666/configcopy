Функция ПолучитьТипСклад() Экспорт
	Возврат ns_Переопределяемый.ПолучитьТипСклад();	
КонецФункции

Процедура УстановитьВыгружатьПоДокументуНаСервере(Документ, ИмяСправочника) Экспорт
	Результат = ns_Переопределяемый.УстановитьВыгружатьПоДокументу(Документ, ИмяСправочника);
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого Стр Из Результат Цикл
		УстановитьВыгружатьЭлементу(Стр.Ссылка, Истина);		
	КонецЦикла; 
КонецПроцедуры

Процедура УстановитьВыгружатьНаСервере(ВыделенныеСтроки, Значение = Истина, ИмяСправочника) Экспорт
	Попытка
		НачатьТранзакцию();
		
		Для каждого СтрокаСписка Из ВыделенныеСтроки Цикл
			Если СтрокаСписка.ЭтоГруппа Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ns_ЗапчастиИАксессуары.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ns_ЗапчастиИАксессуары КАК ns_ЗапчастиИАксессуары
				|ГДЕ
				|	ns_ЗапчастиИАксессуары.Родитель В ИЕРАРХИИ(&Родитель)
				|	И ns_ЗапчастиИАксессуары.ЭтоГруппа = ЛОЖЬ";
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "ns_ЗапчастиИАксессуары", ИмяСправочника);
				Запрос.УстановитьПараметр("Родитель", СтрокаСписка.Ссылка);

				РезультатЗапроса = Запрос.Выполнить();
				Если РезультатЗапроса.Пустой() Тогда
					Возврат;
				КонецЕсли;  
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					УстановитьВыгружатьЭлементу(Выборка.Ссылка, Значение);
				КонецЦикла; 
			Иначе	
				УстановитьВыгружатьЭлементу(СтрокаСписка, Значение);
			КонецЕсли;
		КонецЦикла;  
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
	    ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
КонецПроцедуры

Процедура УстановитьВыгружатьЭлементу(Ссылка, Значение)
	Если НЕ Ссылка.Выгружать = Значение Тогда
		ТекущийОбъект = Ссылка.ПолучитьОбъект();
		ТекущийОбъект.Выгружать = Значение;
		ТекущийОбъект.Записать();
	КонецЕсли; 
КонецПроцедуры

Процедура УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, ИмяРеквизита, НовоеЗначение) Экспорт
	Если ЗначениеЗаполнено(НовоеЗначение) И (НЕ ТекущийОбъект[ИмяРеквизита] = НовоеЗначение) Тогда
		ТекущийОбъект[ИмяРеквизита] = НовоеЗначение;
		Изменен = Истина;
	КонецЕсли; 
КонецПроцедуры

Процедура ЗаполнитьСопоставлениеРеквизитов(Изменен, ТекущийОбъект, ЗначенияСопоставленныхРеквизитов, СопоставлениеРеквизитов) Экспорт
	Если НЕ ЗначениеЗаполнено(СопоставлениеРеквизитов) Тогда
		Возврат;	
	КонецЕсли; 
	
	ЗначениеРеквизита = Неопределено;
	
	Для каждого Стр Из СопоставлениеРеквизитов.Сопоставление Цикл
		ТекущееЗначение = ns_ШаблонизаторСервер.ПолучитьЗначениеРеквизита(ТекущийОбъект.Номенклатура, Стр.Реквизит1С, ЗначенияСопоставленныхРеквизитов.ДопРеквизиты);
		Если НЕ ТекущееЗначение = Неопределено Тогда
			Попытка
				ЗначениеРеквизита = Перечисления["ns_" + Стр.РеквизитАвито].ПолучитьЗначениеПеречисления(ТекущееЗначение.Наименование);
				Если НЕ ЗначениеРеквизита = Неопределено Тогда
					ТекущийОбъект[Стр.РеквизитЗапчасти] = ЗначениеРеквизита;
				КонецЕсли;
				Изменен = Истина;
			Исключение
			КонецПопытки;
			
			Если ЗначениеРеквизита = Неопределено И ЗначениеЗаполнено(ТекущееЗначение) Тогда
				Попытка
					ТекущийОбъект[Стр.РеквизитЗапчасти] = ТекущееЗначение;
					Изменен = Истина;
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла; 

	Для каждого Стр Из СопоставлениеРеквизитов.СопоставлениеНоменклатуры Цикл
		ТекущееЗначение = ns_ШаблонизаторСервер.ПолучитьЗначениеРеквизита(ТекущийОбъект.Номенклатура, Стр.Реквизит1С, ЗначенияСопоставленныхРеквизитов.Номенклатура);
		Если НЕ ТекущееЗначение = Неопределено Тогда
			Попытка
				Если Стр.РеквизитАвито = "Brand_ПроизводителиЗапчастей" Тогда
					ЗначениеРеквизита = Справочники.ns_ПроизводителиЗапчастей.НайтиЭлементПоНаименованию(ТекущееЗначение.Наименование);
					Если НЕ ЗначениеРеквизита = Неопределено Тогда
						ТекущийОбъект[Стр.РеквизитЗапчасти] = ЗначениеРеквизита;
						Изменен = Истина;
					КонецЕсли;
				ИначеЕсли Стр.РеквизитАвито = "Brand_ПроизводителиШин" Тогда
					ЗначениеРеквизита = Справочники.ns_ПроизводителиШин.НайтиЭлементПоНаименованию(ТекущееЗначение.Наименование);
					Если НЕ ЗначениеРеквизита = Неопределено Тогда
						ТекущийОбъект[Стр.РеквизитЗапчасти] = ЗначениеРеквизита;
						Изменен = Истина;
					КонецЕсли;
				Иначе	
					Попытка
						ЗначениеРеквизита = Перечисления["ns_" + Стр.РеквизитАвито].ПолучитьЗначениеПеречисления(ТекущееЗначение.Наименование);
					Исключение
						ЗначениеРеквизита = ТекущееЗначение;
					КонецПопытки;
					Если НЕ ЗначениеРеквизита = Неопределено Тогда
						ТекущийОбъект[Стр.РеквизитЗапчасти] = ЗначениеРеквизита;
						Изменен = Истина;
					КонецЕсли;
				КонецЕсли; 
			Исключение
			КонецПопытки; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки) Экспорт
	
	// Данный функционал доступен с версии платформы 8.3.10
	Ключи = Строки.ПолучитьКлючи();
	
	СписокНоменклатуры = Новый Массив;
	Для каждого Ключ Из Ключи Цикл
		СписокНоменклатуры.Добавить(Ключ.Номенклатура);	
	КонецЦикла;
	
	НоменклатураСКартинками = ns_Переопределяемый.ПолучитьСписокНоменклатурыСКартинками(СписокНоменклатуры);
	Если НоменклатураСКартинками.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из Строки Цикл
		Если Строка.Ключ.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		Если НоменклатураСКартинками[Строка.Ключ.Номенклатура] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Строка.Значение.Данные.ЕстьКартинка = Истина;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьРолиАвито() Экспорт
	
	Если ЕстьРольВПрофиле("ns_АвитоАдминистратор") Тогда
		ДобавитьРоль(Метаданные.Роли.ns_АвитоАдминистратор);
	КонецЕсли;
	Если ЕстьРольВПрофиле("ns_АвитоМенеджер") Тогда
		ДобавитьРоль(Метаданные.Роли.ns_АвитоМенеджер);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьРоль(Роль)
	Пользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	Если Пользователь.Роли.Содержит(Роль) Тогда
	    Возврат;
	КонецЕсли;
	Пользователь.Роли.Добавить(Роль);
	Пользователь.Записать();
	ns_ОбщегоНазначения.СообщениеПользователю("Текущему пользователю добавлена роль для работы с Авито. Перезапустите 1С.");
КонецПроцедуры

Функция ЕстьРольВПрофиле(ИмяРоли, Пользователь = Неопределено) Экспорт
	
	Возврат Ложь;

	УстановитьПривилегированныйРежим(Истина);
	
	//Если Пользователь = Неопределено Тогда
	//	Попытка
	//		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	//	Исключение
	//		Возврат Ложь;
	//	КонецПопытки;    
	//КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	1 
		|ИЗ
		|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|		ПО ГруппыДоступаПользователи.Ссылка.Профиль = ПрофилиГруппДоступаРоли.Ссылка
		|ГДЕ
		|	ПрофилиГруппДоступаРоли.Роль.Имя = &Роль
		|	И ГруппыДоступаПользователи.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Роль", ИмяРоли);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;		
	Иначе	
		Возврат Истина;		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКлючФормы(СсылкаНаОбъект, ИмяСправочника) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ЗапчастиИАксессуары.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ns_ЗапчастиИАксессуары КАК ns_ЗапчастиИАксессуары
	|ГДЕ
	|	ns_ЗапчастиИАксессуары.Номенклатура = &Номенклатура";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ns_ЗапчастиИАксессуары", ИмяСправочника);
	Запрос.УстановитьПараметр("Номенклатура", СсылкаНаОбъект);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
КонецФункции

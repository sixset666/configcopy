// Функция создает документ "Телефонный звонок"
//
// Параметры:
//	СтруктураЗвонка	- Структура	- Структура данных звонка
//
Функция сфпСоздатьТелефонныйЗвонок(СтруктураЗвонка, ДанныеЗаполнения) Экспорт
	
	ИмяДокументаТелефонныйЗвонок = СтрЗаменить(сфпСофтФонПроСервер.сфпИмяДокументаТелефонныйЗвонок(), "Документ.", "");
	РеквизитыДокумента = Метаданные.Документы[ИмяДокументаТелефонныйЗвонок].Реквизиты;
	
	НовыйЗвонок	= Документы[ИмяДокументаТелефонныйЗвонок].СоздатьДокумент();
	НовыйЗвонок.Дата				   = сфпСофтФонПроСервер.сфпТекущаяДата();
	НовыйЗвонок.Входящий			   = СтруктураЗвонка.ВходящийЗвонок;
	НовыйЗвонок.АбонентКакСвязаться	   = СтруктураЗвонка.НомерТелефона; 
	НовыйЗвонок.АбонентПредставление   = НСтр("ru='!!!Не определен!!!'");
	НовыйЗвонок.Автор				   = сфпСофтФонПроСервер.сфпТекущийПользователь();
	НовыйЗвонок.Ответственный		   = НовыйЗвонок.Автор;
	НовыйЗвонок.Описание			   = сфпСофтФонПроСервер.сфпЗаполнитьОписаниеТелефонногоЗвонка(НовыйЗвонок.сфпДлительностьЗвонка);
	НовыйЗвонок.сфпИдентификаторЗвонка = СтруктураЗвонка.hCall;
	НовыйЗвонок.сфпИдентификаторЗаписи = СтруктураЗвонка.ИдентификаторЗаписи;
	НовыйЗвонок.сфпДлительностьЗвонка  = 0;
	НовыйЗвонок.сфпСостояниеЗвонка	   = Перечисления.сфпСостоянияЗвонков.Отвеченный;

	// +АА
	НовыйЗвонок["Организация"] = НовыйЗвонок["Автор"].Организация;
	НовыйЗвонок["ПодразделениеКомпании"] = НовыйЗвонок["Автор"].Подразделение;
	НовыйЗвонок["ХозОперация"] = ПредопределенноеЗначение("Справочник.ХозОперации.ТелефонныйЗвонок");
	// -АА
	
	Если НовыйЗвонок.Ссылка.Метаданные().Реквизиты.Найти("Важность") <> Неопределено Тогда
		ИмяМетаданныхВажность = НовыйЗвонок["Важность"].Метаданные().Имя;
		Если ИмяМетаданныхВажность = "ВариантыВажностиЗадачи" Тогда
			НовыйЗвонок["Важность"] = Перечисления[ИмяМетаданныхВажность].Обычная;
			
		ИначеЕсли ИмяМетаданныхВажность = "ВариантыВажностиВзаимодействия" Тогда
			НовыйЗвонок["Важность"] = Перечисления[ИмяМетаданныхВажность].Обычная;
			
		ИначеЕсли ИмяМетаданныхВажность = "Важность" Тогда
			НовыйЗвонок["Важность"] = Перечисления[ИмяМетаданныхВажность].Низкая;
		КонецЕсли;
	КонецЕсли;
	
	Если РеквизитыДокумента.Найти("Тема") <> Неопределено Тогда
		НовыйЗвонок.Тема = сфпСофтФонПроСервер.сфпЗаполнитьТемуТелефонногоЗвонка(НовыйЗвонок.Входящий, НовыйЗвонок.Дата);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураЗвонка.Контакт) Тогда
		Если ЗначениеЗаполнено(СтруктураЗвонка.ContactID) Тогда
			СтруктураЗвонка.Контакт	= сфпСофтФонПроСервер.сфпНайтиКонтактПоGUID(СтруктураЗвонка.ContactID);
		КонецЕсли;
	КонецЕсли;
	
	НастройкаПриВходящемЗвонке = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпДействиеПриВходящемЗвонке");
	НастройкаПриИсходящемЗвонке = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпДействиеПриИсходящемЗвонке");				
	
	Если НЕ ЗначениеЗаполнено(СтруктураЗвонка.Контакт) Тогда
		Если СтруктураЗвонка.МассивЗвонящих <> Неопределено Тогда
			ДействиеОткрытьТелефонныйЗвонок = НСтр("ru='Открыть телефонный звонок'");
			
			// По новому алгоритму мы всегда в Телефонный звонок подставляем первый элемент из массива звонящих
			// если только не стоит настройка "Открывать телефонный звонок"
			Если (СтруктураЗвонка.ВходящийЗвонок И НастройкаПриВходящемЗвонке = ДействиеОткрытьТелефонныйЗвонок) ИЛИ
				 (НЕ СтруктураЗвонка.ВходящийЗвонок И НастройкаПриИсходящемЗвонке = ДействиеОткрытьТелефонныйЗвонок) Тогда
				Если СтруктураЗвонка.МассивЗвонящих.Количество() = 1 Тогда
					СтруктураЗвонка.Контакт	= СтруктураЗвонка.МассивЗвонящих[0];
				КонецЕсли;
				
			ИначеЕсли СтруктураЗвонка.МассивЗвонящих.Количество() > 0 Тогда
				СтруктураЗвонка.Контакт	= СтруктураЗвонка.МассивЗвонящих[0];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураЗвонка.Контакт) Тогда
		
		// +АА
		// Проверим, что контактом явялется тот кто надо
		Если ЗначениеЗаполнено(СтруктураЗвонка.НомерТелефона) Тогда
			МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(СтруктураЗвонка.НомерТелефона);
			Если МассивЗвонящих.Количество() > 0 И МассивЗвонящих.Найти(СтруктураЗвонка.Контакт) = Неопределено Тогда
				СтруктураЗвонка.Контакт	= МассивЗвонящих[0];
			КонецЕсли;
		КонецЕсли;
		// -АА
		
		НовыйЗвонок.АбонентПредставление = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(СтруктураЗвонка.Контакт);
		
		ИмяРеквизитаАбонентКонтакт = сфпСофтФонПроСервер.сфпИмяРеквизитаАбонентКонтакт();
		НовыйЗвонок[ИмяРеквизитаАбонентКонтакт] = СтруктураЗвонка.Контакт;
		
		Если РеквизитыДокумента.Найти("Контрагент") <> Неопределено Тогда
			ИмяМетаданныхКонтакт = СтруктураЗвонка.Контакт.Метаданные().Имя;
			Если ИмяМетаданныхКонтакт = "Контрагенты" Тогда
				НовыйЗвонок.Контрагент = СтруктураЗвонка.Контакт;
				
			ИначеЕсли ИмяМетаданныхКонтакт = "КонтактныеЛица" Тогда
				НовыйЗвонок.Контрагент = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(СтруктураЗвонка.Контакт);
			КонецЕсли;
		КонецЕсли;

		Если Найти(Строка(СтруктураЗвонка.Контакт), НСтр("ru = '<Объект не найден>'")) > 0 Тогда
			КомментарийЗвонка = Строка(ТипЗнч(СтруктураЗвонка.Контакт)) + ": "
			+ сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(СтруктураЗвонка.Контакт) + НСтр("ru = ', номер: '") 
			+ сфпСофтФонПроСервер.сфпПолучитьПредставлениеНомераТелефона(СтруктураЗвонка.Контакт, СтруктураЗвонка.НомерТелефона)
			+ Символы.ПС + НСтр("ru='К данному абоненту в доступе отказано.'")
			+ НСтр("ru=' Для разрешения работы с абонентом обратитесь к руководителю или администратору.'");
			
			Если РеквизитыДокумента.Найти("Комментарий") = Неопределено Тогда
				НовыйЗвонок.Описание = НовыйЗвонок.Описание + Символы.ПС + КомментарийЗвонка;
				
			Иначе	
				НовыйЗвонок.Комментарий = КомментарийЗвонка;
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли СтруктураЗвонка.ВнешнийЗвонок Тогда
		Если СтруктураЗвонка.МассивЗвонящих = Неопределено Тогда
			Если НЕ ПустаяСтрока(СтруктураЗвонка.CallerInfoName) Тогда
				Если Найти(СтруктураЗвонка.CallerInfoName, СтруктураЗвонка.НомерТелефона) = 0 Тогда
					НовыйЗвонок.АбонентПредставление = СтруктураЗвонка.CallerInfoName;
				КонецЕсли;
			КонецЕсли;

		ИначеЕсли СтруктураЗвонка.МассивЗвонящих.Количество() = 0 Тогда
			Если НЕ ПустаяСтрока(СтруктураЗвонка.CallerInfoName) Тогда
				Если Найти(СтруктураЗвонка.CallerInfoName, СтруктураЗвонка.НомерТелефона) = 0 Тогда
					НовыйЗвонок.АбонентПредставление = СтруктураЗвонка.CallerInfoName;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	

	ИначеЕсли НЕ ПустаяСтрока(СтруктураЗвонка.CalledInfoName) Тогда
		НовыйЗвонок.АбонентПредставление = СтруктураЗвонка.CalledInfoName;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Если ДанныеЗаполнения.Свойство("Основание") Тогда
			Если РеквизитыДокумента.Найти("ВзаимодействиеОснование") <> Неопределено Тогда
				НовыйЗвонок.ВзаимодействиеОснование = ДанныеЗаполнения.Основание;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("сфпСтруктураCoMagic") Тогда
			сфпСтруктураВнешнихДанных = ДанныеЗаполнения.сфпСтруктураCoMagic;
			НовыйЗвонок.сфпCoMagicID = сфпСтруктураВнешнихДанных.comagic_context.visitor_id;
			
			НовыйЗвонок.Описание = НовыйЗвонок.Описание + ?(НовыйЗвонок.Описание, "", Символы.ПС)
			+ НСтр("ru='Кампания: '") + сфпСтруктураВнешнихДанных.comagic_context.campaign + Символы.ПС
			+ НСтр("ru='Сайт: '") + сфпСтруктураВнешнихДанных.comagic_context.site + Символы.ПС
			+ НСтр("ru='Ключевые слова: '") + сфпСтруктураВнешнихДанных.comagic_context.search_query;
		КонецЕсли;	
	КонецЕсли;	
	
	Попытка
		НовыйЗвонок.Записать();
	Исключение
		СтрОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат НовыйЗвонок.Ссылка;

КонецФункции // сфпСоздатьТелефонныйЗвонок()


/////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЕЙСТВИЙ

// Процедура добавляет в массив структуру действий
//
// Параметры:
//	МассивДействий	- Массив	- Массив доступных действий
//	Наименование	- Строка	- Наименование действия
//	Действие		- Строка	- Имя процедуры-обработчика действия, процедура должна располагаться в этом модуле
//
Процедура сфпДобавитьДействие(МассивДействий, Наименование, Действие)
	СтруктураДействия = Новый Структура;
	СтруктураДействия.Вставить("Наименование",	Наименование);
	СтруктураДействия.Вставить("Действие",		Действие);
	МассивДействий.Добавить(СтруктураДействия);
КонецПроцедуры // сфпДобавитьДействие()

// Функция возвращает массив структур доступных действий при звонке
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Массив	- Массив структур доступных действий
//
Функция сфпПолучитьМассивДоступныхДействий() Экспорт
	МассивДействий = Новый Массив;
	сфпДобавитьДействие(МассивДействий,	Нстр("ru='Нет действий'"), "");
	сфпДобавитьДействие(МассивДействий,	Нстр("ru='Открыть телефонный звонок'"), "сфпОткрытьТелефонныйЗвонок");
	сфпДобавитьДействие(МассивДействий,	Нстр("ru='Карточка клиента'"), "сфпОткрытьКарточкуКонтакта");
	Если НЕ (Метаданные.Документы.Найти("Событие") = Неопределено) Тогда        
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Событие'"), "сфпОткрытьСобытие");
	КонецЕсли;	
	Если НЕ (Метаданные.Документы.Найти("ЗаказПокупателя") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Заказ покупателя'"), "сфпОткрытьЗаказПокупателя");
	КонецЕсли;	
	Если НЕ (Метаданные.Документы.Найти("ЗаказКлиента") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Заказ клиента'"), "сфпОткрытьЗаказКлиента");
	КонецЕсли;	
	Если НЕ (Метаданные.Документы.Найти("ЗаказПоставщику") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Заказ поставщику'"), "сфпОткрытьЗаказПоставщику");
	КонецЕсли;	
	Если НЕ (Метаданные.Обработки.Найти("ПомощникПродаж") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Помощник продаж'"), "сфпОткрытьПомощникПродаж");
	КонецЕсли;	
	Если НЕ (Метаданные.Отчеты.Найти("ДебиторскаяЗадолженность") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Дебиторская задолженность'"),	"сфпОткрытьДебиторскаяЗадолженность");
	КонецЕсли;	
	Если НЕ (Метаданные.Отчеты.Найти("Взаиморасчеты") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Взаиморасчеты'"), "сфпОткрытьВзаиморасчеты");
	КонецЕсли;	
	Если НЕ (Метаданные.Отчеты.Найти("РасчетыСПартнерами") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Взаиморасчеты'"), "сфпОткрытьРасчетыСПартнерами");
	КонецЕсли;	
	Если НЕ (Метаданные.Отчеты.Найти("КарточкаРасчетовСКлиентами") = Неопределено) Тогда
		сфпДобавитьДействие(МассивДействий,	Нстр("ru='Карточка расчетов'"), "сфпОткрытьКарточкаРасчетовСКлиентами");
	КонецЕсли;	
	Возврат МассивДействий;
КонецФункции // сфпПолучитьМассивДоступныхДействий()


/////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ COMAGIC

// Функция возвращает идентификатор CoMagic
//
// Парамеры:
//	Ссылка	- ДокументСсылка	- Документ
//
// Возвращаемое значение:
//	Строка	- Идентификатор CoMagic
//
Функция сфпПолучитьCoMagicID(Ссылка) Экспорт
	
	CoMagicID = "";
	
	ДокументИмяМетаданных = Ссылка.Метаданные().Имя;
	Если ДокументИмяМетаданных = "ТелефонныйЗвонок" ИЛИ ДокументИмяМетаданных = "CRM_ТелефонныйЗвонок" Тогда
		CoMagicID = Ссылка.сфпCoMagicID;
		
		ИмяРеквизитаАбонентКонтакт = сфпСофтФонПроСервер.сфпИмяРеквизитаАбонентКонтакт();
		
		АбонентКонтакт = Ссылка[ИмяРеквизитаАбонентКонтакт];
		Если ЗначениеЗаполнено(АбонентКонтакт) Тогда
			Если сфпСофтФонПроСервер.сфпРеквизитСуществует(АбонентКонтакт, "сфпCoMagicID") Тогда
				CoMagicID = АбонентКонтакт.сфпCoMagicID;
				Если ПустаяСтрока(CoMagicID) Тогда
					КонтактИмяМетаданных = АбонентКонтакт.Метаданные().Имя;
					Если КонтактИмяМетаданных = "КонтактныеЛицаПартнеров" ИЛИ КонтактИмяМетаданных = "КонтактныеЛица" Тогда
						ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(АбонентКонтакт);
						Если ЗначениеЗаполнено(ВладелецКонтакта) И сфпСофтФонПроСервер.сфпРеквизитСуществует(ВладелецКонтакта, "сфпCoMagicID") Тогда
							CoMagicID = ВладелецКонтакта.сфпCoMagicID;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат CoMagicID;

КонецФункции // сфпПолучитьCoMagicID()
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ОБМЕНОМ СООБЩЕНИЯМИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДОСТАВКИ СООБЩЕНИЙ
 
// Получает имя файла обмена
//
// Параметры
//	НастройкаДоставки 	- СправочникСсылка.НастройкиДоставки. Настройка
//	Выгрузка 			- Булево. Флан выгрузки сообщения
//	Номер 				- Число. Номер сообщения обмена
//	Расширение 			- Строка. Расширение файла обмена
//	Архивация 			- Булево. Флан архивации сообщения
//	Транслит 			- Булево. Флан необходимости транслитерации сообщения (для фтп)
//
// Возвращаемое значение:
//   СправочникСсылка.СтруктураИнформационныхБаз - ссылка на ИБ
//
Функция ИмяФайлаДоставки(УзелОбмена, ОтправкаСообщения = Истина, Знач Номер = Неопределено, 
	Знач Расширение = Неопределено, Архивация = Ложь,Транслит = Ложь) Экспорт
	
	ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	
	УзелИсточник = ?(ОтправкаСообщения, ЭтотУзел, УзелОбмена);
	УзелПриемник = ?(ОтправкаСообщения, УзелОбмена, ЭтотУзел);
	
	Если Номер = Неопределено Тогда
		Номер = НомерСообщенияОбмена(УзелОбмена, ЭтотУзел = УзелПриемник, 10);
	ИначеЕсли ТипЗнч(Номер) = Тип("Число") Тогда
		Номер = Формат(Номер, "ЧЦ=10; ЧВН=; ЧГ=0");
	КонецЕсли;
	Если Расширение = Неопределено Тогда
		Расширение = ?(Архивация, "zip", "xml");
	КонецЕсли;
	
	// формируем имя файла, исходя из кодов источника и приемника сообщения обмена
	Рез = "MessageExchange_" + СокрЛП(УзелИсточник.Код) + "_" + СокрЛП(УзелПриемник.Код) + "_" + Номер + "." + Расширение;
	
	Если Транслит Тогда
		Рез = ВыполнитьТранслит(Рез);
	КонецЕсли;
	
	Возврат Рез; 
	
КонецФункции // ИмяФайлаДоставки()

// Выполняет транслитерацию переданного текста
//
// Параметры
//  Значение  – Строка
//
// Возвращаемое значение:
//   Строка   – преобразованный текст
//
Функция ВыполнитьТранслит(Значение) Экспорт
	
	ТранслитерацияСоответствие = Новый Соответствие();
	ТранслитерацияСоответствие.Вставить("А", "A");
	ТранслитерацияСоответствие.Вставить("Б", "B");
	ТранслитерацияСоответствие.Вставить("В", "V");
	ТранслитерацияСоответствие.Вставить("Г", "G");
	ТранслитерацияСоответствие.Вставить("Д", "D");
	ТранслитерацияСоответствие.Вставить("Е", "YE");
	ТранслитерацияСоответствие.Вставить("Ё", "YO");
	ТранслитерацияСоответствие.Вставить("Ж", "ZH");
	ТранслитерацияСоответствие.Вставить("З", "Z");
	ТранслитерацияСоответствие.Вставить("И", "E");
	ТранслитерацияСоответствие.Вставить("Й", "Y");
	ТранслитерацияСоответствие.Вставить("К", "K");
	ТранслитерацияСоответствие.Вставить("Л", "L");
	ТранслитерацияСоответствие.Вставить("М", "M");
	ТранслитерацияСоответствие.Вставить("Н", "N");
	ТранслитерацияСоответствие.Вставить("О", "O");
	ТранслитерацияСоответствие.Вставить("П", "P");
	ТранслитерацияСоответствие.Вставить("Р", "R");
	ТранслитерацияСоответствие.Вставить("С", "S");
	ТранслитерацияСоответствие.Вставить("Т", "T");
	ТранслитерацияСоответствие.Вставить("У", "U");
	ТранслитерацияСоответствие.Вставить("Ф", "F");
	ТранслитерацияСоответствие.Вставить("Х", "H");
	ТранслитерацияСоответствие.Вставить("Ц", "C");
	ТранслитерацияСоответствие.Вставить("Ч", "CH");
	ТранслитерацияСоответствие.Вставить("Ш", "SH");
	ТранслитерацияСоответствие.Вставить("Щ", "SHH");
	ТранслитерацияСоответствие.Вставить("Ъ", "##");
	ТранслитерацияСоответствие.Вставить("Ы", "EE");
	ТранслитерацияСоответствие.Вставить("Ь", "#");
	ТранслитерацияСоответствие.Вставить("Э", "EA");
	ТранслитерацияСоответствие.Вставить("Ю", "YU");
	ТранслитерацияСоответствие.Вставить("Я", "YA");
	
	// переведем кириллицу в транслит (не все FTP сервера поддерживают кириллицу)
	Для Каждого ТекСимвол Из ТранслитерацияСоответствие Цикл
		Значение = СтрЗаменить(Значение, ТекСимвол.Ключ, ТекСимвол.Значение);
	КонецЦикла;
	
	Возврат Значение;

КонецФункции // ВыполнитьТранслит()

// Удаляет файлы по маске (MessageExchange_Ц_П1_??????????.*)
//
// Параметры
//	НастройкаДоставки 	- СправочникСсылка.НастройкиДоставки. Настройка
//	Выгрузка 			- Булево. Флаг выгрузки сообщения
//	МаскаНомер 			- Строка. Маска номера файла сообщения обмена
//	МаскаРасш 			- Строка. Маска расширения файла сообщения обмена
//	ИсключатьТекущее 	- Булево. Флаг не удаления текущего сообщения
//	мсвИсключения 		- Массив. Массив файлов-исключений
//
// Возвращаемое значение:
//   Булево. Флаг успешного удаления файлов
//
Функция УдалитьФайлыПоМаске(УзелОбмена,НастройкаДоставки,Выгрузка = Истина, МаскаНомер = "??????????", МаскаРасш = "*", 
	ИсключатьТекущее = Истина, мсвИсключения = Неопределено) Экспорт
	
	Рез = Истина;
	КаталогСообщенийОбмена = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КаталогСообщенийОбмена) Тогда
	    КаталогСообщенийОбмена = НастройкаДоставки.КаталогОбменов;
	КонецЕсли;
	
	Если ПустаяСтрока(КаталогСообщенийОбмена) Тогда
		КаталогСообщенийОбмена = КаталогВременныхФайлов();
	КонецЕсли;
	
	Маска = орМаскаФайлаДоставки(УзелОбмена,НастройкаДоставки, Выгрузка); //отраслевое переопределение маски
	Если ПустаяСтрока(Маска) Тогда
		ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
		ИмяФайлаТекСообщения = ИмяФайлаДоставки(УзелОбмена, Выгрузка, 
			?(Выгрузка, УзелОбмена.НомерОтправленного, УзелОбмена.НомерПринятого), , НастройкаДоставки.АрхивироватьСообщенияОбмена);
	
		Маска = ИмяФайлаДоставки(УзелОбмена, Выгрузка, МаскаНомер, МаскаРасш);
	КонецЕсли;	
	
	
	
	мсвФайлыОбмена = НайтиФайлы(КаталогСообщенийОбмена, Маска);
	ЕстьИсключенияКоторыеНельзяУдалять = (ТипЗнч(мсвИсключения) = Тип("Массив")) И (мсвИсключения.Количество() > 0);
	Для Каждого ТекФайл Из мсвФайлыОбмена Цикл
		Если ИсключатьТекущее И ТекФайл.Имя = ИмяФайлаТекСообщения Тогда
			Продолжить;
		КонецЕсли;
		Если ЕстьИсключенияКоторыеНельзяУдалять И (мсвИсключения.Найти(ТекФайл.ПолноеИмя) <> Неопределено) Тогда
			Продолжить;
		КонецЕсли;
		
		// Пытаемся удалить файл со старым сообщением
		Попытка
			УдалитьФайлы(ТекФайл.ПолноеИмя);
		Исключение
			Рез = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Рез;
КонецФункции  // УдалитьФайлыПоМаске()

// Проверяет доставку файла по указанной настройке
//
// Параметры
//	НастройкаДоставки 	- СправочникСсылка.НастройкиДоставки. Настройка
//	стрОшибки 			- Строка. Строка ошибок
//	флСообщать 			- Булево. Флаг необходимости вывода сообщения
//
// Возвращаемое значение:
//   Булево. Флаг успешной доставки сообщения
//
Функция ПроверитьДоставкуСообщения(НастройкаДоставки, стрОшибки = "", флСообщать = Ложь) Экспорт
	
	стрОшибки = "";
	СпособОбмена = НастройкаДоставки.СпособОбмена;
	
	ЛокКаталог_ = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(ЛокКаталог_) Тогда
	    ЛокКаталог_ = НастройкаДоставки.КаталогОбменов;
	КонецЕсли;
	ИмяТестовогоФайла = "test_exch_" + Формат(ТекущаяДата(), "ДФ = ддММгггг") + ".txt";
	
	// SUIG - мы не можем копировать один и тот же файл в одну и ту же папку
	Если СпособОбмена = 0 Тогда
		Если НастройкаДоставки.ИспользоватьВременныйКаталог Тогда
			ПолноеИмяТестовогоФайла = ?(Прав(ЛокКаталог_,1)="\",ЛокКаталог_,ЛокКаталог_ + "\") + "TEMP\" + ИмяТестовогоФайла;
		Иначе
			// можем проверить только запись в лок каталог
			ПолноеИмяТестовогоФайла = ?(Прав(ЛокКаталог_,1)="\",ЛокКаталог_,ЛокКаталог_ + "\") + ИмяТестовогоФайла;
			ТекстДок = Новый ТекстовыйДокумент;
			Попытка
				ТекстДок.Записать(ПолноеИмяТестовогоФайла);
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Тестовый файл создан в каталоге обмена");
				Возврат Истина;
			Исключение
				стрОшибки = стрОшибки + "Ошибка при создании тестового файла:
				|" + ИнформацияОбОшибке().Описание;
				Если флСообщать Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + стрОшибки);
				КонецЕсли;
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;
	Иначе
	   	ПолноеИмяТестовогоФайла = ?(Прав(ЛокКаталог_,1)="\",ЛокКаталог_,ЛокКаталог_ + "\") + ИмяТестовогоФайла;
	КонецЕсли;
	
	//проверим запись в лок каталог
	ТекстДок = Новый ТекстовыйДокумент;
	Попытка
		ТекстДок.Записать(ПолноеИмяТестовогоФайла);
	Исключение
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		стрОшибки = ТекДата + стрОшибки + "Ошибка при создании тестового файла:
			|" + ИнформацияОбОшибке().Описание;
		Если флСообщать Тогда
			Сообщить(стрОшибки);
		КонецЕсли;
		
		Возврат Ложь;
	КонецПопытки;
	
	Если СпособОбмена = 0 Тогда //по локальной сети
		Если НайтиФайлы(ЛокКаталог_).Количество() = 0 Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при копировании тестового файла в каталог обмена:
				|Каталог не обнаружен";
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли;
			Возврат Ложь;	
		КонецЕсли;
		
		ПолноеИмяТестовогоФайлаПриемник = ?(Прав(ЛокКаталог_,1)="\",
			НастройкаДоставки.КаталогОбменов,ЛокКаталог_ + "\") + ИмяТестовогоФайла;
		Попытка
			КопироватьФайл(ПолноеИмяТестовогоФайла, ПолноеИмяТестовогоФайлаПриемник);
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Тестовый файл скопирован в каталог обмена");
			КонецЕсли; 
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при копировании тестового файла в каталог обмена:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли;
			
			Возврат Ложь;
		КонецПопытки;
		
		Попытка
			КопироватьФайл(ПолноеИмяТестовогоФайлаПриемник, ПолноеИмяТестовогоФайла);
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Тестовый файл получен из каталога обмена");
			КонецЕсли;
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при получении тестового файла из каталога обмена:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли;
			
			Возврат Ложь;
		КонецПопытки;
        
		Попытка
			УдалитьФайлы(ПолноеИмяТестовогоФайлаПриемник);
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Тестовый файл удален из каталога обмена");
			КонецЕсли;
			
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при удалении тестового файла из каталога обмена:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли;
			
			Возврат Ложь;
		КонецПопытки;
		
	ИначеЕсли СпособОбмена = 1 Тогда //MAPI (доступно только на клиенте)
		#Если Клиент Тогда
			Состояние("Подключение к почтовому серверу ...");
			
			Почта = Новый Почта(); 
			Попытка
				Почта.Подключиться();
				Если флСообщать Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Подключение к почтовому профилю пользователя выполнено");
				КонецЕсли; 
			Исключение
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				стрОшибки = ТекДата + стрОшибки + "Ошибка при подключении к почтовому профилю пользователя:
					|" + ИнформацияОбОшибке().Описание;
				
				Если флСообщать Тогда
					Сообщить(стрОшибки, СтатусСообщения.Внимание);
				КонецЕсли;
				
				Возврат Ложь;
			КонецПопытки;
			
			ПочтовоеСообщение = Новый ПочтовоеСообщение;
			ПочтовоеСообщение.Тема = "ТЕСТОВОЕ СООБЩЕНИЕ УРБД";
			ПочтовоеСообщение.Получатели.Добавить(НастройкаДоставки.АдресЭлектроннойПочты);
			
			// Отправляем тестовое сообщение
			Попытка
				Состояние("Отправка тестового сообщения ...");
				Почта.Послать(ПочтовоеСообщение);
				Почта.Отключиться();
				Сообщить("Тестовое сообщение отправлено");
				
			Исключение
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				стрОшибки = ТекДата + стрОшибки + "Ошибка при попытке отправки тестового сообщения:
					|" + ИнформацияОбОшибке().Описание;
				
				Если флСообщать Тогда
					Сообщить(стрОшибки, СтатусСообщения.Внимание);
				КонецЕсли;
				
				Возврат Ложь;
			КонецПопытки;
		#Иначе
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Почта MAPI не доступна на сервере и во внешнем соединении!";
			Возврат Ложь;
		#КонецЕсли
	
	ИначеЕсли СпособОбмена = 2 Тогда //POP-SMTP
		#Если Клиент Тогда
			Состояние("Подключение к почтовому серверу POP3 ...");
		#КонецЕсли
		
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		ПочтовыйПрофиль.АдресСервераPOP3 = НастройкаДоставки.СерверPOP3;
		ПочтовыйПрофиль.ПортPOP3         = НастройкаДоставки.ПортPOP3;
		ПочтовыйПрофиль.Пользователь     = НастройкаДоставки.ПользовательPOP3;
		ПочтовыйПрофиль.Пароль           = НастройкаДоставки.ПарольPOP3;
		
		Почта = Новый ИнтернетПочта(); 
      	Попытка
			Почта.Подключиться(ПочтовыйПрофиль);
			Почта.Отключиться();
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Подключение к почтовому серверу POP3 выполнено");
			КонецЕсли; 
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при подключении к почтовому серверу POP3:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли;
			
			Возврат Ложь;
		КонецПопытки;
		
		#Если Клиент Тогда
			Состояние("Подключение к почтовому серверу SMTP ...");
		#КонецЕсли
		
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		ПочтовыйПрофиль.АдресСервераSMTP = НастройкаДоставки.СерверSMTP;
		ПочтовыйПрофиль.ПортSMTP         = НастройкаДоставки.ПортSMTP;
		ПочтовыйПрофиль.ПользовательSMTP = НастройкаДоставки.ПользовательSMTP;
		ПочтовыйПрофиль.ПарольSMTP       = НастройкаДоставки.ПарольSMTP;
		
		Почта = Новый ИнтернетПочта(); 
      	Попытка
			Почта.Подключиться(ПочтовыйПрофиль);
			Почта.Отключиться();
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Подключение к почтовому серверу SMTP выполнено");
			КонецЕсли;
			
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при подключении к почтовому серверу SMTP:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли;
			
			Возврат Ложь;
		КонецПопытки;
	
	ИначеЕсли СпособОбмена = 3 Тогда //FTP
		#Если Клиент Тогда
			Состояние("Подключение к FTP серверу ...");
		#КонецЕсли
		
		Попытка
			FTP = Новый FTPСоединение(НастройкаДоставки.СерверFTP, НастройкаДоставки.ПортFTP, НастройкаДоставки.ПользовательFTP, НастройкаДоставки.ПарольFTP,, НастройкаДоставки.ПассивныйРежим);
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
			Если Найти(ВРег(ТекстОшибки),ВРег("couldn't connect to server")) Тогда
			    ТекстОшибки = " на указанном сервере не запущен FTP-сервер";
			ИначеЕсли Найти(ВРег(ТекстОшибки),ВРег("couldn't resolve host name")) Тогда 	
			    ТекстОшибки = " указанный сервер не доступен";
			КонецЕсли;
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка подключения к FTP серверу <ftp://" + НастройкаДоставки.СерверFTP + ">: " + ТекстОшибки;
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли; 
			
			Возврат Ложь;
		КонецПопытки;
		
		#Если Клиент Тогда
			Состояние("Публикация файла на FTP сервере ...");
		#КонецЕсли
		ПолноеИмяТестовогоФайлаПриемник = ?(Прав(НастройкаДоставки.КаталогОбменаFTP,1)="/",
			НастройкаДоставки.КаталогОбменаFTP,НастройкаДоставки.КаталогОбменаFTP + "/") + ИмяТестовогоФайла;
		Попытка
			FTP.Записать(ПолноеИмяТестовогоФайла, ПолноеИмяТестовогоФайлаПриемник);
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Публикация на сервере FTP тестового файла выполнена");
			КонецЕсли;
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при попытке публикации на сервере FTP тестового файла:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли; 
			
			Возврат Ложь;
		КонецПопытки;
		
		#Если Клиент Тогда
			Состояние("Получение файла с FTP сервера ...");
		#КонецЕсли
		
		Попытка
			FTP.Получить(ПолноеИмяТестовогоФайлаПриемник, ПолноеИмяТестовогоФайла);
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Получение c сервера FTP тестового файла выполнено");
			КонецЕсли;
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при попытке получения c сервера FTP тестового файла:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли; 
			
			Возврат Ложь;
		КонецПопытки;
		
		#Если Клиент Тогда
			Состояние("Удаление файла с FTP сервера ...");
		#КонецЕсли
		
		Попытка
			FTP.Удалить(НастройкаДоставки.КаталогОбменаFTP, ИмяТестовогоФайла);
			Если флСообщать Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + "Тестовый файл удален с сервера FTP!");
			КонецЕсли;
		Исключение
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			стрОшибки = ТекДата + стрОшибки + "Ошибка при попытке удаления c сервера FTP тестового файла:
				|" + ИнформацияОбОшибке().Описание;
				
			Если флСообщать Тогда
				Сообщить(стрОшибки, СтатусСообщения.Внимание);
			КонецЕсли; 
			
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
	// Удаляем локальный тестовый файл
	Попытка
		УдалитьФайлы(ПолноеИмяТестовогоФайла);
	Исключение
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		стрОшибки = ТекДата + стрОшибки + "Ошибка при удалении тестового файла:
			|" + ИнформацияОбОшибке().Описание;
			
		Если флСообщать Тогда
			Сообщить(стрОшибки, СтатусСообщения.Внимание);
		КонецЕсли;
		
		Возврат Ложь;
	КонецПопытки;
	
	#Если Клиент Тогда
		Состояние();
	#КонецЕсли
	
	Возврат Истина;
	
КонецФункции // ПроверитьДоставкуСообщения()

// Процедура полное имя файла разбивает на путь в файлу и имя самого файла
//
// Параметры
//  ПолноеИмяФайла  – Строка, содержащая полное имя файла на диске.
//  ИмяКаталога  – Строка, содержащая путь к каталогу файла на диске.
//  ИмяФайла     – Строка, содержащая имя файла, без имени каталога.
//
Процедура ПолучитьКаталогИИмяФайла(Знач ПолноеИмяФайла, ИмяКаталога, ИмяФайла) Экспорт
	
	// находим последний с конца "\" все что до него - это путь, после - имя
	НомерПозиции = СтрДлина(ПолноеИмяФайла);
	Пока НомерПозиции <> 0 Цикл
		
		Если Сред(ПолноеИмяФайла, НомерПозиции, 1) = "\" Тогда
			
			ИмяКаталога = Сред(ПолноеИмяФайла, 1, НомерПозиции - 1);
			ИмяФайла = Сред(ПолноеИмяФайла, НомерПозиции + 1);
			Возврат;
			
		КонецЕсли;
		
		НомерПозиции = НомерПозиции - 1;
		
	КонецЦикла;
	
	// так и не нашли слешей, значит все- это имя файла
	ИмяФайла = ПолноеИмяФайла;
	ИмяКаталога = "";
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РЕГИСТРАЦИИ ИЗМЕНЕНИЙ

// Получает доступные узлы обмена для указанного плана обмена (массив),
// исключая текущий узел, в соответствии со структурой информационных баз
//
// Параметры
//	ПланОбменаМетаданные - Строка. Имя плана обмена
//
// Возвращаемое значение:
//   Массив. Узлы обмена
//
Функция ПолучитьУзлыОбмена(ПланОбменаИмя) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	" + ПланОбменаИмя + ".Ссылка КАК УзелОбмена,
	|	" + ПланОбменаИмя + ".ИнформационнаяБаза КАК УзелИБ,
	|	ТЧПодразделения.Подразделение.Родитель КАК Подразделение
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураИнформационныхБаз КАК СтруктураИБ
	|		ПО  " + ПланОбменаИмя + ".ИнформационнаяБаза = СтруктураИБ.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураИнформационныхБаз.Подразделения КАК ТЧПодразделения
	|			ПО СтруктураИБ.Ссылка = ТЧПодразделения.Ссылка
	|ГДЕ
	|	" + ПланОбменаИмя + ".Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ПланыОбмена[ПланОбменаИмя].ЭтотУзел());
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Возврат ТЗ;
	
КонецФункции  // ПолучитьУзлыОбмена()

// Получает доступные узлы обмена для указанного плана обмена (массив),
// исключая текущий узел, без учета структуры информационных баз
//
// Параметры
//	ПланОбменаМетаданные - Строка. Имя плана обмена
//
// Возвращаемое значение:
//   Массив. Узлы обмена
//
Функция ПолныйСписокУзловОбмена(ПланОбменаИмя) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	" + ПланОбменаИмя + ".Ссылка КАК УзелОбмена
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|ГДЕ
	|	" + ПланОбменаИмя + ".Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ПланыОбмена[ПланОбменаИмя].ЭтотУзел());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УзелОбмена");
	
КонецФункции // ПолныйСписокУзловОбмена()

// Обработчик ручной регистрации документа и его движений
// Вызывается при любой записи объекта, включая запись при загрузке сообщения обмена
//
// Параметры
//	ЭтотОбъект - ДокументОбъект/Удаление объекта. Документ, для которого регистрируются изменения
//		или объект УдалениеОбъекта
//  ДокументОбъект - ДокументОбъект. Документ в случае, если первый параметр - УдалениеОбъекта
//  ДоступныеУзлыРегистрацииИзменений - Массив. Используется для возврата уже готового массива узлов в
//  вызывающую процедуру
//
// Возвращаемое значение:
//   Булево. Флаг успешной регистрации
//
Функция РегистрацияИзменений(ЭтотОбъект, ЭтоУдаление = Ложь, МассивУзловПолучателей = Неопределено,
	ДопМассивПодразделений = Неопределено, УзелОтправитель = НЕОПРЕДЕЛЕНО,ПланОбменаМетаданные = Неопределено,
	МетаданныеОбъекта = Неопределено,МетаданныеДвижений = Неопределено,РежимЗаписи = Неопределено) Экспорт
	
	Если ПланОбменаМетаданные = Неопределено Тогда
 		ПланОбменаМетаданные = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Метаданные();
	КонецЕсли;
	ПланОбменаМетаданныеСостав = ПланОбменаМетаданные.Состав;
	Если МетаданныеОбъекта = Неопределено Тогда
		Если ЭтоУдаление Тогда
			МетаданныеОбъекта  = ЭтотОбъект.Ссылка.Метаданные();
		Иначе
			МетаданныеОбъекта  = ЭтотОбъект.Метаданные();
		КонецЕсли;
	КонецЕсли;
	Если РежимЗаписи = Неопределено Тогда
	    РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли; 

	МассивУзловПолучателей = Новый Массив;
	Рез = Ложь;	
	
	// кэша здесь нет - это одиночная регистрация документа - определяем точечно
	ЭлементСостава = ПланОбменаМетаданныеСостав.Найти(МетаданныеОбъекта);
	Если ЭлементСостава = Неопределено Тогда // нет такого вида объекта в плане обмена - это бред
		СтатусАвторегистрации = Истина; // но подстрахуемся и все равно отправим этот бред получателю
	Иначе
		СтатусАвторегистрации = (ЭлементСостава.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить);
	КонецЕсли;
	
	// Если Авторегистрация - здесь больше делать нечего, система сама зарегистрирует
	Если СтатусАвторегистрации Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка                                //ДоступныеУзлыРегистрацииИзменений(ЭтотОбъект,ДопМассивПодразделений,ПланОбменаМетаданные,МетаданныеОбъекта,ЭтоУдаление);
		МассивУзловПолучателей = ПолучитьМассивУзловОбмена(ЭтотОбъект,ДопМассивПодразделений,,ПланОбменаМетаданные,МетаданныеОбъекта,ЭтоУдаление);
	Исключение
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		СтрСообщения = "Ошибка при получении массива узлов для регистрации изменений: " + ТекстОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " + Символы.ВК
		+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
		#Если Клиент Тогда
			Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		#КонецЕсли
		ЗаписьЖурналаРегистрации("Обмен.РегистрацияИзменений", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Рез = Ложь;
	КонецПопытки;
	
	Если  (МассивУзловПолучателей <> Неопределено) И (МассивУзловПолучателей.Количество() > 0) Тогда
		// Исключим из списка получателей узел-отправитель
		Если ЗначениеЗаполнено(УзелОтправитель) Тогда
			Инд = МассивУзловПолучателей.Найти(УзелОтправитель);
			Если Инд <> неопределено Тогда
				МассивУзловПолучателей.Удалить(Инд);
			КонецЕсли; 
		КонецЕсли;
		Если МассивУзловПолучателей.Количество()>0 Тогда
			Попытка
				Если ЭтоУдаление Тогда
					ПланыОбмена.ЗарегистрироватьИзменения(МассивУзловПолучателей, Новый УдалениеОбъекта(ЭтотОбъект.Ссылка));
				Иначе
					ПланыОбмена.ЗарегистрироватьИзменения(МассивУзловПолучателей, ЭтотОбъект);
				КонецЕсли;
				Рез = Истина;
			Исключение
				ТекстОшибки = ИнформацияОбОшибке().Описание;
				СтрСообщения = "Ошибка при регистрации изменений: " + ТекстОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " + Символы.ВК
				+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
				#Если Клиент Тогда
					Сообщить(ТекстОшибки,СтатусСообщения.Важное);
				#КонецЕсли
				ЗаписьЖурналаРегистрации("Обмен.РегистрацияИзменений", УровеньЖурналаРегистрации.Ошибка,,, СтрСообщения);
				Рез = Ложь;
			КонецПопытки;
		Иначе
			Рез = Истина;
			Возврат Рез;
		КонецЕсли;
	Иначе
		Рез = Истина;
		Возврат Рез;
	КонецЕсли;
	
	Если Рез Тогда
		// выполним регистрацию изменений для движений регистров 
		МассивЗаполнен = Ложь;
		ДвигаемыеРегистры = Неопределено;
		Если ТипЗнч(ДвигаемыеРегистры) = Тип("Массив") Тогда
			Если ДвигаемыеРегистры.Количество() > 0 Тогда
				МассивЗаполнен = Истина;
			КонецЕсли; 
		КонецЕсли;
		// на случай, если забудут прописать получение ожидаемых движений
		Если НЕ МассивЗаполнен Тогда
			ДвигаемыеРегистры = ЭтотОбъект.Движения;
		КонецЕсли;
		Если МетаданныеДвижений = Неопределено Тогда
			// если нам не передали определяем самостоятельно
			Для каждого НаборЗаписей из ДвигаемыеРегистры Цикл
				МетаданныеРегистр = НаборЗаписей.Метаданные();
				// отсечем сразу, чтобы потом не обращаться лишний раз к процедуре..
				Если (ПланОбменаМетаданныеСостав.Найти(МетаданныеРегистр) = неопределено) Тогда
					Продолжить; // нет в составе плана обмена
				КонецЕсли;
				Если ЭтоУдаление Тогда
					// При удалении объекта, если у нас уже были зарегистрированы его движения
					//правильнее будет их отменить - пусть в сообщении отправится одно лишь удаление объекта
					ПланыОбмена.УдалитьРегистрациюИзменений(МассивУзловПолучателей,НаборЗаписей);
				Иначе
					ПланыОбмена.ЗарегистрироватьИзменения(МассивУзловПолучателей,НаборЗаписей);
					//Для Каждого УзелПолучатель ИЗ МассивУзловПолучателей Цикл
					//	НаборЗаписей.ОбменДанными.Получатели.Добавить(УзелПолучатель);
					//КонецЦикла;
					//Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
					//	НаборЗаписей.Записывать = Ложь;
					//	НаборЗаписей.Записать();
					//КонецЕсли;	
				КонецЕсли;
			КонецЦикла;
			// используем кэш
		Иначе
			Для Инд = 0 По МетаданныеДвижений.ВГраница() Цикл
				МетаданныеРегистр = МетаданныеДвижений[Инд];
				НаборЗаписей	  = ДвигаемыеРегистры.Найти(МетаданныеРегистр.Имя); 
				Если НаборЗаписей = неопределено Тогда
					Продолжить;
				КонецЕсли; 
				Если ЭтоУдаление Тогда
					// При удалении объекта, если у нас уже были зарегистрированы его движения
					//правильнее будет их отменить - пусть в сообщении отправится одно лишь удаление объекта
					ПланыОбмена.УдалитьРегистрациюИзменений(МассивУзловПолучателей,НаборЗаписей);
				Иначе
					ПланыОбмена.ЗарегистрироватьИзменения(МассивУзловПолучателей,НаборЗаписей);
					//Для Каждого УзелПолучатель ИЗ МассивУзловПолучателей Цикл
					//	НаборЗаписей.ОбменДанными.Получатели.Добавить(УзелПолучатель);
					//КонецЦикла;
					//Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
					//	НаборЗаписей.Записывать = Ложь;
					//	НаборЗаписей.Записать();
					//КонецЕсли;	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

		
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции  // РегистрацияИзменений()

// Обработчик ручной регистрации набора записей регистра сведений
// Вызывается при любой записи регистра, включая запись при загрузке сообщения обмена
//
// Параметры
//	ЭтотОбъект 				- РегистрСведений.НаборЗаписей. Набор записей, для которого регистрируются изменения
//	ПланОбменаИмя			- Строка. Имя плана обмена
//  МассивУзловПолучателей 	- Массив. Используется для возврата уже готового массива узлов в вызывающую процедуру
//
// Возвращаемое значение:
//   Булево. Флаг успешной регистрации
//
Функция РегистрацияИзмененийНабораЗаписей(ЭтотОбъект, ЗначенияИзмерений, ПланОбменаИмя,МассивУзловПолучателей = Неопределено) Экспорт
	
	РезультатРегистрации = Ложь;
	
	// для регистрации набора записей независимого регистра необходим
	// отбор по измерениям с признаком "Основной отбор"
	//ТипЗначенияОбъекта = ТипЗнч(ЭтотОбъект);
	//МетаданныеРегистра = Метаданные.НайтиПоТипу(ТипЗначенияОбъекта);
	//
	//Измерения = МетаданныеРегистра.Измерения;
	//СтрокаИзмерения = "";
	//Для Каждого ЗначениеИзмерения Из Измерения Цикл
	//	Если НЕ ЗначениеИзмерения.ОсновнойОтбор Тогда
	//		Продолжить;
	//	КонецЕсли; 
	//	СтрокаИзмерения = СтрокаИзмерения + "," + ЗначениеИзмерения.Имя;
	//КонецЦикла;
	//СтрокаИзмерения = Сред(СтрокаИзмерения, 2);
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Отбор.Объект.Значение) 
		И ЭтотОбъект.Отбор.Объект.Использование = Истина Тогда
		// если заполнено измерение Объект (документы Событие, Заказ-наряд, Заявка на ремонт)
		// регистрируем выборочно в соответствии с ведущим объектом (документом)

		МассивУзловПолучателей = ПолучитьМассивУзловОбмена(ЭтотОбъект.Отбор.Объект.Значение,,,,);
		
	Иначе
		// иначе необходимо обеспечить регистрацию во все узлы (как при авторегистрации)
        МассивУзловПолучателей = ПолныйСписокУзловОбмена(ПланОбменаИмя);
		
	КонецЕсли;
	
	Если  ЗначениеЗаполнено(МассивУзловПолучателей) Тогда
		
		// Исключим из списка получателей узел-отправитель
		УзелОтправитель = ЭтотОбъект.ОбменДанными.Отправитель;
		Если ЗначениеЗаполнено(УзелОтправитель) Тогда
			Инд = МассивУзловПолучателей.Найти(УзелОтправитель);
			Если Инд <> неопределено Тогда
				МассивУзловПолучателей.Удалить(Инд);
			КонецЕсли; 
		КонецЕсли;
		
	КонецЕсли;
	
	Если МассивУзловПолучателей.Количество() = 0 Тогда
		Возврат РезультатРегистрации;
	КонецЕсли;
   	
	//НаборЗаписей = ЭтотОбъект.Выгрузить();
	//НаборЗаписей.Свернуть(СтрокаИзмерения);
	//Если НаборЗаписей.Количество() = 0 Тогда
	//	//НоваяСтрока = НаборЗаписей.Добавить();
	//	РезультатРегистрации = Истина;
	//	Возврат РезультатРегистрации;
	//КонецЕсли; 
	
	Для каждого ТекСтрока Из ЗначенияИзмерений Цикл
		
		Для каждого ТекКолонка Из ЗначенияИзмерений.Колонки Цикл
			//Если ЭтотОбъект.Отбор[ТекКолонка.Имя].Использование
			//	И ЗначениеЗаполнено(ЭтотОбъект.Отбор[ТекКолонка.Имя].Значение) Тогда
			//	Продолжить;
			//КонецЕсли; 
			ЭтотОбъект.Отбор[ТекКолонка.Имя].Значение 		= ТекСтрока[ТекКолонка.Имя];
			ЭтотОбъект.Отбор[ТекКолонка.Имя].ВидСравнения 	= ВидСравнения.Равно;
            ЭтотОбъект.Отбор[ТекКолонка.Имя].Использование 	= Истина;
		КонецЦикла; 
		
		Попытка
			// регистр независимый, поэтому пустой набор также регистрируем (а не удаляем регистрацию)
			ПланыОбмена.ЗарегистрироватьИзменения(МассивУзловПолучателей, ЭтотОбъект);
			РезультатРегистрации = Истина;
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
			СтрСообщения = "Ошибка при регистрации изменений: " + ТекстОшибки 
				+ Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " + Символы.ВК
				+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
			Сообщить(ТекстОшибки,СтатусСообщения.Важное);
			ЗаписьЖурналаРегистрации("Обмен.РегистрацияИзменений", УровеньЖурналаРегистрации.Ошибка,,, СтрСообщения);
			РезультатРегистрации = Ложь;
		КонецПопытки;
	
	КонецЦикла; 
    	
	Возврат РезультатРегистрации;

КонецФункции

// Получает различные значения измерений набора записей регистра в виде
// таблицы значений для регистрации изменений
//
Функция ПолучитьПоНаборуЗаписейРазличныеЗначенияИзмерений(ЭтотОбъект) Экспорт
	
	ТипЗначенияОбъекта = ТипЗнч(ЭтотОбъект);
	МетаданныеРегистра = Метаданные.НайтиПоТипу(ТипЗначенияОбъекта);

	Измерения = МетаданныеРегистра.Измерения;
	СтрокаИзмерения = "";
	Для Каждого ЗначениеИзмерения Из Измерения Цикл
		Если НЕ ЗначениеИзмерения.ОсновнойОтбор Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаИзмерения = СтрокаИзмерения + "," + ЗначениеИзмерения.Имя;
	КонецЦикла;
	СтрокаИзмерения = Сред(СтрокаИзмерения, 2);
	ЗначенияИзмерений = ЭтотОбъект.Выгрузить();
	ЗначенияИзмерений.Свернуть(СтрокаИзмерения);
	
	Возврат ЗначенияИзмерений;
	
КонецФункции


// Получает массив узлов обмена для миграции документа
//
// Параметры
//	ЭтотОбъект - ДокументОбъект/УдалениеОбъекта. Документ, для которого регистрируются изменения
//		или объект УдалениеОбъекта
//  ПланОбменаИмя - Строка. Имя плана обмена
//	НастройкаМиграции 	- СправочникСсылка.НастройкиДоставки. Настройка
//	ПарамПодразделение	- НЕОПРЕДЕЛЕНО (взять из объекта-документа), СправочникСсылка.ПодразделенияКомпании,
//	МАССИВ ссылок типа СправочникСсылка.ПодразделенияКомпании
//
// Возвращаемое значение:
//   Массив. Массив доступных узлов миграции
//
Функция ПолучитьМассивУзловОбмена(ЭтотОбъект,ПарамПодразделение = Неопределено, 
	Знач НастройкаМиграции = Неопределено, ПланОбменаМетаданные = НЕОПРЕДЕЛЕНО, МетаданныеОбъекта = НЕОПРЕДЕЛЕНО, ЭтоУдаление = Ложь) Экспорт
	
	Если ПланОбменаМетаданные = Неопределено Тогда
		ПланОбменаМетаданные = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Метаданные();
	КонецЕсли;	
	
	ПланОбменаИмя = ПланОбменаМетаданные.Имя;	
	Если МетаданныеОбъекта = НЕОПРЕДЕЛЕНО Тогда
		Если НЕ ЭтоУдаление Тогда
			МетаданныеОбъекта = ЭтотОбъект.Метаданные();
		Иначе
			МетаданныеОбъекта = ЭтотОбъект.Ссылка.Метаданные();
		КонецЕсли;
	КонецЕсли;
	
	Рез = Истина;
	
	// SUIG+ документ ПоступлениеТовара, который является партией отриц. остатков,
	// должен мигрировать вместе с константой независимо от настроек правил его миграции
	// всегда - полная миграция
	ПолнаяМиграция = Ложь;
	ЭтоДокумент = Ложь;
	Если Найти(МетаданныеОбъекта.ПолноеИмя(),"Документ") > 0 Тогда
	    ЭтоДокумент = Истина;
	КонецЕсли;
	Если ЭтоДокумент Тогда
		ТипДокСсылка = Тип("ДокументСсылка.ПоступлениеТоваров");
	    ДанныеСсылка = ЭтотОбъект.Ссылка; // Ссылка от Ссылка - Ссылка, ссылка от Объекта или УдаленияОбъекта - тоже Ссылка
		ТипДанных = ТипЗнч(ДанныеСсылка);
		Если ТипДанных = ТипДокСсылка Тогда
			ПартияОтрицательныхОстатков = Константы.ПартияТоваровОтрицательныхОстатков.Получить();
			Если НЕ ПартияОтрицательныхОстатков.Пустая() Тогда
				Если ПартияОтрицательныхОстатков = ДанныеСсылка Тогда
					ПолнаяМиграция = Истина;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли; 
	Если ПолнаяМиграция Тогда
		НастройкаМиграции = Справочники.ПравилаМиграцииИДоступа.ПолнаяМиграция;
	КонецЕсли;
	// SUIG-
	
	Если НастройкаМиграции = Неопределено Тогда
		Попытка
			Права_ = ЭтотОбъект.Права;
		Исключение
			Права_ = Неопределено;
		КонецПопытки;
		
		НастройкаМиграции = обПраво("Правило миграции документа " + МетаданныеОбъекта.Имя, Права_,Ложь);
		Если НастройкаМиграции = Неопределено Тогда
	 		НастройкаМиграции = Справочники.ПравилаМиграцииИДоступа.ПолнаяМиграция;
		КонецЕсли;
	КонецЕсли;
	
    ВариантНастройки = НастройкаМиграции.ВариантНастройки;
	
	МассивУзлов  = Новый Массив; // Создали будущий результат
	// Миграция запрещена  - пустой список узлов-получателей
	Если ВариантНастройки = Перечисления.СпособыМиграцииОбъектов.МиграцияЗапрещена Тогда
	    Возврат МассивУзлов;
	КонецЕсли; 
	
	// сразу проверка на необходимость миграции непров. документа
	// причем если документ зафиксирован в константе он все равно мигрирует
	Если НЕ ПолнаяМиграция И ЭтоДокумент Тогда
		Если НЕ ЭтоУдаление Тогда
			Если НастройкаМиграции.МиграцияТолькоПроведенных И НЕ ЭтотОбъект.Проведен Тогда
				Возврат Новый Массив;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
	ТЗУзлыОбмена = Новый ТаблицаЗначений;
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ПланОбменаСсылка.УдаленныеПодразделения"));
	ТЗУзлыОбмена.Колонки.Добавить("УзелОбмена",Новый ОписаниеТипов(МассивТипов));
		
	// Полная миграция - никаких ограничений по миграции нет
	Если ВариантНастройки = Перечисления.СпособыМиграцииОбъектов.ПолнаяМиграция Тогда
	    Возврат ПолныйСписокУзловОбмена(ПланОбменаИмя);
	КонецЕсли; 
	
	ЭтотУзел = ПланыОбмена[ПланОбменаИмя].ЭтотУзел();
	ТекУзелИБ = ЭтотУзел.ИнформационнаяБаза;
	
	ТипЗначПарамПодразделения = ТипЗнч(ПарамПодразделение);
	Если ТипЗначПарамПодразделения = Тип("Массив") Тогда
		МассивПодразделений = ПарамПодразделение;
	Иначе
		МассивПодразделений = Новый Массив;
	КонецЕсли;
	
	Если (ПарамПодразделение = Неопределено) ИЛИ (ТипЗначПарамПодразделения = Тип("Массив")) Тогда
		// Определим подразделение документа
		Если МетаданныеОбъекта.Реквизиты.Найти("ПодразделениеКомпании") = Неопределено Тогда
			// Дополнительные подразделения не имеют смысла т.к. пустая ссылка значит ЛЮБОЕ подразделение
			МассивПодразделений = Новый Массив; // игнорируем  ДОПОЛНИТЕЛЬНЫЕ подразделения
			ПодразделениеДокумента = Справочники.ПодразделенияКомпании.ПустаяСсылка();
			МассивПодразделений.Добавить(ПодразделениеДокумента);
		Иначе
			ПодразделениеДокумента = ЭтотОбъект.Ссылка.ПодразделениеКомпании;
			МассивПодразделений.Добавить(ПодразделениеДокумента);
		КонецЕсли;
	ИначеЕсли ТипЗначПарамПодразделения = Тип("СправочникССылка.ПодразделенияКомпании") Тогда
		МассивПодразделений.Добавить(ПарамПодразделение);
	Иначе
		Возврат МассивУзлов;
	КонецЕсли;
	
	Для Каждого ТекПодразделение ИЗ МассивПодразделений Цикл
		Если ВариантНастройки = Перечисления.СпособыМиграцииОбъектов.МиграцияПоПодразделениямУзлов Тогда
			Если ЗначениеЗаполнено(ТекПодразделение) Тогда
				// Получим ссылки на узлы, где есть подразделение документа
				Результат = ПолучитьУзлыОбменаПоПодразделению(ТекПодразделение);
				Если НЕ Результат.Пустой() Тогда
				    Выборка = Результат.Выбрать();
					Пока Выборка.Следующий() Цикл
						Если НЕ Выборка.ПроверитьИерархию // Не нужно проверять значит подразделение как раз равно подразделению объекта проверки
						 ИЛИ НЕ ЗначениеЗаполнено(Выборка.Подразделение)
						 ИЛИ (ТекПодразделение.ПринадлежитЭлементу(Выборка.Подразделение)
						 ИЛИ ТекПодразделение = Выборка.Подразделение) Тогда
							НС = ТЗУзлыОбмена.Добавить();
							НС.УзелОбмена = Выборка.УзелОбмена;
						КонецЕсли;
					КонецЦикла; 
				КонецЕсли; 
			КонецЕсли; 
				
		ИначеЕсли ВариантНастройки = Перечисления.СпособыМиграцииОбъектов.ИндивидуальнаяНастройкаПоПодразделениямИУзлам Тогда
			// Получим ссылки на узлы, где есть подразделение документа
			Результат = ПолучитьУзлыОбменаПоПравилуМиграции(НастройкаМиграции,ТекПодразделение,ЭтотУзел.ССылка);
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Пока Выборка.Следующий() Цикл
					НС = ТЗУзлыОбмена.Добавить();
					НС.УзелОбмена = Выборка.УзелОбмена;
				КонецЦикла; 
			КонецЕсли; 
	 			
		КонецЕсли;
	КонецЦикла; 	
	// свернем таблицу значений
	ТЗУзлыОбмена.Свернуть("УзелОбмена");
	МассивУзлов = ТЗУзлыОбмена.ВыгрузитьКолонку("УзелОбмена");
	Возврат МассивУзлов;
	
КонецФункции // ПолучитьМассивУзловОбмена()

// Получает подчиненные узлы обмена для определенного подразделения
//
// Параметры
//	Подразделение - СправочникСсылка.Подразделения. Подразделение объекта
//  ПланОбменаИмя - Строка. Имя плана обмена
//
// Возвращаемое значение:
//   РезультатЗапроса. Содержит подчиненные узлы
//
Функция ПолучитьУзлыОбменаПоПодразделению(Подразделение,ПланОбменаИмя = "") Экспорт
	Если ПланОбменаИмя = "" Тогда
	    ПланОбменаИмя  = "УдаленныеПодразделения";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ " + ПланОбменаИмя + ".Ссылка КАК УзелОбмена,
	|СтруктураИнформационныхБазПодразделения.Подразделение КАК Подразделение,
	|СтруктураИнформационныхБазПодразделения.ВключаяДочерние КАК ПроверитьИерархию
	|
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураИнформационныхБаз.Подразделения КАК СтруктураИнформационныхБазПодразделения
	|		ПО " + ПланОбменаИмя + ".ИнформационнаяБаза = СтруктураИнформационныхБазПодразделения.Ссылка
	|ГДЕ
	|	(СтруктураИнформационныхБазПодразделения.Подразделение = &ВыбПодразделение ИЛИ СтруктураИнформационныхБазПодразделения.ВключаяДочерние = Истина)
	|	И НЕ (" + ПланОбменаИмя + ".Ссылка  = &ЭтотУзел)
	|";
	
	Запрос.УстановитьПараметр("ВыбПодразделение",Подразделение);

	Запрос.УстановитьПараметр("ЭтотУзел",ПланыОбмена[ПланОбменаИмя].ЭтотУзел());
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // ПолучитьДочерниеУзлыОбменаПоПодразделению()

// Получает подчиненные узлы обмена для определенного подразделения
//
// Параметры
//	Подразделение - СправочникСсылка.Подразделения. Подразделение объекта
//  ПланОбменаИмя - Строка. Имя плана обмена
//
// Возвращаемое значение:
//   РезультатЗапроса. Содержит подчиненные узлы
//
Функция ПолучитьУзлыОбменаПоПравилуМиграции(НастройкаМиграции,Подразделение,ТекущийУзел,ПланОбменаИмя = "") Экспорт
	Если ПланОбменаИмя = "" Тогда
	    ПланОбменаИмя  = "УдаленныеПодразделения";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	" + ПланОбменаИмя + ".Ссылка КАК УзелОбмена
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаМиграцииИДоступа.ПодразделенияИнформационныхБаз КАК ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз
	|		ПО " + ПланОбменаИмя + ".ИнформационнаяБаза = ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.ИнформационнаяБаза
	| 		И ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.Ссылка = &ПравилоМиграции
	|		И (ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.Подразделение = &ВыбПодразделение
	|			ИЛИ ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.Подразделение = &ПустоеПодразделение)
	|	И " + ПланОбменаИмя + ".Ссылка <> &ЭтотУзел";
	
	Запрос.УстановитьПараметр("ВыбПодразделение",Подразделение);
	Запрос.УстановитьПараметр("ПустоеПодразделение",Справочники.ПодразделенияКомпании.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЭтотУзел",ТекущийУзел);
	Запрос.УстановитьПараметр("ПравилоМиграции",НастройкаМиграции);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // ПолучитьУзлыОбменаПоПравилуМиграции()

// Возвращает строку из ТЧ Подразделения Правила миграции для проверки права доступа
//
// Параметры
//	НастройкаМиграции - СправочникСсылка.ПравилаМиграцииИДоступа
//	Подразделение - СправочникСсылка.Подразделения. Подразделение объекта
//  ПланОбменаИмя - Строка. Имя плана обмена
//  УзелИБ 			- СправочникСсылка.СтруктураИнформационныхБаз.
//
// Возвращаемое значение:
//   Выборка. Содержит подчиненные узлы
//
Функция НайтиУзелОбменаПоПодразделениюУзла(НастройкаМиграции,Подразделение,УзелОбмена,ПланОбменаИмя = "") Экспорт
	Если ПланОбменаИмя = "" Тогда
	    ПланОбменаИмя  = "УдаленныеПодразделения";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.ЗапретитьПросмотр КАК ЗапретитьПросмотр,
	|	ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.ЗапретитьИзменение КАК ЗапретитьИзменение
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаМиграцииИДоступа.ПодразделенияИнформационныхБаз КАК ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз
	|		ПО " + ПланОбменаИмя + ".ИнформационнаяБаза = ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.ИнформационнаяБаза
	| 		И ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.Ссылка = &ПравилоМиграции
	|		И (ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.Подразделение = &ВыбПодразделение
	|			ИЛИ ПравилаМиграцииИДоступаПодразделенияИнформационныхБаз.Подразделение = &ПустоеПодразделение)
	|		И " + ПланОбменаИмя + ".Ссылка = &УзелОбмена";
	
	Запрос.УстановитьПараметр("ВыбПодразделение",Подразделение);
	Запрос.УстановитьПараметр("ПустоеПодразделение",Справочники.ПодразделенияКомпании.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПравилоМиграции",НастройкаМиграции);
    Запрос.УстановитьПараметр("УзелОбмена",УзелОбмена);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
	    Возврат Новый Структура("ЗапретитьПросмотр,ЗапретитьИзменение",
			Выборка.ЗапретитьПросмотр,Выборка.ЗапретитьИзменение);
	Иначе	
	    Возврат Неопределено;
	КонецЕсли; 
	
КонецФункции // НайтиУзелОбменаПоПодразделениюУзла()

// Получает узел плана обмена, соответствующий узлу ИБ
//
// Параметры:
//	ИБ 				- СправочникСсылка.СтруктураИнформационныхБаз. Информационная база
//  ПланОбменаИмя 	- Строка. Имя плана обмена
// Возвращаемое значение:
//   ПланыОбменыСсылка.УдаленныеПодразделения   – ссылка на узел плана обмена
//
Функция ПолучитьУзелОбмена(ИБ,ПланОбменаИмя = "") Экспорт
	
	Если ПланОбменаИмя = "" Тогда
	    ПланОбменаИмя = "УдаленныеПодразделения";
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	" + ПланОбменаИмя + ".Ссылка
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|ГДЕ
	|	" + ПланОбменаИмя + ".ИнформационнаяБаза = &ИБ";
	Запрос.УстановитьПараметр("ИБ",ИБ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
	    Возврат Выборка.Ссылка;
	Иначе	
		Возврат неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьУзелОбмена()

// Получает узел плана обмена, соответствующий узлу ИБ
//
// Параметры:
//	Подразделение 	- СправочникСсылка.Подразделения. Подразделение документа
//  ПланОбменаИмя 	- Строка. Имя плана обмена
// Возвращаемое значение:
//   ПланыОбменыСсылка.УдаленныеПодразделения. Ссылка на узел плана обмена
//
Функция ПолучитьУзелОбменаПоПодразделению(Подразделение,ПланОбменаИмя="") Экспорт
	
	Если ПланОбменаИмя = "" Тогда
	    ПланОбменаИмя = "УдаленныеПодразделения";
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	" + ПланОбменаИмя + ".Ссылка
	|ИЗ
	|	ПланОбмена." + ПланОбменаИмя + " КАК " + ПланОбменаИмя + "
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураИнформационныхБаз.Подразделения КАК ТЧПодразделения
	|		ПО  " + ПланОбменаИмя + ".ИнформационнаяБаза = ТЧПодразделения.Ссылка ;
	|ГДЕ
	|	ТЧПодразделения.Подразделение = &Подразделение";
	Запрос.УстановитьПараметр("Подразделение",Подразделение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
	    Возврат Выборка.Ссылка;
	Иначе	
		Возврат неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьУзелОбменаПоПодразделению()

// Получает узел информационной базы по подразделению
//
// Параметры
//  Подразделение  – СправочникСсылка.ПодразделенияКомпании. Подразделение документа
//
// Возвращаемое значение:
//   СправочникСсылка.СтруктураИнформационныхБаз - ссылка на ИБ
//
Функция ПолучитьУзелИБПоПодразделению(Подразделение) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтруктураИнформационныхБазПодразделения.Ссылка
	|ИЗ
	|	Справочник.СтруктураИнформационныхБаз.Подразделения КАК СтруктураИнформационныхБазПодразделения
	|ГДЕ
	|	СтруктураИнформационныхБазПодразделения.Подразделение = &Подразделение";
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьУзелИБПоПодразделению()
 
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заносит строку протокола события с указанием вида и важности события
//	Возвращает структуру: ключи элементов - "Успешность" и "Сообщение"
//
//	Параметры:
//		ТипСобытия - Строка - пример "Данные.Изменение"
//		СтрСообщения - Строка - комментарий
//		Данные - Ссылка, набор записей и т.п. - связанные с событием данные
//		Ошибка - Булево - флаг ошибки
//		МестоФиксацииСобытия - Строка - маска мест ведения лога, например "100"
//			(по порядку: 1 - журнал регистрации, 2 - лог-файл)
//		ПолноеИмяЛогФайла - Строка - полное имя файла логов, если используется сохранение в лог-файл
//		АвторСобытия - спр. Пользователь, Строка - Пользователь-автор события или представление автора
//
Функция ЗафиксироватьСобытие(ТипСобытия, СтрСообщения,Знач Данные = "", Знач СтатусСобытия = Неопределено, МестоФиксацииСобытия = "100",
							ЛогФайл = НЕОПРЕДЕЛЕНО, ПолноеИмяЛогФайла = "", Знач АвторСобытия = Неопределено) Экспорт
		
	ТекВремяДата = Формат(ТекущаяДата(), "ДФ = 'гггг.ММ.дд ЧЧ:мм:сс'");
	
	СтатусыСобытий = Перечисления.СтатусыСобытийСистемногоПротокола;
	СтатусСобытия = ?(СтатусСобытия = Неопределено, СтатусыСобытий.Информация, СтатусСобытия);
	СохранятьВЖурналеРегистрации = (Сред(МестоФиксацииСобытия, 1, 1) = "1");
	СохранятьВЛогФайле = (Сред(МестоФиксацииСобытия, 2, 1) = "1") И НЕ ПустаяСтрока(ПолноеИмяЛогФайла);
	
	//ТипСобытия   = СтрЗаменить(ТипСобытия + ?(СтрЧислоВхождений(ТипСобытия, ".") = 1, ".", ""), ".", РаздВЛоге);
	СтрСообщения = СтрЗаменить(СтрСообщения, Символы.ПС, ". ");
	РаздВЛоге = "|";
	СтрокаЛога   = ТекВремяДата + ?(АвторСобытия=Неопределено, "", РаздВЛоге + АвторСобытия)
		+ РаздВЛоге + ТипСобытия + РаздВЛоге + ?(СтатусСобытия = СтатусыСобытий.Ошибка, "error", "ok") + РаздВЛоге + СтрСообщения;
	
	Рез = Новый Структура("Успешность, Сообщение");
	Рез.Сообщение  = СтрокаЛога + Символы.ПС;
	Рез.Успешность = НЕ (СтатусСобытия = СтатусыСобытий.Ошибка);
	
	//журнал регистрации
	Если СохранятьВЖурналеРегистрации Тогда
		Если СтатусСобытия = СтатусыСобытий.Ошибка Тогда
			УровеньЖурнала = УровеньЖурналаРегистрации.Ошибка;
		ИначеЕсли СтатусСобытия = СтатусыСобытий.Предупреждение Тогда
			УровеньЖурнала = УровеньЖурналаРегистрации.Предупреждение;
		ИначеЕсли СтатусСобытия = СтатусыСобытий.Информация Тогда
			УровеньЖурнала = УровеньЖурналаРегистрации.Информация;
		Иначе
			УровеньЖурнала = УровеньЖурналаРегистрации.Примечание;
		КонецЕсли;
		
		МетаданныеОбъекта = неопределено;
		Попытка
			МетаданныеОбъекта = Данные.Метаданные();
			Данные = Данные.Ссылка;
		Исключение 
		КонецПопытки;
		
		ЗаписьЖурналаРегистрации(ТипСобытия, УровеньЖурнала,МетаданныеОбъекта, Данные, СтрСообщения);
	КонецЕсли;
	
	#Если ВнешнееСоединение Тогда
		Возврат Рез;
	#КонецЕсли
	
	//лог-файл
	Если СохранятьВЛогФайле Тогда
		//обновляем
		Если ЛогФайл <> Неопределено Тогда
			Попытка
				ЛогФайл.ЗаписатьСтроку(СтрокаЛога);
			Исключение
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				СтрСообщения = "Ошибка записи в лог-файл """ + ПолноеИмяЛогФайла + """  - " + ОписаниеОшибки();
				Сообщить(ТекДата + СтрСообщения);
				ЗаписьЖурналаРегистрации("Обмен. Запись в лог-файл", УровеньЖурналаРегистрации.Предупреждение,,, СтрСообщения);
				Рез.Сообщение  = Рез.Сообщение + СтрСообщения;
				Рез.Успешность = Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции //ЗафиксироватьСобытие()

// Получает текущую ИБ типа справочник "Структура ИБ"
//
// Параметры:
//  Нет.
//
Функция ТекущаяИнформационнаяБаза() Экспорт
	
	Рез = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().ИнформационнаяБаза;
	Если Рез.Пустая() Тогда
		Рез = Неопределено;
	КонецЕсли;
	Возврат Рез;
	
КонецФункции // ТекущаяИнформационнаяБаза()

// Получает номер сообщения обмена текущего узла с указанным в параметре
//
// Параметры
//  УзелОбмена  – ПланыОбменыСсылка.УдаленныеПодразделения. Ссылка на узел плана обмена
//  Принятое    – Булево. Флаг принятого сообщения
//  ДлинаНомера – Число. Ограничение длины номера сообщения
//
// Возвращаемое значение:
//   Число. Номер принятого или отправленного сообщения
//
Функция НомерСообщенияОбмена(УзелОбмена, Принятое = Истина, ДлинаНомера = 10) Экспорт
	
	Номер = ?(Принятое, УзелОбмена.НомерПринятого + 1, УзелОбмена.НомерОтправленного);
	Номер = Формат(Номер, "ЧЦ=" + ДлинаНомера + "; ЧВН=; ЧГ=0");
	Возврат Номер;
	
КонецФункции // НомерСообщенияОбмена()

// По настройке обмена устанавливает параметр сеанса "Параметры обмена данными"
//
// Параметры
//	НастройкаОбмена 	- СправочникСсылка.НастройкиДоставки. Настройка
//
Процедура УстановитьПараметрыСеансаОбмена(СтруктураПараметровОбмена) Экспорт
	
	ПараметрыСеанса.ПараметрыОбменаДанными = Новый ХранилищеЗначения(СтруктураПараметровОбмена);
	
КонецПроцедуры // УстановитьПараметрыСеансаОбмена()

// По настройке обмена устанавливает параметр сеанса "Параметры обмена данными"
//
// Параметры
//	НастройкаОбмена 	- СправочникСсылка.НастройкиДоставки. Настройка
//
Функция ПолучитьПараметрыСеансаОбмена() Экспорт
	
	Возврат ПараметрыСеанса.ПараметрыОбменаДанными.Получить();
	
КонецФункции // УстановитьПараметрыСеансаОбмена()


// Очищает значение параметра сеанса "Параметры обмена данными"
//
Процедура ОчиститьПараметрыСеансаОбмена() Экспорт
	ПараметрыСеанса.ПараметрыОбменаДанными = Новый ХранилищеЗначения(Новый Структура);
КонецПроцедуры // ОчиститьПараметрыСеансаОбмена()

// Производит удаление скрипта (файла cmd) для обновления конфигурации базы данных
// и загрузке сообщения обмена в случае сбоев при завершении работы пользователей
// особенно в файловом режиме работы
//
// Параметры
//	Результат - Булево. Флаг успешного удаления скрипта
//
Процедура УдалитьСкриптОбновления(Результат = Ложь) Экспорт
	
	// определим местонахождение файла
	ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	КаталогОбмена = КаталогВременныхФайлов();
	
    ИмяСкрипта = "_Update_*.CMD";
	
	НайденыФайлы = НайтиФайлы(КаталогОбмена, ИмяСкрипта);
	Для Каждого ТекФайл Из НайденыФайлы Цикл
		// Пытаемся удалить файл
		Попытка
			УдалитьФайлы(ТекФайл.ПолноеИмя);
			ТекстСообщения = "Файла-скипт обновления конфигурации базы данных " + ТекФайл.ПолноеИмя + " успешно удален"
				" выполнение скрипта остановлено";
			ЗаписьЖурналаРегистрации("Завершение работы пользователей", УровеньЖурналаРегистрации.Информация, , , ТекстСообщения);
			Результат = Истина;
			Возврат;
		Исключение
			ТекстСообщения = "Ошибка при удалении файла-скрипта обновления конфигурации базы данных " + ТекФайл.ПолноеИмя;
			ЗаписьЖурналаРегистрации("Завершение работы пользователей", УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
			Результат = Ложь;
			Возврат;
		КонецПопытки;
	КонецЦикла;
	
	ТекстСообщения = "Файл-скипта обновления конфигурации базы данных не найден";
			ЗаписьЖурналаРегистрации("Завершение работы пользователей", УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);

КонецПроцедуры // УдалитьСкриптОбновления()

// Преобразует текст из досовской кодировки в 1251
//
// Параметры
//	Значение - Строка. Текст для преобразования в Дос-кодировку
//
// Возвращаемое значение:
//   Строка. Преобразованный текст
//
Функция ПреобразоватьТекстДос(Значение) Экспорт
	
	Если ПустаяСтрока(Значение) Тогда
		Возврат "";
	КонецЕсли;
	
	СоответствиеТекста = Новый Соответствие();
	СоответствиеТекста.Вставить("Ђ", "А");
	СоответствиеТекста.Вставить("Ѓ", "Б");
	СоответствиеТекста.Вставить("‚", "В");
	СоответствиеТекста.Вставить("ѓ", "Г");
	СоответствиеТекста.Вставить("„", "Д");
	СоответствиеТекста.Вставить("…", "Е");
	СоответствиеТекста.Вставить("р", "Ё");
	СоответствиеТекста.Вставить("†", "Ж");
	СоответствиеТекста.Вставить("‡", "З");
	СоответствиеТекста.Вставить("€", "И");
	СоответствиеТекста.Вставить("‰", "Й");
	СоответствиеТекста.Вставить("Љ", "К");
	СоответствиеТекста.Вставить("‹", "Л");
	СоответствиеТекста.Вставить("Њ", "Ь");
	СоответствиеТекста.Вставить("Ќ", "Н");
	СоответствиеТекста.Вставить("Ћ", "О");
	СоответствиеТекста.Вставить("Џ", "П");
	СоответствиеТекста.Вставить("ђ", "Р");
	СоответствиеТекста.Вставить("‘", "С");
	СоответствиеТекста.Вставить("’", "Т");
	СоответствиеТекста.Вставить("“", "У");
	СоответствиеТекста.Вставить("”", "Ф");
	СоответствиеТекста.Вставить("•", "Х");
	СоответствиеТекста.Вставить("–", "Ц");
	СоответствиеТекста.Вставить("—", "Ч");
	СоответствиеТекста.Вставить("", "Ш");
	СоответствиеТекста.Вставить("™", "Щ");
	СоответствиеТекста.Вставить("љ", "Ь");
	СоответствиеТекста.Вставить("›", "Ы");
	СоответствиеТекста.Вставить("њ", "Ъ");
	СоответствиеТекста.Вставить("ќ", "Э");
	СоответствиеТекста.Вставить("ћ", "Ю");
	СоответствиеТекста.Вставить("џ", "Я");

	// переведем кириллицу DOS в Windows 
	Для Каждого ТекСимвол Из СоответствиеТекста Цикл
		Значение = СтрЗаменить(Значение, ТекСимвол.Ключ, ТекСимвол.Значение);
	КонецЦикла;
	
	Возврат Значение; 
	
КонецФункции //ПреобразоватьТекстДос()

// Выполняет проверку на заполненность Родителя для текущей ИБ
// и при необходимости присваивает Родителя
//
// Параметры
//	Нет
//
// Возвращаемое значение:
//  Булево. Флаг успешного завершения проверки
//
Функция ПроверитьИерархиюИБ() Экспорт
	
	Рез = Ложь; // пока все плохо
	ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	ПрефиксЭтогоУзла = ЭтотУзел.Префикс;
	ГлавныйУзел = ПланыОбмена.ГлавныйУзел();
	
	// выберем все подчиненные текущему узлы
	КолвоПодчиненных = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдаленныеПодразделения.Ссылка КАК УзелОбмена
	|ИЗ
	|	ПланОбмена.УдаленныеПодразделения КАК УдаленныеПодразделения
	|ГДЕ
	|	УдаленныеПодразделения.Ссылка <> &ЭтотУзел
	|	И УдаленныеПодразделения.Ссылка <> &ГлавныйУзел";
	Запрос.УстановитьПараметр("ЭтотУзел",ЭтотУзел);
	Запрос.УстановитьПараметр("ГлавныйУзел",ГлавныйУзел);
	Выборка = Запрос.Выполнить().Выбрать();
	// ну нет у него подчиненных узлов, и сам он и его родитель ИБ
	// приедут только по завершении обмена с родителем
	Если Выборка.Количество() = 0 Тогда
	    Возврат Истина;
	КонецЕсли; 
	Пока Выборка.Следующий() Цикл
		ТекУзел = Выборка.УзелОбмена;
		
		// создаем ИБ подчиненного узла
		ТекУзелИБ = Неопределено;
		// в этой же функции идет проверка соответ. узла и ИБ
		Рез = ПроверитьТекущуюИБ(ТекУзел,ТекУзелИБ);
		Если НЕ Рез Тогда
		   Возврат Ложь;
		КонецЕсли; 
		
		// ИБ полностью заполнена, осталось найти родителя
		// берем его как ИБ этого узла
		// для нецентральных родителей это функция будет вызвана еще раз после загрузки сообщения обмена,
		// вместе с которым приедет родитель,и, в случае многоуровневой структуры узлов и родители верхних уровней
		ИзменитьРодителя = Ложь;
		Если ЗначениеЗаполнено(ЭтотУзел) Тогда
			// проверка - в ситуации "натягивания" конфигурации в подчиненном узле
			// не требуется менять родителя для фактического родителя этой ИБ
			ТекИБРодитель = ЭтотУзел.ИнформационнаяБаза;
			Если ТекУзелИБ.Родитель.Пустая() Тогда
				// ТекУзелИБ - это только что созданный узел ИБ (подчиненный этому узлу)
				// ТекИБРодитель - узел ИБ текущего узла обмена
				// - соот-но в случае отсутствия ручного "открепления" главного узла в текущей инф.базе
				// родитель этого узла ИБ никогда не будет равен подчиненному узлу
				// и требуется присвоение родителя для свежесозданного
				// подчиненного узла
				// - при выполнении открепления (в базе, где иерархия ИБ уже была создана ранее)
				// нельзя верить только узлам обмена (плану обмена) и нужно пропустить присвоение родителя
				// для узла, который перестал быть главным узлом в текущей ИБ после открепления
				// Пример: тек.база - Узел1, главный узел - ГлавУзел
				// после открепление узел уже не главный - соот-но попадет в данную выборку
				// но если посмотреть в структуру ИБ - родитель Узел1 по-прежнему = ГлавУзел и его пропустим
				Если НЕ ТекИБРодитель.Родитель = ТекУзелИБ Тогда
					ИзменитьРодителя = Истина;
				Иначе
					Рез = Истина; // пока все идет нормально
					СтрСообщения = "При проверке иерархии ИБ обнаружено несоответствие иерархии узлов плана обмена <" + ТекИБРодитель.Наименование + ">"
						+ " и <" + ТекУзелИБ.Наименование + ">. Возможно было произведено ""снятие"" главного узла для текущего узла"
						+ Символы.ПС + "<" + ТекИБРодитель.Наименование + ">. В данном случае необходимо установить узел <" + ТекУзелИБ.Наименование + ">"
						+ " в качестве главного узла"; 
					#Если Клиент Тогда
						ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
						Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Внимание);
					#КонецЕсли 
					ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 	
		
		Если ИзменитьРодителя Тогда
			ТекУзелИБОбъект = ТекУзелИБ.ПолучитьОбъект();
			ТекУзелИБОбъект.Родитель = ТекИБРодитель;
			Попытка
				ТекУзелИБОбъект.ОбменДанными.Загрузка = Истина;
				ТекУзелИБОбъект.Записать();
				Рез = Истина; // пока все идет нормально
				СтрСообщения = "Присвоен родитель <" + ТекИБРодитель.Наименование + "> для информационной базы <" + ТекУзелИБ.Наименование + ">";
				#Если Клиент Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Информация);
				#КонецЕсли 
				ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , СтрСообщения);
				
			Исключение
				СтрСообщения = "Ошибка проверки иерархии для информационной базы <" + ТекУзелИБ.Наименование + ">: " 
				+ ИнформацияОбОшибке().Описание + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", "
				+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")";
				#Если Клиент Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
				#КонецЕсли 
				ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
				Рез = Ложь;
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла; // Пока Выборка.Следующий()

	
	// SUIG - возможна ситуация, когда существует узел ИБ, который в соответствии со своим родителем
	// должен соот-ть доступному узлу обмена (РБД), но данного узла по различным причинам не существует
	// то есть структура Иб вроде как правильная и у узла есть доступные ему узлы, 
	// но судя по плану обмена доступного узла нет (может его админ по ошибке грохнул)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтруктураИБ.Ссылка КАК УзелИБ,
	|	СтруктураИБ.Родитель КАК РодительУзелИБ,
	|	ЕСТЬNULL(УдаленныеПодразделения.Ссылка,&ПустойУзел) КАК УзелОбмена
	|ИЗ
	|	Справочник.СтруктураИнформационныхБаз КАК СтруктураИБ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.УдаленныеПодразделения КАК УдаленныеПодразделения
	|		ПО (УдаленныеПодразделения.ИнформационнаяБаза = СтруктураИБ.Ссылка)
	|ГДЕ
	|	 УдаленныеПодразделения.Ссылка ЕСТЬ NULL
	|";
	Запрос.УстановитьПараметр("ПустойУзел",ПланыОбмена.УдаленныеПодразделения.ПустаяСсылка());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекУзелИБ = Выборка.УзелИБ;
		РодительУзелОбмена = ПланыОбмена.УдаленныеПодразделения.НайтиПоРеквизиту("Префикс",Выборка.РодительУзелИБ.Префикс);
		УзелДоступен = Ложь;
		
		Если НЕ ЗначениеЗаполнено(РодительУзелОбмена) Тогда
			// если не заполнен узел-родитель - это центральный
			УзелДоступен = Истина;
		Иначе
			// может быть и родителя затерли ненароком ... кривыми ручками...
			Если РодительУзелОбмена = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел() Тогда
			    // перед нами - подчиненный узел этого узла
				УзелДоступен = Истина;
			ИначеЕсли РодительУзелОбмена = ПланыОбмена.ГлавныйУзел() Тогда	
			    // перед нами - этот узел
			КонецЕсли; 
		КонецЕсли; 
		
		Если УзелДоступен  Тогда
			Если ЗначениеЗаполнено(ПланыОбмена.ГлавныйУзел()) Тогда
				// Проверка - проверяемый узел ИБ является родителем родителя текущей ИБ,
				// соответственно с проверяемым узлом напрямую обмена нет
				Если ПланыОбмена.ГлавныйУзел().ИнформационнаяБаза.Родитель = ТекУзелИБ Тогда
					УзелДоступен = Ложь;
				КонецЕсли;
			Иначе
				// На случай снятия главного узла
				УзелДоступен = Ложь;
			КонецЕсли;
		КонецЕсли;

		
		Если НЕ УзелДоступен Тогда
			// узел вне доступа в данном узел РБД
		    Продолжить;
		КонецЕсли; 
		
		ТекУзелОбмена = ПланыОбмена.УдаленныеПодразделения.НайтиПоРеквизиту("Префикс",ТекУзелИБ.Префикс); 
	
		Если ЗначениеЗаполнено(ТекУзелОбмена) Тогда
		    Попытка
                ТекУзелОбменаОбъект = ТекУзелОбмена.ПолучитьОбъект();
				ТекУзелОбменаОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина);
				ТекУзелОбменаОбъект.ИнформационнаяБаза = ТекУзелИБ;
			    ТекУзелОбменаОбъект.Записать();
				Рез = Истина; // пока все идет нормально
				СтрСообщения = "Исправлена информационная база для узла <" + ТекУзелОбмена.Наименование + ">";
				#Если Клиент Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Информация);
				#КонецЕсли 
				ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
			Исключение
				СтрСообщения = "Ошибка при записи узла <" + ТекУзелИБОбъект.Наименование + ">: " 
					+ ИнформацияОбОшибке().Описание + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", "
					+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")";
				#Если Клиент Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
				#КонецЕсли 
				ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
				Рез = Ложь;
			КонецПопытки;
		Иначе
			Рез = Ложь;
			Попытка
				ТекУзелИБОбъект = ТекУзелИБ.ПолучитьОбъект();
				ТекУзелИБОбъект.ОбменДанными.Загрузка = Истина;
				ТекУзелИБОбъект.УстановитьПометкуУдаления(Истина);
			Исключение
			    СтрСообщения = "Ошибка при записи узла <" + ТекУзелОбменаОбъект.Наименование + ">: " 
				+ ИнформацияОбОшибке().Описание + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", "
				+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")";
				#Если Клиент Тогда
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
				#КонецЕсли 
				ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
				Рез = Ложь;
			КонецПопытки;
			СтрСообщения = "Не удалось определить узел для информационной базы <" + ТекУзелИБ.Наименование + ">.
				|Данная информационная база помечена на удаление.
				|Необходимо вручную исправить несоответствие узла и информационной базы, присвоив им 
				|одинаковые префиксы, наименования, для узла присвоить соответствующую информационную базу
				|или удалить информационную базу.";
			#Если Клиент Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
			#КонецЕсли 
			ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
		КонецЕсли; 
	КонецЦикла; 

	
	Возврат рез;

КонецФункции // ПроверитьИерархиюИБ()

// Выполняет проверку на наличие текущей информационной базы
// и при необходимости создает
//
// Параметры
//   Нет
// Возвращаемое значение
// 	 Булево. Флаг успешного завершения проверки
//
Функция ПроверитьТекущуюИБ(ЭтотУзел = Неопределено,ЭтотУзелИБ = Неопределено) Экспорт

	Рез = Ложь; // все плохо
	// кэшировали этот узел
	Если ЭтотУзел = Неопределено Тогда
	   ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Ссылка;
	КонецЕсли; 
	
	ПрефиксЭтогоУзла = ЭтотУзел.Префикс;
	НеобходимоСохранениеУзла = Ложь;
	// вполне возможна ситуация, когда префикс узла не заполнен
	// берем префикс из кода узла
	Если  ПустаяСтрока(ПрефиксЭтогоУзла) Тогда
		ПрефиксЭтогоУзла = ЭтотУзел.Код;
		НеобходимоСохранениеУзла = Истина;
	КонецЕсли;
	
	Если ПустаяСтрока(ПрефиксЭтогоУзла) Тогда
		СтрСообщения = "Ошибка при обновлении структуры информационных баз: " + Символы.ПС
			+ "Не заполнен код узла <" + ЭтотУзел.Наименование + ">. Возможно, код узла был ошибочно очищен. Необходимо присвоить код узлу вручную."; 
		#Если Клиент Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
		#КонецЕсли 
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
		Возврат Ложь;
	КонецЕсли;
		
	// попробовали найти по префиксу
	ЭтотУзелИБ = Справочники.СтруктураИнформационныхБаз.НайтиПоРеквизиту("Префикс",ПрефиксЭтогоУзла);
	
	Если ЗначениеЗаполнено(ЭтотУзелИБ) Тогда
		ЭтотУзелИБОбъект = ЭтотУзелИБ.ПолучитьОбъект();
	Иначе
		ЭтотУзелИБОбъект = Справочники.СтруктураИнформационныхБаз.СоздатьЭлемент();
	КонецЕсли;
	// заполнили основные реквизиты, проверив заодно соответствие
	НеобходимоСохранениеИБ = Ложь;
	Если ЭтотУзелИБОбъект.Наименование <> ЭтотУзел.Наименование Тогда
	    ЭтотУзелИБОбъект.Наименование = ЭтотУзел.Наименование;
		НеобходимоСохранениеИБ = Истина;
	КонецЕсли;
	Если ЭтотУзелИБОбъект.Префикс <> ПрефиксЭтогоУзла Тогда
	    ЭтотУзелИБОбъект.Префикс = ПрефиксЭтогоУзла;
		НеобходимоСохранениеИБ = Истина;
	КонецЕсли;
	Если ЭтотУзелИБОбъект.ЭтоНовый() Тогда
		ЭтотУзелИБОбъект.ПометкаУдаления 		= Ложь;
		ЭтотУзелИБОбъект.УстановитьНовыйКод(СокрЛП(ПрефиксЭтогоУзла));
		// в сущ-м узле настройка уже д.б. заполнена
		// верная настройка для тек.узла заполнится при ЗавершенииОбновленияДочернегоУзла
		ЭтотУзелИБОбъект.НастройкаДоставкиПоУмолчанию = Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть;
		НеобходимоСохранениеИБ = Истина;
	КонецЕсли; 
	
	// получение режима проведения по партиям
	Если НЕ ЗначениеЗаполнено(ЭтотУзелИБОбъект.РежимПроведенияПоПартиям) Тогда
		ЭтотУзелИБОбъект.РежимПроведенияПоПартиям = Перечисления.РежимыПроведенияПартий.ПервичнымиДокументамиДопроведение;
		НеобходимоСохранениеИБ = Истина;
	КонецЕсли; 
	
	Если НЕ ЭтотУзелИБОбъект.ЭтоНовый() Тогда
		Если ЭтотУзелИБ <> ЭтотУзел.ИнформационнаяБаза Тогда
			НеобходимоСохранениеИБУзла = Истина;
		КонецЕсли;
	Иначе	
		НеобходимоСохранениеУзла = Истина;
	КонецЕсли; 
	
	Попытка
		Если НеобходимоСохранениеИБ Тогда
			// запишем тек. ИБ без всяких проверок в модуле объекта
			ЭтотУзелИБОбъект.ОбменДанными.Загрузка = Истина;
			ЭтотУзелИБОбъект.Записать();
			ЭтотУзелИБ = ЭтотУзелИБОбъект.Ссылка;
		КонецЕсли; 
		
		Если НеобходимоСохранениеУзла Тогда
			ЭтотУзелОбменаОбъект = ЭтотУзел.ПолучитьОбъект();
			ЭтотУзелОбменаОбъект.Префикс = ПрефиксЭтогоУзла;
			ЭтотУзелОбменаОбъект.ИнформационнаяБаза = ЭтотУзелИБ;
			ЭтотУзелОбменаОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина);
			ЭтотУзелОбменаОбъект.Записать();
		КонецЕсли; 
		
		Если НеобходимоСохранениеИБ Тогда
			СтрСообщения = "Создан информационная база, соответствующий узлу <" + ЭтотУзелИБ.Наименование + ">";
			#Если Клиент Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Информация);
			#КонецЕсли 
			ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , СтрСообщения);
		КонецЕсли; 
		
		
		Если ЭтотУзелИБ.Подразделения.Количество() = 0 И ПараметрыСеанса.ИспользованиеРБД Тогда
			СтрСообщения =  "Не удалось получить подразделение из настроек плана обмена. Необходимо заполнить табличную часть ""Подразделения"" справочника ""Структуры информационных баз"""
			+ " для элемента <" + ЭтотУзелИБ.Наименование + "> ";
			#Если Клиент Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Информация);
			#КонецЕсли
			ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
		КонецЕсли;
		Рез = Истина;
		
	Исключение
		
		СтрСообщения = "Ошибка при обновлении структуры информационных баз: " + Символы.ПС
		+ ИнформацияОбОшибке().Описание + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " + Символы.ПС
		+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
		#Если Клиент Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
		#КонецЕсли 
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
		Рез = Ложь;
		
	КонецПопытки;
	
	Возврат Рез;

	
КонецФункции

// Создает настройки доставки сообщений в виде элементов справочника НастройкиДоставкиСообщений
// на основании настроек узла РБД (старый вариант)
//
// Параметры
//	НастройкаДляДочернегоУзла - СправочникСсылка.НастройкиДоставкиСообщений. Служит для возврата
//	 							 на созданную настройку доставки в вызывающую функцию
// Возвращаемое значение
//	Нет	
//
Процедура ПеренестиНастройкиДоставкиСообщений(НастройкаДляДочернегоУзла = Неопределено) Экспорт
	
    ЭтотУзелОбмена = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	// Возьмем все доступные узлы кроме текущего
	// и заполним настройки доставки
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПлановОбмена.Узел КАК Узел,
	|	НастройкиПлановОбмена.Наименование,
	|	НастройкиПлановОбмена.Значение
	|ИЗ
	|	РегистрСведений.НастройкиПлановОбмена КАК НастройкиПлановОбмена
	|ГДЕ
	|	НастройкиПлановОбмена.Узел <> &ЭтотУзелОбмена
	|	И НастройкиПлановОбмена.Узел ССЫЛКА ПланОбмена.УдаленныеПодразделения
	|ИТОГИ ПО
	|	Узел";
	Запрос.УстановитьПараметр("ЭтотУзелОбмена", ЭтотУзелОбмена);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаУзлы = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	КолвоУзлов = 0;
	НастройкаДляДочернегоУзла = Неопределено;
	Пока ВыборкаУзлы.Следующий() Цикл
		КолвоУзлов = КолвоУзлов + 1;
		
		ТекУзелОбмена = ВыборкаУзлы.Узел;
	
		Выборка = ВыборкаУзлы.Выбрать();
		
		Настройки = Новый Структура;
		Пока Выборка.Следующий() Цикл
			Попытка
				ТекЗначение = Выборка.Значение.Получить();
				Настройки.Вставить(Выборка.Наименование,ТекЗначение);
			Исключение КонецПопытки;
		КонецЦикла;
		
		// делаем неск. настроек из общей кучи
		ТекСпособОбмена = Настройки.СпособОбмена;
		ТекКаталогОбмена = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
		Если ПустаяСтрока(ТекКаталогОбмена) Тогда
			ТекКаталогОбмена = Настройки.КаталогОбмена;
		КонецЕсли;
		
		НастройкаДоставкиПоУмолчанию = Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть;
		ДопНаименование = "";
		Если НЕ ЗначениеЗАполнено(ТекКаталогОбмена) Тогда
			ТекКаталогОбмена = НастройкаДоставкиПоУмолчанию.КаталогОбменов;
		КонецЕсли;
		ДопНаименование = "( " + ТекУзелОбмена.Наименование + " -> " + ЭтотУзелОбмена.Наименование + " )"; // чтобы не было задвоения наименования с предопр.элементом
		
		// создаем с контролем существования (вдруг раньше все создали, настроили, а потом кто-то откатил релиз...)
		// лок. сеть = 0
				
		Если  ТекСпособОбмена = 0  Тогда
			ТекНаименование = "Локальный каталог "  + ДопНаименование + " " 
				+ ?(ТекКаталогОбмена = "","","( " + ТекКаталогОбмена + " )");
			НастройкаСсылка = Справочники.НастройкиДоставкиСообщений.НайтиПоНаименованию(ТекНаименование);
			Если НЕ ЗначениеЗаполнено(НастройкаСсылка) Тогда
				НастройкаОбъект 									= Справочники.НастройкиДоставкиСообщений.СоздатьЭлемент();
				НастройкаОбъект.СпособОбмена 						= 0;
				НастройкаОбъект.КаталогОбменов 						= ?(Прав(ТекКаталогОбмена,1)="\",ТекКаталогОбмена,ТекКаталогОбмена+"\");
				НастройкаОбъект.ИспользоватьВременныйКаталог 		= Ложь;
				НастройкаОбъект.Наименование 						= ТекНаименование;
				НастройкаОбъект.УстановитьНовыйКод(СокрЛП(ТекУзелОбмена.Код));
				Попытка
					НастройкаОбъект.ОбменДанными.Загрузка = Истина;
					НастройкаОбъект.Записать();
					НастройкаДоставкиПоУмолчанию = НастройкаОбъект.Ссылка;
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Создана настройка доставки <" + НастройкаОбъект.Наименование + "> на основании настроек плана обмена");
				Исключение
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Ошибка при обновлении структуры информационных баз: " + ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
			
		// MAPI = 1
		ИначеЕсли ТекСпособОбмена = 1 Тогда
			ТекНаименование = "MAPI "  + ДопНаименование + " " 
				+ ?(Настройки.АдресЭлектроннойПочты = "",""," ( " + Настройки.АдресЭлектроннойПочты + " )");
			НастройкаСсылка = Справочники.НастройкиДоставкиСообщений.НайтиПоНаименованию(ТекНаименование);
			Если НЕ ЗначениеЗаполнено(НастройкаСсылка) Тогда
				НастройкаОбъект 									= Справочники.НастройкиДоставкиСообщений.СоздатьЭлемент();
				НастройкаОбъект.СпособОбмена 						= 1;
				НастройкаОбъект.КаталогОбменов 						= ?(Прав(ТекКаталогОбмена,1)="\",ТекКаталогОбмена,ТекКаталогОбмена+"\");
				НастройкаОбъект.ПрофильMAPI 						= Настройки.ПрофильMAPI;
				НастройкаОбъект.ПарольMAPI 							= Настройки.ПарольMAPI;
				НастройкаОбъект.АдресЭлектроннойПочты 				= Настройки.АдресЭлектроннойПочты;
				НастройкаОбъект.ИспользоватьВременныйКаталог 		= Истина;
				НастройкаОбъект.СохранятьОтправленные 				= Истина;
				НастройкаОбъект.СохранятьПолученные 				= Истина;
				НастройкаОбъект.Наименование 						= ТекНаименование;
				НастройкаОбъект.УстановитьНовыйКод(СокрЛП(ТекУзелОбмена.Код));
				Попытка
				    НастройкаОбъект.ОбменДанными.Загрузка = Истина;
					НастройкаОбъект.Записать();
					НастройкаДоставкиПоУмолчанию = НастройкаОбъект.Ссылка;
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Создана настройка доставки <" + НастройкаОбъект.Наименование + "> на основании настроек плана обмена");
				Исключение
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Ошибка при обновлении структуры информационных баз: " + ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
		
		// SMTP / POP3 = 2
		ИначеЕсли ТекСпособОбмена = 2 Тогда
			ТекНаименование = "SMTP / POP3 "  + ДопНаименование + " " 
				+ ?(Настройки.АдресЭлектроннойПочты = "",""," ( " + Настройки.АдресЭлектроннойПочты + " )");
			НастройкаСсылка = Справочники.НастройкиДоставкиСообщений.НайтиПоНаименованию(ТекНаименование);
			Если НЕ ЗначениеЗаполнено(НастройкаСсылка) Тогда
				НастройкаОбъект 									= Справочники.НастройкиДоставкиСообщений.СоздатьЭлемент();
				НастройкаОбъект.СпособОбмена 						= 2;
				НастройкаОбъект.КаталогОбменов 						= ?(Прав(ТекКаталогОбмена,1)="\",ТекКаталогОбмена,ТекКаталогОбмена+"\");
				НастройкаОбъект.СерверPOP3 							= Настройки.СерверPOP3;
				НастройкаОбъект.ПортPOP3 							= Настройки.ПортPOP3;
				НастройкаОбъект.ПользовательPOP3 					= Настройки.ПользовательPOP3;
				НастройкаОбъект.ПарольPOP3 							= Настройки.ПарольPOP3;
				НастройкаОбъект.СерверSMTP 							= Настройки.СерверSMTP;
				НастройкаОбъект.ПортSMTP 							= Настройки.ПортSMTP;
				НастройкаОбъект.ПользовательSMTP 					= Настройки.ПользовательSMTP;
				НастройкаОбъект.ПарольSMTP 							= Настройки.ПарольSMTP;
				НастройкаОбъект.АдресЭлектроннойПочты 				= Настройки.АдресЭлектроннойПочты;
				НастройкаОбъект.ИспользоватьВременныйКаталог 		= Истина;
				НастройкаОбъект.СохранятьОтправленные 				= Истина;
				НастройкаОбъект.СохранятьПолученные 				= Истина;
				НастройкаОбъект.Наименование 						=  ТекНаименование;
				НастройкаОбъект.УстановитьНовыйКод(СокрЛП(ТекУзелОбмена.Код));
				Попытка
					НастройкаОбъект.ОбменДанными.Загрузка = Истина;
					НастройкаОбъект.Записать();
					НастройкаДоставкиПоУмолчанию = НастройкаОбъект.Ссылка;
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Создана настройка доставки <" + НастройкаОбъект.Наименование + "> на основании настроек плана обмена");
				Исключение
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Ошибка при обновлении структуры информационных баз: " + ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
			// FTP = 3
		ИначеЕсли ТекСпособОбмена = 3 Тогда
			ТекНаименование = "FTP "  + ДопНаименование + " " + ?(Настройки.СерверFTP + Настройки.КаталогОбмена = "",
			""," ( " + Настройки.СерверFTP + Настройки.КаталогОбмена + " )");
			НастройкаСсылка = Справочники.НастройкиДоставкиСообщений.НайтиПоНаименованию(ТекНаименование);
			Если НЕ ЗначениеЗаполнено(НастройкаСсылка) Тогда
				
				НастройкаОбъект 									= Справочники.НастройкиДоставкиСообщений.СоздатьЭлемент();
				НастройкаОбъект.СпособОбмена 						= 3;
				НастройкаОбъект.КаталогОбменов 						= ?(ЗначениеЗаполнено(НастройкаДоставкиПоУмолчанию.КаталогОбменов),
					НастройкаДоставкиПоУмолчанию.КаталогОбменов,"C:\RBD\");
				НастройкаОбъект.СерверFTP 							= Настройки.СерверFTP;
				НастройкаОбъект.ПортFTP 							= Настройки.ПортFTP;
				НастройкаОбъект.ПользовательFTP 					= Настройки.ПользовательFTP;             
				НастройкаОбъект.ПарольFTP 							= Настройки.ПарольFTP;
				НастройкаОбъект.Таймаут								= Настройки.Таймаут;
				НастройкаОбъект.ПассивныйРежим 						= Настройки.ПассивныйРежим;
				// в старой версии КаталогОбмена для локального каталога и для FTP - один и тот же реквизит,
				// поэтому будем брать каталог для фтп только если тек.способ и есть фтп
				Если НастройкаОбъект.СпособОбмена = ТекСпособОбмена Тогда 
					КаталогОбменаFTP = ?(Прав(ТекКаталогОбмена, 1) = "\",Лев(ТекКаталогОбмена,СтрДлина(ТекКаталогОбмена) - 1),ТекКаталогОбмена);
					НастройкаОбъект.КаталогОбменаFTP = КаталогОбменаFTP +"/";
				КонецЕсли;
				НастройкаОбъект.КаталогОбменаFTP = ?(Лев(НастройкаОбъект.КаталогОбменаFTP,1) = "/",НастройкаОбъект.КаталогОбменаFTP,"/"+НастройкаОбъект.КаталогОбменаFTP);
				НастройкаОбъект.ИспользоватьВременныйКаталог	= Истина;
				НастройкаОбъект.СохранятьОтправленные 			= Истина;
				НастройкаОбъект.СохранятьПолученные 			= Истина;
				НастройкаОбъект.Наименование 					= ТекНаименование;
				НастройкаОбъект.УстановитьНовыйКод(СокрЛП(ТекУзелОбмена.Код));
				Попытка
					НастройкаОбъект.ОбменДанными.Загрузка = Истина;
					НастройкаОбъект.Записать();
					НастройкаДоставкиПоУмолчанию = НастройкаОбъект.Ссылка;
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Создана настройка доставки <" + НастройкаОбъект.Наименование + "> на основании настроек плана обмена");
				Исключение 
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + "Ошибка при обновлении структуры информационных баз: " + ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
		
			
		Если ЗНачениеЗаполнено(НастройкаДоставкиПоУмолчанию) Тогда
			
			ТекУзелИБ = ТекУзелОбмена.ИнформационнаяБаза;
			Если ТекУзелИБ.Пустая() Тогда
				ТекУзелИБ  = Справочники.СтруктураИнформационныхБаз.НайтиПоРеквизиту("Префикс",ТекУзелОбмена.Префикс);
			КонецЕсли;
					
			Если НЕ ТекУзелИБ.Пустая() Тогда
				ТекУзелИБОбъект = ТекУзелИБ.ПолучитьОбъект();
				Если ТекУзелИБОбъект.НастройкаДоставкиПоУмолчанию.Пустая()
					ИЛИ ((ТекУзелИБОбъект.НастройкаДоставкиПоУмолчанию = Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть) И (НастройкаДоставкиПоУмолчанию <> Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть)) Тогда
					ТекУзелИБОбъект.НастройкаДоставкиПоУмолчанию = НастройкаДоставкиПоУмолчанию;
					ТекУзелИБОбъект.ОбменДанными.Загрузка = Истина;
					ТекУзелИБОбъект.Записать();
					ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Установлена настройка доставки сообщений по умолчанию <" + НастройкаДоставкиПоУмолчанию.Наименование + ">");
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли КолвоУзлов > 0 Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + "Не удалось определить настройку доставки сообщений РБД по умолчанию для узла <" + ТекУзелИБОбъект.Наименование + ">
			|Внимание! Необходимо указать правильную настройку доставки (см. справочник ""Структура ИБ"")",СтатусСообщения.Внимание);
		КонецЕсли;
		// в контексте завершения обновления доч. узла - это настройка главного узла
		Если ТекУзелОбмена = ПланыОбмена.ГлавныйУзел() Тогда
		    НастройкаДляДочернегоУзла = НастройкаСсылка;
			Если НЕ ЗначениеЗаполнено(НастройкаДляДочернегоУзла) Тогда
			    НастройкаДляДочернегоУзла = НастройкаДоставкиПоУмолчанию;
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЦикла; // Пока ВыборкаУзлы.Следующий()
	
КонецПроцедуры

// Определяет наличие записей в таблице регистрации справочника ПравилаМиграцииИДоступа
//
Функция ОпределитьИзмененияНастроекМиграции(УзелОбмена) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПравилаМиграцииИДоступаИзменения.Узел,
	|	ПравилаМиграцииИДоступаИзменения.НомерСообщения
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ПравилаМиграцииИДоступаИзменения.Узел КАК Узел,
	|		ПравилаМиграцииИДоступаИзменения.НомерСообщения КАК НомерСообщения
	|	ИЗ
	|		Справочник.ПравилаМиграцииИДоступа.Изменения КАК ПравилаМиграцииИДоступаИзменения
	|	ГДЕ
	|		ПравилаМиграцииИДоступаИзменения.Узел = &УзелОбмена
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ПраваИНастройкиИзменения.Узел,
	|		ПраваИНастройкиИзменения.НомерСообщения
	|	ИЗ
	|		РегистрСведений.ПраваИНастройки.Изменения КАК ПраваИНастройкиИзменения
	|	ГДЕ
	|		ПраваИНастройкиИзменения.ПравоНастройка.ЭтоГруппа = ЛОЖЬ
	|		И ПраваИНастройкиИзменения.ПравоНастройка.Родитель = &Родитель
	|		И ПраваИНастройкиИзменения.ПравоНастройка.Предопределенный = ЛОЖЬ
	|		И ПраваИНастройкиИзменения.Узел = &УзелОбмена
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ПраваИНастройкиИзменения.Узел,
	|		ПраваИНастройкиИзменения.НомерСообщения
	|	ИЗ
	|		ПланВидовХарактеристик.ПраваИНастройки.Изменения КАК ПраваИНастройкиИзменения
	|	ГДЕ
	|		ПраваИНастройкиИзменения.Ссылка.Родитель = &Родитель
	|		И ПраваИНастройкиИзменения.Узел = &УзелОбмена
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		СтруктураИнформационныхБазИзменения.Узел,
	|		СтруктураИнформационныхБазИзменения.НомерСообщения
	|	ИЗ
	|		Справочник.СтруктураИнформационныхБаз.Изменения КАК СтруктураИнформационныхБазИзменения
	|	ГДЕ
	|		СтруктураИнформационныхБазИзменения.Узел = &УзелОбмена
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ПодразделенияКомпанииИзменения.Узел,
	|		ПодразделенияКомпанииИзменения.НомерСообщения
	|	ИЗ
	|		Справочник.ПодразделенияКомпании.Изменения КАК ПодразделенияКомпанииИзменения
	|	ГДЕ
	|		ПодразделенияКомпанииИзменения.Узел = &УзелОбмена) КАК ПравилаМиграцииИДоступаИзменения";
		 
	Запрос.УстановитьПараметр("Родитель", ПланыВидовХарактеристик.ПраваИНастройки.ПравилаМиграцииДокументов.Ссылка);
	Запрос.УстановитьПараметр("УзелОбмена",УзелОбмена);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	    Возврат Истина;
	Иначе	
	    Возврат Ложь;
	КонецЕсли;
	
КонецФункции

//OkoE - запись в регистр параметров последних обменов
Процедура ЗаписатьПараметрыОбменаДанными(УзелИБ, НастройкаДоставки, 
	Загрузка = Истина, Результат = Истина, ИмяФайлаСообщения = "", Ошибки = "") Экспорт
	
	Если обЗначениеНеЗаполнено(НастройкаДоставки) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ПараметрыОбменаДанными.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НастройкаОбменаДанными = НастройкаДоставки;
	МенеджерЗаписи.УзелИБ = УзелИБ;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.НастройкаОбменаДанными = НастройкаДоставки;
		МенеджерЗаписи.УзелИБ = УзелИБ;
	КонецЕсли;
	
	Если Загрузка Тогда
		МенеджерЗаписи.ДатаПоследнейЗагрузки = ТекущаяДата();
		МенеджерЗаписи.РезультатПоследнейЗагрузки = Результат;
		Если Результат Тогда
			МенеджерЗаписи.ДатаПоследнейУдачнойЗагрузки = ТекущаяДата();
			МенеджерЗаписи.КомментарийЗагрузка = "";
		Иначе
			МенеджерЗаписи.КомментарийЗагрузка = Ошибки;
		КонецЕсли;
		МенеджерЗаписи.ИмяФайлаПоследнейЗагрузки = ИмяФайлаСообщения;
	Иначе
		МенеджерЗаписи.ДатаПоследнейВыгрузки = ТекущаяДата();
		МенеджерЗаписи.РезультатПоследнейВыгрузки = Результат;
		Если Результат Тогда
			МенеджерЗаписи.ДатаПоследнейУдачнойВыгрузки = ТекущаяДата();
			МенеджерЗаписи.КомментарийВыгрузка = "";
		Иначе
			МенеджерЗаписи.КомментарийВыгрузка = Ошибки;
		КонецЕсли;
		МенеджерЗаписи.ИмяФайлаПоследнейВыгрузки = ИмяФайлаСообщения;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
КонецПроцедуры

// Получает текущий режим проведения по партиям для данного узла 
// плана обмена "Удаленные подразделения"
//
Функция ПолучитьРежимПроведенияПоПартиям() Экспорт
	
	 УзелИБ = ТекущаяИнформационнаяБаза();
	 РежимПроведенияПоПартиям = УзелИБ.РежимПроведенияПоПартиям;
	 Если НЕ ЗначениеЗаполнено(РежимПроведенияПоПартиям) Тогда
	 	 РежимПроведенияПоПартиям = Перечисления.РежимыПроведенияПартий.ПартииОтсутствуют;
	 КонецЕсли; 
	 
	 Возврат РежимПроведенияПоПартиям;
		
КонецФункции

// Определяет наличие режима загрузки
//
Функция ПолучитьЗагрузку(ЭтотОбъект) Экспорт
	
	Загрузка = Ложь;
	Попытка
		// Для редких случаев, когда ЭтотОбъект неопределен
		// проверку на режим обмена данными делаем через попытку
		Загрузка = ЭтотОбъект.ОбменДанными.Загрузка;
		Если Загрузка Тогда
			// если текущий режим Загрузка, то производим минимум проверок
			// т.к. все проверки были произведены в ИБ источнике
			Возврат Ложь;
		КонецЕсли;
		ДопЗагрузка = Неопределено;
		Если НЕ Загрузка Тогда
			ЭтотОбъект.ДополнительныеСвойства.Свойство("Загрузка",ДопЗагрузка);
			Загрузка = ?(НЕ ДопЗагрузка = Неопределено,ДопЗагрузка,Загрузка); 
		КонецЕсли;
	Исключение КонецПопытки;
	
	Возврат Загрузка;
	
КонецФункции

	 
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ ЧТЕНИЯ И ЗАПИСИ

//Обновляет кэш правил миграции объектов (переменная модуля)
//	Если обновление выполнено или не нужно, поскольку правило уже в кэше, то возвращает Истина,
//	Если объект не входит в правила, то возвращает Ложь.
Процедура ОбновитьКэшПравилМиграции(ЭлементДанных,ЭлементДанныхМетаданные,ПланОбменаМетаданные,ПравилаМиграцииОбъектов,ЭтоУдаление = Ложь)
	
	Если ЭтоУдаление Тогда
		ПодразделениеОбъекта = ЭлементДанных.Ссылка.ПодразделениеКомпании;
	Иначе
		ПодразделениеОбъекта = ЭлементДанных.ПодразделениеКомпании;
	КонецЕсли;
	ПланОбменаМетаданныеСостав = ПланОбменаМетаданные.Состав;
	//получаем правила для текущего вида объекта
	ПравилаОбъекта = ПравилаМиграцииОбъектов[ЭлементДанныхМетаданные];
	Если ПравилаОбъекта = Неопределено Тогда //такого правила ещё нет - создаем
		ПравилаМиграцииОбъектов.Вставить(ЭлементДанныхМетаданные, Новый Соответствие());
		ПравилаОбъекта = ПравилаМиграцииОбъектов[ЭлементДанныхМетаданные];
	КонецЕсли;
	
	//теперь получаем правила для пары вид объекта/подразделение объекта
	ПравилаОбъектаПодразделение = ПравилаОбъекта[ПодразделениеОбъекта];
	Если ПравилаОбъектаПодразделение = Неопределено Тогда
		ПравилаОбъекта[ПодразделениеОбъекта] = одОбменДанными.ПолучитьМассивУзловОбмена(ЭлементДанных,ПодразделениеОбъекта,,
			ПланОбменаМетаданные,ЭлементДанныхМетаданные,ЭтоУдаление);
		ПравилаОбъектаПодразделение = ПравилаОбъекта[ПодразделениеОбъекта];
	КонецЕсли;
	
	// для ускорения регистрации изменений добавим также кэш метаданных
	// движений документа по регистрам накопления
	ПравилаОбъектаМетаданныеДвижений = ПравилаМиграцииОбъектов["МетаданныеДвижений_" + ЭлементДанныхМетаданные];
	Если ПравилаОбъектаМетаданныеДвижений = Неопределено Тогда
		МетаданныеДвижений = ЭлементДанныхМетаданные.Движения;    
		МассивМетаданныеДвижений = Новый Массив;
		Для каждого МетаданныеРегистр из МетаданныеДвижений Цикл
			//Если (Метаданные.РегистрыСведений.Содержит(МетаданныеРегистр)) Тогда
			//	Продолжить; // регистры сведений у нас регистрируются автоматом
			//КонецЕсли;
			// отсечем сразу, чтобы потом не обращаться лишний раз к процедуре..
			Если (ПланОбменаМетаданныеСостав.Найти(МетаданныеРегистр) = неопределено) Тогда
				Продолжить; // нет в составе плана обмена
			КонецЕсли;
			МассивМетаданныеДвижений.Добавить(МетаданныеРегистр);
		КонецЦикла;
		ПравилаМиграцииОбъектов["МетаданныеДвижений_" + ЭлементДанныхМетаданные] = МассивМетаданныеДвижений;
		ПравилаОбъектаМетаданныеДвижений = ПравилаМиграцииОбъектов["МетаданныеДвижений_" + ЭлементДанныхМетаданные];
	КонецЕсли; 
	
КонецПроцедуры

// Выполняет установку правил миграции и метаданных движений
// в конкретном экземпляре документа для ускорения выполнения обмена
//
// Параметры
// 	ЭлементДанных- Объект информационной базы. Мигрирующий объект
//
Процедура УстановитьПравилаМиграцииВОбъекте(ТипДанных,ЭлементДанных,ЭлементДанныхМетаданные,ПланОбменаМетаданные,
	ПравилаМиграцииОбъектов,ЭтоУдаление = Ложь)  Экспорт
	
	// правила устанавливаем только в документе,
	// так как сама регистрация изменений происходит только при наличии документа в базе
	// хранение правил миграции в регистре накопления лишено смысла, если вдруг
	// движения попадут в базу раньше документа при загрузке
	
	Если ЭлементДанныхМетаданные.Реквизиты.Найти("ПодразделениеКомпании") <> неопределено Тогда
		ОбновитьКэшПравилМиграции(ЭлементДанных,ЭлементДанныхМетаданные,ПланОбменаМетаданные,ПравилаМиграцииОбъектов,ЭтоУдаление);
		// при получении удаления объекта кэширование его метаданных  и прочего
		// теряет смысл, т.к. в момент его записи экземпляр объекта будет уже другой и
		// ЭлементДанных.ПолучитьОбъект() не поможет
		Если НЕ ЭтоУдаление Тогда
			Попытка
				ЭлементДанных.ДополнительныеСвойства.Вставить("ПравилаМиграции",ПравилаМиграцииОбъектов[ЭлементДанныхМетаданные][ЭлементДанных.ПодразделениеКомпании]);
				ЭлементДанных.ДополнительныеСвойства.Вставить("МетаданныеДвижений",ПравилаМиграцииОбъектов["МетаданныеДвижений_" + ЭлементДанныхМетаданные]);
				ЭлементДанных.ДополнительныеСвойства.Вставить("МетаданныеОбъекта",ЭлементДанныхМетаданные);  //!!!   Взять из глобального кеша и передать в документ-объект
				ЭлементДанных.ДополнительныеСвойства.Вставить("ПланОбменаМетаданные",ПланОбменаМетаданные);  //!!!    Взять из глобального кеша и передать в документ-объект
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает полное представление объекта
//
Функция ПредставлениеДанных(ТипДанных,Данные,ЭтоУдаление) Экспорт
	
	ТипДанныхСтр = Строка(ТипДанных);
	Если ЭтоУдаление Тогда
		Представление = "Удаление объекта: " + Строка(ТипДанныхСтр) + ": " + Строка(Данные);
	Иначе
		Представление = Строка(ТипДанныхСтр) + ": " + Строка(Данные);
	КонецЕсли;
	
	Код = ""; Наименование = ""; Отбор = НЕОПРЕДЕЛЕНО;
	
	Если (Найти(ВРЕГ(ТипДанныхСтр),"ССЫЛКА")=0) И (Найти(ВРЕГ(ТипДанныхСтр),"ОБЪЕКТ")=0) Тогда
		Попытка
			Отбор = Данные.Отбор;
		Исключение
		КонецПопытки;
	Иначе
		Попытка
			Код = СокрЛП(Данные.Код);
		Исключение
		КонецПопытки;
		Если ПустаяСтрока(Код) Тогда
			Попытка
				Наименование = СокрЛП(Данные.Наименование);
			Исключение
				Наименование = "";
			КонецПопытки;
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Код) Тогда
		Представление = Представление + ", (Код = " + СокрЛП(Данные.Код) + ")";
	ИначеЕсли НЕ ПустаяСтрока(Наименование) Тогда
		Представление = Представление + ", (" + СокрЛП(Данные.Наименование) + ")";
	ИначеЕсли ЗначениеЗаполнено(Отбор) Тогда
		Представление  = Представление + ", (" + Отбор + ")";
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции //ПредставлениеДанных()

// Обновляет кэш авторегистрации объектов по типам данных
//
Функция ОбновитьКешАвторегистрацииОбъектов(ТипДанных,СсылкаДанных,КешАвторегистрацииОбъектов,КешМетаданныхОбъектов,
	ЭтоДокумент,ПланОбменаМетаданныеСостав, РежимЗаписиРегистраПодчинениеРегистратору = Неопределено) Экспорт
	
	// Получим статус авторегистрации объекта
	СтатусАвторегистрации = КешАвторегистрацииОбъектов[ТипДанных];
	Если СтатусАвторегистрации = НЕОПРЕДЕЛЕНО Тогда
		// Необходимо определить 
		Если ЭтоДокумент Тогда
			МетаданныеОбъекта = одОбменДанными.ОбновитьКешМетаданныхОбъектов(ТипДанных,СсылкаДанных,КешМетаданныхОбъектов);
			ЭлементСостава = ПланОбменаМетаданныеСостав.Найти(МетаданныеОбъекта);
			Если ЭлементСостава = Неопределено Тогда // нет такого вида объекта в плане обмена - это бред
				СтатусАвторегистрации = Истина; // но подстрахуемся и все равно отправим этот бред получателю
			Иначе
				СтатусАвторегистрации = (ЭлементСостава.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить);
			КонецЕсли;
		Иначе
			// если это регистр накопления или регистр сведений, подчиненный регистратору,
			// по ним необходимо также обновить кэш авторегистрации
			СтатусАвторегистрации = Истина;
			МетаданныеОбъекта = ОбновитьКешМетаданныхОбъектов(ТипДанных,,КешМетаданныхОбъектов);
			Если НЕ МетаданныеОбъекта = Неопределено Тогда
				Попытка
					ВидРегистра = МетаданныеОбъекта.ВидРегистра;
					СтатусАвторегистрации = Ложь;
				Исключение
				    Попытка
						РежимЗаписи = МетаданныеОбъекта.РежимЗаписи;
						Если РежимЗаписи = РежимЗаписиРегистраПодчинениеРегистратору Тогда
							СтатусАвторегистрации = Ложь;
						КонецЕсли; 
					Исключение
					    СтатусАвторегистрации = Истина;
					КонецПопытки;
				КонецПопытки; 
			КонецЕсли;
		КонецЕсли;
		КешАвторегистрацииОбъектов.Вставить(ТипДанных,СтатусАвторегистрации);
	КонецЕсли;
	
	Возврат СтатусАвторегистрации;
	
КонецФункции //ОбновитьКешАвторегистрацииОбъектов()

// Обновляет кэш метаданных объектов по типам данных
//
Функция ОбновитьКешМетаданныхОбъектов(ТипДанных,СсылкаДанных = Неопределено,КешМетаданныхОбъектов) Экспорт
	
	МетаданныеОбъекта = КешМетаданныхОбъектов[ТипДанных];
	Если МетаданныеОбъекта = НЕОПРЕДЕЛЕНО Тогда
		
		Если СсылкаДанных = Неопределено Тогда
			// Вариант 1. Менее быстрый, но возможен случай, когда ссылка еще не прочитана (при разборе XML)
			МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипДанных);
		Иначе
			// Вариант 2. Более предпочтительный - знаем ссылку - быстрее получим метаданные
			МетаданныеОбъекта = СсылкаДанных.Метаданные();
		КонецЕсли;
		КешМетаданныхОбъектов.Вставить(ТипДанных,МетаданныеОбъекта);
	КонецЕсли;
	
	Возврат МетаданныеОбъекта;
	
КонецФункции // ОбновитьКешМетаданныхОбъектов()

// Производит обновление кэша, включающего массивы узлов обмена для каждого вида документов
// с целью использования данного массива для регистрации изменений движений данного вида документов
// отдельно от регистратора
//
Функция ОбновитьКешУзловПолучателей(ТипДанных,СсылкаДанных,КешУзловПолучателей,КешМетаданныхОбъектов,ПланОбменаМетаданные) Экспорт
	
	// Получим статус авторегистрации объекта
	МассивУзловПолучателей = КешУзловПолучателей[ТипДанных];
	Если НЕ ЗначениеЗаполнено(МассивУзловПолучателей) Тогда
		МетаданныеОбъекта = одОбменДанными.ОбновитьКешМетаданныхОбъектов(ТипДанных,СсылкаДанных,КешМетаданныхОбъектов);
		МассивУзловПолучателей = одОбменДанными.ПолучитьМассивУзловОбмена(СсылкаДанных,,,
			ПланОбменаМетаданные,МетаданныеОбъекта,Ложь);
		КешУзловПолучателей.Вставить(ТипДанных,МассивУзловПолучателей);
	КонецЕсли;
	
	Возврат МассивУзловПолучателей;
	
КонецФункции

	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПИСИ/ЧТЕНИЯ СООБЩЕНИЙ ОБМЕНА

// Процедура получения сообщения обмена данными
// Получает сообщение с новыми данными для этого узла и загружает данные
//	Возвращаемое значение: Истина, если удалось прочитать сообщение
Функция ПрочитатьСообщениеСИзменениями(ИмяФайлаСообщения = "",УзелОбменаОбъект,КоличествоЭлементовВТранзакции = 0,
	СтрОшибок = "", ДопПараметры = Неопределено) Экспорт
	Перем НомерСообщения;	// Номер принимаемого сообщения
	
	РезЧтения = Ложь;
	УзелОбмена =  УзелОбменаОбъект.Ссылка;
	ПрефиксЛога = "Обмен." + ?(УзелОбмена.Пустая(),"","Узел_" + СокрЛП(УзелОбмена.Префикс) + ".");
	
	НастройкаОбмена = Неопределено;
	Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
		ДопПараметры.Свойство("НастройкаОбмена", НастройкаОбмена);
	КонецЕсли; 
	
	ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);
	
	// Проверим возможность получения сообщения обмена
	СтрОшибок = "";
	Если НЕ УзелОбменаОбъект.ПроверитьКорректность(СтрОшибок) Тогда
		УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Чтение", "Для узла """ + УзелОбмена.Наименование + """ обнаружены следующие ошибки: " + СтрОшибок,
			УзелОбмена, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	ТемпФайлыДляУдаления = "";
	
	// Проверяем входные параметры
	Если ПустаяСтрока(ИмяФайлаСообщения) Тогда
		СтрОшибок = "В функцию чтения сообщения не передали имя файла с сообщением";
		Возврат Ложь;
	КонецЕсли;
	
	// А теперь убедимся что нам не подсунули чего попало
	ТестФайл = Новый Файл(ИмяФайлаСообщения);
	Если НЕ ТестФайл.Существует() ИЛИ НЕ ТестФайл.ЭтоФайл() ИЛИ (ВРЕГ(ТестФайл.Расширение) <> ".XML") ИЛИ НЕ (ТестФайл.Размер() > 600) Тогда
		СтрОшибок = "Функция чтения сообщения получила некорректное имя файла <" + ИмяФайлаСообщения + ">";
		Возврат Ложь;
	КонецЕсли;
	
	КонфигурацияУжеБылаИзменена = КонфигурацияИзменена();
	// Читаем полученное сообщение из файла	
	Попытка
		#Если Клиент Тогда
			Состояние("Чтение сообщения из файла...");
		#КонецЕсли
		
		УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Чтение", "Начало чтения файла сообщения """ + ИмяФайлаСообщения + """", УзелОбмена);
		ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);
		
		// ЧТЕНИЕ ФАЙЛА СООБЩЕНИЯ
		// Прочитаем сообщение обмена
		МассивИсключенияДляПопытки = Новый Массив;
		МассивИсключенияДляПопытки.Добавить("неизвестный получатель");
		МассивИсключенияДляПопытки.Добавить("неизвестный отправитель");
		МассивИсключенияДляПопытки.Добавить("Ошибка преобразования данных XML");
		МассивИсключенияДляПопытки.Добавить("получены изменения конфигурации");
		МассивИсключенияДляПопытки.Добавить("зарегистрированы изменения конфигурации");
		МассивИсключенияДляПопытки.Добавить("Номер сообщения меньше или равен номеру ранее принятого сообщения");
		МассивИсключенияДляПопытки.Добавить("не соответствует ожидаемой");

		// получим способ выполнения обмена - на клиенте или на сервере
		ПараметрыСеанса_ПараметрыОбмена = ПараметрыСеанса.ПараметрыОбменаДанными.Получить();
		ВыполнятьОбменНаКлиенте = ПараметрыСеанса_ПараметрыОбмена.ВыполнятьОбменНаКлиенте;
		
		КоличествоПопыток = 20; ТекОшибка = "";
		НомерСообщения = 0;
		к = 0; // Счетчик ошибок одного типа
		Пока Истина Цикл
			// отдельно попробуем прочесть шапку сообщения
			// - могут быть блокировки узла, либо открытие конфигуратора и прочее
			Попытка
				
				ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);
				// если стоит опция для обработки обмена ВыполнятьОбменНаКлиенте для сохранения совместимости
				// (каталоги обменов располагаются на клиентском компьютере) - не требуется  передача управления
				// на сервер - жертвуем производ-тью во имя сохранения настроек
				Если ВыполнятьОбменНаКлиенте Тогда
					НомерСообщения = ПрочитатьСообщениеОбменаНаКлиенте(ИмяФайлаСообщения,УзелОбмена,КоличествоЭлементовВТранзакции);
				Иначе
					// По умолчанию!!!специально для пользователя Робот без админ.прав передаем управление в привилег.модуль
					НомерСообщения = пвПривилегированныйМодуль.ПрочитатьСообщениеОбмена(ИмяФайлаСообщения,УзелОбмена,
						КоличествоЭлементовВТранзакции);
				КонецЕсли;
               	Прервать; // все нормально, удалось прочитать сообщение

			Исключение
				ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);
				СтрОшибки = ИнформацияОбОшибке().Описание;
				Если СтрОшибки = ТекОшибка Тогда
					к = к + 1; // та же самая ошибка
				Иначе
					к = 0; // перещелкиваем счетчик ошибок, это уже другая ошибка
				КонецЕсли;
				
				//  сформируем место расп. ошибки
				СтрСообщения =  СтрОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", "
						+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")";
						
				Если КонфигурацияИзменена() Тогда
					ВызватьИсключение СтрСообщения;
				КонецЕсли; 
				 
				Для Инд = 0 По МассивИсключенияДляПопытки.ВГраница() Цикл
				    Если Найти(ВРЕГ(СтрОшибки), ВРег(МассивИсключенияДляПопытки[Инд])) > 0 Тогда
					    ВызватьИсключение СтрСообщения; 
				    КонецЕсли;
				КонецЦикла;
						
				Если Найти(ВРЕГ(СтрОшибки), ВРег("открыта конфигуратором")) > 0 Тогда
					Если обЭтоКлиентСервер() Тогда
						// "вырубим" конфигуратор в подчиненной базе в клиент-серверном варианте
						Рез = ЗавершитьРаботуКонфигуратора();
						// вырубить не вышло - прерываем и ругаемся
						Если Не рез Тогда
							ВызватьИсключение "Ошибка при попытке закрытия конфигуратора (клиент-серверный тип расположения базы) в подчиненном узле"
								+ Символы.ВК + "Возможно некорректное заполнение констант по администрированию."
								+ Символы.ВК + "Необходимо вручную закрыть конфигуратор и продолжить обмен.";
						КонецЕсли; 
					Иначе
						// в файловом режиме сразу вываливаемся, так как помочь ничем не можем
						ВызватьИсключение "Открыт конфигуратор в подчиненном узле (файловый тип расположения базы). Загрузка измененной конфигурации невозможна."
							+ Символы.ВК + "Необходимо вручную закрыть конфигуратор и продолжить обмен.";
					КонецЕсли; 
					
				Иначе
					// здесь мы точно не знаем, что за ошибка, поэтому пока не прерываем, а пытаемся опять
				КонецЕсли;
				
			КонецПопытки;

			// фиксируем до какой попытки докатились
			УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка чтения сообщения. Попытка № " + к + " : " + СтрСообщения, УзелОбмена, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
            ТекОшибка = СтрОшибки;
			
			// исчерпали попытки
			Если к = КоличествоПопыток Тогда // прерываем - все что могли сделали
				ВызватьИсключение "Исчерпан лимит (" + Строка(КоличествоПопыток) + ") попыток чтения сообщения обмена.";
            КонецЕсли;
			
		КонецЦикла;

		//ЧтениеСообщения.ЗакончитьЧтение();
		//// закроем xml-файл
		//Попытка 
		//	ЧтениеXML.Закрыть();
		//Исключение 
		//	ВызватьИсключение СтрСообщения; // не смогли закрыть - прерываем 
		//КонецПопытки;
		
		ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);
		
		// КОНЕЦ ЧТЕНИЯ ФАЙЛА СООБЩЕНИЯ
		
		Если ЗначениеЗаполнено(НомерСообщения) Тогда
			СтрСообщения = "Успешно принято сообщение № " + НомерСообщения + " из узла """ 
				+ УзелОбмена.Наименование + """ (файл """ + ИмяФайлаСообщения + """)";
			РезЧтения = Истина;
			ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Информация, , , СтрСообщения);
			УзелОбменаОбъект.ИдетОбменНастройкамиОбмена = ПараметрыСеанса.ИдетОбменНастройкамиОбмена;
			Если УзелОбменаОбъект.ИдетОбменНастройкамиОбмена Тогда
				ТекстСообщения = "Получены новые правила миграции. Необходимо выполнить исправление регистрации через обработку ""Исправление ошибок БД""";
				УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Чтение", ТекстСообщения, УзелОбмена,Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
				// запишем в режимы работы необходимость обновить права для других активных пользователей
				сфЗаписатьДанныеСеансаОбновитьПрава();
			КонецЕсли;
		ИНаче
			ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Ошибка, , , "Пустой номер сообщения");
		КонецЕсли;
		ПараметрыСеанса.ИдетОбменНастройкамиОбмена = Ложь;
		УзелОбменаОбъект.ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО;
		
	Исключение
				
		ПараметрыСеанса_ПараметрыОбмена = ПараметрыСеанса.ПараметрыОбменаДанными.Получить();
		ЕстьПараметрыСеансаОбмен = (ПараметрыСеанса_ПараметрыОбмена.Количество() > 0);
        Если ЕстьПараметрыСеансаОбмен Тогда
		   КаталогЛогФайлов = ПараметрыСеанса_ПараметрыОбмена.КаталогЛогФайла;
		Иначе	
		   КаталогЛогФайлов = КаталогВременныхФайлов();
		КонецЕсли; 
		
		ПараметрыСеанса.ИдетОбменНастройкамиОбмена = Ложь; 
		УзелОбменаОбъект.ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО;
		
		РезЧтения = Ложь;
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		СтатусСобытия = Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение;
		БудемЗапускатьСкриптОбновления = Ложь;
		Если (Найти(ТекстОшибки, "получены изменения конфигурации") > 0) ИЛИ (КонфигурацияИзменена()) Тогда
			СтрСообщения = "ВНИМАНИЕ! Из главного узла получены изменения конфигурации!";
			#Если Клиент Тогда
			Состояние(СтрСообщения);
			#КонецЕсли
			ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
			СтрСообщения = СтрСообщения + "
				|Необходимо выполнить обновление конфигурации базы данных.";
				
			// Попробуем определить параметры подключения к текущей базе, если не получится не будем автообновление предлагать
			СтрокаПодключенияКБазе = СтрокаСоединенияИнформационнойБазы();
			ЗапускатьРоботаПослеОбновления = Ложь;
			Если ВРЕГ(Лев(СтрокаПодключенияКБазе,5)) = ВРЕГ("File=") Тогда
				СтрокаПодключенияКБазе = " /F""" + Сред(СтрокаПодключенияКБазе,7,СтрДлина(СтрокаПодключенияКБазе)-8) + """";
				ЗапускатьРоботаПослеОбновления = Истина;
			Иначе
				ПодстрокиСтрокиСоединения  = обРазложитьСтрокуВМассивПодстрок(СтрокаПодключенияКБазе,";");
				Если НЕ (ПодстрокиСтрокиСоединения.Количество()> 1 и Лев(ПодстрокиСтрокиСоединения[0], 5) = "Srvr=" и Лев(ПодстрокиСтрокиСоединения[1], 4) = "Ref=") Тогда
					СтрокаПодключенияКБазе = ""; // Обнуляем строку подключения - это флаг что мы не можем запустить обновление в авто-режиме
					СтрСообщения = "Ошибка: не удалось определить параметры подключения к базе данных. Невозможно предложить автоматическое обновление конфигурации.";
					ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
					Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Важное);
				Иначе
					ИмяСервера = ВРЕГ(Сред(ПодстрокиСтрокиСоединения[0],7, СтрДлина(ПодстрокиСтрокиСоединения[0]) - 7));
					ИмяИБ      = ВРЕГ(Сред(ПодстрокиСтрокиСоединения[1],6, СтрДлина(ПодстрокиСтрокиСоединения[1]) - 6));
					СтрокаПодключенияКБазе = " /S""" + ИмяСервера + "\" + ИмяИБ + """";
				КонецЕсли;
			КонецЕсли;
			
			#Если Клиент Тогда
			Если ПустаяСтрока(СтрокаПодключенияКБазе) Тогда // Не получится запустить автоматику значит не будем и спрашивать, допишем обычное сообщение
				СтрСообщения = СтрСообщения + Символы.ПС +"
				|Необходимо вручную обновить конфигурацию базы данных, затем
				|повторно запустить процедуру принятия данных обменов.";
			Иначе // Есть возможность запустить автообновление, составим текст вопроса пользователю
				СтрВопроса = СтрСообщения + "			   
				|Запустить процесс обновления конфигурации?";
			КонецЕсли;
			
			#КонецЕсли
			
			// Предложим пользователю запустить автоматику обновления конфигурации
			Если ПустаяСтрока(СтрокаПодключенияКБазе) Тогда
				БудемЗапускатьСкриптОбновления = Ложь;
				СтрСообщения = "Ошибка определения строки подключения к ИБ - невозможно запустить автоматическое обновление измененной конфигурации!";
				ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
			Иначе
				БудемЗапускатьСкриптОбновления = Истина;
				#Если Клиент Тогда
				Если (Вопрос(СтрВопроса,РежимДиалогаВопрос.ДаНет,15,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Нет) Тогда
					СтрСообщения = "Пользователь отказался от автоматического обновления конфигурации";
					БудемЗапускатьСкриптОбновления = Ложь;
					ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
				КонецЕсли;	
				#КонецЕсли
			КонецЕсли;	
			
			Если БудемЗапускатьСкриптОбновления Тогда
				ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
				ИмяАдминистратораИБ 					= Константы.ИмяАдминистратораИБ.Получить();
				ПарольАдминистратораИБ 					= СокрЛП(Константы.ПарольАдминистратораИБ.Получить());
				Таймаут								 	= Константы.ТаймаутЗавершенияРаботыПользователей.Получить();
				ИсполняемыйФайлКлиентаНаСервере		 	= Константы.ИсполняемыйФайлКлиентаНаСервере.Получить();
				ПланОбменаМетаданные					= УзелОбмена.Метаданные();
				
				// Пишем скрипт обновления
				ТекстДок = Новый ТекстовыйДокумент();
				ТекстДок.Вывод = ИспользованиеВывода.Разрешить;
				
				// Пишем скрипт обновления
				ТекстДок = Новый ТекстовыйДокумент();
				ТекстДок.Вывод = ИспользованиеВывода.Разрешить;
				// если сервер - запускаем на его стороне, иначе берем текущий файл
				#Если Клиент Тогда
					//+SUIG - для клиента проблемы в определении каталога нет
					СтрокаЗапускаПрограммы = """" + КаталогПрограммы() + "1cv8.exe"+ """"; // при работе в 64-х ОС  возвращает (х 86)
					ТестФайл = Новый Файл(КаталогПрограммы() + "1cv8.exe");
					Если НЕ ТестФайл.Существует() И НЕ ПустаяСтрока(ИсполняемыйФайлКлиентаНаСервере) Тогда
						СтрокаЗапускаПрограммы = """" + ИсполняемыйФайлКлиентаНаСервере + """";	
					КонецЕсли;
				#Иначе
					//+SUIG - для сервера - есть тонкость - фоновое задание может вернуть не тот каталог
					СтрокаЗапускаПрограммы = "";
					// DOOY+ 08.10.20 Сначала проверим по (x86) а потом по традиционному пути
					ТекКаталог = "C:\Program Files (x86)\1cv81\bin\";
					//проверка на существование файла
					ТестФайл = Новый Файл(ТекКаталог + "1cv8.exe");
					Если ТестФайл.Существует() Тогда
						СтрокаЗапускаПрограммы = """" + ТекКаталог + "1cv8.exe"+ """";	
					КонецЕсли;
					Если НЕ ТестФайл.Существует() Тогда
						ТекКаталог = "C:\Program Files\1cv81\bin\";
						//проверка на существование файла
						ТестФайл = Новый Файл(ТекКаталог + "1cv8.exe");
						Если ТестФайл.Существует() Тогда
							СтрокаЗапускаПрограммы = """" + ТекКаталог + "1cv8.exe"+ """";
						КонецЕсли;
                    Иначе
						СтрокаЗапускаПрограммы = """" + ТекКаталог + "1cv8.exe"+ """";
					КонецЕсли;
					// DOOY- 08.10.20 Сначала проверим по (x86) а потом по традиционному пути
					Если СтрокаЗапускаПрограммы = "" Тогда
						Если ПустаяСтрока(ИсполняемыйФайлКлиентаНаСервере) Тогда
							СтрокаЗапускаПрограммы = """" + КаталогПрограммы() + "1cv8.exe"+ """";
						Иначе
							СтрокаЗапускаПрограммы = """" + ИсполняемыйФайлКлиентаНаСервере + """";
						КонецЕсли;
					КонецЕсли;	
				#КонецЕсли

				// Сформируем команду обновления конфигурации
				КодРазрешения 							= Константы.КодРазрешенияВходаПриБлокировке.Получить();
				Если ПустаяСтрока(КодРазрешения) Тогда // DooY поискать используется ли он по назначению именно как параметр ли?
					КодРазрешения = "AllowRobotLogon";
				КонецЕсли;
				
				// 1. Завершение работы пользователей
				СтрокаЗапуска = СтрокаЗапускаПрограммы + " ENTERPRISE /CLogOffAllUsers /uc" + КодРазрешения + " /DisableStartupMessages";
				СтрокаЗапуска = СтрокаЗапуска + СтрокаПодключенияКБазе;
				СтрокаЗапуска = СтрокаЗапуска + " /N""" + ИмяАдминистратораИБ + """"; 
				Если НЕ ПустаяСтрока(ПарольАдминистратораИБ) Тогда
					СтрокаЗапуска = СтрокаЗапуска + " /P""" + ПарольАдминистратораИБ + """";
				КонецЕсли;
                ТекстДок.ДобавитьСтроку(СтрокаЗапуска);
				
				// 2. Обновление конфигурации базы данных
				СтрокаЗапуска = СтрокаЗапускаПрограммы + " DESIGNER /uc" + КодРазрешения + " /UpdateDBCfg /DisableStartupMessages /Visible";
				СтрокаЗапуска = СтрокаЗапуска + СтрокаПодключенияКБазе;
				СтрокаЗапуска = СтрокаЗапуска + " /N""" + ИмяАдминистратораИБ + """"; 
				Если НЕ ПустаяСтрока(ПарольАдминистратораИБ) Тогда
					СтрокаЗапуска = СтрокаЗапуска + " /P""" + ПарольАдминистратораИБ + """";
				КонецЕсли;
				          
				ИмяЛогаКонфигуратора = "";
				СтрокаЗапуска = СтрокаЗапуска + " /DumpResult""" + ИмяЛогаКонфигуратора + """";
				ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Информация, , , "Имя лога конфигуратора: <" + ИмяЛогаКонфигуратора + ">");
				ТекстДок.ДобавитьСтроку(СтрокаЗапуска);
				
				// 3. Дочитывание файла обмена
				ПараметрОбмена = "UpdateIB=" + "$" + ИмяФайлаСообщения + "$";
				ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Информация, , , "Доп.параметр обмена <" + ПараметрОбмена + ">");

				СтрокаЗапуска = СтрокаЗапускаПрограммы + " ENTERPRISE /C" + ПараметрОбмена +  " /uc" + КодРазрешения + " /DisableStartupMessages";
				СтрокаЗапуска = СтрокаЗапуска + СтрокаПодключенияКБазе;
				СтрокаЗапуска = СтрокаЗапуска + " /N""" + ИмяАдминистратораИБ + """";
				Если НЕ ПустаяСтрока(ПарольАдминистратораИБ) Тогда
					СтрокаЗапуска = СтрокаЗапуска + " /P""" + ПарольАдминистратораИБ + """";
				КонецЕсли;
                ТекстДок.ДобавитьСтроку(СтрокаЗапуска);
				
				// 4. Снятие блокировки доступа к информационной базе
				СтрокаЗапуска = СтрокаЗапускаПрограммы + " ENTERPRISE /CAllowLogonInToBase /uc" + КодРазрешения + " /DisableStartupMessages";
				СтрокаЗапуска = СтрокаЗапуска + СтрокаПодключенияКБазе;
				СтрокаЗапуска = СтрокаЗапуска + " /N""" + ИмяАдминистратораИБ + """"; 
				Если НЕ ПустаяСтрока(ПарольАдминистратораИБ) Тогда
					СтрокаЗапуска = СтрокаЗапуска + " /P""" +ПарольАдминистратораИБ + """";
				КонецЕсли;
				ТекстДок.ДобавитьСтроку(СтрокаЗапуска);
				
				// 5. Отправка ответного файла обмена
				КодНастройкиОбмена = "";
				Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
					КодНастройкиОбмена = СокрЛП(НастройкаОбмена.Код);
				КонецЕсли;
				Если НЕ ПустаяСтрока(КодНастройкиОбмена) Тогда
					КодНастройкиОбмена = "(" + КодНастройкиОбмена + ")";
				КонецЕсли; 
				СтрокаЗапуска = СтрокаЗапускаПрограммы + " ENTERPRISE /CSendExchangeMessage" + КодНастройкиОбмена + " /uc" + КодРазрешения + " /DisableStartupMessages";
				СтрокаЗапуска = СтрокаЗапуска + СтрокаПодключенияКБазе;
				СтрокаЗапуска = СтрокаЗапуска + " /N""" + ИмяАдминистратораИБ + """"; 
				Если НЕ ПустаяСтрока(ПарольАдминистратораИБ) Тогда
					СтрокаЗапуска = СтрокаЗапуска + " /P""" +ПарольАдминистратораИБ + """";
				КонецЕсли;
				ТекстДок.ДобавитьСтроку(СтрокаЗапуска);
				
				КодТекущегоУзла = СокрЛП(ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Ссылка.Код);
				
				// Сгенерируем имя скрипт-файла обновления
				ИмяСкрипта = "_Update_" + КодТекущегоУзла + Формат(ТекущаяДата(),"ДФ='ггггММдд-ЧЧммсс'") + ".CMD";
				// может не быть доступа к запуску из сетевой папки
				ПолноеИмяСкрипта = КаталогВременныхФайлов() + ИмяСкрипта;
				// Добавим команду на самоочистку скрипт-файла (не нужно пароли светить)
				СтрокаЗапуска = "DEL """ + ПолноеИмяСкрипта + """";
				ТекстДок.ДобавитьСтроку(СтрокаЗапуска);
				// Запишем скрипт
				ТекстДок.Записать(ПолноеИмяСкрипта,КодировкаТекста.OEM);
				ТекстДок = НЕОПРЕДЕЛЕНО;
				
				// до запуска скрипта сразу установим блокировку соединений
				// скрипт же после запуска проверит эту блокировку
				// тем самым исключается ситуация, когда скрипт не успевает запуститься до перезапуска Робота
				// до  этого Робот завершал работу без перезапуска с доп шагом скрипта
				пвПривилегированныйМодуль.УстановитьБлокировкуСоединений(,,,,Таймаут);
				
				СтрСообщения = "Получены изменения конфигурации ИБ, запускается скрипт для автообновления конфигурации...";
				// Есть 2 варианта запуска скрипта
				// 1 - если мы на стороне клиента (это ручной обмен или просто файловая база)
				// В данном случае используем ЗапуститьПриложение. 
				// 2 - мы на стороне сервера (это фоновое задание в клиент-серверном варианте базы)
				// ЗапуститьПриложение при этом недоступно. Используем wScript.Shell
				// ** Неиспользование wScript.Shell в обоих вариантах обусловлено возможностью админ.
				// запрета на запуск скриптов wScript.Shell в Windows
				#Если Клиент Тогда
					Состояние("Запускаем скрипт обновления...");
					// Запускаем скрипт
					Попытка
						ЗапуститьПриложение(ПолноеИмяСкрипта,,Ложь);
						ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Запущен скрипт обновления базы " + ПолноеИмяСкрипта);
					Исключение
						ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , "Ошибка запуска скрипта обновления " + ПолноеИмяСкрипта);
					КонецПопытки;
					// По-быстренькому вываливаемся сами
					// ВНИМАНИЕ!! Текущий робот или любой иной юзер д.б. запущен без ключа обхода блокировки
					// иначе он успеет зайти в базу до полного запуска скрипта и нарушить обновление конфигурации
					ЗавершитьРаботуСистемы(Ложь,Истина); 
				#Иначе
					Попытка
						Ком = Новый COMObject("wScript.Shell");
						Ком.Run("""" + ПолноеИмяСкрипта + """");
						ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Запущен скрипт обновления базы " + ПолноеИмяСкрипта);
					Исключение
						ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , "Ошибка запуска скрипта обновления " + ПолноеИмяСкрипта);
					КонецПопытки;
				#КонецЕсли
			КонецЕсли; 
			
			// ТекстОшибки - это детальное описание ошибки с номер строки, текстом
		ИначеЕсли Найти(ТекстОшибки, "зарегистрированы изменения конфигурации") > 0 Тогда
			СтрСообщения = "Данные принимаются от узла, для которого зарегистрированы изменения конфигурации. "
				+ "Необходимо произвести перенос изменений конфигурации в узел! ";
			РезЧтения = Истина; // SUIG - в фоновом задании то надо потом и принять это самое изменение конфигурации
			
		ИначеЕсли Найти(ТекстОшибки, "Номер сообщения меньше или равен номеру ранее принятого сообщения") > 0 Тогда
			СтрСообщения = "Номер сообщения меньше или равен номеру ранее принятого сообщения! ";
		ИначеЕсли Найти(ТекстОшибки, "не соответствует ожидаемой") > 0 Тогда
			СтрСообщения = "Конфигурация главной узла не соответствует ожидаемой. "; 
			СтатусСобытия = Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка;
			СтрСообщения = СтрСообщения + Символы.ВК 
				+ "Возможная причина: проведение неполного цикла обмена с главной базой." + Символы.ВК
				+ "Необходимо выполнить следующие действия для загрузки сообщения в подчиненной базе:" + Символы.ВК
				+ "- снять главный узел в подчиненной базе" + Символы.ВК
				+ "- загрузить конфигурацию главной базы в подчиненную базу" + Символы.ВК
				+ "- запустить 1С:Предприятие в подчиненной базе с параметром запуска BackDoor" + Символы.ВК
				+ "- установить главный узел в подчиненной базе." + Символы.ВК
				+ "При невозможности выполнить данные действия обратитесь к квалифицированному специалисту.";
		Иначе	
			СтрСообщения = "При чтении сообщения возникла ошибка: " + ТекстОшибки;  // здесь уже надо описать детально
			СтатусСобытия = Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка;
			СтрОшибок = СтрОшибок + СтрСообщения;
		КонецЕсли;
		// чтения уже не ожидается, поэтому закроем лог-файл во избежание повторного его создания
		ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);  
		УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Чтение", СтрСообщения, УзелОбмена, СтатусСобытия);
	КонецПопытки;
	#Если Клиент Тогда
		Состояние("");
	#КонецЕсли
	УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Чтение", "Конец чтения файла сообщения """ + ИмяФайлаСообщения + """", УзелОбмена);
	 
	Возврат РезЧтения;
КонецФункции // ПрочитатьСообщениеСИзменениями()

// Процедура записи сообщения с измененными данными
//	Возвращаемое значение: Истина, если удалось записать сообщение
Функция ЗаписатьСообщениеСИзменениями(ИмяФайлаСообщения = "",УзелОбменаОбъект,КоличествоЭлементовВТранзакции,
	КаталогТемпФайлов) Экспорт
	
	РезЗаписи = Ложь; // все плохо
	УзелОбмена = УзелОбменаОбъект.Ссылка;
	ПрефиксЛога = "Обмен." + ?(УзелОбмена.Пустая(),"","Узел_" + СокрЛП(УзелОбмена.Префикс) + ".");
	ПараметрыСеанса_ПараметрыОбмена = ПараметрыСеанса.ПараметрыОбменаДанными.Получить();
	ЕстьПараметрыСеансаОбмен = (ПараметрыСеанса_ПараметрыОбмена.Количество() > 0);
	
	Если ЕстьПараметрыСеансаОбмен Тогда
		ФиксироватьПоОбъекту 	= ПараметрыСеанса_ПараметрыОбмена.ФиксироватьПриемОтправкуОбъектов;
    	КомментироватьХодОбмена = ПараметрыСеанса_ПараметрыОбмена.КомментироватьХодОбмена;
	Иначе
		ФиксироватьПоОбъекту 	= Ложь;
		КомментироватьХодОбмена = Ложь;
	КонецЕсли;
	ТемпФайлыДляУдаления = "";
	
	// Проверим правильность заполнения реквизитов
	СтрОшибок = "";
	Если НЕ УзелОбменаОбъект.ПроверитьКорректность(СтрОшибок) Тогда
		УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", "Для узла """ + УзелОбмена.Наименование + """ обнаружены следующие ошибки: " + СтрОшибок,
			УзелОбмена,	Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	
	// Формируем имя файла сообщения
	ИмяТемпФайла = КаталогТемпФайлов + одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Истина, УзелОбменаОбъект.НомерОтправленного+1, "xml");
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяТемпФайла;
	
	УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", "Начало записи файла сообщения " + """" + ИмяТемпФайла + """", УзелОбмена);
	// Записываем новое сообщение обмена в файл (в запись входит и архивация)
	Попытка
	    #Если Клиент Тогда
			Состояние("Запись сообщения в файл...");
		#КонецЕсли
		
		// Проверим, есть ли изменения в ПравилахМиграции
		УзелОбменаОбъект.ИдетОбменНастройкамиОбмена = одОбменДанными.ОпределитьИзмененияНастроекМиграции(УзелОбмена);
		ПараметрыСеанса.ИдетОбменНастройкамиОбмена = УзелОбменаОбъект.ИдетОбменНастройкамиОбмена;
		Если ФиксироватьПоОбъекту Тогда
			Если УзелОбменаОбъект.ИдетОбменНастройкамиОбмена Тогда
				УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись",
					"Обнаружено изменение настроек обмена. Установлен режим отправки только данных настроек.", УзелОбмена, 
					Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
			КонецЕсли; 
		КонецЕсли;
		
		// получим способ выполнения обмена - на клиенте или на сервере
		ВыполнятьОбменНаКлиенте = ПараметрыСеанса_ПараметрыОбмена.ВыполнятьОбменНаКлиенте;
		
		// ЗАПИСЬ СООБЩЕНИЯ  В ФАЙЛ
		КоличествоПопыток = 20; ТекОшибка = "";
		к = 0; // Счетчик ошибок
		Пока Истина Цикл
			// Сформируем сообщение обмена
			Попытка
				ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект);
				// если стоит опция для обработки обмена ВыполнятьОбменНаКлиенте для сохранения совместимости
				// (каталоги обменов располагаются на клиентском компьютере) - не требуется  передача управления
				// на сервер - жертвуем производ-тью во имя сохранения настроек
				Если ВыполнятьОбменНаКлиенте Тогда
					НомерСообщения = ЗаписатьСообщениеОбменаНаКлиенте(ИмяТемпФайла,УзелОбмена,КоличествоЭлементовВТранзакции);
				Иначе
					// По умолчанию!!!специально для пользователя Робот без админ.прав передаем управление в привилег.модуль
					НомерСообщения = пвПривилегированныйМодуль.ЗаписатьСообщениеОбмена(ИмяТемпФайла,УзелОбмена,КоличествоЭлементовВТранзакции);
				КонецЕсли;
               	Прервать; // все нормально, удалось записать сообщение
			Исключение
				СтрОшибки = ИнформацияОбОшибке().Описание;
				Если СтрОшибки = ТекОшибка Тогда
					к = к + 1; // та же самая ошибка
				Иначе
					к = 0; // перещелкиваем счетчик ошибок, это уже другая ошибка
				КонецЕсли;
                				
				// сформируем детальное представление ошибки
				СтрСообщения = СтрОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " 
					+ "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
                			
				Если Найти(ВРег(СтрОшибки),ВРег("Недостаточно прав")) > 0 Тогда
                    ВызватьИсключение СтрСообщения; // при отсутствии прав выходим сразу - легче не станет  
				КонецЕсли;
			КонецПопытки;
			
			// фиксируем до какой попытки докатились
			УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", "Ошибка записи сообщения. Попытка № " + к + " : " + СтрОшибки, УзелОбмена, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
            ТекОшибка = СтрОшибки;

			ЗакрытьЛогФайл(УзелОбменаОбъект.ЛогФайлОбъект); // закроем лог,т.к. при НачатьЗапись объект узла обмена будет уже другим
			// исчерпали попытки
			Если к = КоличествоПопыток Тогда // выходим - все что могли сделали
			    ВызватьИсключение "Исчерпан лимит (" + Строка(КоличествоПопыток) + ") попыток записи сообщения обмена.";
			КонецЕсли; 
            
		КонецЦикла;
		
		// КОНЕЦ ЗАПИСИ СООБЩЕНИЯ В ФАЙЛ
		
		Если ЗначениеЗаполнено(НомерСообщения) Тогда
			СтрСообщения = "Успешно записано сообщение № " + НомерСообщения + " для узла """ 
				+ УзелОбмена.Наименование + """" + " в файл " + "" + ИмяТемпФайла + """";
			РезЗаписи = Истина;
			УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись",СтрСообщения,УзелОбмена);
		Иначе
			УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", "Не определен номер отправляемого сообщения.",
				УзелОбмена, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		КонецЕсли;
		РезЗаписи = Истина;
		ИмяФайлаСообщения = ИмяТемпФайла;
	Исключение
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		Если Найти(ТекстОшибки,"Недостаточно прав") > 0 Тогда
		    СтрСообщения = "Недостаточно прав доступа для выгрузки объектов из базы: " + Символы.ВК  + ТекстОшибки;
			СтатусСобытия = Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка;
			
			СтрСообщения = СтрСообщения + Символы.ВК 
				+ "Необходимо выполнить следующие действия для записи сообщения:" + Символы.ВК
				+ "- завершить работу всех активных пользователей"  + Символы.ВК
				+ "- в режиме Конфигуратора открыть пункт меню Администрирование - Тестирование и исправление..." + Символы.ВК
				+ "- установить только галочку ""Реструктуризация таблиц информационной базы""" + Символы.ВК
				+ "- выбрать ""Только тестирование""" + Символы.ВК
				+ "- запустить тестирование" + Символы.ВК
				+ "- после тестирования повторить выгрузку сообщения." + Символы.ВК
				+ "При невозможности выполнить данные действия обратитесь к квалифицированному специалисту.";
			УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", СтрСообщения, УзелОбмена, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);

		Иначе	
            УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", "При записи сообщения возникла ошибка: " + Символы.ВК
				+ ТекстОшибки, УзелОбмена, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		КонецЕсли; 
		Рез = Ложь;
	КонецПопытки;
	
	ПараметрыСеанса.ИдетОбменНастройкамиОбмена =  Ложь;
	ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО;
	
	//Удаляем временные файлы обменов
	#Если Клиент Тогда
	Состояние("Удаляем временные файлы...");
	#КонецЕсли
	Для ИндСтр=1 По СтрЧислоСтрок(ТемпФайлыДляУдаления) Цикл
		ИмяФайла=СтрПолучитьСтроку(ТемпФайлыДляУдаления,ИндСтр);
		Если ПустаяСтрока(ИмяФайла) ИЛИ (РезЗаписи И (ИмяФайла=ИмяТемпФайла)) Тогда
			Продолжить; //кроме файла с сообщением - его удалим уже после доставки
		Иначе
			Попытка	УдалитьФайлы(ИмяФайла);
			Исключение КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	#Если Клиент Тогда
		Состояние("");
	#КонецЕсли

	УзелОбменаОбъект.ЗаписьЛога(ПрефиксЛога + "Запись", "Конец записи файла сообщения " + """" + ИмяТемпФайла + """", УзелОбмена);
	
	Возврат РезЗаписи;
	
КонецФункции // ЗаписатьСообщениеСИзменениями()

// Проверят наличие указанного в параметре каталога
//если отсутствует, то пытается создать новый
//Добавляет заключительный знак "\" к переданному пути
//Возвращает:
//-	Истина если каталог есть (или был создан) 
//-	Ложь в противном случае
//Во втором (необязательном) параметре возвращает текст сообщения об ошибке
Функция ЕстьКаталог(Путь,ТекстОшибки="") Экспорт
	Если НЕ ЗначениеЗаполнено(Путь) ИЛИ (ТипЗнч(Путь) <> Тип("Строка")) Тогда
		ТекстОшибки = "Ошибка: Неверный параметр";
		Возврат Ложь;
	КонецЕсли;
	Если Прав(Путь,1) <> "\" Тогда
		Путь = Путь + "\";
	КонецЕсли;
    Попытка
		ПроверкаКаталога = Новый Файл(Путь);
		Если НЕ ПроверкаКаталога.Существует() Тогда
			СоздатьКаталог(Путь);
			ТекстОшибки = "Создан каталог: " + Путь;
			ПроверкаКаталога = Новый Файл(Путь);
		КонецЕсли;			
		Если ПроверкаКаталога.Существует() Тогда
			Если ПроверкаКаталога.ЭтоКаталог() Тогда
				Результат = Истина;
			Иначе
				ТекстОшибки = "Ошибка: заданный путь <"+Путь+"> не является каталогом (возможно это файл?)";
				Результат = Ложь;
			КонецЕсли;
		Иначе
			ТекстОшибки = "Ошибка: не удалось создать каталог: <"+Путь+">";
			Результат = Ложь;
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Результат = Ложь;
	КонецПопытки;
	// Возвращаем что у нас получилось
	Возврат Результат;
КонецФункции // ЕстьКаталог
	 
// Завершает работу конфигуратора в подчиненной информационной базе
//
// Параметры
//  Нет
//
// Возвращаемое значение:
//   Булево   – Флаг успешного завершения
//
Функция ЗавершитьРаботуКонфигуратора()
	
	Результат = Ложь; // пока ничего не совершили
	СтрОшибка = "";
	// вспомогательные параметры
	ИмяАдминистратораСервера				= СокрЛП(Константы.ИмяАдминистратораСервера.Получить());
	ПарольАдминистратораСервера				= СокрЛП(Константы.ПарольАдминистратораСервера.Получить());
	НомерПортаАгента						= Константы.НомерПортаПодключенияКАгенту.Получить();	
	ИмяАдминистратораКластера 				= СокрЛП(Константы.ИмяАдминистратораКластера.Получить());
	ПарольАдминистратораКластера			= СокрЛП(Константы.ПарольАдминистратораКластера.Получить());
	ИмяАдминистратораИБ 					= СокрЛП(Константы.ИмяАдминистратораИБ.Получить());
	ПарольАдминистратораИБ 					= СокрЛП(Константы.ПарольАдминистратораИБ.Получить());
	
	// определим параметры текущей ИБ
	НомерТекущегоСоединения = НомерСоединенияИнформационнойБазы();
	ПодстрокиСтрокиСоединения  = обРазложитьСтрокуВМассивПодстрок(СтрокаСоединенияИнформационнойБазы(),";");
	Если обЭтоКлиентСервер() Тогда
		Если НЕ (ПодстрокиСтрокиСоединения.Количество()> 1 и Лев(ПодстрокиСтрокиСоединения[0], 5) = "Srvr=" и Лев(ПодстрокиСтрокиСоединения[1], 4) = "Ref=") Тогда
			ТекстСообщения = "Ошибка: не удалось определить параметры подключения к серверу";
			ЗаписьЖурналаРегистрации("Завершение работы пользователей", УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
			#Если Клиент Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + ТекстСообщения,СтатусСообщения.Важное);
			#КонецЕсли
			Возврат ТекстСообщения;
		Иначе
			ИмяСервера = ВРЕГ(Сред(ПодстрокиСтрокиСоединения[0],7, СтрДлина(ПодстрокиСтрокиСоединения[0]) - 7));
			ИмяИБ      = ВРЕГ(Сред(ПодстрокиСтрокиСоединения[1],6, СтрДлина(ПодстрокиСтрокиСоединения[1]) - 6));
		КонецЕсли;
	КонецЕсли; 
	
	// Настало время со всеми поступать жестоко, причем может быть даже ногами
	ЗаписьЖурналаРегистрации("Завершение работы конфигуратора подчиненной ИБ", УровеньЖурналаРегистрации.Предупреждение, , ,
	"Начало принудительного завершения работы: " + Строка(ТекущаяДата()) + "."
	+ "Наше соединение N " + НомерТекущегоСоединения);
	ЕстьРегАгент = Ложь;	// Есть ли у нас регистрация агента
	ComConnector = Новый COMОбъект(сфПолучитьИмяCOMСоединителя());
	// Подключаемся к агенту сервера 1С Предприятия
	ConToAgent1C = ComConnector.ConnectAgent("TCP://"+ИмяСервера+?(НомерПортаАгента>0 И Найти(ИмяСервера,":") = 0,":"+XMLСтрока(НомерПортаАгента),""));
	// Аутентифицируемся как администратор центрального сервера
	ConToAgent1C.AuthenticateAgent(ИмяАдминистратораСервера,ПарольАдминистратораСервера);
	// Получим список кластеров и в нем найдем свой, в котором мы сейчас работаем
	ClustersArray = ConToAgent1C.GetClusters();
	Для ТекущийИндекс = ClustersArray.GetLowerBound() По ClustersArray.GetUpperBound() Цикл
		// Среди множества прописанных кластеров (ну а вдруг :-) нам нужен только один
		Cluster = ClustersArray.GetValue(ТекущийИндекс);
		Если (ВРЕГ(Cluster.HostName) = ИмяСервера) Тогда // вот именно этот нас вполне устроит
			Прервать; // так что никакие другие кластеры нам не нужны
		КонецЕсли
	КонецЦикла;
	// А если не имеем кластер то и делать нам дальше нечего
	Если Cluster = НЕОПРЕДЕЛЕНО Тогда
		СтрОшибка = "Не удалось определить рабочий кластер серверов 1С предприятия";
		Возврат Ложь;
	КонецЕсли;
	
	// Попытаемся аутентифицироваться как администраторы кластера 
	Если НЕ ЕстьРегАгент Тогда
		Попытка // Тогда от спец-пользователя
			ConToAgent1C.Authenticate(Cluster,ИмяАдминистратораКластера,ПарольАдминистратораКластера);
			ЕстьРегАгент=Истина;
		Исключение
			СтрОшибка = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	// Обойдем все сервера и их рабочие процессы 
	WorkingServers = ConToAgent1C.GetWorkingServers(Cluster);
	Для Каждого CurrentServer Из WorkingServers Цикл
		WorkingProcesses = ConToAgent1C.GetServerWorkingProcesses(Cluster, CurrentServer);
		Если WorkingProcesses <> НЕОПРЕДЕЛЕНО Тогда
			Для Каждого CurrentProcess Из WorkingProcesses Цикл
				Если CurrentProcess.Running <> 1 Тогда
					Продолжить; // Этот процесс сейчас не работает так что нам не интересен
				КонецЕсли;
				ЕстьРегПроцесса  = Ложь;
				СтрокаСоединения = "TCP://"+CurrentProcess.HostName+":"+XMLСтрока(CurrentProcess.MainPort);
				ConToCurrProcess = ComConnector.ConnectWorkingProcess(СтрокаСоединения);
				Если ConToCurrProcess <> Неопределено Тогда
					InfoBasesArray = ConToCurrProcess.GetInfoBases();
					Если InfoBasesArray  <> НЕОПРЕДЕЛЕНО Тогда
						Для Каждого InfoBase ИЗ InfoBasesArray Цикл
							Если ВРЕГ(InfoBase.Name) <> ИмяИБ Тогда
								Продолжить; // Это не наша база - пропускаем
							КонецЕсли;
							// Восстановим админское подключение к базе (в случае ошибки и при переходе на другой рабочий процесс)
							Если НЕ ЕстьРегПроцесса Тогда
								Попытка
									ConToCurrProcess.AddAuthentication(ИмяАдминистратораИБ, ПарольАдминистратораИБ);
									ЕстьРегПроцесса=Истина;
								Исключение
									СтрОшибка=ОписаниеОшибки();
								КонецПопытки;
							КонецЕсли;
							// Получим и обойдем все соединения с нашей базой в текущем рабочем процессе
							IBaseConnArray=ConToCurrProcess.GetInfoBaseConnections(InfoBase);
							Если IBaseConnArray<>НЕОПРЕДЕЛЕНО Тогда
								Для Каждого IBaseConn Из IBaseConnArray Цикл
									UserName = IBaseConn.UserName;
									ConnID	 = IBaseConn.ConnID;
									AppID    = ВРЕГ(IBaseConn.AppID);
									Если (Найти(ВРег(AppID),ВРег("DESIGNER")) = 0) Тогда
										Продолжить; // выключаем только конфигуратор
									КонецЕсли;
									Попытка
										ConToCurrProcess.Disconnect(IBaseConn);
										ЗаписьЖурналаРегистрации("Завершение работы конфигуратора подчиненной ИБ", УровеньЖурналаРегистрации.Предупреждение, , ,
										"Отключено соединение: " + "User=["+UserName+"] ConnID=["+ConnID+"] AppID=["+AppID+"]");
									Исключение
										ЕстьРегПроцесса=Ложь;
									КонецПопытки;
								КонецЦикла; // По соединениям с текущей базой
								// Аккуратно подчищаем за собой объекты
								IBaseConn=НЕОПРЕДЕЛЕНО;
								IBaseConnArray=НЕОПРЕДЕЛЕНО;
							КонецЕсли;
						КонецЦикла; // По базам с которым есть соединения в текущем рабочем процессе сервера
						// Аккуратно подчищаем за собой объекты
						InfoBase=НЕОПРЕДЕЛЕНО;
						InfoBasesArray=НЕОПРЕДЕЛЕНО;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; // по рабочим процессам сервера
		КонецЕсли;
		// Аккуратно подчищаем за собой объекты
		ConToCurrProcess=НЕОПРЕДЕЛЕНО;
		CurrentProcess=НЕОПРЕДЕЛЕНО;
		WorkingProcesses=НЕОПРЕДЕЛЕНО;
	КонецЦикла; // по серверам
	// соединение валится не сразу, поэтому немного подождем
	ЗадержкаНужна = Истина;
	#Если Клиент Тогда
		Попытка
			Обработки.Защита.Создать().Компонента.Сон(3000);	// Подождем еще пока соединения отвалятся в пул
			ОбработкаПрерыванияПользователя();
			ЗадержкаНужна = Ложь;
		Исключение 
		КонецПопытки;
	#КонецЕсли
	Если ЗадержкаНужна Тогда
		Тек = 0;
		Для Инд = 1 По 2500000 Цикл
			Тек = Тек + 1; 
		КонецЦикла;
	КонецЕсли;
	// Проверяем сколько у нас осталось текущих соединений
	Соединения = ПолучитьСоединенияИнформационнойБазы();
	ЧислоСоединений = Соединения.Количество();
	Если ЧислоСоединений > 1 Тогда
		Для каждого Соединение Из Соединения Цикл
			ИмяПользователя = СокрЛП(Соединение.Пользователь);
			НомерСоединения = СокрЛП(Соединение.НомерСоединения);
			ИмяПриложения = СокрЛП(Соединение.ИмяПриложения);
			Если Не Соединение.НомерСоединения = НомерСоединенияИнформационнойБазы() Тогда
				Сообщение = "Не удалось разорвать соединение N " + НомерСоединения + " ( " + ИмяПриложения 
				+ ") пользователя " + ИмяПользователя;
				#Если Клиент Тогда
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + Сообщение, СтатусСообщения.Внимание);
				#КонецЕсли
				ЗаписьЖурналаРегистрации("Завершение работы конфигуратора подчиненной ИБ", УровеньЖурналаРегистрации.Предупреждение, , , Сообщение);
			КонецЕсли;
		КонецЦикла; 
		
		Сообщение = "Не удалось завершить работу конфигуратора подчиненной ИБ.";
		#Если Клиент Тогда
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
        Сообщить(ТекДата + Сообщение, СтатусСообщения.Важное);	// Пусть вызвавший завершение работы всех пользователей администратор увидит проблему
		#КонецЕсли
		ЗаписьЖурналаРегистрации("Завершение работы конфигуратора подчиненной ИБ", УровеньЖурналаРегистрации.Ошибка, , , Сообщение);
		Результат = Ложь; 
		Возврат Результат;	// Возврат результата полезен если данная функция была вызвана из COM-соединения 
	Иначе
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Сообщение = "Работа конфигуратора подчиненной ИБ успешно прекращена."; 
		ЗаписьЖурналаРегистрации("Завершение работы конфигуратора подчиненной ИБ", УровеньЖурналаРегистрации.Информация, , ,Сообщение);
		#Если Клиент Тогда
		Сообщить(ТекДата + Сообщение, СтатусСообщения.Информация);
		#КонецЕсли
		Результат = Истина; 
		Возврат Результат;	
	КонецЕсли;
	
КонецФункции // ()

// Выполняет закрытие лог-файл (ЗаписьТекста)
// для последующей записи в этот же файл сообщений
// в другом контексте
//
// Параметры
//  ЛогФайлОбъект  – ЗаписьТекста
//
Процедура ЗакрытьЛогФайл(ЛогФайлОбъект) Экспорт

	Если ЛогФайлОбъект <> Неопределено Тогда
		ЛогФайлОбъект.Закрыть();
		ЛогФайлОбъект = неопределено;
	КонецЕсли; 
	
КонецПроцедуры // ЗакрытьЛогФайл()

// Возвращает структуру с параметрами последнего обмена по узлу
//
// Параметры
//  УзелОбмена  – ПланОбменаСсылка.УдаленныеПодразделения – узел обмена
//
// Возвращаемое значение:
//   Структура   – параметры последнего обмена в разрезе узла обмена
//
Функция ПолучитьДанныеПоследнегоОбмена(УзелИБ) Экспорт

	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПараметрыОбмена.УзелИБ КАК УзелИБ,
	|	ПараметрыОбмена.РезультатПоследнейЗагрузки КАК РезультатПоследнейЗагрузки,
	|	ПараметрыОбмена.ДатаПоследнейЗагрузки КАК ДатаПоследнейЗагрузки,
	|	ПараметрыОбмена.РезультатПоследнейВыгрузки КАК РезультатПоследнейВыгрузки,
	|	ПараметрыОбмена.ДатаПоследнейВыгрузки КАК ДатаПоследнейВыгрузки,
	|	ПараметрыОбмена.ИмяФайлаПоследнейЗагрузки КАК ИмяФайлаПоследнейЗагрузки,
	|	ПараметрыОбмена.ИмяФайлаПоследнейВыгрузки КАК ИмяФайлаПоследнейВыгрузки
	|ИЗ
	|	РегистрСведений.ПараметрыОбменаДанными КАК ПараметрыОбмена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПараметрыОбмена.УзелИБ КАК УзелИБ,
	|			МАКСИМУМ(ПараметрыОбмена.ДатаПоследнейЗагрузки) КАК ДатаПоследнейЗагрузки
	|		ИЗ
	|			РегистрСведений.ПараметрыОбменаДанными КАК ПараметрыОбмена
	|		ГДЕ
	|			ПараметрыОбмена.УзелИБ =  &УзелИБ
	|		СГРУППИРОВАТЬ ПО
	|			ПараметрыОбмена.УзелИБ) КАК ПараметрыЗагрузка
	|		ПО ПараметрыОбмена.УзелИБ = ПараметрыЗагрузка.УзелИБ
	|			И ПараметрыОбмена.ДатаПоследнейЗагрузки = ПараметрыЗагрузка.ДатаПоследнейЗагрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПараметрыОбмена.УзелИБ КАК УзелИБ,
	|			МАКСИМУМ(ПараметрыОбмена.ДатаПоследнейВыгрузки) КАК ДатаПоследнейВыгрузки
	|		ИЗ
	|			РегистрСведений.ПараметрыОбменаДанными КАК ПараметрыОбмена
	|		ГДЕ
	|			ПараметрыОбмена.УзелИБ =  &УзелИБ
	|		СГРУППИРОВАТЬ ПО
	|			ПараметрыОбмена.УзелИБ) КАК ПараметрыВыгрузка
	|		ПО ПараметрыОбмена.УзелИБ = ПараметрыВыгрузка.УзелИБ
	|			И ПараметрыОбмена.ДатаПоследнейВыгрузки = ПараметрыВыгрузка.ДатаПоследнейВыгрузки
	|			И ПараметрыОбмена.УзелИБ = &УзелИБ";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("УзелИБ",УзелИБ);
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураОбмена = Новый Структура("УзелИБ,РезультатПоследнейЗагрузки,ДатаПоследнейЗагрузки,
		|РезультатПоследнейВыгрузки,ДатаПоследнейВыгрузки,ИмяФайлаПоследнейЗагрузки,
		|ИмяФайлаПоследнейВыгрузки",ПланыОбмена.УдаленныеПодразделения.ПустаяСсылка(),Ложь,'00010101',
		Ложь,'00010101',"","");
	Если Выборка.Следующий() Тогда
	    ЗаполнитьЗначенияСвойств(СтруктураОбмена,Выборка);
	КонецЕсли; 
	
	Возврат СтруктураОбмена;
	
	
КонецФункции // ПолучитьИмяФайлаПоследнегоОбмена()

// Производит непосредственное чтение сообщения обмена из файла
//
// Параметры
//  ИмяФайлаСообщения  – Строка – имя файла сообщения обмена
//  УзелОбмена  – ПланОбменаСсылка.УдаленныеПодразделения – ссылка на узел обмена
//  КоличествоЭлементовВТранзакции - Число
//
// Возвращаемое значение:
//	Число - номер сообщения обмена
//
Функция ПрочитатьСообщениеОбменаНаКлиенте(ИмяФайлаСообщения,УзелОбмена,КоличествоЭлементовВТранзакции) Экспорт
    НомерСообщения = 0;
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаСообщения);
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	НомерСообщения = ЧтениеСообщения.НомерСообщения;
	Если ЧтениеСообщения.Отправитель <> УзелОбмена Тогда
		ВызватьИсключение "Сообщение обмена предназначено не для этого узла!";  // прерываем сразу - легче от попыток не станет
	КонецЕсли;
	
	ПланыОбмена.ПрочитатьИзменения(ЧтениеСообщения, КоличествоЭлементовВТранзакции);
	
	ЧтениеСообщения.ЗакончитьЧтение();
	// закроем xml-файл
	Попытка 
		ЧтениеXML.Закрыть();
	Исключение 
		СтрСообщения = ИнформацияОбОшибке().Описание;
		ВызватьИсключение СтрСообщения; // не смогли закрыть - прерываем 
	КонецПопытки;
	
	Возврат НомерСообщения;
	
КонецФункции // пвПрочитатьСообщениеОбмена()

// Производит непосредственную запись сообщения обмена из файла
//
// Параметры
//  ИмяФайлаСообщения  – Строка – имя файла сообщения обмена
//  УзелОбмена  – ПланОбменаСсылка.УдаленныеПодразделения – ссылка на узел обмена
//  КоличествоЭлементовВТранзакции - Число
//
// Возвращаемое значение:
//	Число - номер сообщения обмена
//
Функция ЗаписатьСообщениеОбменаНаКлиенте(ИмяФайлаСообщения,УзелОбмена,КоличествоЭлементовВТранзакции) Экспорт
    НомерСообщения = 0;
	
	// Создаем объект записи XML
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайлаСообщения);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	// Создаем новое сообщение
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, УзелОбмена);
	
	НомерСообщения = ЗаписьСообщения.НомерСообщения;
	// сразу определим соответствие пространства имен
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsd", "http://www.w3.org/2001/XMLSchema");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8",  "http://v8.1c.ru/data");
	
	ПланыОбмена.ЗаписатьИзменения(ЗаписьСообщения, КоличествоЭлементовВТранзакции);
	ЗаписьСообщения.ЗакончитьЗапись();
	
	// закроем xml-файл
	Попытка 
		ЗаписьXML.Закрыть();
	Исключение 
		СтрСообщения = ИнформацияОбОшибке().Описание;
		ВызватьИсключение СтрСообщения; // не смогли закрыть - прерываем 
	КонецПопытки;
	
	Возврат НомерСообщения;
	
КонецФункции // ЗаписатьСообщениеОбмена()

// Процедура возвращает каталог временных файлов
//
Функция ПолучитьКаталогВременныхФайлов(ВыполнятьОбменНаКлиенте = Ложь) Экспорт
	
	Если ВыполнятьОбменНаКлиенте Тогда
		Возврат КаталогВременныхФайлов();
	Иначе
		Возврат сфПолучитьКаталогВременныхФайловНаСервере();
	КонецЕсли; 
		
КонецФункции


Функция ПолучитьОбязательныеПоля() Экспорт
	ОбязательныеПоля = Новый Массив;
	ОбязательныеПоля.Добавить("Category");
	ОбязательныеПоля.Добавить("GoodsType");
	ОбязательныеПоля.Добавить("Title");
	ОбязательныеПоля.Добавить("AdType");
	
	Возврат ОбязательныеПоля;
КонецФункции

Функция ПолучитьОбязательныеПоляШаблона() Экспорт
	ОбязательныеПоля = Новый Массив;
	ОбязательныеПоля.Добавить("Address");
	ОбязательныеПоля.Добавить("Category");
	ОбязательныеПоля.Добавить("Description");
	
	Возврат ОбязательныеПоля;
КонецФункции

Функция ПолучитьДополнительныеОбязательныеПоля(Category) Экспорт
	ОбязательныеПоля = Новый Массив;
	
	Возврат ОбязательныеПоля;
КонецФункции

Функция СнятьБезОстатковСВыгрузки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ЛичныеВещи.Ссылка КАК Ссылка,
	|	ns_ЛичныеВещи.Номенклатура КАК Номенклатура,
	|	ns_ЛичныеВещи.Характеристика КАК Характеристика
	|ИЗ
	|	Справочник.ns_ЛичныеВещи КАК ns_ЛичныеВещи
	|ГДЕ
	|	ns_ЛичныеВещи.Выгружать = ИСТИНА";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Выгрузка = РезультатЗапроса.Выгрузить();
	Номенклатура = Выгрузка.ВыгрузитьКолонку("Номенклатура");
	Остатки = ns_Переопределяемый.ПолучитьТаблицуОстатков(Номенклатура);
	
	Для каждого Стр Из Выгрузка Цикл
		Если ЗначениеЗаполнено(Стр.Характеристика) Тогда
			Идентификатор = Строка(Стр.Номенклатура.УникальныйИдентификатор()) + "#" + Строка(Стр.Характеристика.УникальныйИдентификатор());
		Иначе
			Идентификатор = Строка(Стр.Номенклатура.УникальныйИдентификатор());
		КонецЕсли; 
		
		Если Остатки[Идентификатор] = Неопределено Тогда
			СпрОбъект = Стр.Ссылка.ПолучитьОбъект();
			СпрОбъект.Выгружать = Ложь;
			СпрОбъект.Записать();
		КонецЕсли;	
	КонецЦикла;
	
КонецФункции

Функция ПроверитьОшибкиПриВыгрузке(Данные, ТаблицаОшибок, Картинки = Неопределено) Экспорт
	ЕстьОшибка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Данные.Category) Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле Category", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Данные.Title) Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле Title", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	КонецЕсли;

	Если Картинки = Неопределено Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не указана картинка товара", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	Иначе
		КартинкиТовара = Картинки[Данные.Ссылка];
		Если КартинкиТовара = Неопределено Тогда
			ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не указана картинка товара", Данные, ТаблицаОшибок);
			ЕстьОшибка = Истина; 
		ИначеЕсли КартинкиТовара.Количество() = 0 Тогда	
			ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не указана картинка товара", Данные, ТаблицаОшибок);
			ЕстьОшибка = Истина; 
		КонецЕсли;
	КонецЕсли;	
	
	ДопПоля = ПолучитьДополнительныеОбязательныеПоля(Данные.Category);
	Для каждого ТекПоле Из ДопПоля Цикл
		Если НЕ ЗначениеЗаполнено(Данные[ТекПоле]) Тогда
			ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле " + ТекПоле, Данные, ТаблицаОшибок);
			ЕстьОшибка = Истина;
		КонецЕсли;
	КонецЦикла;        
	
	Возврат ЕстьОшибка;
КонецФункции

Функция УстановитьВидимость_Apparel(Category, GoodsType) Экспорт
	Если Category = ns_ЛичныеВещиПовтИсп.ПолучитьДетскаяОдеждаИОбувь() Тогда
		Возврат Истина;	
	КонецЕсли; 
	
	Если Category = ns_ЛичныеВещиПовтИсп.ПолучитьОдеждаОбувьАксессуары() Тогда
		Если GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьМужскаяОдежда() ИЛИ
			GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьЖенскаяОдежда() ИЛИ
			GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьАксессуары() Тогда
			Возврат Истина;	
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Ложь;
КонецФункции

Функция УстановитьВидимость_ApparelType(Category, GoodsType, Apparel) Экспорт
	Если НЕ Category = ns_ЛичныеВещиПовтИсп.ПолучитьОдеждаОбувьАксессуары() Тогда
		Возврат Ложь;	
	КонецЕсли; 
	
	Если НЕ GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьМужскаяОдежда() И
		НЕ GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьЖенскаяОдежда() Тогда
		Возврат Ложь;	
	КонецЕсли; 
	
	Если Apparel = ns_ЛичныеВещиПовтИсп.ПолучитьМужскаяОдежда_ВерхняяОдежда() ИЛИ
		Apparel = ns_ЛичныеВещиПовтИсп.ПолучитьЖенскаяОдежда_ВерхняяОдежда() ИЛИ
		Apparel = ns_ЛичныеВещиПовтИсп.ПолучитьЖенскаяОдежда_Обувь() ИЛИ
		Apparel = ns_ЛичныеВещиПовтИсп.ПолучитьМужскаяОдежда_Обувь()Тогда
		Возврат Истина;
	КонецЕсли; 
	
	Возврат Ложь;
КонецФункции

Функция УстановитьВидимость_Size(Category, GoodsType, Apparel) Экспорт
	Если НЕ Category = ns_ЛичныеВещиПовтИсп.ПолучитьОдеждаОбувьАксессуары() И
		НЕ Category = ns_ЛичныеВещиПовтИсп.ПолучитьДетскаяОдеждаИОбувь() Тогда
		Возврат Ложь;	
	КонецЕсли; 
	
	Если GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьМужскаяОдежда() ИЛИ
		GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьЖенскаяОдежда() ИЛИ
		GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьДляДевочек() ИЛИ
		GoodsType = ns_ЛичныеВещиПовтИсп.ПолучитьДляМальчиков() Тогда
		Возврат Истина;	
	КонецЕсли; 
	
	Возврат Ложь;
КонецФункции
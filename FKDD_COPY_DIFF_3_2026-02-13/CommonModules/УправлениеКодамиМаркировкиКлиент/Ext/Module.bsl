
#Область ПрограммныйИнтерфейс

Процедура СформироватьДанныеОтправки(Форма, Документ, Действие, ФайлСформирован = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Документ.Ссылка)
		ИЛИ Форма.Модифицированность Тогда
		Сообщить("Перед выполнением необходимо записать документ!");
		Возврат;
	КонецЕсли;
	
	Если Действие = "ОтправкаЗапросаНаПолучениеКодовМаркировки" Тогда
		ОтправкаЗапросаНаПолучениеКодовМаркировки(Документ, ФайлСформирован);
	ИначеЕсли Действие = "ОтправкаОписанияОстатков" Тогда
		ОтправкаЗапросаНаОписаниеОстатковНоменклатуры(Документ);
	ИначеЕсли Действие = "ОтправкаЗапросаВводаВОборотКодовМаркировки" Тогда
		ОтправкаЗапросаВводаВОборотКодовМаркировки(Документ,  ФайлСформирован);
	ИначеЕсли Действие = "ОтправкаЗапросаПеремаркировки" Тогда
		ОтправкаЗапросаПеремаркировки(Документ, ФайлСформирован);
	ИначеЕсли Действие = "ОтправкаЗапросаВыводаИзОборота" Тогда
		ОтправкаЗапросаВыводаИзОборота(Документ, ФайлСформирован);
	ИначеЕсли Действие = "ОтправкаЗапросаСписанияКодаМаркировки" Тогда
		ОтправкаЗапросаСписанияКодаМаркировки(Документ, ФайлСформирован);
	ИначеЕсли Действие = "ОтправкаЗапросаВозвратаВОборот" Тогда
		ОтправкаЗапросаВозвратаВОборот(Документ, ФайлСформирован);
	ИначеЕсли Действие = "ОтправкаЗапросаОтгрузкаТоваров" Тогда
		ОтправкаЗапросаОтгрузкаТоваров(Документ, ФайлСформирован);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьКодыМаркировки(Форма, Документ, ДанныеЗагружены = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Документ.Ссылка)
		ИЛИ Форма.Модифицированность Тогда
		Сообщить("Перед выполнением необходимо записать документ!");
		Возврат;
	КонецЕсли;
	
	МассивКодовМаркировки = Новый Массив;
	
	ЗагрузитьКодыМаркировкиЧерезФайл(Документ, МассивКодовМаркировки);
	
	ДанныеЗагружены = (МассивКодовМаркировки.Количество() > 0);
	
	Если ДанныеЗагружены Тогда
		Если Вопрос("Коды маркировок загружены. Перейти к печати?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		СписокКодов = Новый СписокЗначений;
		СписокКодов.ЗагрузитьЗначения(МассивКодовМаркировки);
		ФормаПечати = ПолучитьФорму("Обработка.ПечатьКодовМаркировки.Форма", , Форма);
		ФормаПечати.ЗаказНаЭмиссию = Документ.Ссылка;
		ФормаПечати.СписокКодовМаркировки = СписокКодов;
		ФормаПечати.ОткрытьМодально();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтправкаЗапросаНаПолучениеКодовМаркировки(Документ, ФайлСформирован)
	
	// Проверим, что в ТЧ у всех указан GTIN
	СтрокиБезGTIN = Новый Массив;
	Для Каждого ТекущаяСтрока Из Документ.Товары Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.GTIN) Тогда
			СтрокиБезGTIN.Добавить(ТекущаяСтрока.НомерСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиБезGTIN.Количество() > 0 Тогда
		Сообщить("Таблица <Товары>. Не заполнен GTIN в строках: " + СтрСоединить(СтрокиБезGTIN, ", "));
		Возврат;
	КонецЕсли;
	
	СформироватьФайлЗаказовКодовМаркировки(Документ, ФайлСформирован);
	
КонецПроцедуры

Процедура ОтправкаЗапросаНаОписаниеОстатковНоменклатуры(Документ)
	
	СформироватьФайлОстатковНоменклатуры(Документ);
	
КонецПроцедуры

Процедура ОтправкаЗапросаВводаВОборотКодовМаркировки(Документ, ФайлСформирован)
	
	Если Документ.СпособВводаВОборот = Перечисления.СпособыВводаВОборот.МаркировкаОстатков Тогда
		СформироватьФайлВводаВОборотМаркировкаОстатков(Документ, ФайлСформирован);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучениеКаталогаФайлов(ИмяФайла, Расширение = "xml", Фильтр = "(*.xml)|*.xml")
	
	Результат = Ложь;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.Заголовок = "Сохранить файл ...";
	ДиалогВыбора.ПолноеИмяФайла = ИмяФайла;
	ДиалогВыбора.Расширение = Расширение;
	ДиалогВыбора.Фильтр = Фильтр;
	
	Если ДиалогВыбора.Выбрать()
		И НЕ ПустаяСтрока(ДиалогВыбора.ПолноеИмяФайла) Тогда
		ИмяФайла =  ДиалогВыбора.ПолноеИмяФайла;
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьКодМаркировкиДляXML(КодМаркировки)
	
	КодМаркировки = СтрЗаменить(КодМаркировки, "&", "&amp");
	КодМаркировки = СтрЗаменить(КодМаркировки, Символ(34), "&quot"); // кавычки
	КодМаркировки = СтрЗаменить(КодМаркировки, "'", "&apos"); // апостроф
	КодМаркировки = СтрЗаменить(КодМаркировки, ">", "&gt");
	КодМаркировки = СтрЗаменить(КодМаркировки, "<", "&lt");
	
КонецПроцедуры

Процедура ЗаписьТекстаВФайл(ЗаписьXML, ИмяЭлемента, Текст)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
	ЗаписьXML.ЗаписатьТекст(Текст);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура СформироватьФайлЗаказовКодовМаркировки(Документ, ФайлСформирован)
	
	ВидПродукции = УправлениеКодамиМаркировкиКлиентСервер.ВидПродуции(Документ);
	ШаблонЭтикетки = УправлениеКодамиМаркировкиКлиентСервер.ИдентификаторШаблонаЭтикетки(Документ);
	СпособВыпуска = УправлениеКодамиМаркировкиКлиентСервер.СпособВыпускаВОборот(Документ.СпособВыпускаВОборот);
	
	Если ВидПродукции = Неопределено
		ИЛИ СпособВыпуска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Заказ_кодов_маркировки_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("order");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьАтрибут("xmlns", "urn:oms.order");
	ЗаписьXML.ЗаписатьАтрибут("xsi:schemaLocation", "urn:oms.order order-v5.5.xsd");
	
	// Заполним данными документа 
	ЗаписьXML.ЗаписатьНачалоЭлемента(ВидПродукции);
	
	ЗаписьТекстаВФайл(ЗаписьXML, "productGroup", ВидПродукции);
	ЗаписьТекстаВФайл(ЗаписьXML, "contactPerson", Документ.Ответственный.Наименование);
	ЗаписьТекстаВФайл(ЗаписьXML, "releaseMethodType", СпособВыпуска);
	ЗаписьТекстаВФайл(ЗаписьXML, "createMethodType", "SELF_MADE");  // SELF_MADE или CEM
	
	Если ЗначениеЗаполнено(Документ.ИдентификаторПроизводственногоЗаказа) Тогда
		ЗаписьТекстаВФайл(ЗаписьXML, "productionOrderId", Документ.ИдентификаторПроизводственногоЗаказа);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ.НомерДоговораСОператором)
		И ЗначениеЗаполнено(Документ.ДатаДоговораСОператором) Тогда
		ЗаписьТекстаВФайл(ЗаписьXML, "contractNumber", Документ.НомерДоговораСОператором);
		ЗаписьТекстаВФайл(ЗаписьXML, "contractDate", Формат(Документ.ДатаДоговораСОператором, "ДФ=yyyy-MM-dd"));
	КонецЕсли;
	
	УказыватьЕдиницу = (Документ.ТоварнаяГруппа = Справочники.ТипыМаркировки.ЛегкаяПромышленность)
		ИЛИ (Документ.ТоварнаяГруппа = Справочники.ТипыМаркировки.Парфюмерия);
	
	// Заполнение ТЧ Товары
	ЗаписьXML.ЗаписатьНачалоЭлемента("products");
	
	СтруктураПоиска = Новый Структура("ИдентификаторСтроки");
	Для Каждого ТекущаяСтрока Из Документ.Товары Цикл
		
		ЗаполнениеСерийныхНомеров =
			УправлениеКодамиМаркировкиКлиентСервер.СпособЗаполненияСерийныхНомеров(
				ТекущаяСтрока.СпособЗаполненияСерийногоНомера);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("product");
		
		ЗаписьТекстаВФайл(ЗаписьXML, "gtin", ТекущаяСтрока.GTIN);
		ЗаписьТекстаВФайл(ЗаписьXML, "quantity", Строка(ТекущаяСтрока.Количество));
		ЗаписьТекстаВФайл(ЗаписьXML, "serialNumberType", ЗаполнениеСерийныхНомеров);
		
		Если ЗаполнениеСерийныхНомеров = "SELF_MADE" Тогда
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("serialNumbers");
			
			// Заполняем список созданных серийных номеров
			СтруктураПоиска.ИдентификаторСтроки = ТекущаяСтрока.ИдентификаторСтроки;
			НайденныеСтроки = Документ.СерийныеНомераКодовМаркировки.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого ТекущийНомер Из НайденныеСтроки Цикл
				ЗаписьТекстаВФайл(ЗаписьXML, "serialNumber", ТекущийНомер.СерийныйНомерМаркировки);
			КонецЦикла;
			
			ЗаписьXML.ЗаписатьКонецЭлемента();
			
		КонецЕсли;
		
		Если УказыватьЕдиницу Тогда
			ЗаписьТекстаВФайл(ЗаписьXML, "cisType", "UNIT"); // Единица товара
		КонецЕсли;
		
		ЗаписьТекстаВФайл(ЗаписьXML, "templateId", ШаблонЭтикетки);
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры

Процедура СформироватьФайлОстатковНоменклатуры(Документ)
	
	// Проверить, что есть товар, для которого необходимо сформировать описание
	ТаблицаТоваров = УправлениеКодамиМаркировкиКлиентСервер.ПолучитьТаблицуУпращенногоВводаМаркировок(Документ);
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Сообщить("У всех строк указан GTIN или у некоторых строк не заполнена модель и/или код ТНВЭД");
		Возврат;
	КонецЕсли;
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Описание_остатков_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла, "csv", "(*.csv)|*.csv") Тогда
		Возврат;
	КонецЕсли;
	
	Разделитель = ",";
	
	ТекстовыйФайл = Новый ТекстовыйДокумент();
	
	ТекстовыйФайл.ДобавитьСтроку(СтрШаблон("ИНН участника оборота%1Версия", Разделитель));
	ТекстовыйФайл.ДобавитьСтроку(СтрШаблон("%1%2%3", СокрЛП(Документ.Организация.ИНН), Разделитель, "2"));
	ТекстовыйФайл.ДобавитьСтроку(
		СтрШаблон("Список товаров%1%2%3%4%5%6", Разделитель, Разделитель, Разделитель, Разделитель, Разделитель, Разделитель));
	ТекстовыйФайл.ДобавитьСтроку(
		СтрШаблон("Код товарной номенклатуры (2 знака)%1Торговая Марка Бренд%2Наименование товара%3Целевой пол%4"
		+ "Способ ввода товара в оборот%5Возраст потребителя%6Модель / артикул производителя",
		Разделитель, Разделитель, Разделитель, Разделитель, Разделитель, Разделитель));
		
	Для Каждого ТекущаяСтрока Из ТаблицаТоваров Цикл
		
		ТекстовыйФайл.ДобавитьСтроку(
			ТекущаяСтрока.Код + Разделитель + Разделитель + Разделитель
			+ Разделитель + Разделитель + Разделитель + ТекущаяСтрока.model);
		
	КонецЦикла;
	
	ТекстовыйФайл.УстановитьТекст(СтрЗаменить(ТекстовыйФайл.ПолучитьТекст() + Символы.ПС, Символы.ПС + Символы.ПС, ""));
	ТекстовыйФайл.Записать(ИмяФайла, КодировкаТекста.UTF8);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьФайлВводаВОборотМаркировкаОстатков(Документ, ФайлСформирован)
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Маркировка_остатков_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Заполним данными документа 
	ЗаписьXML.ЗаписатьНачалоЭлемента("vvod_ostatky");
	ЗаписьXML.ЗаписатьАтрибут("version", "2");
	ЗаписьXML.ЗаписатьАтрибут("action_id", "5.4");
	
	ЗаписьТекстаВФайл(ЗаписьXML, "trade_participant_inn",  СокрЛП(Документ.Организация.ИНН));
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("products_list");
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	Для Каждого ТекущаяСтрока Из Документ.КодыМаркировки Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		// Заполнение ТЧ Товары
		ЗаписьXML.ЗаписатьНачалоЭлемента("product");
		
		Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		// Замена по специфике csv
		Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
		КИ = Идентификатор01 + Идентификатор21;
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("ki");
		ЗаписьXML.ЗаписатьСекциюCDATA(КИ);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		// Параметры по ТЧ Товары
		НайденнаяСтрока = Документ.Товары.Найти(ТекущаяСтрока.ИдентификаторТовара, "ИдентификаторТовара");
		
		Если ЗначениеЗаполнено(НайденнаяСтрока.СтранаПроизводства) Тогда
			СтранаПроизводства = НайденнаяСтрока.СтранаПроизводства.Код;
			ЗаписьТекстаВФайл(ЗаписьXML, "country", СтранаПроизводства);
		КонецЕсли;
		
		ДатаРегистрации = "";
		Если ЗначениеЗаполнено(НайденнаяСтрока.ДатаРегистрацииДТ) Тогда
			ДатаРегистрации = Формат(НайденнаяСтрока.ДатаРегистрацииДТ, "ДФ=dd.MM.yyyy");
			ЗаписьТекстаВФайл(ЗаписьXML, "declaration_date", ДатаРегистрации);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденнаяСтрока.РегистрационныйНомерДТ) Тогда
			РегистрацинныйНомер = НайденнаяСтрока.РегистрационныйНомерДТ;
			ЗаписьТекстаВФайл(ЗаписьXML, "declaration_number", РегистрацинныйНомер);
		КонецЕсли;
		
		ВидДокумента = УправлениеКодамиМаркировкиКлиентСервер.ВидДокументаСоответствия(
			НайденнаяСтрока.ВидДокументаРегистрации);
			
		Если ЗначениеЗаполнено(ВидДокумента) Тогда
			ЗаписьТекстаВФайл(ЗаписьXML, "certificate_type", ВидДокумента);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденнаяСтрока.НомерДокументаСоответствия) Тогда
			НомерДокументаСоответствия = НайденнаяСтрока.НомерДокументаСоответствия;
			ЗаписьТекстаВФайл(ЗаписьXML, "certificate_number", НомерДокументаСоответствия);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденнаяСтрока.ДатаДокументаСоответсвия) Тогда
			ДатаДокументаСоответсвия = Формат(НайденнаяСтрока.ДатаДокументаСоответсвия, "ДФ=dd.MM.yyyy");
			ЗаписьТекстаВФайл(ЗаписьXML, "certificate_date", ДатаДокументаСоответсвия);
		КонецЕсли;
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	
	// Из-за специфики работы записи нужно удалить лишнее
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяФайла);
	ТекстФайла = ТекстовыйДокумент.ПолучитьТекст();
	ТекстФайла = СтрЗаменить(ТекстФайла, "<ki>" + Символы.ПС, "<ki>");
	
	Пока СтрНайти(ТекстФайла, "	<![CDATA") > 0 Цикл
		ТекстФайла = СтрЗаменить(ТекстФайла, "	<![CDATA", "<![CDATA");
	КонецЦикла;
	
	ТекстовыйДокумент.УстановитьТекст(ТекстФайла);
	ТекстовыйДокумент.Записать(ИмяФайла);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры

Процедура ОтправкаЗапросаПеремаркировки(Документ, ФайлСформирован)
	
	// Если не запонен новый код, то не создаем файл
	Для Каждого ТекущаяСтрока Из Документ.Товары Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.КодМаркировкиНовый) Тогда
			Сообщить("Имеются строки, в которых не указан новый код маркировки. Выгрузка файла отменена.");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Перемаркировка_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла, "csv", "(*.csv)|*.csv") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоШина = (Документ.ТоварнаяГруппа = Справочники.ТипыМаркировки.ШиныИАвтопокрышки);
	ЭтоОбувь = (Документ.ТоварнаяГруппа = Справочники.ТипыМаркировки.ОбувныеТовары);
	ЭтоВозвратРозница = (Документ.ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа);
	ЭтоВозвратДистанционный = (Документ.ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиДистанционнаяПродажа);
	ПричинаПеремаркировки = УправлениеКодамиМаркировкиКлиентСервер.ПричинаПеремаркировки(Документ.ПричинаПеремаркировки);
	
	
	
	ТекстовыйФайл = Новый ТекстовыйДокумент();
	
	ТекстовыйФайл.ДобавитьСтроку("ИНН участника оборота,Дата перемаркировки,Причина перемаркировки,Версия");
	ТекстовыйФайл.ДобавитьСтроку(СтрШаблон("%1,%2,%3,%4",
			СокрЛП(Документ.Организация.ИНН),
			Формат(Документ.Дата, "ДФ=dd.MM.yyyy"),
			ПричинаПеремаркировки,
			"5"));
	ТекстовыйФайл.ДобавитьСтроку("Параметры товаров,,,,,,,,,,,");
	ТекстовыйФайл.ДобавитьСтроку(
		НСтр("ru = 'Предыдущий КИ,Новый КИ,Код товарной номерклатуры,Дата перемаркировки,Товар оплачен,Тип первичного документа,"
		+ "Наименование первичного документа,Номер первичного документа,Дата первичного документа,"
		+ """Вид документа, подтверждающего соответствие"",""Номер документа, подтверждающего соответствие"","
		+ """Дата документа, подтверждающего соответствие"", Страна производства, Цвет, Размер'"));
		
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	Для Каждого ТекущаяСтрока Из Документ.Товары Цикл
		
		НоваяСтрока = Новый Массив;
		
		КИПредыдущая = "";
		Если ЗначениеЗаполнено(ТекущаяСтрока.КодМаркировки) Тогда
			СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
			
			Если НЕ СтруктураМаркировки.Разобран Тогда
				Продолжить;
			КонецЕсли;
			
			Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
			// Замена по специфике csv
			Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
			
			КИПредыдущая = Идентификатор01 + Идентификатор21;
			КИПредыдущая = СтрЗаменить(КИПредыдущая, """", """""");
			
		КонецЕсли;
		НоваяСтрока.Добавить("""" + КИПредыдущая + """");
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировкиНовый);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		// Замена по специфике csv
		Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
		КИ = Идентификатор01 + Идентификатор21;
		КИ = СтрЗаменить(КИ, """", """""");
		НоваяСтрока.Добавить("""" + КИ + """");
		
		// Код ТНВЭД
		НоваяСтрока.Добавить(ТекущаяСтрока.КодТНВЭД.Код);
		
		НоваяСтрока.Добавить(Формат(Документ.Дата, "ДФ=dd.MM.yyyy"));
		
		Если ЭтоВозвратДистанционный ИЛИ ЭтоВозвратРозница Тогда
			
			НоваяСтрока.Добавить(ТекущаяСтрока.Оплачен);
			
			Если ТекущаяСтрока.Оплачен = "Да" ИЛИ ЭтоВозвратРозница Тогда
				ВидДокумента = УправлениеКодамиМаркировкиКлиентСервер.ВидПервичногоДокументаCSV(ТекущаяСтрока.ВидПервичногоДокумента);
				
				НоваяСтрока.Добавить(ВидДокумента);
				НоваяСтрока.Добавить(ТекущаяСтрока.НаименованиеПервичногоДокумента);
				НоваяСтрока.Добавить(ТекущаяСтрока.НомерПервичногоДокумента);
				НоваяСтрока.Добавить(Формат(ТекущаяСтрока.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"));
			КонецЕсли;
			
		Иначе
			НоваяСтрока.Добавить("");
			НоваяСтрока.Добавить("");
			НоваяСтрока.Добавить("");
			НоваяСтрока.Добавить("");
			НоваяСтрока.Добавить("");
		КонецЕсли;
		
		ВидДокумента = УправлениеКодамиМаркировкиКлиентСервер.ВидДокументаСоответствияCSV(
			ТекущаяСтрока.ВидДокументаРегистрации);
		
		НоваяСтрока.Добавить(ВидДокумента);
		НоваяСтрока.Добавить(ТекущаяСтрока.НомерДокументаСоответствия);
		НоваяСтрока.Добавить(Формат(ТекущаяСтрока.ДатаДокументаСоответствия, "ДФ=dd.MM.yyyy"));
		
		Если НЕ ЭтоШина Тогда
			НоваяСтрока.Добавить(?(ЗначениеЗаполнено(ТекущаяСтрока.СтранаПроизводства), ТекущаяСтрока.СтранаПроизводства.Код, ""));
		Иначе
			НоваяСтрока.Добавить("");
		КонецЕсли;
		
		Если ЭтоОбувь Тогда
			НоваяСтрока.Добавить(ТекущаяСтрока.Цвет);
			НоваяСтрока.Добавить(ТекущаяСтрока.Размер);
		Иначе
			НоваяСтрока.Добавить("");
			НоваяСтрока.Добавить("");
		КонецЕсли;
		
		ТекстовыйФайл.ДобавитьСтроку(СтрСоединить(НоваяСтрока, ","));
		
	КонецЦикла;
	
	ТекстовыйФайл.УстановитьТекст(СтрЗаменить(ТекстовыйФайл.ПолучитьТекст() + Символы.ПС, Символы.ПС + Символы.ПС, ""));
	ТекстовыйФайл.Записать(ИмяФайла, КодировкаТекста.UTF8);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры // ОтправкаЗапросаПеремаркировки()

Процедура ОтправкаЗапросаВыводаИзОборота(Документ, ФайлСформирован)
	
	ПричинаВывода = УправлениеКодамиМаркировкиКлиентСервер.ПричинаВыводаИзОборотаCSV(Документ.ПричинаВывода);
	
	Если НЕ ЗначениеЗаполнено(ПричинаВывода) Тогда
		Возврат;
	КонецЕсли;
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Вывод_из_оборота_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла, "csv", "(*.csv)|*.csv") Тогда
		Возврат;
	КонецЕсли;
	
	ВидПервичногоДокумента = УправлениеКодамиМаркировкиКлиентСервер.ВидПервичногоДокументаCSV(Документ.ВидПервичногоДокумента);
	
	НаименованиеПервичногоДокумента = "";
	Если Документ.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументов.Прочее Тогда
		НаименованиеПервичногоДокумента = Документ.НаименованиеПервичногоДокумента;
	КонецЕсли;
	ДатаПервичногоДокумента = Формат(Документ.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy");
	НомерПервичногоДокумента = Документ.НомерПервичногоДокумента;
	
	ТекстовыйФайл = Новый ТекстовыйДокумент();
	
	ТекстовыйФайл.ДобавитьСтроку(
		"ИНН участника оборота,ИНН покупателя,Причина вывода из оборота,Другая причина вывода из оборота,"
		+ "Дата вывода из оборота,Тип первичного документа,Номер первичного документа,Дата первичного документа,"
		+ "Наименование первичного документа,Идентификатор ФИАС,Идентификатор госконтракта,Регистрационный номер ККТ,Версия");
	СтрокаШапкиФайлаОбмена1 = СтрШаблон("%1,%2,%3,%4,%5,%6,%7,%8,%9,%10",
			СокрЛП(Документ.Организация.ИНН),
			?(Документ.ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.БезвозмезднаяПередача, СокрЛП(Документ.Контрагент.ИНН), ""),
			ПричинаВывода,
			?(ПустаяСтрока(Документ.ОписаниеПричины),"""""","""" + Документ.ОписаниеПричины + """"),
			Формат(Документ.ДатаВыводаИзОборота, "ДФ=dd.MM.yyyy"),
			"",
			"",
			"",
			"",
			СокрЛП(Документ.АдресМестаВыбытия));
	СтрокаШапкиФайлаОбмена2 = СтрШаблон("%1,%2,%3,%4",
			"",
			СокрЛП(Документ.ИдентификаторГосударственногоКонтракта),
			"",
			"7");
	ТекстовыйФайл.ДобавитьСтроку(СтрокаШапкиФайлаОбмена1 + СтрокаШапкиФайлаОбмена2);
	ТекстовыйФайл.ДобавитьСтроку("Параметры товаров,,,,,");
	ТекстовыйФайл.ДобавитьСтроку("КИ,Цена за единицу,Тип первичного документа,Номер первичного документа,"
		+ "Дата первичного документа,Наименование первичного документа");

	Если Документ.ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ДистанционнаяПродажа
		 ИЛИ Документ.ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоОбразцам
		 ИЛИ Документ.ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.РозничнаяПродажа
		 ИЛИ Документ.ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.БезвозмезднаяПередача Тогда
		 ПередаватьЦену = Истина;
	Иначе
		 ПередаватьЦену = Ложь; 
	КонецЕсли;
		
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	Для Каждого ТекущаяСтрока Из Документ.КодыМаркировки Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		// Замена по специфике csv
		Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
		КИ = Идентификатор01 + Идентификатор21;
		КИ = СтрЗаменить(КИ, """", """""");
		
		// Параметры по ТЧ Товары
		НайденнаяСтрока = Документ.Товары.Найти(ТекущаяСтрока.ИдентификаторТовара, "ИдентификаторТовара");
		
		Цена = СтрЗаменить(Строка(НайденнаяСтрока.Цена * 100), Символы.НПП, "");
		
		ВидПервичногоДокументаСтроки = "";
		НомерПервичногоДокументаСтроки = "";
		ДатаПервичногоДокументаСтроки = "";
		НаименованиеПервичногоДокументаСтроки = "";
		Если ЗначениеЗаполнено(НайденнаяСтрока.ВидПервичногоДокумента) Тогда
			ВидПервичногоДокументаСтроки = УправлениеКодамиМаркировкиКлиентСервер.ВидПервичногоДокументаCSV(НайденнаяСтрока.ВидПервичногоДокумента);
			НомерПервичногоДокументаСтроки = НайденнаяСтрока.НомерПервичногоДокумента;
			ДатаПервичногоДокументаСтроки = Формат(НайденнаяСтрока.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy");
			Если НайденнаяСтрока.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументов.Прочее Тогда
				НаименованиеПервичногоДокументаСтроки = НайденнаяСтрока.НаименованиеПервичногоДокумента;
			КонецЕсли;
		Иначе
			ВидПервичногоДокументаСтроки = ВидПервичногоДокумента;
			НомерПервичногоДокументаСтроки = НомерПервичногоДокумента;
			ДатаПервичногоДокументаСтроки = ДатаПервичногоДокумента;
			НаименованиеПервичногоДокументаСтроки = НаименованиеПервичногоДокумента;
		КонецЕсли;
		
		ТекстовыйФайл.ДобавитьСтроку(
			СтрШаблон(
				"%1,%2,%3,%4,%5,%6",
				"""" + КИ + """",
				?(ПередаватьЦену, Цена, ""),
				ВидПервичногоДокументаСтроки,
				НомерПервичногоДокументаСтроки,
				ДатаПервичногоДокументаСтроки,
				НаименованиеПервичногоДокументаСтроки));
		
	КонецЦикла;
	
	ТекстовыйФайл.УстановитьТекст(СтрЗаменить(ТекстовыйФайл.ПолучитьТекст() + Символы.ПС, Символы.ПС + Символы.ПС, ""));
	ТекстовыйФайл.Записать(ИмяФайла, КодировкаТекста.UTF8);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры // ОтправкаЗапросаВыводаИзОборота()

Процедура ОтправкаЗапросаСписанияКодаМаркировки(Документ, ФайлСформирован)
	
	ПричинаСписания = УправлениеКодамиМаркировкиКлиентСервер.ПричинаСписанияКодаМаркировки(Документ.ПричинаСписания);
	
	Если НЕ ЗначениеЗаполнено(ПричинаСписания) Тогда
		Возврат;
	КонецЕсли;
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Списание_кодов_маркировки_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	// Сформируем файл
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Заполним данными документа 
	ЗаписьXML.ЗаписатьНачалоЭлемента("km_cancellation");
	ЗаписьXML.ЗаписатьАтрибут("action_id", "14");
	ЗаписьXML.ЗаписатьАтрибут("version", "2");
	
	ЗаписьТекстаВФайл(ЗаписьXML, "trade_participant_inn",  СокрЛП(Документ.Организация.ИНН));
	ЗаписьТекстаВФайл(ЗаписьXML, "cancellation_reason", ПричинаСписания);
	ЗаписьТекстаВФайл(ЗаписьXML, "cancellation_document_date", Формат(Документ.Дата, "ДФ=dd.MM.yyyy"));
	ЗаписьТекстаВФайл(ЗаписьXML, "cancellation_document_number", Документ.Номер);
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("km_list");
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	Для Каждого ТекущаяСтрока Из Документ.Товары Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		// Заполнение ТЧ Товары
		ЗаписьXML.ЗаписатьНачалоЭлемента("km");
		
		Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		// Замена по специфике csv
		Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
		КИ = Идентификатор01 + Идентификатор21;
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("kit");
		ЗаписьXML.ЗаписатьСекциюCDATA(КИ);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ПричинаСписания = УправлениеКодамиМаркировкиКлиентСервер.ПричинаСписанияКодаМаркировки(ТекущаяСтрока.ПричинаСписания);
		
		Если НЕ ЗначениеЗаполнено(ПричинаСписания) Тогда
			Возврат;
		КонецЕсли;
		
		ЗаписьТекстаВФайл(ЗаписьXML, "cancellation_reason", ПричинаСписания);
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	
	// Из-за специфики работы записи нужно удалить лишнее
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяФайла);
	ТекстФайла = ТекстовыйДокумент.ПолучитьТекст();
	ТекстФайла = СтрЗаменить(ТекстФайла, "<kit>" + Символы.ПС, "<kit>");
	
	Пока СтрНайти(ТекстФайла, "	<![CDATA") > 0 Цикл
		ТекстФайла = СтрЗаменить(ТекстФайла, "	<![CDATA", "<![CDATA");
	КонецЦикла;
	
	ТекстовыйДокумент.УстановитьТекст(ТекстФайла);
	ТекстовыйДокумент.Записать(ИмяФайла);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры

Процедура ОтправкаЗапросаОтгрузкаТоваров(Документ, ФайлСформирован)
	
	Если НЕ ЗначениеЗаполнено(Документ.Контрагент.ИНН) Тогда
		Сообщить("У контрагента не заполнен ИНН. Операция отменена.");
		Возврат;
	КонецЕсли;
	
	ЭтоПродажаСВыводом = (Документ.ВидТоварооборота = Перечисления.ВидыОтгрузкиТоваров.Продажа И Документ.ВыводИзОборота);
	ВидОперации = ?(
		ЭтоПродажаСВыводом,
		"Отгрузка_товаров_с_выводом_из_оборота_", "Отгрузка_товаров_");
	
	// Получим путь для записи файла
	ИмяФайла = 
		ВидОперации + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла, "csv", "(*.csv)|*.csv") Тогда
		Возврат;
	КонецЕсли;
	
	ВидОборотаТовара = УправлениеКодамиМаркировкиКлиентСервер.ВидТоварооборота(Документ.ВидТоварооборота);
	ПричинаВыводаИзОборота = УправлениеКодамиМаркировкиКлиентСервер.ПричинаОтгрузкиТовара(Документ.ПричинаВыводаИзОборота);
	ОтгрузкаНеучастнику = ?(Документ.ОтгрузкаНеучастнику, "Да", "Нет");
	
	ТекстовыйФайл = Новый ТекстовыйДокумент();
	
	// Заполним шапку документа в зависимости от типа оборота
	Если ЭтоПродажаСВыводом Тогда
		
		ТекстовыйФайл.ДобавитьСтроку(
			НСтр("ru = 'ИНН отправителя,ИНН получателя,Дата отгрузки,Номер "
			+ "первичного документа,Дата первичного документа,Вид оборота товаров,Причина "
			+ "вывода из оборота, Дата вывода из оборота,Идентификатор гос.контракта,Отгрузка неучастнику,Версия'"));
		
		ШапкаЗапроса = Новый Массив;
		ШапкаЗапроса.Добавить(СокрЛП(Документ.Организация.ИНН));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.Контрагент.ИНН));
		ШапкаЗапроса.Добавить(Формат(Документ.ДатаОтгрузкиТоваров, "ДФ=dd.MM.yyyy"));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.НомерПервичногоДокумента));
		ШапкаЗапроса.Добавить(Формат(Документ.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"));
		ШапкаЗапроса.Добавить(ВидОборотаТовара);
		ШапкаЗапроса.Добавить(ПричинаВыводаИзОборота);
		ШапкаЗапроса.Добавить(Формат(Документ.ДатаВыводаИзОборота, "ДФ=dd.MM.yyyy"));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.ИдентификаторГосКонтракта));
		ШапкаЗапроса.Добавить(ОтгрузкаНеучастнику);
		ШапкаЗапроса.Добавить(5);
		
		ТекстовыйФайл.ДобавитьСтроку(СтрСоединить(ШапкаЗапроса, ","));
		
	Иначе
		
		ТекстовыйФайл.ДобавитьСтроку(
			НСтр("ru = 'ИНН отправителя,ИНН получателя,ИНН собственника,Дата передачи товара,Номер "
			+ "первичного документа,Дата первичного документа,Вид оборота товаров,Причина "
			+ "вывода из оборота, Дата вывода из оборота,Идентификатор гос.контракта,Отгрузка неучастнику,Версия'"));
		
		ШапкаЗапроса = Новый Массив;
		ШапкаЗапроса.Добавить(СокрЛП(Документ.Организация.ИНН));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.Контрагент.ИНН));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.Организация.ИНН));
		ШапкаЗапроса.Добавить(Формат(Документ.ДатаОтгрузкиТоваров, "ДФ=dd.MM.yyyy"));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.НомерПервичногоДокумента));
		ШапкаЗапроса.Добавить(Формат(Документ.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"));
		ШапкаЗапроса.Добавить(ВидОборотаТовара);
		ШапкаЗапроса.Добавить(ПричинаВыводаИзОборота);
		ШапкаЗапроса.Добавить(Формат(Документ.ДатаВыводаИзОборота, "ДФ=dd.MM.yyyy"));
		ШапкаЗапроса.Добавить(СокрЛП(Документ.ИдентификаторГосКонтракта));
		ШапкаЗапроса.Добавить(ОтгрузкаНеучастнику);
		ШапкаЗапроса.Добавить(4);
		
		ТекстовыйФайл.ДобавитьСтроку(СтрСоединить(ШапкаЗапроса, ","));
		
	КонецЕсли;
	
	// Заполним табличную часть
	ТекстовыйФайл.ДобавитьСтроку("Параметры товаров,,,");
	ТекстовыйФайл.ДобавитьСтроку(НСтр("ru = 'КИ,КИТУ,Цена за единицу,Сумма НДС'"));
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	Для Каждого ТекущаяСтрока Из Документ.КодыМаркировки Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		// Замена по специфике csv
		Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
		КИ = Идентификатор01 + Идентификатор21;
		КИ = СтрЗаменить(КИ, """", """""");
		
		// Параметры по ТЧ Товары
		НайденнаяСтрока = Документ.Товары.Найти(ТекущаяСтрока.ИдентификаторТовара, "ИдентификаторТовара");
		
		Цена = СтрЗаменить(Строка(НайденнаяСтрока.Цена * 100), Символы.НПП, "");
		СуммаНДС = СтрЗаменить(Строка(Окр(НайденнаяСтрока.СуммаНДС / НайденнаяСтрока.Количество, 2) * 100), Символы.НПП, "");
		
		ТекстовыйФайл.ДобавитьСтроку(
			СтрШаблон(
				"%1,%2,%3,%4",
				"""" + КИ + """",
				"",
				Цена,
				СуммаНДС));
		
	КонецЦикла;
	
	ТекстовыйФайл.УстановитьТекст(СтрЗаменить(ТекстовыйФайл.ПолучитьТекст() + Символы.ПС, Символы.ПС + Символы.ПС, ""));
	ТекстовыйФайл.Записать(ИмяФайла, КодировкаТекста.UTF8);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры

Процедура ОтправкаЗапросаВозвратаВОборот(Документ, ФайлСформирован)
	
	ВидВозврата = УправлениеКодамиМаркировкиКлиентСервер.ВидВозвратаВОборотCSV(Документ.ВидВозврата);
	
	Если НЕ ЗначениеЗаполнено(ВидВозврата) Тогда
		Возврат;
	КонецЕсли;
	
	// Получим путь для записи файла
	ИмяФайла = 
		"Возврат_в_оборот_кодов_маркировки_" + Документ.Номер
		+ "_" + СтрЗаменить(Формат(ТекущаяДатаСеанса(), "ДФ=yyyy.MM.dd"), ".", "_");
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла, "csv", "(*.csv)|*.csv") Тогда
		Возврат;
	КонецЕсли;
	
	ВидПервичногоДокумента = УправлениеКодамиМаркировкиКлиентСервер.ВидПервичногоДокументаCSV(Документ.ВидПервичногоДокумента);
	ВидДокументаРегистрации = УправлениеКодамиМаркировкиКлиентСервер.ВидДокументаСоответствияCSV(Документ.ВидДокументаРегистрации);
	
	ТекстовыйФайл = Новый ТекстовыйДокумент();
	
	ТекстовыйФайл.ДобавитьСтроку(
		"ИНН участника оборота,Вид возврата,Оплачен покупателем,Тип первичного "
		+ "документа,Наименование первичного документа,Номер первичного "
		+ "документа,Дата первичного документа,Вид документа подтверждающего "
		+ "соответствие,Номер документа подтверждающего соответствие,Дата документа "
		+ "подтверждающего соответствие,Версия");
	
	СтрокаДокумента = СтрШаблон(
		"%1,%2,%3,%4,%5,%6,%7,%8,%9,",
		СокрЛП(Документ.Организация.ИНН),
		ВидВозврата,
		?(ЗначениеЗаполнено(Документ.ТоварОплачен), ?(Документ.ТоварОплачен = "Да", "Да", "Нет"), ""),
		ВидПервичногоДокумента,
		?(Документ.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументов.Прочее, Документ.НаименованиеПервичногоДокумента, ""),
		Документ.НомерПервичногоДокумента,
		Формат(Документ.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"),
		ВидДокументаРегистрации,
		?(ЗначениеЗаполнено(ВидДокументаРегистрации) И ЗначениеЗаполнено(Документ.НомерДокументаСоответствия), Документ.НомерДокументаСоответствия, ""));
		
	СтрокаДокументаПродолжение = СтрШаблон(
		"%1,%2",
		?(ЗначениеЗаполнено(ВидДокументаРегистрации) И ЗначениеЗаполнено(Документ.ДатаПервичногоДокумента), Формат(Документ.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"), ""),
		1);
	
	ТекстовыйФайл.ДобавитьСтроку(СтрокаДокумента + СтрокаДокументаПродолжение);
	
	ТекстовыйФайл.ДобавитьСтроку("Параметры товаров,,,,,,,,,,");
	
	ТекстовыйФайл.ДобавитьСтроку(
		"КИ,Оплачен покупателем,Тип первичного документа,Наименование первичного "
		+ "документа,Номер первичного документа,Дата первичного документа,Вид "
		+ "документа подтверждающего соответствие,Номер документа подтверждающего "
		+ "соответствие,Дата документа подтверждающего соответствие,,");
		
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	Для Каждого ТекущаяСтрока Из Документ.Товары Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		Идентификатор01 = "01" + СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		// Замена по специфике csv
		Идентификатор21 = "21" + СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
		КИ = Идентификатор01 + Идентификатор21;
		КИ = СтрЗаменить(КИ, """", """""");
		
		ВидДокументаТовара = "";
		Если ЗначениеЗаполнено(ТекущаяСтрока.ВидПервичногоДокумента) Тогда
			ВидДокументаТовара = УправлениеКодамиМаркировкиКлиентСервер.ВидПервичногоДокументаCSV(ТекущаяСтрока.ВидПервичногоДокумента);
		КонецЕсли;
		
		ВидДокументаРегистрации = УправлениеКодамиМаркировкиКлиентСервер.ВидДокументаСоответствия(ТекущаяСтрока.ВидДокументаРегистрации);
		
		ТекстовыйФайл.ДобавитьСтроку(
			СтрШаблон("%1,%2,%3,%4,%5,%6,%7,%8,%9,,",
				"""" + КИ + """",
				?(ЗначениеЗаполнено(ТекущаяСтрока.ТоварОплачен), ?(ТекущаяСтрока.ТоварОплачен = "Да", "Да", "Нет"), ""),
				ВидДокументаТовара,
				?(ТекущаяСтрока.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументов.Прочее, ТекущаяСтрока.НаименованиеПервичногоДокумента, ""),
				?(ЗначениеЗаполнено(ТекущаяСтрока.НомерПервичногоДокумента), ТекущаяСтрока.НомерПервичногоДокумента, ""),
				?(ЗначениеЗаполнено(ТекущаяСтрока.ДатаПервичногоДокумента), Формат(ТекущаяСтрока.ДатаПервичногоДокумента, "ДФ=dd.MM.yyyy"), ""),
				ВидДокументаРегистрации,
				?(ЗначениеЗаполнено(ВидДокументаРегистрации), ТекущаяСтрока.НомерДокументаСоответствия, ""),
				?(ЗначениеЗаполнено(ВидДокументаРегистрации), Формат(ТекущаяСтрока.ДатаДокументаСоответсвия, "ДФ=dd.MM.yyyy"), "")));
		
	КонецЦикла;
	
	ТекстовыйФайл.УстановитьТекст(СтрЗаменить(ТекстовыйФайл.ПолучитьТекст() + Символы.ПС, Символы.ПС + Символы.ПС, ""));
	ТекстовыйФайл.Записать(ИмяФайла, КодировкаТекста.UTF8);
	
	Сообщить("Сформирован файл: " + ИмяФайла);
	
	ФайлСформирован = Истина;
	
КонецПроцедуры

Процедура ЗагрузитьКодыМаркировкиЧерезФайл(Документ, МассивКодовМаркировки)
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбора.Заголовок = "Выберите файл с кодами маркировки";
	ДиалогВыбора.Фильтр = "Все файлы (*.csv)|*.csv";
	
	Если НЕ ДиалогВыбора.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстовыйФайл = Новый ТекстовыйДокумент();
	ТекстовыйФайл.Прочитать(ДиалогВыбора.ПолноеИмяФайла, КодировкаТекста.UTF8);
	СписокКодовМаркировок = ТекстовыйФайл.ПолучитьТекст();
	
	МассивКодовМаркировки = СтрРазделить(СписокКодовМаркировок, Символы.ПС, Ложь);
	
	// Заменим кавычки
	Для НомерКода = 0 По МассивКодовМаркировки.Количество() - 1 Цикл
		ЕстьПризнакКавычекНачало = Ложь;
		ЕстьПризнакКавычекКонец = Ложь;
		Пока СтрНачинаетсяС(МассивКодовМаркировки[НомерКода], """") Цикл
			МассивКодовМаркировки[НомерКода] =
				Прав(МассивКодовМаркировки[НомерКода], СтрДлина(МассивКодовМаркировки[НомерКода]) - 1);
			ЕстьПризнакКавычекНачало = Истина;
		КонецЦикла;
		Пока СтрЗаканчиваетсяНа(МассивКодовМаркировки[НомерКода], """") Цикл
			МассивКодовМаркировки[НомерКода] =
				Лев(МассивКодовМаркировки[НомерКода], СтрДлина(МассивКодовМаркировки[НомерКода]) - 1);
			ЕстьПризнакКавычекКонец = Истина;
		КонецЦикла;
		Если ЕстьПризнакКавычекНачало И НЕ ЕстьПризнакКавычекКонец Тогда
			// Найдем строки
			НомерСтроки = СтрНайти(МассивКодовМаркировки[НомерКода], """;");
			Если НомерСтроки <> 0 И (Сред(МассивКодовМаркировки[НомерКода], НомерСтроки - 1, 1) <> """"
				ИЛИ (Сред(МассивКодовМаркировки[НомерКода], НомерСтроки - 2, 1) = """"
				И Сред(МассивКодовМаркировки[НомерКода], НомерСтроки - 1, 1) = """")) Тогда
				// Уберем кавычку
				МассивКодовМаркировки[НомерКода] = Сред(МассивКодовМаркировки[НомерКода], 1, НомерСтроки - 1)
					+ Прав(МассивКодовМаркировки[НомерКода], СтрДлина(МассивКодовМаркировки[НомерКода]) - НомерСтроки);
			КонецЕсли;
		ИначеЕсли НЕ ЕстьПризнакКавычекНачало И ЕстьПризнакКавычекКонец Тогда
			// Найдем строки 
			НомерСтроки = СтрНайти(МассивКодовМаркировки[НомерКода], ";""");
			Если НомерСтроки <> 0 И (Сред(МассивКодовМаркировки[НомерКода], НомерСтроки + 2, 1) <> """"
				ИЛИ (Сред(МассивКодовМаркировки[НомерКода], НомерСтроки + 3, 1) = """"
				И Сред(МассивКодовМаркировки[НомерКода], НомерСтроки + 2, 1) = """")) Тогда
				// Уберем кавычку
				МассивКодовМаркировки[НомерКода] = Сред(МассивКодовМаркировки[НомерКода], 1, НомерСтроки)
					+ Прав(МассивКодовМаркировки[НомерКода], СтрДлина(МассивКодовМаркировки[НомерКода]) - (НомерСтроки + 1));
			КонецЕсли;
		КонецЕсли;
		МассивКодовМаркировки[НомерКода] = СтрЗаменить(МассивКодовМаркировки[НомерКода], """""", """");
	КонецЦикла;
	
	ОписаниеОшибки = "";
	УправлениеКодамиМаркировкиВызовСервера.ЗаписатьКодыМаркировкиВРегистр(
		Документ.Ссылка,
		Документ.Товары.Выгрузить(),
		МассивКодовМаркировки,
		Перечисления.СостоянияКодовМаркировки.Эмитирован,
		ОписаниеОшибки,
		Истина,
		Истина);
	
	Если ОписаниеОшибки <> "" Тогда
		ОписаниеОшибки = "Ошибки при загрузке кодов маркировки:" + ОписаниеОшибки;
		Сообщить(ОписаниеОшибки);
	КонецЕсли;
	
	Сообщить("Загрузка кодов маркировки завершена.");
	
КонецПроцедуры

#КонецОбласти

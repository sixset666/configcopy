// ОБЩИЙ МОДУЛЬ "отОТЧЕТЫ" 

// Данный модуль содержит библиотеку функций, предназначенную для построения отчетов
// на базе стандартного макета 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура получения значения показателей
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект     - Объект отчета
//	Выборка             - ТаблицаЗначений - Данные отчета
//	Индекс              - Число           - Индекс колонки отчета
//	СтруктураПараметров - Структура       - Параметры
//	ДокументРезультат   - ДокументОбъект  - Документ для вывода отчета
//	ДеревоСтроки        - ДеревоЗначений  - Дерево строки 
//
Процедура ПолучитьПоказатели(ОтчетОбъект, Выборка, Знач Индекс, СтруктураПараметров, ДокументРезультат, ДеревоСтроки)
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	Пока Выборка.Следующий() Цикл
		// Запомним соответствие значений показателей и группировки в дереве
		НоваяСтрока = ДеревоСтроки.Строки.Добавить();
		НоваяСтрока.Значение = Выборка[Выборка.Группировка()];
		НоваяСтрока.ИмяИзмерения = ПостроительОтчета.ИзмеренияКолонки[Индекс].Имя;
		НоваяСтрока.СтруктураЗначенийПоказателей = Новый Структура;
		
		Для Каждого ИмяПоказателя Из СтруктураПараметров.МассивПоказателей Цикл
			НоваяСтрока.СтруктураЗначенийПоказателей.Вставить(ИмяПоказателя, ?(Выборка[ИмяПоказателя]=NULL,0,Выборка[ИмяПоказателя]));
		КонецЦикла;
		
		Если Индекс < ПостроительОтчета.ИзмеренияКолонки.Количество()-1 Тогда
			ПолучитьПоказатели(ОтчетОбъект, Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, ПостроительОтчета.ИзмеренияКолонки[Индекс+1].Имя, ПостроительОтчета.ИзмеренияКолонки[Индекс].Имя), 
			Индекс+1, СтруктураПараметров, ДокументРезультат, НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ПолучитьПоказатели()

// Функция проверяет дополнительное поля отчета, проверяя его на предмет наличия связи с измерениями строк
// при отсутствии связи возвращается Ложь, Иначе Истина. Используется для устранения некорректных доп. полей
//
// Параметры:
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//	ПутьКДанным		  - Строка			  - Путь к данным
//
// Возвращаемое значение:
//	Булево - Зависимость от строки
//
Функция ПолеЗависитОтСтроки(ПостроительОтчета, ПутьКДанным)
	//	Возврат Истина;	// Отладочная строка
	Для Сч=0 По ПостроительОтчета.ИзмеренияСтроки.Количество()-1 Цикл
		// Проверим "Если поле - подчиненное..." по пути к данным поля и измерений строки
		Если ПутьКДанным = ПостроительОтчета.ИзмеренияСтроки[Сч].ПутьКДанным Тогда
			// Не может быть доп. поля одинакового с группировкой
			//Сигнал();
			Возврат Ложь;
		ИначеЕсли Найти(ПутьКДанным, ПостроительОтчета.ИзмеренияСтроки[Сч].ПутьКДанным) > 0 Тогда
			// Отлично, родительское измерение найдено
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	// Увы, мы не нашли родительской строки
	//Сигнал();
	Возврат Ложь;
КонецФункции // ПолеЗависитОтСтроки()

// Процедура проверяет все дополнительные поля построителя и удаляет некорректные
//
// Параметры:
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//	Сообщать		  - Булево			  - Признак вывода сообщения
//
Процедура ПроверкаДопПолей(ПостроительОтчета, Сообщать = Ложь)
	Сч = 0; Сообщение ="";
	Пока Сч <= ПостроительОтчета.ВыбранныеПоля.Количество()-1 Цикл
		Если НЕ ПолеЗависитОтСтроки(ПостроительОтчета, ПостроительОтчета.ВыбранныеПоля[Сч].ПутьКДанным) Тогда
			Сообщение = Сообщение + ", "+ПостроительОтчета.ВыбранныеПоля[Сч].Представление;
			ПостроительОтчета.ВыбранныеПоля.Удалить(ПостроительОтчета.ВыбранныеПоля[Сч]);
		Иначе Сч = Сч + 1;
		КонецЕсли;
	КонецЦикла;
	Сообщение = Сред(Сообщение,3);
	Если Сообщать И НЕ ПустаяСтрока(Сообщение) Тогда
		Сообщить("Очищены независимые от строк дополнительные поля отчета - "+Сообщение);
	КонецЕсли; 
КонецПроцедуры // ПроверкаДопПолей()

// Функция возвращает строку для подстановки в условие запроса
// #-надо заменить на имя поля, &-надо заменить на имя параметра
//
// Параметры:
//	ВидСравн - ВидСравнения - Вид сравнения
//
// Возвращаемое значение:
//	Строка - Строка подстановки в условие запроса
//
Функция отСформироватьСтрокуВидаСравнения(ВидСравн) Экспорт
	Если ТипЗнч(ВидСравн)<>Тип("ВидСравнения") Тогда Возврат ""; КонецЕсли; 
	
	СтрокаУсловия="";
	Если ВидСравн = ВидСравнения.Равно Тогда
		СтрокаУсловия="#=&";
	ИначеЕсли ВидСравн=ВидСравнения.НеРавно Тогда
		СтрокаУсловия="#<>&";
	ИначеЕсли ВидСравн=ВидСравнения.ВСписке Тогда
		СтрокаУсловия = "# В (&)";
	ИначеЕсли ВидСравн= ВидСравнения.ВСпискеПоИерархии Тогда
		СтрокаУсловия = "# В ИЕРАРХИИ (&)";
	ИначеЕсли ВидСравн= ВидСравнения.ВИерархии Тогда
		СтрокаУсловия = "# В ИЕРАРХИИ (&)";	
	ИначеЕсли ВидСравн= ВидСравнения.НеВСписке Тогда
		СтрокаУсловия = "НЕ # В (&)";
	ИначеЕсли ВидСравн= ВидСравнения.НеВСпискеПоИерархии Тогда
		СтрокаУсловия = "НЕ # В ИЕРАРХИИ (&)";
	ИначеЕсли ВидСравн= ВидСравнения.НеВИерархии Тогда
		СтрокаУсловия = "НЕ # В ИЕРАРХИИ (&)";
	ИначеЕсли ВидСравн= ВидСравнения.Больше Тогда
		СтрокаУсловия="#>&";
	ИначеЕсли ВидСравн= ВидСравнения.БольшеИлиРавно Тогда
		СтрокаУсловия="#>=&";
	ИначеЕсли ВидСравн= ВидСравнения.Меньше Тогда
		СтрокаУсловия="#<&";
	ИначеЕсли ВидСравн= ВидСравнения.МеньшеИлиРавно Тогда
		СтрокаУсловия="#<=&";
	ИначеЕсли ВидСравн= ВидСравнения.Содержит Тогда
		СтрокаУсловия = "# ПОДОБНО &";
	ИначеЕсли ВидСравн= ВидСравнения.НеСодержит Тогда
		СтрокаУсловия = "НЕ # ПОДОБНО &";
	КонецЕсли;

	Возврат СтрокаУсловия;
	
КонецФункции // отСформироватьСтрокуВидаСравнения()

// Функция возвращает строку для подстановки в условие запроса и от вида сравнения 
// заполняет структуру параметров.
// Если не задан параметр ПутьКДанным, то надо заменить "#" на имя поля,
//
// Параметры:
//	СтрокаОтбора - ЭлементОтбора - текущая строка отбора
//	Параметры	 - Структура	 - Структура параметров запроса или построителя запроса
//	ПутьКДанным	 - Строка		 - Путь к полю в запросе (ИмяТаблицы.Номенклатура.Код)
//
// Возвращаемое значение:
//	Строка - Строка подстановки в условие запроса
//
Функция отСформироватьСтрокуОтбора(СтрокаОтбора, Параметры=Неопределено, ПутьКДанным="#") Экспорт
	
	СтрокаУсловия="";
	Попытка
		ИмяПараметра	= СтрЗаменить(СтрокаОтбора.ПутьКДанным,".", "");
		ВидСравн		= СтрокаОтбора.ВидСравнения;
	Исключение
		Возврат СтрокаУсловия;
	КонецПопытки;
	
	Если ТипЗнч(Параметры)<>Тип("Структура") Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	
	Если ТипЗнч(ПутьКДанным)<>Тип("Строка") Тогда
		ПутьКДанным = "#";
	КонецЕсли;
	
	ТипПараметра = 1;
	Если ВидСравн = ВидСравнения.Равно Тогда
		СтрокаУсловия = ПутьКДанным+" = &"+ИмяПараметра;
	ИначеЕсли ВидСравн= ВидСравнения.НеРавно Тогда
		СтрокаУсловия = ПутьКДанным+" <> &"+ИмяПараметра;
	ИначеЕсли ВидСравн= ВидСравнения.ВСписке Тогда
		СтрокаУсловия = ПутьКДанным+" В (&"+ИмяПараметра+")";
	ИначеЕсли ВидСравн= ВидСравнения.ВСпискеПоИерархии Тогда
		СтрокаУсловия = ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+")";
	ИначеЕсли ВидСравн= ВидСравнения.ВИерархии Тогда
		СтрокаУсловия = ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+")";	
	ИначеЕсли ВидСравн= ВидСравнения.НеВСписке Тогда
		СтрокаУсловия = "(НЕ "+ПутьКДанным+" В (&"+ИмяПараметра+"))";
	ИначеЕсли ВидСравн= ВидСравнения.НеВСпискеПоИерархии Тогда
		СтрокаУсловия = "(НЕ "+ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+"))";
	ИначеЕсли ВидСравн= ВидСравнения.НеВИерархии Тогда
		СтрокаУсловия = "(НЕ "+ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+"))";
	ИначеЕсли ВидСравн= ВидСравнения.Больше Тогда
		СтрокаУсловия = ПутьКДанным+">&"+ИмяПараметра;
	ИначеЕсли ВидСравн= ВидСравнения.БольшеИлиРавно Тогда
		СтрокаУсловия = ПутьКДанным+">=&"+ИмяПараметра;
	ИначеЕсли ВидСравн= ВидСравнения.Меньше Тогда
		СтрокаУсловия = ПутьКДанным+"<&"+ИмяПараметра;
	ИначеЕсли ВидСравн= ВидСравнения.МеньшеИлиРавно Тогда
		СтрокаУсловия = ПутьКДанным+"<=&"+ИмяПараметра;
	ИначеЕсли ВидСравн= ВидСравнения.Содержит Тогда
		СтрокаУсловия = ПутьКДанным+" ПОДОБНО &"+ИмяПараметра;
		ТипПараметра  = 2;
	ИначеЕсли ВидСравн= ВидСравнения.НеСодержит Тогда
		СтрокаУсловия = "(НЕ "+ПутьКДанным+" ПОДОБНО &"+ИмяПараметра+")";
		ТипПараметра  = 2;
	ИначеЕсли ВидСравн= ВидСравнения.Интервал Тогда
		СтрокаУсловия = ПутьКДанным+" > &"+ИмяПараметра+"С
		|	И "+ПутьКДанным+" < &"+ИмяПараметра+"По";
		ТипПараметра  = 3;
	ИначеЕсли ВидСравн= ВидСравнения.ИнтервалВключаяГраницы Тогда
		СтрокаУсловия = ПутьКДанным+" >= &"+ИмяПараметра+"С
		|	И "+ПутьКДанным+" <= &"+ИмяПараметра+"По";
		ТипПараметра  = 3;
	ИначеЕсли ВидСравн= ВидСравнения.ИнтервалВключаяНачало Тогда
		СтрокаУсловия = ПутьКДанным+" >= &"+ИмяПараметра+"С
		|	И "+ПутьКДанным+" < &"+ИмяПараметра+"По";
		ТипПараметра  = 3;
	ИначеЕсли ВидСравн= ВидСравнения.ИнтервалВключаяОкончание Тогда
		СтрокаУсловия = ПутьКДанным+" > &"+ИмяПараметра+"С
		|	И "+ПутьКДанным+" <= &"+ИмяПараметра+"По";
		ТипПараметра  = 3;
	КонецЕсли;
	
	Если ТипПараметра=1 Тогда
		Параметры.Вставить(ИмяПараметра, СтрокаОтбора.Значение);
	ИначеЕсли ТипПараметра=2 Тогда
		Параметры.Вставить(ИмяПараметра, "%"+СтрокаОтбора.Значение+"%");
	ИначеЕсли ТипПараметра=3 Тогда
		Параметры.Вставить(ИмяПараметра+"С", СтрокаОтбора.ЗначениеС);
		Параметры.Вставить(ИмяПараметра+"По", СтрокаОтбора.ЗначениеПо);
	КонецЕсли;
	
	Возврат СтрокаУсловия;
	
КонецФункции // отСформироватьСтрокуОтбора()

// Процедура формирует структуру сохраняемых значений реквизитов отчета
//
// Параметры:
//  ЭтаФорма	      – Форма	  – Ссылка на форму документа.
//	СтруктураЗначений - Структура - Структура значений
//
Процедура отСтруктураСохраняемыхЗначений(ЭтаФорма, СтруктураЗначений) Экспорт
	
	Попытка
		ТаблицаОтбора = Новый ТаблицаЗначений;
		ТаблицаОтбора.Колонки.Добавить("Имя");
		ТаблицаОтбора.Колонки.Добавить("ПутьКДанным");
		ТаблицаОтбора.Колонки.Добавить("Представление");
		ТаблицаОтбора.Колонки.Добавить("Использование");
		ТаблицаОтбора.Колонки.Добавить("ВидСравнения");
		ТаблицаОтбора.Колонки.Добавить("Значение");
		
		Отбор = ЭтаФорма.ПостроительОтчета.Отбор;
		Для Каждого ТекОтбор Из Отбор Цикл
			НовыйОтбор = ТаблицаОтбора.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйОтбор, ТекОтбор);
		КонецЦикла;
		СтруктураЗначений.Вставить("ОтборПостроителя", ТаблицаОтбора);
	Исключение КонецПопытки;
	
	Попытка СтруктураЗначений.Вставить("ВидОтчета", ЭтаФорма.ВидОтчета); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ВыводитьДополнительныеПоляВОтдельнойКолонке", ЭтаФорма.ВыводитьДополнительныеПоляВОтдельнойКолонке); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ВариантОформленияСводнойТаблицы", ЭтаФорма.ВариантОформленияСводнойТаблицы); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ВыбТипДиаграммы", ЭтаФорма.ВыбТипДиаграммы); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ВыбТипСводнойДиаграммы", ЭтаФорма.ВыбТипСводнойДиаграммы); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ДеревоПоказателей", ЭтаФорма.ДеревоПоказателей.Скопировать()); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("Показатели", ЭтаФорма.Показатели.Выгрузить()); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("Функции", ЭтаФорма.Функции.Выгрузить()); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ИзмеренияСтроки", ЭтаФорма.ИзмеренияСтроки.Выгрузить()); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ИзмеренияКолонки", ЭтаФорма.ИзмеренияКолонки.Выгрузить()); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("ПараметрыИзмеренийСтрок", ЭтаФорма.ПараметрыИзмеренийСтрок); Исключение КонецПопытки;
	Попытка СтруктураЗначений.Вставить("СтруктураСвязиСвойств", ЭтаФорма.СтруктураСвязиСвойств); Исключение КонецПопытки;
	
КонецПроцедуры	//	отСтруктураСохраняемыхЗначений()

// Процедура обработчик сохранения настроек отчета
//
// Параметры:
//  ЭтаФорма – Форма	– Ссылка на форму документа.
//	Отказ    - Булево	- Признак отказа от записи
//
Процедура отОтчетПередСохранениемЗначений(ЭтаФорма, Отказ) Экспорт
	
	ЭтаФорма.СохраненныеНастройки = Новый Структура;
	отСтруктураСохраняемыхЗначений(ЭтаФорма, ЭтаФорма.СохраненныеНастройки);
	
КонецПроцедуры	//	отОтчетПередСохранениемЗначений()

// Процедура добавляет к переданной структуре стандартные значения форматирования полей
// и форматирование показателей отчета
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект  - Отчет
//	СтруктураЗначений - Структура    - Структура значений
//
Процедура отСтруктураФорматированияПолей(ОтчетОбъект, СтруктураЗначений) Экспорт
	
	СтруктураЗначений.Вставить("ПериодГод",     "ДФ='гггг ""г.""'");
	СтруктураЗначений.Вставить("ПериодКвартал", "ДФ='к ""квартал"" гггг ""г.""'");
	СтруктураЗначений.Вставить("ПериодМесяц",   "ДФ='ММММ гггг ""г.""'");
	СтруктураЗначений.Вставить("ПериодНеделя",  "ДФ='""Неделя с"" дд.ММ.гггг ""г.""'");
	СтруктураЗначений.Вставить("ПериодДень",    "ДФ=дд.ММ.гггг");
	
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		
		Для Каждого ТекФункция Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			Если Не ПустаяСтрока(ТекФункция.ПутьКДанным) Тогда
				СтруктураЗначений.Вставить(ТекФункция.ПутьКДанным, ТекФункция.ФорматнаяСтрока);
			КонецЕсли;
			Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
				Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				Если Не ПустаяСтрока(ТекПоказатель.ПутьКДанным) Тогда
					СтруктураЗначений.Вставить(ТекПоказатель.ПутьКДанным, ТекПоказатель.ФорматнаяСтрока);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	Исключение
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				
				СтруктураЗначений.Вставить(ТекПоказатель.Имя + ТекФункция.Имя, ТекПоказатель.ФорматнаяСтрока);
			КонецЦикла;
		КонецЦикла;
	КонецПопытки;
	
КонецПроцедуры	//	отСтруктураФорматированияПолей()

// Процедура обработчик восстановления настроек отчета
//
// Параметры:
//  ЭтаФорма – Форма	– Ссылка на форму документа.
//
Процедура отОтчетПослеВосстановленияЗначений(ЭтаФорма) Экспорт
	
	Если ЭтаФорма.ВосстанавливатьЗначенияПриОткрытии Тогда
		Если ТипЗнч(ЭтаФорма.СохраненныеНастройки) = Тип("Структура") Тогда
			ЭтаФорма.ЗаполнитьНачальныеНастройки(ЭтаФорма.ИмяМакета);
			
			Попытка
				Показатели = ЭтаФорма.СохраненныеНастройки.Показатели;
				ТекущиеПоказатели = ЭтаФорма.Показатели.Выгрузить();
				ЭтаФорма.Показатели.Очистить();
				Для Каждого ТекПоказатель Из Показатели Цикл
					НайденныйПоказатель = ТекущиеПоказатели.Найти(ТекПоказатель.Имя, "Имя");
					Если НайденныйПоказатель = Неопределено Тогда
						// таких показателей больше нет
						Продолжить;
					Иначе
						НовыйПоказатель = ЭтаФорма.Показатели.Добавить();
						// параметры возьмем из текущих настроек а использование и позиция сохраненные
						ЗаполнитьЗначенияСвойств(НовыйПоказатель, НайденныйПоказатель);
						НовыйПоказатель.Использование = ТекПоказатель.Использование;
						// отделим старые показатели от новых
						ТекущиеПоказатели.Удалить(НайденныйПоказатель);
					КонецЕсли;
				КонецЦикла;
				// Если появились новые показатели добавим их в конец списка
				Для Каждого ТекПоказатель Из ТекущиеПоказатели Цикл
					НовыйПоказатель = ЭтаФорма.Показатели.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйПоказатель, ТекПоказатель);
				КонецЦикла;
			Исключение
			КонецПопытки;
			
			Попытка
				ДеревоПоказателей = ЭтаФорма.СохраненныеНастройки.ДеревоПоказателей.Строки;
				ТекущееДеревоПоказателей = ЭтаФорма.ДеревоПоказателей.Скопировать();
				ТекущееДеревоПоказателей = ТекущееДеревоПоказателей.Строки;
				ЭтаФорма.ДеревоПоказателей.Строки.Очистить();
				
				Для Каждого ТекФункция Из ДеревоПоказателей Цикл
					НайденнаяФункция = ТекущееДеревоПоказателей.Найти(ТекФункция.Имя, "Имя", Ложь);
					Если НайденнаяФункция = Неопределено Тогда
						// таких функции больше нет
						Продолжить;
					Иначе
						НоваяФункция = ЭтаФорма.ДеревоПоказателей.Строки.Добавить();
						// параметры возьмем из текущих настроек а использование и позиция сохраненные
						ЗаполнитьЗначенияСвойств(НоваяФункция, НайденнаяФункция);
						НоваяФункция.Использование = ТекФункция.Использование;
						Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
							НайденныйПоказатель = НайденнаяФункция.Строки.Найти(ТекПоказатель.Имя, "Имя");
							Если НайденныйПоказатель = Неопределено Тогда
								// таких показателей больше нет
								Продолжить;
							Иначе
								НовыйПоказатель = НоваяФункция.Строки.Добавить();
								// параметры возьмем из текущих настроек а использование и позиция сохраненные
								ЗаполнитьЗначенияСвойств(НовыйПоказатель, НайденныйПоказатель);
								НовыйПоказатель.Использование = ТекПоказатель.Использование;
								// отделим старые показатели от новых
								НайденнаяФункция.Строки.Удалить(НайденныйПоказатель);
							КонецЕсли;
						КонецЦикла;
						// Если у функции нет новых показателей, то удалим для получения только новых функций
						Если НайденнаяФункция.Строки.Количество() = 0 Тогда
							ТекущееДеревоПоказателей.Удалить(НайденнаяФункция);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				// Если появились новые функции или показатели добавим их в конец списка
				Для Каждого ТекФункция Из ТекущееДеревоПоказателей Цикл
					НайденнаяФункция = ЭтаФорма.ДеревоПоказателей.Строки.Найти(ТекФункция.Имя, "Имя", Ложь);
					// Это новая функция
					Если НайденнаяФункция = Неопределено Тогда
						НайденнаяФункция = ЭтаФорма.ДеревоПоказателей.Строки.Добавить();
						ЗаполнитьЗначенияСвойств(НайденнаяФункция, ТекФункция);
					КонецЕсли;
					// Добавим новые показатели
					Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
						НовыйПоказатель = НайденнаяФункция.Строки.Добавить();
						ЗаполнитьЗначенияСвойств(НовыйПоказатель, ТекПоказатель);
					КонецЦикла;
				КонецЦикла;
				
			Исключение
			КонецПопытки;
			
			Попытка
				
				Функции = ЭтаФорма.СохраненныеНастройки.Функции;
				ТекущиеФункции = ЭтаФорма.Функции.Выгрузить();
				ЭтаФорма.Функции.Очистить();
				Для Каждого ТекФункция Из Функции Цикл
					НайденнаяФункция = ТекущиеФункции.Найти(ТекФункция.Имя, "Имя");
					Если НайденнаяФункция = Неопределено Тогда
						// таких показателей больше нет
						Продолжить;
					Иначе
						НоваяФункция = ЭтаФорма.Функции.Добавить();
						// параметры возьмем из текущих настроек а использование и позиция сохраненные
						ЗаполнитьЗначенияСвойств(НоваяФункция, НайденнаяФункция);
						НоваяФункция.Использование = ТекФункция.Использование;
						// отделим старые Функции от новых
						ТекущиеФункции.Удалить(НайденнаяФункция);
					КонецЕсли;
				КонецЦикла;
				// Если появились новые Функции добавим их в конец списка
				Для Каждого ТекФункция Из ТекущиеФункции Цикл
					НоваяФункция = ЭтаФорма.Функции.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяФункция, ТекФункция);
				КонецЦикла;
				
			Исключение
			КонецПопытки;
			
			Попытка
				СтруктураСвязиСвойств = ЭтаФорма.СохраненныеНастройки.СтруктураСвязиСвойств;
			Исключение
				СтруктураСвязиСвойств = Новый Структура;
			КонецПопытки;
			
			Попытка
				ЭтаФорма.ИзмеренияСтроки.Очистить();
				ДеревоДоступныхПолей  = ЭтаФорма.ДеревоДоступныхПолей.Строки;
				ИзмеренияСтроки       = ЭтаФорма.СохраненныеНастройки.ИзмеренияСтроки;
				Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
					ИмяСтроки = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "");
					Если СтруктураСвязиСвойств.Свойство(ИмяСтроки) Тогда
						ЗаполнитьЗначенияСвойств(ЭтаФорма.ИзмеренияСтроки.Добавить(), ТекСтрока);
						Продолжить;
					КонецЕсли;
					НайденнаяСтрока = ДеревоДоступныхПолей.Найти(ТекСтрока.ПутьКДанным, "ПутьКДанным", Истина);
					Если НЕ НайденнаяСтрока = Неопределено Тогда
						НовоеИзмерение = ЭтаФорма.ИзмеренияСтроки.Добавить();
						НовоеИзмерение.Использование = ТекСтрока.Использование;
						НовоеИзмерение.ПутьКДанным   = ТекСтрока.ПутьКДанным;
						НовоеИзмерение.ТипИзмерения  = ТекСтрока.ТипИзмерения;
						НовоеИзмерение.Представление = НайденнаяСтрока.Представление;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
			Попытка
				ЭтаФорма.ИзмеренияКолонки.Очистить();
				ДеревоДоступныхПолей  = ЭтаФорма.ДеревоДоступныхПолей.Строки;
				ИзмеренияКолонки      = ЭтаФорма.СохраненныеНастройки.ИзмеренияКолонки;
				Для Каждого ТекКолонка Из ИзмеренияКолонки Цикл
					ИмяКолонки = СтрЗаменить(ТекКолонка.ПутьКДанным, ".", "");
					Если СтруктураСвязиСвойств.Свойство(ИмяКолонки) Тогда
						ЗаполнитьЗначенияСвойств(ЭтаФорма.ИзмеренияКолонки.Добавить(), ТекКолонка);
						Продолжить;
					КонецЕсли;
					НайденнаяКолонка = ДеревоДоступныхПолей.Найти(ТекКолонка.ПутьКДанным, "ПутьКДанным", Истина);
					Если НЕ НайденнаяКолонка = Неопределено Тогда
						НовоеИзмерение = ЭтаФорма.ИзмеренияКолонки.Добавить();
						НовоеИзмерение.Использование = ТекКолонка.Использование;
						НовоеИзмерение.ПутьКДанным   = ТекКолонка.ПутьКДанным;
						НовоеИзмерение.ТипИзмерения  = ТекКолонка.ТипИзмерения;
						НовоеИзмерение.Представление = НайденнаяКолонка.Представление;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
			Попытка ЭтаФорма.ПараметрыИзмеренийСтрок = ЭтаФорма.СохраненныеНастройки.ПараметрыИзмеренийСтрок; Исключение КонецПопытки;
			
			отЗаполнитьНастройкиСвойствИОтборов(ЭтаФорма, ЭтаФорма.СохраненныеНастройки);
			
			Попытка отФорматированиеПостроителяОтчета(ЭтаФорма);
			Исключение КонецПопытки;
			
			// Остальные реквизиты отчета восстанавливаются стандартно
			ЭтаФорма.ЗаполнитьНастройки = Ложь;
			
			Попытка ЭтаФорма.ДатаНачала = ""; Исключение КонецПопытки;
			Попытка ЭтаФорма.ДатаКонца = ""; Исключение КонецПопытки;
			отОтчетПриИнициализацииОбъекта(ЭтаФорма);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отОтчетПослеВосстановленияЗначений()

// Процедура обработчик восстановления настроек текущего отчета при изменении режима(Эксперт/Стандарт)
// или вида(Остатки/Обороты) ФормыНастроек
//
// Параметры:
//	ОтчетОбъект    - ОтчетОбъект  - Отчет
//	ФормаНастройки - Форма		  - Форма настройки отчета
//
Процедура ВосстановитьНастройкиДляТекущейФормы(ОтчетОбъект, ФормаНастройки)
	
	СтруктураНастроекДляТекущейФормы=ВосстановитьЗначение(ОтчетОбъект.Метаданные().Имя+"СтруктураНастроек");
	
	Если ТипЗнч(СтруктураНастроекДляТекущейФормы)<>Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Эксперт = ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт;
	
	ФормаНастройки.ЗаполнитьНачальныеНастройки(ФормаНастройки.ИмяМакета);
	
	Попытка ФормаНастройки.ДатаНачала = СтруктураНастроекДляТекущейФормы.ДатаНачала; Исключение КонецПопытки;
	Попытка ФормаНастройки.ДатаКонца = СтруктураНастроекДляТекущейФормы.ДатаКонца; Исключение КонецПопытки;
	Попытка ФормаНастройки.ВариантОформленияСводнойТаблицы = СтруктураНастроекДляТекущейФормы.ВариантОформленияСводнойТаблицы; Исключение КонецПопытки;
	Попытка ФормаНастройки.ВыбТипДиаграммы = СтруктураНастроекДляТекущейФормы.ВыбТипДиаграммы; Исключение КонецПопытки;
	Попытка ФормаНастройки.ВыбТипСводнойДиаграммы = СтруктураНастроекДляТекущейФормы.ВыбТипСводнойДиаграммы; Исключение КонецПопытки;
	Попытка 
		СохраненнаяСтруктураСвязиСвойств = СтруктураНастроекДляТекущейФормы.СтруктураСвязиСвойств;
	Исключение 
		СохраненнаяСтруктураСвязиСвойств = Новый Структура;
	КонецПопытки;
	
	ФормаНастройки.ИзмеренияСтроки.Очистить();
	
	Попытка
		тзСтроки = СтруктураНастроекДляТекущейФормы.ИзмеренияСтроки;
		Для Каждого ТекИзмерение из тзСтроки Цикл
			ИмяИзмерения = СтрЗаменить(ТекИзмерение.ПутьКДанным,".","");
			ИмяИзмерения = ?(СохраненнаяСтруктураСвязиСвойств.Свойство(ИмяИзмерения), СохраненнаяСтруктураСвязиСвойств[ИмяИзмерения].ПутьКДаннымИзмерения, ИмяИзмерения);
			Если ФормаНастройки.ДеревоДоступныхПолей.Строки.Найти(ИмяИзмерения, "ПутьКДанным", Истина) <> Неопределено Тогда
				НовоеИзмерение = ФормаНастройки.ИзмеренияСтроки.Добавить();
				НовоеИзмерение.Использование=ТекИзмерение.Использование;
				НовоеИзмерение.Представление=ТекИзмерение.Представление;
				НовоеИзмерение.ПутьКДанным=ТекИзмерение.ПутьКДанным;
				НовоеИзмерение.ТипИзмерения=ТекИзмерение.ТипИзмерения;
			КонецЕсли;
		КонецЦикла;
	Исключение КонецПопытки;
	
	Попытка ФормаНастройки.ВыводитьДополнительныеПоляВОтдельнойКолонке = Ложь;	Исключение КонецПопытки;
	
	Если Эксперт Тогда
		Попытка ФормаНастройки.ВыводитьДополнительныеПоляВОтдельнойКолонке = СтруктураНастроекДляТекущейФормы.ВыводитьДополнительныеПоляВОтдельнойКолонке;	Исключение КонецПопытки;
		
		Попытка 
			тзПоказатели = СтруктураНастроекДляТекущейФормы.Показатели;
			ФормаНастройки.Показатели.Загрузить(тзПоказатели); 
		Исключение КонецПопытки;
		
		Попытка 
			тзДеревоПоказателей = СтруктураНастроекДляТекущейФормы.ДеревоПоказателей;
			ФормаНастройки.тзДеревоПоказателей = тзДеревоПоказателей.Скопировать();
		Исключение КонецПопытки;
		
		Попытка
			тзКолонки = СтруктураНастроекДляТекущейФормы.ИзмеренияКолонки;
			ФормаНастройки.ИзмеренияКолонки.Очистить();
			Для Каждого ТекИзмерение из тзКолонки Цикл
				ИмяИзмерения = СтрЗаменить(ТекИзмерение.ПутьКДанным,".","");
				ИмяИзмерения = ?(СохраненнаяСтруктураСвязиСвойств.Свойство(ИмяИзмерения), СохраненнаяСтруктураСвязиСвойств[ИмяИзмерения].ПутьКДаннымИзмерения, ТекИзмерение.ПутьКДанным);
				Если ФормаНастройки.ДеревоДоступныхПолей.Строки.Найти(ИмяИзмерения, "ПутьКДанным", Истина) <> Неопределено Тогда
					НовоеИзмерение = ФормаНастройки.ИзмеренияКолонки.Добавить();
					НовоеИзмерение.Использование=ТекИзмерение.Использование;
					НовоеИзмерение.Представление=ТекИзмерение.Представление;
					НовоеИзмерение.ПутьКДанным=ТекИзмерение.ПутьКДанным;
					НовоеИзмерение.ТипИзмерения=ТекИзмерение.ТипИзмерения;
				КонецЕсли;
			КонецЦикла;
		Исключение
		КонецПопытки;
		
		Попытка ФормаНастройки.ПараметрыИзмеренийСтрок = СтруктураНастроекДляТекущейФормы.ПараметрыИзмеренийСтрок; Исключение КонецПопытки;
	КонецЕсли;
	
	отЗаполнитьНастройкиСвойствИОтборов(ФормаНастройки, СтруктураНастроекДляТекущейФормы);
	
	Попытка отФорматированиеПостроителяОтчета(ФормаНастройки);
	Исключение КонецПопытки;
	ФормаНастройки.ЗаполнитьНастройки = Ложь;
	
КонецПроцедуры	//	ВосстановитьНастройкиДляТекущейФормы()

// Процедура запрещает интерактивное открытие объекта
//
// Параметры:
//	ЗапретОткрытия - Булево - Параметр Отказ из вызвавшей процедуры
//  ВиСообщения    - Число  - Вид текста сообщения
//                 1 - для вторичных объектов (открываемых строго из определенного контекста) - по умолчанию
//                 2 - для служебных объектов
Процедура отЗапретИнтерактивногоОткрытия(ЗапретОткрытия) Экспорт
	
	ЗапретОткрытия = Истина;
	Предупреждение("Данная обработка используется другими объектами конфигурации!
	|Запрещен самостоятельный вызов!", 60);
	
КонецПроцедуры	//	отЗапретИнтерактивногоОткрытия()

// Процедура получает поле из структуры ПараметрыИспользованияПолейИПоказателей
// по пути к данным измерения строки или колонки
//
// Параметры:
//	ИмяИзмерения                            - Строка    - Путь к данным выбранного измерения 
//	ПараметрыИспользованияПолейИПоказателей - Структура - Структура ПараметрыИспользованияПолейИПоказателей отчета
//
// Возвращаемое значение:
//	Поле - поле из структуры ПараметрыИспользованияПолейИПоказателей	
//
Функция отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьКДанным, ПараметрыИспользованияПолейИПоказателей) Экспорт
	Попытка
		ПозицияТочки = Найти(ПутьКДанным, ".");
		Если ПозицияТочки = 0 Тогда
			Результат = ПараметрыИспользованияПолейИПоказателей[ПутьКДанным];
		Иначе
			ТекПараметрИспользования = ПараметрыИспользованияПолейИПоказателей[Лев(ПутьКДанным, ПозицияТочки - 1)]["Поля"];
			Результат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(Сред(ПутьКДанным, ПозицияТочки + 1), ТекПараметрИспользования);
		КонецЕсли;
	Исключение
		Результат = Неопределено;
	КонецПопытки;
	Возврат Результат;
КонецФункции	//	отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей()

// Процедура добавляет вызов текущей формы отчета в Избранное
//
// Параметры:
//	Кнопка      - Кнопка       - Кнопка на форме
//	ОтчетОбъект - ОтчетОбъект  - Отчет
//	
Процедура ОтДействияФормыИзбранноеДобавитьФорму(Кнопка, ОтчетОбъект) Экспорт
	
	Избранное = Обработки.Избранное.Создать();
	Менеджер = "Отчеты." + ОтчетОбъект.Метаданные().Имя;
	
	НаименованиеОтчета = "";
	Попытка НаименованиеОтчета = ОтчетОбъект.ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек).НаименованиеОтчета;
	Исключение КонецПопытки;	
	Если ПустаяСтрока(НаименованиеОтчета) Тогда
		НаименованиеОтчета = ?(ОтчетОбъект.Метаданные().Синоним = "", ОтчетОбъект.Метаданные().Имя, ОтчетОбъект.Метаданные().Синоним);
	КонецЕсли;
	
	Если (ОтчетОбъект.Метаданные().Реквизиты.Найти("ДатаНачала")<>Неопределено) и (ОтчетОбъект.Метаданные().Реквизиты.Найти("ДатаКонца")<>Неопределено) Тогда
		ПериодСтрокой = ПредставлениеПериода(ОтчетОбъект.ДатаНачала, ОтчетОбъект.ДатаКонца, "ФП=Истина");
		Если Найти(ПериодСтрокой, " - ") > 0 Тогда
			Если ОтчетОбъект.ДатаНачала = ОтчетОбъект.ДатаКонца Тогда
				ПериодСтрокой = Формат(ОтчетОбъект.ДатаНачала, "ДЛФ=DD");
			Иначе	
				ПериодСтрокой = Формат(ОтчетОбъект.ДатаНачала, "ДФ=dd.MM.yyyy") + " - " + Формат(ОтчетОбъект.ДатаКонца, "ДФ=dd.MM.yyyy");
			КонецЕсли;
		КонецЕсли;
		НаименованиеОтчета = "Отчет: " + НаименованиеОтчета + " (" + ПериодСтрокой + ")";
	Иначе
		НаименованиеОтчета = "Отчет: " + НаименованиеОтчета;
	КонецЕсли;
	
	СтруктураЗначений = Новый Структура;
	отСтруктураСохраняемыхЗначений(ОтчетОбъект, СтруктураЗначений);
	
	СтруктураЗначений.Вставить("Менеджер",         Менеджер);
	СтруктураЗначений.Вставить("Наименование",     НаименованиеОтчета);
	Если (ОтчетОбъект.Метаданные().Реквизиты.Найти("ДатаНачала")<>Неопределено) и (ОтчетОбъект.Метаданные().Реквизиты.Найти("ДатаКонца")<>Неопределено) Тогда
		СтруктураЗначений.Вставить("ДатаНачала",       ОтчетОбъект.ДатаНачала);
		СтруктураЗначений.Вставить("ДатаКонца",        ОтчетОбъект.ДатаКонца);
	КонецЕсли;
	СтруктураЗначений.Вставить("ИмяФормыНастроек", ОтчетОбъект.ИмяФормыНастроек);
	
	Избранное.ДобавитьВИзбранное(СтруктураЗначений, Менеджер, ОтчетОбъект.ИмяФормыНастроек);  
	
КонецПроцедуры	//	ОтДействияФормыИзбранноеДобавитьФорму()

// Процедура добавления значения к списку владельцев
//
// Параметры:
//	ТекЗначение      - Ссылка         - Ссылка на объект
//	СписокВладельцев - СписокЗначений - Список владельцев 
//	ИмяОбъекта       - Строка         - Имя объекта
//	ТипВладельца     - Произвольный   - Тип владельца
//
Процедура ОтДобавитьЗначениеКСпискуВладельцевПоИерархии(ТекЗначение, СписокВладельцев, ИмяОбъекта, ТипВладельца)
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипВладельца) Тогда
		ВидМетаданных = "Справочник";
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипВладельца) Тогда
		ВидМетаданных = "ПланыВидовХарактеристик";
	ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипВладельца) Тогда
		ВидМетаданных = "ПланыВидовРасчета";
	Иначе
		Возврат;
	КонецЕсли;
	
	Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка КАК ТекЭлемент
	|ИЗ
	|	"+ВидМетаданных+"."+ИмяОбъекта+" КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В ИЕРАРХИИ(&Ссылка)
	|	И Таблица.ЭтоГруппа = ЛОЖЬ";
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ТекЗначение);
	Запрос.Текст = Текст;
	Выборка = Запрос.Выполнить().Выгрузить();
	Для Каждого ТекСтрока Из Выборка Цикл
		СписокВладельцев.Добавить(ТекСтрока.ТекЭлемент);
	КонецЦикла;
	
КонецПроцедуры // отСтруктураСохраняемыхЗначений()

// Процедура добавляет несвязанный показатель
//
// Параметры:
//	ОтчетОбъект 			   - ОтчетОбъект  - Объект отчета
//  Использование 			   - Булево       - Флаг включение показателя при открытии формы по умолчанию
//	ПутьКДаннымРодителя 	   - Строка       - Путь измерения, для которого добавляется показатель
//	ИмяПоля					   - Строка       - Имя показателя
//	Представление 			   - Строка       - Представление показателя, если этот параметр не заполнять,
//							   то представление берется из имени
//	ТипПоля 				   - Произвольный - Тип значения показателя
//						       Примеры: "Строка", "Строка10", "Число10", "Число10,3", "Число10.3", "Дата" или 
//							   строка содержащая имя типа описывающего объект конфигурации
//	ШиринаКолонки 			   - Число        - Ширина колонки табличного документа, в которой выводится данный показатель
//	ФорматВывода	 		   - Строка       - Формат вывода показателя в табличном документе
//	ВыводитьИтогПоИерархии	   - Булево       - Выводить показатель для иерархического измерения
//	ВыводитьИтогДляВсехУровней - Булево       - Выводить показатель для всех измерений
//	ВыводитьОбщийИтог	       - Булево       - выводить общий итог для показателя
//
Процедура отДобавитьСтруктуруНеСвязанныхПоказателей(ОтчетОбъект, Использование=Ложь, ПутьКДаннымРодителя, ИмяПоля, Представление="", ТипПоля,  ШиринаКолонки=20, ФорматВывода="", ВыводитьИтогПоИерархии=Ложь, ВыводитьИтогДляВсехУровней=Ложь, ВыводитьОбщийИтог=Ложь) Экспорт 
	
	Попытка 
		СтруктураНеСвязанныхПоказателей = ОтчетОбъект.СтруктураНеСвязанныхПоказателей;
		ВетвьДереваДоступныхПолей = ОтчетОбъект.ДеревоДоступныхПолей.Строки.Найти(ПутьКДаннымРодителя, "ПутьКДанным", Истина);;
	Исключение
		Возврат;
	КонецПопытки;
	
	Если ВетвьДереваДоступныхПолей = Неопределено Тогда Возврат; КонецЕсли;
	
	ПутьКДанным = ИмяПоля;
	ИмяПоля     = СтрЗаменить(ИмяПоля, ".", "");
	
	ДоступноеПоле = ВетвьДереваДоступныхПолей.Строки.Добавить();
	ДоступноеПоле.Имя           = ИмяПоля;
	ДоступноеПоле.ПутьКДанным   = ПутьКДанным;
	ДоступноеПоле.Представление = ?(обЗначениеНеЗаполнено(Представление), ПутьКДанным, Представление);
	ДоступноеПоле.Измерение     = Ложь;
	ДоступноеПоле.Отбор         = Ложь;
	ДоступноеПоле.Порядок       = Ложь;
	
	РодительПоля = ВетвьДереваДоступныхПолей.Родитель;
	ПредставлениеРодителя = ВетвьДереваДоступныхПолей.Представление;
	Пока РодительПоля <> Неопределено Цикл
		ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
		РодительПоля = РодительПоля.Родитель;
	КонецЦикла;
	ПолноеПредставление = ДоступноеПоле.Представление+" ("+ПредставлениеРодителя+")";
	ДоступноеПоле.ПолноеПредставление = ПолноеПредставление;
	
	Если Найти(ТипПоля, "Строка")>0 Тогда
		ТекСтрока = СокрЛП(СтрЗаменить(ТипПоля, "Строка",""));
		Попытка 
			ДлиннаСтроки = Число(ТекСтрока);
		Исключение
			ДлиннаСтроки = 0;
		КонецПопытки;
		ДоступноеПоле.ТипЗначения   = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(ДлиннаСтроки));
	ИначеЕсли Найти(ТипПоля, "Число")>0 Тогда
		ТекСтрока = СтрЗаменить(ТипПоля, "Число", "");
		РазделительныйСимвол = ?(Найти(ТекСтрока,".")>0, ".", ",");
		ПозицияЗапятой = Найти(ТекСтрока, РазделительныйСимвол);
		ЦелаяЧасть = СокрЛП(Лев(ТекСтрока, ПозицияЗапятой-1));
		ДробнаяЧасть = СокрЛП(Сред(ТекСтрока, ПозицияЗапятой+1));
		Если ЦелаяЧасть="" Тогда
			ЦелаяЧасть = ДробнаяЧасть;
			ДробнаяЧасть = 0;
		КонецЕсли;
		
		Попытка 
			ЦелаяЧасть = Число(ЦелаяЧасть);
		Исключение
			ЦелаяЧасть = 0;
		КонецПопытки;
		
		Попытка 
			ДробнаяЧасть = Число(ДробнаяЧасть);
		Исключение
			ДробнаяЧасть = 0;
		КонецПопытки;
		
		ДоступноеПоле.ТипЗначения   = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(ЦелаяЧасть, ДробнаяЧасть));
	ИначеЕсли Найти(ТипПоля, "Дата")>0 Тогда
		ДоступноеПоле.ТипЗначения   = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
	Иначе
		ДоступноеПоле.ТипЗначения   = Новый ОписаниеТипов(ТипПоля);
	КонецЕсли;
	
	ИдентификаторПути = СтрЗаменить(ПутьКДаннымРодителя, ".", "_");
	Если Не СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПути) Тогда
		СтруктураНеСвязанныхПоказателей.Вставить(ИдентификаторПути, Новый Структура("Показатели", Новый Структура));
	КонецЕсли;
	
	СтруктураПоказателей = СтруктураНеСвязанныхПоказателей[ИдентификаторПути]["Показатели"];
	СтруктураПоказателей.Вставить(ИмяПоля, Новый Структура("Представление, ШиринаКолонки, Формат, ВыводитьИтогПоИерархии, ВыводитьИтогДляВсехУровней, ВыводитьОбщийИтог", ДоступноеПоле.Представление, ШиринаКолонки, ФорматВывода, ВыводитьИтогПоИерархии, ВыводитьИтогДляВсехУровней, ВыводитьОбщийИтог));
	
	Если Использование Тогда
		Если ТипЗнч(ОтчетОбъект.ПараметрыИзмеренийСтрок) <> Тип("Структура") Тогда
			ОтчетОбъект.ПараметрыИзмеренийСтрок = Новый Структура();
		КонецЕсли;
		
		Если НЕ ОтчетОбъект.ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) Тогда
			ТаблицаПолей = Новый ТаблицаЗначений();
			ТаблицаПолей.Колонки.Добавить("Имя");
			ТаблицаПолей.Колонки.Добавить("ПутьКДанным");
			ТаблицаПолей.Колонки.Добавить("Представление");
			ОтчетОбъект.ПараметрыИзмеренийСтрок.Вставить(ИдентификаторПути, Новый Структура("Поля,ПоляПредставление", ТаблицаПолей, ""));
		КонецЕсли;
		
		Если НЕ ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Свойство("Поля") Тогда
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Поля", "");
		КонецЕсли;
		ДополнительныеПоля = ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Поля;
		Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
			ВыбранныеПоля = ДополнительныеПоля.Скопировать();
		Иначе
			ВыбранныеПоля = Новый ТаблицаЗначений();
			ВыбранныеПоля.Колонки.Добавить("Имя");
			ВыбранныеПоля.Колонки.Добавить("ПутьКДанным");
			ВыбранныеПоля.Колонки.Добавить("Представление");
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("ПоляПредставление", "");
		КонецЕсли;
		
		НоваяСтрока = ВыбранныеПоля.Добавить();
		НоваяСтрока.ПутьКДанным = ДоступноеПоле.ПутьКДанным;
		НоваяСтрока.Представление = ДоступноеПоле.Представление;
		
		СтрПредставлений = ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].ПоляПредставление;
		СтрПредставлений = ?(СтрПредставлений = "", "", СтрПредставлений + ", ") + ДоступноеПоле.Представление;
		
		ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Поля",              ВыбранныеПоля);
		ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("ПоляПредставление", СтрПредставлений);
	КонецЕсли;
КонецПроцедуры // отДобавитьСтруктуруНеСвязанныхПоказателей()

// Функция выбора свойства
//
// Параметры:
//  ЭтотОбъект	      – Объект	                   – Ссылка на объект
//  ЭтаФорма	      – Форма	                   – Ссылка на форму
//	РезультатОткрытия - СтрокаДереваЗначений       - Выбранное свойство из дерева свойств
//	Измерения         - ИзмеренияПостроителяОтчета - Список доступных измерений построителя отчета
//	РезультатВыбора   - Структура -                - Структура результата выбора
//
// Возвращаемое значение:
//	Булево - Результат выбора свойства
//
Функция отВыбратьСвойство(ЭтотОбъект, ЭтаФорма=Неопределено, РезультатОткрытия, Измерения = Неопределено, РезультатВыбора = Неопределено) Экспорт
	ВыбралиСвойство = Ложь;
	Если ЭтаФорма=Неопределено Тогда
		ЭтаФорма = ЭтотОбъект;
	КонецЕсли;
	
	ТекЗапрос = Новый Запрос("ВЫБРАТЬ
	|	СвойстваОбъектов.Ссылка КАК Свойство,
	|	СвойстваОбъектов.Наименование КАК Наименование,
	|	СвойстваОбъектов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|ГДЕ
	|	СвойстваОбъектов.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
	
	ФормаВыбора = ПолучитьОбщуюФорму("ВыборИзДерева", ЭтаФорма);
	ФормаВыбора.ДеревоВыбора = ТекЗапрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ФормаВыбора.Заголовок    = "Выбор свойства";
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.СоздатьКолонки();
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки.Свойство.Видимость = Ложь;
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки.ТипЗначения.Видимость = Ложь;
	РезультатВыборкаСвойств = ФормаВыбора.ОткрытьМодально();
	Если ТипЗнч(РезультатВыборкаСвойств) = Тип("СтрокаДереваЗначений") Тогда
		
		СтруктураСвязиСвойств = ЭтотОбъект.СтруктураСвязиСвойств;
		ТекДеревоДоступныхПолей = ЭтотОбъект.ДеревоДоступныхПолей;
		
		Если Измерения=Неопределено Тогда
			Измерения = РезультатОткрытия.Родитель.Родитель;
		Конецесли;
		
		СвязанноеСвойство = Неопределено;
		Для Каждого ТекСвойство Из СтруктураСвязиСвойств Цикл 
			Если ТекСвойство.Значение.ИмяИзмерения = Измерения.Имя И ТекСвойство.Значение.Свойство = РезультатВыборкаСвойств.Свойство Тогда
				СвязанноеСвойство = ТекСвойство;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ПолноеПредставление = "";
		Если СвязанноеСвойство = Неопределено Тогда
			НомерСвойства = СтруктураСвязиСвойств.Количество()+1;
			ИмяПараметра = "ПараметрСвойства" + НомерСвойства;
			ПараметрыСвойства = Новый Структура("ИмяИзмерения, ПутьКДаннымИзмерения,Свойство,Представление,ТипЗначения,ИмяПараметра",
			Измерения.Имя, Измерения.ПутьКДанным, РезультатВыборкаСвойств.Свойство, РезультатВыборкаСвойств.Наименование, РезультатВыборкаСвойств.ТипЗначения, ИмяПараметра);
			ИмяСвойства = "Свойство"+НомерСвойства+"Значение";
			СтруктураСвязиСвойств.Вставить(ИмяСвойства, ПараметрыСвойства);
			
			ВетвьВладельцаСвойств = ТекДеревоДоступныхПолей.Строки.Найти(Измерения.Имя, "Имя");
			ВетвьСвойств = ВетвьВладельцаСвойств.Строки.Найти("Свойства", "Имя");
			ДоступноеПоле = ВетвьСвойств.Строки.Добавить();
			ДоступноеПоле.Имя           = ИмяСвойства;
			ДоступноеПоле.ПутьКДанным   = ИмяСвойства;
			ДоступноеПоле.Представление = РезультатВыборкаСвойств.Наименование;
			ДоступноеПоле.Измерение     = Истина;
			ДоступноеПоле.Отбор         = Истина;
			ДоступноеПоле.Порядок       = Ложь;
			ДоступноеПоле.ТипЗначения   = РезультатВыборкаСвойств.ТипЗначения;
			
			ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(РезультатВыборкаСвойств.ТипЗначения);
			
			РодительПоля = ВетвьСвойств.Родитель.Родитель;
			ПредставлениеРодителя = ВетвьСвойств.Родитель.Представление;
			Пока РодительПоля <> Неопределено Цикл
				ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
				РодительПоля = РодительПоля.Родитель;
			КонецЦикла;
			ПолноеПредставление = ДоступноеПоле.Представление+" ("+ПредставлениеРодителя+")";
			ДоступноеПоле.ПолноеПредставление = ПолноеПредставление;
			
			ВетвьСвойств.Строки.Сортировать("Представление");
			
			ТекстЗапроса = ЭтотОбъект.ТекстЗапроса;
			ТекИмяСвойства = "Свойство"+НомерСвойства;
			//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСВОЙСТВА", (", ВЫРАЗИТЬ(" + ТекИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ТекИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА"));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСВОЙСТВА", (", " + отПолчитьЗначениеСвойства(ТекИмяСвойства, ТипСвойства) + " КАК " + ТекИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС + "//ВЫБРАННЫЕСВОЙСТВА"));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕИТОГИСВОЙСТВА", (",МАКСИМУМ(" + ТекИмяСвойства + "Значение)	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕИТОГИСВОЙСТВА"));
			//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСГРУППИРОВАТЬПО", (", ВЫРАЗИТЬ(" + ТекИмяСвойства + ".Значение КАК " + ТипСвойства + ")	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСГРУППИРОВАТЬПО"));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСГРУППИРОВАТЬПО", (", " + отПолчитьЗначениеСвойства(ТекИмяСвойства, ТипСвойства) + " //СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСГРУППИРОВАТЬПО"));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСОЕДИНЕНИЯ", ("{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ТекИмяСвойства + " ПО " + ТекИмяСвойства + ".Объект = " + СтрЗаменить(РезультатОткрытия.ПутьКДанным,"@","") + " И " + ТекИмяСвойства + ".Свойство = &" + ИмяПараметра + "}	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСОЕДИНЕНИЯ"));
			ЭтотОбъект.ТекстЗапроса = ТекстЗапроса;
		Иначе
			ИмяСвойства = СвязанноеСвойство.Ключ;
			ВетвьВладельцаСвойств = ТекДеревоДоступныхПолей.Строки.Найти(Измерения.Имя, "Имя");
			РодительПоля = ВетвьВладельцаСвойств.Родитель;
			ПредставлениеРодителя = ВетвьВладельцаСвойств.Представление;
			Пока РодительПоля <> Неопределено Цикл
				ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
				РодительПоля = РодительПоля.Родитель;
			КонецЦикла;
			ПолноеПредставление = РезультатВыборкаСвойств.Наименование+" ("+ПредставлениеРодителя+")";
		КонецЕсли;
		
		Если РезультатВыбора = Неопределено Тогда
			РезультатОткрытия.Имя = ИмяСвойства;
			РезультатОткрытия.ПутьКДанным = ИмяСвойства;
			РезультатОткрытия.Представление = РезультатВыборкаСвойств.Наименование;
			РезультатОткрытия.ПолноеПредставление = ПолноеПредставление;
		Иначе
			РезультатВыбора.Имя = ИмяСвойства;
			РезультатВыбора.ПутьКДанным = ИмяСвойства;
			РезультатВыбора.Представление = РезультатВыборкаСвойств.Наименование;
		КонецЕсли;
		ВыбралиСвойство = Истина;	
	КонецЕсли;
	
	Возврат ВыбралиСвойство;
КонецФункции // отВыбратьСвойство()

// Процедура сохранение настройки свойств и отборов
//
// Параметры:
//	ЭтотОбъект           - Объект		   - Переданный объект
//	СохраненныеНастройки - ТаблицаЗначений - Таблица настроек
//	ОбъектКопирования    - Объект		   - Объект копирования
//
Процедура отЗаполнитьНастройкиСвойствИОтборов(ЭтотОбъект, СохраненныеНастройки = Неопределено, ОбъектКопирования = Неопределено)
	
	Попытка 
		Если СохраненныеНастройки = Неопределено Тогда
			ТаблицаОтбора = Новый ТаблицаЗначений;
			ТаблицаОтбора.Колонки.Добавить("Имя");
			ТаблицаОтбора.Колонки.Добавить("ПутьКДанным");
			ТаблицаОтбора.Колонки.Добавить("Представление");
			ТаблицаОтбора.Колонки.Добавить("Использование");
			ТаблицаОтбора.Колонки.Добавить("ВидСравнения");
			ТаблицаОтбора.Колонки.Добавить("Значение");
			Для Каждого ТекОтбор Из ОбъектКопирования.ПостроительОтчета.Отбор Цикл
				НовыйОтбор = ТаблицаОтбора.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйОтбор, ТекОтбор);
			КонецЦикла;
		Иначе
			ТаблицаОтбора = СохраненныеНастройки.ОтборПостроителя; 
		КонецЕсли;
	Исключение 
		ТаблицаОтбора = Новый ТаблицаЗначений;
		ТаблицаОтбора.Колонки.Добавить("ПутьКДанным");
	КонецПопытки;
	
	ИзмеренияСтроки = ЭтотОбъект.ИзмеренияСтроки;
	ИзмеренияКолонки = ЭтотОбъект.ИзмеренияКолонки;
	ПараметрыИзмеренийСтрок = ЭтотОбъект.ПараметрыИзмеренийСтрок;
	ПостроительОтчета = ЭтотОбъект.ПостроительОтчета;
	
	Попытка 
		Если СохраненныеНастройки = Неопределено Тогда
			СохраненнаяСтруктураСвязиСвойств = ОбъектКопирования.СтруктураСвязиСвойств;
		Иначе
			СохраненнаяСтруктураСвязиСвойств = СохраненныеНастройки.СтруктураСвязиСвойств;
		КонецЕсли;
		
		ВосстанавливаемаяСтруктураСвязиСвойств = ЭтотОбъект.СтруктураСвязиСвойств;
	Исключение 
		СохраненнаяСтруктураСвязиСвойств = Новый Структура;
	КонецПопытки;
	
	ДеревоДоступныхПолей = ЭтотОбъект.ДеревоДоступныхПолей;
	
	ПерезалитьПостроитель = Ложь;
	Для Каждого СвойствоПоиска Из СохраненнаяСтруктураСвязиСвойств Цикл
		// Проверяем есть ли полное совпадение Свойства
		Если (ВосстанавливаемаяСтруктураСвязиСвойств.Свойство(СвойствоПоиска.Ключ))
			И ((ВосстанавливаемаяСтруктураСвязиСвойств[СвойствоПоиска.Ключ].Свойство = СвойствоПоиска.Значение.Свойство)
			И (ВосстанавливаемаяСтруктураСвязиСвойств[СвойствоПоиска.Ключ].ИмяИзмерения = СвойствоПоиска.Значение.ИмяИзмерения)) Тогда
			// да все полностью совпало не надо ничего изменять
			Продолжить;
		КонецЕсли;
		
		// полного совпадения нет а значит надо проверить, стоит ли восстанавливать это свойство
		// ищем в измерениях 
		Строка = ИзмеренияСтроки.Найти(СвойствоПоиска.Ключ, "ПутьКДанным");
		Колонка = ИзмеренияКолонки.Найти(СвойствоПоиска.Ключ, "ПутьКДанным");
		Отбор = ТаблицаОтбора.Найти(СвойствоПоиска.Ключ, "ПутьКДанным");
		// ищем в дополнительных полях
		Поле = Неопределено;
		Если ТипЗнч(ПараметрыИзмеренийСтрок) = Тип("Структура") Тогда
			ПараметрыИзмеренийСтрок = ПараметрыИзмеренийСтрок;
			ТекИзмерение = ДеревоДоступныхПолей.Строки.Найти(СвойствоПоиска.Значение.ПутьКДаннымИзмерения, "ПутьКДанным", Истина);
			Если ТекИзмерение <> Неопределено Тогда
				ПутьКДанным = СтрЗаменить(ТекИзмерение.ПутьКДанным, ".", "_");
				Если ПараметрыИзмеренийСтрок.Свойство(ПутьКДанным) Тогда
					Если ПараметрыИзмеренийСтрок[ПутьКДанным].Свойство("Поля") Тогда
						ДополнительныеПоля = ПараметрыИзмеренийСтрок[ПутьКДанным].Поля;
						Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
							Поле = ДополнительныеПоля.Найти(СвойствоПоиска.Ключ, "ПутьКДанным");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Строка <> Неопределено ИЛИ Колонка <> Неопределено ИЛИ Поле <> Неопределено ИЛИ Отбор <> Неопределено Тогда
			НайденноеСвойство = Неопределено;
			// нет полного совпадения поищем может данное свойство есть, но под другим ключом
			Для Каждого ТекСвойство Из ВосстанавливаемаяСтруктураСвязиСвойств Цикл
				Если (ТекСвойство.Значение.ИмяИзмерения = СвойствоПоиска.Значение.ИмяИзмерения)
					И (ТекСвойство.Значение.Свойство = СвойствоПоиска.Значение.Свойство) Тогда
					НайденноеСвойство = ТекСвойство.Ключ;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НайденноеСвойство = Неопределено Тогда
				// в отчете не найдено свойство поищем, а есть ли оно в СвойстваОбъектов 
				ТекЗапрос = Новый Запрос("ВЫБРАТЬ
				|	СвойстваОбъектов.Ссылка КАК Свойство,
				|	СвойстваОбъектов.Наименование КАК Наименование,
				|	СвойстваОбъектов.ТипЗначения КАК ТипЗначения
				|ИЗ
				|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
				|ГДЕ
				|	СвойстваОбъектов.ПометкаУдаления = ЛОЖЬ
				|	И СвойстваОбъектов.Ссылка = &Свойство");
				
				ТекЗапрос.УстановитьПараметр("Свойство", СвойствоПоиска.Значение.Свойство);
				РезультатВыборкаСвойств = ТекЗапрос.Выполнить().Выбрать();
				
				Если РезультатВыборкаСвойств.Следующий() Тогда
					
					ТекДеревоДоступныхПолей = ЭтотОбъект.ДеревоДоступныхПолей;
					
					НомерСвойства = ВосстанавливаемаяСтруктураСвязиСвойств.Количество()+1;
					ИмяПараметра = "ПараметрСвойства" + НомерСвойства;
					ПараметрыСвойства = Новый Структура("ИмяИзмерения, ПутьКДаннымИзмерения, Свойство,Представление,ТипЗначения,ИмяПараметра",
					ТекИзмерение.Имя, ТекИзмерение.ПутьКДанным, РезультатВыборкаСвойств.Свойство, РезультатВыборкаСвойств.Наименование, РезультатВыборкаСвойств.ТипЗначения, ИмяПараметра);
					ИмяСвойства = "Свойство"+НомерСвойства+"Значение";
					ВосстанавливаемаяСтруктураСвязиСвойств.Вставить(ИмяСвойства, ПараметрыСвойства);
					
					НайденноеСвойство = ИмяСвойства;
					
					ТекРодитель = ТекИзмерение.Родитель;
					ПредставлениеРодителя = ТекИзмерение.Представление;
					Пока ТекРодитель <> Неопределено Цикл
						ПредставлениеРодителя = ТекРодитель.Представление+"->"+ПредставлениеРодителя;
						ТекРодитель = ТекРодитель.Родитель;
					КонецЦикла;
					
					ВетвьСвойств = ТекИзмерение.Строки.Найти("Свойства", "Имя");
					ДоступноеПоле = ВетвьСвойств.Строки.Добавить();
					ДоступноеПоле.Имя           = ИмяСвойства;
					ДоступноеПоле.ПутьКДанным   = ИмяСвойства;
					ДоступноеПоле.Представление = РезультатВыборкаСвойств.Наименование;
					ДоступноеПоле.ПолноеПредставление = РезультатВыборкаСвойств.Наименование+" ("+ПредставлениеРодителя+")";
					ДоступноеПоле.Измерение     = Истина;
					ДоступноеПоле.Отбор         = Истина;
					ДоступноеПоле.Порядок       = Ложь;
					ДоступноеПоле.ТипЗначения   = РезультатВыборкаСвойств.ТипЗначения;
					ВетвьСвойств.Строки.Сортировать("Представление");
					
					ПутьКДаннымОбъекта = СтрЗаменить(ВетвьСвойств.Строки.Найти("ВыбСвойства", "Имя").ПутьКДанным,"@","");
					
					ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(РезультатВыборкаСвойств.ТипЗначения);
					
					ТекстЗапроса = ЭтотОбъект.ТекстЗапроса;
					ТекИмяСвойства = "Свойство"+НомерСвойства;
					//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСВОЙСТВА", (", ВЫРАЗИТЬ(" + ТекИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ТекИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА"));
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСВОЙСТВА", (", " + отПолчитьЗначениеСвойства(ТекИмяСвойства, ТипСвойства) + " КАК " + ТекИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА"));
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕИТОГИСВОЙСТВА", (",МАКСИМУМ(" + ТекИмяСвойства + "Значение)	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕИТОГИСВОЙСТВА"));
					//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСГРУППИРОВАТЬПО", (", ВЫРАЗИТЬ(" + ТекИмяСвойства + ".Значение КАК " + ТипСвойства + ")	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСГРУППИРОВАТЬПО"));
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСГРУППИРОВАТЬПО", (", " + отПолчитьЗначениеСвойства(ТекИмяСвойства, ТипСвойства) + " //СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСГРУППИРОВАТЬПО"));
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАННЫЕСОЕДИНЕНИЯ", ("{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ТекИмяСвойства + " ПО " + ТекИмяСвойства + ".Объект = " + ПутьКДаннымОбъекта + " И " + ТекИмяСвойства + ".Свойство = &" + ИмяПараметра + "}	//СВОЙСТВО" + Символы.ПС+"//ВЫБРАННЫЕСОЕДИНЕНИЯ"));
					ЭтотОбъект.ТекстЗапроса = ТекстЗапроса;
					ПерезалитьПостроитель = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если НайденноеСвойство=Неопределено Тогда
				// Зачем восстанавливать чего нет
				Если Строка <> Неопределено Тогда
					ИзмеренияСтроки.Удалить(Строка);
				КонецЕсли;
				Если Колонка <> Неопределено Тогда
					ИзмеренияКолонки.Удалить(Колонка);
				КонецЕсли;
				Если Отбор <> Неопределено Тогда
					ТаблицаОтбора.Удалить(Отбор);
				КонецЕсли;
				Если Поле <> Неопределено Тогда
					ПоляПредставление = СтрЗаменить(ПараметрыИзмеренийСтрок[ПутьКДанным].ПоляПредставление, ", "+Поле.Представление, "");
					ПараметрыИзмеренийСтрок[ПутьКДанным].ПоляПредставление = СтрЗаменить(ПоляПредставление, Поле.Представление, "");
					ДополнительныеПоля.Удалить(Поле);
				КонецЕсли;
			Иначе
				// установим корректный путь
				Если Строка <> Неопределено Тогда
					Строка.ПутьКДанным = НайденноеСвойство;
				КонецЕсли;
				Если Колонка <> Неопределено Тогда
					Колонка.ПутьКДанным = НайденноеСвойство;
				КонецЕсли;
				Если Отбор <> Неопределено Тогда
					Отбор.Имя = НайденноеСвойство;
					Отбор.ПутьКДанным = НайденноеСвойство;
				КонецЕсли;
				Если Поле <> Неопределено Тогда
					Поле.Имя = НайденноеСвойство;
					Поле.ПутьКДанным = НайденноеСвойство;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ПерезалитьПостроитель Тогда
		ПостроительОтчета.Текст = ЭтотОбъект.ТекстЗапроса;
		ПостроительОтчета.ЗаполнитьНастройки();
	КонецЕсли;
	
	Если ТаблицаОтбора.Количество()>0 Тогда
		// почистим отборы
		ОтборПостроителя = ПостроительОтчета.Отбор;
		ОтборКоличество = ОтборПостроителя.Количество();
		Для к = 1 По ОтборКоличество Цикл
			ОтборПостроителя.Удалить(ОтборКоличество - к);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ТекОтбор Из ТаблицаОтбора Цикл
		Попытка
			НовыйОтбор = ПостроительОтчета.Отбор.Добавить(ТекОтбор.ПутьКДанным, ТекОтбор.Имя, ТекОтбор.Представление);
			НовыйОтбор.Использование = ТекОтбор.Использование;
			НовыйОтбор.ВидСравнения  = ТекОтбор.ВидСравнения;
			НовыйОтбор.Значение  	 = ТекОтбор.Значение;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры // отЗаполнитьНастройкиСвойствИОтборов()

// Процедура заполнения веток дерева
//
// Параметры:
//	СтрокаДерева - СтрокаДереваЗначений - Строка дерева
//	ИмяКолонки   - Строка               - Имя колонки
//	Значение     - Произвольный         - Значение строки дерева
//
Процедура отЗаполнитьВетвиДерева(СтрокаДерева, ИмяКолонки, Значение)
	
	СтрокаДерева[ИмяКолонки]=Значение;
	Для Каждого ТекСтрока Из СтрокаДерева.Строки Цикл
		отЗаполнитьВетвиДерева(ТекСтрока, ИмяКолонки, Значение);
	КонецЦикла;
		
КонецПроцедуры // отЗаполнитьВетвиДерева()


///////////////////////////////////////////////////////////////////////////////////////////////////////////////
//ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПОСТРОЕНИЯ ОТЧЕТОВ С ИСПОЛЬЗОВАНИЕМ ТАБЛИЦЫ ЗАПРОСОВ             /////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// БЛОК ОБЩИХ ПРОЦЕДУР
///////////////////////////////////////////////////////////////////////////////

// Возвращает строку с указанием типа переменной
//
Функция отПолучитьСтрокуПредставленияТипа(ПередаваемыйТип) Экспорт
	
	Результат = Неопределено;
	
	ТекущийТип = ?(Тип("ОписаниеТипов") = ТипЗнч(ПередаваемыйТип), ПередаваемыйТип.Типы()[0], ПередаваемыйТип);
	
	Если ТекущийТип=Тип("Строка") Тогда
		Результат = "Строка";
	ИначеЕсли ТекущийТип=Тип("Число") Тогда
		Результат = "Число";
	ИначеЕсли ТекущийТип=Тип("Дата") Тогда
		Результат = "Дата";
	ИначеЕсли ТекущийТип=Тип("Булево") Тогда
		Результат = "Булево";	
	Иначе
		Результат = Метаданные.НайтиПоТипу(ТекущийТип).ПолноеИмя();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // отПолучитьСтрокуПредставленияТипа()

// Возвращает строку с указанием формата выражения в запросе для типа переменной
//
Функция отПолучитьСтрокуФорматаВыраженияПоТипу(ПередаваемыйТип) Экспорт
	
	Результат = "";
	ДлинаСтроки = 0;
	Разрядность = 0;
	РазрядностьДробнойЧасти = 0;
	Если Тип("ОписаниеТипов") = ТипЗнч(ПередаваемыйТип) Тогда
		ТекущийТип  = ПередаваемыйТип.Типы()[0];
		ДлинаСтроки = ПередаваемыйТип.КвалификаторыСтроки.Длина;
		КвалификаторыЧисла      = ПередаваемыйТип.КвалификаторыЧисла;
		Разрядность             = КвалификаторыЧисла.Разрядность;
		РазрядностьДробнойЧасти = КвалификаторыЧисла.РазрядностьДробнойЧасти;
	Иначе
		ТекущийТип  = ПередаваемыйТип;
	КонецЕсли;
	
	Если ТекущийТип=Тип("Строка") Тогда
		Если ДлинаСтроки = 0 Тогда
			Результат = "Строка(500)";
		Иначе
			Результат = "Строка("+Строка(ДлинаСтроки)+")";
		КонецЕсли;
	ИначеЕсли ТекущийТип=Тип("Число") Тогда
		Если Разрядность = 0 И РазрядностьДробнойЧасти = 0 Тогда
			Результат = "Число";
		Иначе
			Результат = "Число("+Строка(Разрядность)+", " + Строка(РазрядностьДробнойЧасти) + ")";
		КонецЕсли;
	ИначеЕсли ТекущийТип=Тип("Дата") Тогда
		Результат = "Дата";
	ИначеЕсли ТекущийТип=Тип("Булево") Тогда
		Результат = "Булево";
	Иначе
		Результат = Метаданные.НайтиПоТипу(ТекущийТип).ПолноеИмя();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // отПолучитьСтрокуФорматаВыраженияПоТипу()

// Функция формирует текст запроса по таблице запроса
// Параметры:
//	ОтчетОбъект - Объект отчета
//	ПоляОбъединения - поля для объединения
//	ПоляОтбора - поля указанные для отбора
//	ИспользуемыеТаблицы - имена используемых таблиц
//
Функция отПолучитьТекстЗапросаПоТаблицеЗапросов(ОтчетОбъект, ПоляОбъединения, ПоляОтбора = Неопределено, ИспользуемыеТаблицы = Неопределено, мПустыеПоля = Неопределено)
	
	ТаблицаЗапроса = ОтчетОбъект.ТаблицаЗапроса;
	ФункцииОтчета = ОтчетОбъект.Функции;
	СтруктураТекстаПолей = Новый Структура;
	
	ТаблицыИПоля = Новый ТаблицаЗначений;
	ТаблицыИПоля.Колонки.Добавить("ИмяПоля", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТаблицыИПоля.Колонки.Добавить("ИмяТаблицы", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТаблицыИПоля.Индексы.Добавить("ИмяПоля");
	ТаблицыИПоля.Индексы.Добавить("ИмяТаблицы");
	
	ЕстьОтбор = ЗначениеЗаполнено(ПоляОтбора);
	мНеПокрытыеПоля				= Новый Массив;
	КоличествоСовпадающихПолей	= Новый Структура;
	Для Каждого ТекПоле Из ПоляОбъединения Цикл
		мНеПокрытыеПоля.Добавить(ТекПоле.Ключ);
		КоличествоСовпадающихПолей.Вставить(ТекПоле.Ключ, 0);
	КонецЦикла;
	
	мИспользуемыеФункции = Новый Массив;
	Для Каждого ТекФункция Из ФункцииОтчета Цикл
		Если Не ТекФункция.Использование Тогда Продолжить; КонецЕсли;
		ИмяФункции = ?(ПустаяСтрока(ТекФункция.Имя), "ПустаяФункция", ТекФункция.Имя);
		мИспользуемыеФункции.Добавить(ИмяФункции);
	КонецЦикла;
	
	// перебор всех частей запроса
	Для Каждого ТекТаблица Из ТаблицаЗапроса Цикл
		// отбраковка таблицы разработчиком
		Если Не ТекТаблица.Использование Тогда 
			Продолжить; 
		КонецЕсли;
		
		БесполезнаяТаблица = Истина;
		Для Каждого ТекФункция Из мИспользуемыеФункции Цикл
			Если ТекТаблица[ТекФункция] Тогда
				БесполезнаяТаблица = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если БесполезнаяТаблица Тогда Продолжить; КонецЕсли;
		
		// отбраковка таблицы полями отбора
		Если ЕстьОтбор Тогда
			БесполезнаяТаблица = Ложь;
			Для Каждого	ТекПоле Из ПоляОтбора Цикл
				Если Не ТекТаблица.Поля.Свойство(ТекПоле.Ключ) Тогда
					БесполезнаяТаблица = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если БесполезнаяТаблица Тогда Продолжить; КонецЕсли;
		КонецЕсли;
		
		ТекстПолей = "";
		Для Каждого ТекПоле Из ПоляОбъединения Цикл
			ИмяПоля = ТекПоле.Ключ;
			Если ТекТаблица.Поля.Свойство(ИмяПоля) Тогда
				ТекстПолей = ТекстПолей + ?(ТекстПолей="","",",")+Символы.ПС+ТекТаблица.Имя+"."+ИмяПоля+" КАК "+ИмяПоля;
				
				КоличествоСовпадающихПолей.Вставить(ИмяПоля, КоличествоСовпадающихПолей[ИмяПоля]+1);
				
				НоваяСтрока = ТаблицыИПоля.Добавить();
				НоваяСтрока.ИмяПоля = ИмяПоля;
				НоваяСтрока.ИмяТаблицы = ТекТаблица.Имя;
			Иначе
				ТекстПолей = ТекстПолей + ?(ТекстПолей="","",",")+Символы.ПС+ТекПоле.Значение+" КАК "+ИмяПоля;
			КонецЕсли;
		КонецЦикла;
		
		СтруктураТекстаПолей.Вставить(ТекТаблица.Имя, ТекстПолей);
	КонецЦикла;
	
	ИспользуемыеТаблицы = Новый ТаблицаЗначений;
	ИспользуемыеТаблицы.Колонки.Добавить("ИмяТаблицы");
	ИспользуемыеТаблицы.Колонки.Добавить("ТекстЗапроса");
	ИспользуемыеТаблицы.Колонки.Добавить("ТекстПолей");
	//Поиск уникальных полей, такие таблицы включаются в первую очередь
	Для Каждого ТекПоле Из КоличествоСовпадающихПолей Цикл
		Если ТекПоле.Значение = 1 Тогда
			Если мНеПокрытыеПоля.Найти(ТекПоле.Ключ)=Неопределено Тогда Продолжить; КонецЕсли;
			ИмяТаблицы = ТаблицыИПоля.Найти(ТекПоле.Ключ, "ИмяПоля").ИмяТаблицы;
			
			ТекТаблицаЗапроса = ТаблицаЗапроса.Найти(ИмяТаблицы, "Имя");
			
			НоваяСтрока = ИспользуемыеТаблицы.Добавить();
			НоваяСтрока.ИмяТаблицы = ИмяТаблицы;
			НоваяСтрока.ТекстЗапроса = ТекТаблицаЗапроса.ТекстЗапроса;
			НоваяСтрока.ТекстПолей = СтруктураТекстаПолей[ИмяТаблицы];
			
			ПоляТаблицы = ТекТаблицаЗапроса.Поля;
			Для Каждого ТекПолеТаблицы Из ПоляТаблицы Цикл
				ИмяПоляТаблицы = ТекПолеТаблицы.Ключ;
				ПолеПоиска = мНеПокрытыеПоля.Найти(ИмяПоляТаблицы);
				Если ПолеПоиска<>Неопределено Тогда
					мНеПокрытыеПоля.Удалить(ПолеПоиска);
					СтрокиДляУдаления = ТаблицыИПоля.НайтиСтроки(Новый Структура("ИмяПоля", ИмяПоляТаблицы));
					
					Для Каждого ТекСтрока Из СтрокиДляУдаления Цикл
						ТаблицыИПоля.Удалить(ТекСтрока);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	мПустыеПоля = Новый Массив;
	Для Каждого ТекПоле Из мНеПокрытыеПоля Цикл
		ТаблицыПоля = ТаблицыИПоля.НайтиСтроки(Новый Структура("ИмяПоля", ТекПоле));
		Если ТаблицыПоля.Количество() = 0 Тогда
			мПустыеПоля.Добавить(ТекПоле);
		Иначе
			Для Каждого ТекТаблица Из ТаблицыПоля Цикл
				ИмяТаблицы = ТекТаблица.ИмяТаблицы;
				
				ТекТаблицаЗапроса = ТаблицаЗапроса.Найти(ИмяТаблицы, "Имя");
				НоваяСтрока = ИспользуемыеТаблицы.Добавить();
				НоваяСтрока.ИмяТаблицы = ИмяТаблицы;
				НоваяСтрока.ТекстЗапроса = ТекТаблицаЗапроса.ТекстЗапроса;
				НоваяСтрока.ТекстПолей = СтруктураТекстаПолей[ИмяТаблицы];
				
				ПоляТаблицы = ТекТаблицаЗапроса.Поля;
				Для Каждого ТекПолеТаблицы Из ПоляТаблицы Цикл
					СтрокиДляУдаления = ТаблицыИПоля.НайтиСтроки(Новый Структура("ИмяТаблицы, ИмяПоля", ИмяТаблицы, ТекПолеТаблицы.Ключ));
					Для Каждого ТекСтрока Из СтрокиДляУдаления Цикл
						ТаблицыИПоля.Удалить(ТекСтрока);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса = "";
	Если ИспользуемыеТаблицы.Количество() = 1 Тогда
		ТекстЗапроса = ИспользуемыеТаблицы[0].ТекстЗапроса;
		Если мПустыеПоля.Количество() = 0 Тогда
			ТекстЗапроса = ИспользуемыеТаблицы[0].ТекстЗапроса;
		Иначе
			ТекСтрока = ИспользуемыеТаблицы[0];
			ТекстЗапроса = "ВЫБРАТЬ "+ТекСтрока.ТекстПолей+Символы.ПС+"ИЗ"+Символы.ПС+"("+ТекСтрока.ТекстЗапроса+") КАК "+ТекСтрока.ИмяТаблицы;
		КонецЕсли;
	Иначе
		Для Каждого ТекСтрока Из ИспользуемыеТаблицы Цикл
			ВложенныйТекстЗапроса = "ВЫБРАТЬ "+ТекСтрока.ТекстПолей+Символы.ПС+"ИЗ"+Символы.ПС+"("+ТекСтрока.ТекстЗапроса+") КАК "+ТекСтрока.ИмяТаблицы;
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + ?(ТекстЗапроса="","","ОБЪЕДИНИТЬ ВСЕ")+Символы.ПС+ВложенныйТекстЗапроса;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции // отПолучитьТекстЗапросаПоТаблицеЗапросов()

///////////////////////////////////////////////////////////////////////////////
// БЛОК НАЧАЛЬНОГО ЗАПОЛНЕНИЯ
///////////////////////////////////////////////////////////////////////////////

// Функция возвращает таблицу запроса
// Параметры:
//	ОтчетОбъект - Формируемый отчет
//	ОбластьТаблицЗапроса - Имя области таблиц запроса
//	МакетТексаЗапроса - Имя макета текста запроса
//	СтруктураОписанияОбластиФункций - Структура описания области функций
//	ПоляОбъединения - Поля для объединения
//
Функция отПолучитьТаблицуЗапроса(ОтчетОбъект, ОбластьТаблицЗапроса, МакетТекстЗапроса, СтруктураОписанияОбластиФункций, ПоляОбъединения)
	
	НачальнаяПозицияФункций	= СтруктураОписанияОбластиФункций.НачальнаяПозицияФункций;
	КонечнаяПозицияФункций	= СтруктураОписанияОбластиФункций.КонечнаяПозицияФункций;
	НомераКолонокФункций	= СтруктураОписанияОбластиФункций.НомераКолонокФункций;
	
	ТаблицаЗапроса = Новый ТаблицаЗначений;
	ТаблицаЗапроса.Колонки.Добавить("Имя");
	ТаблицаЗапроса.Колонки.Добавить("Использование", Новый ОписаниеТипов("Булево"));
	ТаблицаЗапроса.Колонки.Добавить("Поля");
	ТаблицаЗапроса.Колонки.Добавить("ТекстЗапроса");
	
	Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
		ИмяФункции = ?(ПустаяСтрока(ТекФункция.Имя), "ПустаяФункция", ТекФункция.Имя);
		ТаблицаЗапроса.Колонки.Добавить(ИмяФункции, Новый ОписаниеТипов("Булево"));
	КонецЦикла;
	
	ВспомогательныйПостроитель = Новый ПостроительЗапроса;
	Для к = 1 по ОбластьТаблицЗапроса.ВысотаТаблицы Цикл
		ИмяТаблицы = ОбластьТаблицЗапроса.Область(к, 1, к, 1).Текст;
		Если ПустаяСтрока(ИмяТаблицы) Тогда Продолжить; КонецЕсли;
		
		Попытка
			ОбластьТекстаТаблицы = МакетТекстЗапроса.ПолучитьОбласть(ИмяТаблицы);
		Исключение
			Сообщить("Ошибка! не существует области запроса <"+ИмяТаблицы+"> !"); 
			Продолжить;
		КонецПопытки;
		ОбластьТекстаТаблицы.УдалитьСтроку(1);
		ОбластьТекстаТаблицы.УдалитьСтроку(ОбластьТекстаТаблицы.КоличествоСтрок());
		ТекстТаблицы = ОбластьТекстаТаблицы.ПолучитьТекст();
		
		НоваяТаблица = ТаблицаЗапроса.Добавить();
		ОбластьФункцийТаблиц = ОбластьТаблицЗапроса.ПолучитьОбласть(к, НачальнаяПозицияФункций, к, КонечнаяПозицияФункций);
		ТаблицаВСценарии = Ложь;
		Для Каждого ПозицияКолонки Из НомераКолонокФункций Цикл
			Использование = ВРег(ОбластьФункцийТаблиц.Область(1, ПозицияКолонки.Значение).Текст)="ИСТИНА";
			ТаблицаВСценарии = (ТаблицаВСценарии ИЛИ Использование);
			ИмяФункции = ?(ПустаяСтрока(ПозицияКолонки.Представление), "ПустаяФункция", ПозицияКолонки.Представление);
			НоваяТаблица[ИмяФункции] = Использование;
		КонецЦикла;
		
		Если Не ТаблицаВСценарии Тогда
			ТаблицаЗапроса.Удалить(НоваяТаблица);
			Продолжить;
		КонецЕсли;
		
		НоваяТаблица.Имя = ИмяТаблицы;
		НоваяТаблица.Использование = Истина;
		НоваяТаблица.Поля = Новый Структура;
		
		ПоляЗапроса = НоваяТаблица.Поля;
		ВспомогательныйПостроитель.Текст = ТекстТаблицы;
		ВспомогательныйПостроитель.ЗаполнитьНастройки();
		ТекстПолейПостроителя = "";
		Для Каждого ТекПоле Из ВспомогательныйПостроитель.ДоступныеПоля Цикл
			Если Не ТекПоле.Поле Тогда Продолжить; КонецЕсли;
			ПутьКДанным = ТекПоле.ПутьКДанным;
			//получим пустое значение поля для ровного счета полей объединения
			//ПустоеЗначение  = ?(ТекПоле.ТипЗначения.Типы()[0] = Тип("Число"),"0","NULL");
			Если ТекПоле.ТипЗначения.Типы()[0] = Тип("Число") Тогда
				ПустоеЗначение  = "0";
			ИначеЕсли ТекПоле.ТипЗначения.Типы()[0] = Тип("Дата") Тогда
				ПустоеЗначение  = "ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)";
			ИначеЕсли ТекПоле.ТипЗначения.Типы()[0] = Тип("Булево") Тогда
				ПустоеЗначение  = "ЛОЖЬ";
			Иначе
				ПустоеЗначение  = "NULL";
			КонецЕсли;
			ПоляЗапроса.Вставить(ПутьКДанным, ПустоеЗначение);
			ТекстПолейПостроителя = ТекстПолейПостроителя + ?(ТекстПолейПостроителя="", "", ",")+Символы.ПС+ПутьКДанным+" КАК "+ПутьКДанным;
			ПоляОбъединения.Вставить(ПутьКДанным, ПустоеЗначение);
		КонецЦикла;
		НоваяТаблица.ТекстЗапроса = ТекстТаблицы+"{ВЫБРАТЬ"+ТекстПолейПостроителя+"}";
	КонецЦикла;
	
	Возврат ТаблицаЗапроса;
	
КонецФункции // отПолучитьТаблицуЗапроса()

// Функция формирует линейный запрос построителя отчетов
// Параметры:
//	ОтчетОбъект - формируемый отчет
//	ТекстЗапроса - текст запроса
//
Функция отПолучитьЛинейныйЗапросПостроителяОтчетов(ОтчетОбъект, ТекстЗапроса) Экспорт
	
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение 
	КонецПопытки;
	
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	ТаблицаВыраженийПостроителя = ОтчетОбъект.ТаблицаВыраженийПостроителя;
	
	СтрЗапросаСвойств    = "";
	СтрСоединенийСвойств = "";
	
	ДоступныеПоляОтчета = ОтчетОбъект.ДеревоДоступныхПолей.Строки;
	
	// Поля запроса
	ТекстПолей			= "";
	// Поля построителя
	ТекстПолейЗапроса	= "";
	// Отборы запроса
	ТекстОтбора = "";
	СтруктураУникальныхИменСоединений = Новый Структура;
	Для Каждого ТекПоле Из ДоступныеПоляОтчета Цикл
		ИмяПоля = СтрЗаменить(ТекПоле.ПутьКДанным, ".", "");
		СтрокаВыражения = ТаблицаВыраженийПостроителя.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным");
		Если СтрокаВыражения<>Неопределено Тогда
			Если ТекПоле.Отбор Тогда
				ТекстОтбора = ТекстОтбора + ?(ТекстОтбора="","", ",")+Символы.ПС+"("+СтрокаВыражения.ВыражениеПоля+").* КАК "+ИмяПоля;
			КонецЕсли;
			Если ТекПоле.Поле Тогда
				ТекстПолей = ТекстПолей + ?(ТекстПолей="","", ",")+Символы.ПС+СтрокаВыражения.ВыражениеПоля+" КАК "+ИмяПоля;
				ТекстПолейЗапроса = ТекстПолейЗапроса + ?(ТекстПолейЗапроса="","", ",")+Символы.ПС+ИмяПоля+".* КАК "+ИмяПоля;
			КонецЕсли;
			Если (Не ПустаяСтрока(СтрокаВыражения.ИмяСоединения)) И (Не СтруктураУникальныхИменСоединений.Свойство(СтрокаВыражения.ИмяСоединения)) Тогда
				СтрСоединенийСвойств = СтрСоединенийСвойств+СтрокаВыражения.ВыражениеСоединения;
				СтруктураУникальныхИменСоединений.Вставить(СтрокаВыражения.ИмяСоединения);
			КонецЕсли;
		ИначеЕсли ТекПоле.Поле Тогда
			ТекстПолей = ТекстПолей + ?(ТекстПолей="", "", ",") + Символы.ПС + "ОбщаяТаблицаДанных." + ТекПоле.ПутьКДанным + " КАК "+ИмяПоля;
			ТекстПолейЗапроса = ТекстПолейЗапроса + ?(ТекстПолейЗапроса="", "", ",") + Символы.ПС + ИмяПоля + ".* КАК " + ИмяПоля;
			Если ТекПоле.Показатель И ТекПоле.Отбор Тогда
				ТекстОтбора = ТекстОтбора + ?(ТекстОтбора="", "", ",") + Символы.ПС + "ОбщаяТаблицаДанных." + ТекПоле.ПутьКДанным + " КАК " + ИмяПоля;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСвойство Из СтруктураСвязиСвойств Цикл
		ПутьКДанным = ТекСвойство.Ключ;
		ДоступноеПолеОтчета = ДоступныеПоляОтчета.Найти(ПутьКДанным, "ПутьКДанным", Истина);
		Если ДоступноеПолеОтчета=Неопределено ИЛИ Не ДоступноеПолеОтчета.Отбор Тогда Продолжить; КонецЕсли;	
		
		ИмяСвойства = СтрЗаменить(ПутьКДанным, "Значение", "");
		ЗначениеСвойства = ТекСвойство.Значение;
		НаименованиеСвойства = ЗначениеСвойства.Представление;
		ИмяПараметра = ЗначениеСвойства.ИмяПараметра;
		ПутьКДаннымПоля = "ОбщаяТаблицаДанных."+ЗначениеСвойства.ИмяИзмерения;
		
		ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(ЗначениеСвойства.ТипЗначения);
		
		//ЗапросСвойстваПоля = "ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ПутьКДанным;
		ЗапросСвойстваПоля = отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " КАК " + ПутьКДанным;
		ЗапросСвойстваСоединение = "{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ПутьКДаннымПоля + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + "}" + Символы.ПС;
		
		ТекстОтбора = ТекстОтбора + ?(ТекстОтбора="","", ",")+Символы.ПС+ЗапросСвойстваПоля;
		ТекстПолей = ТекстПолей + ?(ТекстПолей="","", ",")+Символы.ПС+ЗапросСвойстваПоля;
		ТекстПолейЗапроса = ТекстПолейЗапроса + ?(ТекстПолейЗапроса="","", ",")+Символы.ПС+ПутьКДанным+" КАК "+ПутьКДанным;
		
		СтрСоединенийСвойств   = СтрСоединенийСвойств+ЗапросСвойстваСоединение;
	КонецЦикла;
	
	СтрСоединенийСвойств	= СтрСоединенийСвойств+Символы.ПС+"//ВЫБРАННЫЕСОЕДИНЕНИЯ";
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	"+ТекстПолей+"
	|{ВЫБРАТЬ
	|	"+ТекстПолейЗапроса+"}
	|	ИЗ("+ТекстЗапроса+Символы.ПС+") КАК ОбщаяТаблицаДанных"+Символы.ПС+СтрСоединенийСвойств;
	
	Если Не ПустаяСтрока(ТекстОтбора) Тогда
		ТекстОтбора = "{ГДЕ" + Символы.ПС+ТекстОтбора+Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА}";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса+Символы.ПС+ТекстОтбора;
	
	Возврат ТекстЗапроса;
	
КонецФункции // отПолучитьЛинейныйЗапросПостроителяОтчетов()

// Функция возвращает параметры поля
// Параметры:
//	ОтчетОбъект - Формируемый отчет
//	ОбластьПоля - Имя области поля
//	ОбластьФункцийПоля - Имя области функций поля
//	НомераКолонокФункций - список номеров колонок функций
//
Функция отПолучитьПараметрыПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, НомераКолонокФункций)
	
	ПараметрыПоля = Новый Структура();
	
	ПутьКДанным   = ОбластьПоля.Область(1, 1).Текст;
	Представление = ОбластьПоля.Область(1, 2).Текст;
	Если ПустаяСтрока(Представление) Тогда
		Представление = ПутьКДанным;
		Пока Найти(Представление, ".") > 0 Цикл
			Представление = Сред(Представление, Найти(Представление, ".") + 1);
		КонецЦикла;
	КонецЕсли;
	ПараметрыПоля.Вставить("Представление", Представление);
	
	ИспользованиеПоля = Новый Структура();
	
	Использование = ОбластьПоля.Область(1, 3).Текст;
	Если Не ПустаяСтрока(Использование) Тогда
		ИспользованиеПоля.Вставить("Свойства", Найти(Использование, "СВОЙСТВА") > 0);
		ИспользованиеПоля.Вставить("ОтсутствуетВОстатках", Найти(Использование, "ОТСУТСТВУЕТВОСТАТКАХ") > 0);
	КонецЕсли;
	
	РесурсыПоля = Новый Структура();
	Для Каждого ТекПозиция Из НомераКолонокФункций Цикл
		ТекИспользование = НЕ ВРег(ОбластьФункцийПоля.Область(1, ТекПозиция.Значение).Текст) = "ЛОЖЬ";
		Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
			РесурсыПоля.Вставить(ТекПоказатель.Имя + ТекПозиция.Представление, ТекИспользование);
		КонецЦикла;
	КонецЦикла;
	ИспользованиеПоля.Вставить("Ресурсы", РесурсыПоля);
	
	ПараметрыПоля.Вставить("Использование", ИспользованиеПоля);
	ПараметрыПоля.Вставить("Формат", СокрЛП(ОбластьПоля.Область(1, 4).Текст));
	
	ШиринаКолонки = ОбластьПоля.Область(1, 5).Текст;
	Если ПустаяСтрока(ШиринаКолонки) Тогда
		ПараметрыПоля.Вставить("ШиринаКолонки", 20);
	Иначе
		ПараметрыПоля.Вставить("ШиринаКолонки", Число(ШиринаКолонки));
	КонецЕсли;
	
	Возврат ПараметрыПоля;
	
КонецФункции // отПолучитьПараметрыПоля()

// Функция создает структуру использования полей и ресурсов построителя отчета
// по шаблону, хранящемся в табличном документе макета отчета
//
// Параметры:
//	ОтчетОбъект    - ОтчетОбъект                     - Объект отчета
//  МакетПараметры - ТабличныйДокумент               - Макет отчета
//	ОбластьФункций - ОбластьЯчеекТабличногоДокумента - Область функций
//
//	Возвращаемое значение:
//	Структура - Параметры использования
//
Функция отПолучитьДеревоДоступныхПолейИПараметрыИспользования(ОтчетОбъект, ДоступныеПоля, МакетПараметры, СтруктураОписанияОбластиФункций, СвойстваПутиКДанным, ТаблицаОтборов) Экспорт
	
	ПараметрыИспользованияПолейИПоказателей = Новый Структура();
	
	НачальнаяПозицияФункций = СтруктураОписанияОбластиФункций.НачальнаяПозицияФункций;
	КонечнаяПозицияФункций = СтруктураОписанияОбластиФункций.КонечнаяПозицияФункций;
	НомераКолонокФункций = СтруктураОписанияОбластиФункций.НомераКолонокФункций;
	
	ОтчетОбъект.ИзмеренияСтроки.Очистить();
	ОтчетОбъект.ИзмеренияКолонки.Очистить();
	ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей = Новый Структура();
	ОтчетОбъект.ПараметрыИзмеренийСтрок = Новый Структура();
	ОтчетОбъект.ДеревоДоступныхПолей = Новый ДеревоЗначений;
	
	ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки;
	ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки;
	ПараметрыИспользования = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей;
	ПараметрыИзмерений = ОтчетОбъект.ПараметрыИзмеренийСтрок;
	ДеревоДоступныхПолей = ОтчетОбъект.ДеревоДоступныхПолей;
	ТаблицаВыраженийПостроителя = ОтчетОбъект.ТаблицаВыраженийПостроителя;
	
	// Заполним дерево доступных полей отчета
	ДеревоДоступныхПолей.Колонки.Добавить("Представление",);
	ДеревоДоступныхПолей.Колонки.Добавить("ПолноеПредставление",);
	ДеревоДоступныхПолей.Колонки.Добавить("Имя",);
	ДеревоДоступныхПолей.Колонки.Добавить("ПутьКДанным",);
	ДеревоДоступныхПолей.Колонки.Добавить("Поле", Новый ОписаниеТипов("Булево"));
	ДеревоДоступныхПолей.Колонки.Добавить("Отбор", Новый ОписаниеТипов("Булево"));
	ДеревоДоступныхПолей.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Булево"));
	ДеревоДоступныхПолей.Колонки.Добавить("Измерение", Новый ОписаниеТипов("Булево"));
	ДеревоДоступныхПолей.Колонки.Добавить("Показатель", Новый ОписаниеТипов("Булево"));
	ДеревоДоступныхПолей.Колонки.Добавить("ТипЗначения",);
	
	ОбластьИзмерений = МакетПараметры.ПолучитьОбласть("Измерения");
	Добавлять = Ложь;
	Для к = 1 по ОбластьИзмерений.ВысотаТаблицы Цикл
		ОбластьПоля = ОбластьИзмерений.ПолучитьОбласть("R" + к);
		ОбластьФункцийПоля = ОбластьИзмерений.ПолучитьОбласть(к, НачальнаяПозицияФункций, к, КонечнаяПозицияФункций);
		
		ОбластьПервойКолонки = ОбластьПоля.Область(1, 1);
		ПутьКДанным  		 = ОбластьПервойКолонки.Текст;
		Использование		 = ВРег(ОбластьПоля.Область(1, 3).Текст);
		Ограничения	  		 = ВРег(ОбластьПоля.Область(1, 6).Текст);
		ТипЗначенияИзмерения = Неопределено;
		Если ОбластьПервойКолонки.Автоотступ = 0 и ОбластьПервойКолонки.Отступ = 0 Тогда
			// Новое измерение
			ИмяИзмерения = ПутьКДанным;
			Добавлять = Истина;
			ТипЗначенияИзмерения = Неопределено;
			Если ТаблицаВыраженийПостроителя.Найти(ПутьКДанным, "ПутьКДанным")<>Неопределено Тогда
				Добавлять = Ложь;
				ТипЗначенияИзмерения = ТаблицаВыраженийПостроителя.Найти(ПутьКДанным, "ПутьКДанным").ТипЗначения;
			Иначе
				ПолеПостроителя = ДоступныеПоля.Найти(ИмяИзмерения);
				РодительПолей = ПолеПостроителя;
				Если ПолеПостроителя = Неопределено Тогда 
					Добавлять=Ложь;
					Продолжить; 
				КонецЕсли;
				ТипЗначенияИзмерения = ПолеПостроителя.ТипЗначения;
			КонецЕсли;
			
			ПараметрыПоля = отПолучитьПараметрыПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, НомераКолонокФункций);
			ПараметрыПоля.Вставить("Поля", Новый Структура());
			ПараметрыИспользования.Вставить(ИмяИзмерения, ПараметрыПоля);
			
			Если Добавлять И Не ПустаяСтрока(ПараметрыПоля.Представление) Тогда
				ПолеПостроителя.Представление = ПараметрыПоля.Представление;
			КонецЕсли;	
			НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
			НоваяСтрока.Представление		= ПараметрыПоля.Представление;
			НоваяСтрока.ПолноеПредставление = ПараметрыПоля.Представление;
			НоваяСтрока.Имя					= ИмяИзмерения;
			НоваяСтрока.ПутьКДанным			= ИмяИзмерения;
			НоваяСтрока.Поле				= Найти(Ограничения, "ПОЛЕ")=0;
			НоваяСтрока.Отбор				= Найти(Ограничения, "ОТБОР")=0;
			НоваяСтрока.Порядок             = Найти(Ограничения, "ПОРЯДОК")=0;
			НоваяСтрока.Измерение           = Найти(Ограничения, "ИЗМЕРЕНИЕ")=0;
			НоваяСтрока.ТипЗначения			= ТипЗначенияИзмерения;
			
			ДеревоИзмерения = НоваяСтрока;
		Иначе
			Если Не Добавлять Тогда Продолжить; КонецЕсли;
			Если ПутьКДанным = "Свойства" Тогда
				ТекСтрока = ДеревоИзмерения.Строки.Найти(ПутьКДанным, "Имя");
				Если ТекСтрока = Неопределено Тогда
					ТекСтрока = ДеревоИзмерения.Строки.Добавить();
					
					СвойстваПутиКДанным.Вставить(ИмяИзмерения, "ОбщаяТаблицаДанных."+ИмяИзмерения);
					ВетвьВыбСвойства = ТекСтрока.Строки.Добавить();
					ВетвьВыбСвойства.Имя           = "ВыбСвойства";
					ВетвьВыбСвойства.Представление = "<Выбрать свойство...>";
					ВетвьВыбСвойства.ПутьКДанным   = "@ОбщаяТаблицаДанных."+ИмяИзмерения+"@";
					ВетвьВыбСвойства.Поле		   = Найти(Ограничения, "ПОЛЕ")=0;
					ВетвьВыбСвойства.Отбор		   = Найти(Ограничения, "ОТБОР")=0;
					ВетвьВыбСвойства.Порядок       = Найти(Ограничения, "ПОРЯДОК")=0;
					ВетвьВыбСвойства.Измерение     = Найти(Ограничения, "ИЗМЕРЕНИЕ")=0;
				КонецЕсли;
				ТекСтрока.Имя 				   = ПутьКДанным;
				ТекСтрока.Представление		   = ПутьКДанным;
				ТекСтрока.ПолноеПредставление  = ПутьКДанным;
				ТекСтрока.Поле				   = Найти(Ограничения, "ПОЛЕ")=0;
				ТекСтрока.Отбор				   = Найти(Ограничения, "ОТБОР")=0;
				ТекСтрока.Порядок              = Найти(Ограничения, "ПОРЯДОК")=0;
				ТекСтрока.Измерение            = Найти(Ограничения, "ИЗМЕРЕНИЕ")=0;
				Продолжить;
			КонецЕсли;
			
			Если Найти(ПутьКДанным, "СоставноеПредставление")>0 Тогда
				ТекстПредставления = ОбластьПоля.Область(1, 3).Текст;
				ПутьКПараметру = ?(ПутьКДанным="СоставноеПредставление",ИмяИзмерения, ИмяИзмерения+"."+СтрЗаменить(ПутьКДанным,".СоставноеПредставление",""));
				ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьКПараметру, ПараметрыИспользования);
				Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("СоставноеПредставление") Тогда
					ПараметрыИспользованияДляОсновногоПредставления.Вставить("СоставноеПредставление", Новый Структура("Представление, Поля", СтрЗаменить(ТекстПредставления,".",""), Новый Массив));
					ПараметрыПредставления = ПараметрыИспользованияДляОсновногоПредставления.СоставноеПредставление.Поля;
					ЧислоПолей = СтрЧислоВхождений(ТекстПредставления, "[");
					Для Индекс = 1 по ЧислоПолей Цикл
						ОткрПозиция = Найти(ТекстПредставления, "[");
						ЗакрПозиция = Найти(ТекстПредставления, "]");
						ЧастьПредставления = СокрЛП(Сред(ТекстПредставления, ОткрПозиция+1, ЗакрПозиция-ОткрПозиция-1));
						Если НЕ ИмяИзмерения = ЧастьПредставления Тогда
							ПараметрыПредставления.Добавить(ЧастьПредставления);
						КонецЕсли;
						ТекстПредставления = СокрЛП(Сред(ТекстПредставления, ЗакрПозиция+1));
					КонецЦикла;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			Если РодительПолей = Неопределено Тогда Продолжить; КонецЕсли;
			ПоляПостроителя = РодительПолей.Поля;
			
			Если ПоляПостроителя.Количество() = 0 Тогда Продолжить; КонецЕсли;
			
			ДеревоПоля = ДеревоИзмерения;
			СтруктураПолей = ПараметрыИспользования[ИмяИзмерения]["Поля"];
			
			ПутьКДаннымПоля = ПутьКДанным;
			КоличествоПройденныхЗнаков = 0;
			Для Итерация=1 по СтрЧислоВхождений(ПутьКДаннымПоля, ".") Цикл
				ПозРазделителя = Найти(ПутьКДаннымПоля, ".");
				Идентификатор = Лев(ПутьКДаннымПоля, ПозРазделителя - 1);
				ПутьКДаннымПоля = Сред(ПутьКДаннымПоля, ПозРазделителя + 1);
				КоличествоПройденныхЗнаков = КоличествоПройденныхЗнаков+ПозРазделителя;
				ПолеПостроителя = ПоляПостроителя.Найти(Идентификатор);
				Если ПолеПостроителя = Неопределено Тогда Продолжить; КонецЕсли;
				ПоляПостроителя = ПолеПостроителя.Поля;
				ТекСтрока = ДеревоПоля.Строки.Найти(Идентификатор, "Имя");
				Если ТекСтрока = Неопределено Тогда
					ТекСтрока = ДеревоПоля.Строки.Добавить();
					ТекСтрока.Представление		  = Идентификатор;
					ТекСтрока.ПолноеПредставление = Идентификатор;
					ТекСтрока.Имя				  = Идентификатор;
					ТекСтрока.ПутьКДанным		  = ИмяИзмерения+"."+Лев(ПутьКДанным, КоличествоПройденныхЗнаков - 1);
					ТекСтрока.ТипЗначения		  = ПолеПостроителя.ТипЗначения;
				КонецЕсли;
				
				ДеревоПоля = ТекСтрока;
				Если НЕ СтруктураПолей.Свойство(Идентификатор) Тогда
					СтруктураПолей.Вставить(Идентификатор, Новый Структура());
				КонецЕсли;
				Если НЕ СтруктураПолей[Идентификатор].Свойство("Поля") Тогда
					СтруктураПолей[Идентификатор].Вставить("Поля", Новый Структура());
				КонецЕсли;
				СтруктураПолей = СтруктураПолей[Идентификатор].Поля;
			КонецЦикла;
			
			ПолеПостроителя = ПоляПостроителя.Найти(ПутьКДаннымПоля);
			Если ПолеПостроителя = Неопределено Тогда Продолжить; КонецЕсли;
			
			ПараметрыПоля = отПолучитьПараметрыПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, НомераКолонокФункций);
			ПараметрыПоля.Вставить("Поля", Новый Структура());
			СтруктураПолей.Вставить(ПутьКДаннымПоля, ПараметрыПоля);
			
			Если Не ПустаяСтрока(ПараметрыПоля.Представление) Тогда
				ПолеПостроителя.Представление = ПараметрыПоля.Представление;
			КонецЕсли;	
			
			НоваяСтрока = ДеревоПоля.Строки.Найти(ПутьКДаннымПоля, "Имя");
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = ДеревоПоля.Строки.Добавить();
			КонецЕсли;
			
			ТекРодитель = НоваяСтрока.Родитель.Родитель;
			ПредставлениеРодителя = НоваяСтрока.Родитель.Представление;
			Пока ТекРодитель <> Неопределено Цикл
				ПредставлениеРодителя = ТекРодитель.Представление+"->"+ПредставлениеРодителя;
				ТекРодитель = ТекРодитель.Родитель;
			КонецЦикла;
			
			НоваяСтрока.Представление		= ПараметрыПоля.Представление;
			НоваяСтрока.ПолноеПредставление = ПараметрыПоля.Представление+" ("+ПредставлениеРодителя+")";
			НоваяСтрока.Имя					= ПутьКДаннымПоля;
			НоваяСтрока.ПутьКДанным			= ИмяИзмерения+"."+ПутьКДанным;
			НоваяСтрока.Поле				= Найти(Ограничения, "ПОЛЕ")=0;
			НоваяСтрока.Отбор				= Найти(Ограничения, "ОТБОР")=0;
			НоваяСтрока.Порядок             = Найти(Ограничения, "ПОРЯДОК")=0;
			НоваяСтрока.Измерение           = Найти(Ограничения, "ИЗМЕРЕНИЕ")=0;
			НоваяСтрока.ТипЗначения			= ПолеПостроителя.ТипЗначения;
			
			Если Найти(Использование, "ПОРЯДОК")>0 Тогда
				ИдентификаторПолей = СтрЗаменить(ИмяИзмерения, ".", "_");
				Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
					ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
				КонецЕсли;
				ТаблицаСортировки = Неопределено;
				Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Порядок") Тогда
					ТаблицаСортировки = ПараметрыИзмерений[ИдентификаторПолей]["Порядок"];
				КонецЕсли;
				Если ТипЗнч(ТаблицаСортировки) <> Тип("ТаблицаЗначений") Тогда
					ТаблицаСортировки = Новый ТаблицаЗначений();
					ТаблицаСортировки.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
					ТаблицаСортировки.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
					ТаблицаСортировки.Колонки.Добавить("Убывание",      Новый ОписаниеТипов("Булево"));
					ПараметрыИзмерений[ИдентификаторПолей].Вставить("Порядок", ТаблицаСортировки);
				КонецЕсли;
				ТекСтрока = ТаблицаСортировки.Добавить();
				ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
				ТекСтрока.Представление = НоваяСтрока.Представление;
				ТекСтрока.Убывание      = (Найти(Использование, "ПОРЯДОКВОЗР")=0);
			КонецЕсли;
			
			Если Найти(Использование, "ПОЛЕ")>0 И ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
				ИдентификаторПолей = СтрЗаменить(ИмяИзмерения, ".", "_");
				Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
					ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
				КонецЕсли;
				ТаблицаПолей = Неопределено;
				Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Поля") Тогда
					ТаблицаПолей = ПараметрыИзмерений[ИдентификаторПолей]["Поля"];
				КонецЕсли;
				Если ТипЗнч(ТаблицаПолей) <> Тип("ТаблицаЗначений") Тогда
					ТаблицаПолей = Новый ТаблицаЗначений();
					ТаблицаПолей.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
					ТаблицаПолей.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
					ПараметрыИзмерений[ИдентификаторПолей].Вставить("Поля", ТаблицаПолей);
				КонецЕсли;
				ТекСтрока = ТаблицаПолей.Добавить();
				ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
				ТекСтрока.Представление = НоваяСтрока.Представление;
			КонецЕсли;
			
			Если Найти(Использование, "ОСНОВНОЕПРЕДСТАВЛЕНИЕ") > 0 Тогда
				ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(НоваяСтрока.Родитель.ПутьКДанным, ПараметрыИспользования);
				Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("ОсновноеПредставление") Тогда
					ПараметрыИспользованияДляОсновногоПредставления.Вставить("ОсновноеПредставление", НоваяСтрока.ПутьКДанным);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ТипИзмерения = "Элементы";
		Если Найти(Использование, "ТОЛЬКОИЕРАРХИЯ") > 0 Тогда
			ТипИзмерения = "Только иерархия";
		ИначеЕсли Найти(Использование, "ИЕРАРХИЯ") > 0 Тогда
			ТипИзмерения = "Иерархия";
		КонецЕсли;
		
		Активность = Найти(Использование, "ЛОЖЬ") = 0;
		Если Найти(Использование, "СТРОКА")>0 Тогда
			ТекСтрока = ИзмеренияСтроки.Добавить();
			ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
			ТекСтрока.Представление = НоваяСтрока.ПолноеПредставление;
			ТекСтрока.Использование = Активность;
			ТекСтрока.ТипИзмерения  = ТипИзмерения;
		ИначеЕсли Найти(Использование, "КОЛОНКА")>0 и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
			ТекСтрока = ИзмеренияКолонки.Добавить();
			ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
			ТекСтрока.Представление = НоваяСтрока.ПолноеПредставление;
			ТекСтрока.Использование = Активность;
			ТекСтрока.ТипИзмерения  = ТипИзмерения;
		КонецЕсли;
		
		Если Найти(Использование, "ФИЛЬТР")>0 Тогда
			НовыйОтбор = ТаблицаОтборов.Добавить();
			НовыйОтбор.ПутьКДанным	 = НоваяСтрока.ПутьКДанным;
			НовыйОтбор.Имя			 = НоваяСтрока.Имя;
			НовыйОтбор.Представление = НоваяСтрока.ПолноеПредставление;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПараметрыИспользования.Свойство("ОбщийИтог") Тогда
		ПараметрыИспользования.Вставить("ОбщийИтог", Новый Структура("Представление, Использование", "Итог", Новый Структура));
		
		РесурсыПоля = Новый Структура();
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				РесурсыПоля.Вставить(ТекПоказатель.Имя + ТекФункция.Имя, Истина);
			КонецЦикла;
		КонецЦикла;
		ПараметрыИспользования.ОбщийИтог.Использование.Вставить("Ресурсы", РесурсыПоля);
	КонецЕсли;
	
	// в конце добавим представления и форматирование общих полей
	Если НЕ ПараметрыИспользования.Свойство("ПериодРегистратор") Тогда
		ПараметрыИспользования.Вставить("ПериодРегистратор", Новый Структура("Представление", "По документам движения"));
	КонецЕсли;
	// Добавление предопределенных выражений
	НастройкиПериода = ДоступныеПоля.Найти("Период");
	Если НастройкиПериода<>Неопределено И НастройкиПериода.Поле Тогда
		
		ДеревоПериода = ДеревоДоступныхПолей.Строки.Найти("Период", "ПутьКДанным");
		Если ДеревоПериода=Неопределено Тогда
			ДеревоПериода = ДеревоДоступныхПолей.Строки.Добавить();
			ДеревоПериода.Представление			= "Период";
			ДеревоПериода.ПолноеПредставление	= "Период";
			ДеревоПериода.Имя					= "Период";
			ДеревоПериода.ПутьКДанным			= "Период";
			ДеревоПериода.Поле					= Истина;
			ДеревоПериода.Отбор					= Ложь;
			ДеревоПериода.Порядок      	   	    = Истина;
			ДеревоПериода.Измерение        	 	= Истина;
			ДеревоПериода.ТипЗначения			= Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата));
		КонецЕсли;
		
		МассивПериодов = Новый Структура;
		МассивПериодов.Вставить("ПериодГод", "НАЧАЛОПЕРИОДА(ОбщаяТаблицаДанных.Период, ГОД)");
		МассивПериодов.Вставить("ПериодКвартал", "НАЧАЛОПЕРИОДА(ОбщаяТаблицаДанных.Период, КВАРТАЛ)");
		МассивПериодов.Вставить("ПериодМесяц", "НАЧАЛОПЕРИОДА(ОбщаяТаблицаДанных.Период, МЕСЯЦ)");
		МассивПериодов.Вставить("ПериодНеделя", "НАЧАЛОПЕРИОДА(ОбщаяТаблицаДанных.Период, НЕДЕЛЯ)");
		МассивПериодов.Вставить("ПериодДень", "НАЧАЛОПЕРИОДА(ОбщаяТаблицаДанных.Период, ДЕНЬ)");
		
		Если НЕ ПараметрыИспользования.Свойство("ПериодГод") Тогда
			ПараметрыИспользования.Вставить("ПериодГод", Новый Структура("Представление, Формат", "По годам", "ДФ='гггг ""г.""'"));
		КонецЕсли;
		Если НЕ ПараметрыИспользования.Свойство("ПериодКвартал") Тогда
			ПараметрыИспользования.Вставить("ПериодКвартал", Новый Структура("Представление, Формат", "По кварталам", "ДФ='к ""квартал"" гггг ""г.""'"));
		КонецЕсли;
		Если НЕ ПараметрыИспользования.Свойство("ПериодМесяц") Тогда
			ПараметрыИспользования.Вставить("ПериодМесяц", Новый Структура("Представление, Формат", "По месяцам", "ДФ='ММММ гггг ""г.""'"));
		КонецЕсли;
		Если НЕ ПараметрыИспользования.Свойство("ПериодНеделя") Тогда
			ПараметрыИспользования.Вставить("ПериодНеделя", Новый Структура("Представление, Формат", "По неделям", "ДФ='""Неделя с"" дд.ММ.гггг ""г.""'"));
		КонецЕсли;
		Если НЕ ПараметрыИспользования.Свойство("ПериодДень") Тогда
			ПараметрыИспользования.Вставить("ПериодДень", Новый Структура("Представление, Формат", "По дням", "ДФ=дд.ММ.гггг"));
		КонецЕсли;
		
		Для Каждого ТекПериод Из МассивПериодов Цикл
			ПутьКДанным = ТекПериод.Ключ;
			Если ДоступныеПоля.Найти(ПутьКДанным)=Неопределено И ДеревоПериода.Строки.Найти(ПутьКДанным, "ПутьКДанным")=Неопределено Тогда
				ПараметрыПериода = ПараметрыИспользования[ПутьКДанным];
				НоваяСтрока = ДеревоПериода.Строки.Добавить();
				НоваяСтрока.Представление		= ПараметрыПериода.Представление;
				НоваяСтрока.ПолноеПредставление = ПараметрыПериода.Представление;
				НоваяСтрока.Имя					= ПутьКДанным;
				НоваяСтрока.ПутьКДанным			= ПутьКДанным;
				НоваяСтрока.Поле				= Истина;
				НоваяСтрока.Отбор				= Ложь;
				НоваяСтрока.Порядок             = Истина;
				НоваяСтрока.Измерение           = Истина;
				НоваяСтрока.ТипЗначения			= Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата));
				Если ТаблицаВыраженийПостроителя.Найти(ПутьКДанным, "ПутьКДанным")=Неопределено Тогда
					НовоеВыражение = ТаблицаВыраженийПостроителя.Добавить();
					НовоеВыражение.ПутьКДанным	 	= ПутьКДанным;
					НовоеВыражение.ВыражениеПоля 	= ТекПериод.Значение;
					НовоеВыражение.ВыражениеИтога	= "";
					НовоеВыражение.Поля			 	= Новый Массив;
					НовоеВыражение.Поля.Добавить("Период");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	Для Каждого ТекПараметр Из ПараметрыИзмерений Цикл
		СтрПредставленийПолей = "";
		Если ТекПараметр.Значение.Свойство("Поля") Тогда
			Если ТипЗнч(ТекПараметр.Значение.Поля) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекСтрока Из ТекПараметр.Значение.Поля Цикл
					СтрПредставленийПолей = СтрПредставленийПолей + ?(СтрПредставленийПолей = "", "", ", ") + ТекСтрока.Представление;
				КонецЦикла;
				
				ТекПараметр.Значение.Вставить("ПоляПредставление", СтрПредставленийПолей);
			КонецЕсли;
		КонецЕсли;
		
		СтрПредставленийПорядка = "";
		Если ТекПараметр.Значение.Свойство("Порядок") Тогда
			Если ТипЗнч(ТекПараметр.Значение.Порядок) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекСтрока Из ТекПараметр.Значение.Порядок Цикл
					СтрПредставленийПорядка = СтрПредставленийПорядка + ?(СтрПредставленийПорядка = "", "", ", ") + ТекСтрока.Представление + ?(ТекСтрока.Убывание, " (убыв)", "");
				КонецЦикла;
				ТекПараметр.Значение.Вставить("ПорядокПредставление", СтрПредставленийПорядка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыИспользования;
	
КонецФункции //	отПолучитьДеревоДоступныхПолейИПараметрыИспользования()

// Процедура начальной инициализации отчета для таблицы запросов (по построителю и макету)
//
// Параметры:
//	СтруктураПараметрыЗаполнения - Структура    - Структура параметров заполнения
//	Права						 - Неопределено - права	
//
Процедура отЗаполнитьНачальныеНастройкиДляТаблицыЗапросов(СтруктураПараметрыЗаполнения, Права = Неопределено) Экспорт
	
	// заполним настройки по макету
	ОтчетОбъект         = СтруктураПараметрыЗаполнения["ОтчетОбъект"];
	ИмяСценария			= СтруктураПараметрыЗаполнения["ИмяМакетаТекстаЗапроса"];
	ИмяМакетаПараметров = СтруктураПараметрыЗаполнения["ИмяМакетаПараметров"];
	
	Попытка МакетТекстЗапроса = ОтчетОбъект.ПолучитьМакет("ТекстЗапроса");
	Исключение
		Сообщить("Ошибка! Не найден макет для заполнения текста запроса!"); 
		Возврат;
	КонецПопытки;
	
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	ВидОтчета         = ОтчетОбъект.ВидОтчета;
	
	ТаблицаВыраженийПостроителя = Новый ТаблицаЗначений;
	ТаблицаВыраженийПостроителя.Колонки.Добавить("Имя",);
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ПутьКДанным", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ВыражениеПоля",);
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ИмяСоединения",);
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ВыражениеИтога",);
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ВыражениеСоединения",);
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ТипЗначения");
	ТаблицаВыраженийПостроителя.Колонки.Добавить("РассчитыватьПо", Новый ОписаниеТипов("Структура"));
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ПоляРассчитыватьПо", Новый ОписаниеТипов("Структура"));
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ПоляВыражениеИтога", Новый ОписаниеТипов("Структура"));
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ПоляВыражениеПоля", Новый ОписаниеТипов("Структура"));
	ТаблицаВыраженийПостроителя.Колонки.Добавить("Поля", Новый ОписаниеТипов("Массив"));
	ТаблицаВыраженийПостроителя.Колонки.Добавить("ИспользоватьОбщийИтог", Новый ОписаниеТипов("Булево"));
	ТаблицаВыраженийПостроителя.Индексы.Добавить("ПутьКДанным");
	ОтчетОбъект.ТаблицаВыраженийПостроителя = ТаблицаВыраженийПостроителя;
	
	ДополнительныеПоляОтчета = Новый ТаблицаЗначений;
	ДополнительныеПоляОтчета.Колонки.Добавить("Имя");
	ДополнительныеПоляОтчета.Колонки.Добавить("ПутьКДанным");
	ДополнительныеПоляОтчета.Колонки.Добавить("ПолноеПредставление");
	ДополнительныеПоляОтчета.Колонки.Добавить("ШиринаКолонки");
	ДополнительныеПоляОтчета.Колонки.Добавить("Формат");
	ДополнительныеПоляОтчета.Колонки.Добавить("НеСвязанные");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьИтогПоИерархии");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьИтогДляВсехУровней");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьОбщийИтог");
	ОтчетОбъект.ДополнительныеПоляОтчета = ДополнительныеПоляОтчета;
	
	МакетПараметры = ОтчетОбъект.ПолучитьМакет(ИмяМакетаПараметров);
	ОбластьПоказателей 	 = МакетПараметры.ПолучитьОбласть("Показатели");
	ОбластьФункций     	 = МакетПараметры.ПолучитьОбласть("Функции");
	ОбластьТаблицЗапроса = МакетПараметры.ПолучитьОбласть("ТаблицыЗапроса");
	
	ЕстьСценарии = Ложь;
	Попытка
		ОбластьСценариев = МакетПараметры.ПолучитьОбласть("ИменаСценариев");
		ЕстьСценарии = Истина;
	Исключение
	КонецПопытки;
	
	ЕстьВыражения = Ложь;
	Попытка
		ОбластьВыражений = МакетПараметры.ПолучитьОбласть("Выражения");
		ЕстьВыражения = Истина;
	Исключение
	КонецПопытки;
	
	// Получим параметры использования функций для текущего макета
	Если ЕстьСценарии Тогда
		Для к = 1 по ОбластьСценариев.ВысотаТаблицы Цикл
			Если ОбластьСценариев.Область(к, 1).Текст = ИмяСценария Тогда
				ОбластьИспользованияФункций = ОбластьСценариев.ПолучитьОбласть("R" + к);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НомераКолонокФункций = Новый СписокЗначений;
	
	// Заполним функции отчета
	ОтчетОбъект.Функции.Очистить();
	ШиринаОбластиФункций = ОбластьФункций.ШиринаТаблицы;
	Для к = 1 по ШиринаОбластиФункций Цикл
		Если ЕстьСценарии И (Не Булево(ОбластьИспользованияФункций.Область(1, к + 7).Текст)) Тогда Продолжить; КонецЕсли;
		Идентификатор = ОбластьФункций.Область(1, к).Текст;
		Представление = ОбластьФункций.Область(2, к).Текст;
		Представление = ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Если ВидОтчета = Перечисления.ВидыОтчетов.Остатки Тогда
			Представление = "Остаток";
		КонецЕсли;
		НомераКолонокФункций.Добавить(к, Идентификатор);
		отДобавитьФункциюПоказатель(ОтчетОбъект.Функции, Идентификатор, Представление, Истина);
	КонецЦикла;
	
	СтруктураИтоговыхВыраженийПоказателей = Новый Структура;
	// Заполним показатели отчета
	ОтчетОбъект.Показатели.Очистить();
	Для к = 1 по ОбластьПоказателей.ВысотаТаблицы Цикл
		Идентификатор  = ОбластьПоказателей.Область(к, 1).Текст;
		Представление  = ОбластьПоказателей.Область(к, 2).Текст;
		Представление  = ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Использование  = ВРег(ОбластьПоказателей.Область(к, 3).Текст) = "ИСТИНА";
		Форматирование = ОбластьПоказателей.Область(к, 4).Текст;
		ШиринаКолонки = ОбластьПоказателей.Область(к, 5).Текст;
		Если ПустаяСтрока(ШиринаКолонки)  Тогда
			ШиринаКолонки = 14;
		Иначе
			ШиринаКолонки = Число(ШиринаКолонки);
		КонецЕсли;
			
		ПромежуточнаяОбласть = МакетПараметры.Область("Функции");
		НачальнаяПозицияФункций	= ПромежуточнаяОбласть.Лево;
		КонечнаяПозицияФункций	= ПромежуточнаяОбласть.Право;
		ОбластьФункцийПоказателя = ОбластьПоказателей.ПолучитьОбласть(к, НачальнаяПозицияФункций, к, КонечнаяПозицияФункций);
		
		Для Каждого ПозицияКолонки Из НомераКолонокФункций Цикл
			ИмяРесурса = Идентификатор + ПозицияКолонки.Представление;
			ЗначениеВыражения = ОбластьФункцийПоказателя.Область(1, ПозицияКолонки.Значение).Текст;
			ЗначениеВыражения = ?(ПустаяСтрока(ЗначениеВыражения),"СУММА",ЗначениеВыражения);
			Если ВРег(ЗначениеВыражения) = "СУММА" ИЛИ ВРег(ЗначениеВыражения) = "МАКСИМУМ"
				ИЛИ ВРег(ЗначениеВыражения) = "МИНИМУМ" ИЛИ ВРег(ЗначениеВыражения) = "СРЕДНЕЕ" Тогда
				СтруктураИтоговыхВыраженийПоказателей.Вставить(ИмяРесурса, ЗначениеВыражения+"("+ИмяРесурса+")");
			Иначе
				СтруктураИтоговыхВыраженийПоказателей.Вставить(ИмяРесурса, ЗначениеВыражения);
				НовоеВыражение = ТаблицаВыраженийПостроителя.Добавить();
				НовоеВыражение.ПутьКДанным			= ИмяРесурса;
				НовоеВыражение.ВыражениеПоля		= "";
				НовоеВыражение.ВыражениеИтога		= "";
				НовоеВыражение.ИмяСоединения		= "";
				НовоеВыражение.ВыражениеСоединения	= "";
			КонецЕсли;
		КонецЦикла;
		отДобавитьФункциюПоказатель(ОтчетОбъект.Показатели, Идентификатор, Представление, Использование, Форматирование, ШиринаКолонки);
	КонецЦикла;
	
	ОтчетОбъект.СтруктураИтоговыхВыраженийПоказателей = СтруктураИтоговыхВыраженийПоказателей;
	
	СтруктураОписанияОбластиФункций = Новый Структура("НачальнаяПозицияФункций, КонечнаяПозицияФункций, НомераКолонокФункций", НачальнаяПозицияФункций, КонечнаяПозицияФункций, НомераКолонокФункций);
	
	ПоляОбъединения = Новый Структура;
	ОтчетОбъект.ТаблицаЗапроса = отПолучитьТаблицуЗапроса(ОтчетОбъект, ОбластьТаблицЗапроса, МакетТекстЗапроса, СтруктураОписанияОбластиФункций, ПоляОбъединения);
	
	ТекстЗапроса = отПолучитьТекстЗапросаПоТаблицеЗапросов(ОтчетОбъект, ПоляОбъединения);
	ПромежуточныйПостроитель = Новый ПостроительЗапроса;
	ПромежуточныйПостроитель.Текст = ТекстЗапроса;
	ПромежуточныйПостроитель.ЗаполнитьНастройки();
	ДоступныеПоля = ПромежуточныйПостроитель.ДоступныеПоля;
	
	// Заполним выражения отчета
	Если ЕстьВыражения Тогда
		Добавлять = Ложь;
		ТипыИзмерения = Новый Массив;
		НовоеВыражение = Неопределено;
		Для к = 1 по ОбластьВыражений.ВысотаТаблицы Цикл
			ОбластьПоля = ОбластьВыражений.ПолучитьОбласть("R" + к);
			ОбластьПервойКолонки = ОбластьПоля.Область(1, 1);
			ВыражениеПоля		 = ОбластьПоля.Область(1, 2).Текст;
			ВыражениеИтога		 = ОбластьПоля.Область(1, 3).Текст;
			РассчитыватьПо		 = ОбластьПоля.Область(1, 6).Текст;
			СтруктураРассчитыватьПо	= Новый Структура;
			Если ЗначениеЗаполнено(РассчитыватьПо) Тогда
				Позиция = Найти(РассчитыватьПо, ",");
				Пока Позиция>0 Цикл
					ТекПоле = СокрЛП(Лев(РассчитыватьПо, Позиция-1));
					РассчитыватьПо = СокрЛП(Сред(РассчитыватьПо, Позиция+1));
					Позиция = Найти(РассчитыватьПо, ",");
					ИмяПоля=СтрЗаменить(ТекПоле,".*","");
					СтруктураРассчитыватьПо.Вставить(ИмяПоля, Найти(ТекПоле,".*")>0);
				КонецЦикла;
				ИмяПоля=СтрЗаменить(РассчитыватьПо,".*","");
				СтруктураРассчитыватьПо.Вставить(ИмяПоля, Найти(РассчитыватьПо,".*")>0);
			КонецЕсли;
			ПутьКДанным	= СокрЛП(ОбластьПервойКолонки.Текст);
			Если ОбластьПервойКолонки.Автоотступ = 0 и ОбластьПервойКолонки.Отступ = 0 Тогда
				ИмяСоединения		= ОбластьПоля.Область(1, 4).Текст;
				ВыражениеСоединения = ОбластьПоля.Область(1, 5).Текст;
				//создаем составной тип
				Если НовоеВыражение<>Неопределено И ТипыИзмерения.Количество()>0 Тогда
					НовоеВыражение.ТипЗначения = Новый ОписаниеТипов(ТипыИзмерения);
					ТипыИзмерения.Очистить();
				КонецЕсли;
				
				НовоеВыражение = ТаблицаВыраженийПостроителя.Найти(ПутьКДанным, "ПутьКДанным");
				Если НовоеВыражение=Неопределено Тогда
					НовоеВыражение = ТаблицаВыраженийПостроителя.Добавить();
				КонецЕсли;;
				НовоеВыражение.ПутьКДанным			= ПутьКДанным;
				НовоеВыражение.ВыражениеПоля		= СокрЛП(ВыражениеПоля);
				НовоеВыражение.ВыражениеИтога		= СокрЛП(ВыражениеИтога);
				НовоеВыражение.ИмяСоединения		= СокрЛП(ИмяСоединения);
				НовоеВыражение.ВыражениеСоединения	= СокрЛП(ВыражениеСоединения);
				НовоеВыражение.РассчитыватьПо		= СтруктураРассчитыватьПо;
				//НовоеВыражение.ПоляРассчитыватьПо	= Новый Структура;
				НовоеВыражение.Поля					= Новый Массив;
				Добавлять = Истина;
			ИначеЕсли Добавлять Тогда
				ПозицияТочки = Найти(ПутьКДанным, ".");
				ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(ПутьКДанным, ПозицияТочки-1), ПутьКДанным);
				НайденноеПоле = ДоступныеПоля.Найти(ПутьКДаннымПоля);
				Если НайденноеПоле=Неопределено Тогда
					ТаблицаВыраженийПостроителя.Удалить(НовоеВыражение);
					НовоеВыражение = Неопределено;
					Добавлять = Ложь;
					Продолжить;
				КонецЕсли;
				
				ТипыПоля = НайденноеПоле.ТипЗначения.Типы();
				Для Каждого ТекТип Из ТипыПоля Цикл
					Если ТипыИзмерения.Найти(ТекТип)=Неопределено Тогда
						ТипыИзмерения.Добавить(ТекТип);
					КонецЕсли;
				КонецЦикла;
				Если СтруктураРассчитыватьПо.Количество()=0 Тогда
					НовоеВыражение.ИспользоватьОбщийИтог=Истина;
				Иначе
					НовоеВыражение.ПоляРассчитыватьПо.Вставить(ПутьКДаннымПоля, СтруктураРассчитыватьПо);
				КонецЕсли;
				Если Не ПустаяСтрока(ВыражениеИтога) Тогда
					ВыражениеИтога=ВРег(ВыражениеИтога);
					Если ВыражениеИтога = "СУММА" ИЛИ ВыражениеИтога = "МАКСИМУМ"
						ИЛИ ВыражениеИтога = "МИНИМУМ" ИЛИ ВыражениеИтога = "СРЕДНЕЕ" Тогда
						ВыражениеИтога=ВыражениеИтога+"("+ПутьКДаннымПоля+")";
					КонецЕсли;
					НовоеВыражение.ПоляВыражениеИтога.Вставить(ПутьКДаннымПоля, СокрЛП(ВыражениеИтога));
				КонецЕсли;
				Если ПустаяСтрока(ВыражениеПоля) Тогда
					НовоеВыражение.ПоляВыражениеПоля.Вставить(ПутьКДаннымПоля, "ОбщаяТаблицаДанных."+СокрЛП(ПутьКДаннымПоля));
				Иначе
					НовоеВыражение.ПоляВыражениеПоля.Вставить(ПутьКДаннымПоля, СокрЛП(ВыражениеПоля));
				КонецЕсли;
				
				НовоеВыражение.Поля.Добавить(ПутьКДанным);
			КонецЕсли;
		КонецЦикла;
		
		Если НовоеВыражение<>Неопределено И ТипыИзмерения.Количество()>0 Тогда
			НовоеВыражение.ТипЗначения = Новый ОписаниеТипов(ТипыИзмерения);
			ТипыИзмерения.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	СвойстваПутиКДанным = Новый Структура();
	
	ТаблицаОтборов = Новый ТаблицаЗначений;
	ТаблицаОтборов.Колонки.Добавить("ПутьКДанным");
	ТаблицаОтборов.Колонки.Добавить("Имя");
	ТаблицаОтборов.Колонки.Добавить("Представление");
	
	ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей = отПолучитьДеревоДоступныхПолейИПараметрыИспользования(ОтчетОбъект, ДоступныеПоля, МакетПараметры, СтруктураОписанияОбластиФункций, СвойстваПутиКДанным, ТаблицаОтборов);
	
	ДеревоДоступныхПолей = ОтчетОбъект.ДеревоДоступныхПолей;
	
	// Добавим ресурсы в доступные поля
	Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
		Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
			ПутьКДанным = ТекПоказатель.Имя + ТекФункция.Имя;
			Представление = ТекПоказатель.Представление+" "+ТекФункция.Представление;
			
			ЭтоНеВыражение = Истина;
			ТипЗначенияИзмерения = Неопределено;
			СтрокаВыражения = ТаблицаВыраженийПостроителя.Найти(ПутьКДанным, "ПутьКДанным");
			Если СтрокаВыражения<>Неопределено Тогда
				ТипЗначенияИзмерения = СтрокаВыражения.ТипЗначения;
				ЭтоНеВыражение = Ложь;
			Иначе
				ТекПоле = ДоступныеПоля.Найти(ПутьКДанным);
				Если ТекПоле = Неопределено Тогда Продолжить; КонецЕсли;
				ТипЗначенияИзмерения = ТекПоле.ТипЗначения;
			КонецЕсли;
			
			ДоступноеПоле = ДеревоДоступныхПолей.Строки.Найти(ПутьКДанным, "ПутьКДанным");
			Если ДоступноеПоле = Неопределено Тогда
				ДоступноеПоле = ДеревоДоступныхПолей.Строки.Добавить();
				ДоступноеПоле.ПутьКДанным   = ПутьКДанным;
				ДоступноеПоле.Представление = Представление;
				ДоступноеПоле.Измерение     = Ложь;
				ДоступноеПоле.Поле          = Истина;
				Если ЭтоНеВыражение Тогда
					ДоступноеПоле.Отбор     = Истина;
				Иначе
					ДоступноеПоле.Отбор     = (СтрокаВыражения.ВыражениеПоля <> "0");
				КонецЕсли;
				ДоступноеПоле.Порядок       = Истина;
			КонецЕсли;
			
			ДоступноеПоле.Имя = ПутьКДанным;
			Если ЭтоНеВыражение И Не ПустаяСтрока(Представление) Тогда
				ТекПоле.Представление = Представление;
			КонецЕсли;
			ДоступноеПоле.ПолноеПредставление = Представление;
			ДоступноеПоле.Показатель          = Истина;
			ДоступноеПоле.ТипЗначения         = ТипЗначенияИзмерения;
		КонецЦикла;
	КонецЦикла;
	
	ОтчетОбъект.СтруктураСвязиСвойств  = Новый Структура();
	СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	
	Если СвойстваПутиКДанным.Количество()>0 Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = "
		|ВЫБРАТЬ
		|ВложеннаяТаблица.Свойство,
		|ВложеннаяТаблица.Назначение,
		|ВложеннаяТаблица.Назначение.ТипЗначения КАК ТипЗначенияНазначения,
		|ВЫБОР КОГДА ВложеннаяТаблица.Назначение.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ХАРАКТЕРИСТИКИ) ТОГДА
		|	ИСТИНА
		|ИНАЧЕ
		|	ЛОЖЬ
		|КОНЕЦ КАК ЭтоХарактеристика
		|ИЗ(
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначенияСвойств.Свойство КАК Свойство,
		|	ВЫБОР КОГДА НазначенияСвойств.Ссылка.БазовоеНазначение = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ПустаяСсылка) ТОГДА НазначенияСвойств.Ссылка
		|	ИНАЧЕ НазначенияСвойств.Ссылка.БазовоеНазначение
		|	КОНЕЦ КАК Назначение
		|ИЗ ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойств
		|ГДЕ 
		| НазначенияСвойств.Ссылка.ПометкаУдаления = Ложь
		| И НазначенияСвойств.Ссылка.ЭтоГруппа = Ложь) КАК ВложеннаяТаблица
		|УПОРЯДОЧИТЬ ПО ВложеннаяТаблица.Свойство.Наименование,
		|	ВложеннаяТаблица.Назначение.Наименование";
		
		ИскатьСвойстваДляХарактеристики = ДоступныеПоля.Найти("ХарактеристикаНоменклатуры")<>Неопределено И СвойстваПутиКДанным.Свойство("ХарактеристикаНоменклатуры");
		
		НазначенияСвойств = Новый ТаблицаЗначений;
		НазначенияСвойств.Колонки.Добавить("Свойство");
		НазначенияСвойств.Колонки.Добавить("Измерение");
		НазначенияСвойств.Колонки.Добавить("ПутьКДаннымИзмерения");
		НазначенияСвойств.Колонки.Добавить("ПутьКДанным");
		
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДаннымПоля");
		ТаблицаПолей.Колонки.Добавить("ИмяИзмерения");
		ТаблицаПолей.Колонки.Добавить("ПутьКДаннымИзмерения");
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");
		
		Для Каждого ТекВыбСвойство Из СвойстваПутиКДанным Цикл
			ТекПоле = ДоступныеПоля.Найти(ТекВыбСвойство.Ключ);
			Если ТекПоле <> Неопределено Тогда
				Если НЕ (ТекПоле.Измерение ИЛИ ТекПоле.Отбор) Тогда Продолжить; КонецЕсли;
				НовоеПоле = ТаблицаПолей.Добавить();
				НовоеПоле.ПутьКДаннымПоля		= СокрЛП(ТекВыбСвойство.Значение);
				НовоеПоле.ИмяИзмерения			= ТекПоле.Имя;
				НовоеПоле.ПутьКДаннымИзмерения	= ТекПоле.ПутьКДанным;
				НовоеПоле.ТипЗначения			= ТекПоле.ТипЗначения
			КонецЕсли;
		КонецЦикла;
		
		СвойстваВыборка = Запрос.Выполнить().Выбрать();
		Пока СвойстваВыборка.Следующий() Цикл
			Если НЕ СвойстваВыборка.ЭтоХарактеристика Тогда
				ТипНазначения = СвойстваВыборка.ТипЗначенияНазначения.Типы()[0];
				Для Каждого ТекПоле Из ТаблицаПолей Цикл
					Если ТекПоле.ТипЗначения.СодержитТип(ТипНазначения) Тогда
						ТекСтрока = НазначенияСвойств.Добавить();
						ТекСтрока.Свойство    = СвойстваВыборка.Свойство.Ссылка;
						ТекСтрока.Измерение   = ТекПоле.ИмяИзмерения;
						ТекСтрока.ПутьКДаннымИзмерения   = ТекПоле.ПутьКДаннымИзмерения;
						ТекСтрока.ПутьКДанным = ТекПоле.ПутьКДаннымПоля;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ИскатьСвойстваДляХарактеристики Тогда
				ТекСтрока = НазначенияСвойств.Добавить();
				ТекСтрока.Свойство    = СвойстваВыборка.Свойство.Ссылка;
				ТекСтрока.Измерение   = "ХарактеристикаНоменклатуры";
				ТекСтрока.ПутьКДаннымИзмерения   = "ХарактеристикаНоменклатуры";
				ТекСтрока.ПутьКДанным = СокрЛП(СвойстваПутиКДанным["ХарактеристикаНоменклатуры"]);
			КонецЕсли;
		КонецЦикла;
		
		ПредВидСвойства = "";
		НазначенияСвойств.Свернуть("Свойство,Измерение,ПутьКДаннымИзмерения,ПутьКДанным", );
		НазначенияСвойств.Сортировать("Свойство,ПутьКДанным");
		
		Для Каждого ТекСтрока Из НазначенияСвойств Цикл
			НомерСвойства = НазначенияСвойств.Индекс(ТекСтрока) + 1;
			ИмяСвойства = "Свойство" + НомерСвойства;
			Если ТекСтрока.Свойство <> ПредВидСвойства Тогда
				ИмяПараметра = "ПараметрСвойства" + НомерСвойства;
				ПредВидСвойства = ТекСтрока.Свойство;
			КонецЕсли;
			ПараметрыСвойства = Новый Структура("ИмяИзмерения,ПутьКДаннымИзмерения,Свойство,Представление,ТипЗначения,ИмяПараметра",
			ТекСтрока.Измерение, ТекСтрока.ПутьКДаннымИзмерения, ТекСтрока.Свойство.Ссылка, ТекСтрока.Свойство.Наименование, ТекСтрока.Свойство.ТипЗначения, ИмяПараметра);
			КлючСвойства = ИмяСвойства + "Значение";
			СтруктураСвязиСвойств.Вставить(КлючСвойства, ПараметрыСвойства);
			
			ВладелецСвойства = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.Измерение, "ПутьКДанным", Ложь);
			Если ВладелецСвойства = Неопределено Тогда Продолжить; КонецЕсли;
			ВетвьСвойств = ВладелецСвойства.Строки.Найти("Свойства", "Имя");
			ДоступноеПоле = ВетвьСвойств.Строки.Добавить();
			ДоступноеПоле.Имя           = КлючСвойства;
			ДоступноеПоле.ПутьКДанным   = КлючСвойства;
			ДоступноеПоле.Представление = ПараметрыСвойства.Представление;
			ДоступноеПоле.ПолноеПредставление = ПараметрыСвойства.Представление+" ("+ВладелецСвойства.Представление+")";
			ДоступноеПоле.Измерение     = ВетвьСвойств.Измерение;
			ДоступноеПоле.Поле		    = ВетвьСвойств.Поле;
			ДоступноеПоле.Отбор         = ВетвьСвойств.Отбор;
			ДоступноеПоле.Порядок       = ВетвьСвойств.Порядок;
			ДоступноеПоле.ТипЗначения   = ПараметрыСвойства.ТипЗначения;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ДеревоДоступныхПолей.Строки Цикл
		Если ТекСтрока.ПутьКДанным = "Период" ИЛИ ТекСтрока.Строки.Количество() = 0 Тогда Продолжить; КонецЕсли;
		ТекСтрока.Строки.Сортировать("Представление", Истина);
	КонецЦикла;
	
	ТекстЗапроса = отПолучитьЛинейныйЗапросПостроителяОтчетов(ОтчетОбъект, ТекстЗапроса);
	ОтчетОбъект.ТекстЗапроса = ТекстЗапроса;
	ПостроительОтчета.Текст = ТекстЗапроса;
	
	//очистим отбор построителя
	ОтборКоличество = ПостроительОтчета.Отбор.Количество();
	Для к = 1 По ОтборКоличество Цикл
		ПостроительОтчета.Отбор.Удалить(ОтборКоличество - к);
	КонецЦикла;
	Для Каждого ТекОтбор Из ТаблицаОтборов Цикл
		Попытка
			ПостроительОтчета.Отбор.Добавить(ТекОтбор.ПутьКДанным, ТекОтбор.Имя, ТекОтбор.Представление);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	ДоступныеПоля = ПостроительОтчета.ДоступныеПоля;
	
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		ДеревоПоказателей.Колонки.Добавить("Использование");
		ДеревоПоказателей.Колонки.Добавить("Имя");
		ДеревоПоказателей.Колонки.Добавить("ПутьКДанным");
		ДеревоПоказателей.Колонки.Добавить("Представление");
		ДеревоПоказателей.Колонки.Добавить("ФорматнаяСтрока");
		ДеревоПоказателей.Колонки.Добавить("ШиринаКолонки");
		ДеревоПоказателей.Колонки.Добавить("Группа");
		ДеревоПоказателей.Колонки.Добавить("КартинкаЭлемента");
		
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			ЕстьГруппа = Ложь;
			Если ПустаяСтрока(ТекФункция.Имя) Тогда
				НоваяГруппа = ДеревоПоказателей;
			Иначе
				НоваяГруппа = ДеревоПоказателей.Строки.Добавить();
				НоваяГруппа.Имя = ТекФункция.Имя;
				НоваяГруппа.ПутьКДанным = ТекФункция.Имя;
				НоваяГруппа.Представление = ТекФункция.Представление;
				НоваяГруппа.Группа = Истина;
				НоваяГруппа.КартинкаЭлемента = 6;
				ЕстьГруппа = Истина;
			КонецЕсли;
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				ИмяРесурса = ТекПоказатель.Имя+ТекФункция.Имя;
				Если ДоступныеПоля.Найти(ИмяРесурса) <> Неопределено Тогда
					НоваяСтрока = НоваяГруппа.Строки.Добавить();
					НоваяСтрока.Использование = Число(ТекПоказатель.Использование);
					НоваяСтрока.Имя = ТекПоказатель.Имя;
					НоваяСтрока.ПутьКДанным = ИмяРесурса;
					НоваяСтрока.Представление = ТекПоказатель.Представление;
					НоваяСтрока.ФорматнаяСтрока = ТекПоказатель.ФорматнаяСтрока;
					НоваяСтрока.ШиринаКолонки = ТекПоказатель.ШиринаКолонки;
					НоваяСтрока.Группа = Ложь;
					НоваяСтрока.КартинкаЭлемента = 7;
				Иначе
					СтруктураИтоговыхВыраженийПоказателей.Удалить(ИмяРесурса);
				КонецЕсли;
			КонецЦикла;
			Если ЕстьГруппа Тогда
				КоличествоВключенных = НоваяГруппа.Строки.НайтиСтроки(Новый Структура("Использование", 1)).Количество();
				Если КоличествоВключенных=0 Тогда
					НоваяГруппа.Использование = 0;
				ИначеЕсли НоваяГруппа.Строки.Количество() = КоличествоВключенных Тогда
					НоваяГруппа.Использование = 1;
				Иначе
					НоваяГруппа.Использование = 2;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Исключение
	КонецПопытки;
	
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		СтрокаФикс = ОтчетОбъект.ИзмеренияСтроки.Найти("ПериодРегистратор", "ПутьКДанным");
		Если СтрокаФикс <> Неопределено Тогда
			СтрокаФикс.Фиксированное = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отЗаполнитьНачальныеНастройкиДляТаблицыЗапросов()

// Функция получения значения свойства в запросе
//
// Параметры:                          
//	ИмяСвойства		- Строка         - Имя свойства
//  ТипСвойства		- Строка         - Тип свойства
//
// Возвращаемое значение:
//	Строка - Строка получения значения свойства
//
Функция отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) Экспорт 
	Если СтрНайти(ТипСвойства, "Число(") <> 0 Тогда
		ЗначениеСвойства = "ВЫБОР КОГДА " + ИмяСвойства + ".Значение ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КОНЕЦ";
	ИначеЕсли СтрНайти(ТипСвойства, "Строка(") <> 0 Тогда
		ЗначениеСвойства = "ВЫБОР КОГДА " + ИмяСвойства + ".Значение ЕСТЬ NULL ТОГДА """" ИНАЧЕ ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КОНЕЦ";
	ИначеЕсли СтрНайти(ТипСвойства, "Дата") <> 0 Тогда
		ЗначениеСвойства = "ВЫБОР КОГДА " + ИмяСвойства + ".Значение ЕСТЬ NULL ТОГДА ДАТАВРЕМЯ(1, 1, 1) ИНАЧЕ ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КОНЕЦ";
	ИначеЕсли СтрНайти(ТипСвойства, "Булево") <> 0 Тогда
		ЗначениеСвойства = "ВЫБОР КОГДА " + ИмяСвойства + ".Значение ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КОНЕЦ";
	Иначе
		ЗначениеСвойства = "ВЫБОР КОГДА " + ИмяСвойства + ".Значение ЕСТЬ NULL ТОГДА ЗНАЧЕНИЕ(" + ТипСвойства + ".ПустаяСсылка) ИНАЧЕ ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КОНЕЦ";
	КонецЕсли;
	
	Возврат ЗначениеСвойства;
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// БЛОК ФОРМИРОВАНИЯ ОТЧЕТА
///////////////////////////////////////////////////////////////////////////////

// Процедура заполняет выбранные поля и порядок сортировки измерений построителя отчета
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект       - Объект отчета
//	Построитель - ПостроительОтчета - Построитель отчета
//
Функция отПолучитьИспользованиеИзмерений(ОтчетОбъект) Экспорт
	
	ТаблицаИзмеренийСтрок = Новый ТаблицаЗначений;
	ТаблицаИзмеренийСтрок.Колонки.Добавить("ПутьКДанным");
	ТаблицаИзмеренийСтрок.Колонки.Добавить("Представление");
	ТаблицаИзмеренийСтрок.Колонки.Добавить("ТолькоДляПостроителя", Новый ОписаниеТипов("Булево"));
	ТаблицаИзмеренийСтрок.Колонки.Добавить("ТолькоДляМакета", Новый ОписаниеТипов("Булево"));
	ТаблицаИзмеренийСтрок.Колонки.Добавить("Тип");
	
	ТаблицаИзмеренийКолонок = Новый ТаблицаЗначений;
	ТаблицаИзмеренийКолонок.Колонки.Добавить("ПутьКДанным");
	ТаблицаИзмеренийКолонок.Колонки.Добавить("Представление");
	ТаблицаИзмеренийКолонок.Колонки.Добавить("ТолькоДляПостроителя", Новый ОписаниеТипов("Булево"));
	ТаблицаИзмеренийКолонок.Колонки.Добавить("ТолькоДляМакета", Новый ОписаниеТипов("Булево"));
	ТаблицаИзмеренийКолонок.Колонки.Добавить("Тип");
	
	ТаблицаСортировки = Новый ТаблицаЗначений;
	ТаблицаСортировки.Колонки.Добавить("Описание");
	ТаблицаСортировки.Колонки.Добавить("Направление");
	
	ТаблицаПолей = Новый ТаблицаЗначений;
	ТаблицаПолей.Колонки.Добавить("Имя");
	ТаблицаПолей.Колонки.Добавить("ПутьКДанным");
	ТаблицаПолей.Колонки.Добавить("ПолноеПредставление");
	ТаблицаПолей.Колонки.Добавить("ПутьКДаннымРодителя");
	ТаблицаПолей.Колонки.Добавить("ТолькоДляПостроителя", Новый ОписаниеТипов("Булево"));
	ТаблицаПолей.Колонки.Добавить("ТолькоДляМакета", Новый ОписаниеТипов("Булево"));
	ТаблицаПолей.Колонки.Добавить("НеСвязанные", Новый ОписаниеТипов("Булево"));
	ТаблицаПолей.Колонки.Добавить("ШиринаКолонки");
	ТаблицаПолей.Колонки.Добавить("Формат");
	ТаблицаПолей.Колонки.Добавить("ВыводитьИтогПоИерархии", Новый ОписаниеТипов("Булево"));
	ТаблицаПолей.Колонки.Добавить("ВыводитьИтогДляВсехУровней", Новый ОписаниеТипов("Булево"));
	ТаблицаПолей.Колонки.Добавить("ВыводитьОбщийИтог", Новый ОписаниеТипов("Булево"));
	
	РесурсыОтчета  = Новый Массив();
	ДеревоРесурсов = Новый ДеревоЗначений;
	ДеревоРесурсов.Колонки.Добавить("ИмяРесурса");
	ДеревоРесурсов.Колонки.Добавить("Представление");
	ДеревоРесурсов.Колонки.Добавить("ФорматнаяСтрока");
	ДеревоРесурсов.Колонки.Добавить("ШиринаКолонки");
	ДеревоРесурсов.Колонки.Добавить("Одиночный", Новый ОписаниеТипов("Булево"));
	
	СтруктураПолейОтчета = Новый Структура("ТаблицаИзмеренийСтрок, ТаблицаИзмеренийКолонок, ТаблицаСортировки, ТаблицаПолей, РесурсыОтчета, ДеревоРесурсов",
	ТаблицаИзмеренийСтрок, ТаблицаИзмеренийКолонок, ТаблицаСортировки, ТаблицаПолей, РесурсыОтчета, ДеревоРесурсов);
	
	Попытка 
		ПараметрыИзмерений = ОтчетОбъект.ПараметрыИзмеренийСтрок;
	Исключение 
		Возврат СтруктураПолейОтчета;
	КонецПопытки;
	
	Попытка 
		ДеревоДоступныхПолей = ОтчетОбъект.ДеревоДоступныхПолей;
	Исключение 
		Возврат СтруктураПолейОтчета; 
	КонецПопытки;
	
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение КонецПопытки;
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	Попытка
		СтруктураНеСвязанныхПоказателей = ОтчетОбъект.СтруктураНеСвязанныхПоказателей;
	Исключение КонецПопытки;
	Если ТипЗнч(СтруктураНеСвязанныхПоказателей) <> Тип("Структура") Тогда
		СтруктураНеСвязанныхПоказателей = Новый Структура();
	КонецЕсли;
	
	Построитель = ОтчетОбъект.ПостроительОтчета;
	
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			НоваяГруппа = ДеревоРесурсов.Строки.Добавить();
			НоваяГруппа.ИмяРесурса = ТекГруппа.ПутьКДанным;
			НоваяГруппа.Представление = ТекГруппа.Представление;
			НоваяГруппа.ФорматнаяСтрока = ТекГруппа.ФорматнаяСтрока;
			НоваяГруппа.ШиринаКолонки = ТекГруппа.ШиринаКолонки;
			НоваяГруппа.Одиночный = Ложь;
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				РесурсыОтчета.Добавить(ТекСтрока.ПутьКДанным);
				НоваяСтрока = НоваяГруппа.Строки.Добавить();
				НоваяСтрока.ИмяРесурса = ТекСтрока.ПутьКДанным;
				НоваяСтрока.Представление = ТекСтрока.Представление;
				НоваяСтрока.ФорматнаяСтрока = ТекСтрока.ФорматнаяСтрока;
				НоваяСтрока.ШиринаКолонки = ТекСтрока.ШиринаКолонки;
				НоваяСтрока.Одиночный = Ложь;
			КонецЦикла;
			Если НоваяГруппа.Строки.Количество()=0 Тогда
				РесурсыОтчета.Добавить(ТекГруппа.ПутьКДанным);
				НоваяГруппа.Одиночный = Истина;
			КонецЕсли;
		КонецЦикла;
	Исключение
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			НоваяГруппа = ДеревоРесурсов.Строки.Добавить();
			НоваяГруппа.ИмяРесурса = "";
			НоваяГруппа.Представление = ТекФункция.Представление;
			НоваяГруппа.ФорматнаяСтрока = "";
			НоваяГруппа.ШиринаКолонки = "";
			НоваяГруппа.Одиночный = Ложь;
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				ИмяРесурса = ТекПоказатель.Имя + ТекФункция.Имя;
				РесурсыОтчета.Добавить(ИмяРесурса);
				НоваяСтрока = НоваяГруппа.Строки.Добавить();
				НоваяСтрока.ИмяРесурса = ИмяРесурса;
				НоваяСтрока.Представление = ТекПоказатель.Представление;
				НоваяСтрока.ФорматнаяСтрока = ТекПоказатель.ФорматнаяСтрока;
				НоваяСтрока.ШиринаКолонки = ТекПоказатель.ШиринаКолонки;
				НоваяСтрока.Одиночный = Ложь;
			КонецЦикла;
		КонецЦикла;
	КонецПопытки;
	
	Попытка 
		СтруктураСортировкиПоля = ОтчетОбъект.СтруктураСортировки();
		Если ТипЗнч(СтруктураСортировкиПоля) = Тип("СписокЗначений") Тогда
			Для Каждого ТекПорядок Из СтруктураСортировкиПоля Цикл
				//Добавим сортировку
				НоваяСтрока = ТаблицаСортировки.Добавить();
				НоваяСтрока.Описание = ТекПорядок.Представление;
				НоваяСтрока.Направление = ТекПорядок.Значение;
			КонецЦикла;
		Иначе
			//Добавим сортировку
			НоваяСтрока = ТаблицаСортировки.Добавить();
			НоваяСтрока.Описание = СтруктураСортировкиПоля.Описание;
			НоваяСтрока.Направление = СтруктураСортировкиПоля.НаправлениеСортировки;
		КонецЕсли;
	Исключение КонецПопытки;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияСтроки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		
		ЭтоИзмерение = Ложь;
		СтрокаПоиска = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным, "ПутьКДанным", Истина);
		Если СтрокаПоиска<>Неопределено Тогда
			ЭтоИзмерение = СтрокаПоиска.Измерение;
		КонецЕсли;
		Если Не ЭтоИзмерение Тогда Продолжить; КонецЕсли;
		
		ПорядокУстановлен = Ложь;
		ИдентификаторПоля = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "_");
		
		Тип = ТекСтрока.ТипИзмерения;
		Тип = ?(Тип = "Только иерархия", ТипИзмеренияПостроителяОтчета.ТолькоИерархия, ?(Тип = "Иерархия", ТипИзмеренияПостроителяОтчета.Иерархия, ТипИзмеренияПостроителяОтчета.Элементы));
		//Добавим измерение строки
		НоваяСтрока = ТаблицаИзмеренийСтрок.Добавить();
		НоваяСтрока.ПутьКДанным		= ТекСтрока.ПутьКДанным;
		НоваяСтрока.Представление	= ТекСтрока.Представление;
		НоваяСтрока.Тип 			= Тип;
		
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
				//Добавим поле
				НоваяСтрока = ТаблицаПолей.Добавить();
				НоваяСтрока.ПутьКДанным			 = ПутьОсновногоПредставления;
				НоваяСтрока.ТолькоДляПостроителя = Истина;
				// Если основное представление не связано с группировкой, то его надо выводить в итогах
				Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
					ИмяТекПоля=СтрЗаменить(ПутьОсновногоПредставления, ".","");
					Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
						НоваяСтрока.НеСвязанные = Истина;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ПоляСоставногоПредставления = ТекПараметрыИзмерения.СоставноеПредставление.Поля;
				Для Каждого ПолеПредставления Из ПоляСоставногоПредставления Цикл
					//Добавим поле
					НоваяСтрока = ТаблицаПолей.Добавить();
					НоваяСтрока.ПутьКДанным			 = ПолеПредставления;
					НоваяСтрока.ТолькоДляПостроителя = Истина;
					// Если основное представление не связано с группировкой, то его надо выводить в итогах
					Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
						ИмяТекПоля=СтрЗаменить(ПолеПредставления, ".","");
						Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
							НоваяСтрока.НеСвязанные = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Если ПараметрыИзмерений.Свойство(ИдентификаторПоля) Тогда
			//перебор полей сортировки измерений строк
			Если ПараметрыИзмерений[ИдентификаторПоля].Свойство("Порядок") и
				НЕ СтруктураСвязиСвойств.Свойство(ИдентификаторПоля) Тогда
				ПоляСортировки = ПараметрыИзмерений[ИдентификаторПоля].Порядок;
				Если ТипЗнч(ПоляСортировки) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ПоляСортировки Цикл
						Направление = ?(ТекПоле.Убывание, НаправлениеСортировки.Убыв, НаправлениеСортировки.Возр);
						//Добавим сортировку
						НоваяСтрока = ТаблицаСортировки.Добавить();
						НоваяСтрока.Описание	= ТекПоле.ПутьКДанным;
						НоваяСтрока.Направление = Направление;
						ПорядокУстановлен = Истина;
					КонецЦикла;	
				КонецЕсли;	
			КонецЕсли;
			//перебор дополнительных полей измерений строк
			Если ПараметрыИзмерений[ИдентификаторПоля].Свойство("Поля") Тогда
				ДополнительныеПоля = ПараметрыИзмерений[ИдентификаторПоля].Поля;
				Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ДополнительныеПоля Цикл
						ИмяТекПоля=СтрЗаменить(ТекПоле.ПутьКДанным, ".","");
						Если ПустаяСтрока(ИмяТекПоля) Тогда
							Продолжить;
						КонецЕсли;
						НоваяСтрока = ТаблицаПолей.Добавить();
						НоваяСтрока.Имя = ИмяТекПоля;
						НоваяСтрока.ПутьКДанным = ТекПоле.ПутьКДанным;
						НоваяСтрока.ПутьКДаннымРодителя = ТекСтрока.ПутьКДанным;
						
						Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
							Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
								ТекНесвязанныйПоказатель = СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели[ИмяТекПоля];
								НоваяСтрока.ПолноеПредставление = ТекНесвязанныйПоказатель.Представление;
								НоваяСтрока.ШиринаКолонки = ТекНесвязанныйПоказатель.ШиринаКолонки;
								НоваяСтрока.Формат = ТекНесвязанныйПоказатель.Формат;
								НоваяСтрока.НеСвязанные = Истина;
								НоваяСтрока.ВыводитьИтогПоИерархии = ТекНесвязанныйПоказатель.ВыводитьИтогПоИерархии;
								НоваяСтрока.ВыводитьИтогДляВсехУровней = ТекНесвязанныйПоказатель.ВыводитьИтогДляВсехУровней;
								НоваяСтрока.ВыводитьОбщийИтог = ТекНесвязанныйПоказатель.ВыводитьОбщийИтог;
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						
						НайденноеПоле = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина);
						Если НайденноеПоле = Неопределено Тогда
							НоваяСтрока.ПолноеПредставление = ТекПоле.Представление;
						Иначе
							НоваяСтрока.ПолноеПредставление = НайденноеПоле.ПолноеПредставление;
						КонецЕсли;
						НоваяСтрока.НеСвязанные = Ложь;
						Попытка
							ПолеИзПараметрыИспользования = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекПоле.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
							НоваяСтрока.ШиринаКолонки = ПолеИзПараметрыИспользования["ШиринаКолонки"];
							Если ПолеИзПараметрыИспользования.Свойство("Формат") Тогда
								НоваяСтрока.Формат = ПолеИзПараметрыИспользования["Формат"];
							КонецЕсли;
						Исключение
							НоваяСтрока.ШиринаКолонки = 20;
						КонецПопытки;
					КонецЦикла;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПорядокУстановлен Тогда
			// добавим сортировку по измерению
			НоваяСтрока = ТаблицаСортировки.Добавить();
			НоваяСтрока.Описание	= ТекСтрока.ПутьКДанным;
			НоваяСтрока.Направление = НаправлениеСортировки.Возр;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияКолонки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		
		ЭтоИзмерение = Ложь;
		СтрокаПоиска = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным, "ПутьКДанным", Истина);
		Если СтрокаПоиска<>Неопределено Тогда
			ЭтоИзмерение = СтрокаПоиска.Измерение;
		КонецЕсли;
		Если Не ЭтоИзмерение Тогда Продолжить; КонецЕсли;
		
		Тип = ТекСтрока.ТипИзмерения;
		Тип = ?(Тип = "Только иерархия", ТипИзмеренияПостроителяОтчета.ТолькоИерархия, ?(Тип = "Иерархия", ТипИзмеренияПостроителяОтчета.Иерархия, ТипИзмеренияПостроителяОтчета.Элементы));
		
		//Добавить измерение строки
		НоваяСтрока = ТаблицаИзмеренийКолонок.Добавить();
		НоваяСтрока.ПутьКДанным		= ТекСтрока.ПутьКДанным;
		НоваяСтрока.Представление	= ТекСтрока.Представление;
		НоваяСтрока.Тип 			= Тип;
		
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		ИдентификаторПоля = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "_");
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
				//Добавить поле
				НоваяСтрока = ТаблицаПолей.Добавить();
				НоваяСтрока.ПутьКДанным			 = ПутьОсновногоПредставления;
				НоваяСтрока.ТолькоДляПостроителя = Истина;
				// Если основное представление не связано с группировкой, то его надо выводить в итогах
				Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
					ИмяТекПоля=СтрЗаменить(ПутьОсновногоПредставления, ".","");
					Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
						НоваяСтрока.НеСвязанные = Истина;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ПоляСоставногоПредставления = ТекПараметрыИзмерения.СоставноеПредставление.Поля;
				Для Каждого ПолеПредставления Из ПоляСоставногоПредставления Цикл
					//Добавить поле
					НоваяСтрока = ТаблицаПолей.Добавить();
					НоваяСтрока.ПутьКДанным			 = ПолеПредставления;
					НоваяСтрока.ТолькоДляПостроителя = Истина;
					// Если основное представление не связано с группировкой, то его надо выводить в итогах
					Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
						ИмяТекПоля=СтрЗаменить(ПолеПредставления, ".","");
						Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
							НоваяСтрока.НеСвязанные = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Исключение
		КонецПопытки;
		// добавим сортировку по измерению
		НоваяСтрока = ТаблицаСортировки.Добавить();
		НоваяСтрока.Описание	= ТекСтрока.ПутьКДанным;
		НоваяСтрока.Направление = НаправлениеСортировки.Возр;
	КонецЦикла;
	
	Для Каждого ТекУсловноеОформление Из Построитель.УсловноеОформление Цикл
		Для Каждого ТекОбластьУсловногоОформления Из ТекУсловноеОформление.Область Цикл
			ИмяТекПоля=СтрЗаменить(ТекОбластьУсловногоОформления.ПутьКДанным, ".","");
			Если Построитель.ВыбранныеПоля.Найти(ИмяТекПоля)=Неопределено Тогда
				//Добавить поле
				НоваяСтрока = ТаблицаПолей.Добавить();
				НоваяСтрока.ПутьКДанным			 = ТекОбластьУсловногоОформления.ПутьКДанным;
				НоваяСтрока.ТолькоДляПостроителя = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтруктураПолейОтчета;
	
КонецФункции //	отПолучитьИспользованиеИзмерений()

// процедура получает текст запроса для построителя отчетов
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект       - Объект отчета
//  СтруктураПолейОтчета - Структура - используемые Измерения, Поля, Порядок, Ресурсы, ...,
//
Функция отПолучитьИерархическийЗапросПостроителяОтчетов(ОтчетОбъект, СтруктураПолейОтчета) Экспорт
	
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение 
	КонецПопытки;
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	ПостроительОтчета						= ОтчетОбъект.ПостроительОтчета;
	ТаблицаВыраженийПостроителя 			= ОтчетОбъект.ТаблицаВыраженийПостроителя;
	СтруктураИтоговыхВыраженийПоказателей	= ОтчетОбъект.СтруктураИтоговыхВыраженийПоказателей;
	
	//выборка полей
	ИспользуемыеПоляСвойств	  = Новый Структура;
	ИспользуемыеПоля		  = Новый Структура;
	ИспользуемыеПоляВыражений = Новый Структура;
	//блоки запроса
	ВнешниеОтборы	   = Новый Структура;
	ВнутренниеОтборы   = Новый Структура;
	ВнешнийПорядок	   = Новый Структура;
	ВнешниеГруппировки = Новый Структура;
	//итоговые поля
	ИспользуемыеИтоги  = Новый Структура;
	ИгнорироватьПоля   = Новый Структура;
	// ПОЛУЧЕНИЕ ОБЩИХ ПОЛЕЙ ЗАПРОСА
	// Формируем используемые поля выборки
	ТаблицаПолей = СтруктураПолейОтчета.ТаблицаПолей;
	Для Каждого ТекСтрока Из ТаблицаПолей Цикл
		Если ТекСтрока.ТолькоДляМакета Тогда Продолжить; КонецЕсли;
		ПозицияТочки = Найти(ТекСтрока.ПутьКДанным, ".");
		ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(ТекСтрока.ПутьКДанным, ПозицияТочки-1), ТекСтрока.ПутьКДанным);
		
		Если СтруктураСвязиСвойств.Свойство(ПутьКДаннымПоля) Тогда
			ИспользуемыеПоляСвойств.Вставить(ПутьКДаннымПоля, СтруктураСвязиСвойств[ПутьКДаннымПоля]);
		ИначеЕсли ТаблицаВыраженийПостроителя.Найти(ПутьКДаннымПоля, "ПутьКДанным")<> Неопределено Тогда
			ИспользуемыеПоляВыражений.Вставить(ПутьКДаннымПоля);
			Если ТекСтрока.НеСвязанные Тогда
				ТекВыражение = ТаблицаВыраженийПостроителя.Найти(ПутьКДаннымПоля, "ПутьКДанным");
				СтрокаВыражения = "";
				Если ПустаяСтрока(ТекВыражение.ВыражениеИтога) Тогда
					СтрокаВыражения = "МАКСИМУМ("+ПутьКДаннымПоля+")";
				Иначе
					СтрокаВыражения = ТекВыражение.ВыражениеИтога;
				КонецЕсли;
				ИспользуемыеИтоги.Вставить(ПутьКДаннымПоля, СтрокаВыражения);
			КонецЕсли;
		Иначе
			ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "NULL");
			Если ТекСтрока.НеСвязанные Тогда
				ИспользуемыеИтоги.Вставить(ПутьКДаннымПоля, "МАКСИМУМ("+ПутьКДаннымПоля+")");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Формируем используемые поля отбора
	Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
		Если Не ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		ПозицияТочки = Найти(ТекОтбор.ПутьКДанным, ".");
		ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(ТекОтбор.ПутьКДанным, ПозицияТочки-1),ТекОтбор.ПутьКДанным);
		ЭтоВнешнийОтбор = Ложь;
		Если ПозицияТочки > 0 Тогда
			ПолеНастройки = ПостроительОтчета.ДоступныеПоля.Найти(ПутьКДаннымПоля);
			Если (НЕ ПолеНастройки = Неопределено) И (ПолеНастройки.ТипЗначения.Типы().Количество()>5) Тогда
				ЭтоВнешнийОтбор = Истина;
			КонецЕсли;
		КонецЕсли;
		Если СтруктураСвязиСвойств.Свойство(ПутьКДаннымПоля) Тогда
			ИспользуемыеПоляСвойств.Вставить(ПутьКДаннымПоля, СтруктураСвязиСвойств[ПутьКДаннымПоля]);
			ВнешниеОтборы.Вставить(ПутьКДаннымПоля);
		ИначеЕсли ТаблицаВыраженийПостроителя.Найти(ПутьКДаннымПоля, "ПутьКДанным")<>Неопределено Тогда
			ИспользуемыеПоляВыражений.Вставить(ПутьКДаннымПоля);
			ВнешниеОтборы.Вставить(ПутьКДаннымПоля);
		ИначеЕсли СтруктураИтоговыхВыраженийПоказателей.Свойство(ПутьКДаннымПоля) Тогда
			ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, 0);
			ВнешниеОтборы.Вставить(ПутьКДаннымПоля);
		ИначеЕсли ЭтоВнешнийОтбор Тогда
			ИспользуемыеПоля.Вставить(ПутьКДаннымПоля);
			ВнешниеОтборы.Вставить(ПутьКДаннымПоля);
			ВнутренниеОтборы.Вставить(ПутьКДаннымПоля);
		Иначе
			ВнутренниеОтборы.Вставить(ПутьКДаннымПоля);
		КонецЕсли;
	КонецЦикла;
	
	// Формируем используемые поля сортировки
	ТаблицаСортировки = СтруктураПолейОтчета.ТаблицаСортировки;
	Для Каждого ТекСтрока Из ТаблицаСортировки Цикл
		ПозицияТочки = Найти(ТекСтрока.Описание, ".");
		ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(ТекСтрока.Описание, ПозицияТочки-1), ТекСтрока.Описание);
		Если СтруктураСвязиСвойств.Свойство(ПутьКДаннымПоля) Тогда
			ИспользуемыеПоляСвойств.Вставить(ПутьКДаннымПоля, СтруктураСвязиСвойств[ПутьКДаннымПоля]);
		ИначеЕсли ТаблицаВыраженийПостроителя.Найти(ПутьКДаннымПоля, "ПутьКДанным")<>Неопределено Тогда
			ИспользуемыеПоляВыражений.Вставить(ПутьКДаннымПоля);
		Иначе
			ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "NULL");
		КонецЕсли;
		ВнешнийПорядок.Вставить(ПутьКДаннымПоля);
	КонецЦикла;
	
	// Формируем используемые поля измерений
	ТаблицаИзмеренийСтрок = СтруктураПолейОтчета.ТаблицаИзмеренийСтрок;
	Для Каждого ТекСтрока Из ТаблицаИзмеренийСтрок Цикл
		Если ТекСтрока.ТолькоДляМакета Тогда Продолжить; КонецЕсли;
		ПозицияТочки = Найти(ТекСтрока.ПутьКДанным, ".");
		ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(ТекСтрока.ПутьКДанным, ПозицияТочки-1), ТекСтрока.ПутьКДанным);
		Если СтруктураСвязиСвойств.Свойство(ПутьКДаннымПоля) Тогда
			ИспользуемыеПоляСвойств.Вставить(ПутьКДаннымПоля, СтруктураСвязиСвойств[ПутьКДаннымПоля]);
		ИначеЕсли ТаблицаВыраженийПостроителя.Найти(ПутьКДаннымПоля, "ПутьКДанным")<> Неопределено Тогда
			ИспользуемыеПоляВыражений.Вставить(ПутьКДаннымПоля);
		Иначе
			ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "NULL");
		КонецЕсли;
		Если Не ВнешниеГруппировки.Свойство(ПутьКДаннымПоля) Тогда
			ВнешниеГруппировки.Вставить(ПутьКДаннымПоля, Новый Массив);
		КонецЕсли;
		ВнешниеГруппировки[ПутьКДаннымПоля].Добавить(СтрЗаменить(ТекСтрока.ПутьКДанным, ".", ""));
	КонецЦикла;
	
	// Формируем используемые поля измерений
	ТаблицаИзмеренийКолонок = СтруктураПолейОтчета.ТаблицаИзмеренийКолонок;
	Для Каждого ТекСтрока Из ТаблицаИзмеренийКолонок Цикл
		Если ТекСтрока.ТолькоДляМакета Тогда Продолжить; КонецЕсли;
		ПозицияТочки = Найти(ТекСтрока.ПутьКДанным, ".");
		ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(ТекСтрока.ПутьКДанным, ПозицияТочки-1), ТекСтрока.ПутьКДанным);
		
		Если СтруктураСвязиСвойств.Свойство(ПутьКДаннымПоля) Тогда
			ИспользуемыеПоляСвойств.Вставить(ПутьКДаннымПоля, СтруктураСвязиСвойств[ПутьКДаннымПоля]);
		ИначеЕсли ТаблицаВыраженийПостроителя.Найти(ПутьКДаннымПоля, "ПутьКДанным")<> Неопределено Тогда
			ИспользуемыеПоляВыражений.Вставить(ПутьКДаннымПоля);
		Иначе
			ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "NULL");
		КонецЕсли;
		Если Не ВнешниеГруппировки.Свойство(ПутьКДаннымПоля) Тогда
			ВнешниеГруппировки.Вставить(ПутьКДаннымПоля, Новый Массив);
		КонецЕсли;
		ВнешниеГруппировки[ПутьКДаннымПоля].Добавить(СтрЗаменить(ТекСтрока.ПутьКДанным, ".", ""));
	КонецЦикла;
	
	// Формируем используемые поля ресурсов
	РесурсыОтчета = СтруктураПолейОтчета.РесурсыОтчета;
	Для Каждого ТекСтрока Из РесурсыОтчета Цикл
		Если ТаблицаВыраженийПостроителя.Найти(ТекСтрока, "ПутьКДанным")<> Неопределено Тогда
			ИспользуемыеПоляВыражений.Вставить(ТекСтрока);
		Иначе
			ИспользуемыеПоля.Вставить(ТекСтрока, 0);
		КонецЕсли;
		ИспользуемыеИтоги.Вставить(ТекСтрока, СтруктураИтоговыхВыраженийПоказателей[ТекСтрока]);
	КонецЦикла;
	
	// добавляем поля выражений
	Для Каждого ТекПоле Из ИспользуемыеПоляВыражений Цикл
		ИмяВыражения = ТекПоле.Ключ;
		ТекВыражение = ТаблицаВыраженийПостроителя.Найти(ИмяВыражения, "ПутьКДанным");
		// если ресурс рассчитывается только для определенных группировок, то уберем его если таких нет.
		Если ТекВыражение.РассчитыватьПо.Количество()>0 Тогда
			Добавлять=Ложь;
			Для Каждого ТекСтрока Из ТекВыражение.РассчитыватьПо Цикл
				// если ресурс не выводится для реквизитов измерения
				Если ВнешниеГруппировки.Свойство(ТекСтрока.Ключ) И 
					(ТекСтрока.Значение ИЛИ (НЕ ВнешниеГруппировки[ТекСтрока.Ключ].Найти(ТекСтрока.Ключ)=Неопределено)) Тогда
					Добавлять=Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не Добавлять Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ПоляВОтборах				= Новый Структура;
		ПоляВГруппировках			= Новый Структура;
		ПоляВОтборахИГруппировках	= Новый Структура;
		ЕстьИсключенияПоГруппировкам = (ТекВыражение.ПоляРассчитыватьПо.Количество()>0);
		Если ЕстьИсключенияПоГруппировкам Тогда
			Для Каждого ВложенноеПоле Из ТекВыражение.ПоляРассчитыватьПо Цикл
				Для Каждого ТекГруппировка Из ВложенноеПоле.Значение Цикл
					Если ВнутренниеОтборы.Свойство(ТекГруппировка.Ключ) Тогда
						ПоляВОтборах.Вставить(ВложенноеПоле.Ключ);
						Если ВнешниеГруппировки.Свойство(ТекГруппировка.Ключ) И
							(ТекГруппировка.Значение ИЛИ (Не ВнешниеГруппировки[ТекГруппировка.Ключ].Найти(ТекГруппировка.Ключ)=Неопределено)) Тогда
							ПоляВОтборахИГруппировках.Вставить(ВложенноеПоле.Ключ);
						КонецЕсли;
						Прервать;
					КонецЕсли;
					Если ВнешниеГруппировки.Свойство(ТекГруппировка.Ключ) И 
						(ТекГруппировка.Значение ИЛИ (Не ВнешниеГруппировки[ТекГруппировка.Ключ].Найти(ТекГруппировка.Ключ)=Неопределено)) Тогда
						ПоляВГруппировках.Вставить(ВложенноеПоле.Ключ);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		Для Каждого СоставляющееПоле Из ТекВыражение.Поля Цикл
			ПозицияТочки = Найти(СоставляющееПоле, ".");
			ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(СоставляющееПоле, ПозицияТочки-1), СоставляющееПоле);
			ПолеНастройки = ПостроительОтчета.ДоступныеПоля.Найти(ПутьКДаннымПоля);
			Если ПолеНастройки=Неопределено Тогда
				Прервать;
			КонецЕсли;
			Если ЕстьИсключенияПоГруппировкам Тогда
				Если ТекВыражение.ПоляРассчитыватьПо.Свойство(ПутьКДаннымПоля) Тогда
					Если (Не (ПоляВОтборахИГруппировках.Свойство(ПутьКДаннымПоля) ИЛИ
						(ПоляВОтборахИГруппировках.Количество()=0 И ПоляВОтборах.Свойство(ПутьКДаннымПоля)) ИЛИ
						(ПоляВОтборах.Количество()=0 И ПоляВГруппировках.Свойство(ПутьКДаннымПоля)))) Тогда
						ИгнорироватьПоля.Вставить(ПутьКДаннымПоля);
						Продолжить;
					КонецЕсли;
				ИначеЕсли ПоляВОтборах.Количество()>0 Тогда
					ИгнорироватьПоля.Вставить(ПутьКДаннымПоля);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			//ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, ?(ПолеНастройки.ТипЗначения.Типы()[0]=Тип("Число"),0,"NULL"));
			Если ПолеНастройки.ТипЗначения.Типы()[0] = Тип("Число") Тогда
				ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, 0);
			ИначеЕсли ПолеНастройки.ТипЗначения.Типы()[0] = Тип("Дата") Тогда
				ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)");
			ИначеЕсли ПолеНастройки.ТипЗначения.Типы()[0] = Тип("Булево") Тогда
				ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "ЛОЖЬ");
			Иначе
				ИспользуемыеПоля.Вставить(ПутьКДаннымПоля, "NULL");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ТекСвойство Из ИспользуемыеПоляСвойств Цикл
		ИмяИзмерения = ТекСвойство.Значение.ИмяИзмерения;
		ПолеНастройки = ПостроительОтчета.ДоступныеПоля.Найти(ИмяИзмерения);
		Если ПолеНастройки<>Неопределено Тогда
			//ИспользуемыеПоля.Вставить(ИмяИзмерения, ?(ПолеНастройки.ТипЗначения.Типы()[0]=Тип("Число"),0,"NULL"));
			Если ПолеНастройки.ТипЗначения.Типы()[0] = Тип("Число") Тогда
				ИспользуемыеПоля.Вставить(ИмяИзмерения, 0);
			ИначеЕсли ПолеНастройки.ТипЗначения.Типы()[0] = Тип("Дата") Тогда
				ИспользуемыеПоля.Вставить(ИмяИзмерения, "ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)");
			ИначеЕсли ПолеНастройки.ТипЗначения.Типы()[0] = Тип("Булево") Тогда
				ИспользуемыеПоля.Вставить(ИмяИзмерения, "ЛОЖЬ");
			Иначе
				ИспользуемыеПоля.Вставить(ИмяИзмерения, "NULL");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//Формируем запрос объединения таблиц по выбранным полям
	ИспользуемыеТаблицы = Неопределено;
	мПустыеПоля         = Неопределено;
	ТекстЗапроса = отПолучитьТекстЗапросаПоТаблицеЗапросов(ОтчетОбъект, ИспользуемыеПоля, ВнутренниеОтборы, ИспользуемыеТаблицы, мПустыеПоля);
	
	Попытка
		СоздатьГруппировку = ОтчетОбъект.СгруппироватьОбъединение(ИспользуемыеТаблицы.Количество());
	Исключение
		СоздатьГруппировку = ИспользуемыеТаблицы.Количество()>1;
		//Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		//	СоздатьГруппировку = ИспользуемыеТаблицы.Количество()>1;
		//ИначеЕсли ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты Тогда
		//	СоздатьГруппировку = Истина;
		//Иначе
		//	СоздатьГруппировку = Ложь;
		//КонецЕсли;
	КонецПопытки;
	
	Попытка
		УбиратьСтрокиСПустымиПоказателями = ОтчетОбъект.УбиратьСтрокиСПустымиПоказателями;
	Исключение
		УбиратьСтрокиСПустымиПоказателями = (СоздатьГруппировку И ИспользуемыеТаблицы.Количество()>1);
		//Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты Тогда
		//	УбиратьСтрокиСПустымиПоказателями = Истина;
		//Иначе
		//	УбиратьСтрокиСПустымиПоказателями = Ложь;
		//КонецЕсли;
	КонецПопытки;
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	//ФОРМИРОВАНИЕ ТЕКСТА ЗАПРОСА
	
	ПромежуточныйПостроитель = Новый ПостроительЗапроса;
	ПромежуточныйПостроитель.Текст = ТекстЗапроса;
	ДоступныеПоля = ПромежуточныйПостроитель.ДоступныеПоля;
	
	РазрешенныеПоля = Новый Структура;
	// Формируем текст выборки полей запроса
	ТекстВыборкиПолей = "";
	ТекстВыборкиПолейПостроителя = "";
	ТекстСгруппировать = "";
	ТекстОтборПоПоказателям = "";
	Для Каждого ТекПоле Из ИспользуемыеПоля Цикл
		ИмяПоля = ТекПоле.Ключ;
		Если ДоступныеПоля.Найти(ИмяПоля)=Неопределено Тогда 
			Если НЕ мПустыеПоля.Найти(ИмяПоля) = Неопределено Тогда
				ТекстВыборкиПолей = ТекстВыборкиПолей + ?(ТекстВыборкиПолей = "","" , ",") + Символы.ПС + ТекПоле.Значение + " КАК " + ИмяПоля;
				ТекстВыборкиПолейПостроителя = ТекстВыборкиПолейПостроителя + ?(ТекстВыборкиПолейПостроителя="","", ",")+Символы.ПС+ИмяПоля+".* КАК "+ИмяПоля;
				РазрешенныеПоля.Вставить(ИмяПоля);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		Если ИспользуемыеПоляВыражений.Свойство(ИмяПоля) Тогда 
			РазрешенныеПоля.Вставить(ИмяПоля); 
			Продолжить; 
		КонецЕсли;
		//Если СоздаватьВложеннуюТаблицу Тогда
		РасширениеПолей = "";
		Если СоздатьГруппировку Тогда
			Если НЕ РесурсыОтчета.Найти(ИмяПоля)=Неопределено Тогда
				Выражение = "СУММА(ОбщаяТаблицаДанных."+ИмяПоля+")";
				Если УбиратьСтрокиСПустымиПоказателями Тогда
					ТекстОтборПоПоказателям = ТекстОтборПоПоказателям+?(ТекстОтборПоПоказателям="","", " ИЛИ ")+Символы.ПС+Выражение+" <> 0";
				КонецЕсли;
			ИначеЕсли СтруктураИтоговыхВыраженийПоказателей.Свойство(ИмяПоля) Тогда
				Выражение = "СУММА(ОбщаяТаблицаДанных."+ИмяПоля+")";
			Иначе
				ТекстСгруппировать = ТекстСгруппировать+?(ТекстСгруппировать="","", ",")+"ОбщаяТаблицаДанных."+ИмяПоля+Символы.ПС;
				Выражение = "ОбщаяТаблицаДанных."+ИмяПоля;
				РасширениеПолей = ".*";
			КонецЕсли;
			ТекстВыборкиПолей = ТекстВыборкиПолей+?(ТекстВыборкиПолей="","", ",")+Символы.ПС+Выражение+" КАК "+ИмяПоля;
		Иначе
			Выражение = "ОбщаяТаблицаДанных."+ИмяПоля;
			РасширениеПолей = ".*";
			ТекстВыборкиПолей = ТекстВыборкиПолей+?(ТекстВыборкиПолей="","", ",")+Символы.ПС+"ОбщаяТаблицаДанных."+ИмяПоля+" КАК "+ИмяПоля;
		КонецЕсли;
		Если ВнешниеОтборы.Свойство(ИмяПоля) Тогда
			ВнешниеОтборы.Вставить(ИмяПоля, Выражение+РасширениеПолей);
			Если ТаблицаПолей.Найти(ИмяПоля, "ПутьКДанным") = Неопределено Тогда
				НовоеПоле = ТаблицаПолей.Добавить();
				НовоеПоле.Имя = ИмяПоля;
				НовоеПоле.ПутьКДанным = ИмяПоля;
				НовоеПоле.ПолноеПредставление = ИмяПоля;
				НовоеПоле.ТолькоДляПостроителя = Истина;
			КонецЕсли;
		КонецЕсли;
		//КонецЕсли;
		// Формируем текст блока выборки полей построителя
		ТекстВыборкиПолейПостроителя = ТекстВыборкиПолейПостроителя + ?(ТекстВыборкиПолейПостроителя="","", ",")+Символы.ПС+ИмяПоля+".* КАК "+ИмяПоля;
		РазрешенныеПоля.Вставить(ИмяПоля);
	КонецЦикла;
	
	СтрСоединенийСвойств   = "";
	СтруктураУникальныхИменСоединений = Новый Структура;
	Для Каждого ТекПоле Из ИспользуемыеПоляВыражений Цикл
		ИмяВыражения = ТекПоле.Ключ;
		ТекВыражение = ТаблицаВыраженийПостроителя.Найти(ИмяВыражения, "ПутьКДанным");
		НеДобавлять  = Ложь;
		Счетчик		 = 0;
		ПоследнееПоле= Неопределено;
		ИспользоватьОбщийИтог = Истина;
		
		Для Каждого СоставляющееПоле Из ТекВыражение.Поля Цикл
			ПозицияТочки = Найти(СоставляющееПоле, ".");
			ПутьКДаннымПоля = ?(ПозицияТочки>0, Лев(СоставляющееПоле, ПозицияТочки-1), СоставляющееПоле);
			Если (Не РазрешенныеПоля.Свойство(ПутьКДаннымПоля)) Тогда
				Если (Не ТекВыражение.ПоляРассчитыватьПо.Свойство(ПутьКДаннымПоля)) Тогда
					ИспользоватьОбщийИтог = Ложь;
				КонецЕсли;
				Если (Не ИгнорироватьПоля.Свойство(ПутьКДаннымПоля)) Тогда
					НеДобавлять = Истина;
					Прервать;
				КонецЕсли;
			ИначеЕсли ТекВыражение.ПоляРассчитыватьПо.Свойство(ПутьКДаннымПоля) Тогда
				Счетчик = Счетчик+1;
				ПоследнееПоле = ПутьКДаннымПоля;
			КонецЕсли;
		КонецЦикла;
		//ВыражениеПоля
		Если НеДобавлять Тогда Продолжить; КонецЕсли;
		Если ИспользоватьОбщийИтог Тогда
			ВыражениеПоля = ТекВыражение.ВыражениеПоля;
		ИначеЕсли Счетчик=1 Тогда
			ВыражениеПоля = ТекВыражение.ПоляВыражениеПоля[ПоследнееПоле];
			РазрешенныеПоля.Удалить(ПоследнееПоле);
		Иначе
			ТекВыражение.ИспользоватьОбщийИтог = Ложь;
			ПредставлениеСвойства = отПолучитьСтрокуПредставленияТипа(ТекВыражение.ТипЗначение);
			СтрПустоеЗначение = """""";
			Если ПредставлениеСвойства="Строка" Тогда
			ИначеЕсли ПредставлениеСвойства="Число" Тогда
				СтрПустоеЗначение = "0";
			ИначеЕсли ПредставлениеСвойства="Дата" Тогда
				СтрПустоеЗначение = "ДАТАВРЕМЯ(1,1,1)";
			ИначеЕсли ПредставлениеСвойства="Булево" Тогда
				СтрПустоеЗначение = "ЛОЖЬ";	
			Иначе
				СтрПустоеЗначение = "ЗНАЧЕНИЕ("+ПредставлениеСвойства+".ПустаяСсылка)";
			КонецЕсли;
			ВыражениеПоля = СтрПустоеЗначение;
		КонецЕсли;
		ТекстВыборкиПолейПостроителя = ТекстВыборкиПолейПостроителя + ?(ТекстВыборкиПолейПостроителя="","", ",")+Символы.ПС+ИмяВыражения+".* КАК "+ИмяВыражения;
		
		Если СоздатьГруппировку Тогда
			Если НЕ РесурсыОтчета.Найти(ИмяВыражения)=Неопределено Тогда
				Если ВыражениеПоля <> "0" Тогда
					ВыражениеПоля = "СУММА("+ВыражениеПоля+")";
					Если УбиратьСтрокиСПустымиПоказателями Тогда
						ТекстОтборПоПоказателям = ТекстОтборПоПоказателям+?(ТекстОтборПоПоказателям="","", " ИЛИ ")+Символы.ПС+ВыражениеПоля+" <> 0";
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли СтруктураИтоговыхВыраженийПоказателей.Свойство(ИмяВыражения) Тогда
				Если ВыражениеПоля <> "0" Тогда
					ВыражениеПоля = "СУММА("+ВыражениеПоля+")";
				КонецЕсли;
			Иначе
				ТекстСгруппировать = ТекстСгруппировать+?(ТекстСгруппировать="","", ",")+ВыражениеПоля+Символы.ПС;
			КонецЕсли;
		КонецЕсли;
		Если мПустыеПоля.Найти(ИмяВыражения) = Неопределено Тогда
			ТекстВыборкиПолей = ТекстВыборкиПолей+?(ТекстВыборкиПолей="","", ",")+Символы.ПС+ВыражениеПоля+" КАК "+ИмяВыражения;
		КонецЕсли;
		РазрешенныеПоля.Вставить(ИмяВыражения);
		
		Если (Не ПустаяСтрока(ТекВыражение.ИмяСоединения)) И (Не СтруктураУникальныхИменСоединений.Свойство(ТекВыражение.ИмяСоединения)) Тогда
			СтрСоединенийСвойств = СтрСоединенийСвойств+ТекВыражение.ВыражениеСоединения;
			СтруктураУникальныхИменСоединений.Вставить(ТекВыражение.ИмяСоединения);
		КонецЕсли;
		
		Для Каждого СоставляющееПоле Из ТекВыражение.Поля Цикл
			ИмяПоля = СтрЗаменить(СоставляющееПоле,".","");
			Если ИгнорироватьПоля.Свойство(ИмяПоля) Тогда Продолжить; КонецЕсли;
			Если ТаблицаПолей.Найти(СоставляющееПоле,"ПутьКДанным")=Неопределено Тогда
				НовоеПоле = ТаблицаПолей.Добавить();
				НовоеПоле.Имя = ИмяПоля;
				НовоеПоле.ПутьКДанным = СоставляющееПоле;
				НовоеПоле.ПолноеПредставление = СоставляющееПоле;
				НовоеПоле.ТолькоДляПостроителя = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ВнешниеОтборы.Свойство(ИмяВыражения) Тогда
			ВнешниеОтборы.Вставить(ИмяВыражения, ВыражениеПоля);
		КонецЕсли;
	КонецЦикла;
	// Формируем текст получения свойств объектов
	СтрЗапросаИтогиСвойств = "";
	Для Каждого ТекСвойство Из ИспользуемыеПоляСвойств Цикл
		ИмяПоля = ТекСвойство.Ключ;
		ЗначениеСвойства = ТекСвойство.Значение;
		ИмяИзмерения = ЗначениеСвойства.ИмяИзмерения;
		Если Не РазрешенныеПоля.Свойство(ИмяИзмерения) Тогда Продолжить; КонецЕсли;
		
		Если ТаблицаПолей.Найти(ИмяИзмерения, "ПутьКДанным")=Неопределено Тогда
			НовоеПоле = ТаблицаПолей.Добавить();
			НовоеПоле.Имя = ИмяИзмерения;
			НовоеПоле.ПутьКДанным = ИмяИзмерения;
			НовоеПоле.ПолноеПредставление = ИмяИзмерения;
			НовоеПоле.ТолькоДляПостроителя = Истина;
		КонецЕсли;
		ИмяСвойства = СтрЗаменить(ИмяПоля, "Значение", "");
		ЗначениеСвойства = СтруктураСвязиСвойств[ИмяПоля];
		НаименованиеСвойства = ЗначениеСвойства.Представление;
		ИмяПараметра = ЗначениеСвойства.ИмяПараметра;
		ПутьКДаннымПоля = "ОбщаяТаблицаДанных." + ИмяИзмерения;
		
		ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(ЗначениеСвойства.ТипЗначения);
		
//		ЗапросСвойстваПоля = "ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ИмяПоля;
		ЗапросСвойстваПоля = отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " КАК " + ИмяПоля;
		ЗапросСвойстваИтоги = ",МАКСИМУМ(" + ИмяПоля + ")" + Символы.ПС;
		ЗапросСвойстваСоединение = "{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ПутьКДаннымПоля + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + "}" + Символы.ПС;
		
		ТекстВыборкиПолей = ТекстВыборкиПолей+?(ТекстВыборкиПолей="","", ",")+Символы.ПС+ЗапросСвойстваПоля;
		ТекстВыборкиПолейПостроителя = ТекстВыборкиПолейПостроителя + ?(ТекстВыборкиПолейПостроителя="","", ",")+Символы.ПС+ИмяПоля+".* КАК "+ИмяПоля;
		СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств + ЗапросСвойстваИтоги;
		СтрСоединенийСвойств   = СтрСоединенийСвойств   + ЗапросСвойстваСоединение;
		Если СоздатьГруппировку Тогда
			//ТекстСгруппировать = ТекстСгруппировать+?(ТекстСгруппировать="","", ",")+Символы.ПС+"ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ")";
			ТекстСгруппировать = ТекстСгруппировать+?(ТекстСгруппировать="","", ",") + Символы.ПС +
				отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства);
		КонецЕсли;
		ПостроительОтчета.Параметры.Вставить(ИмяПараметра, ЗначениеСвойства.Свойство);
		
		Если ВнешниеОтборы.Свойство(ИмяПоля) Тогда
			ПредставлениеСвойства = отПолучитьСтрокуПредставленияТипа(ЗначениеСвойства.ТипЗначения);
			СтрПустоеЗначение = """""";
			Если ПредставлениеСвойства="Строка" Тогда
			ИначеЕсли ПредставлениеСвойства="Число" Тогда
				СтрПустоеЗначение = "0";
			ИначеЕсли ПредставлениеСвойства="Дата" Тогда
				СтрПустоеЗначение = "ДАТАВРЕМЯ(1,1,1)";
			ИначеЕсли ПредставлениеСвойства="Булево" Тогда
				СтрПустоеЗначение = "ЛОЖЬ";	
			Иначе
				СтрПустоеЗначение = "ЗНАЧЕНИЕ("+ПредставлениеСвойства+".ПустаяСсылка)";
			КонецЕсли;
//			ВнешниеОтборы.Вставить(ИмяПоля, "ВЫРАЗИТЬ("+ИмяСвойства+".Значение КАК " + ТипСвойства + ")");
			ВнешниеОтборы.Вставить(ИмяПоля, отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства));
		КонецЕсли;
		РазрешенныеПоля.Вставить(ИмяПоля);
	КонецЦикла;
	
	// Формируем текст внешнего отбора построителя
	ТекстОтбораПостроителя = "";
	Для Каждого ТекОтбор Из ВнешниеОтборы Цикл
		Если ПустаяСтрока(ТекОтбор.Значение) Тогда Продолжить; КонецЕсли;
		ТекстОтбораПостроителя = ТекстОтбораПостроителя + ?(ТекстОтбораПостроителя="","", ",")+Символы.ПС+ТекОтбор.Значение+" КАК "+ТекОтбор.Ключ;
	КонецЦикла;
	
	// Формируем текст блока сортировки построителя
	ТекстПорядкаПостроителя = "";
	Для Каждого ТекПорядок Из ВнешнийПорядок Цикл
		Если Не РазрешенныеПоля.Свойство(ТекПорядок.Ключ) Тогда Продолжить; КонецЕсли;
		ТекстПорядкаПостроителя = ТекстПорядкаПостроителя + ?(ТекстПорядкаПостроителя="","", ",")+Символы.ПС+ТекПорядок.Ключ+".* КАК "+ТекПорядок.Ключ;
	КонецЦикла;
	
	// Формируем текст блока группировок построителя
	ТекстИтогаПостроителя = "";
	ТекстГруппировок = "";
	ГруппировкиОтчета = Новый Массив;
	Для Каждого ТекГруппировка Из ВнешниеГруппировки Цикл
		Если Не РазрешенныеПоля.Свойство(ТекГруппировка.Ключ) Тогда Продолжить; КонецЕсли;
		ГруппировкиОтчета.Добавить(ТекГруппировка.Ключ);
		ТекстГруппировок = ТекстГруппировок+?(ТекстГруппировок="","",",")+Символы.ПС+ТекГруппировка.Ключ;
		ТекстИтогаПостроителя = ТекстИтогаПостроителя + ?(ТекстИтогаПостроителя="","", ",")+Символы.ПС+ТекГруппировка.Ключ+".* КАК "+ТекГруппировка.Ключ;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ТекстВыборкиПолейПостроителя) Тогда
		ТекстВыборкиПолейПостроителя = "{ВЫБРАТЬ" + Символы.ПС + ТекстВыборкиПолейПостроителя + "}";
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстОтбораПостроителя) Тогда
		ТекстОтбораПостроителя = "{ГДЕ" + Символы.ПС+ТекстОтбораПостроителя + "}";
	КонецЕсли;
	
	//Формируем иерархический запрос
	Если Не ПустаяСтрока(ТекстПорядкаПостроителя) Тогда
		ТекстПорядкаПостроителя = "{УПОРЯДОЧИТЬ ПО" + Символы.ПС + ТекстПорядкаПостроителя + "}";
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстИтогаПостроителя) Тогда
		ТекстИтогаПостроителя = "{ИТОГИ ПО" + Символы.ПС + ТекстИтогаПостроителя + "}";
	КонецЕсли;
	
	//Формируем текст получения итогов всех ресурсов
	ТекстИтоговыхВыражений = "";
	Для Каждого ТекИтог Из ИспользуемыеИтоги Цикл
		ИмяПоля = ТекИтог.Ключ;
		Если Не РазрешенныеПоля.Свойство(ИмяПоля) Тогда Продолжить; КонецЕсли;
		ТекстВыражения = ТекИтог.Значение;
		ТекВыражение = ТаблицаВыраженийПостроителя.Найти(ИмяПоля, "ПутьКДанным");
		Если Не ТекВыражение=Неопределено Тогда
			Если ТекВыражение.ПоляРассчитыватьПо.Количество()>0 Тогда
				ПоляРассчитыватьПо	= Новый Структура();
				Для Каждого ТекПоле Из ТекВыражение.ПоляРассчитыватьПо Цикл
					Если ИгнорироватьПоля.Свойство(ТекПоле.Ключ) ИЛИ Не РазрешенныеПоля.Свойство(ТекПоле.Ключ) Тогда Продолжить; КонецЕсли;
					Для Каждого ВложенноеПоле Из ТекПоле.Значение Цикл
						ПоляРассчитыватьПо.Вставить(ВложенноеПоле.Ключ, ТекПоле.Ключ);
					КонецЦикла;
				КонецЦикла;
				ТекстУсловия = "";
				Если ПоляРассчитыватьПо.Количество()>0 Тогда
					Для Каждого ТекСтрока Из ГруппировкиОтчета Цикл
						Если ПоляРассчитыватьПо.Свойство(ТекСтрока) Тогда
							ТекстУсловия = "КОГДА НЕ "+ТекСтрока+" ЕСТЬ NULL ТОГДА "+ТекВыражение.ПоляВыражениеИтога[ПоляРассчитыватьПо[ТекСтрока]]+"
							|	"+ТекстУсловия;
							Если СоздатьГруппировку И УбиратьСтрокиСПустымиПоказателями Тогда
								ВыражениеПоля = ТекВыражение.ПоляВыражениеПоля[ПоляРассчитыватьПо[ТекСтрока]];
								Если ВыражениеПоля <> "0" Тогда
									ВыражениеПоля = "СУММА("+ВыражениеПоля+")";
									Если Найти(ТекстОтборПоПоказателям, ВыражениеПоля) = 0 Тогда
										ТекстОтборПоПоказателям = ТекстОтборПоПоказателям+?(ТекстОтборПоПоказателям="","", " ИЛИ ")+Символы.ПС+ВыражениеПоля+" <> 0";
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				Если ТекВыражение.ИспользоватьОбщийИтог Тогда
					ТекстУсловияИтога = "";
					Для Каждого ТекСтрока Из ТекВыражение.РассчитыватьПо Цикл
						Если ГруппировкиОтчета.Свойство(ТекСтрока.Ключ) Тогда
							ТекстУсловияИтога = ТекстУсловияИтога + ?(ТекстУсловияИтога="", "", " И ")+" ЕСТЬ NULL "+ТекСтрока.Ключ;
						КонецЕсли;
					КонецЦикла;
					Если Не ТекстУсловияИтога = "" Тогда
						ТекстВыражения = "
						|ВЫБОР 
						|	КОГДА "+ТекстУсловияИтога+ " ТОГДА 0
						|	ИНАЧЕ "+ТекстВыражения+"
						|КОНЕЦ";
					КонецЕсли;
					Если Не ТекстУсловия = "" Тогда
						ТекстВыражения = "ВЫБОР
						|	"+ТекстУсловия+"	ИНАЧЕ "+ТекстВыражения+"
						|КОНЕЦ";
					КонецЕсли;
				Иначе
					Если Не ТекстУсловия = "" Тогда
						ТекстВыражения = "ВЫБОР
						|	"+ТекстУсловия+"
						|КОНЕЦ";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ТекстВыражения = ТекстВыражения+" КАК "+ИмяПоля;
		КонецЕсли;
		
		ТекстИтоговыхВыражений = ТекстИтоговыхВыражений+?(ТекстИтоговыхВыражений="","", ",")+Символы.ПС+ТекстВыражения;
	КонецЦикла;
	
	//Если СоздаватьВложеннуюТаблицу Тогда
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	"+ТекстВыборкиПолей+"
		|"+ТекстВыборкиПолейПостроителя+"
		|ИЗ(
		|	"+ТекстЗапроса+Символы.ПС+") КАК ОбщаяТаблицаДанных"+Символы.ПС+СтрСоединенийСвойств;
	//Иначе
	//	Если Не ПустаяСтрока(ТекстВыборкиПолей) Тогда
	//		ПозицияВставки = Найти(ВРег(ТекстЗапроса), "ВЫБРАТЬ");
	//		ТекстЗапроса = СокрЛП(Лев(ТекстЗапроса, ПозицияВставки+6))+Символы.ПС+СокрЛП(ТекстВыборкиПолей)+
	//		","+Символы.ПС+СокрЛП(Сред(ТекстЗапроса, ПозицияВставки+7));
	//	КонецЕсли;
	//	ТекстЗапроса = ТекстЗапроса+"
	//	|	"+СтрСоединенийСвойств+"
	//	|	"+ТекстВыборкиПолейПостроителя;
	//КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса+Символы.ПС+ТекстОтбораПостроителя;
	Если СоздатьГруппировку И Не ПустаяСтрока(ТекстСгруппировать) Тогда
		ТекстЗапроса = ТекстЗапроса+Символы.ПС+"СГРУППИРОВАТЬ ПО
		|	"+ТекстСгруппировать;
		Если НЕ ТекстОтборПоПоказателям = "" Тогда
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + "ИМЕЮЩИЕ
		|	(" + ТекстОтборПоПоказателям + ")";
		КонецЕсли;
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса+Символы.ПС+ТекстПорядкаПостроителя;
	ТекстЗапроса = ТекстЗапроса+Символы.ПС+ТекстИтогаПостроителя;
	
	ТекстИтоговыхВыражений = "ИТОГИ" + Символы.ПС + ТекстИтоговыхВыражений + Символы.ПС + СтрЗапросаИтогиСвойств;
	ТекстИтогиПо = "ПО" + ?(ПостроительОтчета.ВыводитьОбщиеИтоги, " ОБЩИЕ"+?(ТекстГруппировок = "", "", ", "), "")+ТекстГруппировок;
	ТекстЗапроса = ТекстЗапроса+Символы.ПС+ТекстИтоговыхВыражений+Символы.ПС+ТекстИтогиПо + "
	|
	| АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат ТекстЗапроса;
	
КонецФункции // отПолучитьИерархическийЗапросПостроителяОтчетов()

// Процедура заполняет выбранные поля и порядок сортировки измерений построителя отчета
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект       - Объект отчета
//	Построитель - ПостроительОтчета - Построитель отчета
//
Процедура отУстановитьИспользованиеИзмеренийДляТаблицыЗапросов(ОтчетОбъект, Построитель = Неопределено, СтруктураПолейОтчета) Экспорт
	
	Если Построитель = Неопределено Тогда
		Построитель = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	
	Построитель.ИзмеренияСтроки.Очистить();
	Построитель.ИзмеренияКолонки.Очистить();
	Построитель.Порядок.Очистить();
	ОтчетОбъект.ДополнительныеПоляОтчета.Очистить();
	ДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета;
	
	Если Не Построитель.ВыводитьДетальныеЗаписи Тогда
		Построитель.ВыбранныеПоля.Очистить();
	КонецЕсли;
	
	Попытка 
		Если ОтчетОбъект.УстановитьИспользованиеИзмерений(Построитель) Тогда
			Возврат;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка ДеревоДоступныхПолей = ОтчетОбъект.ДеревоДоступныхПолей;
	Исключение Возврат; КонецПопытки;
	
	Для Каждого ТекРесурс Из СтруктураПолейОтчета.РесурсыОтчета Цикл
		Попытка 
			Построитель.ВыбранныеПоля.Добавить(ТекРесурс);
		Исключение 
		КонецПопытки;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из СтруктураПолейОтчета.ТаблицаИзмеренийСтрок Цикл
		Если ТекСтрока.ТолькоДляМакета Тогда Продолжить; КонецЕсли;
		Попытка
			Построитель.ВыбранныеПоля.Добавить(ТекСтрока.ПутьКДанным);
			ТекИзмерение = Построитель.ИзмеренияСтроки.Добавить(ТекСтрока.ПутьКДанным, СтрЗаменить(ТекСтрока.ПутьКДанным, ".", ""), ТекСтрока.Тип);
			ТекИзмерение.Представление = ТекСтрока.Представление;
		Исключение 
		КонецПопытки;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из СтруктураПолейОтчета.ТаблицаИзмеренийКолонок Цикл
		Если ТекСтрока.ТолькоДляМакета Тогда Продолжить; КонецЕсли;
		Попытка
			ТекИзмерение = Построитель.ИзмеренияКолонки.Добавить(ТекСтрока.ПутьКДанным, СтрЗаменить(ТекСтрока.ПутьКДанным, ".", ""), ТекСтрока.Тип);
			ТекИзмерение.Представление = ТекСтрока.Представление;
		Исключение 
		КонецПопытки;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из СтруктураПолейОтчета.ТаблицаСортировки Цикл
		Попытка
			Построитель.Порядок.Добавить(ТекСтрока.Описание,,,ТекСтрока.Направление);
		Исключение 
		КонецПопытки;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из СтруктураПолейОтчета.ТаблицаПолей Цикл
		Если Не ТекСтрока.ТолькоДляПостроителя Тогда
			ЗаполнитьЗначенияСвойств(ДополнительныеПоляОтчета.Добавить(), ТекСтрока);
		КонецЕсли;
		
		Если Не ТекСтрока.ТолькоДляМакета Тогда
			Попытка
				ТекВыбранноеПоле = Построитель.ВыбранныеПоля.Добавить(ТекСтрока.ПутьКДанным, ТекСтрока.Имя);
				ТекВыбранноеПоле.Представление = ТекСтрока.ПолноеПредставление;
			Исключение 
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры	//	отУстановитьИспользованиеИзмеренийДляТаблицыЗапросов()


///////////////////////////////////////////////////////////////////////////////
//ПРОЧИЕ ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПОСТРОЕНИЯ ОТЧЕТОВ

// Функция добавления показателя
//
// Параметры:                          
//	Список          - СписокЗначений - Список
//	Имя             - Строка         - Имя 
//  Представление   - Строка         - Преставление
//	Использование   - Булево         - Использование
//	ФорматнаяСтрока - Строка         - Форматная строка
//  ШиринаКолонки   - Число          - Ширина колонки
//
// Возвращаемое значение:
//	ЭлементСпискаЗначений - строка списка
//
Функция отДобавитьФункциюПоказатель(Список, Имя, Представление, Использование = Истина, ФорматнаяСтрока = "", ШиринаКолонки = "")	Экспорт
	
	Поле = Список.Добавить();
	Поле.Имя           = Имя;
	Поле.Представление = Представление;
	Поле.Использование = Использование;
	Попытка Поле.ШиринаКолонки = ?(ПустаяСтрока(ШиринаКолонки), 14, Число(ШиринаКолонки));
	Исключение КонецПопытки;
	Попытка Поле.ФорматнаяСтрока = ФорматнаяСтрока;	
	Исключение КонецПопытки;
	
	Возврат Поле;
	
КонецФункции	//	отДобавитьФункциюПоказатель()

// Процедура форматирует построитель отчета
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект - объект отчета
//
Процедура отФорматированиеПостроителяОтчета(ОтчетОбъект) Экспорт
	
	// Оформим ПостроительОтчетов
	Построитель = ОтчетОбъект.ПостроительОтчета;
	ДеревоПолей = ОтчетОбъект.ДеревоДоступныхПолей;
	
	Если Построитель.ИзмеренияСтроки.Количество() > 0 Тогда
		к = 0;
		Пока к <= Построитель.ИзмеренияСтроки.Количество() - 1 Цикл
			ТекИзмерение = Построитель.ИзмеренияСтроки[к];
			ТекПоле = ДеревоПолей.Строки.Найти(ТекИзмерение.ПутьКДанным, "ПутьКДанным", Истина);
			Если ТекПоле <> Неопределено Тогда
				ТекИзмерение.Представление = ТекПоле.Представление;
			Иначе
				Построитель.ИзмеренияСтроки.Удалить(ТекИзмерение);
				Продолжить;
			КонецЕсли;
			
			к = к + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если Построитель.ИзмеренияКолонки.Количество() > 0 Тогда
		к = 0;
		Пока к <= Построитель.ИзмеренияКолонки.Количество() - 1 Цикл
			ТекИзмерение = Построитель.ИзмеренияКолонки[к];
			ТекПоле = ДеревоПолей.Строки.Найти(ТекИзмерение.ПутьКДанным, "ПутьКДанным", Истина);
			Если ТекПоле <> Неопределено Тогда
				ТекИзмерение.Представление = ТекПоле.Представление;
			Иначе
				Построитель.ИзмеренияКолонки.Удалить(ТекИзмерение);
				Продолжить;
			КонецЕсли;
			
			к = к + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если Построитель.Отбор.Количество() > 0 Тогда
		к = 0;
		Пока к <= Построитель.Отбор.Количество() - 1 Цикл
			ТекОтбор = Построитель.Отбор[к];
			ТекПоле = ДеревоПолей.Строки.Найти(ТекОтбор.ПутьКДанным, "ПутьКДанным", Истина);
			Если ТекПоле <> Неопределено Тогда
				ТекОтбор.Представление = ТекПоле.ПолноеПредставление;
			Иначе
				Построитель.Отбор.Удалить(к);
				Продолжить;
			КонецЕсли;
			
			к = к + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	//	отФорматированиеПостроителяОтчета()

// Процедура начальной инициализации стандартного отчета (по построителю и макету)
//
// Параметры:
//	СтруктураПараметрыЗаполнения - Структура    - Структура параметров заполнения
//	Права						 - Неопределено - права	
//
Процедура отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, Права = Неопределено) Экспорт
	
	// заполним настройки по макету
	ОтчетОбъект            = СтруктураПараметрыЗаполнения["ОтчетОбъект"];
	ИмяМакетаТекстаЗапроса = СтруктураПараметрыЗаполнения["ИмяМакетаТекстаЗапроса"];
	ИмяМакетаПараметров    = СтруктураПараметрыЗаполнения["ИмяМакетаПараметров"];
	
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	ВидОтчета         = ОтчетОбъект.ВидОтчета;
	
	Попытка МакетТекстЗапроса = ОтчетОбъект.ПолучитьМакет(ИмяМакетаТекстаЗапроса);
	Исключение
		Сообщить("Ошибка! Не найден макет для заполнения текста запроса!"); 
		Возврат;
	КонецПопытки;
	
	ТекстЗапроса = МакетТекстЗапроса.ПолучитьТекст();
	Если ПустаяСтрока(ТекстЗапроса) Тогда
		Сообщить("Ошибка! Текст запроса не заполнен!"); 
		Возврат;
	КонецЕсли;
	
	СтруктураИтоговыхВыраженийПоказателей = Неопределено;
	Попытка
		СтруктураИтоговыхВыраженийПоказателей = ОтчетОбъект.СтруктураИтоговыхВыраженийПоказателей;
	Исключение КонецПопытки;
	
	ДополнительныеПоляОтчета = Новый ТаблицаЗначений;
	ДополнительныеПоляОтчета.Колонки.Добавить("Имя");
	ДополнительныеПоляОтчета.Колонки.Добавить("ПутьКДанным");
	ДополнительныеПоляОтчета.Колонки.Добавить("ПолноеПредставление");
	ДополнительныеПоляОтчета.Колонки.Добавить("ШиринаКолонки");
	ДополнительныеПоляОтчета.Колонки.Добавить("Формат");
	ДополнительныеПоляОтчета.Колонки.Добавить("НеСвязанные");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьИтогПоИерархии");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьИтогДляВсехУровней");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьОбщийИтог");
	ОтчетОбъект.ДополнительныеПоляОтчета = ДополнительныеПоляОтчета;
	
	МакетПараметры = ОтчетОбъект.ПолучитьМакет(ИмяМакетаПараметров);
	ОбластьПоказателей = МакетПараметры.ПолучитьОбласть("Показатели");
	ОбластьФункций     = МакетПараметры.ПолучитьОбласть("Функции");
	ОбластьМакетов     = МакетПараметры.ПолучитьОбласть("ИменаМакетов");
	
	// Получим параметры использования функций для текущего макета
	Для к = 1 по ОбластьМакетов.ВысотаТаблицы Цикл
		Если ОбластьМакетов.Область(к, 1).Текст = ИмяМакетаТекстаЗапроса Тогда
			ОбластьТекущегоМакета = ОбластьМакетов.ПолучитьОбласть("R" + к);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Заполним функции отчета
	ОтчетОбъект.Функции.Очистить();
	Для к = 1 по ОбластьФункций.ШиринаТаблицы Цикл
		Если Не Булево(ОбластьТекущегоМакета.Область(1, к + 6).Текст) Тогда Продолжить; КонецЕсли;
		
		Идентификатор = ОбластьФункций.Область(1, к).Текст;
		Представление = ОбластьФункций.Область(2, к).Текст;
		Представление = ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Если ВидОтчета = Перечисления.ВидыОтчетов.Остатки Тогда
			Представление = "Остаток";
		КонецЕсли;
		
		отДобавитьФункциюПоказатель(ОтчетОбъект.Функции, Идентификатор, Представление, Истина);
	КонецЦикла;
	
	// Заполним показатели отчета
	ОтчетОбъект.Показатели.Очистить();
	Для к = 1 по ОбластьПоказателей.ВысотаТаблицы Цикл
		Идентификатор  = ОбластьПоказателей.Область(к, 1).Текст;
		Представление  = ОбластьПоказателей.Область(к, 2).Текст;
		Представление  = ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Использование  = ВРег(ОбластьПоказателей.Область(к, 3).Текст) = "ИСТИНА";
		Форматирование = ОбластьПоказателей.Область(к, 4).Текст;
		ШиринаКолонки = ОбластьПоказателей.Область(к, 5).Текст;
		Если ПустаяСтрока(ШиринаКолонки)  Тогда
			ШиринаКолонки = 14;
		Иначе
			ШиринаКолонки = Число(ШиринаКолонки);
		КонецЕсли;
		
		Если ТипЗнч(СтруктураИтоговыхВыраженийПоказателей) = Тип("Структура") Тогда
			ОбластьФункцийПоказателя = ОбластьПоказателей.ПолучитьОбласть(к, 7, к, 6 + ОбластьФункций.ШиринаТаблицы);
			Для ПозицияКолонки = 1 по ОбластьФункцийПоказателя.ШиринаТаблицы Цикл
				ИмяРесурса = Идентификатор + ОбластьФункций.Область(1, ПозицияКолонки).Текст;
				ЗначениеВыражения = ОбластьФункцийПоказателя.Область(1, ПозицияКолонки).Текст;
				Если ВРег(ЗначениеВыражения) = "СУММА" ИЛИ ВРег(ЗначениеВыражения) = "МАКСИМУМ"
					ИЛИ ВРег(ЗначениеВыражения) = "МИНИМУМ" ИЛИ ВРег(ЗначениеВыражения) = "СРЕДНЕЕ" Тогда
					СтруктураИтоговыхВыраженийПоказателей.Вставить(ИмяРесурса, ЗначениеВыражения+"("+ИмяРесурса+")");
				Иначе
					СтруктураИтоговыхВыраженийПоказателей.Вставить(ИмяРесурса, ЗначениеВыражения);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		отДобавитьФункциюПоказатель(ОтчетОбъект.Показатели, Идентификатор, Представление, Использование, Форматирование, ШиринаКолонки);
	КонецЦикла;
	
	Попытка    СекцияСвойств = МакетТекстЗапроса.ПолучитьОбласть("ИзмеренияПостроителяДляСвойств");
	Исключение СекцияСвойств = Неопределено;
	КонецПопытки;
	
	СтруктураСвязиСвойств  = Новый Структура();
	
	СвойстваПутиКДанным = Новый Структура();
	Если СекцияСвойств <> Неопределено Тогда
		Для к = 1 по СекцияСвойств.КоличествоСтрок() Цикл
			ТекСтрока = СокрЛП(СекцияСвойств.ПолучитьСтроку(к));
			Если Найти(ТекСтрока, "КАК") = 0 Тогда Продолжить; КонецЕсли;
			
			ТекСтрока = СтрЗаменить(ТекСтрока, ".*", "");
			Поз = Найти(ТекСтрока, "КАК");
			СвойстваПутиКДанным.Вставить(СтрЗаменить(Сред(ТекСтрока, Поз + 4), ",", ""), Лев(ТекСтрока, Поз - 1));
		КонецЦикла;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Область ИзмеренияПостроителяДляСвойств", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#КонецОбласти", "");
		
		ПостроительОтчета.Текст = ТекстЗапроса;
		ДоступныеПоля = ПостроительОтчета.ДоступныеПоля;
		
		Запрос = Новый Запрос();
		Запрос.Текст = "
		|ВЫБРАТЬ
		|ВложеннаяТаблица.Свойство,
		|ВложеннаяТаблица.Назначение,
		|ВложеннаяТаблица.Назначение.ТипЗначения КАК ТипЗначенияНазначения,
		|ВЫБОР КОГДА ВложеннаяТаблица.Назначение.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ХАРАКТЕРИСТИКИ) ТОГДА
		|	ИСТИНА
		|ИНАЧЕ
		|	ЛОЖЬ
		|КОНЕЦ КАК ЭтоХарактеристика
		|ИЗ(
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначенияСвойств.Свойство КАК Свойство,
		|	ВЫБОР КОГДА НазначенияСвойств.Ссылка.БазовоеНазначение = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ПустаяСсылка) ТОГДА НазначенияСвойств.Ссылка
		|	ИНАЧЕ НазначенияСвойств.Ссылка.БазовоеНазначение
		|	КОНЕЦ КАК Назначение
		|ИЗ ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойств
		|ГДЕ 
		| НазначенияСвойств.Ссылка.ПометкаУдаления = Ложь
		| И НазначенияСвойств.Ссылка.ЭтоГруппа = Ложь) КАК ВложеннаяТаблица
		|УПОРЯДОЧИТЬ ПО ВложеннаяТаблица.Свойство.Наименование,
		|	ВложеннаяТаблица.Назначение.Наименование";
		
		// Строка запроса по всем существующим свойствам
		СтрЗапросаСвойств      = "";
		СтрЗапросаИтогиСвойств = "";
		СтрГруппировкиСвойств  = "";
		СтрСоединенийСвойств   = "";
		СтрЗапросаСвойствВыбрать = "";
		
		ИскатьСвойстваДляХарактеристики = ДоступныеПоля.Найти("ХарактеристикаНоменклатуры")<>Неопределено И СвойстваПутиКДанным.Свойство("ХарактеристикаНоменклатуры");
		
		НазначенияСвойств = Новый ТаблицаЗначений;
		НазначенияСвойств.Колонки.Добавить("Свойство");
		НазначенияСвойств.Колонки.Добавить("Измерение");
		НазначенияСвойств.Колонки.Добавить("ПутьКДаннымИзмерения");
		НазначенияСвойств.Колонки.Добавить("ПутьКДанным");
		
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДаннымПоля");
		ТаблицаПолей.Колонки.Добавить("ИмяИзмерения");
		ТаблицаПолей.Колонки.Добавить("ПутьКДаннымИзмерения");
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");
		
		Для Каждого ТекВыбСвойство Из СвойстваПутиКДанным Цикл
			ТекПоле = ДоступныеПоля.Найти(ТекВыбСвойство.Ключ);
			Если ТекПоле <> Неопределено Тогда
				Если НЕ (ТекПоле.Измерение ИЛИ ТекПоле.Отбор) Тогда Продолжить; КонецЕсли;
				НовоеПоле = ТаблицаПолей.Добавить();
				НовоеПоле.ПутьКДаннымПоля		= СокрЛП(ТекВыбСвойство.Значение);
				НовоеПоле.ИмяИзмерения			= ТекПоле.Имя;
				НовоеПоле.ПутьКДаннымИзмерения	= ТекПоле.ПутьКДанным;
				НовоеПоле.ТипЗначения			= ТекПоле.ТипЗначения
			КонецЕсли;
		КонецЦикла;
		
		СвойстваВыборка = Запрос.Выполнить().Выбрать();
		Пока СвойстваВыборка.Следующий() Цикл
			Если НЕ СвойстваВыборка.ЭтоХарактеристика Тогда
				ТипНазначения = СвойстваВыборка.ТипЗначенияНазначения.Типы()[0];
				Для Каждого ТекПоле Из ТаблицаПолей Цикл
					Если ТекПоле.ТипЗначения.СодержитТип(ТипНазначения) Тогда
						ТекСтрока = НазначенияСвойств.Добавить();
						ТекСтрока.Свойство    = СвойстваВыборка.Свойство.Ссылка;
						ТекСтрока.Измерение   = ТекПоле.ИмяИзмерения;
						ТекСтрока.ПутьКДаннымИзмерения   = ТекПоле.ПутьКДаннымИзмерения;
						ТекСтрока.ПутьКДанным = ТекПоле.ПутьКДаннымПоля;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ИскатьСвойстваДляХарактеристики Тогда
				ТекСтрока = НазначенияСвойств.Добавить();
				ТекСтрока.Свойство    = СвойстваВыборка.Свойство.Ссылка;
				ТекСтрока.Измерение   = "ХарактеристикаНоменклатуры";
				ТекСтрока.ПутьКДаннымИзмерения   = "ХарактеристикаНоменклатуры";
				ТекСтрока.ПутьКДанным = СокрЛП(СвойстваПутиКДанным["ХарактеристикаНоменклатуры"]);
			КонецЕсли;
		КонецЦикла;
		
		ПредВидСвойства = "";
		НазначенияСвойств.Свернуть("Свойство,Измерение,ПутьКДаннымИзмерения,ПутьКДанным", );
		НазначенияСвойств.Сортировать("Свойство,ПутьКДанным");
		Для Каждого ТекСтрока Из НазначенияСвойств Цикл
			НомерСвойства = НазначенияСвойств.Индекс(ТекСтрока) + 1;
			ИмяСвойства = "Свойство" + НомерСвойства;
			Если ТекСтрока.Свойство <> ПредВидСвойства Тогда
				ИмяПараметра = "ПараметрСвойства" + НомерСвойства;
				ПредВидСвойства = ТекСтрока.Свойство;
			КонецЕсли;
			
			ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(ТекСтрока.Свойство.ТипЗначения);

            ИмяСвойствоЗначение = ИмяСвойства + "Значение";
			
			//ЗапросСвойстваПоля = ", ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС;
			ЗапросСвойстваПоля = ", " + отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " КАК " + ИмяСвойствоЗначение + " //СВОЙСТВО" + Символы.ПС;
			//ЗапросСвойстваГруппы = ", ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") //СВОЙСТВО" + Символы.ПС;
			ЗапросСвойстваГруппы = ", " + отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " //СВОЙСТВО" + Символы.ПС;
			ЗапросСвойстваИтоги = ",МАКСИМУМ(" + ИмяСвойствоЗначение + ")	//СВОЙСТВО" + Символы.ПС;
			ЗапросСвойстваСоединение = "{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ТекСтрока.ПутьКДанным + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + "}	//СВОЙСТВО" + Символы.ПС;
			
			СтрЗапросаСвойств      = СтрЗапросаСвойств      + ЗапросСвойстваПоля;
			СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств + ЗапросСвойстваИтоги;
			СтрГруппировкиСвойств  = СтрГруппировкиСвойств  + ЗапросСвойстваГруппы;
			СтрСоединенийСвойств   = СтрСоединенийСвойств   + ЗапросСвойстваСоединение;
			
			ПараметрыСвойства = Новый Структура("ИмяИзмерения,ПутьКДаннымИзмерения,Свойство,Представление,ТипЗначения,ИмяПараметра",
			ТекСтрока.Измерение, ТекСтрока.ПутьКДаннымИзмерения, ТекСтрока.Свойство.Ссылка, ТекСтрока.Свойство.Наименование, ТекСтрока.Свойство.ТипЗначения, ИмяПараметра);
			СтруктураСвязиСвойств.Вставить(ИмяСвойствоЗначение, ПараметрыСвойства);
			СтрЗапросаСвойствВыбрать = СтрЗапросаСвойствВыбрать + " , " + ИмяСвойствоЗначение + " КАК " + ИмяСвойствоЗначение + Символы.ПС;
		КонецЦикла;
		
		СтрЗапросаСвойств      = СтрЗапросаСвойств+Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА";
		СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств+Символы.ПС+"//ВЫБРАННЫЕИТОГИСВОЙСТВА";
		СтрГруппировкиСвойств  = СтрГруппировкиСвойств+Символы.ПС+"//ВЫБРАННЫЕСГРУППИРОВАТЬПО";
		СтрСоединенийСвойств   = СтрСоединенийСвойств+Символы.ПС+"//ВЫБРАННЫЕСОЕДИНЕНИЯ";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//СВОЙСТВА",        СтрЗапросаСвойств);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ИТОГИСВОЙСТВА",   СтрЗапросаИтогиСвойств);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//СГРУППИРОВАТЬПО", СтрГруппировкиСвойств);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//СОЕДИНЕНИЯ",      СтрСоединенийСвойств);

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ВЫБРАТЬСВОЙСТВА", СтрЗапросаСвойствВыбрать);
		
		Если СтрЧислоВхождений(ТекстЗапроса, "//ВЫБРАННЫЕСВОЙСТВА")<5 ИЛИ Найти(ТекстЗапроса, "//ВЫБРАННЫЕСОЕДИНЕНИЯ")=0 Тогда
			СвойстваПутиКДанным = Новый Структура();
		КонецЕсли;
	КонецЕсли;
	
	ОтчетОбъект.СтруктураСвязиСвойств = СтруктураСвязиСвойств;
	
	ОтчетОбъект.ТекстЗапроса = ТекстЗапроса;
	ПостроительОтчета.Текст = ТекстЗапроса;
	
	ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей = отПолучитьПараметрыИспользованияПолейИПоказателей(ОтчетОбъект, МакетПараметры, ОбластьФункций);
	отПреобразованиеПолейПостроителя(ОтчетОбъект, СвойстваПутиКДанным);
	
	АвтозаполнениеПолей = Истина;
	Попытка
		АвтозаполнениеПолей = ОтчетОбъект.АвтозаполнениеПолей;
	Исключение
	КонецПопытки;
	
	Если АвтозаполнениеПолей Тогда
		ПостроительОтчета.ЗаполнитьНастройки();
		
		Для Каждого ТекСтрока Из ОтчетОбъект.ДеревоДоступныхПолей.Строки Цикл
			Если ПостроительОтчета.ДоступныеПоля.Найти(ТекСтрока.Имя).Измерение<>ТекСтрока.Измерение Тогда
				отЗаполнитьВетвиДерева(ТекСтрока, "Измерение", ПостроительОтчета.ДоступныеПоля.Найти(ТекСтрока.Имя).Измерение);
			КонецЕсли;
			
			Если ТекСтрока.Строки.Количество() = 0 Тогда Продолжить; КонецЕсли;
			
			ТекСтрока.Строки.Сортировать("Представление", Истина);
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		ДеревоПоказателей.Колонки.Добавить("Использование");
		ДеревоПоказателей.Колонки.Добавить("Имя");
		ДеревоПоказателей.Колонки.Добавить("ПутьКДанным");
		ДеревоПоказателей.Колонки.Добавить("Представление");
		ДеревоПоказателей.Колонки.Добавить("ФорматнаяСтрока");
		ДеревоПоказателей.Колонки.Добавить("ШиринаКолонки");
		ДеревоПоказателей.Колонки.Добавить("Группа");
		ДеревоПоказателей.Колонки.Добавить("КартинкаЭлемента");
		
		ЕстьФункции = Не ПустаяСтрока(ОтчетОбъект.Функции[0].Имя);
		
		Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
			
			Если ПостроительОтчета.ДоступныеПоля.Найти(ТекПоказатель.Имя) <> Неопределено Тогда
				НоваяСтрока = ДеревоПоказателей.Строки.Добавить();
				НоваяСтрока.Использование = ТекПоказатель.Использование;
				НоваяСтрока.Имя = ТекПоказатель.Имя;
				НоваяСтрока.ПутьКДанным = ТекПоказатель.Имя;
				НоваяСтрока.Представление = ТекПоказатель.Представление;
				НоваяСтрока.ФорматнаяСтрока = ТекПоказатель.ФорматнаяСтрока;
				НоваяСтрока.ШиринаКолонки = ТекПоказатель.ШиринаКолонки;
				НоваяСтрока.Группа = Ложь;
				НоваяСтрока.КартинкаЭлемента = 7;
			КонецЕсли;
			
			Если ЕстьФункции Тогда
				Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
					ИмяРесурса = ТекПоказатель.Имя+ТекФункция.Имя;
					Если ПостроительОтчета.ДоступныеПоля.Найти(ИмяРесурса) <> Неопределено Тогда
						НоваяГруппа = ДеревоПоказателей.Строки.Найти(ТекФункция.Имя, "Имя");
						НоваяГруппа = ?(НоваяГруппа=Неопределено, ДеревоПоказателей.Строки.Добавить(), НоваяГруппа);
						НоваяГруппа.Использование = ТекФункция.Использование;
						НоваяГруппа.Имя = ТекФункция.Имя;
						НоваяГруппа.ПутьКДанным = ТекФункция.Имя;
						НоваяГруппа.Представление = ТекФункция.Представление;
						НоваяГруппа.Группа = Истина;
						НоваяГруппа.КартинкаЭлемента = 6;
						
						НоваяСтрока = НоваяГруппа.Строки.Добавить();
						НоваяСтрока.Использование = ТекПоказатель.Использование;
						НоваяСтрока.Имя = ТекПоказатель.Имя;
						НоваяСтрока.ПутьКДанным = ИмяРесурса;
						НоваяСтрока.Представление = ТекПоказатель.Представление;
						НоваяСтрока.ФорматнаяСтрока = ТекПоказатель.ФорматнаяСтрока;
						НоваяСтрока.ШиринаКолонки = ТекПоказатель.ШиринаКолонки;
						НоваяСтрока.Группа = Ложь;
						НоваяСтрока.КартинкаЭлемента = 7;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	Исключение
	КонецПопытки;
	
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		СтрокаФикс = ОтчетОбъект.ИзмеренияСтроки.Найти("ПериодРегистратор", "ПутьКДанным");
		Если СтрокаФикс <> Неопределено Тогда
			СтрокаФикс.Фиксированное = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отЗаполнитьНачальныеНастройки()

// Процедура начальной инициализации отчета с источником данных (по построителю и макету)
//
// Параметры:
//	СтруктураПараметрыЗаполнения - Структура    - Структура параметров заполнения
//	Права						 - Неопределено - права	
//
Процедура отЗаполнитьНачальныеНастройкиОтчетаСИсточникомДанных(СтруктураПараметрыЗаполнения, Права = Неопределено) Экспорт
	
	// заполним настройки по макету
	ОтчетОбъект            = СтруктураПараметрыЗаполнения["ОтчетОбъект"];
	ИмяМакетаПараметров    = СтруктураПараметрыЗаполнения["ИмяМакетаПараметров"];
	
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	ВидОтчета         = ОтчетОбъект.ВидОтчета;
	СтруктураИтоговыхВыраженийПоказателей = ОтчетОбъект.СтруктураИтоговыхВыраженийПоказателей;
	
	ДополнительныеПоляОтчета = Новый ТаблицаЗначений;
	ДополнительныеПоляОтчета.Колонки.Добавить("Имя");
	ДополнительныеПоляОтчета.Колонки.Добавить("ПутьКДанным");
	ДополнительныеПоляОтчета.Колонки.Добавить("ПолноеПредставление");
	ДополнительныеПоляОтчета.Колонки.Добавить("ШиринаКолонки");
	ДополнительныеПоляОтчета.Колонки.Добавить("Формат");
	ДополнительныеПоляОтчета.Колонки.Добавить("НеСвязанные");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьИтогПоИерархии");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьИтогДляВсехУровней");
	ДополнительныеПоляОтчета.Колонки.Добавить("ВыводитьОбщийИтог");
	ОтчетОбъект.ДополнительныеПоляОтчета = ДополнительныеПоляОтчета;
	
	МакетПараметры		= ОтчетОбъект.ПолучитьМакет(ИмяМакетаПараметров);
	ОбластьПоказателей	= МакетПараметры.ПолучитьОбласть("Показатели");
	ОбластьФункций    	= МакетПараметры.ПолучитьОбласть("Функции");
	
	// Заполним функции отчета
	ОтчетОбъект.Функции.Очистить();
	Для к = 1 по ОбластьФункций.ШиринаТаблицы Цикл
		Идентификатор = ОбластьФункций.Область(1, к).Текст;
		Представление = ОбластьФункций.Область(2, к).Текст;
		Представление = ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Если ВидОтчета = Перечисления.ВидыОтчетов.Остатки Тогда
			Представление = "Остаток";
		КонецЕсли;
		
		отДобавитьФункциюПоказатель(ОтчетОбъект.Функции, Идентификатор, Представление, Истина);
	КонецЦикла;
	
	ОтчетОбъект.ШаблонИсточникаДанных = Новый ТаблицаЗначений;
	ШаблонИсточникаДанных             = ОтчетОбъект.ШаблонИсточникаДанных;
	
	// Заполним показатели отчета
	ОтчетОбъект.Показатели.Очистить();
	Для к = 1 по ОбластьПоказателей.ВысотаТаблицы Цикл
		Идентификатор  	= ОбластьПоказателей.Область(к, 1).Текст;
		Представление	= ОбластьПоказателей.Область(к, 2).Текст;
		Представление  	= ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Использование  	= ВРег(ОбластьПоказателей.Область(к, 3).Текст) = "ИСТИНА";
		Форматирование 	= ОбластьПоказателей.Область(к, 4).Текст;
		ШиринаКолонки 	= ОбластьПоказателей.Область(к, 5).Текст;
		Если ПустаяСтрока(ШиринаКолонки)  Тогда
			ШиринаКолонки = 14;
		Иначе
			ШиринаКолонки = Число(ШиринаКолонки);
		КонецЕсли;
		
		ПараметрыТипа = ОбластьПоказателей.Область(к, 7);
		ТипПоля = ?(ПараметрыТипа.СодержитЗначение, ПараметрыТипа.ТипЗначения, ПараметрыТипа.Текст);
		Если ТипПоля="Строка" ИЛИ СокрЛП(ТипПоля)="" Тогда
			ТипПоля = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0));
		ИначеЕсли ТипПоля="Число" Тогда
			ТипПоля = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 6));
		ИначеЕсли ТипПоля="Дата" Тогда
			ТипПоля = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
		ИначеЕсли ТипПоля="ДокументСсылка" Тогда
			ТипПоля = Новый ОписаниеТипов(Документы.ТипВсеСсылки());
		Иначе
			ТипПоля = Новый ОписаниеТипов(ТипПоля);
		КонецЕсли;
		
		ОбластьФункцийПоказателя = ОбластьПоказателей.ПолучитьОбласть(к, 10, к, 9 + ОбластьФункций.ШиринаТаблицы);
		Для ПозицияКолонки = 1 по ОбластьФункцийПоказателя.ШиринаТаблицы Цикл
			ИмяРесурса = Идентификатор + ОбластьФункций.Область(1, ПозицияКолонки).Текст;
			ЗначениеВыражения = ОбластьФункцийПоказателя.Область(1, ПозицияКолонки).Текст;
			Если ВРег(ЗначениеВыражения) = "СУММА" ИЛИ ВРег(ЗначениеВыражения) = "МАКСИМУМ"
				ИЛИ ВРег(ЗначениеВыражения) = "МИНИМУМ" ИЛИ ВРег(ЗначениеВыражения) = "СРЕДНЕЕ" Тогда
				СтруктураИтоговыхВыраженийПоказателей.Вставить(ИмяРесурса, ЗначениеВыражения+"("+ИмяРесурса+")");
			Иначе
				СтруктураИтоговыхВыраженийПоказателей.Вставить(ИмяРесурса, ЗначениеВыражения);
			КонецЕсли;
			
			ШаблонИсточникаДанных.Колонки.Добавить(ИмяРесурса, ТипПоля, Представление + " " + ОбластьФункций.Область(2, ПозицияКолонки).Текст, ШиринаКолонки);
			
		КонецЦикла;
		
		отДобавитьФункциюПоказатель(ОтчетОбъект.Показатели, Идентификатор, Представление, Использование, Форматирование, ШиринаКолонки);
		
	КонецЦикла;
	
	отПолучитьСтруктурыИзмеренийИПолейДляИсточникаДанных(ОтчетОбъект, МакетПараметры, ОбластьФункций);
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ДеревоДоступныхПолей.Строки Цикл
		Если ТекСтрока.Строки.Количество() = 0 Тогда Продолжить; КонецЕсли;
		ТекСтрока.Строки.Сортировать("Представление", Истина);
	КонецЦикла;
	
КонецПроцедуры	//	отЗаполнитьНачальныеНастройки()

// Процедура удаляет доступные поля построителя, отсутствующие в макете параметров
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект   - Объект отчета
//	СвойстваПутиКДанным - Структура     - Свойства пути к данным
//	ПараметрыПолей      - Структура     - Параметры полей
//	ПоляНастройки       - ПоляНастройки - Поля настройки построителя отчета
//	ДоступныеПоля       - ПоляНастройки - Доступные поля построителя отчета
//	СтруктураРесурсов   - Структура     - Структура ресурсов
//	ЭтоКорень           - Булево        - Признак корня дерева
//
Процедура отПреобразованиеПолейПостроителя(ОтчетОбъект, СвойстваПутиКДанным = Неопределено, ПараметрыПолей="", ПоляНастройки="", ДоступныеПоля="", СтруктураРесурсов="", ЭтоКорень = Истина) Экспорт
	
	Если СвойстваПутиКДанным = Неопределено Тогда
		Возврат;
	Конецесли;
	
	Если ЭтоКорень Тогда
		ПоляНастройки  = ОтчетОбъект.ПостроительОтчета.ДоступныеПоля;
		ПараметрыПолей = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей;
		
		Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
		Исключение КонецПопытки;
		Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
			СтруктураСвязиСвойств = Новый Структура();
		КонецЕсли;
		
		СтруктураРесурсов = Новый Структура();
		Попытка
			Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
				Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
					СтруктураРесурсов.Вставить(ТекПоказатель.Имя+ТекФункция.Имя, ТекПоказатель.Представление+" "+ТекФункция.Представление);
				КонецЦикла;
			КонецЦикла;
		Исключение	
		КонецПопытки;
		
		ОтчетОбъект.ДеревоДоступныхПолей = Новый ДеревоЗначений();
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Представление",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("ПолноеПредставление",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Имя",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("ПутьКДанным",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Измерение",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Отбор",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Порядок",);
		ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("ТипЗначения",);
		ДоступныеПоля = ОтчетОбъект.ДеревоДоступныхПолей;
	КонецЕсли;
	
	ТекИндекс = 0;
	Пока ТекИндекс <= ПоляНастройки.Количество() - 1 Цикл
		ТекПоле = ПоляНастройки.Получить(ТекИндекс);
		//Если (НЕ ТекПоле.Измерение) И (НЕ ТекПоле.Отбор) Тогда ТекИндекс = ТекИндекс + 1; Продолжить; КонецЕсли;
		
		Если НЕ ПараметрыПолей.Свойство(ТекПоле.Имя) Тогда
			Если НЕ ЭтоКорень Тогда
				ПоляНастройки.Удалить(ТекПоле);
				Продолжить;
				
			Иначе
				Если СтруктураСвязиСвойств.Свойство(ТекПоле.Имя) Тогда
					ТекСвойство = СтруктураСвязиСвойств[ТекПоле.Имя];
					
					ТекИзмерение = ПоляНастройки.Найти(ТекСвойство["ИмяИзмерения"]);
					Если ТекИзмерение = Неопределено Тогда
						ПоляНастройки.Удалить(ТекПоле);
						Продолжить;
					КонецЕсли;
					
					ВладелецСвойства = ДоступныеПоля.Строки.Найти(ТекИзмерение.Имя, "Имя");
					Если ВладелецСвойства = Неопределено Тогда
						ВладелецСвойства = ДоступныеПоля.Строки.Добавить();
						ВладелецСвойства.Имя = ТекИзмерение.Имя;
					КонецЕсли;
					
					// Добавим в доступные поля
					ВетвьСвойств = ВладелецСвойства.Строки.Найти("Свойства", "Имя");
					Если ВетвьСвойств = Неопределено Тогда
						ВетвьСвойств = ВладелецСвойства.Строки.Добавить();
						ВетвьСвойств.Имя           = "Свойства";
						ВетвьСвойств.Представление = "Свойства";
					КонецЕсли;
					
					ДоступноеПоле = ВетвьСвойств.Строки.Добавить();
					ДоступноеПоле.Имя           = ТекПоле.Имя;
					ДоступноеПоле.ПутьКДанным   = ТекПоле.ПутьКДанным;
					ДоступноеПоле.Представление = ТекСвойство["Представление"];
					ДоступноеПоле.Измерение     = ТекПоле.Измерение;
					ДоступноеПоле.Отбор         = ТекПоле.Отбор;
					ДоступноеПоле.Порядок       = Ложь;
					ДоступноеПоле.ТипЗначения   = ТекСвойство["ТипЗначения"];
					
					РодительПоля = ВетвьСвойств.Родитель.Родитель;
					ПредставлениеРодителя = ВетвьСвойств.Родитель.Представление;
					Пока РодительПоля <> Неопределено Цикл
						ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
						РодительПоля = РодительПоля.Родитель;
					КонецЦикла;
					ДоступноеПоле.ПолноеПредставление = ДоступноеПоле.Представление+" ("+ПредставлениеРодителя+")";
					
				Иначе	
					// Добавим в доступные поля
					ДоступноеПоле = ДоступныеПоля.Строки.Найти(ТекПоле.Имя, "Имя");
					Если ДоступноеПоле = Неопределено Тогда
						ДоступноеПоле = ДоступныеПоля.Строки.Добавить();
						ДоступноеПоле.Имя = ТекПоле.Имя;
					КонецЕсли;
					
					Если СтруктураРесурсов.Свойство(ТекПоле.Имя) Тогда
						Представление = СтруктураРесурсов[ТекПоле.Имя];
						Если НЕ ПустаяСтрока(Представление) Тогда
							ТекПоле.Представление = Представление;
						КонецЕсли;
					КонецЕсли;
					
					ДоступноеПоле.ПутьКДанным   = ТекПоле.ПутьКДанным;
					ДоступноеПоле.Представление = ТекПоле.Представление;
					ДоступноеПоле.ПолноеПредставление = ТекПоле.Представление;
					ДоступноеПоле.Измерение     = ТекПоле.Измерение;
					ДоступноеПоле.Отбор         = ТекПоле.Отбор;
					ДоступноеПоле.Порядок       = ТекПоле.Порядок;
					ДоступноеПоле.ТипЗначения   = ТекПоле.ТипЗначения;
				КонецЕсли;
				ТекИндекс = ТекИндекс + 1;
			КонецЕсли;
			
		Иначе
			Представление = ПараметрыПолей[ТекПоле.Имя]["Представление"];
			Если НЕ ПустаяСтрока(Представление) Тогда
				ТекПоле.Представление = Представление;
			КонецЕсли;
			
			// Добавим в доступные поля
			ДоступноеПоле = ДоступныеПоля.Строки.Найти(ТекПоле.Имя, "Имя");
			Если ДоступноеПоле = Неопределено Тогда
				ДоступноеПоле = ДоступныеПоля.Строки.Добавить();
				ДоступноеПоле.Имя = ТекПоле.Имя;
			КонецЕсли;
			
			Если ЭтоКорень Тогда
				ДоступноеПоле.ПолноеПредставление = ТекПоле.Представление;
			Иначе
				РодительПоля = ДоступноеПоле.Родитель.Родитель;
				ПредставлениеРодителя = ДоступноеПоле.Родитель.Представление;
				Пока РодительПоля <> Неопределено Цикл
					ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
					РодительПоля = РодительПоля.Родитель;
				КонецЦикла;
				ДоступноеПоле.ПолноеПредставление = ТекПоле.Представление+" ("+ПредставлениеРодителя+")";
			КонецЕсли;
		
			ДоступноеПоле.ПутьКДанным   = ТекПоле.ПутьКДанным;
			ДоступноеПоле.Представление = ТекПоле.Представление;
			ДоступноеПоле.Измерение     = ТекПоле.Измерение;
			ДоступноеПоле.Отбор         = ТекПоле.Отбор;
			ДоступноеПоле.Порядок       = ТекПоле.Порядок;
			ДоступноеПоле.ТипЗначения   = ТекПоле.ТипЗначения;
			
			ИдентификаторПути  = СтрЗаменить(ТекПоле.ПутьКДанным, ".", "");
			Если СвойстваПутиКДанным.Свойство(ИдентификаторПути) Тогда
				ВетвьСвойств = ДоступноеПоле.Строки.Найти("Свойства", "Имя");
				Если ВетвьСвойств = Неопределено Тогда
					ВетвьСвойств = ДоступноеПоле.Строки.Добавить();
					ВетвьСвойств.Имя           = "Свойства";
					ВетвьСвойств.Представление = "Свойства";
				КонецЕсли;
				
				ВетвьВыбСвойства = ВетвьСвойств.Строки.Найти("ВыбСвойства", "Имя");
				Если ВетвьВыбСвойства = Неопределено Тогда
					ВетвьВыбСвойства = ВетвьСвойств.Строки.Добавить();
					ВетвьВыбСвойства.Имя           = "ВыбСвойства";
					ВетвьВыбСвойства.Представление = "<Выбрать свойство...>";
					ВетвьВыбСвойства.ПутьКДанным = "@"+СокрЛП(СвойстваПутиКДанным[ИдентификаторПути])+"@";
				КонецЕсли;
			КонецЕсли;
			
			Если ТекПоле.Поля.Количество() > 0 Тогда
				Если НЕ ПараметрыПолей[ТекПоле.Имя].Свойство("Поля") Тогда
					ТекПоле.Поля.Очистить();
				Иначе
					отПреобразованиеПолейПостроителя(ОтчетОбъект, СвойстваПутиКДанным, ПараметрыПолей[ТекПоле.Имя]["Поля"], ТекПоле.Поля, ДоступноеПоле,, Ложь);
				КонецЕсли;
			КонецЕсли;
			
			ТекИндекс = ТекИндекс + 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	//	отПреобразованиеПолейПостроителя()

// Функция возвращает параметры использования поля по шаблону
//
// Параметры:
//	ОтчетОбъект        - ОтчетОбъект                     - Объект отчета
//	ОбластьПоля        - ОбластьЯчеекТабличногоДокумента - Область поля 
//	ОбластьФункцииПоля - ОбластьЯчеекТабличногоДокумента - Область функции поля
//	ОбластьФункций      - ОбластьЯчеекТабличногоДокумента - Область функции
//
// Возвращаемое значение:
//	Структура - Параметры поля
//
Функция отПолучитьПараметрыИспользованияПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, ОбластьФункций) Экспорт
	
	ПараметрыПоля = Новый Структура();
	
	ПутьКДанным   = ОбластьПоля.Область(1, 1).Текст;
	Представление = ОбластьПоля.Область(1, 2).Текст;
	Если ПустаяСтрока(Представление) Тогда
		Представление = ПутьКДанным;
		Пока Найти(Представление, ".") > 0 Цикл
			Представление = Сред(Представление, Найти(Представление, ".") + 1);
		КонецЦикла;
	КонецЕсли;
	ПараметрыПоля.Вставить("Представление", Представление);
	
	ИспользованиеПоля = Новый Структура();
	Использование = ОбластьПоля.Область(1, 3).Текст;
	Если НЕ ПустаяСтрока(Использование) Тогда
		ИспользованиеПоля.Вставить("Строка",   Найти(Использование, "Строка")   > 0);
		ИспользованиеПоля.Вставить("Колонка",  Найти(Использование, "Колонка")  > 0);
		ИспользованиеПоля.Вставить("Фильтр",   Найти(Использование, "Фильтр")   > 0);
		ИспользованиеПоля.Вставить("Поле",     Найти(Использование, "Поле")     > 0);
		ИспользованиеПоля.Вставить("Свойства", Найти(Использование, "Свойства") > 0);
		ИспользованиеПоля.Вставить("ОтсутствуетВОстатках", Найти(Использование, "ОтсутствуетВОстатках") > 0);
		
		Если Найти(Использование, "Порядок") > 0 Тогда
			Если Найти(Использование, "ПорядокВозр")  > 0 Тогда
				ИспользованиеПоля.Вставить("Порядок", "Возр");
			Иначе ИспользованиеПоля.Вставить("Порядок", "Убыв");
			КонецЕсли;
		КонецЕсли;
		
		Если ИспользованиеПоля["Строка"] или ИспользованиеПоля["Колонка"] Тогда
			ТипИзмерения = "Элементы";
			Если Найти(Использование, "ТолькоИерархия") > 0 Тогда
				ТипИзмерения = "Только иерархия";
			ИначеЕсли Найти(Использование, "Иерархия") > 0 Тогда
				ТипИзмерения = "Иерархия";
			КонецЕсли;
			
			ИспользованиеПоля.Вставить("Активность", Найти(ВРег(Использование), "ЛОЖЬ") = 0);
			ИспользованиеПоля.Вставить("ТипИзмерения", ТипИзмерения);
		КонецЕсли;
	КонецЕсли;
	
	РесурсыПоля = Новый Структура();
	Для к = 1 по ОбластьФункций.ШиринаТаблицы Цикл
		Идентификатор = ОбластьФункций.Область(1, к).Текст;
		Использование = НЕ ВРег(ОбластьФункцийПоля.Область(1, к).Текст) = "ЛОЖЬ";
		Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
			РесурсыПоля.Вставить(ТекПоказатель.Имя + Идентификатор, Использование);
		КонецЦикла;
	КонецЦикла;
	ИспользованиеПоля.Вставить("Ресурсы", РесурсыПоля);
	
	ПараметрыПоля.Вставить("Использование", ИспользованиеПоля);
	ПараметрыПоля.Вставить("Формат", СокрЛП(ОбластьПоля.Область(1, 4).Текст));
		
	ШиринаКолонки = ОбластьПоля.Область(1, 5).Текст;
	Если ПустаяСтрока(ШиринаКолонки) Тогда
		ПараметрыПоля.Вставить("ШиринаКолонки", 20);
	Иначе
		ПараметрыПоля.Вставить("ШиринаКолонки", Число(ШиринаКолонки));
	КонецЕсли;
	
	Возврат ПараметрыПоля;
	
КонецФункции	//	отПолучитьПараметрыИспользованияПоля()

// Функция создает структуру использования полей и ресурсов построителя отчета
// по шаблону, хранящемся в табличном документе макета отчета
//
// Параметры:
//	ОтчетОбъект    - ОтчетОбъект                     - Объект отчета
//  МакетПараметры - ТабличныйДокумент               - Макет отчета
//	ОбластьФункций - ОбластьЯчеекТабличногоДокумента - Область функций
//
//	Возвращаемое значение:
//	Структура - Параметры использования
//
Функция отПолучитьПараметрыИспользованияПолейИПоказателей(ОтчетОбъект, МакетПараметры, ОбластьФункций) Экспорт
	
	ПараметрыИспользованияПолейИПоказателей = Новый Структура();
	
	ОтчетОбъект.ИзмеренияСтроки.Очистить();
	ОтчетОбъект.ИзмеренияКолонки.Очистить();
	ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей = Новый Структура();
	ОтчетОбъект.ПараметрыИзмеренийСтрок = Новый Структура();
	
	ПараметрыИспользования = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей;
	ПараметрыИзмерений = ОтчетОбъект.ПараметрыИзмеренийСтрок;
	ИзмеренияСтроки  = ОтчетОбъект.ИзмеренияСтроки;
	ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки;
	
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	ОтборКоличество = ПостроительОтчета.Отбор.Количество();
	Для к = 1 По ОтборКоличество Цикл
		ПостроительОтчета.Отбор.Удалить(ОтборКоличество - к);
	КонецЦикла;
	
	ОбластьИзмерений = МакетПараметры.ПолучитьОбласть("Измерения");
	Для к = 1 по ОбластьИзмерений.ВысотаТаблицы Цикл
		ОбластьПоля = ОбластьИзмерений.ПолучитьОбласть("R" + к);
		ОбластьФункцийПоля = ОбластьИзмерений.ПолучитьОбласть(к, 7, к, 6 + ОбластьФункций.ШиринаТаблицы);
		
		ПутьКДанным   = ОбластьПоля.Область(1, 1).Текст;
		Представление = ОбластьПоля.Область(1, 2).Текст;
		
		Если ОбластьПоля.Область(1, 1).Автоотступ = 0 и ОбластьПоля.Область(1, 1).Отступ = 0 Тогда
			// Новое измерение
			ИмяИзмерения = ПутьКДанным;
			ПолеПостроителя = ПостроительОтчета.ДоступныеПоля.Найти(ИмяИзмерения);
			Если ПолеПостроителя = Неопределено Тогда Продолжить; КонецЕсли;
			РодительПолей = ПолеПостроителя;
			
			ПараметрыПоля = отПолучитьПараметрыИспользованияПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, ОбластьФункций);
			ПараметрыПоля.Вставить("Поля", Новый Структура());
			ПараметрыИспользования.Вставить(ИмяИзмерения, ПараметрыПоля);
			
			Если НЕ ПустаяСтрока(ПараметрыПоля.Представление) Тогда
				ПолеПостроителя.Представление = ПараметрыПоля.Представление;
			КонецЕсли;	
			
			Если ПараметрыПоля.Свойство("Использование") Тогда
				Использование = ПараметрыПоля["Использование"];
				Попытка
					Если Использование["Строка"] Тогда
						ТекСтрока = ИзмеренияСтроки.Добавить();
						ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
						ТекСтрока.Представление = ПараметрыПоля["Представление"];
						ТекСтрока.Использование = Использование.Активность;
						ТекСтрока.ТипИзмерения  = Использование["ТипИзмерения"];
						
						РодительПолей = ПолеПостроителя;
						
					ИначеЕсли Использование["Колонка"] и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
						ТекСтрока = ИзмеренияКолонки.Добавить();
						ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
						ТекСтрока.Представление = ПараметрыПоля["Представление"];
						ТекСтрока.Использование = Использование.Активность;
						ТекСтрока.ТипИзмерения  = Использование["ТипИзмерения"];
					КонецЕсли;
				Исключение КонецПопытки;
				
				Попытка
					Если Использование["Фильтр"] Тогда
						ПостроительОтчета.Отбор.Добавить(ПолеПостроителя.ПутьКДанным, ПолеПостроителя.Имя, ПараметрыПоля["Представление"]);
					КонецЕсли;
				Исключение КонецПопытки;
				
				Если Использование.Свойство("Порядок") Тогда
					ИдентификаторПолей = СтрЗаменить(РодительПолей.ПутьКДанным, ".", "_");
					Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
						ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
					КонецЕсли;
					
					ТаблицаСортировки = Неопределено;
					Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Порядок") Тогда
						ТаблицаСортировки = ПараметрыИзмерений[ИдентификаторПолей]["Порядок"];
					КонецЕсли;
					Если ТипЗнч(ТаблицаСортировки) <> Тип("ТаблицаЗначений") Тогда
						ТаблицаСортировки = Новый ТаблицаЗначений();
						ТаблицаСортировки.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
						ТаблицаСортировки.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
						ТаблицаСортировки.Колонки.Добавить("Убывание",      Новый ОписаниеТипов("Булево"));
						
						ПараметрыИзмерений[ИдентификаторПолей].Вставить("Порядок", ТаблицаСортировки);
					КонецЕсли;
					
					ТекСтрока = ТаблицаСортировки.Добавить();
					ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
					ТекСтрока.Представление = ПараметрыПоля["Представление"];
					ТекСтрока.Убывание      = Использование["Порядок"] = "Убыв";
				КонецЕсли;
			КонецЕсли;
			
			СтруктураПолей = ПараметрыИспользования[ИмяИзмерения]["Поля"];
			
			Продолжить;
		КонецЕсли;
		ПолеПостроителя = ПостроительОтчета.ДоступныеПоля.Найти(ИмяИзмерения);
		Если ПолеПостроителя = Неопределено Тогда Продолжить; КонецЕсли;
		ПоляПостроителя = ПолеПостроителя.Поля;
		
		Если Найти(ПутьКДанным, "СоставноеПредставление")>0 Тогда
			ТекстПредставления = ОбластьПоля.Область(1, 3).Текст;
			ПутьКПараметру = ?(ПутьКДанным="СоставноеПредставление",ИмяИзмерения, ИмяИзмерения+"."+СтрЗаменить(ПутьКДанным,".СоставноеПредставление",""));
			ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьКПараметру, ПараметрыИспользования);
			Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("СоставноеПредставление") Тогда
				ПараметрыИспользованияДляОсновногоПредставления.Вставить("СоставноеПредставление", Новый Структура("Представление, Поля", СтрЗаменить(ТекстПредставления,".",""), Новый Массив));
				ПараметрыПредставления = ПараметрыИспользованияДляОсновногоПредставления.СоставноеПредставление.Поля;
				ЧислоПолей = СтрЧислоВхождений(ТекстПредставления, "[");
				Для Индекс = 1 по ЧислоПолей Цикл
					ОткрПозиция = Найти(ТекстПредставления, "[");
					ЗакрПозиция = Найти(ТекстПредставления, "]");
					ЧастьПредставления = СокрЛП(Сред(ТекстПредставления, ОткрПозиция+1, ЗакрПозиция-ОткрПозиция-1));
					Если НЕ ИмяИзмерения = ЧастьПредставления Тогда
						ПараметрыПредставления.Добавить(ЧастьПредставления);
					КонецЕсли;
					ТекстПредставления = СокрЛП(Сред(ТекстПредставления, ЗакрПозиция+1));
				КонецЦикла;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если ПоляПостроителя.Количество() > 0 Тогда 
			
			ПозРазделителя = Найти(ПутьКДанным, ".");
			Если ПозРазделителя > 0 Тогда
				Идентификатор = Лев(ПутьКДанным, ПозРазделителя - 1);
				ПолеПостроителя = ПоляПостроителя.Найти(Идентификатор);
				Если ПолеПостроителя = Неопределено Тогда Продолжить; КонецЕсли;
				
				Если НЕ СтруктураПолей.Свойство(Идентификатор) Тогда
					СтруктураПолей.Вставить(Идентификатор, Новый Структура());
				КонецЕсли;
				
				ПолеНайдено = Истина;
				Если ПолеПостроителя.Поля.Количество() > 0 Тогда
					ПоляПостроителя = ПолеПостроителя.Поля;
					Если НЕ СтруктураПолей[Идентификатор].Свойство("Поля") Тогда
						СтруктураПолей[Идентификатор].Вставить("Поля", Новый Структура());
					КонецЕсли;
					ПоляНастройки = СтруктураПолей[Идентификатор]["Поля"];
					
					Пока ПозРазделителя > 0 Цикл
						ПутьКДанным    = Сред(ПутьКДанным, ПозРазделителя + 1);
						ПозРазделителя = Найти(ПутьКДанным, ".");
						
						Если ПозРазделителя > 0 Тогда
							Идентификатор = Лев(ПутьКДанным, ПозРазделителя - 1);
							ПолеПостроителя = ПоляПостроителя.Найти(Идентификатор);
							Если ПолеПостроителя = Неопределено Тогда
								ПолеНайдено = Ложь;
								Прервать;
							КонецЕсли;
							
							Если НЕ ПоляНастройки.Свойство(Идентификатор) Тогда
								ПоляНастройки.Вставить(Идентификатор, Новый Структура());
							КонецЕсли;
							
							Если ПолеПостроителя.Поля.Количество() > 0 Тогда
								ПоляПостроителя = ПолеПостроителя.Поля;
								Если НЕ ПоляНастройки[Идентификатор].Свойство("Поля") Тогда
									ПоляНастройки[Идентификатор].Вставить("Поля", Новый Структура());
								КонецЕсли;
								ПоляНастройки = ПоляНастройки[Идентификатор]["Поля"];
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				Если НЕ ПолеНайдено Тогда Продолжить; КонецЕсли;
				
				ПараметрыПоля = отПолучитьПараметрыИспользованияПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, ОбластьФункций);
				ПоляНастройки.Вставить(ПутьКДанным, ПараметрыПоля);
				
			Иначе
				ПолеПостроителя = ПоляПостроителя.Найти(ПутьКДанным);
				Если ПолеПостроителя = Неопределено Тогда Продолжить; КонецЕсли;
				
				ПараметрыПоля = отПолучитьПараметрыИспользованияПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, ОбластьФункций);
				СтруктураПолей.Вставить(ПутьКДанным, ПараметрыПоля);
				
			КонецЕсли;
		Иначе
			Если Найти(ОбластьПоля.Область(1, 3).Текст, "ОсновноеПредставление") > 0 Тогда
				ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ИмяИзмерения, ПараметрыИспользования);
				Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("ОсновноеПредставление") Тогда
					ПараметрыИспользованияДляОсновногоПредставления.Вставить("ОсновноеПредставление", ПутьКДанным);
				КонецЕсли;
			КонецЕсли;
			Продолжить; 
		КонецЕсли;
		
		Если Найти(ОбластьПоля.Область(1, 3).Текст, "ОсновноеПредставление") > 0 Тогда
			ПутьКДаннымПоляПостроителя = ПоляПостроителя.Найти(ПутьКДанным).ПутьКДанным;
			ПутьКДаннымДляОсновногоПредставления = Лев(ПутьКДаннымПоляПостроителя,СтрДлина(ПутьКДаннымПоляПостроителя)-СтрДлина(ПутьКДанным)-1);
			ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьКДаннымДляОсновногоПредставления, ПараметрыИспользования);
			Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("ОсновноеПредставление") Тогда
				ПараметрыИспользованияДляОсновногоПредставления.Вставить("ОсновноеПредставление", ПутьКДаннымПоляПостроителя);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ПараметрыПоля.Представление) Тогда
			ПолеПостроителя.Представление = ПараметрыПоля.Представление;
		КонецЕсли;
		
		Если ПараметрыПоля.Свойство("Использование") Тогда
			Использование = ПараметрыПоля["Использование"];
			Попытка
				Если Использование["Строка"] Тогда
					ТекСтрока = ИзмеренияСтроки.Добавить();
					ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
					ТекСтрока.Представление = ПараметрыПоля["Представление"];
					ТекСтрока.Использование = Использование.Активность;
					ТекСтрока.ТипИзмерения  = Использование["ТипИзмерения"];
					
					РодительПолей = ПолеПостроителя;
					
				ИначеЕсли Использование["Колонка"] и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
					ТекСтрока = ИзмеренияКолонки.Добавить();
					ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
					ТекСтрока.Представление = ПараметрыПоля["Представление"];
					ТекСтрока.Использование = Использование.Активность;
					ТекСтрока.ТипИзмерения  = Использование["ТипИзмерения"];
				КонецЕсли;
			Исключение КонецПопытки;
			
			Попытка
				Если Использование["Фильтр"] Тогда
					ПостроительОтчета.Отбор.Добавить(ПолеПостроителя.ПутьКДанным, ПолеПостроителя.Имя, ПараметрыПоля["Представление"]);
				КонецЕсли;
			Исключение КонецПопытки;
			
			Если Использование.Свойство("Порядок") Тогда
				ИдентификаторПолей = СтрЗаменить(РодительПолей.ПутьКДанным, ".", "_");
				Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
					ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
				КонецЕсли;
				
				ТаблицаСортировки = Неопределено;
				Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Порядок") Тогда
					ТаблицаСортировки = ПараметрыИзмерений[ИдентификаторПолей]["Порядок"];
				КонецЕсли;
				Если ТипЗнч(ТаблицаСортировки) <> Тип("ТаблицаЗначений") Тогда
					ТаблицаСортировки = Новый ТаблицаЗначений();
					ТаблицаСортировки.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
					ТаблицаСортировки.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
					ТаблицаСортировки.Колонки.Добавить("Убывание",      Новый ОписаниеТипов("Булево"));
					
					ПараметрыИзмерений[ИдентификаторПолей].Вставить("Порядок", ТаблицаСортировки);
				КонецЕсли;
				
				ТекСтрока = ТаблицаСортировки.Добавить();
				ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
				ТекСтрока.Представление = ПараметрыПоля["Представление"];
				ТекСтрока.Убывание      = Использование["Порядок"] = "Убыв";
			КонецЕсли;
			
			Если Использование.Свойство("Поле") и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
				Если Использование["Поле"] Тогда
					ИдентификаторПолей = СтрЗаменить(РодительПолей.ПутьКДанным, ".", "_");
					Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
						ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
					КонецЕсли;
					
					ТаблицаПолей = Неопределено;
					Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Поля") Тогда
						ТаблицаПолей = ПараметрыИзмерений[ИдентификаторПолей]["Поля"];
					КонецЕсли;
					Если ТипЗнч(ТаблицаПолей) <> Тип("ТаблицаЗначений") Тогда
						ТаблицаПолей = Новый ТаблицаЗначений();
						ТаблицаПолей.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
						ТаблицаПолей.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
						
						ПараметрыИзмерений[ИдентификаторПолей].Вставить("Поля", ТаблицаПолей);
					КонецЕсли;
					
					ТекСтрока = ТаблицаПолей.Добавить();
					ТекСтрока.ПутьКДанным   = ПолеПостроителя.ПутьКДанным;
					ТекСтрока.Представление = ПараметрыПоля["Представление"];
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПараметрыИспользования.Свойство("ОбщийИтог") Тогда
		ПараметрыИспользования.Вставить("ОбщийИтог", Новый Структура("Представление, Использование", "Итог", Новый Структура));
		
		РесурсыПоля = Новый Структура();
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				РесурсыПоля.Вставить(ТекПоказатель.Имя + ТекФункция.Имя, Истина);
			КонецЦикла;
		КонецЦикла;
		ПараметрыИспользования.ОбщийИтог.Использование.Вставить("Ресурсы", РесурсыПоля);
	КонецЕсли;
	
	// в конце добавим представления и форматирование общих полей
	Если НЕ ПараметрыИспользования.Свойство("ПериодГод") Тогда
		ПараметрыИспользования.Вставить("ПериодГод", Новый Структура("Представление, Формат", "По годам", "ДФ='гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодКвартал") Тогда
		ПараметрыИспользования.Вставить("ПериодКвартал", Новый Структура("Представление, Формат", "По кварталам", "ДФ='к ""квартал"" гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодМесяц") Тогда
		ПараметрыИспользования.Вставить("ПериодМесяц", Новый Структура("Представление, Формат", "По месяцам", "ДФ='ММММ гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодНеделя") Тогда
		ПараметрыИспользования.Вставить("ПериодНеделя", Новый Структура("Представление, Формат", "По неделям", "ДФ='""Неделя с"" дд.ММ.гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодДень") Тогда
		ПараметрыИспользования.Вставить("ПериодДень", Новый Структура("Представление, Формат", "По дням", "ДФ=дд.ММ.гггг"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодРегистратор") Тогда
		ПараметрыИспользования.Вставить("ПериодРегистратор", Новый Структура("Представление", "По документам движения"));
	КонецЕсли;
	
	Для Каждого ТекПараметр Из ПараметрыИзмерений Цикл
		СтрПредставленийПолей = "";
		Если ТекПараметр.Значение.Свойство("Поля") Тогда
			Если ТипЗнч(ТекПараметр.Значение.Поля) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекСтрока Из ТекПараметр.Значение.Поля Цикл
					СтрПредставленийПолей = СтрПредставленийПолей + ?(СтрПредставленийПолей = "", "", ", ") + ТекСтрока.Представление;
				КонецЦикла;
				
				ТекПараметр.Значение.Вставить("ПоляПредставление", СтрПредставленийПолей);
			КонецЕсли;
		КонецЕсли;
		
		СтрПредставленийПорядка = "";
		Если ТекПараметр.Значение.Свойство("Порядок") Тогда
			Если ТипЗнч(ТекПараметр.Значение.Порядок) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекСтрока Из ТекПараметр.Значение.Порядок Цикл
					СтрПредставленийПорядка = СтрПредставленийПорядка + ?(СтрПредставленийПорядка = "", "", ", ") + ТекСтрока.Представление + ?(ТекСтрока.Убывание, " (убыв)", "");
				КонецЦикла;
				
				ТекПараметр.Значение.Вставить("ПорядокПредставление", СтрПредставленийПорядка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыИспользования;
	
КонецФункции	//	отПолучитьПараметрыИспользованияПолейИПоказателей()

// Функция создает структуру использования полей и ресурсов построителя отчета
// по шаблону, хранящемся в табличном документе макета отчета. 
//
// Параметры:
//	ОтчетОбъект    - ОтчетОбъект                     - Объект отчета
//  МакетПараметры - ТабличныйДокумент               - Макет отчета
//	ОбластьФункций - ОбластьЯчеекТабличногоДокумента - Область функций
//
Процедура отПолучитьСтруктурыИзмеренийИПолейДляИсточникаДанных(ОтчетОбъект, МакетПараметры, ОбластьФункций) Экспорт
	
	ПараметрыИспользованияПолейИПоказателей = Новый Структура();
	
	ОтчетОбъект.ИзмеренияСтроки.Очистить();
	ОтчетОбъект.ИзмеренияКолонки.Очистить();
	ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей = Новый Структура();
	ОтчетОбъект.ПараметрыИзмеренийСтрок = Новый Структура();
	ОтчетОбъект.ДеревоДоступныхПолей = Новый ДеревоЗначений();
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Представление",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("ПолноеПредставление",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Имя",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("ПутьКДанным",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Измерение",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Итог",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Отбор",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("Порядок",);
	ОтчетОбъект.ДеревоДоступныхПолей.Колонки.Добавить("ТипЗначения",);
	
	ПараметрыИспользования	= ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей;
	ПараметрыИзмерений		= ОтчетОбъект.ПараметрыИзмеренийСтрок;
	ИзмеренияСтроки			= ОтчетОбъект.ИзмеренияСтроки;
	ИзмеренияКолонки		= ОтчетОбъект.ИзмеренияКолонки;
	ШаблонИсточникаДанных	= ОтчетОбъект.ШаблонИсточникаДанных;
	ДеревоДоступныхПолей 	= ОтчетОбъект.ДеревоДоступныхПолей;
	
	ТаблицаОтборов = Новый ТаблицаЗначений;
	ТаблицаОтборов.Колонки.Добавить("ПутьКДанным",);
	ТаблицаОтборов.Колонки.Добавить("Имя",);
	ТаблицаОтборов.Колонки.Добавить("Представление",);
	
	СвойстваПутиКДанным = Новый Структура();
	ОграниченияИзмерения = "";
	ОграниченияПоля = "";
	ОбластьИзмерений = МакетПараметры.ПолучитьОбласть("Измерения");
	Для к = 1 по ОбластьИзмерений.ВысотаТаблицы Цикл
		ОбластьПоля = ОбластьИзмерений.ПолучитьОбласть("R" + к);
		ОбластьФункцийПоля = ОбластьИзмерений.ПолучитьОбласть(к, 10, к, 9 + ОбластьФункций.ШиринаТаблицы);
		
		ПутьКДанным	  = ОбластьПоля.Область(1, 1).Текст;
		ПараметрыТипа = ОбластьПоля.Область(1, 7);
		ТипПоля = ?(ПараметрыТипа.СодержитЗначение, ПараметрыТипа.ТипЗначения, ПараметрыТипа.Текст);
		
		Если ОбластьПоля.Область(1, 1).Автоотступ = 0 и ОбластьПоля.Область(1, 1).Отступ = 0 Тогда
			ОграниченияИзмерения = ОбластьПоля.Область(1, 6).Текст;
			ИмяИзмерения = ПутьКДанным;
			ПутьКДаннымПоля = "";
			
			ПараметрыПоля = отПолучитьПараметрыИспользованияПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, ОбластьФункций);
			ПараметрыПоля.Вставить("Поля", Новый Структура());
			ПараметрыИспользования.Вставить(ИмяИзмерения, ПараметрыПоля);
			
			Если ТипПоля="Строка" ИЛИ СокрЛП(ТипПоля)="" Тогда
				ТипПоля = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0));
			ИначеЕсли ТипПоля="Число" Тогда
				ТипПоля = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 6));
			ИначеЕсли ТипПоля="Дата" Тогда
				ТипПоля = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
			ИначеЕсли ТипПоля="ДокументСсылка" Тогда
				ТипПоля = Новый ОписаниеТипов(Документы.ТипВсеСсылки());
			Иначе         
				ТипПоля = Новый ОписаниеТипов(ТипПоля);
			КонецЕсли;
			
			Если ШаблонИсточникаДанных.Колонки.Найти(ИмяИзмерения) = Неопределено Тогда
				ШаблонИсточникаДанных.Колонки.Добавить(ИмяИзмерения, ТипПоля, ПараметрыПоля["Представление"], ПараметрыПоля["ШиринаКолонки"]);
			КонецЕсли;
			НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
			НоваяСтрока.Имя = ИмяИзмерения;
			НоваяСтрока.Представление		= ПараметрыПоля["Представление"];
			НоваяСтрока.ПолноеПредставление = НоваяСтрока.Представление;
			НоваяСтрока.ПутьКДанным			= ИмяИзмерения;
			НоваяСтрока.Измерение	 		= (Найти(ОграниченияИзмерения, "Измерение")=0);
			НоваяСтрока.Отбор				= (Найти(ОграниченияИзмерения, "Отбор")=0);
			НоваяСтрока.Порядок				= (Найти(ОграниченияИзмерения, "Порядок")=0);
			НоваяСтрока.ТипЗначения			= ТипПоля;
			
			ИспользоватьСвойства	= ОбластьПоля.Область(1, 8).Текст;
			Если ВРег(СокрЛП(ИспользоватьСвойства)) <> "ЛОЖЬ" Тогда
				СвойстваПутиКДанным.Вставить(НоваяСтрока.Имя);
			КонецЕсли;
		Иначе	
			
			ОграниченияПоля = ОбластьПоля.Область(1, 6).Текст;
			ОграниченияПоля = ?(ПустаяСтрока(ОграниченияПоля), ОграниченияИзмерения, ОграниченияПоля);
			РодительПолей = ДеревоДоступныхПолей.Строки.Найти(ИмяИзмерения, "Имя");
			ТекПутьКДанным = ИмяИзмерения;
			Если РодительПолей=Неопределено Тогда Продолжить; КонецЕсли;
			
			СтруктураПолей = ПараметрыИспользования[ИмяИзмерения]["Поля"];
			Если Найти(ПутьКДанным, "СоставноеПредставление")>0 Тогда
				ТекстПредставления = ОбластьПоля.Область(1, 3).Текст;
				ПутьКПараметру = ?(ПутьКДанным="СоставноеПредставление", ИмяИзмерения, ИмяИзмерения+"."+СтрЗаменить(ПутьКДанным,".СоставноеПредставление",""));
				ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьКПараметру, ПараметрыИспользования);
				Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("СоставноеПредставление") Тогда
					ПараметрыИспользованияДляОсновногоПредставления.Вставить("СоставноеПредставление", Новый Структура("Представление, Поля", СтрЗаменить(ТекстПредставления,".",""), Новый Массив));
					ПараметрыПредставления = ПараметрыИспользованияДляОсновногоПредставления.СоставноеПредставление.Поля;
					ЧислоПолей = СтрЧислоВхождений(ТекстПредставления, "[");
					Для Индекс = 1 по ЧислоПолей Цикл
						ОткрПозиция = Найти(ТекстПредставления, "[");
						ЗакрПозиция = Найти(ТекстПредставления, "]");
						ЧастьПредставления = СокрЛП(Сред(ТекстПредставления, ОткрПозиция+1, ЗакрПозиция-ОткрПозиция-1));
						Если НЕ ИмяИзмерения = ЧастьПредставления Тогда
							ПараметрыПредставления.Добавить(ЧастьПредставления);
						КонецЕсли;
						ТекстПредставления = СокрЛП(Сред(ТекстПредставления, ЗакрПозиция+1));
					КонецЦикла;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			ПутьКДаннымПоля = ПутьКДанным;
			Для Итерация=1 по СтрЧислоВхождений(ПутьКДаннымПоля, ".") Цикл
				ПозРазделителя = Найти(ПутьКДаннымПоля, ".");
				Идентификатор = Лев(ПутьКДанным, ПозРазделителя - 1);
				
				ТекСтрока = РодительПолей.Строки.Найти(Идентификатор, "Имя");
				Если ТекСтрока = Неопределено Тогда
					ТекСтрока = РодительПолей.Строки.Добавить();
					ТекСтрока.Имя = Идентификатор;
				КонецЕсли;
				
				РодительПолей = ТекСтрока;
				
				Если НЕ СтруктураПолей.Свойство(Идентификатор) Тогда
					СтруктураПолей.Вставить(Идентификатор, Новый Структура());
				КонецЕсли;
				Если НЕ СтруктураПолей[Идентификатор].Свойство("Поля") Тогда
					СтруктураПолей[Идентификатор].Вставить("Поля", Новый Структура());
				КонецЕсли;
				СтруктураПолей = СтруктураПолей[Идентификатор].Поля;
				
				ПутьКДаннымПоля = Сред(ПутьКДаннымПоля, ПозРазделителя + 1);
			КонецЦикла;
			
			Если НЕ СтруктураПолей.Свойство(ПутьКДаннымПоля) Тогда
				СтруктураПолей.Вставить(ПутьКДаннымПоля, Новый Структура());
			КонецЕсли;
			
			ПараметрыПоля = отПолучитьПараметрыИспользованияПоля(ОтчетОбъект, ОбластьПоля, ОбластьФункцийПоля, ОбластьФункций);
			СтруктураПолей.Вставить(ПутьКДаннымПоля, ПараметрыПоля);
			
			НоваяСтрока = РодительПолей.Строки.Найти(ИмяИзмерения, "Имя");
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = РодительПолей.Строки.Добавить();
			КонецЕсли;
			
			Если ПутьКДаннымПоля="Родитель" Тогда
				ТипПоля = РодительПолей.ТипЗначения;
			Иначе
				МетаданныеРодителя = Метаданные.НайтиПоТипу(РодительПолей.ТипЗначения.Типы()[0]);
				Если МетаданныеРодителя<>Неопределено Тогда
					ТекПолеМетаданных = МетаданныеРодителя.Реквизиты.Найти(ПутьКДаннымПоля);
					Если ТекПолеМетаданных<>Неопределено Тогда
						ТипПоля = ТекПолеМетаданных.Тип;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипПоля="Строка" ИЛИ СокрЛП(ТипПоля)="" Тогда
				ТипПоля = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0));
			ИначеЕсли ТипПоля="Число" Тогда
				ТипПоля = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 6));
			ИначеЕсли ТипПоля="Дата" Тогда
				ТипПоля = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
			Иначе
				ТипПоля = Новый ОписаниеТипов(ТипПоля);
			КонецЕсли;
			
			Если ШаблонИсточникаДанных.Колонки.Найти(ИмяИзмерения+СтрЗаменить(ПутьКДанным, ".", ""))=Неопределено Тогда
				ШаблонИсточникаДанных.Колонки.Добавить(ИмяИзмерения+СтрЗаменить(ПутьКДанным, ".", ""), ТипПоля, ПараметрыПоля["Представление"], ПараметрыПоля["ШиринаКолонки"]);
			КонецЕсли;
			
			НоваяСтрока.Имя = ПутьКДаннымПоля;
			НоваяСтрока.Представление	= ПараметрыПоля["Представление"];
			НоваяСтрока.ПутьКДанным		= ИмяИзмерения+"."+ПутьКДанным;
			НоваяСтрока.Измерение 		= (Найти(ОграниченияПоля, "Измерение")=0);
			НоваяСтрока.Отбор			= (Найти(ОграниченияПоля, "Отбор")=0);
			НоваяСтрока.Порядок			= (Найти(ОграниченияПоля, "Порядок")=0);
			НоваяСтрока.ТипЗначения		= ТипПоля;
			
			РодительПоля = НоваяСтрока.Родитель.Родитель;
			ПредставлениеРодителя = НоваяСтрока.Родитель.Представление;
			Пока РодительПоля <> Неопределено Цикл
				ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
				РодительПоля = РодительПоля.Родитель;
			КонецЦикла;
			НоваяСтрока.ПолноеПредставление = НоваяСтрока.Представление+" ("+ПредставлениеРодителя+")";
			
			Если ПараметрыПоля.Свойство("Использование") Тогда
				
				Использование = ПараметрыПоля["Использование"];
				Если Использование.Свойство("Порядок") Тогда
					ИдентификаторПолей = СтрЗаменить(ИмяИзмерения, ".", "_");
					Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
						ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
					КонецЕсли;
					
					ТаблицаСортировки = Неопределено;
					Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Порядок") Тогда
						ТаблицаСортировки = ПараметрыИзмерений[ИдентификаторПолей]["Порядок"];
					КонецЕсли;
					Если ТипЗнч(ТаблицаСортировки) <> Тип("ТаблицаЗначений") Тогда
						ТаблицаСортировки = Новый ТаблицаЗначений();
						ТаблицаСортировки.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
						ТаблицаСортировки.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
						ТаблицаСортировки.Колонки.Добавить("Убывание",      Новый ОписаниеТипов("Булево"));
						ПараметрыИзмерений[ИдентификаторПолей].Вставить("Порядок", ТаблицаСортировки);
					КонецЕсли;
					
					ТекСтрока = ТаблицаСортировки.Добавить();
					ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
					ТекСтрока.Представление = НоваяСтрока.Представление;
					ТекСтрока.Убывание      = Использование["Порядок"] = "Убыв";
				КонецЕсли;
				
				Если Использование.Свойство("Поле") И ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
					Если Использование["Поле"] Тогда
						ИдентификаторПолей = СтрЗаменить(ИмяИзмерения, ".", "_");
						Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПолей) Тогда
							ПараметрыИзмерений.Вставить(ИдентификаторПолей, Новый Структура("Порядок,Поля", "", ""));
						КонецЕсли;
						
						ТаблицаПолей = Неопределено;
						Если ПараметрыИзмерений[ИдентификаторПолей].Свойство("Поля") Тогда
							ТаблицаПолей = ПараметрыИзмерений[ИдентификаторПолей]["Поля"];
						КонецЕсли;
						Если ТипЗнч(ТаблицаПолей) <> Тип("ТаблицаЗначений") Тогда
							ТаблицаПолей = Новый ТаблицаЗначений();
							ТаблицаПолей.Колонки.Добавить("ПутьКДанным",   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
							ТаблицаПолей.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
							ПараметрыИзмерений[ИдентификаторПолей].Вставить("Поля", ТаблицаПолей);
						КонецЕсли;
						
						ТекСтрока = ТаблицаПолей.Добавить();
						ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
						ТекСтрока.Представление = НоваяСтрока.Представление;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Найти(ОбластьПоля.Область(1, 3).Текст, "ОсновноеПредставление") > 0 Тогда
				ПараметрыИспользованияДляОсновногоПредставления = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(НоваяСтрока.Родитель.ПутьКДанным, ПараметрыИспользования);
				Если НЕ ПараметрыИспользованияДляОсновногоПредставления.Свойство("ОсновноеПредставление") Тогда
					ПараметрыИспользованияДляОсновногоПредставления.Вставить("ОсновноеПредставление", НоваяСтрока.ПутьКДанным);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыПоля.Свойство("Использование") Тогда
			Использование = ПараметрыПоля["Использование"];
			
			Если СокрЛП(ИмяИзмерения) = НоваяСтрока.ПутьКДанным Тогда
				Представление = НоваяСтрока.Представление;
			Иначе
				Представление = НоваяСтрока.Представление + " (" + НоваяСтрока.Родитель.Представление + ")";
			КонецЕсли;
			
			Попытка
				Если Использование["Строка"] Тогда
					ТекСтрока = ИзмеренияСтроки.Добавить();
					ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
					ТекСтрока.Представление = Представление;
					ТекСтрока.Использование = Использование.Активность;
					ТекСтрока.ТипИзмерения  = Использование["ТипИзмерения"];
				ИначеЕсли Использование["Колонка"] и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
					ТекСтрока = ИзмеренияКолонки.Добавить();
					ТекСтрока.ПутьКДанным   = НоваяСтрока.ПутьКДанным;
					ТекСтрока.Представление = Представление;
					ТекСтрока.Использование = Использование.Активность;
					ТекСтрока.ТипИзмерения  = Использование["ТипИзмерения"];
				КонецЕсли;
			Исключение КонецПопытки;
			
			Если Использование.Свойство("Фильтр") И Использование["Фильтр"] Тогда
				НовыйОтбор = ТаблицаОтборов.Добавить();
				НовыйОтбор.ПутьКДанным = НоваяСтрока.ПутьКДанным;
				НовыйОтбор.Имя = НоваяСтрока.Имя;
				НовыйОтбор.Представление = Представление;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПараметрыИспользования.Свойство("ОбщийИтог") Тогда
		ПараметрыИспользования.Вставить("ОбщийИтог", Новый Структура("Представление, Использование", "Итог", Новый Структура));
		
		РесурсыПоля = Новый Структура();
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				РесурсыПоля.Вставить(ТекПоказатель.Имя + ТекФункция.Имя, Истина);
			КонецЦикла;
		КонецЦикла;
		ПараметрыИспользования.ОбщийИтог.Использование.Вставить("Ресурсы", РесурсыПоля);
	КонецЕсли;
	
	// в конце добавим представления и форматирование общих полей
	Если НЕ ПараметрыИспользования.Свойство("ПериодГод") Тогда
		ПараметрыИспользования.Вставить("ПериодГод", Новый Структура("Представление, Формат", "По годам", "ДФ='гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодКвартал") Тогда
		ПараметрыИспользования.Вставить("ПериодКвартал", Новый Структура("Представление, Формат", "По кварталам", "ДФ='к ""квартал"" гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодМесяц") Тогда
		ПараметрыИспользования.Вставить("ПериодМесяц", Новый Структура("Представление, Формат", "По месяцам", "ДФ='ММММ гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодНеделя") Тогда
		ПараметрыИспользования.Вставить("ПериодНеделя", Новый Структура("Представление, Формат", "По неделям", "ДФ='""Неделя с"" дд.ММ.гггг ""г.""'"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодДень") Тогда
		ПараметрыИспользования.Вставить("ПериодДень", Новый Структура("Представление, Формат", "По дням", "ДФ=дд.ММ.гггг"));
	КонецЕсли;
	Если НЕ ПараметрыИспользования.Свойство("ПериодРегистратор") Тогда
		ПараметрыИспользования.Вставить("ПериодРегистратор", Новый Структура("Представление", "По документам движения"));
	КонецЕсли;
	
	Для Каждого ТекПараметр Из ПараметрыИзмерений Цикл
		СтрПредставленийПолей = "";
		Если ТекПараметр.Значение.Свойство("Поля") Тогда
			Если ТипЗнч(ТекПараметр.Значение.Поля) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекСтрока Из ТекПараметр.Значение.Поля Цикл
					СтрПредставленийПолей = СтрПредставленийПолей + ?(СтрПредставленийПолей = "", "", ", ") + ТекСтрока.Представление;
				КонецЦикла;
				ТекПараметр.Значение.Вставить("ПоляПредставление", СтрПредставленийПолей);
			КонецЕсли;
		КонецЕсли;
		
		СтрПредставленийПорядка = "";
		Если ТекПараметр.Значение.Свойство("Порядок") Тогда
			Если ТипЗнч(ТекПараметр.Значение.Порядок) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекСтрока Из ТекПараметр.Значение.Порядок Цикл
					СтрПредставленийПорядка = СтрПредставленийПорядка + ?(СтрПредставленийПорядка = "", "", ", ") + ТекСтрока.Представление + ?(ТекСтрока.Убывание, " (убыв)", "");
				КонецЦикла;
				
				ТекПараметр.Значение.Вставить("ПорядокПредставление", СтрПредставленийПорядка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей = ПараметрыИспользования;
	
	///////////////////////////////
	СтруктураСвязиСвойств  = Новый Структура();
	
	Если СвойстваПутиКДанным.Количество()>0 Тогда
		Запрос = Новый Запрос();
		Запрос.Текст = "
		|ВЫБРАТЬ
		|ВложеннаяТаблица.Свойство,
		|ВложеннаяТаблица.Назначение,
		|ВложеннаяТаблица.Назначение.ТипЗначения КАК ТипЗначенияНазначения,
		|ВЫБОР КОГДА ВложеннаяТаблица.Назначение.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ХАРАКТЕРИСТИКИ) ТОГДА
		|	ИСТИНА
		|ИНАЧЕ
		|	ЛОЖЬ
		|КОНЕЦ КАК ЭтоХарактеристика
		|ИЗ(
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначенияСвойств.Свойство КАК Свойство,
		|	ВЫБОР КОГДА НазначенияСвойств.Ссылка.БазовоеНазначение = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ПустаяСсылка) ТОГДА НазначенияСвойств.Ссылка
		|	ИНАЧЕ НазначенияСвойств.Ссылка.БазовоеНазначение
		|	КОНЕЦ КАК Назначение
		|ИЗ ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойств
		|ГДЕ 
		| НазначенияСвойств.Ссылка.ПометкаУдаления = Ложь
		| И НазначенияСвойств.Ссылка.ЭтоГруппа = Ложь) КАК ВложеннаяТаблица
		|УПОРЯДОЧИТЬ ПО ВложеннаяТаблица.Свойство.Наименование,
		|	ВложеннаяТаблица.Назначение.Наименование";
		
		// Строка запроса по всем существующим свойствам
		ИскатьСвойстваДляХарактеристики = ДеревоДоступныхПолей.Строки.Найти("ХарактеристикаНоменклатуры", "ПутьКДанным")<>Неопределено И СвойстваПутиКДанным.Свойство("ХарактеристикаНоменклатуры");
		
		НазначенияСвойств = Новый ТаблицаЗначений;
		НазначенияСвойств.Колонки.Добавить("Свойство");
		НазначенияСвойств.Колонки.Добавить("Измерение");
		НазначенияСвойств.Колонки.Добавить("ПутьКДаннымИзмерения");
		
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДаннымПоля");
		ТаблицаПолей.Колонки.Добавить("ИмяИзмерения");
		ТаблицаПолей.Колонки.Добавить("ПутьКДаннымИзмерения");
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");
		
		Для Каждого ТекВыбСвойство Из СвойстваПутиКДанным Цикл
			ТекПоле = ДеревоДоступныхПолей.Строки.Найти(ТекВыбСвойство.Ключ, "ПутьКДанным");
			Если ТекПоле <> Неопределено Тогда
				Если НЕ (ТекПоле.Измерение ИЛИ ТекПоле.Отбор) Тогда Продолжить; КонецЕсли;
				НовоеПоле = ТаблицаПолей.Добавить();
				НовоеПоле.ИмяИзмерения			= ТекПоле.Имя;
				НовоеПоле.ПутьКДаннымИзмерения	= ТекПоле.ПутьКДанным;
				НовоеПоле.ТипЗначения			= ТекПоле.ТипЗначения
			КонецЕсли;
		КонецЦикла;
		
		СвойстваВыборка = Запрос.Выполнить().Выбрать();
		Пока СвойстваВыборка.Следующий() Цикл
			Если НЕ СвойстваВыборка.ЭтоХарактеристика Тогда
				ТипНазначения = СвойстваВыборка.ТипЗначенияНазначения.Типы()[0];
				Для Каждого ТекПоле Из ТаблицаПолей Цикл
					Если ТекПоле.ТипЗначения.СодержитТип(ТипНазначения) Тогда
						ТекСтрока = НазначенияСвойств.Добавить();
						ТекСтрока.Свойство    = СвойстваВыборка.Свойство.Ссылка;
						ТекСтрока.Измерение   = ТекПоле.ИмяИзмерения;
						ТекСтрока.ПутьКДаннымИзмерения   = ТекПоле.ПутьКДаннымИзмерения;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ИскатьСвойстваДляХарактеристики Тогда
				ТекСтрока = НазначенияСвойств.Добавить();
				ТекСтрока.Свойство    = СвойстваВыборка.Свойство.Ссылка;
				ТекСтрока.Измерение   = "ХарактеристикаНоменклатуры";
				ТекСтрока.ПутьКДаннымИзмерения   = "ХарактеристикаНоменклатуры";
			КонецЕсли;
		КонецЦикла;
		
		ПредВидСвойства = "";
		НазначенияСвойств.Свернуть("Свойство,Измерение,ПутьКДаннымИзмерения", );
		НазначенияСвойств.Сортировать("Свойство, ПутьКДаннымИзмерения");
		Для Каждого ТекСтрока Из НазначенияСвойств Цикл
			НомерСвойства = НазначенияСвойств.Индекс(ТекСтрока) + 1;
			ИмяСвойства = "Свойство" + НомерСвойства;
			Если ТекСтрока.Свойство <> ПредВидСвойства Тогда
				ИмяПараметра = "ПараметрСвойства" + НомерСвойства;
				ПредВидСвойства = ТекСтрока.Свойство;
			КонецЕсли;
			
			ПараметрыСвойства = Новый Структура("ИмяИзмерения,ПутьКДаннымИзмерения,Свойство,Представление,ТипЗначения,ИмяПараметра",
			ТекСтрока.Измерение,ТекСтрока.ПутьКДаннымИзмерения, ТекСтрока.Свойство.Ссылка, ТекСтрока.Свойство.Наименование, ТекСтрока.Свойство.ТипЗначения, ИмяПараметра);
			СтруктураСвязиСвойств.Вставить(ИмяСвойства + "Значение", ПараметрыСвойства);
		КонецЦикла;
	КонецЕсли;
	ОтчетОбъект.СтруктураСвязиСвойств = СтруктураСвязиСвойств;
	
	Для Каждого ТекСвойство Из СтруктураСвязиСвойств Цикл
		ЗначениеСвойства = ТекСвойство.Значение;
		ВладелецСвойства = ДеревоДоступныхПолей.Строки.Найти(ЗначениеСвойства.ПутьКДаннымИзмерения,"ПутьКДанным",Истина);
		ПредставлениеСвойства = ЗначениеСвойства.Представление;
		
		ШаблонИсточникаДанных.Колонки.Добавить(ТекСвойство.Ключ, ЗначениеСвойства.Свойство.Метаданные().Тип, ПредставлениеСвойства, 20);
		
		ВетвьСвойств = ВладелецСвойства.Строки.Найти("Свойства", "Имя");
		Если ВетвьСвойств = Неопределено Тогда
			ВетвьСвойств = ВладелецСвойства.Строки.Добавить();
			ВетвьСвойств.Имя           = "Свойства";
			ВетвьСвойств.Представление = "Свойства";
		КонецЕсли;
		
		ДоступноеПоле = ВетвьСвойств.Строки.Добавить();
		ДоступноеПоле.Имя           = ТекСвойство.Ключ;
		ДоступноеПоле.ПутьКДанным   = ТекСвойство.Ключ;
		ДоступноеПоле.Представление = ПредставлениеСвойства;
		ДоступноеПоле.Измерение     = Истина;
		ДоступноеПоле.Отбор         = Истина;
		ДоступноеПоле.Порядок       = Ложь;
		ДоступноеПоле.ТипЗначения   = ЗначениеСвойства.ТипЗначения;
		
		РодительПоля = ВетвьСвойств.Родитель.Родитель;
		ПредставлениеРодителя = ВетвьСвойств.Родитель.Представление;
		Пока РодительПоля <> Неопределено Цикл
			ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
			РодительПоля = РодительПоля.Родитель;
		КонецЦикла;
		ДоступноеПоле.ПолноеПредставление = ПредставлениеСвойства+" ("+ПредставлениеРодителя+")";
	КонецЦикла;
	
	Построитель = ОтчетОбъект.ПостроительОтчета;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ШаблонИсточникаДанных);
	
	Для Каждого ТекОтбор Из ТаблицаОтборов Цикл
		Построитель.Отбор.Добавить(ТекОтбор.ПутьКДанным, ТекОтбор.Имя, ТекОтбор.Представление);
	КонецЦикла;
	
КонецПроцедуры	//	отПолучитьСтруктурыИзмеренийИПолейДляИсточникаДанных()

// Функция проверки корректности заполненных данных реквизитов отчета перед формированием
// Проверяется период отчета, и заполнение и соответствие измерений отчета при использовании построителя
// ОтчетОбъект должен содержать следующие проверяемые реквизиты :
// - ДатаНачала: Дата
// - ДатаКонца: Дата
// - ПостроительОтчета (может отсутствовать)
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
// Возвращаемое значение:
//	Булево - Истина, если все корректно заполнено, иначе Ложь
//		   Есть ошибки (выдаваемые в виде предупреждений)
//
Функция отКорректностьЗаполнения(ОтчетОбъект, ПостроительОтчета=Неопределено) Экспорт
	
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Обороты ИЛИ
		ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты ИЛИ
		ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		Если ОтчетОбъект.ДатаНачала > ОтчетОбъект.ДатаКонца И ОтчетОбъект.ДатаКонца <> '00010101000000' Тогда
			Предупреждение("Дата начала периода не может быть больше даты конца периода", 60);
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ОтчетОбъект.ВидОтчета <> Перечисления.ВидыОтчетов.Прочий Тогда
		Попытка 
			ПоследовательностиОтчета = ОтчетОбъект.ПоследовательностиОтчета;
			Если обПраво("ПредупреждатьОНарушенииПоследовательностей") Тогда
				
				ОбработкаВосстановлениеПоследовательностей = Обработки.ВосстановлениеПоследовательностей.Создать();
				
				Если ТипЗнч(ПоследовательностиОтчета) = Тип("Структура") Тогда
					ТаблицаПоследовательностей = ОбработкаВосстановлениеПоследовательностей.ТаблицаПоследовательностей;
					КоличествоПоследовательностей = ТаблицаПоследовательностей.Количество()-1;
					
					Пока КоличествоПоследовательностей>=0 Цикл
						ТекПоследовательность = ТаблицаПоследовательностей[КоличествоПоследовательностей];
						Если Не ПоследовательностиОтчета.Свойство(ТекПоследовательность.ИмяГраницы) Тогда
							ТаблицаПоследовательностей.Удалить(ТекПоследовательность);
						КонецЕсли;
						КоличествоПоследовательностей = КоличествоПоследовательностей-1;
					КонецЦикла;
				Иначе
					ОбработкаВосстановлениеПоследовательностей.ТаблицаПоследовательностей = ПоследовательностиОтчета.Скопировать();
					ТаблицаПоследовательностей = ОбработкаВосстановлениеПоследовательностей.ТаблицаПоследовательностей;
				КонецЕсли;
				
				Если ТаблицаПоследовательностей.Количество()>0 Тогда
					ТаблицаГраниц = ОбработкаВосстановлениеПоследовательностей.ПолучитьТаблицуНеактуальныхДокументов(ОтчетОбъект.ДатаКонца);
					ТекстПредупреждения = "";
					Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
						Граница = ТаблицаГраниц.Найти(ТекПоследовательность.ИмяГраницы,"ИмяПоследовательности");
						Если Граница <> Неопределено Тогда
							ТекстАктуальна = "";
							Если Граница.Граница=Неопределено Тогда
								ТекстАктуальна	= Формат(Граница.МоментВремени,"ДФ=dd.MM.yyyy");
							Иначе
								ТекстАктуальна	= Формат(Граница.Граница.Дата,"ДФ=dd.MM.yyyy")+" - "+Граница.Граница.Метаданные().Синоним+" № "+СокрЛП(Граница.Граница.Номер);
							КонецЕсли;
							ТекстПредупреждения = ТекстПредупреждения+"
							| Последовательность: <" + ТекПоследовательность.Представление + "> актуальна " + ТекстАктуальна;
						КонецЕсли;
					КонецЦикла;
					Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
						Предупреждение("Последовательности разрушены и возможны некорректные результаты отчетов!"+ТекстПредупреждения);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Попытка
		ПродолжитьФормирование = Истина;
		ПрерватьПроверку = ОтчетОбъект.КорректностьЗаполнения(ПродолжитьФормирование);
		Если ПрерватьПроверку ИЛИ Не ПродолжитьФормирование Тогда
			Возврат ПродолжитьФормирование;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка	// А вдруг отчет реализован не на построителе?
		Если ПостроительОтчета=Неопределено Тогда
			ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
		КонецЕсли;
		КоличествоИзмеренийСтроки  = ПостроительОтчета.ИзмеренияСтроки.Количество()-1;
		КоличествоИзмеренийКолонки = ПостроительОтчета.ИзмеренияКолонки.Количество();
		
		// Проверка совпадения измерений строк и колонок
		//Для Каждого ТекКолонка Из 
		Для Инд=0 По ПостроительОтчета.ИзмеренияКолонки.Количество()-1  Цикл
			Для Инд2=0 По ПостроительОтчета.ИзмеренияСтроки.Количество()-1  Цикл
				Если ПостроительОтчета.ИзмеренияКолонки[Инд].ПутьКДанным = ПостроительОтчета.ИзмеренияСтроки[Инд2].ПутьКДанным Тогда
					Предупреждение("Повторяющаяся группировка " + ПостроительОтчета.ИзмеренияКолонки[Инд].Представление +"."+ Символы.ПС+
					"Нельзя использовать одинаковые поля группировки в строках и в колонках!", 30);
					Возврат Ложь;
				КонецЕсли; 
			КонецЦикла;
		КонецЦикла;
		
		// проверим нет ли повторяющихся измерений и откорректируем путь к данным после автогенерации
		// имени в случае выбора одинаковых измерений стоки и колонки
		
		// ...измерения строки
		СписокЗначений = Новый СписокЗначений;
		Для Инд=0 По ПостроительОтчета.ИзмеренияСтроки.Количество()-1  Цикл
			Если СписокЗначений.НайтиПоЗначению(ПостроительОтчета.ИзмеренияСтроки[Инд].ПутьКДанным) = Неопределено Тогда
				СписокЗначений.Добавить(ПостроительОтчета.ИзмеренияСтроки[Инд].ПутьКДанным);
			Иначе
				Предупреждение("Повторяющаяся группировка " + ПостроительОтчета.ИзмеренияСтроки[Инд].Представление +"."+ Символы.ПС+
				"Нельзя использовать одинаковые поля группировки строк!", 30);
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		//.....измерения колонки
		СписокЗначений = Новый СписокЗначений;
		Для Инд=0 По ПостроительОтчета.ИзмеренияКолонки.Количество()-1  Цикл
			Если СписокЗначений.НайтиПоЗначению(ПостроительОтчета.ИзмеренияКолонки[Инд].ПутьКДанным) = Неопределено Тогда
				СписокЗначений.Добавить(ПостроительОтчета.ИзмеренияКолонки[Инд].ПутьКДанным);
			Иначе
				Предупреждение("Повторяющаяся группировка " + ПостроительОтчета.ИзмеренияКолонки[Инд].Представление +"."+ Символы.ПС+
				"Нельзя использовать одинаковые поля группировки колонок!", 30);
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		// для разворота оборотного отчета по горизонтали недоуказывать только одну функцию
		Если КоличествоИзмеренийКолонки>0 Тогда
			КолФункций=0;
			Для Каждого Функ Из ОтчетОбъект.Функции Цикл
				КолФункций=КолФункций+Число(Функ.Использование);
			КонецЦикла;
			Если КолФункций>1 Тогда
				//Предупреждение("При указанных группировках колонок можно использовать
				//               |только одну функцию !", 30);
				//Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Исключение	// Если отчет не использует построитель
	КонецПопытки;
	
	// если в отчете есть таблица показателей то проверим выбран ли хоть один из них
	Попытка
		Если ОтчетОбъект.ДеревоПоказателей.Строки.Найти(1, "Использование")=Неопределено И ОтчетОбъект.ДеревоПоказателей.Строки.Найти(2, "Использование")=Неопределено Тогда
			Предупреждение("Не выбрано не одной функции и ни одного показателя!", 10); Возврат Ложь;
		КонецЕсли;
	Исключение	
		Попытка
			Если ОтчетОбъект.Показатели.НайтиСтроки(Новый Структура("Использование",Истина)).Количество()=0 Тогда
				Предупреждение("Не выбрано ни одного показателя !", 10); Возврат Ложь;
			КонецЕсли;
		Исключение	// в отчете нет показателей
		КонецПопытки;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции // отКорректностьЗаполнения()

// процедура убирает неиспользуемые показатели из текста запроса
// для дальнейшей разработки
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект       - Объект отчета
//  Построитель - ПостроительОтчета - Построитель отчета
//
Процедура отУбратьНеИспользуемыеПоказателиИзЗапроса(ОтчетОбъект, Построитель = Неопределено) Экспорт
	
	Если Построитель = Неопределено Тогда
		Построитель = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	
	Попытка    ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки;
	Исключение ИзмеренияСтроки = Построитель.ИзмеренияСтроки;
	КонецПопытки;	
	Попытка    ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки;
	Исключение ИзмеренияКолонки = Построитель.ИзмеренияКолонки;
	КонецПопытки;	
	
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение КонецПопытки;
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	Попытка    ПараметрыИзмерений = ОтчетОбъект.ПараметрыИзмеренийСтрок;
	Исключение ПараметрыИзмерений = Новый Структура();
	КонецПопытки;
	
	//сформируем списки используемых и неиспользуемых пар Показатель+Функция
	списИспПФ   = Новый СписокЗначений;
	списНеиспПФ = Новый СписокЗначений;
	Для Каждого СтрФункция Из ОтчетОбъект.Функции Цикл
		Для Каждого Показатель Из ОтчетОбъект.Показатели Цикл
			Если СтрФункция.Использование И Показатель.Использование Тогда 
				списИспПФ.Добавить(СокрЛП(Показатель.Имя + СтрФункция.Имя));
			Иначе
				списНеиспПФ.Добавить(СокрЛП(Показатель.Имя + СтрФункция.Имя));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Попытка
		МакетТекстаЗапроса = ОтчетОбъект.ПолучитьТекстЗапроса();
	Исключение
		МакетТекстаЗапроса = ОтчетОбъект.ТекстЗапроса;
	КонецПопытки;
		
	ИспользуемыеСвойства = Новый Структура();
	
	Для Каждого ТекИзмерение Из ИзмеренияСтроки Цикл
		Если НЕ ТекИзмерение.Использование Тогда Продолжить; КонецЕсли;
		
		Путь = СтрЗаменить(ТекИзмерение.ПутьКДанным, ".", "_");
		
		Если СтруктураСвязиСвойств.Свойство(Путь) Тогда
			Если НЕ ИспользуемыеСвойства.Свойство(Путь) Тогда
				ИспользуемыеСвойства.Вставить(Путь);
				Построитель.Параметры.Вставить(СтруктураСвязиСвойств[Путь].ИмяПараметра, СтруктураСвязиСвойств[Путь].Свойство);
			КонецЕсли;
			
		ИначеЕсли ПараметрыИзмерений.Свойство(Путь) Тогда
			Если ПараметрыИзмерений[Путь].Свойство("Поля") Тогда
				ДопПоля = ПараметрыИзмерений[Путь].Поля;
				Если ТипЗнч(ДопПоля) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекСтрока Из ДопПоля Цикл
						Путь = ТекСтрока.ПутьКДанным;
						
						Попытка
							Если СтруктураСвязиСвойств.Свойство(Путь) Тогда
								Если НЕ ИспользуемыеСвойства.Свойство(Путь) Тогда
									ИспользуемыеСвойства.Вставить(Путь);
									Построитель.Параметры.Вставить(СтруктураСвязиСвойств[Путь].ИмяПараметра, СтруктураСвязиСвойств[Путь].Свойство);
								КонецЕсли;
							КонецЕсли;
						Исключение КонецПопытки;	
					КонецЦикла;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекИзмерение Из ИзмеренияКолонки Цикл
		Если НЕ ТекИзмерение.Использование Тогда Продолжить; КонецЕсли;
		
		Путь = ТекИзмерение.ПутьКДанным;
		
		Попытка
			Если СтруктураСвязиСвойств.Свойство(Путь) Тогда
				Если НЕ ИспользуемыеСвойства.Свойство(Путь) Тогда
					ИспользуемыеСвойства.Вставить(Путь);
					Построитель.Параметры.Вставить(СтруктураСвязиСвойств[Путь].ИмяПараметра, СтруктураСвязиСвойств[Путь].Свойство);
				КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;
	КонецЦикла;
	
	Для Каждого ТекОтбор Из ОтчетОбъект.ПостроительОтчета.Отбор Цикл
		Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		
		Путь = ТекОтбор.ПутьКДанным;
		
		Попытка
			Если СтруктураСвязиСвойств.Свойство(Путь) Тогда
				Если НЕ ИспользуемыеСвойства.Свойство(Путь) Тогда
					ИспользуемыеСвойства.Вставить(Путь);
					Построитель.Параметры.Вставить(СтруктураСвязиСвойств[Путь].ИмяПараметра, СтруктураСвязиСвойств[Путь].Свойство);
				КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;
	КонецЦикла;
	
	// удаляем ненужные пары ПФ из текста запроса
	ТекстЗапроса = Новый ТекстовыйДокумент();
	ТекстЗапроса.УстановитьТекст(СокрЛП(МакетТекстаЗапроса));
	
	// удалим для начала пустые строки, т.к иначе возможен некорректный синтаксис текста запроса
	// после удаления неиспользуемых показателей
     // за счет наличия лишних запятых
     КолСтрок = ТекстЗапроса.КоличествоСтрок()-1;
     Пока КолСтрок>0 Цикл
          //текущая строка текста запроса
          ТекСтрока = ТекстЗапроса.ПолучитьСтроку(КолСтрок);
          // если пустая то удаляем ее
          Если ПустаяСтрока(ТекСтрока) Тогда
               ТекстЗапроса.УдалитьСтроку(КолСтрок);
          КонецЕсли;     
          КолСтрок= КолСтрок-1;
     КонецЦикла;     
	
	//забьем в табличку концы блоков(флЗапятая установим, когда в конце блока есть запятая)
	тблБлоки = Новый ТаблицаЗначений;
	тблБлоки.Колонки.Добавить("НачБлока");
	тблБлоки.Колонки.Добавить("КонБлока");
	тблБлоки.Колонки.Добавить("флЗапятая");
	// сформируем новый текст запроса
	КонецНовогоБлока = 1;
	Для НомСтр = 2 По ТекстЗапроса.КоличествоСтрок()-1 Цикл
		//текущая строка ТЗ
		ТекСтрока = СокрЛП(ТекстЗапроса.ПолучитьСтроку(НомСтр));
		
		Если Найти(ТекСтрока, "//СВОЙСТВО") > 0 Тогда
			//строка со свойствами
			Поз = Найти(ТекСтрока, "Свойство");
			Если Поз > 0 Тогда
				ТекСтрока = СтрЗаменить(Сред(ТекСтрока, Поз + 8), "Значение)	//СВОЙСТВО", ".");
				Если Найти(ТекСтрока, "&ПараметрСвойства") > 0 Тогда
					Поз = Найти(ТекСтрока, " ");
				Иначе Поз = Найти(ТекСтрока, ".");
				КонецЕсли;
				
				Если Поз > 0 Тогда
					Если НЕ ИспользуемыеСвойства.Свойство("Свойство" + Лев(ТекСтрока, Поз - 1) + "Значение") Тогда
						ТекстЗапроса.ЗаменитьСтроку(НомСтр, "");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Продолжить;
			
		ИначеЕсли Найти(ТекСтрока, "//ПОКАЗАТЕЛЬ") = 0 Тогда
			//строка без показателя -> на след. строчку
			Продолжить;
		КонецЕсли;
		
		ПредСтрока = СокрЛП(ТекстЗапроса.ПолучитьСтроку(НомСтр - 1));
		СледСтрока = СокрЛП(ТекстЗапроса.ПолучитьСтроку(НомСтр + 1));
		
		//начало блока
		Если Найти(ТекСтрока, "//ПОКАЗАТЕЛЬ") <> 0 И Найти(ПредСтрока, "//ПОКАЗАТЕЛЬ") = 0 И КонецНовогоБлока <> Неопределено Тогда
			НовыйБлок = тблБлоки.Добавить();
			НовыйБлок.НачБлока = НомСтр-1;
			КонецНовогоБлока = Неопределено;
		КонецЕсли;
		//конец блока
		Если Найти(ТекСтрока, "//ПОКАЗАТЕЛЬ") <> 0 И Найти(СледСтрока, "//ПОКАЗАТЕЛЬ") = 0 Тогда
			НовыйБлок.КонБлока = НомСтр;
			Если Найти(ТекСтрока, ",") <> 0 Тогда
				КолВхождений = СтрЧислоВхождений(ТекСтрока, ")");
				Если КолВхождений = 0 Тогда
					НовыйБлок.флЗапятая = Истина;
				Иначе
					ТекВхождение = 0;
					ПравЧасть = ТекСтрока;
					Пока ТекВхождение < КолВхождений Цикл
						Позиция = Найти(ПравЧасть, ")");
						ПравЧасть = Сред(ПравЧасть, Позиция+1);
						ТекВхождение=ТекВхождение+1;
					КонецЦикла;
					НовыйБлок.флЗапятая = (Найти(ПравЧасть, ",")<>0);
				КонецЕсли;
			Иначе
				НовыйБлок.флЗапятая = Ложь;
			КонецЕсли;
			
			КонецНовогоБлока = НомСтр;
		КонецЕсли;
		
		//удалим ненужные строчки
		
		Если Найти(ТекСтрока, "//ПОКАЗАТЕЛЬ_") = 0 Тогда
			ПозицияПсевдонима = Найти(ТекСтрока, " КАК ")+3;
			
			//2 случая - выбираемые поля(там есть псевдонимы, то есть слово КАК) и остальные(там нет псевдонима)
			Если ПозицияПсевдонима = 3 Тогда
				СтрокаПоиска = ТекСтрока;
			Иначе
				СтрокаПоиска = Прав(ТекСтрока, СтрДлина(ТекСтрока) - ПозицияПсевдонима);
			КонецЕсли;
			СтрокаПоиска = "."+СтрЗаменить( СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрокаПоиска," ","."),Символы.Таб,"." ),",","."),"(","."),")",".")+".";
			
			Для Каждого ТекПФ Из списНеиспПФ Цикл
				Если Найти(СтрокаПоиска, "."+ТекПФ.Значение+".") <> 0 Тогда
					ТекстЗапроса.ЗаменитьСтроку(НомСтр, "");
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	// подкорректируем новый текст запроса (лишние запятые)
	Для каждого ТекБлок Из тблБлоки Цикл
		//если в последней строчке блока есть запятая, то блок не трогаем
		Если ТекБлок.флЗапятая Тогда
			Продолжить;
		КонецЕсли;
		
		//идем с конца блока
		НомСтр = ТекБлок.КонБлока + 1;
		Пока НомСтр <> ТекБлок.НачБлока Цикл
			НомСтр = НомСтр - 1;
			ТекСтрока = СокрЛП(ТекстЗапроса.ПолучитьСтроку(НомСтр));
			//пустую строку пропускаем
			Если ТекСтрока = "" Тогда
				Продолжить;
			КонецЕсли;
			Если Найти(ТекСтрока, ",") <> 0 Тогда
				КолВхождений = СтрЧислоВхождений(ТекСтрока, ")");
				Если КолВхождений = 0 Тогда
					ТекСтрока = СтрЗаменить(ТекСтрока, ",", "");
				Иначе
					ТекВхождение = 0;
					ПравЧасть = ТекСтрока;
					Пока ТекВхождение < КолВхождений Цикл
						Позиция = Найти(ПравЧасть, ")");
						ПравЧасть = Сред(ПравЧасть, Позиция+1);
						ТекВхождение=ТекВхождение+1;
					КонецЦикла;
					Позиция = Найти(ТекСтрока, ПравЧасть);
					ЛевЧасть = Лев(ТекСтрока,Позиция-1);
					ТекСтрока = ЛевЧасть+СтрЗаменить(ПравЧасть, ",", "");
				КонецЕсли;
				ТекстЗапроса.ЗаменитьСтроку(НомСтр, ТекСтрока);
			КонецЕсли;
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	Попытка
		АвтозаполнениеПолей = ОтчетОбъект.АвтозаполнениеПолей;
		СоздатьПостроительОтчета(Построитель, ОтчетОбъект.ПостроительОтчета, ТекстЗапроса.ПолучитьТекст(), АвтозаполнениеПолей);
	Исключение
		СоздатьПостроительОтчета(Построитель, ОтчетОбъект.ПостроительОтчета, ТекстЗапроса.ПолучитьТекст());
	КонецПопытки;
	
КонецПроцедуры	//	отУбратьНеИспользуемыеПоказателиИзЗапроса()

// Процедура устанавливает период построителя отчета по реквизитам отчета
// Проверяется вид отчета, и заполняются соответствующие параметры запроса построителя
// ОтчетОбъект должен содержать следующие используемые реквизиты :
// - ДатаНачала: Дата
// - ДатаКонца: Дата
// - ВидОтчета: Перечисления.ВидыОтчетов (Остатки, Обороты, ОстаткиИОбороты)
// - ПостроительОтчета
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
Процедура отУстановитьПериодОтчета(ОтчетОбъект, ПостроительОтчета = Неопределено) Экспорт
	
	ДатаКонДляОстатка = Новый Граница(КонецДня(ОтчетОбъект.ДатаКонца), ВидГраницы.Включая);
	
	Если ПостроительОтчета = Неопределено Тогда
		ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	ПостроительОтчета.Параметры.Вставить("ДатаНач", НачалоДня(ОтчетОбъект.ДатаНачала));
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Остатки Тогда
		// Для остатков основной параметр - "НаДату"
		Если ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
			ПостроительОтчета.Параметры.Вставить("НаДату", '00010101000000');
			ПостроительОтчета.Параметры.Вставить("ДатаКонДляОстатка", '00010101000000');
		Иначе 
			ПостроительОтчета.Параметры.Вставить("НаДату", ДатаКонДляОстатка);
			ПостроительОтчета.Параметры.Вставить("ДатаКонДляОстатка", ДатаКонДляОстатка);
		КонецЕсли;
		
	Иначе
		// Для оборотов, в т.ч. с остатками используются параметры интервала "ДатаНач" и "ДатаКон"
		Если ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
			ПостроительОтчета.Параметры.Вставить("ДатаКон", '00010101000000');
			ПостроительОтчета.Параметры.Вставить("ДатаКонДляОстатка", '00010101000000');
		Иначе
			ПостроительОтчета.Параметры.Вставить("ДатаКон", КонецДня(ОтчетОбъект.ДатаКонца));
			ПостроительОтчета.Параметры.Вставить("ДатаКонДляОстатка", ДатаКонДляОстатка);
		КонецЕсли;
	КонецЕсли;
	
	ПостроительОтчета.Параметры.Вставить("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	ПостроительОтчета.Параметры.Вставить("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	
КонецПроцедуры	//	отУстановитьПериодОтчета()

// Процедура устанавливает параметры измерения по умолчанию
//
// Параметры:
//	ОтчетФорма - Форма                - Форма отчета
//  ТекСтрока  - СтрокаТабличнойЧасти - Строка табличной части
//
Процедура отУстановитьПараметрыИзмеренияПоУмолчанию(ОтчетФорма, ТекСтрока)
	
	Измерение = ОтчетФорма.ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным, "ПутьКДанным", Истина);
	Если Измерение <> Неопределено Тогда
		ПорядокПоУмолчанию = "";
		
		// Установим значение типа измерения по умолчанию
		ТипИзмерения = Измерение.ТипЗначения;
		Если ТипИзмерения.Типы().Количество()=0 Тогда
			ТекСтрока.ТипИзмерения = "Элементы";
		ИначеЕсли НЕ Справочники.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) и
			НЕ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) Тогда
			ТекСтрока.ТипИзмерения = "Элементы";
			
			Если Документы.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) Тогда
				ПорядокПоУмолчанию = "Дата";
			КонецЕсли;
		Иначе
			Если Измерение.Родитель <> Неопределено Тогда
				Если Измерение.Родитель.Имя = "Свойства" Тогда
					ТекСтрока.ТипИзмерения = "Элементы";
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			ТекОбъект = Новый (ТипИзмерения.Типы()[0]);
			Если НЕ ТекОбъект.Метаданные().Иерархический Тогда
				ТекСтрока.ТипИзмерения = "Элементы";
			КонецЕсли;
			
			Если Строка(ТекОбъект.Метаданные().ОсновноеПредставление) = "ВВидеКода" и ТекОбъект.Метаданные().ДлинаКода > 0 Тогда
				ПорядокПоУмолчанию = "Код";
			ИначеЕсли ТекОбъект.Метаданные().ДлинаНаименования > 0 Тогда
				ПорядокПоУмолчанию = "Наименование";
			КонецЕсли;
		КонецЕсли;
		
		// Установим сортировку по умолчанию
		Если НЕ ПустаяСтрока(ПорядокПоУмолчанию) Тогда
			тзСортировки = "";
			ИдентификаторПути  = СтрЗаменить(Измерение.ПутьКДанным, ".", "_");
			ПараметрыИзмерений = ОтчетФорма.ПараметрыИзмеренийСтрок;
			Если ПараметрыИзмерений.Свойство(ИдентификаторПути) Тогда
				тзСортировки = ПараметрыИзмерений[ИдентификаторПути].Порядок;
			КонецЕсли;
			
			Если ТипЗнч(тзСортировки) <> Тип("ТаблицаЗначений") Тогда
				тзСортировки = Новый ТаблицаЗначений();
				тзСортировки.Колонки.Добавить("ПутьКДанным");
				тзСортировки.Колонки.Добавить("Представление");
				тзСортировки.Колонки.Добавить("Убывание", Новый ОписаниеТипов("Булево"));
			КонецЕсли;
			
			Если тзСортировки.Количество() = 0 Тогда
				ПутьКДаннымПорядка = Измерение.ПутьКДанным + "." + ПорядокПоУмолчанию;
				Если Измерение.Строки.Найти(ПутьКДаннымПорядка, "ПутьКДанным", Истина)<>Неопределено Тогда
					ТекСтрока = тзСортировки.Добавить();
					ТекСтрока.ПутьКДанным   = ПутьКДаннымПорядка;
					ТекСтрока.Представление = ПорядокПоУмолчанию;
					ТекСтрока.Убывание      = Ложь;
					
					Если НЕ ПараметрыИзмерений.Свойство(ИдентификаторПути) Тогда
						ПараметрыИзмерений.Вставить(ИдентификаторПути, Новый Структура("Порядок,ПорядокПредставление", тзСортировки, ПорядокПоУмолчанию));
					Иначе
						ПараметрыИзмерений[ИдентификаторПути].Вставить("Порядок",              тзСортировки);
						ПараметрыИзмерений[ИдентификаторПути].Вставить("ПорядокПредставление", ПорядокПоУмолчанию);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отУстановитьПараметрыИзмеренияПоУмолчанию()

// Процедура заполняет выбранные поля и порядок сортировки измерений построителя отчета
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект       - Объект отчета
//	Построитель - ПостроительОтчета - Построитель отчета
//
Процедура отУстановитьИспользованиеИзмерений(ОтчетОбъект, Построитель = Неопределено) Экспорт
	
	Попытка ПараметрыИзмерений = ОтчетОбъект.ПараметрыИзмеренийСтрок;
	Исключение Возврат; КонецПопытки;
	
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение КонецПопытки;	
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	Попытка
		СтруктураНеСвязанныхПоказателей = ОтчетОбъект.СтруктураНеСвязанныхПоказателей;
	Исключение КонецПопытки;
	Если ТипЗнч(СтруктураНеСвязанныхПоказателей) <> Тип("Структура") Тогда
		СтруктураНеСвязанныхПоказателей = Новый Структура();
	КонецЕсли;
	
	Если Построитель = Неопределено Тогда
		Построитель = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	Построитель.ИзмеренияСтроки.Очистить();
	Построитель.ИзмеренияКолонки.Очистить();
	Построитель.Порядок.Очистить();
	ОтчетОбъект.ДополнительныеПоляОтчета.Очистить();
	
	Если Не Построитель.ВыводитьДетальныеЗаписи Тогда
		Построитель.ВыбранныеПоля.Очистить();
	КонецЕсли;
	
	Попытка 
		Если ОтчетОбъект.УстановитьИспользованиеИзмерений(Построитель) Тогда
			Возврат;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка ДеревоДоступныхПолей = ОтчетОбъект.ДеревоДоступныхПолей;
	Исключение Возврат; КонецПопытки;
	
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если Не ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			Если Не ТекГруппа.Группа Тогда
				Если Построитель.ДоступныеПоля.Найти(ТекГруппа.ПутьКДанным) <> Неопределено Тогда
					Построитель.ВыбранныеПоля.Добавить(ТекГруппа.ПутьКДанным);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если Не ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				Если Построитель.ДоступныеПоля.Найти(ТекСтрока.ПутьКДанным) <> Неопределено Тогда
					Построитель.ВыбранныеПоля.Добавить(ТекСтрока.ПутьКДанным);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	Исключение
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Если Не ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				Если Не ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				ИмяРесурса = ТекПоказатель.Имя+ТекФункция.Имя;
				Если Построитель.ДоступныеПоля.Найти(ИмяРесурса) <> Неопределено Тогда
					Построитель.ВыбранныеПоля.Добавить(ИмяРесурса);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецПопытки;

	Попытка 
		СтруктураСортировкиПоля = ОтчетОбъект.СтруктураСортировки();
		Если ТипЗнч(СтруктураСортировкиПоля) = Тип("СписокЗначений") Тогда
			Для Каждого ТекПорядок Из СтруктураСортировкиПоля Цикл
				Построитель.Порядок.Добавить(ТекПорядок.Представление,,,ТекПорядок.Значение);
			КонецЦикла;
		Иначе
			Построитель.Порядок.Добавить(СтруктураСортировкиПоля.Описание,,,СтруктураСортировкиПоля.НаправлениеСортировки);
		КонецЕсли;
	Исключение КонецПопытки;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияСтроки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		
		ПорядокУстановлен = Ложь;
		
		Тип = ТекСтрока.ТипИзмерения;
		Тип = ?(Тип = "Только иерархия", ТипИзмеренияПостроителяОтчета.ТолькоИерархия, ?(Тип = "Иерархия", ТипИзмеренияПостроителяОтчета.Иерархия, ТипИзмеренияПостроителяОтчета.Элементы));
		
		Попытка
			ТекИзмерение = Построитель.ИзмеренияСтроки.Добавить(ТекСтрока.ПутьКДанным);
		Исключение
			ТекИзмерение = Построитель.ИзмеренияСтроки.Добавить(СтрЗаменить(ТекСтрока.ПутьКДанным,".",""));
		КонецПопытки;
		
		ТекИзмерение.ТипИзмерения  = Тип;
		ТекИзмерение.Представление = ТекСтрока.Представление;
		
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
				Построитель.ВыбранныеПоля.Добавить(ПутьОсновногоПредставления);
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ПоляСоставногоПредставления = ТекПараметрыИзмерения.СоставноеПредставление.Поля;
				Для Каждого ПолеПредставления Из ПоляСоставногоПредставления Цикл
					Построитель.ВыбранныеПоля.Добавить(ПолеПредставления);
				КонецЦикла;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ИдентификаторПоля = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "_");
		Если ПараметрыИзмерений.Свойство(ИдентификаторПоля) Тогда
			Если ПараметрыИзмерений[ИдентификаторПоля].Свойство("Порядок") и
				НЕ СтруктураСвязиСвойств.Свойство(ИдентификаторПоля) Тогда
				ПоляСортировки = ПараметрыИзмерений[ИдентификаторПоля].Порядок;
				Если ТипЗнч(ПоляСортировки) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ПоляСортировки Цикл
						Направление = ?(ТекПоле.Убывание, НаправлениеСортировки.Убыв, НаправлениеСортировки.Возр);
						Построитель.Порядок.Добавить(ТекПоле.ПутьКДанным,,, Направление);
						ПорядокУстановлен = Истина;
					КонецЦикла;	
				КонецЕсли;	
			КонецЕсли;
			Если ПараметрыИзмерений[ИдентификаторПоля].Свойство("Поля") Тогда
				ДополнительныеПоля = ПараметрыИзмерений[ИдентификаторПоля].Поля;
				Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ДополнительныеПоля Цикл
						ИмяТекПоля=СтрЗаменить(ТекПоле.ПутьКДанным, ".","");
						Если ПустаяСтрока(ИмяТекПоля) Тогда
							Продолжить;
						КонецЕсли;                      
						Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
							Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
								ТекНесвязанныйПоказатель = СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели[ИмяТекПоля];
								ТекВыбранноеПоле = ОтчетОбъект.ДополнительныеПоляОтчета.Добавить();
								ТекВыбранноеПоле.Имя = ИмяТекПоля;
								ТекВыбранноеПоле.ПутьКДанным = ИмяТекПоля;
								ТекВыбранноеПоле.ПолноеПредставление = ТекНесвязанныйПоказатель.Представление;
								ТекВыбранноеПоле.ШиринаКолонки = ТекНесвязанныйПоказатель.ШиринаКолонки;
								ТекВыбранноеПоле.Формат = ТекНесвязанныйПоказатель.Формат;
								ТекВыбранноеПоле.НеСвязанные = Истина;
								ТекВыбранноеПоле.ВыводитьИтогПоИерархии = ТекНесвязанныйПоказатель.ВыводитьИтогПоИерархии;
								ТекВыбранноеПоле.ВыводитьИтогДляВсехУровней = ТекНесвязанныйПоказатель.ВыводитьИтогДляВсехУровней;
								ТекВыбранноеПоле.ВыводитьОбщийИтог = ТекНесвязанныйПоказатель.ВыводитьОбщийИтог;
								Попытка
									ТекВыбранноеПоле = Построитель.ВыбранныеПоля.Добавить(ТекПоле.ПутьКДанным,);
								Исключение КонецПопытки;
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						Попытка
							ТекВыбранноеПоле = Построитель.ВыбранныеПоля.Добавить(ТекПоле.ПутьКДанным,);
						Исключение
							Продолжить;
						КонецПопытки;
						ТекВыбранноеПоле.Представление = ТекПоле.Представление;
						ТекВыбранноеПоле = ОтчетОбъект.ДополнительныеПоляОтчета.Добавить();
						ТекВыбранноеПоле.Имя = ИмяТекПоля;
						ТекВыбранноеПоле.ПутьКДанным = ТекПоле.ПутьКДанным;
						НайденноеПоле = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина);
						Если НайденноеПоле = Неопределено Тогда
							ТекВыбранноеПоле.ПолноеПредставление = ТекПоле.Представление;
						Иначе
							ТекВыбранноеПоле.ПолноеПредставление = НайденноеПоле.ПолноеПредставление;
						КонецЕсли;
						
						ТекВыбранноеПоле.НеСвязанные = Ложь;
						Попытка
							ПолеИзПараметрыИспользования = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекПоле.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
							ТекВыбранноеПоле.ШиринаКолонки = ПолеИзПараметрыИспользования["ШиринаКолонки"];
							Если ПолеИзПараметрыИспользования.Свойство("Формат") Тогда
								ТекВыбранноеПоле.Формат = ПолеИзПараметрыИспользования["Формат"];
							КонецЕсли;
						Исключение
							ТекВыбранноеПоле.ШиринаКолонки = 20;
						КонецПопытки;
					КонецЦикла;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПорядокУстановлен Тогда
			// добавим сортировку по измерению
			Попытка // не каждое выбранное поле можно добавить в порядок построителя
				Построитель.Порядок.Добавить(ТекСтрока.ПутьКДанным,,, НаправлениеСортировки.Возр);
			Исключение КонецПопытки;	
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияКолонки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		
		Тип = ТекСтрока.ТипИзмерения;
		Тип = ?(Тип = "Только иерархия", ТипИзмеренияПостроителяОтчета.ТолькоИерархия, ?(Тип = "Иерархия", ТипИзмеренияПостроителяОтчета.Иерархия, ТипИзмеренияПостроителяОтчета.Элементы));
		
		Попытка
			ТекИзмерение = Построитель.ИзмеренияКолонки.Добавить(ТекСтрока.ПутьКДанным);
		Исключение
			ТекИзмерение = Построитель.ИзмеренияКолонки.Добавить(СтрЗаменить(ТекСтрока.ПутьКДанным,".",""));
		КонецПопытки;
		
		ТекИзмерение.ТипИзмерения  = Тип;
		ТекИзмерение.Представление = ТекСтрока.Представление;
		
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
				Построитель.ВыбранныеПоля.Добавить(ПутьОсновногоПредставления);
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ПоляСоставногоПредставления = ТекПараметрыИзмерения.СоставноеПредставление.Поля;
				Для Каждого ПолеПредставления Из ПоляСоставногоПредставления Цикл
					Построитель.ВыбранныеПоля.Добавить(ПолеПредставления);
				КонецЦикла;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		// добавим сортировку по измерению
		Попытка // не каждое выбранное поле можно добавить в порядок построителя
			Построитель.Порядок.Добавить(ТекСтрока.ПутьКДанным,,, НаправлениеСортировки.Возр);
		Исключение КонецПопытки;	
	КонецЦикла;
	
	Для Каждого ТекУсловноеОформление Из Построитель.УсловноеОформление Цикл
		Для Каждого ТекОбластьУсловногоОформления Из ТекУсловноеОформление.Область Цикл
			ИмяТекПоля=СтрЗаменить(ТекОбластьУсловногоОформления.ПутьКДанным, ".","");
			Если Построитель.ВыбранныеПоля.Найти(ИмяТекПоля)=Неопределено Тогда
				Попытка
					ТекВыбранноеПоле = Построитель.ВыбранныеПоля.Добавить(ТекОбластьУсловногоОформления.ПутьКДанным,);
				Исключение
					Продолжить;
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры	//	отУстановитьИспользованиеИзмерений()

// Процедура заполняет выбранные поля и порядок сортировки измерений построителя отчета
//
// Параметры:
//	ОтчетОбъект              - ОтчетОбъект       - Объект отчета
//	Построитель              - ПостроительОтчета - Построитель отчета
//	ПоляНастройкиПостроителя - ПоляНастройки     - Поля настройки построителя отчета
//
Процедура отУстановитьИспользованиеИзмеренийСИсточникомДанных(ОтчетОбъект, Построитель, ПоляНастройкиПостроителя) Экспорт
	
	Построитель.ИзмеренияСтроки.Очистить();
	Построитель.ИзмеренияКолонки.Очистить();
	Построитель.Порядок.Очистить();
	
	Если Не Построитель.ВыводитьДетальныеЗаписи Тогда
		Построитель.ВыбранныеПоля.Очистить();
	КонецЕсли;
	
	Для Каждого ТекСтрока Из ПоляНастройкиПостроителя.ИзмеренияСтрок Цикл
		ТекИзмерение = Построитель.ИзмеренияСтроки.Добавить(ТекСтрока.ПутьКДанным);
		ТекИзмерение.Представление	= ТекСтрока.Представление;
		ТекИзмерение.ТипИзмерения	= ТекСтрока.Тип;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ПоляНастройкиПостроителя.ИзмеренияКолонок Цикл
		ТекИзмерение = Построитель.ИзмеренияКолонки.Добавить(ТекСтрока.ПутьКДанным);
		ТекИзмерение.Представление	= ТекСтрока.Представление;
		ТекИзмерение.ТипИзмерения	= ТекСтрока.Тип;
	КонецЦикла;
	
	Для Каждого ТекПоле Из ПоляНастройкиПостроителя.Поля Цикл
		Построитель.ВыбранныеПоля.Добавить(ТекПоле.Имя);
	КонецЦикла;
	
	Для Каждого ТекПоле Из ПоляНастройкиПостроителя.Свойства Цикл
		Построитель.ВыбранныеПоля.Добавить(ТекПоле.Имя);
	КонецЦикла;
	
	Для Каждого ТекПоле Из ПоляНастройкиПостроителя.Ресурсы Цикл
		Построитель.ВыбранныеПоля.Добавить(ТекПоле.Значение);
	КонецЦикла;
	
	Для Каждого ТекПорядок Из ПоляНастройкиПостроителя.Порядок Цикл
		Построитель.Порядок.Добавить(ТекПорядок.Представление,,,ТекПорядок.Значение);
	КонецЦикла;
	
КонецПроцедуры	//	отУстановитьИспользованиеИзмеренийСИсточникомДанных()

// Процедура заполняет настройки построителя отчета
// Параметры:
//	ОтчетОбъект - Формируемый отчет
//	Построитель - Объект построителя отчета
//
Процедура отЗаполнитьНастройкиПостроителяПослеФормированияМакета(ОтчетОбъект, Построитель = Неопределено) Экспорт
	
	Если Построитель = Неопределено Тогда
		Построитель = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	
	Попытка
		ОтчетОбъект.ЗаполнитьНастройкиПостроителяПослеФормированияМакета(Построитель);
	Исключение
	КонецПопытки;
	
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками И 
		ОтчетОбъект.ИзмеренияСтроки.Найти("ПериодРегистратор", "ПутьКДанным")<>Неопределено Тогда
		ПолеПериода = Построитель.ДоступныеПоля.Найти("Период");
		Если Не ПолеПериода = Неопределено И ПолеПериода.Измерение Тогда
			Если Построитель.ИзмеренияСтроки.Найти("Период") = Неопределено Тогда
				Построитель.ИзмеренияСтроки.Вставить("Период", "Период",,,, Построитель.ИзмеренияСтроки.Количество()-1);
			КонецЕсли;
			Если Построитель.Порядок.Найти("Период") = Неопределено Тогда
				Построитель.Порядок.Добавить("Период", "Период", "Период");
				Построитель.Порядок.Сдвинуть(Построитель.Порядок.Количество()-1, -1);
			КонецЕсли;
		КонецЕсли;
		Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияСтроки Цикл
			Если Не ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
			Если Найти(ТекСтрока.ПутьКДанным, ".")>0 Тогда
				Измерение = Лев(ТекСтрока.ПутьКДанным, Найти(ТекСтрока.ПутьКДанным, ".")-1);
				Если Построитель.ИзмеренияСтроки.Найти(Измерение) = Неопределено И 
					Построитель.ВыбранныеПоля.Найти(Измерение) = Неопределено Тогда
					Построитель.ВыбранныеПоля.Добавить(Измерение);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// функция проверки корректности заполненных данных формы настроек перед её закрытием
// Проверяется период отчета, и корректности заполнения формы настроек перед её закрытием, что б не 
// пришлось её запускать заново работает, как для старых отчетов, так и для отчетов на новом макете
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект - Объект отчета
//
// Возвращаемое значение:
//	Булево - Истина, если все корректно заполнено, иначе Ложь
//		   Есть ошибки (выдаваемые в виде предупреждений)
//
Функция отКорректностьЗаполненияПередФормированием(ОтчетОбъект) Экспорт
	
	// Проверка периода
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Обороты ИЛИ
		ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты ИЛИ
		ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		Если ОтчетОбъект.ДатаНачала > ОтчетОбъект.ДатаКонца И ОтчетОбъект.ДатаКонца <> '00010101000000' Тогда
			Предупреждение("Дата начала периода не может быть больше даты конца периода", 60);
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли;
	
	Попытка 
		ПродолжитьФормирование = Истина;
		ПрерватьПроверку = ОтчетОбъект.КорректностьЗаполнения(ПродолжитьФормирование);
		Если ПрерватьПроверку ИЛИ Не ПродолжитьФормирование Тогда
			Возврат ПродолжитьФормирование;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ИзмеренияСтроки = Неопределено;
	ИзмеренияКолонки = Неопределено;
	
	Попытка
		ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки;
		ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки;
	Исключение // в отчете нет массива измерений строк
		Попытка 
			ИзмеренияСтроки = ОтчетОбъект.ПостроительОтчета.ИзмеренияСтроки;
			ИзмеренияКолонки = ОтчетОбъект.ПостроительОтчета.ИзмеренияКолонки;
			ВыбранныеПоля = ОтчетОбъект.ПостроительОтчета.ВыбранныеПоля;
		Исключение // в отчете нет построителя отчетов
		КонецПопытки;
	КонецПопытки;
	
	ОтборИспользования = Новый Структура("Использование", Истина);
	
	Если ИзмеренияСтроки <> Неопределено И ИзмеренияКолонки <> Неопределено Тогда 
		Если ТипЗнч(ИзмеренияСтроки) = Тип("ИзмеренияПостроителяОтчета") Тогда
			Если ИзмеренияСтроки.Количество() = 0 И ВыбранныеПоля.Количество() = 0 Тогда
				Предупреждение("Не выбрано ни одного измерения строк!", 10); 
				Возврат Ложь;
			КонецЕсли;
			
		Иначе
			Если ИзмеренияСтроки.НайтиСтроки(ОтборИспользования).Количество() = 0 Тогда
				Предупреждение("Не выбрано ни одного измерения строк!", 10); 
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка совпадения измерений строк и колонок
		Для Каждого ТекСтрока Из ИзмеренияСтроки  Цикл
			Для Каждого ТекКолонка Из ИзмеренияКолонки  Цикл
				Если ТекСтрока.ПутьКДанным = ТекКолонка.ПутьКДанным Тогда
					Предупреждение("Повторяющаяся группировка " + ТекСтрока.Представление +"."+ Символы.ПС+
					"Нельзя использовать одинаковые поля группировки в строках и в колонках!", 30);
					Возврат Ложь;
				КонецЕсли; 
			КонецЦикла;
		КонецЦикла;
		
		// проверим нет ли повторяющихся измерений и откорректируем путь к данным после автогенерации
		// имени в случае выбора одинаковых измерений стоки и колонки
		
		// ...измерения строки
		СписокЗначений = Новый СписокЗначений;
		Для Каждого ТекСтрока из  ИзмеренияСтроки Цикл
			Если СписокЗначений.НайтиПоЗначению(ТекСтрока.ПутьКДанным) = Неопределено Тогда
				СписокЗначений.Добавить(ТекСтрока.ПутьКДанным);
			Иначе
				Предупреждение("Повторяющаяся группировка " + ТекСтрока.Представление +"."+ Символы.ПС+
				"Нельзя использовать одинаковые поля группировки строк!", 30);
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		//.....измерения колонки
		СписокЗначений = Новый СписокЗначений;
		Для Каждого ТекКолонка Из ИзмеренияКолонки  Цикл
			Если СписокЗначений.НайтиПоЗначению(ТекКолонка.ПутьКДанным) = Неопределено Тогда
				СписокЗначений.Добавить(ТекКолонка.ПутьКДанным);
			Иначе
				Предупреждение("Повторяющаяся группировка " + ТекКолонка.Представление +"."+ Символы.ПС+
				"Нельзя использовать одинаковые поля группировки колонок!", 30);
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		Если ОтчетОбъект.ДеревоПоказателей.Строки.Найти(1, "Использование")=Неопределено И ОтчетОбъект.ДеревоПоказателей.Строки.Найти(2, "Использование")=Неопределено Тогда
			Предупреждение("Не выбрано не одной функции и ни одного показателя!", 10); Возврат Ложь;
		КонецЕсли;
	Исключение	
	// проверка показателей	
	Попытка
		Если ОтчетОбъект.Показатели.НайтиСтроки(ОтборИспользования).Количество()=0 Тогда
			Предупреждение("Не выбрано ни одного показателя!", 10); Возврат Ложь;
		КонецЕсли;
	Исключение	// в отчете нет показателей
	КонецПопытки;
	
	// проверка функций
	Попытка
		Если ОтчетОбъект.Функции.НайтиСтроки(ОтборИспользования).Количество()=0 Тогда
			Предупреждение("Не выбрано не одной функции!", 10); Возврат Ложь;
		КонецЕсли;
	Исключение	// в отчете нет показателей
	КонецПопытки;
	КонецПопытки;
	
	Возврат Истина;		
	
КонецФункции // отУбратьНеИспользуемыеПоказателиИзЗапроса()

// Функция создает структуру настроек для построителя отчета и таблицу настроек колонок источника данных
//
// Параметры:
//	ОтчетОбъект                     - ОтчетОбъект     - Объект отчета
//	ТаблицаНастроекКолонокИсточника - ТаблицаЗначений - Таблица настроек колонок источника
//	ДобавлятьПоляПоОтбору           - Булево          - Признак добавления поля по отбору
//
// Возвращаемое значение:
//	Структура - Структура настроек для построителя отчета
//
Функция отПолучитьНастройкиКолонокДляИсточникаДанных(ОтчетОбъект, ТаблицаНастроекКолонокИсточника, ДобавлятьПоляПоОтбору)
	
	ПараметрыИзмерений                    = ОтчетОбъект.ПараметрыИзмеренийСтрок;
	ДеревоДоступныхПолей                  = ОтчетОбъект.ДеревоДоступныхПолей;
	Попытка
		ШаблонИсточникаДанных             = ОтчетОбъект.ШаблонИсточникаДанных;
	Исключение
		ШаблонИсточникаДанных             = Новый ТаблицаЗначений;
	КонецПопытки;
	СтруктураСвязиСвойств                 = ОтчетОбъект.СтруктураСвязиСвойств;
	СтруктураИтоговыхВыраженийПоказателей = ОтчетОбъект.СтруктураИтоговыхВыраженийПоказателей;
	
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	Попытка
		СтруктураНеСвязанныхПоказателей = ОтчетОбъект.СтруктураНеСвязанныхПоказателей;
	Исключение КонецПопытки;
	
	Если ТипЗнч(СтруктураНеСвязанныхПоказателей) <> Тип("Структура") Тогда
		СтруктураНеСвязанныхПоказателей = Новый Структура();
	КонецЕсли;
	
	ОтчетОбъект.ДополнительныеПоляОтчета.Очистить();
	
	ТаблицаИзмеренийСтрок 			= Новый ТаблицаЗначений();
	ТаблицаИзмеренийКолонок 		= Новый ТаблицаЗначений();
	ТаблицаПолей 					= Новый ТаблицаЗначений();
	ТаблицаСвойств					= Новый ТаблицаЗначений();
	СтруктураРесурсов 				= Новый СписокЗначений();
	СтруктураСортировки 			= Новый СписокЗначений();
	
	ТаблицаИзмеренийСтрок.Колонки.Добавить("ПутьКДанным");
	ТаблицаИзмеренийСтрок.Колонки.Добавить("Представление");
	ТаблицаИзмеренийСтрок.Колонки.Добавить("Тип");
	ТаблицаИзмеренийКолонок.Колонки.Добавить("ПутьКДанным");
	ТаблицаИзмеренийКолонок.Колонки.Добавить("Представление");
	ТаблицаИзмеренийКолонок.Колонки.Добавить("Тип");
	ТаблицаПолей.Колонки.Добавить("Имя");
	ТаблицаПолей.Колонки.Добавить("ИмяРодителя");
	ТаблицаПолей.Колонки.Добавить("ПутьКДанным");
	ТаблицаПолей.Колонки.Добавить("ТипЗначения");
	
	ТаблицаСвойств.Колонки.Добавить("Имя");
	ТаблицаСвойств.Колонки.Добавить("ИмяРодителя");
	ТаблицаСвойств.Колонки.Добавить("ПутьКДанным");
	ТаблицаСвойств.Колонки.Добавить("Свойство");
	ТаблицаСвойств.Колонки.Добавить("Отбор");
		
	ТаблицаНастроекКолонокИсточника = Новый ТаблицаЗначений;
	ТаблицаНастроекКолонокИсточника.Колонки.Добавить("Измерение");
	ТаблицаНастроекКолонокИсточника.Колонки.Добавить("Имя");
	ТаблицаНастроекКолонокИсточника.Колонки.Добавить("Итог");
	ТаблицаНастроекКолонокИсточника.Колонки.Добавить("ПутьКДанным");
	ТаблицаНастроекКолонокИсточника.Колонки.Добавить("ТипЗначения");
	
	ПоляНастройкиПостроителя = Новый Структура("ИзмеренияСтрок, ИзмеренияКолонок, Поля, Свойства, Порядок, Ресурсы", ТаблицаИзмеренийСтрок, ТаблицаИзмеренийКолонок, ТаблицаПолей, ТаблицаСвойств, СтруктураСортировки, СтруктураРесурсов);
	
	Попытка 
		Если ОтчетОбъект.ПолучитьНастройкиКолонокДляИсточникаДанных(ТаблицаНастроекКолонокИсточника, ПоляНастройкиПостроителя) Тогда
			Возврат ПоляНастройкиПостроителя;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка 
		СтруктураСортировкиПоля = ОтчетОбъект.СтруктураСортировки();
		Если ТипЗнч(СтруктураСортировкиПоля) = Тип("СписокЗначений") Тогда
			Для Каждого ТекПорядок Из СтруктураСортировкиПоля Цикл
				СтруктураСортировки.Добавить(ТекПорядок.Значение, ТекПорядок.Представление);
			КонецЦикла;
		Иначе
			СтруктураСортировки.Добавить(СтруктураСортировкиПоля.НаправлениеСортировки, СтруктураСортировкиПоля.Описание);
		КонецЕсли;
	Исключение КонецПопытки;
	
	ТипРесурса = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,4));
	
	Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
		Если Не ТекФункция.Использование Тогда Продолжить; КонецЕсли;
		Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
			Если Не ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
			ИмяРесурса = ТекПоказатель.Имя+ТекФункция.Имя;
			НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
			НоваяСтрока.Измерение   = Ложь;
			НоваяСтрока.Имя         = ИмяРесурса;
			НоваяСтрока.Итог        = СтруктураИтоговыхВыраженийПоказателей[ИмяРесурса];
			НоваяСтрока.ПутьКДанным = ИмяРесурса;
			ПараметрыКолонки = ШаблонИсточникаДанных.Колонки.Найти(ИмяРесурса);
			Если ПараметрыКолонки = Неопределено Тогда
				НоваяСтрока.ТипЗначения = ТипРесурса;
			Иначе
				НоваяСтрока.ТипЗначения = ПараметрыКолонки.ТипЗначения;
			КонецЕсли;
			СтруктураРесурсов.Добавить(ИмяРесурса);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияСтроки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		
		ПорядокУстановлен = Ложь;
		ПолноеИмяСтроки = СтрЗаменить(ТекСтрока.ПутьКДанным, ".","");		
		
		ТекТипЗначения = Неопределено;
		Если СтруктураСвязиСвойств.Свойство(ПолноеИмяСтроки) Тогда 
			НоваяСтрока = ТаблицаСвойств.Добавить();
			НоваяСтрока.ИмяРодителя = СтруктураСвязиСвойств[ПолноеИмяСтроки].ИмяИзмерения;
			НоваяСтрока.Имя = ПолноеИмяСтроки;
			НоваяСтрока.Свойство = СтруктураСвязиСвойств[ПолноеИмяСтроки].Свойство;
			НоваяСтрока.ПутьКДанным = ПолноеИмяСтроки;
			НоваяСтрока.Отбор = 0;
			
			ТекТипЗначения = Новый ОписаниеТипов(СтруктураСвязиСвойств[ПолноеИмяСтроки].ТипЗначения, "Строка");
		Иначе
			ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
		КонецЕсли;
		
		НоваяСтрокаИзмерения = ТаблицаИзмеренийСтрок.Добавить();
		НоваяСтрокаИзмерения.ПутьКДанным	= ПолноеИмяСтроки;
		НоваяСтрокаИзмерения.Представление	= ТекСтрока.Представление;
		НоваяСтрокаИзмерения.Тип			= ?(ТекСтрока.ТипИзмерения = "Только иерархия", ТипИзмеренияПостроителяОтчета.ТолькоИерархия, ТипИзмеренияПостроителяОтчета.Элементы);
		
		НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ПолноеИмяСтроки, "Имя");
		Если НоваяСтрока=Неопределено Тогда
			НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
			НоваяСтрока.Измерение	= Истина;
			НоваяСтрока.Имя			= ПолноеИмяСтроки;
			НоваяСтрока.Итог	 	= "";
			НоваяСтрока.ПутьКДанным	= ПолноеИмяСтроки;
			НоваяСтрока.ТипЗначения = ТекТипЗначения;
		КонецЕсли;
		
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = СтрЗаменить(ТекПараметрыИзмерения.ОсновноеПредставление,".", "");
				ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным+"."+НоваяСтрока.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
				
				НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ПутьОсновногоПредставления, "Имя");
				Если НоваяСтрока=Неопределено Тогда
					НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
					НоваяСтрока.Измерение	= Истина;
					НоваяСтрока.Имя			= ПутьОсновногоПредставления;
					НоваяСтрока.Итог	 	= "";
					НоваяСтрока.ПутьКДанным	= ТекСтрока.ПутьКДанным+".Представление";
					НоваяСтрока.ТипЗначения = ТекТипЗначения;
				Иначе
					НоваяСтрока.ПутьКДанным = ТекСтрока.ПутьКДанным+".Представление";
				КонецЕсли;
				
				НоваяСтрока = ТаблицаПолей.Добавить();
				НоваяСтрока.Имя			= ПутьОсновногоПредставления;
				НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
				НоваяСтрока.ПутьКДанным	= ТекПараметрыИзмерения.ОсновноеПредставление;
				НоваяСтрока.ТипЗначения = ТекТипЗначения;
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ПоляСоставногоПредставления = ТекПараметрыИзмерения.СоставноеПредставление.Поля;
				Для Каждого ПолеПредставления Из ПоляСоставногоПредставления Цикл
					ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ПолеПредставления, "ПутьКДанным", Истина).ТипЗначения;
					
					ИмяТекПоля = СтрЗаменить(ПолеПредставления, ".","");
					
					НоваяСтрока = ТаблицаПолей.Добавить();
					НоваяСтрока.Имя			= ИмяТекПоля;
					НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
					НоваяСтрока.ПутьКДанным	= ПолеПредставления;
					НоваяСтрока.ТипЗначения = ТекТипЗначения;
					
					НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ИмяТекПоля, "Имя");
					Если НоваяСтрока=Неопределено Тогда
						НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
						НоваяСтрока.Измерение	= Истина;
						НоваяСтрока.Имя			= ИмяТекПоля;
						НоваяСтрока.Итог	 	= "";
						НоваяСтрока.ПутьКДанным	= ПолеПредставления;
						НоваяСтрока.ТипЗначения = ТекТипЗначения;
					Иначе
						НоваяСтрока.ПутьКДанным = ПолеПредставления;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ИдентификаторПоля = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "_");
		Если ПараметрыИзмерений.Свойство(ИдентификаторПоля) Тогда
			Если ПараметрыИзмерений[ИдентификаторПоля].Свойство("Порядок") и
				НЕ СтруктураСвязиСвойств.Свойство(ИдентификаторПоля) Тогда
				ПоляСортировки = ПараметрыИзмерений[ИдентификаторПоля].Порядок;
				Если ТипЗнч(ПоляСортировки) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ПоляСортировки Цикл
						Направление = ?(ТекПоле.Убывание, НаправлениеСортировки.Убыв, НаправлениеСортировки.Возр);
						ИмяТекПоля = СтрЗаменить(ТекПоле.ПутьКДанным, ".","");
						СтруктураСортировки.Добавить(Направление, ИмяТекПоля);
						
						ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
						
						НоваяСтрока = ТаблицаПолей.Добавить();
						НоваяСтрока.Имя			= ИмяТекПоля;
						НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
						НоваяСтрока.ПутьКДанным	= ТекПоле.ПутьКДанным;
						НоваяСтрока.ТипЗначения = ТекТипЗначения;
						
						ПорядокУстановлен = Истина;
						
						НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ИмяТекПоля, "Имя");
						Если НоваяСтрока=Неопределено Тогда
							НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
							НоваяСтрока.Измерение	= Истина;
							НоваяСтрока.Имя			= ИмяТекПоля;
							НоваяСтрока.Итог	 	= "";
							НоваяСтрока.ПутьКДанным	= ТекПоле.ПутьКДанным;
							НоваяСтрока.ТипЗначения = ТекТипЗначения;
						Иначе
							НоваяСтрока.ПутьКДанным = ТекПоле.ПутьКДанным;
						КонецЕсли;
						
					КонецЦикла;	
				КонецЕсли;	
			КонецЕсли;
			Если ПараметрыИзмерений[ИдентификаторПоля].Свойство("Поля") Тогда
				ДополнительныеПоля = ПараметрыИзмерений[ИдентификаторПоля].Поля;
				Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ДополнительныеПоля Цикл
						ИмяТекПоля=СтрЗаменить(ТекПоле.ПутьКДанным, ".","");
						Если ПустаяСтрока(ИмяТекПоля) Тогда
							Продолжить;
						КонецЕсли;                      
						
						Если СтруктураНеСвязанныхПоказателей.Свойство(ИдентификаторПоля) Тогда
							Если СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели.Свойство(ИмяТекПоля) Тогда
								ТекНесвязанныйПоказатель = СтруктураНеСвязанныхПоказателей[ИдентификаторПоля].Показатели[ИмяТекПоля];
								
								ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
								
								НоваяСтрока = ТаблицаПолей.Добавить();
								НоваяСтрока.Имя			= ИмяТекПоля;
								НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
								НоваяСтрока.ПутьКДанным	= ТекПоле.ПутьКДанным;
								НоваяСтрока.ТипЗначения = ТекТипЗначения;
								
								ТекВыбранноеПоле = ОтчетОбъект.ДополнительныеПоляОтчета.Добавить();
								ТекВыбранноеПоле.Имя = ИмяТекПоля;
								ТекВыбранноеПоле.ПутьКДанным = ИмяТекПоля;
								ТекВыбранноеПоле.ПолноеПредставление = ТекНесвязанныйПоказатель.Представление;
								ТекВыбранноеПоле.ШиринаКолонки = ТекНесвязанныйПоказатель.ШиринаКолонки;
								ТекВыбранноеПоле.Формат = ТекНесвязанныйПоказатель.Формат;
								ТекВыбранноеПоле.НеСвязанные = Истина;
								ТекВыбранноеПоле.ВыводитьИтогПоИерархии = ТекНесвязанныйПоказатель.ВыводитьИтогПоИерархии;
								ТекВыбранноеПоле.ВыводитьОбщийИтог = ТекНесвязанныйПоказатель.ВыводитьОбщийИтог;
								
								НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ИмяТекПоля, "Имя");
								Если НоваяСтрока=Неопределено Тогда
									НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
									НоваяСтрока.Измерение	= Истина;
									НоваяСтрока.Имя			= ИмяТекПоля;
									НоваяСтрока.Итог	 	= "";
									НоваяСтрока.ПутьКДанным	= ТекПоле.ПутьКДанным;
									НоваяСтрока.ТипЗначения = ТекТипЗначения;
								Иначе
									НоваяСтрока.ПутьКДанным = ТекПоле.ПутьКДанным;
								КонецЕсли;
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						
						ПутьКДанным = ?(СтруктураСвязиСвойств.Свойство(ИмяТекПоля), СтрЗаменить(ТекСтрока.ПутьКДанным,".","")+"."+ТекПоле.ПутьКДанным,ТекПоле.ПутьКДанным);
						
						Если СтруктураСвязиСвойств.Свойство(ИмяТекПоля) Тогда
							НоваяСтрока = ТаблицаСвойств.Добавить();
							НоваяСтрока.ИмяРодителя = СтруктураСвязиСвойств[ИмяТекПоля].ИмяИзмерения;
							НоваяСтрока.Имя = ИмяТекПоля;
							НоваяСтрока.Свойство = СтруктураСвязиСвойств[ИмяТекПоля].Свойство;
							НоваяСтрока.ПутьКДанным = ПутьКДанным;
							НоваяСтрока.Отбор = 0;
							ТекТипЗначения = Новый ОписаниеТипов(СтруктураСвязиСвойств[ИмяТекПоля].ТипЗначения, "Строка");
						Иначе
							ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
							НоваяСтрока = ТаблицаПолей.Добавить();
							НоваяСтрока.Имя			= ИмяТекПоля;
							НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
							НоваяСтрока.ПутьКДанным	= ТекПоле.ПутьКДанным;
							НоваяСтрока.ТипЗначения = ТекТипЗначения;
						КонецЕсли;
						
						НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ИмяТекПоля, "Имя");
						Если НоваяСтрока=Неопределено Тогда
							НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
							НоваяСтрока.Измерение	= Истина;
							НоваяСтрока.Имя			= ИмяТекПоля;
							НоваяСтрока.Итог	 	= "";
							НоваяСтрока.ПутьКДанным	= ПутьКДанным;
							НоваяСтрока.ТипЗначения = ТекТипЗначения;
						Иначе
							НоваяСтрока.ПутьКДанным = ПутьКДанным;
						КонецЕсли;
						
						ТекВыбранноеПоле = ОтчетОбъект.ДополнительныеПоляОтчета.Добавить();
						ТекВыбранноеПоле.Имя = ИмяТекПоля;
						ТекВыбранноеПоле.ПутьКДанным = ТекПоле.ПутьКДанным;
						
						НайденноеПоле = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина);
						Если НайденноеПоле = Неопределено Тогда
							ТекВыбранноеПоле.ПолноеПредставление = ТекПоле.Представление;
						Иначе
							ТекВыбранноеПоле.ПолноеПредставление = НайденноеПоле.ПолноеПредставление;
						КонецЕсли;
						ТекВыбранноеПоле.НеСвязанные = Ложь;
						Попытка
							ПолеИзПараметрыИспользования = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекПоле.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
							ТекВыбранноеПоле.ШиринаКолонки = ПолеИзПараметрыИспользования["ШиринаКолонки"];
							Если ПолеИзПараметрыИспользования.Свойство("Формат") Тогда
								ТекВыбранноеПоле.Формат = ПолеИзПараметрыИспользования["Формат"];
							КонецЕсли;
						Исключение
							ТекВыбранноеПоле.ШиринаКолонки = 20;
						КонецПопытки;
					КонецЦикла;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПорядокУстановлен Тогда
			СтруктураСортировки.Добавить(НаправлениеСортировки.Возр, ПолноеИмяСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияКолонки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		
		ПолноеИмяСтроки = СтрЗаменить(ТекСтрока.ПутьКДанным, ".","");
		
		Если СтруктураСвязиСвойств.Свойство(ПолноеИмяСтроки) Тогда 
			НоваяСтрока = ТаблицаСвойств.Добавить();
			НоваяСтрока.ИмяРодителя = СтруктураСвязиСвойств[ПолноеИмяСтроки].ИмяИзмерения;
			НоваяСтрока.Имя = ПолноеИмяСтроки;
			НоваяСтрока.Свойство = СтруктураСвязиСвойств[ПолноеИмяСтроки].Свойство;
			НоваяСтрока.ПутьКДанным = ТекСтрока.ПутьКДанным;
			НоваяСтрока.Отбор = 0;
			ТекТипЗначения = СтруктураСвязиСвойств[ПолноеИмяСтроки].ТипЗначения;
		Иначе
			ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
		КонецЕсли;
		
		НоваяСтрокаИзмерения = ТаблицаИзмеренийКолонок.Добавить();
		НоваяСтрокаИзмерения.ПутьКДанным	= ПолноеИмяСтроки;
		НоваяСтрокаИзмерения.Представление	= ТекСтрока.Представление;
		НоваяСтрокаИзмерения.Тип			= ?(ТекСтрока.ТипИзмерения = "Только иерархия", ТипИзмеренияПостроителяОтчета.ТолькоИерархия, ТипИзмеренияПостроителяОтчета.Элементы);
		
		НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ПолноеИмяСтроки, "Имя");
		Если НоваяСтрока=Неопределено Тогда
			НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
			НоваяСтрока.Измерение	= Истина;
			НоваяСтрока.Имя			= ПолноеИмяСтроки;
			НоваяСтрока.Итог	 	= "";
			НоваяСтрока.ПутьКДанным	= ПолноеИмяСтроки;
			НоваяСтрока.ТипЗначения = ТекТипЗначения;
		КонецЕсли;
		
		ОсновноеПредставление = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		Попытка
			Если ОсновноеПредставление.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = СтрЗаменить(ОсновноеПредставление.ОсновноеПредставление,".", "");
				
				ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекСтрока.ПутьКДанным+"."+НоваяСтрока.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
				
				НоваяСтрока = ТаблицаПолей.Добавить();
				НоваяСтрока.Имя			= ПутьОсновногоПредставления;
				НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
				НоваяСтрока.ПутьКДанным	= ТекПараметрыИзмерения.ОсновноеПредставление;
				НоваяСтрока.ТипЗначения = ТекТипЗначения;
				
				НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ПутьОсновногоПредставления, "Имя");
				Если НоваяСтрока=Неопределено Тогда
					НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
					НоваяСтрока.Измерение	= Истина;
					НоваяСтрока.Имя			= ПутьОсновногоПредставления;
					НоваяСтрока.Итог	 	= "";
					НоваяСтрока.ПутьКДанным	= ТекСтрока.ПутьКДанным+".Представление";
					НоваяСтрока.ТипЗначения = ТекТипЗначения;
				Иначе
					НоваяСтрока.ПутьКДанным	= ТекСтрока.ПутьКДанным+".Представление";
				КонецЕсли;
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ПоляСоставногоПредставления = ТекПараметрыИзмерения.СоставноеПредставление.Поля;
				Для Каждого ПолеПредставления Из ПоляСоставногоПредставления Цикл
					ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ПолеПредставления, "ПутьКДанным", Истина).ТипЗначения;
					
					ИмяТекПоля = СтрЗаменить(ПолеПредставления, ".","");
					
					НоваяСтрока = ТаблицаПолей.Добавить();
					НоваяСтрока.Имя			= ИмяТекПоля;
					НоваяСтрока.ИмяРодителя	= ПолноеИмяСтроки;
					НоваяСтрока.ПутьКДанным	= ПолеПредставления;
					НоваяСтрока.ТипЗначения = ТекТипЗначения;
					
					НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ИмяТекПоля, "Имя");
					Если НоваяСтрока=Неопределено Тогда
						НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
						НоваяСтрока.Измерение	= Истина;
						НоваяСтрока.Имя			= ИмяТекПоля;
						НоваяСтрока.Итог	 	= "";
						НоваяСтрока.ПутьКДанным	= ПолеПредставления;
						НоваяСтрока.ТипЗначения = ТекТипЗначения;
					Иначе
						НоваяСтрока.ПутьКДанным = ПолеПредставления;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Исключение
		КонецПопытки;
		СтруктураСортировки.Добавить(НаправлениеСортировки.Возр, ПолноеИмяСтроки);
	КонецЦикла;
	
	Если ДобавлятьПоляПоОтбору Тогда
		Для Каждого ТекОтбор Из ОтчетОбъект.ПостроительОтчета.Отбор Цикл
			Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
			ПозицияТочки = Найти(ТекОтбор.ПутьКДанным, ".");
			
			Если СтруктураСвязиСвойств.Свойство(ТекОтбор.Имя) Тогда 
				НоваяСтрока = ТаблицаСвойств.Добавить();
				НоваяСтрока.ИмяРодителя = СтруктураСвязиСвойств[ТекОтбор.Имя].ИмяИзмерения;
				НоваяСтрока.Имя = ТекОтбор.Имя;
				НоваяСтрока.Свойство = СтруктураСвязиСвойств[ТекОтбор.Имя].Свойство;
				НоваяСтрока.ПутьКДанным = ТекОтбор.ПутьКДанным;
				НоваяСтрока.Отбор = 1;
				ТекТипЗначения = СтруктураСвязиСвойств[ТекОтбор.Имя].ТипЗначения;
			Иначе
				ТекТипЗначения = ДеревоДоступныхПолей.Строки.Найти(ТекОтбор.ПутьКДанным, "ПутьКДанным", Истина).ТипЗначения;
			КонецЕсли;
			
			ТекИмя = СтрЗаменить(ТекОтбор.ПутьКДанным,".","");
			
			Если ПозицияТочки > 0 Тогда 
				НоваяСтрока = ТаблицаПолей.Добавить();
				//НоваяСтрока.Имя			= ТекОтбор.Имя;
				НоваяСтрока.Имя			= ТекИмя;
				НоваяСтрока.ИмяРодителя	= Лев(ТекОтбор.ПутьКДанным, ПозицияТочки-1);
				НоваяСтрока.ПутьКДанным	= ТекОтбор.ПутьКДанным;
				НоваяСтрока.ТипЗначения = ТекТипЗначения;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаНастроекКолонокИсточника.Найти(ТекИмя, "Имя");
			Если НоваяСтрока=Неопределено Тогда
				НоваяСтрока = ТаблицаНастроекКолонокИсточника.Добавить();
				НоваяСтрока.Измерение	= Истина;
				НоваяСтрока.Имя			= ТекИмя;
				НоваяСтрока.Итог	 	= "";
				НоваяСтрока.ПутьКДанным	= ТекОтбор.ПутьКДанным;
				НоваяСтрока.ТипЗначения = ТекТипЗначения;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаСвойств.Свернуть("ИмяРодителя, Имя, Свойство, ПутьКДанным", "Отбор");
	ТаблицаСвойств.Сортировать("Отбор УБЫВ, ИмяРодителя, Имя, Свойство, ПутьКДанным");
	ТаблицаПолей.Свернуть("Имя, ИмяРодителя, ПутьКДанным, ТипЗначения");
	ТаблицаНастроекКолонокИсточника.Свернуть("Имя, Измерение, Итог, ПутьКДанным, ТипЗначения");
	
	Возврат ПоляНастройкиПостроителя;
	
КонецФункции //отПолучитьНастройкиКолонокДляИсточникаДанных()

// Процедура добавляет и заполняет колонки дополнительных полей для колонок Измерений
//
// Параметры:
//	ОтчетОбъект                - ОтчетОбъект     - Объект отчета
//	ТаблицаДанных              - ТаблицаЗначений - Таблица данных
//  ТаблицаДополнительныхПолей - ТаблицаЗначений - Таблица дополнительных полей
//
Процедура отПолучитьПоляДляИсточникаДанных(ОтчетОбъект, ТаблицаДанных, ТаблицаДополнительныхПолей)
	
	ТекстПолей = "";
	Для Каждого ТекПоле Из ТаблицаДополнительныхПолей Цикл
		ИмяПоляКолонки = ТекПоле.Имя;
		Если ТаблицаДанных.Колонки.Найти(ИмяПоляКолонки) = Неопределено Тогда
			ТекстПолей = ТекстПолей+","+Символы.ПС+" ТаблицаДанных."+ТекПоле.ПутьКДанным+" КАК "+ИмяПоляКолонки;
		КонецЕсли;
	КонецЦикла;
	Если Не ПустаяСтрока(ТекстПолей) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ * 
		|ПОМЕСТИТЬ ВременнаяТаблицаДанных
		|ИЗ &ТаблицаДанных КАК ТаблицаДанных");
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
		Запрос.Выполнить();
		
		Запрос.Текст="ВЫБРАТЬ * "+ТекстПолей+"
		|ИЗ 
		|	ВременнаяТаблицаДанных КАК ТаблицаДанных";
		ТаблицаДанных = Запрос.Выполнить().Выгрузить();
		Запрос.МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
КонецПроцедуры // отПолучитьПоляДляИсточникаДанных()

// Процедура добавляет и заполняет колонки свойств для колонок Измерений
Процедура отПолучитьСвойстваДляИсточникаДанных(ОтчетОбъект, ТаблицаДанных, ТаблицаСвойств)
	
	Если ТаблицаСвойств.Количество()>0 Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Объект КАК Объект,
		|	ЗначенияСвойствОбъектов.Свойство КАК Свойство,
		|	ЗначенияСвойствОбъектов.Значение КАК Значение
		|ПОМЕСТИТЬ ТаблицаСвойствОбъектов
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Свойство В(&Свойства)
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект,
		|	Свойство");
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Свойства", ТаблицаСвойств.ВыгрузитьКолонку("Свойство"));
		Запрос.Выполнить();
		
		Запрос.Текст = "ВЫБРАТЬ * 
		|ПОМЕСТИТЬ ВременнаяТаблицаДанных
		|ИЗ &ТаблицаДанных КАК ТаблицаДанных";
		Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
		Запрос.Выполнить();
		
		ТекстПолей="";
		Для Каждого ТекКолонка Из ТаблицаДанных.Колонки Цикл
			ТекстПолей = ТекстПолей+?(ТекстПолей="","",","+Символы.ПС)+"ТаблицаДанных."+ТекКолонка.Имя+" КАК "+ТекКолонка.Имя;
		КонецЦикла;
		
		ЗапросСвойстваПоля = "";
		ЗапросСвойстваСоединение = "";
		Для Каждого ТекСвойство Из ТаблицаСвойств Цикл
			ИмяСвойства = ТекСвойство.Имя;
			Если ТаблицаДанных.Колонки.Найти(ИмяСвойства) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ОтчетОбъект.СтруктураСвязиСвойств.Свойство(ИмяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			
			ТекТипЗначения = ОтчетОбъект.СтруктураСвязиСвойств[ИмяСвойства].ТипЗначения;
			
			ТаблицаДанных.Колонки.Добавить(ИмяСвойства, ТекТипЗначения);
			Если ТекСвойство.Отбор=1 Тогда
				ЗапросСвойстваПоля = ЗапросСвойстваПоля+", Таблица" + ИмяСвойства + ".Значение КАК " + ИмяСвойства + Символы.ПС;
			Иначе
				ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(ТекТипЗначения);
				//ЗапросСвойстваПоля = ЗапросСвойстваПоля+", ВЫРАЗИТЬ(Таблица" + ИмяСвойства + ".Значение  КАК " + ТипСвойства + ") КАК " + ИмяСвойства + Символы.ПС;
				ЗапросСвойстваПоля = ЗапросСвойстваПоля + ", " +
					отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " КАК " + ИмяСвойства + Символы.ПС;
			КонецЕсли;
			ЗапросСвойстваСоединение = ЗапросСвойстваСоединение+"ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвойствОбъектов КАК Таблица" + ИмяСвойства + " ПО Таблица" + ИмяСвойства + ".Объект = ТаблицаДанных." + ТекСвойство.ИмяРодителя + " И Таблица" + ИмяСвойства + ".Свойство = &Выб" + ИмяСвойства + "" + Символы.ПС;
			Запрос.УстановитьПараметр("Выб" + ИмяСвойства, ТекСвойство.Свойство);
		КонецЦикла;
		
		Запрос.Текст="ВЫБРАТЬ 
		|	"+ТекстПолей+"
		|	"+ЗапросСвойстваПоля+"
		|ИЗ 
		|	ВременнаяТаблицаДанных КАК ТаблицаДанных
		|	"+ЗапросСвойстваСоединение;
		ТаблицаДанных = Запрос.Выполнить().Выгрузить();
		Запрос.МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
КонецПроцедуры // отПолучитьСвойстваДляИсточникаДанных()

// Процедура создания построителя отчета
//
// Параметры:
// ПостроительПриемник - ПостроительОтчета - Построитель отчета приемник
// ПостроительИсточник - ПостроительОтчета - Построитель отчета источник
// ТекстЗапроса        - Строка            - Текст запроса
//
Процедура СоздатьПостроительОтчета(ПостроительПриемник, ПостроительИсточник, ТекстЗапроса, АвтозаполнениеПолей = Истина)
	
	НастройкиПостроителя = ПостроительИсточник.ПолучитьНастройки();
	Попытка
		ПостроительПриемник.Текст = ТекстЗапроса;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Если АвтозаполнениеПолей Тогда
		ПостроительПриемник.ЗаполнитьНастройки();
	КонецЕсли;
	ПостроительПриемник.УстановитьНастройки(НастройкиПостроителя);
	Для Каждого ТекОтбор Из ПостроительПриемник.Отбор Цикл
		 ТекОтбор.Представление = ПостроительИсточник.Отбор.Найти(ТекОтбор.Имя).Представление;
	КонецЦикла;
	ПостроительПриемник.ВыводитьОбщиеИтоги = ПостроительИсточник.ВыводитьОбщиеИтоги;
	ПостроительПриемник.ВыводитьПодвалТаблицы = ПостроительИсточник.ВыводитьПодвалТаблицы;
	ПостроительПриемник.ВыводитьШапкуТаблицы = ПостроительИсточник.ВыводитьШапкуТаблицы;
	ПостроительПриемник.ДобавлениеПредставлений = ПостроительИсточник.ДобавлениеПредставлений;
	ПостроительПриемник.ОтображатьСостояние = ПостроительИсточник.ОтображатьСостояние;
	Для Каждого ТекПараметр Из ПостроительИсточник.Параметры Цикл
		ПостроительПриемник.Параметры.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
КонецПроцедуры // СоздатьПостроительОтчета()

// функция производит все необходимые настройки построителя отчета
//
// Параметры:
// 	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
// Возвращаемое значение:
//	Булево - Результат выполнения
//
Функция ПодготовкаПостроителяОтчета(ОтчетОбъект, ПостроительОтчета) Экспорт
	
	Попытка 
		ПостроительОтчета.ВыводитьДетальныеЗаписи = ОтчетОбъект.ВыводитьДетальныеЗаписи;
	Исключение
		ПостроительОтчета.ВыводитьДетальныеЗаписи = Ложь;
	КонецПопытки;
	
	ОбъединенныйЗапрос = Ложь;
	Попытка
		ОбъединенныйЗапрос = ТипЗнч(ОтчетОбъект.ТаблицаЗапроса)=Тип("ТаблицаЗначений");
	Исключение
	КонецПопытки;
	
	Если ОбъединенныйЗапрос Тогда
		Попытка
			ОтчетОбъект.УстановитьПараметры(ПостроительОтчета);
		Исключение
		КонецПопытки;
		// получим структуру используемых полей отчета
		СтруктураПолейОтчета = отПолучитьИспользованиеИзмерений(ОтчетОбъект);
		Попытка
			ОтчетОбъект.КорректировкаСтруктурыПолейОтчета(СтруктураПолейОтчета);
		Исключение
		КонецПопытки;
		// получим текст запроса построителя
		ТекстЗапроса = отПолучитьИерархическийЗапросПостроителяОтчетов(ОтчетОбъект, СтруктураПолейОтчета);
		
		Если ПустаяСтрока(ТекстЗапроса) Тогда Возврат Ложь; КонецЕсли;
		
		ПостроительИсточник = ОтчетОбъект.ПостроительОтчета;
		НастройкиПостроителя = ПостроительИсточник.ПолучитьНастройки();
		
		Попытка
			ПостроительОтчета.Текст = ТекстЗапроса;
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
		
		ПостроительОтчета.УстановитьНастройки(НастройкиПостроителя);
		Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
			ТекОтбор.Представление = ПостроительИсточник.Отбор.Найти(ТекОтбор.Имя).Представление;
		КонецЦикла;
		ПостроительОтчета.ВыводитьОбщиеИтоги = ПостроительИсточник.ВыводитьОбщиеИтоги;
		ПостроительОтчета.ВыводитьПодвалТаблицы = ПостроительИсточник.ВыводитьПодвалТаблицы;
		ПостроительОтчета.ВыводитьШапкуТаблицы = ПостроительИсточник.ВыводитьШапкуТаблицы;
		ПостроительОтчета.ДобавлениеПредставлений = ПостроительИсточник.ДобавлениеПредставлений;
		ПостроительОтчета.ОтображатьСостояние = ПостроительИсточник.ОтображатьСостояние;
		Для Каждого ТекПараметр Из ПостроительИсточник.Параметры Цикл
			ПостроительОтчета.Параметры.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
		КонецЦикла;
		
		// установим период построителя запроса по виду отчета
		отУстановитьПериодОтчета(ОтчетОбъект, ПостроительОтчета); 
				
		// установим измерения, порядок и доп. поля отчета
		отУстановитьИспользованиеИзмеренийДляТаблицыЗапросов(ОтчетОбъект, ПостроительОтчета, СтруктураПолейОтчета);
	Иначе
		Попытка
			ОтчетОбъект.УстановитьПараметры(ПостроительОтчета);
		Исключение
		КонецПопытки;
		// подкорректируем текст запроса построителя - уберем неиспользуемые показатели и функции
		отУбратьНеИспользуемыеПоказателиИзЗапроса(ОтчетОбъект, ПостроительОтчета);
		
		// установим период построителя запроса по виду отчета
		отУстановитьПериодОтчета(ОтчетОбъект, ПостроительОтчета); 
		
		// установим измерения, порядок и доп. поля отчета
		отУстановитьИспользованиеИзмерений(ОтчетОбъект, ПостроительОтчета);
	КонецЕсли;
	
	// проверим корректность заполнения периода и измерений построителя
	Если НЕ отКорректностьЗаполнения(ОтчетОбъект, ПостроительОтчета) Тогда	Возврат Ложь; КонецЕсли;
	
	// Расшифровки ячеек построителя будем заполнять по структуре со значениями группировок
	ПостроительОтчета.ЗаполнениеРасшифровки = ВидЗаполненияРасшифровкиПостроителяОтчета.Расшифровка;
	
	// Подвал отчета не выводится
	ПостроительОтчета.ВыводитьПодвалОтчета = Ложь;
	
	Возврат Истина;

КонецФункции // ПодготовкаПостроителяОтчета()

// функция производит все необходимые действия для получения источника данных, его настройки,
// а так же для передачи его в Построитель
//
// Параметры:
// 	ОтчетОбъект        - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета  - ПостроительОтчета - Построитель отчета
//	ПолностьюПоТаблице - Булево            - Признак передачи всех данных в построитель отчета
//
// Возвращаемое значение:
//	Булево - Результат выполнения
//
Функция ПодготовкаПостроителяСИсточникомДанных(ОтчетОбъект, ПостроительОтчета, ПолностьюПоТаблице) Экспорт
	
	Попытка 
		ПостроительОтчета.ВыводитьДетальныеЗаписи = ОтчетОбъект.ВыводитьДетальныеЗаписи;
	Исключение
		ПостроительОтчета.ВыводитьДетальныеЗаписи = Ложь;
	КонецПопытки;
	
	Попытка
		ОтчетОбъект.УстановитьПараметры(ПостроительОтчета);
	Исключение
	КонецПопытки;
	
	// подкорректируем текст запроса построителя - уберем неиспользуемые показатели и функции
	Если (Не ПолностьюПоТаблице) Тогда
		отУбратьНеИспользуемыеПоказателиИзЗапроса(ОтчетОбъект, ПостроительОтчета);
	КонецЕсли;
	
	// установим период построителя запроса по виду отчета
	отУстановитьПериодОтчета(ОтчетОбъект, ПостроительОтчета); 
	
	Если Не ПолностьюПоТаблице Тогда
		отУстановитьИспользованиеИзмерений(ОтчетОбъект, ПостроительОтчета);
		Для Каждого ТекПорядок Из ПостроительОтчета.Порядок Цикл
			Попытка
				ПостроительОтчета.ВыбранныеПоля.Добавить(ТекПорядок.ПутьКДанным);
			Исключение
			КонецПопытки;
		КонецЦикла;
		
		Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
			Если НЕ ТекОтбор.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Попытка
				ПостроительОтчета.ВыбранныеПоля.Добавить(ТекОтбор.ПутьКДанным);
			Исключение
			КонецПопытки;
		КонецЦикла;
		
		ПостроительОтчета.Выполнить();
	КонецЕсли;
	
	// проверим корректность заполнения периода и измерений построителя
	Если НЕ отКорректностьЗаполнения(ОтчетОбъект, ПостроительОтчета) Тогда	Возврат Ложь; КонецЕсли;
	
	ТаблицаНастроекКолонокИсточника = Неопределено;
	
	ПоляНастройкиПостроителя = отПолучитьНастройкиКолонокДляИсточникаДанных(ОтчетОбъект, ТаблицаНастроекКолонокИсточника, Истина); //ПолностьюПоТаблице);
	
	ТаблицаИсточникаДанных = Новый ТаблицаЗначений;
	Для Каждого ТекКолонка Из ТаблицаНастроекКолонокИсточника Цикл
		ТаблицаИсточникаДанных.Колонки.Добавить(ТекКолонка.Имя, ТекКолонка.ТипЗначения);
	КонецЦикла;
	
	Результат = ОтчетОбъект.ПолучитьТаблицуИсточникаДанных(ПостроительОтчета, ТаблицаИсточникаДанных, ПоляНастройкиПостроителя);
	
	Если Результат = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПолностьюПоТаблице Тогда
		отПолучитьПоляДляИсточникаДанных(ОтчетОбъект, Результат, ПоляНастройкиПостроителя.Поля);
		отПолучитьСвойстваДляИсточникаДанных(ОтчетОбъект, Результат, ПоляНастройкиПостроителя.Свойства);
	КонецЕсли;
	
	ИсточникДанных = Новый ОписаниеИсточникаДанных(Результат);
	
	Попытка ОтчетОбъект.ИзменитьНастройкиКолонокИсточникаДанных(ТаблицаНастроекКолонокИсточника, ПоляНастройкиПостроителя);
	Исключение
	КонецПопытки;
	
	Для Каждого ТекКолонка Из ИсточникДанных.Колонки Цикл
		ТекНастройка = ТаблицаНастроекКолонокИсточника.Найти(ТекКолонка.Имя, "Имя");
		Если ТекНастройка = Неопределено Тогда Продолжить; КонецЕсли;
		ТекКолонка.Измерение	= ТекНастройка.Измерение;
		ТекКолонка.Итог			= ТекНастройка.Итог;
		ТекКолонка.ПутьКДанным	= ТекНастройка.ПутьКДанным;
	КонецЦикла;
	
	НастройкиПостроителя = ОтчетОбъект.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Истина);

	// устанавливаем источник данных построителя отчета
	ПостроительОтчета.ИсточникДанных = ИсточникДанных;
	ПостроительОтчета.ЗаполнитьНастройки();
	
	ОтборКоличество = ПостроительОтчета.Отбор.Количество();
	Для к = 1 По ОтборКоличество Цикл
		ПостроительОтчета.Отбор.Удалить(ОтборКоличество - к);
	КонецЦикла;
	
	Для Каждого ТекОтбор Из ОтчетОбъект.ПостроительОтчета.Отбор Цикл
		Если НЕ ТекОтбор.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ПолеОтбора = ТаблицаНастроекКолонокИсточника.Найти(ТекОтбор.ПутьКДанным, "ПутьКДанным");
		
		Если ПолеОтбора = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			НовыйОтбор = ПостроительОтчета.Отбор.Добавить(ПолеОтбора.Имя, ПолеОтбора.Имя, ТекОтбор.Представление);
			НовыйОтбор.Использование = Истина;
			НовыйОтбор.ВидСравнения  = ТекОтбор.ВидСравнения;
			НовыйОтбор.Значение      = ТекОтбор.Значение;
		Исключение
		КонецПопытки;
		
	КонецЦикла;
		
	// установим измерения, порядок и доп. поля отчета
	отУстановитьИспользованиеИзмеренийСИсточникомДанных(ОтчетОбъект, ПостроительОтчета, ПоляНастройкиПостроителя);
	
	// Расшифровки ячеек построителя будем заполнять по структуре со значениями группировок
	ПостроительОтчета.ЗаполнениеРасшифровки = ВидЗаполненияРасшифровкиПостроителяОтчета.Расшифровка;
	
	// Подвал отчета не выводится
	ПостроительОтчета.ВыводитьПодвалОтчета = Ложь;
	
	Возврат Истина;
КонецФункции // ПодготовкаПостроителяСИсточникомДанных()


///////////////////////////////////////////////////////////////////////////////
//ПРОЦЕДУРЫ И ФУНКЦИИ ПОСТРОЕНИЯ ТАБЛИЧНОГО ДОКУМЕНТА

// Процедура подготавливает макет табличного отчета в соответствии с его размерностью показателей, функций и т.д.
// Предназначена для работы со стандартизированным отчетом "ОтчетОбъект" с использованием построителя
// Формирует необходимое количество полей отступа для иерархического сдвига измерений
// Вызывается из функции "отСформироватьТабличныйДокумент" или аналогичных приватных функций отчета
// Используется перед назначением областей макета построителю
// ОтчетОбъект должен содержать следующие используемые в процедуре реквизиты :
// - Функции: Табличная часть
// - Функции.Имя: Строка
// - Функции.Использование: Булево
// - Показатели: Табличная часть
// - Показатели.Имя: Строка
// - Показатели.Использование: Булево
// СтруктураПараметров должна содержать следующие заполненные элементы :
// - КоличествоПоказателей:	Число (незаполненный, заполняется здесь)
// - КоличествоФункций:	Число (незаполненный, заполняется здесь)
// - МассивПоказателей: Массив (незаполненный, заполняется здесь)
// - ВыводДопРеквизитов: Булево (незаполненный, заполняется здесь) - Истина если нужен вывод доп. параметров в строках
// - ВыводитьДополнительныеПоляВОтдельнойКолонке: Булево, параметр настройки отчета
// - КоличествоДетальныхЗаписей: Число
// - МассивИндексовНезависимыхВыбранныхПолей: Массив
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект       - Объект отчета
//	МакетОтчета         - ТабличныйДокумент - Макет отчета
// 	СтруктураПараметров - Структура         - Структура параметров
//
Процедура отТабличныйДокументПоказателиИФункции(ОтчетОбъект, МакетОтчета, СтруктураПараметров) Экспорт
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	//получим данные из структуры параметров
	КоличествоДетальныхЗаписей = 				СтруктураПараметров.КоличествоДетальныхЗаписей;
	МассивИндексовНезависимыхВыбранныхПолей =	СтруктураПараметров.МассивИндексовНезависимыхВыбранныхПолей;
	
	// сформируем необходимое количество отступов для формирования древовидности отчета и его оформления
	ОбластьКолонкаОтступ = МакетОтчета.Область("Отступ");
	Для Инд=1 По (ПостроительОтчета.ИзмеренияСтроки.Количество() + КоличествоДетальныхЗаписей - 1) Цикл
		МакетОтчета.ВставитьОбласть(ОбластьКолонкаОтступ, ОбластьКолонкаОтступ, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
		ОбластьКолонкаОтступ=МакетОтчета.Область("Отступ");
	КонецЦикла;
	
	// создадим необходимое кол-во колонок Функций и показателей по табличным частям ОтчетОбъект
	// получим область показателей
	ИмяСекцииПоказатель = "";
	Если НЕ ПостроительОтчета.Параметры.Свойство("ИмяСекцииПоказатель",ИмяСекцииПоказатель) Тогда
		ИмяСекцииПоказатель = "Показатель";
	КонецЕсли;
	ОбластьКолонкаПоказатель = МакетОтчета.Область(ИмяСекцииПоказатель);
	// "размножим" на КоличествоФункций * КоличествоПоказателей с установкой соответствующих параметров в структуру
	Для Каждого СтрФункция Из ОтчетОбъект.Функции Цикл
		Если СтрФункция.Использование Тогда
			Для Каждого Показатель Из ОтчетОбъект.Показатели Цикл
				Если Показатель.Использование Тогда
					Если СтруктураПараметров.КоличествоПоказателей > 0 Тогда	// один там уже есть как образец
						МакетОтчета.ВставитьОбласть(ОбластьКолонкаПоказатель, ОбластьКолонкаПоказатель, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
						ОбластьКолонкаПоказатель = МакетОтчета.Область(ИмяСекцииПоказатель);
					КонецЕсли;
					
					Если СтруктураПараметров.КоличествоФункций = 0 Тогда
						СтруктураПараметров.КоличествоПоказателей = СтруктураПараметров.КоличествоПоказателей + 1;
					КонецЕсли; 
					СтруктураПараметров.МассивПоказателей.Добавить(Показатель.Имя+СтрФункция.Имя);
				КонецЕсли;
			КонецЦикла;
			СтруктураПараметров.КоличествоФункций=СтруктураПараметров.КоличествоФункций+1;
		КонецЕсли;
	КонецЦикла;	
	
	// Добавление необходимого количества колонок для вывода выбранных дополнительных полей
	ОбластьПоле = МакетОтчета.Область("Поле");
	СтруктураПараметров.ВыводДопРеквизитов = Ложь;
	Если (ПостроительОтчета.ВыбранныеПоля.Количество() - КоличествоДетальныхЗаписей > 0) Тогда
		// Определим, нужна ли дополнительные колонки для реквизитов измерений
		Для Сч = 0 По СтруктураПараметров.КоличествоИзмеренийСтроки Цикл
			Измерение = ПостроительОтчета.ИзмеренияСтроки[Сч];
			ВыводитьРеквизит = Ложь;
			Для Инд = 0 По ПостроительОтчета.ВыбранныеПоля.Количество()-1 Цикл
				Если (ПостроительОтчета.ВыбранныеПоля[Инд].ПутьКДанным <> Измерение.ПутьКДанным И
					Найти(ПостроительОтчета.ВыбранныеПоля[Инд].ПутьКДанным, Измерение.ПутьКДанным) > 0) Тогда
					Если Не СтруктураПараметров.ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
						СтруктураПараметров.ВыводДопРеквизитов = Истина;
						Прервать;
					КонецЕсли;
					ИмяПараметра = ПостроительОтчета.ВыбранныеПоля[Инд].Имя;
					ВыводитьРеквизит = Истина;
					СтруктураПараметров.ВыводДопРеквизитов = СтруктураПараметров.ВыводДопРеквизитов ИЛИ ВыводитьРеквизит;
					МакетОтчета.ВставитьОбласть(ОбластьПоле,ОбластьПоле,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
					МакетОтчета.Область(ОбластьПоле.Верх,ОбластьПоле.Лево+1,ОбластьПоле.Верх,ОбластьПоле.Лево+1).ШиринаКолонки=15;
					МакетОтчета.Область(ОбластьПоле.Верх,ОбластьПоле.Лево+1,ОбластьПоле.Верх,ОбластьПоле.Лево+1).РазмещениеТекста=ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					СтруктураПараметров.КоличествоКолонок = СтруктураПараметров.КоличествоКолонок+1;
					ОбластьПоле = МакетОтчета.Область("Поле");
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры // отТабличныйДокументПоказателиИФункции()

// Подготавливает и возвращает макет заголовка для построителя в соответствии с размерностью показателей, функций и т.д.
// Предназначена для работы со стандартизированным отчетом "ОтчетОбъект" с использованием построителя
// Вызывается из функции "отСформироватьТабличныйДокумент" или аналогичных приватных функций отчета
// Используется перед назначением макета заголовка построителю, формирует и форматирует все тексты заголовка
// ОтчетОбъект должен содержать следующие используемые в процедуре реквизиты :
// - ВидОтчета: Перечисления.ВидыОтчетов
// - ПостроительОтчета
// СтруктураПараметров должна содержать следующие заполненные элементы :
// - КоличествоИзмеренийСтроки: Число
// - КоличествоИзмеренийКолонки: Число
// - КоличествоДетальныхЗаписей: Число
// - КоличествоКолонок: Число
// - КоличествоФункций: Число
// - КоличествоПоказателей: Число
// - МассивИндексовНезависимыхВыбранныхПолей: Массив
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект       - Объект отчета
//	МакетОтчета         - ТабличныйДокумент - Макет отчета
// 	СтруктураПараметров - Структура         - Структура параметров
//	Заголовок			- Строка			- Заголовок отчета
//
// Возвращаемое значение:
//	ТабличныйДокумент - Область табличного документа
//
Функция отТабличныйДокументЗаголовок(ОтчетОбъект, МакетОтчета, СтруктураПараметров, Заголовок = "") Экспорт
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	//получим размерности из структуры параметров
	КоличествоИзмеренийСтроки  =				СтруктураПараметров.КоличествоИзмеренийСтроки;
	КоличествоИзмеренийКолонки =				СтруктураПараметров.КоличествоИзмеренийКолонки;
	КоличествоДетальныхЗаписей =				СтруктураПараметров.КоличествоДетальныхЗаписей;
	КоличествоКолонок = 						СтруктураПараметров.КоличествоКолонок;
	КоличествоФункций =							СтруктураПараметров.КоличествоФункций;
	КоличествоПоказателей =						СтруктураПараметров.КоличествоПоказателей;
	МассивИндексовНезависимыхВыбранныхПолей = 	СтруктураПараметров.МассивИндексовНезависимыхВыбранныхПолей;
	
	// Получим исходную область заголовка
	ИмяСекцииЗаголовок = "";
	Если НЕ ПостроительОтчета.Параметры.Свойство("ИмяСекцииЗаголовок",ИмяСекцииЗаголовок) Тогда
		ИмяСекцииЗаголовок = "Заголовок";
	КонецЕсли;
	ОбластьЗаголовка = МакетОтчета.Область(ИмяСекцииЗаголовок);
	
	КолКолонокОблИзмерений = 3 + КоличествоКолонок + КоличествоИзмеренийСтроки + КоличествоДетальныхЗаписей;
	КолКолонокОблДанных = КоличествоФункций*КоличествоПоказателей;
	МакетОтчета.Область(ОбластьЗаголовка.Верх,3,ОбластьЗаголовка.Низ,КолКолонокОблИзмерений).Очистить(Истина);	// Очистка всей области измерений заголовка
	МакетОтчета.Область(ОбластьЗаголовка.Верх,2,ОбластьЗаголовка.Низ,2).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;	// Строки заголовка отчета
	МакетОтчета.Область(ОбластьЗаголовка.Верх,2,ОбластьЗаголовка.Низ,КолКолонокОблИзмерений + КолКолонокОблДанных).ПоВыделеннымКолонкам = Истина; // Выравнивание всего заголовка
	ОбластьЗаголовка = МакетОтчета.ПолучитьОбласть(ИмяСекцииЗаголовок);	// заново получим отформатированную область
	
	// Описание периода
	Если (ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Остатки) ИЛИ (ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ПрочиеОстатки) Тогда
		Если ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
			ОписаниеПериода = "на " + Формат(ТекущаяДата(), "ДФ = ""дд.ММ.гггг ЧЧ:мм:сс""; ДП = ""...""");
		Иначе
			ОписаниеПериода = "на конец дня " + Формат(ОтчетОбъект.ДатаКонца, "ДФ = ""дд.ММ.гггг""; ДП = ""...""");
		КонецЕсли;
	ИначеЕсли ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Прочий Тогда
		ОписаниеПериода="";
	Иначе
		Если ОтчетОбъект.ДатаНачала = '00010101000000' И ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
			ОписаниеПериода     = "Весь рабочий период !";
		Иначе
			Если ОтчетОбъект.ДатаНачала = '00010101000000' ИЛИ ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
				ОписаниеПериода = "" + Формат(ОтчетОбъект.ДатаНачала, "ДФ = ""дд.ММ.гггг""; ДП = ""...""") 
				+ " - "      + Формат(ОтчетОбъект.ДатаКонца, "ДФ = ""дд.ММ.гггг""; ДП = ""...""");
			Иначе
				Если ОтчетОбъект.ДатаНачала <= ОтчетОбъект.ДатаКонца Тогда
					ОписаниеПериода = "" + ПредставлениеПериода(НачалоДня(ОтчетОбъект.ДатаНачала), КонецДня(ОтчетОбъект.ДатаКонца), "ФП = Истина");
				Иначе
					ОписаниеПериода = "Неправильно задан период!"
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Текст измерений строк
	СписокИзмерений = "";
	Для Сч=0 По КоличествоИзмеренийСтроки Цикл
		СписокИзмерений = СписокИзмерений +", "+ ПостроительОтчета.ИзмеренияСтроки[Сч].Представление
		+" "+ ПостроительОтчета.ИзмеренияСтроки[Сч].ТипИзмерения;
	КонецЦикла; 
	
	// Текст колонок дополнительных полей
	СписокКолонок = "";
	Для Сч=0 По ПостроительОтчета.ВыбранныеПоля.Количество()-1 Цикл
		Для Инд = 0 По ПостроительОтчета.ИзмеренияСтроки.Количество()-1 Цикл
			Если ПостроительОтчета.ВыбранныеПоля[Сч].ПутьКДанным <> ПостроительОтчета.ИзмеренияСтроки[Инд].ПутьКДанным
				И Найти(ПостроительОтчета.ВыбранныеПоля[Сч].ПутьКДанным, ПостроительОтчета.ИзмеренияСтроки[Инд].ПутьКДанным) >0 Тогда
				СписокКолонок = СписокКолонок +", "+ ПостроительОтчета.ВыбранныеПоля[Сч].Представление;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Текст дополнительных полей измерений строк
	СписокПолейДетальныхЗаписей = "";
	Если ПостроительОтчета.ВыводитьДетальныеЗаписи Тогда
		Для Каждого Инд Из МассивИндексовНезависимыхВыбранныхПолей Цикл
			СписокПолейДетальныхЗаписей = СписокПолейДетальныхЗаписей +", "+ ПостроительОтчета.ВыбранныеПоля[Инд].Представление;
		КонецЦикла;
	КонецЕсли; 
	
	// Текст показателей
	СписокПоказателей = "";
	Для Сч=0 По ОтчетОбъект.Показатели.Количество()-1 Цикл
		Если ОтчетОбъект.Показатели[Сч].Использование Тогда
			СписокПоказателей = СписокПоказателей +", "+ ОтчетОбъект.Показатели[Сч].Представление;
		КонецЕсли; 
	КонецЦикла; 
	
	// Текст отборов
	Если ПостроительОтчета.Отбор.Количество() = 0 Тогда	СписокОтбор = "  Без отбора";
	Иначе СписокОтбор = "";
		Для Сч=0 По ПостроительОтчета.Отбор.Количество()-1 Цикл
			Если ПостроительОтчета.Отбор[Сч].Использование И Не ПостроительОтчета.Отбор[Сч].Имя = "Периодичность" Тогда
				СписокОтбор = СписокОтбор +", "+ ПостроительОтчета.Отбор[Сч].Представление 
				+" "+ ПостроительОтчета.Отбор[Сч].ВидСравнения +" "+ ПостроительОтчета.Отбор[Сч].Значение;
			КонецЕсли;
		КонецЦикла;
		Если СписокОтбор = "" Тогда СписокОтбор = "  Все"; КонецЕсли; 
	КонецЕсли; 
	
	ОбластьЗаголовка.Параметры.ЗаголовокОтчета = Заголовок+" "+ОписаниеПериода;
	// ОбластьЗаголовка.Параметры.Период     = "Период: "+ОписаниеПериода;
	ОбластьЗаголовка.Параметры.Измерения  = "Итоги по: "+Сред(СписокИзмерений,2);
	ОбластьЗаголовка.Параметры.Показатели = "Показатели: " +Сред(СписокПоказателей,2);
	ОбластьЗаголовка.Параметры.Колонки = 
	?((КоличествоИзмеренийСтроки > 0 И Не ПустаяСтрока(СписокКолонок)), "Дополнительные поля: "+ Сред(СписокКолонок, 2)+" ", "") 
	+ ?(ПостроительОтчета.ВыводитьДетальныеЗаписи, "Поля детальных записей: "+Сред(СписокПолейДетальныхЗаписей, 2),"");
	ОбластьЗаголовка.Параметры.Отбор      = "Отбор:"    +Сред(СписокОтбор,2);
	
	Возврат ОбластьЗаголовка;
КонецФункции // отТабличныйДокументЗаголовок()

// Подготавливает и возвращает макет шапки для построителя в соответствии с размерностью показателей, функций и т.д.
// Предназначена для работы со стандартизированным отчетом "ОтчетОбъект" с использованием построителя
// Вызывается из функции "отСформироватьТабличныйДокумент" или аналогичных приватных функций отчета
// Используется перед назначением макета шапки построителю, формирует и форматирует все тексты ячеек шапки
// ОтчетОбъект должен содержать следующие используемые в процедуре реквизиты :
// - ВидОтчета: Перечисления.ВидыОтчетов
// - ПостроительОтчета
// - Функции: Табличная часть
// - Функции.Имя: Строка
// - Функции.Использование: Булево
// - Показатели: Табличная часть
// - Показатели.Имя: Строка
// - Показатели.Использование: Булево
// СтруктураПараметров должна содержать следующие заполненные элементы :
// - КоличествоИзмеренийСтроки: Число
// - КоличествоИзмеренийКолонки: Число
// - КоличествоДетальныхЗаписей: Число
// - КоличествоКолонок: Число
// - КоличествоФункций: Число
// - КоличествоПоказателей: Число
// - МассивИндексовНезависимыхВыбранныхПолей: Массив
// - ВыводитьДополнительныеПоляВОтдельнойКолонке: Булево
// - ГраницаСплошная: ГраницаЯчейки
// - ГраницаСплошнаяТолстая: ГраницаЯчейки
// - ГраницаДвойная: ГраницаЯчейки
// - ГраницаТочечная: ГраницаЯчейки
// - ГраницаНетЛинии: ГраницаЯчейки
//
// Параметры:
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект                     - Объект отчета
//	МакетОтчета         - ТабличныйДокумент               - Макет отчета
// 	СтруктураПараметров - Структура                       - Структура параметров
//	ОформлениеШапки		- ОбластьЯчеекТабличногоДокумента - Оформление шапки
//
// Возвращаемое значение:
//	ТабличныйДокумент - Область табличного документа
//
Функция отТабличныйДокументШапкаОтчета(ОтчетОбъект, МакетОтчета, СтруктураПараметров, ОформлениеШапки) Экспорт
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	//получим данные из структуры параметров
	КоличествоИзмеренийСтроки  =					СтруктураПараметров.КоличествоИзмеренийСтроки;
	КоличествоИзмеренийКолонки =					СтруктураПараметров.КоличествоИзмеренийКолонки;
	КоличествоДетальныхЗаписей =					СтруктураПараметров.КоличествоДетальныхЗаписей;
	КоличествоКолонок = 							СтруктураПараметров.КоличествоКолонок;
	КоличествоФункций = 							СтруктураПараметров.КоличествоФункций;
	КоличествоПоказателей = 						СтруктураПараметров.КоличествоПоказателей;
	МассивИндексовНезависимыхВыбранныхПолей =		СтруктураПараметров.МассивИндексовНезависимыхВыбранныхПолей;
	ВыводитьДополнительныеПоляВОтдельнойКолонке =	СтруктураПараметров.ВыводитьДополнительныеПоляВОтдельнойКолонке;
	ГраницаСплошная =								СтруктураПараметров.ГраницаСплошная;
	ГраницаСплошнаяТолстая =						СтруктураПараметров.ГраницаСплошнаяТолстая;
	ГраницаДвойная =								СтруктураПараметров.ГраницаДвойная;
	ГраницаТочечная =								СтруктураПараметров.ГраницаТочечная;
	ГраницаНетЛинии =								СтруктураПараметров.ГраницаНетЛинии;
	КолКолонокОблИзмерений = 3 + КоличествоКолонок + КоличествоИзмеренийСтроки + КоличествоДетальныхЗаписей;
	КолКолонокОблДанных = КоличествоФункций*КоличествоПоказателей;
	
	ИмяСекцииШапкаТаблицы = "";
	Если НЕ ПостроительОтчета.Параметры.Свойство("ИмяСекцииШапкаТаблицы",ИмяСекцииШапкаТаблицы) Тогда
		ИмяСекцииШапкаТаблицы = "ШапкаТаблицы";
	КонецЕсли;
	ИмяСекцииШапкаТаблицыСтрока = "";
	Если НЕ ПостроительОтчета.Параметры.Свойство("ИмяСекцииШапкаТаблицыСтрока",ИмяСекцииШапкаТаблицыСтрока) Тогда
		ИмяСекцииШапкаТаблицыСтрока = "ШапкаТаблицыСтрока";
	КонецЕсли;
	МакетШапкиТаблицы = МакетОтчета.ПолучитьОбласть(ИмяСекцииШапкаТаблицы);
	// добавим строки шапки в случае наличия измерений - колонок (горизонтальная развертка)
	Для Сч=1 По КоличествоИзмеренийКолонки - 1 Цикл
		ШапкаТаблицыСтрока = МакетШапкиТаблицы.Область(ИмяСекцииШапкаТаблицыСтрока);
		МакетШапкиТаблицы.ВставитьОбласть(ШапкаТаблицыСтрока,,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЦикла; 
	
	Кол = 0 ;
	// назначение имен колонкам шапки и оформление рамками
	ОбластьШапкаТаблицы = МакетОтчета.Область(ИмяСекцииШапкаТаблицы);
	Для Каждого СтрФункция Из ОтчетОбъект.Функции Цикл
		ФункцияОбновилась=Ложь;
		Если СтрФункция.Использование = Истина Тогда
			ФункцияОбновилась=Истина;
			МакетШапкиТаблицы.Область(1,КолКолонокОблИзмерений + Кол).Текст = СтрФункция.Представление;
			МакетШапкиТаблицы.Область(1,КолКолонокОблИзмерений + Кол).Обвести(ГраницаДвойная,ГраницаСплошнаяТолстая,ГраницаДвойная,ГраницаСплошнаяТолстая);
			Для Каждого Показатель Из ОтчетОбъект.Показатели Цикл
				Если Показатель.Использование = Истина Тогда
					МакетШапкиТаблицы.Область(1+?(КоличествоИзмеренийКолонки=0,1,КоличествоИзмеренийКолонки),КолКолонокОблИзмерений + Кол).Текст = Показатель.Представление;
					МакетШапкиТаблицы.Область(1+?(КоличествоИзмеренийКолонки=0,1,КоличествоИзмеренийКолонки),КолКолонокОблИзмерений + Кол).РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					МакетШапкиТаблицы.Область(1+?(КоличествоИзмеренийКолонки=0,1,КоличествоИзмеренийКолонки),КолКолонокОблИзмерений + Кол).Обвести(?(ФункцияОбновилась,ГраницаДвойная,ГраницаСплошнаяТолстая),ГраницаСплошнаяТолстая,,ГраницаСплошнаяТолстая);
					Кол=Кол+1; ФункцияОбновилась=Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	// Обводная рамка шапки
	МакетШапкиТаблицы.Область(1,КолКолонокОблИзмерений + Кол - 1).ГраницаСправа = ГраницаСплошнаяТолстая;
	МакетШапкиТаблицы.Область(1+?(КоличествоИзмеренийКолонки = 0, 1, КоличествоИзмеренийКолонки), КолКолонокОблИзмерений + Кол - 1).ГраницаСправа = ГраницаСплошнаяТолстая;
	
	// объединим ячейки названий функций
	Кол=0;
	Для Каждого СтрФункция Из ОтчетОбъект.Функции Цикл
		Если СтрФункция.Использование = Истина Тогда
			МакетШапкиТаблицы.Область(1,КолКолонокОблИзмерений + Кол*КоличествоПоказателей
			,1,КолКолонокОблИзмерений-1+(Кол+1)*КоличествоПоказателей).Объединить();
			Кол=Кол+1;
		КонецЕсли
	КонецЦикла;
	
	// Заголовок первой колонки
	Если (ПостроительОтчета.ИзмеренияСтроки.Количество() + КоличествоДетальныхЗаписей > 0) Тогда
		ЗаголовокКолонки="";
		// В заголовке колонки детальной записи оставим только название последнего поля
		Для Сч = 0 По КоличествоИзмеренийСтроки Цикл
			текЗаголовокКолонки = ПостроительОтчета.ИзмеренияСтроки[Сч].Представление;
			Для Поз = 1 По СтрЧислоВхождений(ЗаголовокКолонки, ".") Цикл
				текЗаголовокКолонки = Сред(текЗаголовокКолонки, Найти(текЗаголовокКолонки,".")+1);
			КонецЦикла;
			ЗаголовокКолонки = ЗаголовокКолонки+" / "+текЗаголовокКолонки;
		КонецЦикла; 
		ЗаголовокКолонки = Сред(ЗаголовокКолонки,4);
		
		Для Каждого Инд Из МассивИндексовНезависимыхВыбранныхПолей Цикл
			текЗаголовокКолонки = ПостроительОтчета.ВыбранныеПоля[Инд].Представление;
			Для Поз = 1 По СтрЧислоВхождений(ЗаголовокКолонки, ".") Цикл
				текЗаголовокКолонки = Сред(текЗаголовокКолонки, Найти(текЗаголовокКолонки,".")+1);
			КонецЦикла;
			ЗаголовокКолонки = ЗаголовокКолонки+" / "+текЗаголовокКолонки;	
		КонецЦикла;
		
		//Оформление ячеек заголовка
		МакетШапкиТаблицы.Область(1,2,1+?(КоличествоИзмеренийКолонки = 0, 1, КоличествоИзмеренийКолонки),КолКолонокОблИзмерений-КоличествоКолонок).Объединить();
		МакетШапкиТаблицы.Область(1,2).Текст = ЗаголовокКолонки;
		МакетШапкиТаблицы.Область(1,2).ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		МакетШапкиТаблицы.Область(1,2,1+?(КоличествоИзмеренийКолонки = 0, 1, КоличествоИзмеренийКолонки),2).ГраницаСлева = ГраницаСплошнаяТолстая;
		//Нарисуем границу на краю шапки
		МакетШапкиТаблицы.Область(1,КолКолонокОблИзмерений-1+КолКолонокОблДанных,1,КолКолонокОблИзмерений-1+КолКолонокОблДанных).ГраницаСправа = ГраницаСплошнаяТолстая;
		
		// Выведем заголовки детальных записей при выводе в столбце
		Для Сч = 1 По КоличествоКолонок-1 Цикл
			текЗаголовокКолонки = ПостроительОтчета.ВыбранныеПоля[Сч-1].Представление;
			Для Поз = 1 По СтрЧислоВхождений(текЗаголовокКолонки, ".") Цикл
				текЗаголовокКолонки = Сред(текЗаголовокКолонки, Найти(текЗаголовокКолонки,".")+1);
			КонецЦикла;
			Строка = 1+?(КоличествоИзмеренийКолонки = 0, 1, КоличествоИзмеренийКолонки);
			Столбец = 3 + КоличествоИзмеренийСтроки+Сч;
			Область = МакетШапкиТаблицы.Область(Строка,Столбец,Строка,Столбец);
			Область.Текст = текЗаголовокКолонки;
			Область.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр
		КонецЦикла; 
		
	ИначеЕсли НЕ ПостроительОтчета.ВыводитьДетальныеЗаписи Тогда
		МакетШапкиТаблицы.Область(2,2).Текст = "Общий итог";
		
	КонецЕсли;
	
	ОбластьПоле = МакетОтчета.Область("Поле");
	
	Если ПостроительОтчета.ВыводитьДетальныеЗаписи Тогда
		Если ПостроительОтчета.ИзмеренияСтроки.Количество()>0 Тогда
			ОбластьШапкаТаблицыСтрока = МакетОтчета.Область("ШапкаТаблицыСтрока");
		Иначе
			ОбластьШапкаТаблицыСтрока = МакетОтчета.Область("ШапкаТаблицы");
		КонецЕсли; 
		
		Для Сч=1 По МассивИндексовНезависимыхВыбранныхПолей.Количество()-1 Цикл  // на 1(2) меньше количества полей - одно уже есть
			// шапочку пока не трогаем
			МакетШапкиТаблицы.ВставитьОбласть(ОбластьПоле,ОбластьПоле,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
			КоличествоКолонок = КоличествоКолонок+1;
			ОбластьПоле = МакетОтчета.Область("Поле");
		КонецЦикла;
		// Все поля, которые не подчинены группировкам, будут колонками детальных записей
		Сч = 0;
		Для Каждого Инд ИЗ МассивИндексовНезависимыхВыбранныхПолей Цикл
			// В заголовке колонки детальной записи оставим только название последнего поля
			ЗаголовокКолонки = ПостроительОтчета.ВыбранныеПоля[Инд].Представление;
			ЗаголовокКолонки = СтрЗаменить(ЗаголовокКолонки, ". ", "__");
			Для Поз = 1 По СтрЧислоВхождений(ЗаголовокКолонки, ".") Цикл
				ЗаголовокКолонки = Сред(ЗаголовокКолонки, Найти(ЗаголовокКолонки,".")+1);
			КонецЦикла;
			ЗаголовокКолонки = СтрЗаменить(ЗаголовокКолонки, "__", ". ");
			МакетШапкиТаблицы.Область(ОбластьШапкаТаблицыСтрока.Низ,1+1+Сч).Текст = ЗаголовокКолонки;
			Сч = Сч+1;
		КонецЦикла; 
	КонецЕсли;
	
	Область = МакетШапкиТаблицы.Область(1,2,1+?(КоличествоИзмеренийКолонки=0,1,КоличествоИзмеренийКолонки),КолКолонокОблИзмерений-1+КолКолонокОблДанных);
	Область.ЦветФона   = 		ОформлениеШапки.ЦветФона;
	Область.ЦветТекста = 		ОформлениеШапки.ЦветТекста;
	Область.Шрифт =				ОформлениеШапки.Шрифт;
	Область.РазмещениеТекста =	ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	
	Возврат МакетШапкиТаблицы;
КонецФункции // отТабличныйДокументШапкаОтчета()

// Подготавливает и возвращает макет детальной записи для построителя в соответствии с размерностью
// показателей, функций и т.д.
// Предназначена для работы со стандартизированным отчетом "ОтчетОбъект" с использованием построителя
// Вызывается из функции "отСформироватьТабличныйДокумент" или аналогичных приватных функций отчета
// Используется перед назначением макета строки построителю, форматирует оформление ячеек детальной записи
// ОтчетОбъект должен содержать следующие используемые в процедуре реквизиты :
// - ВидОтчета: Перечисления.ВидыОтчетов
// - ПостроительОтчета
// СтруктураПараметров должна содержать следующие заполненные элементы :
// - КоличествоИзмеренийСтроки: Число
// - КоличествоИзмеренийКолонки: Число
// - КоличествоДетальныхЗаписей: Число
// - КоличествоКолонок: Число
// - КоличествоФункций: Число
// - КоличествоПоказателей: Число
// - ВыводитьДополнительныеПоляВОтдельнойКолонке: Булево
// - МассивИндексовНезависимыхВыбранныхПолей: Массив
// - ГраницаСплошная: ГраницаЯчейки
// - ГраницаСплошнаяТолстая: ГраницаЯчейки
// - ГраницаДвойная: ГраницаЯчейки
// - ГраницаТочечная: ГраницаЯчейки
// - ГраницаНетЛинии: ГраницаЯчейки
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект                     - Объект отчета
//	МакетОтчета         - ТабличныйДокумент               - Макет отчета
// 	СтруктураПараметров - Структура         			  - Структура параметров
//  ОформлениеСтроки    - ОбластьЯчеекТабличногоДокумента - Оформление строки
//
// Возвращаемое значение:
//	ТабличныйДокумент - Область табличного документа
//
Функция отТабличныйДокументДетальныеЗаписи(ОтчетОбъект, МакетОтчета, СтруктураПараметров, ОформлениеСтроки) Экспорт
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	//получим из структуры параметров
	ГраницаСплошная =				СтруктураПараметров.ГраницаСплошная;
	ГраницаСплошнаяТолстая =		СтруктураПараметров.ГраницаСплошнаяТолстая;
	ГраницаДвойная =				СтруктураПараметров.ГраницаДвойная;
	ГраницаТочечная =				СтруктураПараметров.ГраницаТочечная;
	ГраницаНетЛинии =				СтруктураПараметров.ГраницаНетЛинии;
	КоличествоИзмеренийСтроки  =	СтруктураПараметров.КоличествоИзмеренийСтроки;
	КоличествоИзмеренийКолонки = 	СтруктураПараметров.КоличествоИзмеренийКолонки;
	КоличествоДетальныхЗаписей =	СтруктураПараметров.КоличествоДетальныхЗаписей;
	КоличествоКолонок =				СтруктураПараметров.КоличествоКолонок;
	КоличествоФункций = 			СтруктураПараметров.КоличествоФункций;
	КоличествоПоказателей =			СтруктураПараметров.КоличествоПоказателей;
	СтруктураФорматаПолей =			СтруктураПараметров.СтруктураФорматаПолей;
	ВыводитьДополнительныеПоляВОтдельнойКолонке = СтруктураПараметров.ВыводитьДополнительныеПоляВОтдельнойКолонке;
	МассивИндексовНезависимыхВыбранныхПолей = СтруктураПараметров.МассивИндексовНезависимыхВыбранныхПолей;
	КолКолонокОблИзмерений = 3 + КоличествоКолонок + КоличествоИзмеренийСтроки + КоличествоДетальныхЗаписей;
	КолКолонокОблДанных = КоличествоФункций*КоличествоПоказателей;
	
	// Определимся с исходной областью макета
	ИмяСекцииДетальныеЗаписи = "";
	Если КоличествоДетальныхЗаписей > 2 Тогда
		ИмяСекцииДетальныеЗаписи = "СтрокаДетали";
		ИмяОдиночнойСекцииДетальныеЗаписи = "СтрокаДеталь";
		МакетДетали = МакетОтчета.Область(ИмяОдиночнойСекцииДетальныеЗаписи);
		Для Сч = 0 По МассивИндексовНезависимыхВыбранныхПолей.Количество() - 3 Цикл
			МакетОтчета.ВставитьОбласть(МакетДетали, МакетДетали, ТипСмещенияТабличногоДокумента.ПоВертикали);
			МакетДетали = МакетОтчета.Область(ИмяОдиночнойСекцииДетальныеЗаписи);
		КонецЦикла;
	ИначеЕсли КоличествоДетальныхЗаписей <= 1 Тогда
		ИмяСекцииДетальныеЗаписи = "СтрокаДеталь";
	ИначеЕсли КоличествоДетальныхЗаписей = 2 Тогда
		ИмяСекцииДетальныеЗаписи = "СтрокаДетали";
	КонецЕсли;
	МакетДетали = МакетОтчета.Область(ИмяСекцииДетальныеЗаписи);
	СмещениеДетали = КоличествоИзмеренийСтроки + 1;
	
	// для каждого независимого дополнительного поля
	Для Сч = 0 По МассивИндексовНезависимыхВыбранныхПолей.Количество() - 1 Цикл
		ОбластьДетали = МакетОтчета.Область(МакетДетали.Верх+Сч,,МакетДетали.Верх+Сч);
		
		Для Инд=0 По СмещениеДетали+Сч Цикл
			МакетОтчета.Область(ОбластьДетали.Верх,2+Инд).Очистить(Истина);
			МакетОтчета.Область(ОбластьДетали.Верх,2+Инд).Обвести(,,ГраницаТочечная,);
		КонецЦикла;
		
		Инд = МассивИндексовНезависимыхВыбранныхПолей[Сч];
		Поле = ПостроительОтчета.ВыбранныеПоля[Инд];
		
		МакетОтчета.Область(ОбластьДетали.Верх,2+КоличествоИзмеренийСтроки+Сч+1,
		ОбластьДетали.Верх,КолКолонокОблИзмерений-1+КолКолонокОблДанных).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетОтчета.Область(ОбластьДетали.Верх,2+КоличествоИзмеренийСтроки+Сч+1,ОбластьДетали.Верх+Сч,2+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей).ПараметрРасшифровки = Поле.Имя;
		МакетОтчета.Область(ОбластьДетали.Верх,2+КоличествоИзмеренийСтроки+Сч+1,ОбластьДетали.Верх+Сч,2+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей).Параметр = Поле.Имя;
		МакетОтчета.Область(ОбластьДетали.Верх,2+КоличествоИзмеренийСтроки+Сч+1,ОбластьДетали.Верх+Сч,КолКолонокОблИзмерений).ПоВыделеннымКолонкам=Истина;
		Область = МакетОтчета.Область(ОбластьДетали.Верх, 2+КоличествоИзмеренийСтроки+Сч+1, ОбластьДетали.Верх, 2+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей);
		Область.Обвести(,ГраницаСплошная,,);
		МакетОтчета.Область(ОбластьДетали.Верх, 2).ГраницаСЛева = ГраницаСплошная;
		
		Кол = 0;
		Для Каждого СтрФункция Из ОтчетОбъект.Функции Цикл
			ФункцияОбновилась=Ложь;
			Если СтрФункция.Использование = Истина Тогда
				ФункцияОбновилась=Истина;
				Для Каждого Показатель Из ОтчетОбъект.Показатели Цикл
					Если Показатель.Использование = Истина Тогда
						ИмяПоказателя = Показатель.Имя+СтрФункция.Имя;
						ФорматПоказателя = Показатель.ФорматнаяСтрока;
						МакетОтчета.Область(ОбластьДетали.Верх,КолКолонокОблИзмерений+Кол).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
						МакетОтчета.Область(ОбластьДетали.Верх,КолКолонокОблИзмерений+Кол).Параметр = ИмяПоказателя;
						МакетОтчета.Область(ОбластьДетали.Верх,КолКолонокОблИзмерений+Кол).Формат= ФорматПоказателя;
						МакетОтчета.Область(ОбластьДетали.Верх,КолКолонокОблИзмерений+Кол).Обвести(?(ФункцияОбновилась,ГраницаДвойная,ГраницаСплошная),ГраницаСплошная,,ГраницаСплошная);
						Кол = Кол+1; ФункцияОбновилась=Ложь;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли; 
		КонецЦикла;
		МакетОтчета.Область(ОбластьДетали.Верх,3+КоличествоКолонок+Кол-1+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей).ГраницаСправа=ГраницаСплошнаяТолстая;
		
		// Покрасим область в соответствии с макетом и отступом
		Размер=Мин(КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+1, ОформлениеСтроки.Количество());
		Начало=Число(Сред("87654332211", Размер, 1 ))-1;
		Для Инд = 1 По КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей Цикл
			Индекс = Мин(Инд+Начало-1,ОформлениеСтроки.Количество());
			Область = МакетОтчета.Область(ОбластьДетали.Верх,2+Инд-1);
			Область.ЦветФона   =	ОформлениеСтроки[Индекс].ЦветФона;
			Область.ЦветТекста =	ОформлениеСтроки[Индекс].ЦветТекста;
			Область.Шрифт =			ОформлениеСтроки[Индекс].Шрифт;
		КонецЦикла;
		Индекс = Мин(КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+Начало,ОформлениеСтроки.Количество());
		Область = МакетОтчета.Область(ОбластьДетали.Верх,2+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей,ОбластьДетали.Верх,2+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+КоличествоПоказателей*КоличествоФункций);
		Область.ЦветФона   =	ОформлениеСтроки[Индекс].ЦветФона;
		Область.ЦветТекста =	ОформлениеСтроки[Индекс].ЦветТекста;
		Область.Шрифт =			ОформлениеСтроки[Индекс].Шрифт;
		
		// объединим ячейки измерения
		МакетОтчета.Область(ОбластьДетали.Верх,2+КоличествоИзмеренийСтроки+Сч+1,ОбластьДетали.Верх,КолКолонокОблИзмерений-1).Объединить();
	КонецЦикла;
	
	Возврат МакетДетали;
КонецФункции // отТабличныйДокументДетальныеЗаписи()

// Подготавливает и возвращает макет общих итогов для построителя в соответствии с размерностью
// показателей, функций и т.д.
//// Предназначена для работы со стандартизированным отчетом "ОтчетОбъект" с использованием построителя
//// Вызывается из функции "отСформироватьТабличныйДокумент" или аналогичных приватных функций отчета
//// Используется перед назначением макета строки построителю, форматирует оформление ячеек общих итогов
//// ОтчетОбъект должен содержать следующие используемые в процедуре реквизиты :
//// - ВидОтчета: Перечисления.ВидыОтчетов
//// - ПостроительОтчета
//// СтруктураПараметров должна содержать следующие заполненные элементы :
//// - КоличествоИзмеренийСтроки: Число
//// - КоличествоИзмеренийКолонки: Число
//// - КоличествоДетальныхЗаписей: Число
//// - КоличествоКолонок: Число
//// - КоличествоФункций: Число
//// - КоличествоПоказателей: Число
//// - ВыводитьДополнительныеПоляВОтдельнойКолонке: Булево
//// - МассивИндексовНезависимыхВыбранныхПолей: Массив
//// - ГраницаСплошная: ГраницаЯчейки
//// - ГраницаСплошнаяТолстая: ГраницаЯчейки
//// - ГраницаДвойная: ГраницаЯчейки
//// - ГраницаТочечная: ГраницаЯчейки
//// - ГраницаНетЛинии: ГраницаЯчейки
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект                     - Объект отчета
//	МакетОтчета         - ТабличныйДокумент               - Макет отчета
// 	СтруктураПараметров - Структура         			  - Структура параметров
//  ОформлениеИтоги     - ОбластьЯчеекТабличногоДокумента - Оформление общих итогов
//
// Возвращаемое значение:
//	ТабличныйДокумент - Область табличного документа
//
Функция отТабличныйДокументОбщиеИтоги(ОтчетОбъект, МакетОтчета, СтруктураПараметров, ОформлениеИтоги) Экспорт
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	ИмяСекцииОбщиеИтоги = "";
	Если НЕ ПостроительОтчета.Параметры.Свойство("ИмяСекцииОбщиеИтоги",ИмяСекцииОбщиеИтоги) Тогда
		ИмяСекцииОбщиеИтоги = "ОбщиеИтоги";
	КонецЕсли;
	
	//получим из структуры параметров
	ГраницаСплошная = СтруктураПараметров.ГраницаСплошная;
	ГраницаСплошнаяТолстая = СтруктураПараметров.ГраницаСплошнаяТолстая;
	ГраницаДвойная = СтруктураПараметров.ГраницаДвойная;
	ГраницаТочечная = СтруктураПараметров.ГраницаТочечная;
	ГраницаНетЛинии = СтруктураПараметров.ГраницаНетЛинии;
	
	КоличествоИзмеренийСтроки  = СтруктураПараметров.КоличествоИзмеренийСтроки;
	КоличествоИзмеренийКолонки = СтруктураПараметров.КоличествоИзмеренийКолонки;
	КоличествоДетальныхЗаписей = СтруктураПараметров.КоличествоДетальныхЗаписей;
	
	КоличествоКолонок = СтруктураПараметров.КоличествоКолонок;
	КоличествоФункций = СтруктураПараметров.КоличествоФункций;
	КоличествоПоказателей = СтруктураПараметров.КоличествоПоказателей;
	
	Попытка
		СтруктураСвязиПоказателейИИзмерений = СтруктураПараметров.СтруктураСвязиПоказателейИИзмерений;
	Исключение
		СтруктураСвязиПоказателейИИзмерений = Новый Структура();
	КонецПопытки;
	//
	МакетОбщиеИтоги = МакетОтчета.Область(ИмяСекцииОбщиеИтоги);
	// Формирование макета общих итогов
	МакетОтчета.Область(МакетОбщиеИтоги.Верх,2+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей,
	МакетОбщиеИтоги.Верх,2+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+
	КоличествоФункций*КоличествоПоказателей).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	
	Область =  МакетОтчета.Область(МакетОбщиеИтоги.Верх, 2, МакетОбщиеИтоги.Верх, 2+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей);
	Область.Объединить();
	
	Область.ГраницаСверху = ГраницаДвойная;
	Область.ГраницаСправа = ГраницаДвойная;
	Область.ГраницаСнизу = ГраницаСплошнаяТолстая;
	
	Область = МакетОтчета.Область(МакетОбщиеИтоги.Верх,2);
	Область.Текст = "ИТОГО:";
	
	Область.ГоризонтальноеПоложение=ГоризонтальноеПоложение.Право;
	Область = МакетОтчета.Область(МакетОбщиеИтоги.Верх, 2, МакетОбщиеИтоги.Верх, 
	2+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+КоличествоФункций*КоличествоПоказателей);
	Область.ЦветФона   = ОформлениеИтоги.ЦветФона;
	Область.ЦветТекста = ОформлениеИтоги.ЦветТекста;
	Область.Шрифт = ОформлениеИтоги.Шрифт;
	Область.ГраницаСверху = ГраницаДвойная;
	
	Кол = 0;
	
	Для Каждого СтрФункция Из ОтчетОбъект.Функции Цикл
		ФункцияОбновилась=Ложь;
		Если СтрФункция.Использование = Истина Тогда
			ФункцияОбновилась=Истина;
			Для Каждого Показатель Из ОтчетОбъект.Показатели Цикл
				Если Показатель.Использование = Истина Тогда
					ИмяПоказателя = Показатель.Имя+СтрФункция.Имя;
					Параметр = ИмяПоказателя;
					
					// если для данного показателя заданы измерения, то посмотрим входит ли это изменение в состав исключаемых
					// для вывода данного показателя
					Если  СтруктураСвязиПоказателейИИзмерений.Свойство(Показатель.Имя) Тогда
						Параметр = "";	
					КонецЕсли;  
					
					
					ФорматПоказателя = Показатель.ФорматнаяСтрока;
					МакетОтчета.Область(МакетОбщиеИтоги.Верх,3+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+Кол).Параметр = Параметр;
					МакетОтчета.Область(МакетОбщиеИтоги.Верх,3+КоличествоКолонок+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей+Кол).Формат= ФорматПоказателя;
					МакетОтчета.Область(МакетОбщиеИтоги.Верх,3+КоличествоКолонок+Кол+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей).Обвести(?(ФункцияОбновилась,ГраницаДвойная,ГраницаСплошнаяТолстая),ГраницаДвойная,,ГраницаСплошнаяТолстая);
					Кол = Кол+1; ФункцияОбновилась=Ложь;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла;
	МакетОтчета.Область(МакетОбщиеИтоги.Верх,3+КоличествоКолонок+Кол-1+КоличествоИзмеренийСтроки+КоличествоДетальныхЗаписей).ГраницаСправа=ГраницаСплошнаяТолстая;
	
	Возврат МакетОбщиеИтоги;
КонецФункции // отТабличныйДокументОбщиеИтоги()

// формирует отчет в виде табличного документа и возвращает её в качестве результата
// Предназначена для работы со стандартизированным отчетом "ОтчетОбъект" с использованием построителя
// ОтчетОбъект должен содержать следующие используемые реквизиты :
// - ДатаНачала: Дата
// - ДатаКонца: Дата
// - ВидОтчета: Перечисления.ВидыОтчетов (Остатки, Обороты, ОстаткиИОбороты)
// - ПостроительОтчета
// - ИмяМакетаОформленияТабличногоДокумента: Строка, необязательный -
//	 нужен для выбора альтернативной схемы (раскраски) отчета
// - ВыводитьДополнительныеПоляВОтдельнойКолонке: Булево, необязательный -
//	 нужен для вывода дополнительных полей в отдельной колонке
// - ИмяФормыНастроек: Строка, необязательный для получения заголовка отчета из формы настроек
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект                     - Объект отчета
//	МакетОтчета         - ТабличныйДокумент               - Макет отчета
// 	СтруктураПараметров - Структура         			  - Структура параметров
//  ОформлениеСтроки    - ОбластьЯчеекТабличногоДокумента - Оформление строки
//	ПраваПользователя	- Неопределено					  - Права пользователя
//
// Возвращаемое значение:
//	ТабличныйДокумент - Табличный документ
//
Функция отСформироватьТабличныйДокумент(ОтчетОбъект, ДокументРезультат, ПараметрФормирования=0, ПраваПользователя=Неопределено) Экспорт
	
	Попытка
		Результат = зфСформироватьТабличныйДокумент(ОтчетОбъект, ДокументРезультат, ПараметрФормирования, ПраваПользователя);
	Исключение
		Сообщить(ОписаниеОшибки());
		Результат = ДокументРезультат;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // отСформироватьТабличныйДокумент()

// формирует таблицу заголовка отчета
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
// 	ИсходныйШрифт	  - Шрифт			  - Шрифт
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица заголовка
//
Функция отПолучитьТаблицуЗаголовкаОтчета(ОтчетОбъект, ПостроительОтчета, ИсходныйШрифт) Экспорт
	
	ТаблицаЗаголовка = Новый ТаблицаЗначений;
	ТаблицаЗаголовка.Колонки.Добавить("ИмяСтроки");
	ТаблицаЗаголовка.Колонки.Добавить("Текст");
	ТаблицаЗаголовка.Колонки.Добавить("ЦветТекста");
	ТаблицаЗаголовка.Колонки.Добавить("Шрифт");
	ТаблицаЗаголовка.Колонки.Добавить("Формат");
	
	СтруктураЗаголовка = отПолучитьСтруктуруЗаголовкаОтчета(ОтчетОбъект, ПостроительОтчета);
	
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "ИмяОтчета";
	НоваяСтрока.Текст = СтруктураЗаголовка.Наименование + Символы.ПС + СтруктураЗаголовка.Период;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 12, Истина);
		
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "Отбор";
	НоваяСтрока.Текст = "Отбор: " + СтруктураЗаголовка.Отбор;
	НоваяСтрока.ЦветТекста = ЦветаСтиля.ЦветФонаВыделенияПоля;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10, Истина);
		
	Попытка
		ВалютаОтчета = ОтчетОбъект.ВалютаОтчета;
		Если Не обЗначениеНеЗаполнено(ОтчетОбъект.ВалютаОтчета) Тогда
			НоваяСтрока = ТаблицаЗаголовка.Добавить();
			НоваяСтрока.ИмяСтроки = "ВалютаОтчета";
			НоваяСтрока.Текст = "Валюта отчета: "+ВалютаОтчета.Наименование;
			НоваяСтрока.ЦветТекста = ЦветаСтиля.ЦветФонаВыделенияПоля;
			НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10, Истина);
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "Показатели";
	НоваяСтрока.Текст = "Показатели: " + СтруктураЗаголовка.Показатели;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10);
	
	Если НЕ ПустаяСтрока(СтруктураЗаголовка.ДополнительныеКолонки) Тогда
		НоваяСтрока = ТаблицаЗаголовка.Добавить();
		НоваяСтрока.ИмяСтроки = "Поля";
		НоваяСтрока.Текст = "Дополнительные поля: " + СтруктураЗаголовка.ДополнительныеКолонки;
		НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10);
	КонецЕсли;
	
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "Итоги";
	НоваяСтрока.Текст = "Итоги по: " + СтруктураЗаголовка.Измерения;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10);
	
	Попытка
		ОтчетОбъект.ОбработкаТаблицыЗаголовка(ТаблицаЗаголовка);
	Исключение
	КонецПопытки;
	
	Возврат ТаблицаЗаголовка;
		
КонецФункции // отПолучитьТаблицуЗаголовкаОтчета()

// Функция возвращает структуру параметров заголовка отчета
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
// Возвращаемое значение:
//	Структура - Структура заголовка отчета
//
Функция отПолучитьСтруктуруЗаголовкаОтчета(ОтчетОбъект, ПостроительОтчета = Неопределено) Экспорт
	
	// Получим наименование отчета из формы настройки
	Попытка	Наименование =	ОтчетОбъект.ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек).НаименованиеОтчета;
	Исключение Наименование = ""; КонецПопытки;
	
	// Описание периода
	Если (ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Остатки) ИЛИ (ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ПрочиеОстатки) Тогда
		Если ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
			Период = "на " + Формат(ТекущаяДата(), "ДФ = ""дд.ММ.гггг ЧЧ:мм:сс""; ДП = ""...""");
		Иначе Период = "на конец дня " + Формат(ОтчетОбъект.ДатаКонца, "ДФ = ""дд.ММ.гггг""; ДП = ""...""");
		КонецЕсли;
	ИначеЕсли ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Прочий Тогда
		Период="";
	Иначе
		Если ОтчетОбъект.ДатаНачала = '00010101000000' И ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
			Период = "Весь рабочий период !";
			
		Иначе
			Если ОтчетОбъект.ДатаНачала = '00010101000000' ИЛИ ОтчетОбъект.ДатаКонца = '00010101000000' Тогда
				СтрФорматирования = "ДФ = ""дд.ММ.гггг""; ДП = ""...""";
				Период = Формат(ОтчетОбъект.ДатаНачала, СтрФорматирования) + " - " + Формат(ОтчетОбъект.ДатаКонца,  СтрФорматирования);
				
			Иначе
				Если ОтчетОбъект.ДатаНачала <= ОтчетОбъект.ДатаКонца Тогда
					Период = ПредставлениеПериода(НачалоДня(ОтчетОбъект.ДатаНачала), КонецДня(ОтчетОбъект.ДатаКонца), "ФП = Истина");
				Иначе Период = "Неправильно задан период!"
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ПостроительОтчета = Неопределено Тогда
		ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	
	// Текст измерений строк
	Измерения = "";
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияСтроки Цикл
		Измерения = Измерения + ?(Измерения = "", "", ", ") + ТекИзмерение.Представление + " " + ТекИзмерение.ТипИзмерения;
	КонецЦикла; 
	
	// Текст колонок дополнительных полей
	ДополнительныеКолонки = "";
	ДоступныеПоля = ОтчетОбъект.ДеревоДоступныхПолей;
	Для Каждого ТекПоле Из ОтчетОбъект.ДополнительныеПоляОтчета Цикл
		ДополнительныеКолонки = ДополнительныеКолонки + ?(ДополнительныеКолонки = "", "", ", ") + ТекПоле.ПолноеПредставление;
	КонецЦикла;
	
	// Текст отборов
	Если ПостроительОтчета.Отбор.Количество() = 0 Тогда
		Отбор = "Без отбора";
	Иначе
		Отбор = "";
		Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
			Если ТекОтбор.Использование И Не ТекОтбор.Имя = "Периодичность" Тогда
				Если Найти(ТекОтбор.ВидСравнения,"Интервал")>0 Тогда
					Отбор = Отбор + ?(Отбор = "", "", ", ") + ТекОтбор.Представление + " " + ТекОтбор.ВидСравнения + " " + ТекОтбор.ЗначениеС+" - "+ТекОтбор.ЗначениеПо;
				Иначе
					Отбор = Отбор + ?(Отбор = "", "", ", ") + ТекОтбор.Представление + " " + ТекОтбор.ВидСравнения + " " + ТекОтбор.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если Отбор = "" Тогда Отбор = "Все"; КонецЕсли; 
	КонецЕсли; 
	
	Показатели = "";
	Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
		Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
		Показатели = Показатели + ?(Показатели = "", "", ", ") + ТекПоказатель.Представление;
	КонецЦикла;
	
	Возврат Новый Структура("Наименование,Период,Отбор,Измерения,ДополнительныеКолонки,Показатели",
	Наименование,Период,Отбор,Измерения,ДополнительныеКолонки,Показатели);
	
КонецФункции	//	отПолучитьСтруктуруЗаголовкаОтчета()

// Процедура формирует строку измерений макета построителя отчета
//
// Параметры:
//	ОтчетОбъект        - ОтчетОбъект                - Объект отчета
//	ПостроительОтчета  - ПостроительОтчета          - Построитель отчета
//	МакетОтчета        - ТабличныйДокумент          - Макет отчета
//	ТекИзмерение       - ИзмерениеПостроителяОтчета - Текущее измерение
//	СчетчикУровня      - Число						- Счетчик уровня
//	ПараметрыИзмерения - Структура					- Параметры измерения
//	КлючСтроки         - Строка						- Ключ строки
//	ГраницаМакета      - Структура					- Границы макета
//
Процедура отСформироватьМакетИзмеренияСтроки(ОтчетОбъект, ПостроительОтчета, МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, КлючСтроки, ГраницаМакета)
	
	РесурсыОтчета              = ПараметрыИзмерения.РесурсыОтчета;
	ДеревоРесурсов             = ПараметрыИзмерения.ДеревоРесурсов;
	КоличествоУровней          = ПараметрыИзмерения.КоличествоУровней;
	КоличествоРазверток        = ПараметрыИзмерения.КоличествоРазверток;
	ЭтоИерархия                = ПараметрыИзмерения.ЭтоИерархия;
	ОбластьОформлениеИзмерений = ПараметрыИзмерения.ОбластьОформлениеИзмерений;
	НачальноеОформление        = ПараметрыИзмерения.НачальноеОформление;
	СтруктураДополненияПоказателей = ПараметрыИзмерения.СтруктураДополненияПоказателей;
	КоличествоКолонокПоказателей = ПараметрыИзмерения.КоличествоКолонокПоказателей;
	
	Попытка	   ДополнительныеПоляВОтдельнойКолонке = ОтчетОбъект.ВыводитьДополнительныеПоляВОтдельнойКолонке;
	Исключение ДополнительныеПоляВОтдельнойКолонке = Ложь;
	КонецПопытки;
	
	Попытка СтруктураФорматаПолей = ОтчетОбъект.СтруктураФорматированияПолей();
	Исключение СтруктураФорматаПолей = Новый Структура();
	КонецПопытки;	
	
	СтруктураСвязиСвойств = Новый Структура();
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение КонецПопытки;	
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	ПоследнееИзмерение = ПостроительОтчета.ИзмеренияСтроки.Индекс(ТекИзмерение) = ПостроительОтчета.ИзмеренияСтроки.Количество() - 1;
	НомерСтроки = МакетОтчета.ВысотаТаблицы + 1;
	НачальныйНомерВыводаКолонок = ?(ОтчетОбъект.ИспользоватьАвтоотступ, 4, КоличествоУровней + 3);
	
	ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекИзмерение.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ИмяТекИзмерения = ТекИзмерение.Имя;
	ПутьОсновногоПредставления = "";
	ТипЗаполненияОбластиИзмерения =	ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	Попытка
		Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
			ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
			ИмяТекИзмерения = СтрЗаменить(ПутьОсновногоПредставления,".","");
		ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
			ИмяТекИзмерения = ТекПараметрыИзмерения.СоставноеПредставление.Представление;
			ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
		КонецЕсли;
	Исключение
	КонецПопытки;
	// Установим ключ строки, что бы в дальнейшем удалить "пустышки"
	Если Не ПустаяСтрока(КлючСтроки) Тогда
		МакетОтчета.Область(НомерСтроки, 1).Текст = КлючСтроки;
	КонецЕсли;
	
	ДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные", Ложь));
	
	Если ДополнительныеПоляОтчета.Количество() > 0 И Не ДополнительныеПоляВОтдельнойКолонке Тогда
		
		ТекстШаблона = "";
		Для ИндПоля=0 По ДополнительныеПоляОтчета.Количество()-1 Цикл
			ТекПоле = ДополнительныеПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			Если (СтруктураСвязиСвойств.Свойство(ТекПоле.Имя)) И (НЕ ЭтоИерархия) Тогда
				ЗаполнятьПараметр = ТекИзмерение.Имя = СтруктураСвязиСвойств[ТекПоле.Имя].ИмяИзмерения;
			Иначе ЗаполнятьПараметр = (ТекПоле.ПутьКДанным <> ТекИзмерение.ПутьКДанным И Найти(ТекПоле.ПутьКДанным, ТекИзмерение.ПутьКДанным) > 0);
			КонецЕсли;
			Если ЗаполнятьПараметр Тогда ТекстШаблона = ТекстШаблона + ", [" + ТекПоле.Имя + "]"; КонецЕсли;
		КонецЦикла;
		
		Если Не обЗначениеНеЗаполнено(ТекстШаблона) Тогда
			ТипЗаполненияОбластиИзмерения =	ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			ИмяТекИзмерения = "[" + ИмяТекИзмерения + "]"+ТекстШаблона;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
		Для к = 1 по СчетчикУровня - 1 Цикл
			ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + к, 1);
			
			ОбластьПредУровня = МакетОтчета.Область(НомерСтроки, к + 2);
			ОбластьПредУровня.Шрифт        = ОформлениеУровня.Шрифт;
			ОбластьПредУровня.ЦветТекста   = ОформлениеУровня.ЦветТекста;
			ОбластьПредУровня.ЦветФона     = ОформлениеУровня.ЦветФона;
			ОбластьПредУровня.ГраницаСлева = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
			ОбластьПредУровня.ШиринаКолонки = 2;
		КонецЦикла;
		
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		
	Иначе
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, 3, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
	КонецЕсли;	
	
	ОбластьФиктивнаяРасшифровка = МакетОтчета.Область(НомерСтроки, 1, НомерСтроки, 1);
	ОбластьФиктивнаяРасшифровка.ПараметрРасшифровки = "Расшифровка";
	
	РасшифровкаГруппировки = Новый Структура();
	РасшифровкаГруппировки.Вставить("ИмяГруппировки", ТекИзмерение.Имя);
	РасшифровкаГруппировки.Вставить("ПутьКДанным",    ТекИзмерение.ПутьКДанным);
	РасшифровкаГруппировки.Вставить("Колонка",        ОбластьКолонки.Лево);
	Если ПостроительОтчета.ВыводитьДетальныеЗаписи И ПоследнееИзмерение И НЕ ЭтоИерархия Тогда
		РасшифровкаГруппировки.Вставить("ДетальнаяЗапись", Истина);
	Иначе
		РасшифровкаГруппировки.Вставить("ДетальнаяЗапись", Ложь);
	КонецЕсли;
	ОбластьИмяФиктивнойРасшифровки = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, 2);
	ОбластьИмяФиктивнойРасшифровки.Расшифровка = РасшифровкаГруппировки;
	
	ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + СчетчикУровня, 1);
	
	ОбластьКолонки.Объединить();
	ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьКолонки.Заполнение    = ТипЗаполненияОбластиИзмерения;
	ОбластьКолонки.Параметр      = ИмяТекИзмерения;
	ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	ОбластьКолонки.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
	ОбластьКолонки.Шрифт         = ОформлениеУровня.Шрифт;
	
	Если ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Параметр Тогда
		Попытка
			Если обЗначениеНеЗаполнено(ПутьОсновногоПредставления) Тогда
				ОбластьКолонки.Формат = ТекПараметрыИзмерения.Формат;
			Иначе
				ОбластьКолонки.Формат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьОсновногоПредставления, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей).Формат;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	ОбластьКолонки.ЦветТекста    = ОформлениеУровня.ЦветТекста;
	ОбластьКолонки.ЦветФона      = ОформлениеУровня.ЦветФона;
	ОбластьКолонки.ГраницаСверху = ГраницаМакета.ИзмерениеСтрока.ГраницаСверху;
	ОбластьКолонки.ГраницаСлева  = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
	Если ОтчетОбъект.ИспользоватьАвтоотступ Тогда
		ОбластьКолонки.Автоотступ = 1;
	КонецЕсли;	
	
	ОбластьКолонки.ПараметрРасшифровки = ТекИзмерение.Имя;
	//ОбластьКолонки.ПараметрРасшифровки = "Расшифровка";
	Если обЗначениеНеЗаполнено(ОбластьКолонки.Формат) И СтруктураФорматаПолей.Свойство(ТекИзмерение.Имя) Тогда
		ОбластьКолонки.Формат = СтруктураФорматаПолей[ТекИзмерение.Имя];
	КонецЕсли;
	
	Если ДополнительныеПоляОтчета.Количество() > 0 И ДополнительныеПоляВОтдельнойКолонке Тогда
		
		// Теперь добавим выбранные дополнительные поля
		Для ИндПоля=0 По ДополнительныеПоляОтчета.Количество()-1 Цикл
			
			ТекПоле = ДополнительныеПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			Если (СтруктураСвязиСвойств.Свойство(ТекПоле.Имя)) И (НЕ ЭтоИерархия) Тогда
				ЗаполнятьПараметр = ТекИзмерение.Имя = СтруктураСвязиСвойств[ТекПоле.Имя].ИмяИзмерения;
			Иначе ЗаполнятьПараметр = (ТекПоле.ПутьКДанным <> ТекИзмерение.ПутьКДанным И Найти(ТекПоле.ПутьКДанным, ТекИзмерение.ПутьКДанным) > 0);
			КонецЕсли;
			
			Если ЗаполнятьПараметр Тогда
				ОбластьДопПоле.Заполнение          = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьДопПоле.Параметр            = ТекПоле.Имя;
				ОбластьДопПоле.ПараметрРасшифровки = ТекПоле.Имя;
				ОбластьДопПоле.Формат              = ТекПоле.Формат;
			Иначе
				ОбластьДопПоле.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьДопПоле.Текст      = "";
			КонецЕсли;
			ОбластьДопПоле.ГраницаСверху = ГраницаМакета.ДопПоляСтрока.ГраницаСверху;
			ОбластьДопПоле.ГраницаСлева  = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
		Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		Иначе ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, 3, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		КонецЕсли;
		ОбластьДопПоле.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьДопПоле.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьДопПоле.ЦветФона      = ОформлениеУровня.ЦветФона;
	КонецЕсли;	
	
	НеСвязанныеДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные", Истина));
	Если НеСвязанныеДополнительныеПоляОтчета.Количество() > 0 Тогда
		
		Для ИндПоля=0 По НеСвязанныеДополнительныеПоляОтчета.Количество()-1 Цикл
			
			ТекПоле = НеСвязанныеДополнительныеПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			ЗаполнятьПараметр = Ложь;
			
			Если ТекПоле.ВыводитьИтогДляВсехУровней Тогда
				ЗаполнятьПараметр = Истина;
			ИначеЕсли ОтчетОбъект.СтруктураНеСвязанныхПоказателей.Свойство(ТекИзмерение.Имя) Тогда
				Если ЭтоИерархия Тогда
					Если (ОтчетОбъект.СтруктураНеСвязанныхПоказателей[ТекИзмерение.Имя]["Показатели"].Свойство(ТекПоле.Имя)) Тогда
						ЗаполнятьПараметр = ОтчетОбъект.СтруктураНеСвязанныхПоказателей[ТекИзмерение.Имя]["Показатели"][ТекПоле.Имя].ВыводитьИтогПоИерархии;
					КонецЕсли;
				Иначе
					ЗаполнятьПараметр = ОтчетОбъект.СтруктураНеСвязанныхПоказателей[ТекИзмерение.Имя]["Показатели"].Свойство(ТекПоле.Имя);
				КонецЕсли;
			Иначе ЗаполнятьПараметр = (ТекПоле.ПутьКДанным <> ТекИзмерение.ПутьКДанным И Найти(ТекПоле.ПутьКДанным, ТекИзмерение.ПутьКДанным) > 0);
			КонецЕсли;
			
			Если ЗаполнятьПараметр Тогда
				ОбластьДопПоле.Заполнение          = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьДопПоле.Параметр            = ТекПоле.Имя;
				ОбластьДопПоле.ПараметрРасшифровки = ТекПоле.Имя;
				ОбластьДопПоле.Формат              = ТекПоле.Формат;
			Иначе
				ОбластьДопПоле.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьДопПоле.Текст      = "";
			КонецЕсли;
			ОбластьДопПоле.ГраницаСверху = ГраницаМакета.ДопПоляСтрока.ГраницаСверху;
			ОбластьДопПоле.ГраницаСлева  = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
		Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		Иначе ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, 3, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		КонецЕсли;
		ОбластьДопПоле.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьДопПоле.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьДопПоле.ЦветФона      = ОформлениеУровня.ЦветФона;
	КонецЕсли;
	
	НомерКолонки = НачальныйНомерВыводаКолонок;
	Если КоличествоРазверток > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		НомерКолонки = НомерКолонки + ((КоличествоРазверток - 1) * КоличествоКолонокПоказателей);
	Иначе
		ОбластьПостроителя = МакетОтчета.Область("R" + НомерСтроки);
	КонецЕсли;
	
	Если НЕ ПостроительОтчета.ВыводитьДетальныеЗаписи ИЛИ НЕ ПоследнееИзмерение ИЛИ ЭтоИерархия Тогда
		ОбластьПостроителя.Имя = ТекИзмерение.Имя + ?(ЭтоИерархия, "Иерархия", "");
	Иначе
		ОбластьПостроителя.Имя = "Детали";
	КонецЕсли;
	
	ИспользованиеРесурсов = Новый Структура();
	Попытка
		Если ЭтоИерархия Тогда
			СтрПутьКДанным = ТекИзмерение.ПутьКДанным+".Родитель";
		Иначе
			СтрПутьКДанным = ТекИзмерение.ПутьКДанным;
		КонецЕсли;
		ИспользованиеРесурсов=отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(СтрПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		ИспользованиеРесурсов=ИспользованиеРесурсов["Использование"]["Ресурсы"]
	Исключение КонецПопытки;
	
	НомерПервойКолонки = НомерКолонки;
	Для Каждого ТекРесурс Из РесурсыОтчета Цикл
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
		Если ПустаяСтрока(ТекРесурс) Тогда
			ОтображатьПоказатель = Ложь;
		Иначе
		Попытка    ОтображатьПоказатель = ИспользованиеРесурсов[ТекРесурс];
		Исключение ОтображатьПоказатель = Истина;
		КонецПопытки;
		КонецЕсли;
		
		ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		
		Если ОтображатьПоказатель Тогда
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьКолонки.Параметр                = ТекРесурс;
			ОбластьКолонки.Формат                  = СтруктураФорматаПолей[ТекРесурс];
			//ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонки.ПараметрРасшифровки     = ТекРесурс;
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		Иначе
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьКолонки.Текст                   = "";
			ОбластьКолонки.ПараметрРасшифровки     = "";
		КонецЕсли;
		ОбластьКолонки.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		
		КоличествоДополнений = 0;
		Если ЗначениеЗаполнено(ТекРесурс) И СтруктураДополненияПоказателей.Свойство(ТекРесурс) Тогда
			Для Каждого СтруктураДополнения Из СтруктураДополненияПоказателей[ТекРесурс] Цикл
				КоличествоДополнений = КоличествоДополнений+1;
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки+КоличествоДополнений, НомерСтроки, НомерКолонки+КоличествоДополнений);
				
				ОтображатьПоле = Ложь;
				Если ОтображатьПоказатель И Не ЭтоИерархия Тогда
					СвязаннаяГруппировка = ПостроительОтчета.ИзмеренияСтроки.Найти(СтруктураДополнения.ИмяСвязанногоПоля);
					Если Не СвязаннаяГруппировка = Неопределено Тогда
						ОтображатьПоле = (ПостроительОтчета.ИзмеренияСтроки.Индекс(СвязаннаяГруппировка)>=ПостроительОтчета.ИзмеренияСтроки.Индекс(ТекИзмерение));
					КонецЕсли;
					СвязаннаяГруппировка = ПостроительОтчета.ИзмеренияКолонки.Найти(СтруктураДополнения.ИмяСвязанногоПоля);
					Если Не СвязаннаяГруппировка = Неопределено Тогда
						ОтображатьПоле = (ОтображатьПоле ИЛИ (ПостроительОтчета.ИзмеренияКолонки.Индекс(СвязаннаяГруппировка)=0));
					КонецЕсли;
					СвязанныйОтбор = ПостроительОтчета.Отбор.Найти(СтруктураДополнения.ИмяСвязанногоПоля);
					Если Не СвязанныйОтбор = Неопределено И СвязанныйОтбор.Использование И СвязанныйОтбор.ВидСравнения=ВидСравнения.Равно Тогда
						ОтображатьПоле = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если ОтображатьПоле Тогда
					ОбластьКолонки.Заполнение             	= ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьКолонки.Параметр               	= СтруктураДополнения.Имя;
					ОбластьКолонки.Формат                 	= СтруктураДополнения.Формат;
					ОбластьКолонки.ПараметрРасшифровки    	= СтруктураДополнения.Имя;
					ОбластьКолонки.ГоризонтальноеПоложение	= СтруктураДополнения.ГоризонтальноеПоложение;
					ОбластьКолонки.ВертикальноеПоложение	= СтруктураДополнения.ВертикальноеПоложение;
				Иначе
					ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
					ОбластьКолонки.Текст                   = "";
					ОбластьКолонки.ПараметрРасшифровки     = "";
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
	КонецЦикла;
	
	ТекНомерКолонки = НомерПервойКолонки;
	СчетчикФункций=0;
	КоличествоДополнений=0;
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		СчетчикПоказателей = 0;
		Если ТекФункция.Одиночный Тогда
			КоличествоДополнений=0;
			Если ЗначениеЗаполнено(ТекФункция.ИмяРесурса) И СтруктураДополненияПоказателей.Свойство(ТекФункция.ИмяРесурса) Тогда
				КоличествоДополнений=СтруктураДополненияПоказателей[ТекФункция.ИмяРесурса].Количество();
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки+КоличествоДополнений);
				ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
			КонецЕсли;
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
			ОбластьКолонки.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСлева,ГраницаМакета.ФункцииСтрока.ГраницаСлева);
			ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
			ТекНомерКолонки = ТекНомерКолонки+КоличествоДополнений+1;
		Иначе
			Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
				КоличествоДополнений=0;
				Если СтруктураДополненияПоказателей.Свойство(ТекПоказатель.ИмяРесурса) Тогда
					КоличествоДополнений=СтруктураДополненияПоказателей[ТекПоказатель.ИмяРесурса].Количество();
					ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки+КоличествоДополнений);
					ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
				КонецЕсли;
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
				ОбластьКолонки.ГраницаСлева  = ?(СчетчикПоказателей=0,?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСлева,ГраницаМакета.ФункцииСтрока.ГраницаСлева), ГраницаМакета.ПоказателиСтрока.ГраницаСлева);
				ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
				
				ТекНомерКолонки = ТекНомерКолонки+КоличествоДополнений+1;
				СчетчикПоказателей = СчетчикПоказателей+1;
				
			КонецЦикла;
		КонецЕсли;
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки-1, НомерСтроки, ТекНомерКолонки-1);
		СчетчикФункций = СчетчикФункций+1;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ФункцииСтрока.ГраницаСправа;
	КонецЦикла;
	ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСправа;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
		ОбластьПостроителя.Имя = ТекИзмерение.Имя + "Ресурсы" + ?(ЭтоИерархия, "Иерархия", "");
	КонецЕсли;
	
	ОформлениеСтроки = ОбластьОформлениеИзмерений.Область(НачальноеОформление + СчетчикУровня, 1);
	
	ОбластьСтроки = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, МакетОтчета.ШиринаТаблицы);
	ОбластьСтроки.Шрифт         = ОформлениеСтроки.Шрифт;
	ОбластьСтроки.ЦветТекста    = ОформлениеСтроки.ЦветТекста;
	ОбластьСтроки.ЦветФона      = ОформлениеСтроки.ЦветФона;
	
КонецПроцедуры	//	отСформироватьМакетИзмеренияСтроки()

// Функция возвращает оформленный табличный документ для использования в качестве макета построителя отчета
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
// Возвращаемое значение:
//	ТабличныйДокумент - Макет отчета
//
Функция отПолучитьМакетПостроителя(ОтчетОбъект, ПостроительОтчета = Неопределено) Экспорт
	
	Если ПостроительОтчета = Неопределено Тогда
		ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	КонецЕсли;
	
	МакетОтчета = Новый ТабличныйДокумент();
	
	Попытка	   МакетОформления = ПолучитьОбщийМакет(ОтчетОбъект.ИмяМакетаОформленияТабличногоДокумента);
	Исключение МакетОформления = ПолучитьОбщийМакет("МакетОформлениеОтчета");
	КонецПопытки;
	Попытка	   ДополнительныеПоляВОтдельнойКолонке = ОтчетОбъект.ВыводитьДополнительныеПоляВОтдельнойКолонке;
	Исключение ДополнительныеПоляВОтдельнойКолонке = Ложь;
	КонецПопытки; 
	Попытка    СтруктураФорматаПолей = ОтчетОбъект.СтруктураФорматированияПолей();
	Исключение СтруктураФорматаПолей = Новый Структура();
	КонецПопытки;
	Попытка    СтруктураОриентацииТекстаПолей = ОтчетОбъект.СтруктураОриентацииТекстаПолей();
	Исключение СтруктураОриентацииТекстаПолей = Новый Структура();
	КонецПопытки;
	Попытка    ПараметрыРесурсов = ОтчетОбъект.ПараметрыРесурсов;
	Исключение ПараметрыРесурсов = Новый Структура();
	КонецПопытки;
	
	ОбластьОформлениеГраниц = Неопределено;
	
	ОбластьОформлениеИзмерений = МакетОформления.ПолучитьОбласть("ОформлениеИзмерений");
	ОформлениеШапки            = МакетОформления.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеИтоги            = МакетОформления.ПолучитьОбласть("ОформлениеИтоги").Область(1, 1);
	Попытка 
		ОбластьОформлениеГраниц    = МакетОформления.ПолучитьОбласть("ОформлениеГраниц");
	Исключение
	КонецПопытки;
	
	ГраницаМакета = Новый Структура();
	
	Если ОбластьОформлениеГраниц<>Неопределено Тогда
		
		
		ГраницаМакета.Вставить("Измерения",ОбластьОформлениеГраниц.Области.Измерения);
		ГраницаМакета.Вставить("ИзмерениеСтрока",ОбластьОформлениеГраниц.Области.ИзмерениеСтрока);
		ГраницаМакета.Вставить("ИзмеренияИтог",ОбластьОформлениеГраниц.Области.ИзмеренияИтог);
		
		ГраницаМакета.Вставить("ДопПоля",ОбластьОформлениеГраниц.Области.ДопПоля);
		ГраницаМакета.Вставить("ДопПоляСтрока",ОбластьОформлениеГраниц.Области.ДопПоляСтрока);
		
		ГраницаМакета.Вставить("ИзмеренияПоГоризонтали",ОбластьОформлениеГраниц.Области.ИзмеренияПоГоризонтали);
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиСтрока",ОбластьОформлениеГраниц.Области.ИзмеренияПоГоризонталиСтрока);
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиИтог",ОбластьОформлениеГраниц.Области.ИзмеренияПоГоризонталиИтог);
		
		ГраницаМакета.Вставить("Функции",ОбластьОформлениеГраниц.Области.Функции);
		ГраницаМакета.Вставить("ФункцииСтрока",ОбластьОформлениеГраниц.Области.ФункцииСтрока);
		ГраницаМакета.Вставить("ФункцииИтог",ОбластьОформлениеГраниц.Области.ФункцииИтог);
		
		ГраницаМакета.Вставить("Показатели",ОбластьОформлениеГраниц.Области.Показатели);
		ГраницаМакета.Вставить("ПоказателиСтрока",ОбластьОформлениеГраниц.Области.ПоказателиСтрока);
		ГраницаМакета.Вставить("ПоказателиИтог",ОбластьОформлениеГраниц.Области.ПоказателиИтог);
		
		ЗначениеГраницОтчета  = ОбластьОформлениеГраниц.Области.ЗначениеГраницОтчета;
		ВключитьГраницуСправа = ЗначениеГраницОтчета.АвтоОтступ=1;
	Иначе
		
		ЛинияОбычная       = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,      1);
		
		ГраницаМакета.Вставить("Измерения",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмерениеСтрока",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмеренияИтог",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа, ГраницаСнизу", ЛинияОбычная, ЛинияОбычная, ЛинияОбычная, ЛинияОбычная));
		
		ГраницаМакета.Вставить("ДопПоля",Новый Структура("ГраницаСлева, ГраницаСверху", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ДопПоляСтрока",Новый Структура("ГраницаСлева, ГраницаСверху", ЛинияОбычная,ЛинияОбычная));
		
		ГраницаМакета.Вставить("ИзмеренияПоГоризонтали",Новый Структура("ГраницаСлева, ГраницаСверху", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиСтрока",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиИтог",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		
		ГраницаМакета.Вставить("Функции",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ФункцииСтрока",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ФункцииИтог",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		
		ГраницаМакета.Вставить("Показатели",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ПоказателиСтрока",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ПоказателиИтог",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		
		ЗначениеГраницОтчета  = Новый Структура("ГраницаСправа" ,ЛинияОбычная);
		ВключитьГраницуСправа = Истина;
	КонецЕсли;
	
	ПараметрыОформления = Новый Структура("ОбластьОформлениеИзмерений, ОформлениеШапки, ОформлениеИтоги, ГраницаМакета", ОбластьОформлениеИзмерений, ОформлениеШапки, ОформлениеИтоги, ГраницаМакета);

	Попытка
		Если ОтчетОбъект.ПолучитьМакетПостроителя(ПостроительОтчета, МакетОтчета, ПараметрыОформления) Тогда
			Возврат МакетОтчета;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ЗаголовокШапки    = "";
	КоличествоУровней = 0;
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияСтроки Цикл
		ЗаголовокШапки = ЗаголовокШапки + ?(ЗаголовокШапки = "", "", " / ") + ТекИзмерение.Представление;
		
		Если ТекИзмерение.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			КоличествоУровней = КоличествоУровней + 1;
		КонецЕсли;	
		
		КоличествоУровней = КоличествоУровней + 1;
	КонецЦикла;
	
	КоличествоРазверток = 0;
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияКолонки Цикл
		Если ТекИзмерение.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			КоличествоРазверток = КоличествоРазверток + 1;
		КонецЕсли;

		КоличествоРазверток = КоличествоРазверток + 1;
	КонецЦикла;
	
	ПеремГраницаСправа = ?(ВключитьГраницуСправа, ЗначениеГраницОтчета.ГраницаСправа, ?(КоличествоРазверток>0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева));
	
	СтруктураДополненияПоказателей = Неопределено;
	ПараметрыРесурсов.Свойство("СтруктураДополненияПоказателей", СтруктураДополненияПоказателей);
	Если Не ТипЗнч(СтруктураДополненияПоказателей)=Тип("Структура") Тогда 
		СтруктураДополненияПоказателей = Новый Структура;
	КонецЕсли;
	РесурсыОтчета = Новый Массив();
	ДеревоРесурсов = Новый ДеревоЗначений;
	ДеревоРесурсов.Колонки.Добавить("ИмяРесурса");
	ДеревоРесурсов.Колонки.Добавить("Имя");
	ДеревоРесурсов.Колонки.Добавить("Представление");
	ДеревоРесурсов.Колонки.Добавить("ФорматнаяСтрока");
	ДеревоРесурсов.Колонки.Добавить("ШиринаКолонки");
	ДеревоРесурсов.Колонки.Добавить("Одиночный");
	КоличествоКолонокПоказателей = 0;
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			НоваяГруппа = ДеревоРесурсов.Строки.Добавить();
			НоваяГруппа.ИмяРесурса      = ТекГруппа.ПутьКДанным;
			НоваяГруппа.Имя             = ТекГруппа.Имя;
			НоваяГруппа.Представление   = ТекГруппа.Представление;
			НоваяГруппа.ФорматнаяСтрока = ТекГруппа.ФорматнаяСтрока;
			НоваяГруппа.ШиринаКолонки   = ТекГруппа.ШиринаКолонки;
			НоваяГруппа.Одиночный       = Ложь;
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				РесурсыОтчета.Добавить(ТекСтрока.ПутьКДанным);
				НоваяСтрока = НоваяГруппа.Строки.Добавить();
				НоваяСтрока.ИмяРесурса       = ТекСтрока.ПутьКДанным;
				НоваяСтрока.Имя              = ТекСтрока.Имя;
				НоваяСтрока.Представление    = ТекСтрока.Представление;
				НоваяСтрока.ФорматнаяСтрока  = ТекСтрока.ФорматнаяСтрока;
				НоваяСтрока.ШиринаКолонки    = ТекСтрока.ШиринаКолонки;
				НоваяСтрока.Одиночный        = Ложь;
				КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + 1;
				Если СтруктураДополненияПоказателей.Свойство(ТекСтрока.ПутьКДанным) Тогда
					КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + СтруктураДополненияПоказателей[ТекСтрока.ПутьКДанным].Количество();
				КонецЕсли;
			КонецЦикла;
			Если НоваяГруппа.Строки.Количество()=0 Тогда
				РесурсыОтчета.Добавить(ТекГруппа.ПутьКДанным);
				НоваяГруппа.Одиночный = Истина;
				КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + 1;
				Если СтруктураДополненияПоказателей.Свойство(ТекГруппа.ПутьКДанным) Тогда
					КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + СтруктураДополненияПоказателей[ТекГруппа.ПутьКДанным].Количество();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Исключение
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			Если ТекФункция.Представление="" И ТекФункция.Имя="" Тогда
				Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
					Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
					ИмяРесурса = ТекПоказатель.Имя;
					РесурсыОтчета.Добавить(ИмяРесурса);
					НоваяГруппа = ДеревоРесурсов.Строки.Добавить();
					НоваяГруппа.ИмяРесурса       = ИмяРесурса;
					НоваяГруппа.Имя              = ТекПоказатель.Имя;
					НоваяГруппа.Представление    = ТекПоказатель.Представление;
					НоваяГруппа.ФорматнаяСтрока  = ТекПоказатель.ФорматнаяСтрока;
					НоваяГруппа.ШиринаКолонки    = ТекПоказатель.ШиринаКолонки;
					НоваяГруппа.Одиночный        = Истина;
					КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + 1;
					Если СтруктураДополненияПоказателей.Свойство(ИмяРесурса) Тогда
						КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + СтруктураДополненияПоказателей[ИмяРесурса].Количество();
					КонецЕсли;
				КонецЦикла;
			Иначе
				НоваяГруппа = ДеревоРесурсов.Строки.Добавить();
				НоваяГруппа.ИмяРесурса      = "";
				НоваяГруппа.Имя             = ТекФункция.Имя;
				НоваяГруппа.Представление   = ТекФункция.Представление;
				НоваяГруппа.ФорматнаяСтрока = "";
				НоваяГруппа.ШиринаКолонки   = "";
				НоваяГруппа.Одиночный       = Ложь;
				Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
					Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
					ИмяРесурса = ТекПоказатель.Имя + ТекФункция.Имя;
					РесурсыОтчета.Добавить(ИмяРесурса);
					НоваяСтрока = НоваяГруппа.Строки.Добавить();
					НоваяСтрока.ИмяРесурса       = ИмяРесурса;
					НоваяСтрока.Имя              = ТекПоказатель.Имя;
					НоваяСтрока.Представление    = ТекПоказатель.Представление;
					НоваяСтрока.ФорматнаяСтрока  = ТекПоказатель.ФорматнаяСтрока;
					НоваяСтрока.ШиринаКолонки    = ТекПоказатель.ШиринаКолонки;
					НоваяСтрока.Одиночный        = Ложь;
					КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + 1;
					Если СтруктураДополненияПоказателей.Свойство(ИмяРесурса) Тогда
						КоличествоКолонокПоказателей = КоличествоКолонокПоказателей + СтруктураДополненияПоказателей[ИмяРесурса].Количество();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецПопытки;
	
	// Приступим к формированию макета
	ТаблицаЗаголовка = отПолучитьТаблицуЗаголовкаОтчета(ОтчетОбъект, ПостроительОтчета, МакетОтчета.Область(1, 2).Шрифт);
	
	Для Инд=0 По ТаблицаЗаголовка.Количество()-1 Цикл
		ТекСтрокаЗаголовка = ТаблицаЗаголовка[Инд];
		ОбластьЗаголовок = МакетОтчета.Область(Инд+1, 3);
		Для Каждого ТекКолонка Из ТаблицаЗаголовка.Колонки Цикл
			Если ТекКолонка.Имя="ИмяСтроки" Тогда Продолжить; КонецЕсли;
			ТекПараметрЗаголовка = ТекСтрокаЗаголовка[ТекКолонка.Имя];
			Если обЗначениеНеЗаполнено(ТекПараметрЗаголовка) Тогда Продолжить; КонецЕсли;
			ОбластьЗаголовок[ТекКолонка.Имя] = ТекПараметрЗаголовка;
		КонецЦикла;
	КонецЦикла;
	
	ВысотаЗаголовка = МакетОтчета.ВысотаТаблицы;
	МакетОтчета.Область(1, 1).ШиринаКолонки = 0;
	МакетОтчета.Область(1, 2).ШиринаКолонки = 1;
	МакетОтчета.Область("R1:R" + ВысотаЗаголовка).Имя = "ЗаголовокШаблон";
	
	// Создадим отступы уровней измерений строк
	Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
		НачальныйНомерВыводаКолонок = 3;
		Для к = 1 по КоличествоУровней Цикл
			МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок).ШиринаКолонки = ?(к = КоличествоУровней, 40, 1.5);
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
	Иначе
		НачальныйНомерВыводаКолонок = 4;
		МакетОтчета.Область(ВысотаЗаголовка + 1, 3).ШиринаКолонки = 40;
	КонецЕсли;
	
	ОбластьЗаголовокСтрок = МакетОтчета.Область(ВысотаЗаголовка + 1, 3, ВысотаЗаголовка + 2 + КоличествоРазверток, НачальныйНомерВыводаКолонок - 1);
	ОбластьЗаголовокСтрок.Объединить();
	ОбластьЗаголовокСтрок.Текст                   = ЗаголовокШапки;
	ОбластьЗаголовокСтрок.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьЗаголовокСтрок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ГраницаСлева            = ГраницаМакета.Измерения.ГраницаСлева;
	ОбластьЗаголовокСтрок.ГраницаСверху           = ГраницаМакета.Измерения.ГраницаСверху;
	ОбластьЗаголовокСтрок.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
	
	ДоступныеПоля = ОтчетОбъект.ДеревоДоступныхПолей;
	ДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета;
	Если ДополнительныеПоляОтчета.Колонки.Найти("НомерСтроки") = Неопределено Тогда
		ДополнительныеПоляОтчета.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
	КонецЕсли;
	
	НомерСтроки = 0 ;
	Для Каждого ТекСтрока Из ДополнительныеПоляОтчета Цикл
		ТекСтрока.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	ДополнительныеПоляОтчета.Сортировать("НеСвязанные ВОЗР, НомерСтроки ВОЗР");
	
	Для Каждого ТекПоле Из ОтчетОбъект.ДополнительныеПоляОтчета Цикл
		
		Если (НЕ ТекПоле.НеСвязанные) И (Не ДополнительныеПоляВОтдельнойКолонке) Тогда Продолжить; КонецЕсли;
		
		ОбластьДопПоле = МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок, ВысотаЗаголовка + 2 + КоличествоРазверток, НачальныйНомерВыводаКолонок);
		ОбластьДопПоле.Объединить();
		ОбластьДопПоле.Текст                   = ТекПоле.ПолноеПредставление;
		ОбластьДопПоле.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.ШиринаКолонки           = ТекПоле.ШиринаКолонки;
		ОбластьДопПоле.Расшифровка             = Новый Структура("ПутьКДанным, ИмяПоля", ТекПоле.ПутьКДанным, ТекПоле.Имя);
		ОбластьДопПоле.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьДопПоле.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьДопПоле.ГраницаСлева            = ГраницаМакета.ДопПоля.ГраницаСлева;
		ОбластьДопПоле.ГраницаСверху           = ГраницаМакета.ДопПоля.ГраницаСверху;
		ОбластьДопПоле.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
		
		НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
	КонецЦикла;
	
	Если КоличествоРазверток > 0 Тогда
		// Для кросс-отчета 
		ОбластьШапкаСтрок = МакетОтчета.Область(ВысотаЗаголовка + 1, 1, ВысотаЗаголовка + 2 + КоличествоРазверток, НачальныйНомерВыводаКолонок - 1);
		ОбластьШапкаСтрок.Имя = "ШапкаСтрок";
		
	Иначе
		ОбластьШапкаСтрок = МакетОтчета.Область("R" + (ВысотаЗаголовка + 1) + ":R" + (ВысотаЗаголовка + 2));
		ОбластьШапкаСтрок.Имя = "ШапкаТаблицы";
	КонецЕсли;
	
	НомерСтроки  = ВысотаЗаголовка + 1;
	НомерКолонки = НачальныйНомерВыводаКолонок;
	УровеньГруппировки=0;
	Для Каждого ТекКолонка Из ПостроительОтчета.ИзмеренияКолонки Цикл
		КлючКолонки  = "";
		НомерПервойКолонки = НомерКолонки;
		ИмяТекИзмерения = ТекКолонка.Имя;
		ПутьОсновногоПредставления = "";
		ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекКолонка.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
				ИмяТекИзмерения = СтрЗаменить(ПутьОсновногоПредставления,".","");
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ИмяТекИзмерения = ТекПараметрыИзмерения.СоставноеПредставление.Представление;
				ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Если ТекКолонка.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			ОбластьКолонкиПоказатель = Неопределено;
			ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
			Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
				НомерПервойКолонкиИзмерения = 0;
				НомерПервойКолонкиФункции = НомерКолонки;
				
				Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
					
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки);
					Если ОстатокВысоты > 0 Тогда
						ОбластьКолонкиПоказатель.Объединить();
					КонецЕсли;
					ОбластьКолонкиПоказатель.ШиринаКолонки	   	   = ТекПоказатель.ШиринаКолонки;
					
					КоличествоДополнений = 0;
					Если СтруктураДополненияПоказателей.Свойство(ТекПоказатель.ИмяРесурса) Тогда
						Для Каждого СтруктураДополнения Из СтруктураДополненияПоказателей[ТекПоказатель.ИмяРесурса] Цикл
							КоличествоДополнений = КоличествоДополнений+1;
							ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки+КоличествоДополнений, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений);
							ОбластьКолонкиПоказатель.ШиринаКолонки = СтруктураДополнения.ШиринаКолонки;
						КонецЦикла;
						ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений);
						ОбластьКолонкиПоказатель.Объединить();
					КонецЕсли;
					
					ОбластьКолонкиПоказатель.Текст = ТекПоказатель.Представление;
					ОбластьКолонкиПоказатель.Расшифровка             = Новый Структура("Показатель, Функция, ПутьКДанным, ИмяПоля", ТекПоказатель, ТекФункция, ТекПоказатель.ИмяРесурса, ТекПоказатель.ИмяРесурса);
					ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
					ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиФункции = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
					ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
					ОбластьКолонкиПоказатель.ГраницаСнизу            = ГраницаМакета.Показатели.ГраницаСнизу;
					ОбластьКолонкиПоказатель.ОриентацияТекста		 = ?((Не ПустаяСтрока(ТекПоказатель.ИмяРесурса)) И СтруктураОриентацииТекстаПолей.Свойство(ТекПоказатель.ИмяРесурса), СтруктураОриентацииТекстаПолей[ТекПоказатель.ИмяРесурса], 0);
					
					НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
					
				КонецЦикла;
				
				Если ТекФункция.Одиночный Тогда
					ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 2+ОстатокВысоты, НомерПервойКолонкиФункции);
					ОбластьКолонкиФункция.ШиринаКолонки = ?(ЗначениеЗаполнено(ТекФункция.ШиринаКолонки), ТекФункция.ШиринаКолонки, 14);
					ОбластьКолонкиФункция.Расшифровка   = Новый Структура("Показатель, ПутьКДанным, ИмяПоля", ТекФункция, ТекФункция.ИмяРесурса, ТекФункция.ИмяРесурса);
					КоличествоДополнений = 0;
					Если СтруктураДополненияПоказателей.Свойство(ТекФункция.ИмяРесурса) Тогда
						Для Каждого СтруктураДополнения Из СтруктураДополненияПоказателей[ТекФункция.ИмяРесурса] Цикл
							КоличествоДополнений = КоличествоДополнений+1;
							ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции+КоличествоДополнений, НомерСтроки + 2+ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений);
							ОбластьКолонкиФункция.ШиринаКолонки = СтруктураДополнения.ШиринаКолонки;
						КонецЦикла;
						ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 2+ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений);
					КонецЕсли;
					
					НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
				Иначе
					ОбластьКолонкиПоказатель.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
					ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 1, НомерКолонки - 1);
				КонецЕсли;
				
				ОбластьКолонкиФункция.Объединить();
				ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
				ОбластьКолонкиФункция.Расшифровка             = Новый Структура("Функция, ПутьКДанным, ИмяПоля", ТекФункция, ТекФункция.ИмяРесурса, ТекФункция.ИмяРесурса);
				ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
				ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
				ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
				ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
				ОбластьКолонкиФункция.ОриентацияТекста 		  = ?((Не ПустаяСтрока(ТекФункция.ИмяРесурса)) И СтруктураОриентацииТекстаПолей.Свойство(ТекФункция.ИмяРесурса), СтруктураОриентацииТекстаПолей[ТекФункция.ИмяРесурса], 0);
				НомерПервойКолонкиИзмерения=НомерПервойКолонкиИзмерения+1;
			КонецЦикла;
			Если ОбластьКолонкиПоказатель <> Неопределено Тогда
				ОбластьКолонкиПоказатель.ГраницаСправа        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			КонецЕсли;
			ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
			ОбластьКолонки.Объединить();
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиИзмерения;
			ОбластьКолонки.Параметр                = ИмяТекИзмерения;
			ОбластьКолонки.ПараметрРасшифровки     = ТекКолонка.Имя;
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьКолонки.ГраницаСлева            = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			ОбластьКолонки.ГраницаСверху           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
			Попытка
				Если обЗначениеНеЗаполнено(ПутьОсновногоПредставления) Тогда
					ОбластьКолонки.Формат = ТекПараметрыИзмерения.Формат;
				Иначе
					ОбластьКолонки.Формат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьОсновногоПредставления, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей).Формат;
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			ОбластьПостроителя = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + 2 + КоличествоРазверток, НомерКолонки - 1);
			ОбластьПостроителя.Имя = ТекКолонка.Имя + "Иерархия";
			
			НомерСтроки = НомерСтроки + 1;
			УровеньГруппировки = УровеньГруппировки+1;
			НомерПервойКолонки = НомерКолонки;
		КонецЕсли;
		НомерПервойКолонкиИзмерения = 0;
		ОбластьКолонкиПоказатель = Неопределено;
		ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
		Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
			НомерПервойКолонкиФункции = НомерКолонки;
			Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
				
				ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки);
				Если ОстатокВысоты > 0 Тогда
					ОбластьКолонкиПоказатель.Объединить();
				КонецЕсли;
				ОбластьКолонкиПоказатель.ШиринаКолонки	  	     = ТекПоказатель.ШиринаКолонки;
				
				КоличествоДополнений = 0;
				Если СтруктураДополненияПоказателей.Свойство(ТекПоказатель.ИмяРесурса) Тогда
					Для Каждого СтруктураДополнения Из СтруктураДополненияПоказателей[ТекПоказатель.ИмяРесурса] Цикл
						КоличествоДополнений = КоличествоДополнений+1;
						ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки+КоличествоДополнений, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений);
						ОбластьКолонкиПоказатель.ШиринаКолонки = СтруктураДополнения.ШиринаКолонки;
					КонецЦикла;
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений);
					ОбластьКолонкиПоказатель.Объединить();
				КонецЕсли;
				
				ОбластьКолонкиПоказатель.Текст                   = ТекПоказатель.Представление;
				ОбластьКолонкиПоказатель.Расшифровка             = Новый Структура("Показатель, Функция, ПутьКДанным, ИмяПоля", ТекПоказатель, ТекФункция, ТекПоказатель.ИмяРесурса, ТекПоказатель.ИмяРесурса);
				ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
				ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиФункции = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
				ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
				ОбластьКолонкиПоказатель.ГраницаСнизу            = ГраницаМакета.Показатели.ГраницаСнизу;
				ОбластьКолонкиПоказатель.ОриентацияТекста		 = ?((Не ПустаяСтрока(ТекПоказатель.ИмяРесурса)) И СтруктураОриентацииТекстаПолей.Свойство(ТекПоказатель.ИмяРесурса), СтруктураОриентацииТекстаПолей[ТекПоказатель.ИмяРесурса], 0);
				
				НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
			КонецЦикла;
			
			Если ТекФункция.Одиночный Тогда
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 2+ОстатокВысоты, НомерПервойКолонкиФункции);
				ОбластьКолонкиФункция.ШиринаКолонки = ?(ЗначениеЗаполнено(ТекФункция.ШиринаКолонки), ТекФункция.ШиринаКолонки, 14);
				ОбластьКолонкиФункция.Расшифровка   = Новый Структура("Показатель, ПутьКДанным, ИмяПоля", ТекФункция, ТекФункция.ИмяРесурса, ТекФункция.ИмяРесурса);
				КоличествоДополнений = 0;
				Если СтруктураДополненияПоказателей.Свойство(ТекФункция.ИмяРесурса) Тогда
					Для Каждого СтруктураДополнения Из СтруктураДополненияПоказателей[ТекФункция.ИмяРесурса] Цикл
						КоличествоДополнений = КоличествоДополнений+1;
						ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции+КоличествоДополнений, НомерСтроки + 2+ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений);
						ОбластьКолонкиФункция.ШиринаКолонки = СтруктураДополнения.ШиринаКолонки;
					КонецЦикла;
					ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 2+ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений);
				КонецЕсли;
				
				НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
			Иначе
				ОбластьКолонкиПоказатель.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 1, НомерКолонки - 1);
			КонецЕсли;
			
			ОбластьКолонкиФункция.Объединить();
			ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
			ОбластьКолонкиФункция.Расшифровка             = Новый Структура("Функция, ПутьКДанным, ИмяПоля", ТекФункция, ТекФункция.ИмяРесурса, ТекФункция.ИмяРесурса);
			ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
			ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
			ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
			ОбластьКолонкиФункция.ОриентацияТекста		  = ?((Не ПустаяСтрока(ТекФункция.ИмяРесурса)) И СтруктураОриентацииТекстаПолей.Свойство(ТекФункция.ИмяРесурса), СтруктураОриентацииТекстаПолей[ТекФункция.ИмяРесурса], 0);
			НомерПервойКолонкиИзмерения=НомерПервойКолонкиИзмерения+1;
		КонецЦикла;
		
		ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		Если ОбластьКолонкиПоказатель <> Неопределено Тогда
			ОбластьКолонкиПоказатель.ГраницаСправа        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		КонецЕсли;
		
		Если УровеньГруппировки >0 Тогда
			ТекУровень=1;
			Пока ТекУровень<УровеньГруппировки+1 Цикл
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки-ТекУровень, НомерПервойКолонки, НомерСтроки-ТекУровень, НомерКолонки - 1);
				ОбластьКолонки.ГраницаСверху       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
				ТекУровень=ТекУровень+1;
			КонецЦикла;
		КонецЕсли;
		
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
		ОбластьКолонки.Объединить();
		ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиИзмерения;
		ОбластьКолонки.Параметр                = ИмяТекИзмерения;
		ОбластьКолонки.ПараметрРасшифровки     = ТекКолонка.Имя;
		ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонки.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонки.ГраницаСлева            = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		ОбластьКолонки.ГраницаСверху           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
		Попытка
			Если обЗначениеНеЗаполнено(ПутьОсновногоПредставления) Тогда
				ОбластьКолонки.Формат = ТекПараметрыИзмерения.Формат;
			Иначе
				ОбластьКолонки.Формат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьОсновногоПредставления, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей).Формат;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Если обЗначениеНеЗаполнено(ОбластьКолонки.Формат) И СтруктураФорматаПолей.Свойство(ТекКолонка.Имя) Тогда
			ОбластьКолонки.Формат = СтруктураФорматаПолей[ТекКолонка.Имя];
		КонецЕсли;	
		
		Попытка
			КлючКолонки = ОтчетОбъект.ПолучитьКлюч(ТекКолонка.ПутьКДанным);
		Исключение
			// Для регистратора и периодичности установим ключ колонки, что бы в дальнейшем удалить "пустышки"
			Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты или
				ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
				
				ПозицияРодителя = Найти(ТекКолонка.ПутьКДанным, ".");
				ПутьКДаннымРодителя = ТекКолонка.ПутьКДанным;
				Если ПозицияРодителя > 0 Тогда
					ПутьКДаннымРодителя = Лев(ТекКолонка.ПутьКДанным, ПозицияРодителя - 1);
				КонецЕсли;
				
				ОтсутствуетВОстатках = Ложь;
				Попытка // необходимых ключей может и не быть в структуре
					ОтсутствуетВОстатках = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей[ПутьКДаннымРодителя].Использование.ОтсутствуетВОстатках;
				Исключение КонецПопытки;
				
				Если Найти(ТекКолонка.ПутьКДанным, "ПериодРегистратор") = 1 или
					ТекКолонка.ПутьКДанным = "ПериодДень" или
					ТекКолонка.ПутьКДанным = "ПериодНеделя" или
					ТекКолонка.ПутьКДанным = "ПериодМесяц" или
					ТекКолонка.ПутьКДанным = "ПериодКвартал" или
					ТекКолонка.ПутьКДанным = "ПериодГод" или
					ОтсутствуетВОстатках Тогда
					
					КлючКолонки = "ПР_";
				КонецЕсли;
			КонецЕсли;
		КонецПопытки;
		
		Если НЕ ПустаяСтрока(КлючКолонки) Тогда
			МакетОтчета.Область(1, НомерПервойКолонки).Текст = КлючКолонки + ТекКолонка.Имя + "_" + НомерСтроки + "_" + (НомерКолонки - 1 - НомерПервойКолонки);
		КонецЕсли;
		
		ОбластьПостроителя = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + 2 + КоличествоРазверток, НомерКолонки - 1);
		ОбластьПостроителя.Имя = ТекКолонка.Имя;
		
		НомерСтроки = НомерСтроки + 1;
		УровеньГруппировки = УровеньГруппировки+1;
	КонецЦикла;
	
	Если (ПостроительОтчета.ИзмеренияКолонки.Количество() > 0) И ВключитьГраницуСправа Тогда
		ОбластьКолонки.ГраницаСправа = ЗначениеГраницОтчета.ГраницаСправа;
	КонецЕсли;
	
	Если (ПостроительОтчета.ИзмеренияКолонки.Количество()>0) И (НЕ ПостроительОтчета.ВыводитьОбщиеИтоги) Тогда
		ОбластьКолонки.ГраницаСправа               = ПеремГраницаСправа;
		ОбластьКолонкиФункция.ГраницаСправа        = ПеремГраницаСправа;
	КонецЕсли;
	
	НомерСтроки = ВысотаЗаголовка + 1;
	НомерПервойКолонки = НомерКолонки;
	НомерПервойКолонкиИзмерения = 0;
	ОбластьКолонкиПоказатель = Неопределено;
	ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		НомерПервойКолонкиФункции = НомерКолонки;
		Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
			Если КоличествоРазверток > 0 Тогда
				ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2 + ОстатокВысоты, НомерКолонки, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки);
			Иначе
				ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 1, НомерКолонки, НомерСтроки + 1, НомерКолонки);
			КонецЕсли;
			ОбластьКолонкиПоказатель.ШиринаКолонки	= ТекПоказатель.ШиринаКолонки;
			
			КоличествоДополнений = 0;
			Если СтруктураДополненияПоказателей.Свойство(ТекПоказатель.ИмяРесурса) Тогда
				Для Каждого ТекКолонка Из СтруктураДополненияПоказателей[ТекПоказатель.ИмяРесурса] Цикл
					КоличествоДополнений = КоличествоДополнений+1;
					Если КоличествоРазверток > 0 Тогда
						ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений);
					Иначе
						ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 1, НомерКолонки+КоличествоДополнений, НомерСтроки + 1, НомерКолонки+КоличествоДополнений);
					КонецЕсли;
					ОбластьКолонкиПоказатель.ШиринаКолонки = ТекКолонка.ШиринаКолонки;
				КонецЦикла;
				
				Если КоличествоРазверток > 0 Тогда
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2 + ОстатокВысоты, НомерКолонки, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки+КоличествоДополнений);
				Иначе
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 1, НомерКолонки, НомерСтроки + 1, НомерКолонки+КоличествоДополнений);
				КонецЕсли;
				ОбластьКолонкиПоказатель.Объединить();
			КонецЕсли;
			
			ОбластьКолонкиПоказатель.Текст                   = ТекПоказатель.Представление;
			ОбластьКолонкиПоказатель.Расшифровка             = Новый Структура("Показатель, Функция, ПутьКДанным, ИмяПоля", ТекПоказатель, ТекФункция, ТекПоказатель.ИмяРесурса, ТекПоказатель.ИмяРесурса);
			ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиФункции = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
			ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
			ОбластьКолонкиПоказатель.ГраницаСнизу            = ГраницаМакета.Показатели.ГраницаСнизу;
			ОбластьКолонкиПоказатель.ОриентацияТекста		 = ?((Не ПустаяСтрока(ТекПоказатель.ИмяРесурса)) И СтруктураОриентацииТекстаПолей.Свойство(ТекПоказатель.ИмяРесурса), СтруктураОриентацииТекстаПолей[ТекПоказатель.ИмяРесурса], 0);
			
			НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
		КонецЦикла;
		
		Если ТекФункция.Одиночный Тогда
			Если КоличествоРазверток > 0 Тогда
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1 + ОстатокВысоты, НомерПервойКолонкиФункции, НомерСтроки + 2 + ОстатокВысоты, НомерПервойКолонкиФункции);
			Иначе	
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции, НомерСтроки+1, НомерПервойКолонкиФункции);
			КонецЕсли;	
			ОбластьКолонкиФункция.ШиринаКолонки = ?(ЗначениеЗаполнено(ТекФункция.ШиринаКолонки), ТекФункция.ШиринаКолонки, 14);
			ОбластьКолонкиФункция.Расшифровка   = Новый Структура("Показатель, ПутьКДанным, ИмяПоля", ТекФункция, ТекФункция.ИмяРесурса, ТекФункция.ИмяРесурса);
			
			КоличествоДополнений = 0;
			Если СтруктураДополненияПоказателей.Свойство(ТекФункция.ИмяРесурса) Тогда
				Для Каждого ТекКолонка Из СтруктураДополненияПоказателей[ТекФункция.ИмяРесурса] Цикл
					КоличествоДополнений = КоличествоДополнений+1;
					Если КоличествоРазверток > 0 Тогда
						ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1 + ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений, НомерСтроки + 2 + ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений);
					Иначе
						ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции+КоличествоДополнений, НомерСтроки+1, НомерПервойКолонкиФункции+КоличествоДополнений);
					КонецЕсли;
					ОбластьКолонкиФункция.ШиринаКолонки = ТекКолонка.ШиринаКолонки;
				КонецЦикла;
				
				Если КоличествоРазверток > 0 Тогда
					ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1 + ОстатокВысоты, НомерПервойКолонкиФункции, НомерСтроки + 2 + ОстатокВысоты, НомерПервойКолонкиФункции+КоличествоДополнений);
				Иначе	
					ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции, НомерСтроки+1, НомерПервойКолонкиФункции+КоличествоДополнений);
				КонецЕсли;
					ОбластьКолонкиФункция.Объединить();
			КонецЕсли;
			НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
		Иначе	
			ОбластьКолонкиПоказатель.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
			Если КоличествоРазверток > 0 Тогда
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1 + ОстатокВысоты, НомерПервойКолонкиФункции, НомерСтроки + 1 + ОстатокВысоты, НомерКолонки - 1);
			Иначе	
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции, НомерСтроки, НомерКолонки - 1);
			КонецЕсли;	
		КонецЕсли;	
		ОбластьКолонкиФункция.Объединить();
		ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
		ОбластьКолонкиФункция.Расшифровка             = Новый Структура("Функция, ПутьКДанным, ИмяПоля", ТекФункция, ТекФункция.ИмяРесурса, ТекФункция.ИмяРесурса);
		ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
		ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.Функции.ГраницаСверху;
		ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
		ОбластьКолонкиФункция.ОриентацияТекста		  = ?((Не ПустаяСтрока(ТекФункция.ИмяРесурса)) И СтруктураОриентацииТекстаПолей.Свойство(ТекФункция.ИмяРесурса), СтруктураОриентацииТекстаПолей[ТекФункция.ИмяРесурса], 0);
		НомерПервойКолонкиИзмерения = НомерПервойКолонкиИзмерения+1;
	КонецЦикла;
	
	ОбластьКолонкиФункция.ГраницаСправа           = ПеремГраницаСправа;
	Если ОбластьКолонкиПоказатель<> Неопределено Тогда
		ОбластьКолонкиПоказатель.ГраницаСправа        = ПеремГраницаСправа;//ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
	КонецЕсли;
	Если КоличествоРазверток > 0 Тогда
		ОбластьКолонки = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + КоличествоРазверток, НомерКолонки - 1);
		ОбластьКолонки.Объединить();
		ОбластьКолонки.Текст                   = "Итог";
		ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонки.ГраницаСлева        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		ОбластьКолонки.ГраницаСверху       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
		ОбластьКолонки.ГраницаСправа       = ПеремГраницаСправа;
	КонецЕсли;
	Если ПостроительОтчета.ИзмеренияКолонки.Количество() > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + 2 + КоличествоРазверток, НомерКолонки - 1);
		ОбластьПостроителя.Имя = "ЗаголовокИтогаПоСтроке";
	КонецЕсли;
	
	ОбластьШапкиТаблицы = МакетОтчета.Область(ВысотаЗаголовка + 1, 3, ВысотаЗаголовка + 2 + КоличествоРазверток, НомерКолонки - 1);
	ОбластьШапкиТаблицы.Шрифт         = ОформлениеШапки.Шрифт;
	ОбластьШапкиТаблицы.ЦветТекста    = ОформлениеШапки.ЦветТекста;
	ОбластьШапкиТаблицы.ЦветФона      = ОформлениеШапки.ЦветФона;
	//	ОбластьШапкиТаблицы.ГраницаСверху = ЛинияОбычная;
	
	НачальноеОформление = Макс(0, 8 - КоличествоУровней);
	ПараметрыИзмерения  = Новый Структура("РесурсыОтчета,ДеревоРесурсов,КоличествоУровней,КоличествоРазверток,ОбластьОформлениеИзмерений,НачальноеОформление",
	РесурсыОтчета,ДеревоРесурсов,КоличествоУровней,КоличествоРазверток,ОбластьОформлениеИзмерений,НачальноеОформление);
	
	КлючСтроки = "";
	СчетчикУровня = 1;
	НачалоОформления = Макс(0, 8 - КоличествоУровней);
	ПараметрыИзмерения.Вставить("СтруктураДополненияПоказателей", СтруктураДополненияПоказателей);
	ПараметрыИзмерения.Вставить("КоличествоКолонокПоказателей", КоличествоКолонокПоказателей);
	
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияСтроки Цикл
		КлючСтроки = "";
		Попытка
			КлючСтроки = ОтчетОбъект.ПолучитьКлюч(ТекИзмерение.ПутьКДанным);
		Исключение
			// Для регистратора и периодичности установим ключ строки, что бы в дальнейшем удалить "пустышки"
			Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты или
				ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
				
				ПозицияРодителя = Найти(ТекИзмерение.ПутьКДанным, ".");
				ПутьКДаннымРодителя = ТекИзмерение.ПутьКДанным;
				Если ПозицияРодителя > 0 Тогда
					ПутьКДаннымРодителя = Лев(ТекИзмерение.ПутьКДанным, ПозицияРодителя - 1);
				КонецЕсли;
				
				ОтсутствуетВОстатках = Ложь;
				Попытка // необходимых ключей может и не быть в структуре
					ОтсутствуетВОстатках = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей[ПутьКДаннымРодителя].Использование.ОтсутствуетВОстатках;
				Исключение КонецПопытки;
				
				Если Найти(ТекИзмерение.ПутьКДанным, "ПериодРегистратор") = 1 или
					ТекИзмерение.ПутьКДанным = "ПериодДень" или
					ТекИзмерение.ПутьКДанным = "ПериодНеделя" или
					ТекИзмерение.ПутьКДанным = "ПериодМесяц" или
					ТекИзмерение.ПутьКДанным = "ПериодКвартал" или
					ТекИзмерение.ПутьКДанным = "ПериодГод" или ОтсутствуетВОстатках Тогда
					КлючСтроки = "ПР_";
				КонецЕсли;
			КонецЕсли;
		КонецПопытки;
		
		Если НЕ ПустаяСтрока(КлючСтроки) Тогда
			КлючСтроки = КлючСтроки + СтрЗаменить(ТекИзмерение.ПутьКДанным, ".", "") + "_";
		КонецЕсли;
		
		Если ТекИзмерение.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			ПараметрыИзмерения.Вставить("ЭтоИерархия", Истина);
			КлючСтрокиИерархии = ?(ПустаяСтрока(КлючСтроки), "", КлючСтроки + ?(ОтчетОбъект.ИспользоватьАвтоотступ, 3, СчетчикУровня + 2));
			отСформироватьМакетИзмеренияСтроки(ОтчетОбъект, ПостроительОтчета, МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, КлючСтрокиИерархии, ГраницаМакета);
			
			СчетчикУровня = СчетчикУровня + 1;
		КонецЕсли;
		
		ПараметрыИзмерения.Вставить("ЭтоИерархия", Ложь);
		КлючСтроки = ?(ПустаяСтрока(КлючСтроки), "", КлючСтроки + ?(ОтчетОбъект.ИспользоватьАвтоотступ, 3, СчетчикУровня + 2));
		отСформироватьМакетИзмеренияСтроки(ОтчетОбъект, ПостроительОтчета, МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, КлючСтроки, ГраницаМакета);
		
		СчетчикУровня = СчетчикУровня + 1;
	КонецЦикла;
	НомерСтроки  = МакетОтчета.ВысотаТаблицы + 1;
	НомерКолонки = НачальныйНомерВыводаКолонок;
	Если КоличествоРазверток > 0 Тогда
		НомерКолонки = НомерКолонки + ((КоличествоРазверток - 1) * КоличествоКолонокПоказателей);
	КонецЕсли;
	
	ДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные", Истина));
	КоличествоДополнительныеПоляОтчета = ДополнительныеПоляОтчета.Количество();
	НачальныйНомерВыводаИтогов = НачальныйНомерВыводаКолонок-КоличествоДополнительныеПоляОтчета;
	
	Для ИндПоля = 0 По КоличествоДополнительныеПоляОтчета-1 Цикл
		ТекПоле = ДополнительныеПоляОтчета[ИндПоля];
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаИтогов+ИндПоля, НомерСтроки, НачальныйНомерВыводаИтогов+ИндПоля);
		
		ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		Если ТекПоле.ВыводитьОбщийИтог Тогда
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьКолонки.Параметр                = ТекПоле.Имя;
			ОбластьКолонки.Формат                  = ТекПоле.Формат;
			ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		Иначе
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьКолонки.Текст                   = "";
			ОбластьКолонки.ПараметрРасшифровки     = "";
		КонецЕсли;
		
		ОбластьКолонки.ГраницаСлева  = ГраницаМакета.ИзмеренияИтог.ГраницаСлева;
		ОбластьКолонки.ГраницаСверху = ГраницаМакета.ИзмеренияИтог.ГраницаСверху;
		ОбластьКолонки.ГраницаСнизу  = ГраницаМакета.ИзмеренияИтог.ГраницаСнизу;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияИтог.ГраницаСправа;
	КонецЦикла;
	
	Попытка		
		ИспользованиеРесурсов=ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы;
	Исключение КонецПопытки;
	
	НомерПервойКолонки = НомерКолонки;
	Для Каждого ТекРесурс Из РесурсыОтчета Цикл
		Если ПустаяСтрока(ТекРесурс) Тогда
			ОтображатьПоказатель = Ложь;
		Иначе
		Попытка    ОтображатьПоказатель = ИспользованиеРесурсов[ТекРесурс];
		Исключение ОтображатьПоказатель = Истина;
		КонецПопытки;
		КонецЕсли;
		
		КоличествоДополнений=0;
		Если ЗначениеЗаполнено(ТекРесурс) И СтруктураДополненияПоказателей.Свойство(ТекРесурс) Тогда
			КоличествоДополнений=СтруктураДополненияПоказателей[ТекРесурс].Количество();
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки+КоличествоДополнений);
			ОбластьКолонки.Объединить();
		Иначе
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
		КонецЕсли;
		
		ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		Если ОтображатьПоказатель Тогда
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьКолонки.Параметр                = ТекРесурс;
			ОбластьКолонки.Формат                  = СтруктураФорматаПолей[ТекРесурс];
			ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		Иначе
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьКолонки.Текст                   = "";
			ОбластьКолонки.ПараметрРасшифровки     = "";
		КонецЕсли;
		
		ОбластьКолонки.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		
		Если КоличествоРазверток > 0 Тогда
			Если КоличествоДополнений>0 Тогда
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки + КоличествоКолонокПоказателей, НомерСтроки, НомерКолонки + КоличествоКолонокПоказателей + КоличествоДополнений);
				ОбластьКолонки.Объединить();
			Иначе
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки + КоличествоКолонокПоказателей, НомерСтроки, НомерКолонки + КоличествоКолонокПоказателей);
			КонецЕсли;
			
			ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			Если ОтображатьПоказатель Тогда
				ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьКолонки.Параметр                = ТекРесурс;
				ОбластьКолонки.Формат                  = СтруктураФорматаПолей[ТекРесурс];
				ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
				ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			Иначе
				ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьКолонки.Текст                   = "";
				ОбластьКолонки.ПараметрРасшифровки     = "";
			КонецЕсли;
		КонецЕсли;
		
		НомерКолонки = НомерКолонки + КоличествоДополнений + 1;
	КонецЦикла;
	ТекНомерКолонки = НомерПервойКолонки;
	СчетчикФункций=0;
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		СчетчикПоказателей = 0;
		Если ТекФункция.Одиночный Тогда
			КоличествоДополнений = 0;
			Если СтруктураДополненияПоказателей.Свойство(ТекФункция.ИмяРесурса) Тогда
				КоличествоДополнений = СтруктураДополненияПоказателей[ТекФункция.ИмяРесурса].Количество();
//				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки,
//					ТекНомерКолонки+КоличествоДополнений);
////				ОбластьКолонки.Объединить();
//			Иначе
//				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
			КонецЕсли;
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки+КоличествоДополнений);
			ОбластьКолонки.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева);
			ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
			ОбластьКолонки.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
			ОбластьКолонки.ГраницаСнизу = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
			
			Если КоличествоРазверток > 0 Тогда
//				Если КоличествоДополнений > 0 Тогда
//					ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки +
//						КоличествоКолонокПоказателей, НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей+КоличествоДополнений);
////					ОбластьКолонкиИтога.Объединить();
//				Иначе
//					ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки +
//					КоличествоКолонокПоказателей, НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей);
//				КонецЕсли;
				ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей, НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей+КоличествоДополнений);
				ОбластьКолонкиИтога.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева);
				ОбластьКолонкиИтога.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
				ОбластьКолонкиИтога.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
				ОбластьКолонкиИтога.ГраницаСнизу = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
			КонецЕсли;
			
			ТекНомерКолонки = ТекНомерКолонки+КоличествоДополнений+1;
		Иначе
			Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
				КоличествоДополнений = 0;
				Если СтруктураДополненияПоказателей.Свойство(ТекПоказатель.ИмяРесурса) Тогда
					КоличествоДополнений = СтруктураДополненияПоказателей[ТекПоказатель.ИмяРесурса].Количество();
					ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки+КоличествоДополнений);
					ОбластьКолонки.Объединить();
				Иначе
					ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
				КонецЕсли;
				
				ОбластьКолонки.ГраницаСлева  = ?(СчетчикПоказателей=0,?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева), ГраницаМакета.ПоказателиИтог.ГраницаСлева);
				ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
				ОбластьКолонки.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
				ОбластьКолонки.ГраницаСнизу = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
				
				Если КоличествоРазверток > 0 Тогда
					Если КоличествоДополнений > 0 Тогда
						ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей, НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей + КоличествоДополнений);
						ОбластьКолонкиИтога.Объединить();
					Иначе
						ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей, НомерСтроки, ТекНомерКолонки + КоличествоКолонокПоказателей);
					КонецЕсли;
					
					ОбластьКолонкиИтога.ГраницаСлева  = ?(СчетчикПоказателей=0,?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева), ГраницаМакета.ПоказателиИтог.ГраницаСлева);
					ОбластьКолонкиИтога.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
					ОбластьКолонкиИтога.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
					ОбластьКолонкиИтога.ГраницаСнизу = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
				КонецЕсли;
				
				ТекНомерКолонки = ТекНомерКолонки+КоличествоДополнений+1;
				СчетчикПоказателей = СчетчикПоказателей+1;
			КонецЦикла;
		КонецЕсли;
		СчетчикФункций=СчетчикФункций+1;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ФункцииИтог.ГраницаСправа;
	КонецЦикла;
	ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСправа;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьКолонкиИтога.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСправа;
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
		ОбластьПостроителя.Имя = "РесурсыИтогПоКолонке";
		
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки + КоличествоКолонокПоказателей, НомерСтроки, НомерКолонки - 1 + КоличествоКолонокПоказателей);
	Иначе
		ОбластьПостроителя = МакетОтчета.Область("R" + НомерСтроки);
	КонецЕсли;
	ОбластьПостроителя.Имя = "ОбщиеИтоги";
	ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, 3, НомерСтроки, НачальныйНомерВыводаИтогов - 1);
	ОбластьПостроителя.Объединить();
	ОбластьПостроителя.Текст                   = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Представление; //"Итог";
	ОбластьПостроителя.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
	ОбластьПостроителя.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
	ОбластьПостроителя.ГраницаСлева  = ГраницаМакета.ИзмеренияИтог.ГраницаСлева;
	ОбластьПостроителя.ГраницаСверху = ГраницаМакета.ИзмеренияИтог.ГраницаСверху;
	ОбластьПостроителя.ГраницаСнизу  = ГраницаМакета.ИзмеренияИтог.ГраницаСнизу;
	ОбластьПостроителя.ГраницаСправа = ГраницаМакета.ИзмеренияИтог.ГраницаСправа;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		ОбластьПостроителя.Имя = "ЗаголовокИтогаПоКолонке";
	КонецЕсли;
	
	ОбластьПодвалаТаблицы = МакетОтчета.Область(НомерСтроки, 3, НомерСтроки, НомерКолонки - 1 + ?(КоличествоРазверток > 0, КоличествоКолонокПоказателей, 0));
	ОбластьПодвалаТаблицы.Шрифт         = ОформлениеИтоги.Шрифт;
	ОбластьПодвалаТаблицы.ЦветТекста    = ОформлениеИтоги.ЦветТекста;
	ОбластьПодвалаТаблицы.ЦветФона      = ОформлениеИтоги.ЦветФона;
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "ПодвалТаблицы";
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "Подвал";
	
	ОбластьПостроителя = МакетОтчета.Область("R1:R" + (ВысотаЗаголовка + КоличествоРазверток + 2));
	ОбластьПостроителя.Имя = "ФиксированныеСтроки";    
	
	ОбластьПостроителя = МакетОтчета.Область("C1:C" + (НачальныйНомерВыводаКолонок - ?(ОтчетОбъект.ВыводитьДополнительныеПоляВОтдельнойКолонке, ОтчетОбъект.ДополнительныеПоляОтчета.Количество(), КоличествоДополнительныеПоляОтчета) - 1));
	ОбластьПостроителя.Имя = "ФиксированныеКолонки";
	
	Для к = 1 по ВысотаЗаголовка Цикл
		ТекОбласть = МакетОтчета.Область(к, 3, к, МакетОтчета.ШиринаТаблицы);
		ТекОбласть.ПоВыделеннымКолонкам = Истина;
		ТекОбласть.РазмещениеТекста     = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	КонецЦикла;
	
	Попытка
		Если ОтчетОбъект.ИзменитьМакетПостроителя(ПостроительОтчета, МакетОтчета, ПараметрыОформления) Тогда
			Возврат МакетОтчета;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Возврат МакетОтчета;
	
КонецФункции	//	отПолучитьМакетПостроителя()

// Процедура формирует оформленный табличный документ для использования в качестве макета построителя отчета
// у которого нет измерений
//
// Параметры:
//	МакетОтчета       - Табличный документ       - Макет отчета
//	ПараметрыОформления - Структура - параметры оформления макета
//  ТаблицаКолонок - Таблица значений - таблица колонок макета
//
Процедура отПолучитьМакетДетальныхЗаписейПостроителя(МакетОтчета, ПараметрыОформления, ТаблицаКолонок) Экспорт
	
	ОбластьОформлениеИзмерений	= ПараметрыОформления.ОбластьОформлениеИзмерений;
	ОформлениеШапки 			= ПараметрыОформления.ОформлениеШапки;
	ГраницаМакета				= ПараметрыОформления.ГраницаМакета;
	
	ВысотаЗаголовка = МакетОтчета.ВысотаТаблицы;
	
	МакетОтчета.Область(1, 1).ШиринаКолонки = 1;
	МакетОтчета.Область("R1:R" + ВысотаЗаголовка).Имя = "ЗаголовокШаблон";
	
	КонечныйНомерВыводаКолонок = ТаблицаКолонок.Количество()+1;
	НачальныйНомерВыводаКолонок = 2;
	Для Каждого ТекКолонка из ТаблицаКолонок Цикл
		ОбластьЗаголовокСтрок = МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок, ВысотаЗаголовка + 2, НачальныйНомерВыводаКолонок);
		ОбластьЗаголовокСтрок.Объединить();
		ОбластьЗаголовокСтрок.Текст                   = ТекКолонка.Представление;
		ОбластьЗаголовокСтрок.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьЗаголовокСтрок.ШиринаКолонки           = ТекКолонка.ШиринаКолонки;
		ОбластьЗаголовокСтрок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьЗаголовокСтрок.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьЗаголовокСтрок.ГраницаСлева            = ?(НачальныйНомерВыводаКолонок = 2, ГраницаМакета.ГраницаТаблицы.ГраницаСлева, ГраницаМакета.ГраницаКолонок.ГраницаСлева);
		ОбластьЗаголовокСтрок.ГраницаСверху           = ГраницаМакета.ГраницаТаблицы.ГраницаСверху;
		
		НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
	КонецЦикла;
	
	ОбластьЗаголовокСтрок.ГраницаСправа = ГраницаМакета.ГраницаТаблицы.ГраницаСправа;
	
	ОбластьШапкаСтрок = МакетОтчета.Область("R" + (ВысотаЗаголовка + 1) + ":R" + (ВысотаЗаголовка + 2));
	ОбластьШапкаСтрок.Имя = "ШапкаТаблицы";	
	
	ОбластьШапкиТаблицы = МакетОтчета.Область(ВысотаЗаголовка + 1, 2, ВысотаЗаголовка + 2, КонечныйНомерВыводаКолонок);
	ОбластьШапкиТаблицы.Шрифт         = ОформлениеШапки.Шрифт;
	ОбластьШапкиТаблицы.ЦветТекста    = ОформлениеШапки.ЦветТекста;
	ОбластьШапкиТаблицы.ЦветФона      = ОформлениеШапки.ЦветФона;
	
	ОформлениеУровня = ОбластьОформлениеИзмерений.Область(8, 1);
	
	НомерКолонки = 2;
	Для Каждого ТекКолонка Из ТаблицаКолонок Цикл
		ОбластьКолонки = МакетОтчета.Область(ВысотаЗаголовка+3, НомерКолонки, ВысотаЗаголовка+3, НомерКолонки);
		ОбластьКолонки.РазмещениеТекста		   = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ОбластьКолонки.Параметр                = ТекКолонка.Имя;
		ОбластьКолонки.Формат                  = ТекКолонка.Формат;
		ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Верх;
		ОбластьКолонки.ПараметрРасшифровки = ТекКолонка.Имя;
		
		ОбластьКолонки.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьКолонки.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьКолонки.ЦветФона      = ОформлениеУровня.ЦветФона;
		ОбластьКолонки.ГраницаСлева  = ?(НомерКолонки=2, ГраницаМакета.ГраницаТаблицыСтрока.ГраницаСлева, ГраницаМакета.ГраницаКолонокСтрока.ГраницаСлева);
		ОбластьКолонки.ГраницаСверху = ГраницаМакета.ГраницаКолонокСтрока.ГраницаСверху;
		НомерКолонки = НомерКолонки + 1;
	КонецЦикла;
	
	ОбластьКолонки.ГраницаСправа = ГраницаМакета.ГраницаТаблицыСтрока.ГраницаСправа;
	
	ОбластьСтрок = МакетОтчета.Область("R" + (ВысотаЗаголовка+3) + ":R" + (ВысотаЗаголовка+3));
	ОбластьСтрок.Имя = "Детали";
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "ПодвалТаблицы";
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "Подвал";
	
	ОбластьПостроителя = МакетОтчета.Область("R1:R" + (ВысотаЗаголовка + 2));
	ОбластьПостроителя.Имя = "ФиксированныеСтроки";    
	
	ОбластьПостроителя = МакетОтчета.Область("C1:C1");
	ОбластьПостроителя.Имя = "ФиксированныеКолонки";
	
	Для к = 1 по ВысотаЗаголовка Цикл
		ТекОбласть = МакетОтчета.Область(к, 2, к, МакетОтчета.ШиринаТаблицы);
		ТекОбласть.ПоВыделеннымКолонкам = Истина;
		ТекОбласть.РазмещениеТекста     = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	КонецЦикла;
	
КонецПроцедуры

// Процедура убирает строки "пустышки" из результата запроса
//
// Параметры:
//	ОтчетОбъект       - объект формируемого отчета
//	ПостроительОтчета - ссылка на построитель отчета
//	ДокументРезультат - результирующий табличный документ
//
Процедура отОбработкаРезультатаТабличногоДокумента(ОтчетОбъект, ПостроительОтчета, ДокументРезультат) Экспорт
	
	// Удалим "пустышки" документов движений или периодов в строках отчета
	Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты или
		ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		
		ИзмеренияОтсутствующиеВОстатках = Новый Структура();
		Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияСтроки Цикл
			Если Найти(ТекИзмерение.ПутьКДанным, "ПериодРегистратор") = 1 или
				ТекИзмерение.ПутьКДанным = "ПериодДень" или
				ТекИзмерение.ПутьКДанным = "ПериодНеделя" или
				ТекИзмерение.ПутьКДанным = "ПериодМесяц" или
				ТекИзмерение.ПутьКДанным = "ПериодКвартал" или
				ТекИзмерение.ПутьКДанным = "ПериодГод" Тогда
				
				ИзмеренияОтсутствующиеВОстатках.Вставить(ТекИзмерение.Имя);
				
			Иначе
				ПозицияРодителя = Найти(ТекИзмерение.ПутьКДанным, ".");
				РодительИзмерения = ТекИзмерение.ПутьКДанным;
				Если ПозицияРодителя > 0 Тогда
					РодительИзмерения = Лев(ТекИзмерение, ПозицияРодителя - 1);
				КонецЕсли;
				
				Попытка				
					Если ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей[РодительИзмерения].Использование["ОтсутствуетВОстатках"] Тогда
						ИзмеренияОтсутствующиеВОстатках.Вставить(ТекИзмерение.Имя);
					КонецЕсли;
				Исключение КонецПопытки;	
			КонецЕсли;
		КонецЦикла;	
		Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияКолонки Цикл
			Если Найти(ТекИзмерение.ПутьКДанным, "ПериодРегистратор") = 1 или
				ТекИзмерение.ПутьКДанным = "ПериодДень" или
				ТекИзмерение.ПутьКДанным = "ПериодНеделя" или
				ТекИзмерение.ПутьКДанным = "ПериодМесяц" или
				ТекИзмерение.ПутьКДанным = "ПериодКвартал" или
				ТекИзмерение.ПутьКДанным = "ПериодГод" Тогда
				
				ИзмеренияОтсутствующиеВОстатках.Вставить(ТекИзмерение.Имя);
				
			Иначе
				ПозицияРодителя = Найти(ТекИзмерение.ПутьКДанным, ".");
				РодительИзмерения = ТекИзмерение.ПутьКДанным;
				Если ПозицияРодителя > 0 Тогда
					РодительИзмерения = Лев(ТекИзмерение, ПозицияРодителя - 1);
				КонецЕсли;
				
				Попытка				
					Если ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей[РодительИзмерения].Использование["ОтсутствуетВОстатках"] Тогда
						ИзмеренияОтсутствующиеВОстатках.Вставить(ТекИзмерение.Имя);
					КонецЕсли;
				Исключение КонецПопытки;	
			КонецЕсли;
		КонецЦикла;	
		
		Если ПостроительОтчета.ИзмеренияСтроки.Количество() > 0 Тогда
			ИмяПервогоИзмерения = СтрЗаменить(ПостроительОтчета.ИзмеренияСтроки[0].ПутьКДанным, ".", "");
			к = ДокументРезультат.ФиксацияСверху + 1;
			Пока к <= ДокументРезультат.ВысотаТаблицы Цикл
				// Проверим каждую строку на наличие ключа (ПР)
				// если ключ найден, то это строка с регистратором или периодом, которая может являться "пустышкой"
				ТекПараметр = ДокументРезультат.Область(к, 1).Текст;
				Если Лев(ТекПараметр, 3) = "ПР_" Тогда
					ДокументРезультат.Область(к, 1).Текст = "";
					
					ТекПараметр  = Сред(ТекПараметр, 4);
					НомерКолонки = Число(Сред(ТекПараметр, Найти(ТекПараметр, "_") + 1));
					ИмяИзмерения = Лев(ТекПараметр, Найти(ТекПараметр, "_") - 1);
					
					Если ИмяПервогоИзмерения = ИмяИзмерения или
						ИзмеренияОтсутствующиеВОстатках.Свойство(ИмяИзмерения) Тогда
						УдалятьОбласть = Ложь;
					КонецЕсли;
					
					Если НЕ УдалятьОбласть Тогда
						// "Пустышку" определяем по отсутствию расшифровки
						Попытка // вдруг в расшифровке что-нибудь непонятное
							Расшифровка = ДокументРезультат.Область(к, НомерКолонки).Расшифровка;
							Если Расшифровка = Неопределено ИЛИ Расшифровка = NULL Тогда УдалятьОбласть = Истина;
							ИначеЕсли Расшифровка[ИмяИзмерения] = Неопределено ИЛИ Расшифровка[ИмяИзмерения] = NULL Тогда УдалятьОбласть = Истина;
							КонецЕсли;
						Исключение КонецПопытки;	
					КонецЕсли;
					
					Если УдалятьОбласть Тогда
						ДокументРезультат.УдалитьОбласть(ДокументРезультат.Область(к,, к,), ТипСмещенияТабличногоДокумента.ПоВертикали);
						
					Иначе
						к = к + 1;
					КонецЕсли;
					
				Иначе
					УдалятьОбласть = Ложь;
					
					// Переходим на следующую строку отчета
					к = к + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Удалим "пустышки" документов движений или периодов в колонках отчета
		Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты Тогда
			Если ПостроительОтчета.ИзмеренияКолонки.Количество() > 0 Тогда
				ИмяПервогоИзмерения = СтрЗаменить(ПостроительОтчета.ИзмеренияКолонки[0].ПутьКДанным, ".", "");
				к = ДокументРезультат.ФиксацияСлева + 1;
				Пока к <= ДокументРезультат.ШиринаТаблицы Цикл
					// Проверим каждую колонку на наличие ключа (ПР)
					// если ключ найден, то это колонка с регистратором или периодом, которая может являться "пустышкой"
					ТекПараметр = ДокументРезультат.Область(1, к).Текст;
					Если Лев(ТекПараметр, 3) = "ПР_" Тогда
						ДокументРезультат.Область(1, к).Текст = "";
						
						ТекПараметр  = Сред(ТекПараметр, 4);
						ИмяИзмерения = Лев(ТекПараметр, Найти(ТекПараметр, "_") - 1);
						ТекПараметр  = Сред(ТекПараметр, Найти(ТекПараметр, "_") + 1);
						НомерСтроки  = Число(Лев(ТекПараметр, Найти(ТекПараметр, "_") - 1));
						ЧислоКолонок = Число(Сред(ТекПараметр, Найти(ТекПараметр, "_") + 1));
						
						Если ИмяПервогоИзмерения = ИмяИзмерения или
							ИзмеренияОтсутствующиеВОстатках.Свойство(ИмяИзмерения) Тогда
							УдалятьОбласть = Ложь;
						КонецЕсли;
						
						Если НЕ УдалятьОбласть Тогда
							// "Пустышку" определяем по отсутствию расшифровки
							Попытка // вдруг в расшифровке что-нибудь непонятное
								Расшифровка = ДокументРезультат.Область(НомерСтроки, к).Расшифровка;
								Если Расшифровка = Неопределено ИЛИ Расшифровка = NULL Тогда УдалятьОбласть = Истина;
								ИначеЕсли Расшифровка[ИмяИзмерения] = Неопределено ИЛИ Расшифровка[ИмяИзмерения] = NULL Тогда УдалятьОбласть = Истина;
								КонецЕсли;
							Исключение КонецПопытки;	
						КонецЕсли;
						
						Если УдалятьОбласть Тогда
							ДокументРезультат.УдалитьОбласть(ДокументРезультат.Область(, к,, к + ЧислоКолонок), ТипСмещенияТабличногоДокумента.ПоВертикали);
							
						Иначе
							к = к + 1;
						КонецЕсли;
						
					Иначе
						УдалятьОбласть = Ложь;
						
						// Переходим на следующую строку отчета
						к = к + 1;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
//ПРОЦЕДУРЫ И ФУНКЦИИ ПОСТРОЕНИЯ ДИАГРАММ И СВОДНЫХ ТАБЛИЦ

// Функция формирует отчет в виде сводной таблицы
//
// Параметры:
//	ОтчетОбъект          - ОтчетОбъект    - Объект отчета
//	ДокументРезультат    - ДокументОбъект - Документ вывода отчета
//	ПараметрФормирования - Число		  - Параметр формирования	
//
// Возвращаемое значение:
//	ДокументОбъект - Документ вывода отчета
//
Функция отСформироватьСводнуюТаблицу(ОтчетОбъект, ДокументРезультат=Неопределено, ПараметрФормирования=0) Экспорт
	
	ПостроительОтчета = Новый ПостроительОтчета;
	
	Если ТипЗнч(ДокументРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокументРезультат) <> Тип("ТабличныйДокумент") Тогда
		ДокументРезультат = Новый ТабличныйДокумент();
	КонецЕсли;
	ДокументРезультат.Очистить();
	
	Если Не ПараметрФормирования Тогда
		Если Не ПодготовкаПостроителяОтчета(ОтчетОбъект, ПостроительОтчета) Тогда
			Возврат ДокументРезультат;
		КонецЕсли;
	Иначе
		Если Не ПодготовкаПостроителяСИсточникомДанных(ОтчетОбъект, ПостроительОтчета, ПараметрФормирования = 2) Тогда
			Возврат ДокументРезультат;
		КонецЕсли;
	КонецЕсли;
	
	ПостроительОтчета.Порядок.Очистить();
	
	Попытка
		ТепПараметрыИспользованияПолей = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей;
	Исключение
		ТепПараметрыИспользованияПолей = Неопределено;
	КонецПопытки;
	
	Попытка
		ТепСтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение
		ТепСтруктураСвязиСвойств = Неопределено;
	КонецПопытки;
	
	СтруктураРесурсов = Новый Структура;
	
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			Если Не ТекГруппа.Группа Тогда
				СтруктураРесурсов.Вставить(ТекГруппа.ПутьКДанным, ТекГруппа.Представление);
			КонецЕсли;
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				СтруктураРесурсов.Вставить(ТекСтрока.ПутьКДанным, ТекСтрока.Представление + " " + ТекСтрока.Представление);
			КонецЦикла;
		КонецЦикла;
	Исключение
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				ИмяРесурса = ТекПоказатель.Имя + ТекФункция.Имя;
				СтруктураРесурсов.Вставить(ИмяРесурса, ТекПоказатель.Представление + " " + ТекФункция.Представление);
			КонецЦикла;
		КонецЦикла;
	КонецПопытки;
	
	Если ТипЗнч(ТепСтруктураСвязиСвойств)=Тип("Структура") И ТипЗнч(ТепПараметрыИспользованияПолей)=Тип("Структура") Тогда
		Для Каждого ТекПоле Из ПостроительОтчета.ДоступныеПоля Цикл
			Если ТепПараметрыИспользованияПолей.Свойство(ТекПоле.Имя) Тогда
				ТекПоле.Представление = ТепПараметрыИспользованияПолей[ТекПоле.Имя].Представление;
			ИначеЕсли ТепСтруктураСвязиСвойств.Свойство(ТекПоле.Имя) Тогда
				ТекПоле.Представление = ТепСтруктураСвязиСвойств[ТекПоле.Имя].Представление+" (" +(ТепСтруктураСвязиСвойств[ТекПоле.Имя].ИмяИзмерения)+")";
			ИначеЕсли СтруктураРесурсов.Свойство(ТекПоле.Имя) Тогда
				ТекПоле.Представление = СтруктураРесурсов[ТекПоле.Имя];
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// добавляем сводную таблицу в документ результат
	СводнаяТаблица = ДокументРезультат.ВстроенныеТаблицы.Добавить(Тип("СводнаяТаблица"));
	СводнаяТаблица.ИсточникДанных = ПостроительОтчета;
	СводнаяТаблица.Обновление     = Ложь;
	// добавим выбранные измерения строк и колонок
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияСтроки Цикл
		Если СводнаяТаблица.Поля.Найти(ТекИзмерение.ПутьКДанным) = Неопределено тогда продолжить; КонецЕсли;
		НоваяСтрока = СводнаяТаблица.Строки.Добавить(ТекИзмерение.ПутьКДанным);
		Если ТекИзмерение.ТипИзмерения<>ТипИзмеренияПостроителяОтчета.Элементы Тогда 
			НоваяСтрока.КоличествоОткрытыхУровней = НоваяСтрока.КоличествоУровней();
		КонецЕсли;
	КонецЦикла;
	Для Каждого ТекКолонка Из ПостроительОтчета.ИзмеренияКолонки Цикл
		Если СводнаяТаблица.Поля.Найти(ТекКолонка.ПутьКДанным) = Неопределено тогда продолжить; КонецЕсли;
		НоваяКолонка = СводнаяТаблица.Колонки.Добавить(ТекКолонка.ПутьКДанным);
		Если ТекКолонка.ТипИзмерения<>ТипИзмеренияПостроителяОтчета.Элементы Тогда 
			НоваяКолонка.КоличествоОткрытыхУровней = НоваяКолонка.КоличествоУровней();
		КонецЕсли;
	КонецЦикла;
	
	СводнаяТаблица.Данные.Очистить();
	Для Каждого ТекРесурс Из СтруктураРесурсов Цикл
		Если СводнаяТаблица.Поля.Найти(ТекРесурс.Ключ) = Неопределено Тогда Продолжить; КонецЕсли;
		СводнаяТаблица.Данные.Добавить(ТекРесурс.Ключ);
	КонецЦикла;
	
	СводнаяТаблица.ОтображатьПоля = Истина;
	СводнаяТаблица.АвтоФиксация = Истина;
	СводнаяТаблица.ПоложениеИтоговКолонок = ПоложениеИтоговКолонокСводнойТаблицы.Лево;
	СводнаяТаблица.ПоложениеИтоговСтрок   = ПоложениеИтоговСтрокСводнойТаблицы.Верх;
	СводнаяТаблица.РазмещениеИзмеренийВКолонках = ТипРазмещенияИзмерений.Отдельно;
	СводнаяТаблица.РазмещениеИзмеренийВСтроках  = ТипРазмещенияИзмерений.Вместе;
	СводнаяТаблица.МакетОформления = ОтчетОбъект.ВариантОформленияСводнойТаблицы;
	СводнаяТаблица.Обновление = Истина;	
	
	Возврат ДокументРезультат;
	
КонецФункции // отПолучитьСтруктуруЗаголовкаОтчета()

// Функция формирует диаграмму
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект - Объект отчета
//	ПолеДиаграммы		- Диаграмма	  - Диаграмма
// 	СтруктураПараметров - Структура   - Структура параметров
//	ПараметрФормирования - Число	  - Параметр формирования	
//
// Возвращаемое значение:
//	Диаграмма - Диаграмма
//
Функция отСформироватьДиаграмму(ОтчетОбъект, ПолеДиаграммы, СтруктураПараметров, ПараметрФормирования=0) Экспорт
	
	// проверим корректность заполнения
	Если НЕ отКорректностьЗаполнения(ОтчетОбъект) Тогда
		Возврат ПолеДиаграммы;
	КонецЕсли;
	
	#Если Клиент Тогда
		Состояние("Подготовка к построению ...");
	#КонецЕсли
	
	ПостроительОтчета =  Новый ПостроительОтчета;
	
	//распакуем параметры диаграммы
	ВыбСерия         = СтруктураПараметров.Серии;
	ВыбТочка         = СтруктураПараметров.Точки; 
	ВыбПоказатель    = СтруктураПараметров.Показатель;
	ВыбФункция       = СтруктураПараметров.Функция;
	КоличествоСерий  = СтруктураПараметров.МаксСерий;
	КоличествоТочек  = СтруктураПараметров.МаксТочек;
	флСводнаяСерия   = СтруктураПараметров.СводнаяСерия;
	флСводнаяТочка   = СтруктураПараметров.СводнаяТочка;
	СписокГруппСерий = СтруктураПараметров.ГруппыСерий;
	СписокГруппТочек = СтруктураПараметров.ГруппыТочек;
	флФильтроватьСерии = (СписокГруппСерий.Количество() > 0);
	флФильтроватьТочки = (СписокГруппТочек.Количество() > 0);
	
	ТекПараметрыСерии = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ВыбСерия, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ФорматСерии = ?(ТекПараметрыСерии=Неопределено ИЛИ Не ТекПараметрыСерии.Свойство("Формат"),"",ТекПараметрыСерии.Формат);
	
	ТекПараметрыТочки = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ВыбТочка, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ФорматТочки = ?(ТекПараметрыТочки=Неопределено ИЛИ Не ТекПараметрыТочки.Свойство("Формат"),"",ТекПараметрыТочки.Формат);
	
	//выводимые измерения
	ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки;
	ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки;
	
	//сохраняемая на этапе вывода диаграммы структура параметров отчета
	ТекущееСостояние = Новый Структура("ИзмеренияСтроки, ИзмеренияКолонки, Показатели, Функции");
	ТекущееСостояние.ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки.Выгрузить();
	ТекущееСостояние.ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки.Выгрузить();
	ТекущееСостояние.Показатели = ОтчетОбъект.Показатели.Выгрузить();
	ТекущееСостояние.Функции = ОтчетОбъект.Функции.Выгрузить();
	
	//выбранные серии и точки должны соответствовать измерениям построителя (серия на 1-м месте, точка - на 2-м)
	НайдСерия = ИзмеренияСтроки.Найти(ВыбСерия);
	Если НайдСерия = Неопределено Тогда //не нашли в строках, ищем в колонках
		НайдСерия = ИзмеренияКолонки.Найти(ВыбСерия);
	КонецЕсли;
	НайдСерия_ПутьКДанным   = НайдСерия.ПутьКДанным;
	НайдСерия_Представление = НайдСерия.Представление;
	НайдСерия_ТипИзмерения  = НайдСерия.ТипИзмерения;
	
	Если ВыбТочка <> Неопределено Тогда
		НайдТочка = ИзмеренияСтроки.Найти(ВыбТочка);
		Если НайдТочка = Неопределено Тогда //не нашли в строках, ищем в колонках
			НайдТочка = ИзмеренияКолонки.Найти(ВыбТочка);
		КонецЕсли;
		НайдТочка_ПутьКДанным   = НайдТочка.ПутьКДанным;
		НайдТочка_Представление = НайдТочка.Представление;
		НайдТочка_ТипИзмерения  = НайдТочка.ТипИзмерения;
	КонецЕсли;
	
	ИзмеренияСтроки.Очистить();
	ИзмеренияКолонки.Очистить();
	НовоеИзмерение = ИзмеренияСтроки.Добавить();
	НовоеИзмерение.Использование = Истина;
	НовоеИзмерение.ПутьКДанным   = НайдСерия_ПутьКДанным;
	НовоеИзмерение.Представление = НайдСерия_Представление;
	НовоеИзмерение.ТипИзмерения  = ?(НайдСерия_ТипИзмерения = "Иерархия", "Только иерархия", НайдСерия_ТипИзмерения);
	
	//запоминаем имя, тип измерения серии
	Измерение1Имя = СтрЗаменить(НовоеИзмерение.ПутьКДанным, ".", "");
	Измерение1ТипИзмерения = НовоеИзмерение.ТипИзмерения;
	
	ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(НовоеИзмерение.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ИмяТекИзмерения1 = Измерение1Имя;
	ТипЗаполненияОбластиИзмерения1 = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	Попытка
		Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
			ИмяТекИзмерения1 = СтрЗаменить(ТекПараметрыИзмерения.ОсновноеПредставление, ".", "");
		ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
			ИмяТекИзмерения1 = ТекПараметрыИзмерения.СоставноеПредставление.Представление;
			ТипЗаполненияОбластиИзмерения1 = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
		КонецЕсли;
	Исключение
	КонецПопытки;
	Если ВыбТочка <> Неопределено Тогда
		НовоеИзмерение = ИзмеренияКолонки.Добавить();
		НовоеИзмерение.Использование = Истина;
		НовоеИзмерение.ПутьКДанным   = НайдТочка_ПутьКДанным;
		НовоеИзмерение.Представление = НайдТочка_Представление;
		НовоеИзмерение.ТипИзмерения  = ?(НайдТочка_ТипИзмерения = "Иерархия", "Только иерархия", НайдТочка_ТипИзмерения);;
		//запоминаем имя, тип измерения точки
		Измерение2Имя = СтрЗаменить(НовоеИзмерение.ПутьКДанным, ".", "");
		Измерение2ТипИзмерения = НовоеИзмерение.ТипИзмерения;
		
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(НовоеИзмерение.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		ИмяТекИзмерения2 = Измерение2Имя;
		ТипЗаполненияОбластиИзмерения2 = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ИмяТекИзмерения2 = СтрЗаменить(ТекПараметрыИзмерения.ОсновноеПредставление, ".", "");
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ИмяТекИзмерения2 = ТекПараметрыИзмерения.СоставноеПредставление.Представление;
				ТипЗаполненияОбластиИзмерения2 = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	// получим поле диаграммы
	Если ТипЗнч(ПолеДиаграммы) <> Тип("Диаграмма") Тогда
		ПолеДиаграммы = Новый Диаграмма();
	Иначе
		ПолеДиаграммы.Очистить();	
	КонецЕсли;
	
	НайденнаяФункция = ОтчетОбъект.Функции.Найти(ВыбФункция);
	НайденныйПоказатель = ОтчетОбъект.Показатели.Найти(ВыбПоказатель);
	Если НайденнаяФункция = Неопределено Тогда
		НайденнаяФункция = ОтчетОбъект.Функции.Найти(Истина, "Использование");
	КонецЕсли;
	
	Если НайденныйПоказатель = Неопределено Тогда
		НайденныйПоказатель = ОтчетОбъект.Показатели.Найти(Истина, "Использование"); 
	КонецЕсли;
	ПоказательФункция = СокрЛП(НайденныйПоказатель.Имя + НайденнаяФункция.Имя);	
	
	#Если Клиент Тогда
		Состояние("Получение данных ...");
	#КонецЕсли
	
	ПостроительОтчета.Выполнить();
	
	//формируем макет для вывода данных в промежуточный ТабДок
	МакетДанных = Новый ТабличныйДокумент;
	Если ВыбТочка = Неопределено Тогда
		МакетДанных.Область(1, 0, 1, 0).Имя = "ШапкаСтрок";
		МакетДанных.Область(2,  , 2,  ).Имя = Измерение1Имя;
		МакетДанных.Область(2, 1, 2, 1).Заполнение = ТипЗаполненияОбластиИзмерения1;
		МакетДанных.Область(2, 1, 2, 1).Формат     = ФорматСерии;
		МакетДанных.Область(2, 1, 2, 1).Параметр = ИмяТекИзмерения1;
		МакетДанных.Область(2, 1, 2, 1).ПараметрРасшифровки = Измерение1Имя;
		МакетДанных.Область(2, 2, 2, 2).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(2, 2, 2, 2).Параметр = ПоказательФункция;
		МакетДанных.Область(2, 2, 2, 2).ПараметрРасшифровки = ПоказательФункция;
	Иначе
		МакетДанных.Область(1, 1, 1, 1).Имя = "ШапкаСтрок";
		МакетДанных.Область(2, 1, 2, 1).Имя = Измерение1Имя;
		МакетДанных.Область(2, 2, 2, 2).Имя = Измерение1Имя + "Ресурсы";
		МакетДанных.Область(1, 2, 1, 2).Имя = Измерение2Имя;
		МакетДанных.Область(1, 2, 1, 2).Заполнение = ТипЗаполненияОбластиИзмерения2;
		МакетДанных.Область(1, 2, 1, 2).Формат     = ФорматТочки;
		МакетДанных.Область(1, 2, 1, 2).Параметр = ИмяТекИзмерения2;
		МакетДанных.Область(1, 2, 1, 2).ПараметрРасшифровки = Измерение2Имя;
		МакетДанных.Область(2, 1, 2, 1).Заполнение = ТипЗаполненияОбластиИзмерения1;
		МакетДанных.Область(2, 1, 2, 1).Формат     = ФорматСерии;
		МакетДанных.Область(2, 1, 2, 1).Параметр = ИмяТекИзмерения1;
		МакетДанных.Область(2, 1, 2, 1).ПараметрРасшифровки = Измерение1Имя;
		МакетДанных.Область(2, 2, 2, 2).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(2, 2, 2, 2).Параметр = ПоказательФункция;
		МакетДанных.Область(2, 2, 2, 2).ПараметрРасшифровки = ПоказательФункция;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	
	РезультатВыполнения = Ложь;
	
	Если Не ПараметрФормирования Тогда
		РезультатВыполнения = ПодготовкаПостроителяОтчета(ОтчетОбъект, ПостроительОтчета);
	Иначе
		РезультатВыполнения = ПодготовкаПостроителяСИсточникомДанных(ОтчетОбъект, ПостроительОтчета, ПараметрФормирования = 2);
	КонецЕсли;
	
	Если Не РезультатВыполнения Тогда
		ОтчетОбъект.ИзмеренияСтроки.Загрузить(ТекущееСостояние.ИзмеренияСтроки);
		ОтчетОбъект.ИзмеренияКолонки.Загрузить(ТекущееСостояние.ИзмеренияКолонки);
		ОтчетОбъект.Функции.Загрузить(ТекущееСостояние.Функции);
		ОтчетОбъект.Показатели.Загрузить(ТекущееСостояние.Показатели);
		Возврат ПолеДиаграммы;
	КонецЕсли;
	
	//переопределяем макет и выводим результат в промежуточный ТабДок
	ПостроительОтчета.Макет = МакетДанных;
	ПостроительОтчета.Вывести(ТабДок);
	// вывод диаграммы
	ПолеДиаграммы.Обновление = Ложь;
	
	//получаем массив цветовой палитры для диаграммы
	ЦветаСерий = ПолучитьОбщийМакет("ПечатьДиаграммы").ПолучитьОбласть("ЦветаСерий");
	МассивЦветовСерий = Новый Массив;
	Для Сч = 1 По ЦветаСерий.ВысотаТаблицы Цикл
		МассивЦветовСерий.Добавить(ЦветаСерий.Область(Сч, 2).ЦветФона);
	КонецЦикла;
	КоличЦветов = МассивЦветовСерий.Количество();
	
	#Если Клиент Тогда
		Состояние("Вывод диаграммы ...");
	#КонецЕсли
	
	//структура ТЗ диаграммы
	ЗначенияДиаграммы = Новый ТаблицаЗначений;
	ЗначенияДиаграммы.Колонки.Добавить("Серия");
	ЗначенияДиаграммы.Колонки.Добавить("Точка");
	ЗначенияДиаграммы.Колонки.Добавить("СерияПредставление");
	ЗначенияДиаграммы.Колонки.Добавить("ТочкаПредставление");
	ЗначенияДиаграммы.Колонки.Добавить("Значение");
	Отбор = Новый Структура("Серия, Точка");
	//перекидываем значения из табличного документа в ТЗ диаграммы
	Для ИндС = 1 По ТабДок.ВысотаТаблицы Цикл
		Ячейка = ТабДок.Область(ИндС, 1, ИндС, 1);
		Знач1 = Ячейка.Расшифровка; //серия
		СерияПредставление = Ячейка.Текст;
		//пропускаем пустые серии
		Если обЗначениеНеЗаполнено(Знач1) Тогда Продолжить; КонецЕсли;
		//пропускаем группы, не входящие в список значений серий
		Если флФильтроватьСерии И СписокГруппСерий.НайтиПоЗначению(Знач1) = Неопределено Тогда Продолжить; КонецЕсли;
		Если ВыбТочка = Неопределено Тогда //только серии
			Знач3 = ТабДок.Область(ИндС, 2, ИндС, 2).Расшифровка; //значение
			//пропускаем пустые значения
			Если ТипЗнч(Знач3) <> Тип("Число") ИЛИ Знач3 = 0 Тогда Продолжить; КонецЕсли;
			// удаляем паразитные записи
			Если ЗначенияДиаграммы.Найти(Знач1, "Серия") <> Неопределено Тогда Продолжить; КонецЕсли;
			НовоеЗначение = ЗначенияДиаграммы.Добавить();
			НовоеЗначение.Серия = Знач1;
			НовоеЗначение.СерияПредставление = СерияПредставление;
			НовоеЗначение.Значение = Знач3;
		Иначе //и серии и точки
			Для ИндК = 2 По ТабДок.ШиринаТаблицы Цикл
				Ячейка = ТабДок.Область(1, ИндК, 1, ИндК); //точка
				Знач2 = Ячейка.Расшифровка; //точка
				ТочкаПредставление = Ячейка.Текст; //точка
				//пропускаем пустые точки
				Если обЗначениеНеЗаполнено(Знач2) Тогда Продолжить; КонецЕсли;
				Знач3 = ТабДок.Область(ИндС, ИндК, ИндС, ИндК).Расшифровка; //значение
				//пропускаем пустые значения
				Если ТипЗнч(Знач3) <> Тип("Число") ИЛИ Знач3 = 0 Тогда Продолжить; КонецЕсли;
				//пропускаем группы, не входящие в список значений серий
				Если флФильтроватьСерии И СписокГруппСерий.НайтиПоЗначению(Знач1) = Неопределено Тогда Продолжить; КонецЕсли;
				//пропускаем группы, не входящие в список значений точек
				Если флФильтроватьТочки И СписокГруппТочек.НайтиПоЗначению(Знач2) = Неопределено Тогда Продолжить; КонецЕсли;
				// удаляем паразитные записи
				Отбор.Вставить("Серия", Знач1);
				Отбор.Вставить("Точка", Знач2);
				Если ЗначенияДиаграммы.НайтиСтроки(Отбор).Количество()>0 Тогда Продолжить; КонецЕсли;
				
				НовоеЗначение = ЗначенияДиаграммы.Добавить();
				НовоеЗначение.Серия    = Знач1;
				НовоеЗначение.Точка    = Знач2;
				НовоеЗначение.ТочкаПредставление = ТочкаПредставление;
				НовоеЗначение.СерияПредставление = СерияПредставление;
				НовоеЗначение.Значение = Знач3;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	//введем списки серий и точек, и отсортируем их
	тблСерии = ЗначенияДиаграммы.Скопировать();
	тблСерии.Свернуть("Серия, СерияПредставление", "Значение");
	ЗначенияДиаграммы.Свернуть("Серия, Точка, ТочкаПредставление, СерияПредставление", "Значение");
	
	СортироватьПоЗначению = Ложь;
	Если ПостроительОтчета.Порядок.Количество()>0 Тогда
		СортироватьПоЗначению = (ВыбПоказатель+ВыбФункция=ПостроительОтчета.Порядок[0].ПутьКДанным);
	КонецЕсли;
	
	Если СортироватьПоЗначению Тогда
		ТекНаправлениеСортировки = ?(ПостроительОтчета.Порядок[0].Направление = НаправлениеСортировки.Возр, "Возр", "Убыв");
		тблСерии.Сортировать("Значение "+ТекНаправлениеСортировки);
		ЗначенияДиаграммы.Сортировать("Значение "+ТекНаправлениеСортировки+", Серия ВОЗР, Точка ВОЗР");
	Иначе
		тблСерии.Сортировать("Серия ВОЗР");
		ЗначенияДиаграммы.Сортировать("Серия ВОЗР, Точка ВОЗР, Значение ВОЗР");
	КонецЕсли;
	
	ИндСерий = 0;
	Пока ИндСерий < тблСерии.Количество() Цикл
		ТекСерия = тблСерии[ИндСерий].Серия;
		СерияПредставление = тблСерии[ИндСерий].СерияПредставление;
		ВыборкаТочек = ЗначенияДиаграммы.НайтиСтроки(Новый Структура("Серия", ТекСерия));
		Если ИндСерий >= КоличествоСерий Тогда
			Если флСводнаяСерия Тогда
				ТекСерия = "Прочие...";
				СерияПредставление = "Прочие...";
			Иначе
				Для Каждого ТекТочка Из ВыборкаТочек Цикл
					ЗначенияДиаграммы.Удалить(ТекТочка);
				КонецЦикла;
				ИндСерий = ИндСерий + 1;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ИндТочек = 0;
		Пока ИндТочек < ВыборкаТочек.Количество() Цикл
			ТекСтрока = ВыборкаТочек[ИндТочек];
			ТекСтрока.Серия = ТекСерия;
			ТекСтрока.СерияПредставление = СерияПредставление;
			Если ИндТочек >= КоличествоТочек Тогда
				Если флСводнаяТочка Тогда
					ТекСтрока.Точка = "Прочие...";
					ТекСтрока.ТочкаПредставление = "Прочие...";
				Иначе
					ЗначенияДиаграммы.Удалить(ТекСтрока);
				КонецЕсли;
			КонецЕсли;
			ИндТочек = ИндТочек+1;
		КонецЦикла;
		ИндСерий = ИндСерий + 1;
	КонецЦикла;
	
	ЗначенияДиаграммы.Свернуть("Серия, Точка, ТочкаПредставление, СерияПредставление", "Значение");
	тблТочки = ЗначенияДиаграммы.Скопировать();
	тблТочки.Свернуть("Точка, ТочкаПредставление");
	тблТочки.Сортировать("Точка ВОЗР");
	тблСерии = ЗначенияДиаграммы.Скопировать();
	тблСерии.Свернуть("Серия, СерияПредставление");
	тблСерии.Сортировать("Серия ВОЗР");
	
	ПрочиеСерии = тблСерии.Найти("Прочие...", "Серия");
	Если ПрочиеСерии <> Неопределено Тогда
		тблСерии.Сдвинуть(ПрочиеСерии, тблСерии.Количество()-тблСерии.Индекс(ПрочиеСерии)-1);
	КонецЕсли;
	ПрочиеТочки = тблТочки.Найти("Прочие...", "Точка");
	Если ПрочиеТочки <> Неопределено Тогда
		тблТочки.Сдвинуть(ПрочиеТочки, тблТочки.Количество()-тблТочки.Индекс(ПрочиеТочки)-1);
	КонецЕсли;
	
	//РУЧНОЕ ФОРМИРОВАНИЕ ДИАГРАММЫ - НАЧАЛО
	// добавляем точки
	Для Каждого ТекТочка Из тблТочки Цикл
		//добавляем новую точку в диаграмму
		//ПолеДиаграммы.Точки.Добавить(Формат(ТекТочка.ТочкаПредставление, ФорматТочки));
		ПолеДиаграммы.Точки.Добавить(ТекТочка.ТочкаПредставление);
	КонецЦикла;
	
	//добавляем серии, а точки берем из сформированного списка
	ИндСерия = 0;
	Для Каждого ТекСерия Из тблСерии Цикл
		//добавляем новую серию в диаграмму
		//НоваяСерия = ПолеДиаграммы.Серии.Добавить(Формат(ТекСерия.СерияПредставление, ФорматСерии));
		НоваяСерия = ПолеДиаграммы.Серии.Добавить(ТекСерия.СерияПредставление);
		НоваяСерия.Цвет = МассивЦветовСерий[ИндСерия - Цел((ИндСерия)/КоличЦветов)*КоличЦветов];
		НоваяСерия.Расшифровка = Новый Структура(Измерение1Имя, ТекСерия.Серия);
		ИндТочка = 0;
		Для Каждого НоваяТочка Из ПолеДиаграммы.Точки Цикл
			ТекТочка = тблТочки[ИндТочка];
			НоваяТочка.Расшифровка = Новый Структура(Измерение2Имя, ТекТочка.ТочкаПредставление);
			Значение = 0;
			Расшифровка = Неопределено;	
			ТекЗначение = ЗначенияДиаграммы.НайтиСтроки(Новый Структура("Точка, Серия", ТекТочка.Точка, ТекСерия.Серия));
			Если НЕ (ТекЗначение.Количество() = 0 ИЛИ ТекЗначение[0].Значение = 0) Тогда
				ТекСтрока = ТекЗначение[0];
				Значение = ТекСтрока.Значение;
				Если ТекТочка.Точка<>"Прочие..." И ТекСерия.Серия<>"Прочие..." Тогда
					Расшифровка = Новый Структура(Измерение1Имя+","+Измерение2Имя, ТекСтрока.Серия, ТекСтрока.Точка);
				КонецЕсли;
			КонецЕсли;
			//устанавливаем числовое значение в [ТекТочка, ТекСерия], а заодно и расшифровку
			ПолеДиаграммы.УстановитьЗначение(НоваяТочка, НоваяСерия, Значение, Расшифровка);
			ИндТочка = ИндТочка + 1;
		КонецЦикла;
		ИндСерия = ИндСерия + 1;
	КонецЦикла;
	////РУЧНОЕ ФОРМИРОВАНИЕ ДИАГРАММЫ - КОНЕЦ
	//
	#Если Клиент Тогда
		Состояние();
	#КонецЕсли
	
	ПолеДиаграммы.Обновление = Истина;
	
	//восстановим параметры отчета
	ОтчетОбъект.ИзмеренияСтроки.Загрузить(ТекущееСостояние.ИзмеренияСтроки);
	ОтчетОбъект.ИзмеренияКолонки.Загрузить(ТекущееСостояние.ИзмеренияКолонки);
	ОтчетОбъект.Функции.Загрузить(ТекущееСостояние.Функции);
	ОтчетОбъект.Показатели.Загрузить(ТекущееСостояние.Показатели);
	
	Возврат ПолеДиаграммы;
КонецФункции //	отСформироватьДиаграмму()

// Функция формирует диаграмму
//
// Параметры:
//	ОтчетОбъект         - ОтчетОбъект - Объект отчета
//	ПолеДиаграммы		- Диаграмма	  - Диаграмма
// 	СтруктураПараметров - Структура   - Структура параметров
//
// Возвращаемое значение:
//	Диаграмма - Диаграмма
//
Функция _отСформироватьДиаграмму(ОтчетОбъект, ПолеДиаграммы, СтруктураПараметров) Экспорт
	// проверим корректность заполнения
	Если НЕ отКорректностьЗаполнения(ОтчетОбъект) Тогда
		Возврат ПолеДиаграммы;
	КонецЕсли;
	
	#Если Клиент Тогда
		Состояние("Подготовка к построению ...");
	#КонецЕсли
	
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	//распакуем параметры диаграммы
	ВыбСерия         = СтруктураПараметров.Серии;
	ВыбТочка         = СтруктураПараметров.Точки; 
	ВыбПоказатель    = СтруктураПараметров.Показатель;
	ВыбФункция       = СтруктураПараметров.Функция;
	КоличествоСерий  = СтруктураПараметров.МаксСерий;
	КоличествоТочек  = СтруктураПараметров.МаксТочек;
	флСводнаяСерия   = СтруктураПараметров.СводнаяСерия;
	флСводнаяТочка   = СтруктураПараметров.СводнаяТочка;
	СписокГруппСерий = СтруктураПараметров.ГруппыСерий;
	СписокГруппТочек = СтруктураПараметров.ГруппыТочек;
	флФильтроватьСерии = (СписокГруппСерий.Количество() > 0);
	флФильтроватьТочки = (СписокГруппТочек.Количество() > 0);
	
	ТекПараметрыСерии = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ВыбСерия, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ФорматСерии = ?(ТекПараметрыСерии=Неопределено ИЛИ Не ТекПараметрыСерии.Свойство("Формат"),"",ТекПараметрыСерии.Формат);
	
	ТекПараметрыТочки = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ВыбТочка, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ФорматТочки = ?(ТекПараметрыТочки=Неопределено ИЛИ Не ТекПараметрыТочки.Свойство("Формат"),"",ТекПараметрыТочки.Формат);
	
	//выводимые измерения
	ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки;
	ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки;
	
	//сохраняемая на этапе вывода диаграммы структура параметров отчета
	ТекущееСостояние = Новый Структура("ИзмеренияСтроки, ИзмеренияКолонки, Показатели, Функции");
	ТекущееСостояние.ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки.Выгрузить();
	ТекущееСостояние.ИзмеренияКолонки = ОтчетОбъект.ИзмеренияКолонки.Выгрузить();
	ТекущееСостояние.Показатели = ОтчетОбъект.Показатели.Выгрузить();
	ТекущееСостояние.Функции = ОтчетОбъект.Функции.Выгрузить();
	
	//ТекущееСостояние.Макет = ПостроительОтчета.Макет;
	
	//выбранные серии и точки должны соответствовать измерениям построителя (серия на 1-м месте, точка - на 2-м)
	НайдСерия = ИзмеренияСтроки.Найти(ВыбСерия);
	Если НайдСерия = Неопределено Тогда //не нашли в строках, ищем в колонках
		НайдСерия = ИзмеренияКолонки.Найти(ВыбСерия);
	КонецЕсли;
	НайдСерия_ПутьКДанным   = НайдСерия.ПутьКДанным;
	НайдСерия_Представление = НайдСерия.Представление;
	НайдСерия_ТипИзмерения  = НайдСерия.ТипИзмерения;
	
	Если ВыбТочка <> Неопределено Тогда
		НайдТочка = ИзмеренияСтроки.Найти(ВыбТочка);
		Если НайдТочка = Неопределено Тогда //не нашли в строках, ищем в колонках
			НайдТочка = ИзмеренияКолонки.Найти(ВыбТочка);
		КонецЕсли;
		НайдТочка_ПутьКДанным   = НайдТочка.ПутьКДанным;
		НайдТочка_Представление = НайдТочка.Представление;
		НайдТочка_ТипИзмерения  = НайдТочка.ТипИзмерения;
	КонецЕсли;
	
	ИзмеренияСтроки.Очистить();
	ИзмеренияКолонки.Очистить();
	НовоеИзмерение = ИзмеренияСтроки.Добавить();
	НовоеИзмерение.Использование = Истина;
	НовоеИзмерение.ПутьКДанным   = НайдСерия_ПутьКДанным;
	НовоеИзмерение.Представление = НайдСерия_Представление;
	НовоеИзмерение.ТипИзмерения  = ?(НайдСерия_ТипИзмерения = "Иерархия", "Только иерархия", НайдСерия_ТипИзмерения);
	
	//запоминаем имя, тип измерения серии
	Измерение1Имя = СтрЗаменить(НовоеИзмерение.ПутьКДанным, ".", "");
	Измерение1ТипИзмерения = НовоеИзмерение.ТипИзмерения;
	Если ВыбТочка <> Неопределено Тогда
		НовоеИзмерение = ИзмеренияКолонки.Добавить();
		НовоеИзмерение.Использование = Истина;
		НовоеИзмерение.ПутьКДанным   = НайдТочка_ПутьКДанным;
		НовоеИзмерение.Представление = НайдТочка_Представление;
		НовоеИзмерение.ТипИзмерения  = ?(НайдТочка_ТипИзмерения = "Иерархия", "Только иерархия", НайдТочка_ТипИзмерения);;
		//запоминаем имя, тип измерения точки
		Измерение2Имя = СтрЗаменить(НовоеИзмерение.ПутьКДанным, ".", "");
		Измерение2ТипИзмерения = НовоеИзмерение.ТипИзмерения;
	КонецЕсли;
	
	//выбранные показатель/функция должны соответствовать построителю (должны быть на 1 месте)
	Найд = ОтчетОбъект.Функции.Найти(ВыбФункция);
	Найд.Использование = Истина;
	ОтчетОбъект.Функции.Сдвинуть(Найд, -ОтчетОбъект.Функции.Индекс(Найд));
	Найд = ОтчетОбъект.Показатели.Найти(ВыбПоказатель);
	Найд.Использование = Истина;
	ОтчетОбъект.Показатели.Сдвинуть(Найд, -ОтчетОбъект.Показатели.Индекс(Найд));
	
	//измерения в основном типа справочник, но может быть, например, и строка, и документ, и ПВХ...
	Попытка
		Измерение1ТипСсылки = ПостроительОтчета.ДоступныеПоля.Найти(Измерение1Имя).ТипЗначения.Типы()[0];
		Измерение1_ЭтоСправочник = Справочники.ТипВсеСсылки().СодержитТип(Измерение1ТипСсылки);
	Исключение
		Измерение1_ЭтоСправочник = Ложь;
	КонецПопытки;
	Если Измерение1_ЭтоСправочник Тогда
		Измерение1_НетГрупп = (Метаданные.НайтиПоТипу(Измерение1ТипСсылки).ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов);
	Иначе
		Измерение1_НетГрупп = Истина;
	КонецЕсли;
	// если физически групп не может быть, скорректируем тип измерения
	Если Измерение1_НетГрупп Тогда
		ИзмеренияСтроки[0].ТипИзмерения = "Элементы";
	КонецЕсли;
	Если ВыбТочка <> Неопределено Тогда
		Попытка
			Измерение2ТипСсылки = ПостроительОтчета.ДоступныеПоля.Найти(Измерение2Имя).ТипЗначения.Типы()[0];
			Измерение2_ЭтоСправочник = Справочники.ТипВсеСсылки().СодержитТип(Измерение2ТипСсылки);
		Исключение
			Измерение2_ЭтоСправочник = Ложь;
		КонецПопытки;
		Если Измерение2_ЭтоСправочник Тогда
			Измерение2_НетГрупп = (Метаданные.НайтиПоТипу(Измерение2ТипСсылки).ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов);
		Иначе
			Измерение2_НетГрупп = Истина;
		КонецЕсли;
		// если физически групп не может быть, скорректируем тип измерения
		Если Измерение2_НетГрупп Тогда
			ИзмеренияКолонки[0].ТипИзмерения = "Элементы";
		КонецЕсли;
	КонецЕсли;
	
	Попытка 
		ПостроительОтчета.ВыводитьДетальныеЗаписи = ОтчетОбъект.ВыводитьДетальныеЗаписи;
	Исключение
		ПостроительОтчета.ВыводитьДетальныеЗаписи = Ложь;
	КонецПопытки;
	
	//подкорректируем текст запроса построителя - уберем неиспользуемые показатели и функции
	Если Не ПостроительОтчета.ВыводитьДетальныеЗаписи Тогда
		отУбратьНеИспользуемыеПоказателиИзЗапроса(ОтчетОбъект);
	КонецЕсли;
	
	// установим период построителя запроса по виду отчета
	отУстановитьПериодОтчета(ОтчетОбъект); 
	
	отУстановитьИспользованиеИзмерений(ОтчетОбъект);
	
	// получим поле диаграммы
	Если ТипЗнч(ПолеДиаграммы) <> Тип("Диаграмма") Тогда
		ПолеДиаграммы = Новый Диаграмма();
	Иначе
		ПолеДиаграммы.Очистить();	
	КонецЕсли;
	
	//получим функцию и показатель для диаграммы
	Функ = "КонечныйОстаток";
	Показатель = "";
	НайденнаяСтрока = ОтчетОбъект.Функции.Найти(Истина, "Использование");
	Если НайденнаяСтрока <> Неопределено Тогда
		Функ = НайденнаяСтрока.Имя; 
	КонецЕсли;
	НайденнаяСтрока = ОтчетОбъект.Показатели.Найти(Истина, "Использование");
	
	Если НайденнаяСтрока <> Неопределено Тогда
		Показатель = НайденнаяСтрока.Имя; 
	КонецЕсли;
	ПоказательФункция = СокрЛП(Показатель + Функ);
	
	// установим корректный период отчета
	Попытка
		ОбъектДатаНач = ОтчетОбъект.ДатаНачала;
		ОбъектДатаКон = ОтчетОбъект.ДатаКонца;
	Исключение
	КонецПопытки; 
	
	#Если Клиент Тогда
		Состояние("Получение данных ...");
	#КонецЕсли
	
	ПостроительОтчета.Выполнить();
	
	#Если Клиент Тогда
		Состояние("Вывод диаграммы ...");
	#КонецЕсли
	
	//формируем макет для вывода данных в промежуточный ТабДок
	МакетДанных = Новый ТабличныйДокумент;
	Если ВыбТочка = Неопределено Тогда
		МакетДанных.Область(1, 0, 1, 0).Имя = "ШапкаСтрок";
		МакетДанных.Область(2,  , 2,  ).Имя = Измерение1Имя;
		МакетДанных.Область(2, 1, 2, 1).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(2, 1, 2, 1).Параметр = Измерение1Имя;
		МакетДанных.Область(2, 1, 2, 1).ПараметрРасшифровки = Измерение1Имя;
		МакетДанных.Область(2, 2, 2, 2).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(2, 2, 2, 2).Параметр = ПоказательФункция;
		МакетДанных.Область(2, 2, 2, 2).ПараметрРасшифровки = ПоказательФункция;
		
	Иначе
		МакетДанных.Область(1, 1, 1, 1).Имя = "ШапкаСтрок";
		МакетДанных.Область(2, 1, 2, 1).Имя = Измерение1Имя;
		МакетДанных.Область(2, 2, 2, 2).Имя = Измерение1Имя + "Ресурсы";
		МакетДанных.Область(1, 2, 1, 2).Имя = Измерение2Имя;
		МакетДанных.Область(1, 2, 1, 2).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(1, 2, 1, 2).Параметр = Измерение2Имя;
		МакетДанных.Область(1, 2, 1, 2).ПараметрРасшифровки = Измерение2Имя;
		МакетДанных.Область(2, 1, 2, 1).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(2, 1, 2, 1).Параметр = Измерение1Имя;
		МакетДанных.Область(2, 1, 2, 1).ПараметрРасшифровки = Измерение1Имя;
		МакетДанных.Область(2, 2, 2, 2).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		МакетДанных.Область(2, 2, 2, 2).Параметр = ПоказательФункция;
		МакетДанных.Область(2, 2, 2, 2).ПараметрРасшифровки = ПоказательФункция;
	КонецЕсли;
	
	//переопределяем макет и выводим результат в промежуточный ТабДок
	ПостроительОтчета.Макет = МакетДанных;
	ТабДок = Новый ТабличныйДокумент;
	ПостроительОтчета.Вывести(ТабДок);
	
	// вывод диаграммы
	ПолеДиаграммы.Обновление = Ложь;
	
	//получаем массив цветовой палитры для диаграммы
	ЦветаСерий = ПолучитьОбщийМакет("ПечатьДиаграммы").ПолучитьОбласть("ЦветаСерий");
	МассивЦветовСерий = Новый Массив;
	Для Сч = 1 По ЦветаСерий.ВысотаТаблицы Цикл
		МассивЦветовСерий.Добавить(ЦветаСерий.Область(Сч, 2).ЦветФона);
	КонецЦикла;
	КоличЦветов = МассивЦветовСерий.Количество();
	
	//структура ТЗ диаграммы
	ЗначенияДиаграммы = Новый ТаблицаЗначений;
	ЗначенияДиаграммы.Колонки.Добавить("Серия");
	ЗначенияДиаграммы.Колонки.Добавить("Точка");
	ЗначенияДиаграммы.Колонки.Добавить("Значение");
	ЗначенияДиаграммы.Колонки.Добавить("Подсказка");
	ЗначенияДиаграммы.Колонки.Добавить("Расшифровка");
	//ЗначенияДиаграммы.Колонки.Добавить("РасшифровкаСерии");
	//ЗначенияДиаграммы.Колонки.Добавить("РасшифровкаТочки");
	
	//перекидываем значения из табличного документа в ТЗ диаграммы
	Для ИндС = 1 По ТабДок.ВысотаТаблицы Цикл
		Знач1 = ТабДок.Область(ИндС, 1, ИндС, 1).Расшифровка; //серия
		//пропускаем пустые серии
		Если обЗначениеНеЗаполнено(Знач1) Тогда Продолжить; КонецЕсли;
		
		//пропускаем группы, не входящие в список значений серий
		Если флФильтроватьСерии И СписокГруппСерий.НайтиПоЗначению(Знач1) = Неопределено Тогда Продолжить; КонецЕсли;
		
		Если ВыбТочка = Неопределено Тогда //только серии
			Знач3 = ТабДок.Область(ИндС, 2, ИндС, 2).Расшифровка; //значение
			//пропускаем пустые значения
			Если ТипЗнч(Знач3) <> Тип("Число") ИЛИ Знач3 = 0 Тогда Продолжить; КонецЕсли;
			
			НовоеЗначение = ЗначенияДиаграммы.Добавить();
			НовоеЗначение.Серия = Знач1;
			НовоеЗначение.Значение = Знач3;
			НовоеЗначение.Расшифровка = Новый Структура(Измерение1Имя, Знач1);
			
		Иначе //и серии и точки
			Для ИндК = 2 По ТабДок.ШиринаТаблицы Цикл
				Знач2 = ТабДок.Область(1, ИндК, 1, ИндК).Расшифровка; //точка
				//пропускаем пустые точки
				Если обЗначениеНеЗаполнено(Знач2) Тогда Продолжить; КонецЕсли;
				
				Знач3 = ТабДок.Область(ИндС, ИндК, ИндС, ИндК).Расшифровка; //значение
				//пропускаем пустые значения				
				Если ТипЗнч(Знач3) <> Тип("Число") ИЛИ Знач3 = 0 Тогда Продолжить; КонецЕсли;
				
				//пропускаем группы, не входящие в список значений серий
				Если флФильтроватьСерии И СписокГруппСерий.НайтиПоЗначению(Знач1) = Неопределено Тогда Продолжить; КонецЕсли;
				
				//пропускаем группы, не входящие в список значений точек
				Если флФильтроватьТочки И СписокГруппТочек.НайтиПоЗначению(Знач2) = Неопределено Тогда Продолжить; КонецЕсли;
				
				НовоеЗначение = ЗначенияДиаграммы.Добавить();
				НовоеЗначение.Серия    = Знач1;
				НовоеЗначение.Точка    = Знач2;
				НовоеЗначение.Значение = Знач3;
				НовоеЗначение.Расшифровка      = Новый Структура(Измерение1Имя + "," + Измерение2Имя, Знач1, Знач2);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	//введем списки серий и точек, и отсортируем их
	тблТочки = ЗначенияДиаграммы.Скопировать();
	тблТочки.Свернуть("Точка", "Значение");
	тблТочки.Сортировать("Точка ВОЗР");
	тблСерии = ЗначенияДиаграммы.Скопировать();
	тблСерии.Свернуть("Серия", "Значение");
	тблСерии.Сортировать("Серия ВОЗР");
	
	//списки серий и точек, входящих в сводные
	тблСводныеСерии = Новый ТаблицаЗначений;
	тблСводныеСерии.Колонки.Добавить("Серия");
	тблСводныеСерии.Колонки.Добавить("Значение");
	
	тблСводныеТочки = Новый ТаблицаЗначений;
	тблСводныеТочки.Колонки.Добавить("Точка");
	тблСводныеТочки.Колонки.Добавить("Значение");
	
	//посмотрим, если нужно - отрежем лишние и добавим сводные
	Если тблСерии.Количество() > КоличествоСерий Тогда
		Инд = тблСерии.Количество()-1;
		Пока Инд >= КоличествоСерий Цикл
			//добавляем в сводные серии
			Если флСводнаяСерия Тогда
				Новая = тблСводныеСерии.Добавить();
				Новая.Серия = тблСерии[Инд].Серия;
				
				Новая.Значение = тблСерии[Инд].Значение;
			КонецЕсли;
			//удаляем из несводных серий
			тблСерии.Удалить(Инд);
			Инд = Инд - 1;
		КонецЦикла;
		Если флСводнаяСерия Тогда
			СводнаяСерия = тблСерии.Добавить();
			СводнаяСерия.Серия = "прочие...";
		КонецЕсли;	
	КонецЕсли;
	Если тблТочки.Количество() > КоличествоТочек Тогда
		Инд = тблТочки.Количество()-1;
		Пока Инд >= КоличествоТочек Цикл
			//добавляем в сводные точки
			Если флСводнаяТочка Тогда
				Новая = тблСводныеТочки.Добавить();
				Новая.Точка = тблТочки[Инд].Точка;
				Новая.Значение = тблТочки[Инд].Значение;
			КонецЕсли;
			//удаляем из несводных точек
			тблТочки.Удалить(Инд);
			Инд = Инд - 1;
		КонецЦикла;
		Если флСводнаяТочка Тогда
			СводнаяТочка = тблТочки.Добавить();
			СводнаяТочка.Точка = "прочие...";
		КонецЕсли;
	КонецЕсли;
	
	//РУЧНОЕ ФОРМИРОВАНИЕ ДИАГРАММЫ - НАЧАЛО
	// добавляем точки
	Для каждого ТекТочка Из тблТочки Цикл
		//добавляем новую точку в диаграмму
		ПолеДиаграммы.Точки.Добавить(Формат(ТекТочка.Точка, ФорматТочки));
	КонецЦикла;
	//добавляем серии, а точки берем из сформированного списка
	ИндСерия = 0;
	Для каждого ТекСерия Из тблСерии Цикл
		//добавляем новую серию в диаграмму
		НоваяСерия = ПолеДиаграммы.Серии.Добавить(Формат(ТекСерия.Серия, ФорматСерии));
		НоваяСерия.Цвет = МассивЦветовСерий[ИндСерия - Цел((ИндСерия)/КоличЦветов)*КоличЦветов];
		НоваяСерия.Расшифровка = Новый Структура(Измерение1Имя, ТекСерия.Серия);
		
		ИндТочка = 0;
		Для каждого НоваяТочка Из ПолеДиаграммы.Точки Цикл
			ТекТочка = тблТочки[ИндТочка];
			НоваяТочка.Расшифровка = Новый Структура(Измерение2Имя, ТекТочка.Точка);
			
			//очистим параметры значения диаграммы
			Значение = 0;
			Расшифровка = Неопределено;
			
			//case на вид текущей точки/серии
			Если ИндТочка = КоличествоТочек И флСводнаяТочка Тогда
				Если ИндСерия = КоличествоСерий И флСводнаяСерия Тогда
					//самая "сводная" точко-серия
					Для каждого ТекСводнаяСерия Из тблСводныеСерии Цикл
						Для каждого ТекСводнаяТочка Из тблСводныеТочки Цикл
							ТекЗначение = ЗначенияДиаграммы.НайтиСтроки(Новый Структура("Точка, Серия", ТекСводнаяТочка.Точка, ТекСводнаяСерия.Серия));
							Если НЕ (ТекЗначение.Количество() = 0 ИЛИ ТекЗначение[0].Значение = 0) Тогда
								Значение = Значение + ТекЗначение[0].Значение;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				Иначе
					//сводная точка и несводная серия
					Для каждого ТекСводнаяТочка Из тблСводныеТочки Цикл
						ТекЗначение = ЗначенияДиаграммы.НайтиСтроки(Новый Структура("Точка, Серия", ТекСводнаяТочка.Точка, ТекСерия.Серия));
						Если НЕ (ТекЗначение.Количество() = 0 ИЛИ ТекЗначение[0].Значение = 0) Тогда
							Значение = Значение + ТекЗначение[0].Значение;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Иначе
				Если ИндСерия = КоличествоСерий И флСводнаяСерия Тогда
					//сводная серия и несводная точка
					Для каждого ТекСводнаяСерия Из тблСводныеСерии Цикл
						ТекЗначение = ЗначенияДиаграммы.НайтиСтроки(Новый Структура("Точка, Серия", ТекТочка.Точка, ТекСводнаяСерия.Серия));
						Если НЕ (ТекЗначение.Количество() = 0 ИЛИ ТекЗначение[0].Значение = 0) Тогда
							Значение = Значение + ТекЗначение[0].Значение;
						КонецЕсли;
					КонецЦикла;
				Иначе
					//обычная точко-серия
					ТекЗначение = ЗначенияДиаграммы.НайтиСтроки(Новый Структура("Точка, Серия", ТекТочка.Точка, ТекСерия.Серия));
					Если НЕ (ТекЗначение.Количество() = 0 ИЛИ ТекЗначение[0].Значение = 0) Тогда
						Значение = ТекЗначение[0].Значение;
						Расшифровка = ТекЗначение[0].Расшифровка;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			//устанавливаем числовое значение в [ТекТочка, ТекСерия], а заодно и расшифровку
			ПолеДиаграммы.УстановитьЗначение(НоваяТочка, НоваяСерия, Значение, Расшифровка);
			
			ИндТочка = ИндТочка + 1;
		КонецЦикла;
		
		ИндСерия = ИндСерия + 1;
	КонецЦикла;
	//РУЧНОЕ ФОРМИРОВАНИЕ ДИАГРАММЫ - КОНЕЦ
	
	#Если Клиент Тогда
		Состояние();
	#КонецЕсли
	
	ПолеДиаграммы.Обновление = Истина;
	
	//восстановим параметры отчета
	ОтчетОбъект.ИзмеренияСтроки.Загрузить(ТекущееСостояние.ИзмеренияСтроки);
	ОтчетОбъект.ИзмеренияКолонки.Загрузить(ТекущееСостояние.ИзмеренияКолонки);
	ОтчетОбъект.Функции.Загрузить(ТекущееСостояние.Функции);
	ОтчетОбъект.Показатели.Загрузить(ТекущееСостояние.Показатели);
	
	////5) макет построителя
	//ПостроительОтчета.Макет = ТекущееСостояние.Макет;
	
	Возврат ПолеДиаграммы;
	
КонецФункции //	отСформироватьДиаграмму()

// Функция формирует отчет в виде сводной диаграммы
//
// Параметры:
//	ОтчетОбъект          - ОтчетОбъект - Объект отчета
//	ПолеСводнойДиаграммы - Диаграмма	  - Диаграмма
//	ПараметрФормирования - Число	  - Параметр формирования	
//
// Возвращаемое значение:
//	Диаграмма - Диаграмма
//
Функция отСформироватьСводнуюДиаграмму(ОтчетОбъект, ПолеСводнойДиаграммы=Неопределено, ПараметрФормирования=0) Экспорт
	
	ПостроительОтчета = Новый ПостроительОтчета;
	
	Если ТипЗнч(ПолеСводнойДиаграммы) <> Тип("СводнаяДиаграмма") Тогда
		ПолеСводнойДиаграммы = Новый СводнаяДиаграмма();
	КонецЕсли;
	
	Если Не ПараметрФормирования Тогда
		Если Не ПодготовкаПостроителяОтчета(ОтчетОбъект, ПостроительОтчета) Тогда
			Возврат ПолеСводнойДиаграммы;
		КонецЕсли;
	Иначе
		Если Не ПодготовкаПостроителяСИсточникомДанных(ОтчетОбъект, ПостроительОтчета, ПараметрФормирования = 2) Тогда
			Возврат ПолеСводнойДиаграммы;
		КонецЕсли;
	КонецЕсли;
	
	ПостроительОтчета.Порядок.Очистить();
	Попытка
		ТепПараметрыИспользованияПолей = ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей;
	Исключение
		ТепПараметрыИспользованияПолей = Неопределено;
	КонецПопытки;
	
	Попытка
		ТепСтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение
		ТепСтруктураСвязиСвойств = Неопределено;
	КонецПопытки;
	
	СтруктураРесурсов = Новый Структура;
	Попытка
		ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей;
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			Если Не ТекГруппа.Группа Тогда
				СтруктураРесурсов.Вставить(ТекГруппа.ПутьКДанным, ТекГруппа.Представление);
			КонецЕсли;
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				СтруктураРесурсов.Вставить(ТекСтрока.ПутьКДанным, ТекСтрока.Представление + " " + ТекСтрока.Представление);
			КонецЦикла;
		КонецЦикла;
	Исключение
		Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
			Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
			Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
				Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				ИмяРесурса = ТекПоказатель.Имя + ТекФункция.Имя;
				СтруктураРесурсов.Вставить(ИмяРесурса, ТекПоказатель.Представление + " " + ТекФункция.Представление);
			КонецЦикла;
		КонецЦикла;
	КонецПопытки;
	
	Если ТипЗнч(ТепСтруктураСвязиСвойств)=Тип("Структура") И ТипЗнч(ТепПараметрыИспользованияПолей)=Тип("Структура") Тогда
		Для Каждого ТекПоле Из ПостроительОтчета.ДоступныеПоля Цикл
			Если ТепПараметрыИспользованияПолей.Свойство(ТекПоле.Имя) Тогда
				ТекПоле.Представление = ТепПараметрыИспользованияПолей[ТекПоле.Имя].Представление;
			ИначеЕсли ТепСтруктураСвязиСвойств.Свойство(ТекПоле.Имя) Тогда
				ТекПоле.Представление = ТепСтруктураСвязиСвойств[ТекПоле.Имя].Представление+" (" +(ТепСтруктураСвязиСвойств[ТекПоле.Имя].ИмяИзмерения)+")";
			ИначеЕсли СтруктураРесурсов.Свойство(ТекПоле.Имя) Тогда
				ТекПоле.Представление = СтруктураРесурсов[ТекПоле.Имя];	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Вывод данных
	ПолеСводнойДиаграммы.Градиент = Истина;
	ПолеСводнойДиаграммы.Свет     = Истина;
	ПолеСводнойДиаграммы.ГоризонтальнаяПоддержкаМасштаба = ПоддержкаМасштабаСводнойДиаграммы.КоличествоЗначений;
	ПолеСводнойДиаграммы.КоличествоЗначенийГоризонтальнойШкалы = 10;
	ПолеСводнойДиаграммы.ОтображениеЗначенийТочек = ОтображениеЗначенийСводнойДиаграммы.ЗначенияПоследнегоУровня;
	ПолеСводнойДиаграммы.ОтображениеЗначенийСерий = ОтображениеЗначенийСводнойДиаграммы.ЗначенияПоследнегоУровня;
	ПолеСводнойДиаграммы.ИсточникДанных = ПостроительОтчета;
	ПолеСводнойДиаграммы.ОбластьПостроения.ОриентацияМетокГоризонтальнойШкалы = ОриентацияМетокСводнойДиаграммы.ВсеУровниГоризонтально;
	
	ПолеСводнойДиаграммы.Точки.Очистить();
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияСтроки Цикл
		Если ПолеСводнойДиаграммы.Поля.Найти(ТекИзмерение.ПутьКДанным) = Неопределено тогда продолжить; КонецЕсли;
		ПолеСводнойДиаграммы.Точки.Добавить(ПолеСводнойДиаграммы.Поля[ТекИзмерение.ПутьКДанным]);
	КонецЦикла;
	
	ПолеСводнойДиаграммы.Серии.Очистить();
	Для Каждого ТекИзмерение Из ПостроительОтчета.ИзмеренияКолонки Цикл
		Если ПолеСводнойДиаграммы.Поля.Найти(ТекИзмерение.ПутьКДанным) = Неопределено тогда продолжить; КонецЕсли;
		ПолеСводнойДиаграммы.Серии.Добавить(ПолеСводнойДиаграммы.Поля[ТекИзмерение.ПутьКДанным]);
	КонецЦикла;
	
	ПолеСводнойДиаграммы.Ресурсы.Очистить();
	Для Каждого ТекРесурс Из СтруктураРесурсов Цикл
		Если ПолеСводнойДиаграммы.Поля.Найти(ТекРесурс.Ключ) = Неопределено Тогда Продолжить; КонецЕсли;
		ПолеСводнойДиаграммы.Ресурсы.Добавить(ТекРесурс.Ключ);
	КонецЦикла;
	
	Возврат ПолеСводнойДиаграммы;
	
КонецФункции	//	отСформироватьСводнуюДиаграмму()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ РЕКВИЗИТОВ ФОРМ ОТЧЕТОВ

// Процедура выбора периода
//
// Параметры:
//  ЭтаФорма – Форма             – Ссылка на форму документа.
//	Элемент  - ЭлементУправления - Элемент управления формы
//
Процедура отВыборПериодаНажатие(ЭтаФорма, Элемент) Экспорт
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(ЭтаФорма.ДатаНачала, ?(ЭтаФорма.ДатаКонца='0001-01-01', ЭтаФорма.ДатаКонца, КонецДня(ЭтаФорма.ДатаКонца)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	НастройкаПериода.Редактировать();
	ЭтаФорма.ДатаНачала = НастройкаПериода.ПолучитьДатуНачала();
	ЭтаФорма.ДатаКонца = НастройкаПериода.ПолучитьДатуОкончания();
КонецПроцедуры // отСформироватьСводнуюДиаграмму()

// Процедура смещения периода в форме настройки отчета
//
// Параметры:
//	ОтчетОбъект - ОтчетОбъект - Объект отчета
//	Смещение    - Число       - Смещение
//
Процедура ОтСмещениеПериодаФормыНастройкиОтчета(ОтчетОбъект, Смещение) Экспорт
	ОтчетОбъект.ДатаНачала = НачалоМесяца(ДобавитьМесяц(ОтчетОбъект.ДатаНачала,Смещение));
	ОтчетОбъект.ДатаКонца = КонецМесяца(ОтчетОбъект.ДатаНачала);
КонецПроцедуры // ОтСмещениеПериодаФормыНастройкиОтчета()

// Процедура измерения строк перед окончанием редактирования
//
// Параметры:
//  ЭтаФорма             – Форма             – Ссылка на форму документа.
//	Элемент              - ЭлементУправления - Элемент управления формы
//	НоваяСтрока          - Булево            - Признак редактирования новой строки
//	ОтменаРедактирования - Булево			 - Признак отмены редактирования	
// 	Отказ                - Булево			 - Признак отказа от записи объекта
//
Процедура отИзмеренияСтрокПередОкончаниемРедактирования(ЭтаФорма, Элемент, НоваяСтрока, ОтменаРедактирования, Отказ) Экспорт
	ПостроительОтчета = ЭтаФорма.ПостроительОтчета;
	
	// Если есть такое измерение колонки - удалим его
	Измерение = ПостроительОтчета.ИзмеренияКолонки.Найти(Элемент.ТекущиеДанные.ПутьКДанным);
	Если Измерение <> Неопределено Тогда
		ПостроительОтчета.ИзмеренияКолонки.Удалить(Измерение);
	КонецЕсли;
	
	// Проверим дополнительные поля...
	ПроверкаДопПолей(ПостроительОтчета, Истина);
КонецПроцедуры // отИзмеренияСтрокПередОкончаниемРедактирования()

// Процедура измерения колонок перед окончанием редактирования
//
// Параметры:
//  ЭтаФорма             – Форма             – Ссылка на форму документа.
//	Элемент              - ЭлементУправления - Элемент управления формы
//	НоваяСтрока          - Булево            - Признак редактирования новой строки
//	ОтменаРедактирования - Булево			 - Признак отмены редактирования	
// 	Отказ                - Булево			 - Признак отказа от записи объекта
//
Процедура отИзмеренияКолонокПередОкончаниемРедактирования(ЭтаФорма, Элемент, НоваяСтрока, ОтменаРедактирования, Отказ) Экспорт
	
	Измерение = ЭтаФорма.ПостроительОтчета.ИзмеренияСтроки.Найти(Элемент.ТекущиеДанные.ПутьКДанным);
	Если Измерение <> Неопределено Тогда
		ЭтаФорма.ПостроительОтчета.ИзмеренияСтроки.Удалить(Измерение);
	КонецЕсли;
	
КонецПроцедуры	//	отИзмеренияКолонокПередОкончаниемРедактирования()

// Процедура обработчик вывода строки измерений строк и колонок построителя отчета
//
// Параметры:
//	ОтчетОбъект      - ОтчетОбъект                     - Объект отчета
//  ОформлениеСтроки - ОбластьЯчеекТабличногоДокумента - Оформление строки
//	ДанныеСтроки	 - ТекущиеДанные					  - Данные строки
//
Процедура отИзмеренияПриВыводеСтроки(ОтчетОбъект, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки = Неопределено Тогда Возврат; КонецЕсли;
	Если ПустаяСтрока(ДанныеСтроки.ПутьКДанным) Тогда Возврат; КонецЕсли;
	
	ОформлениеСтроки.ЦветТекста = ?(ДанныеСтроки.Использование, Новый Цвет(), Новый Цвет(128, 128, 128));
	
	Если ОформлениеСтроки.Ячейки.Найти("Сортировка") <> Неопределено Тогда
		ИдентификаторПути = СтрЗаменить(ДанныеСтроки.ПутьКДанным, ".", "_");
		Если ОтчетОбъект.ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
			ПараметрыИзмерения = ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути];
			
			ОформлениеЯчейки = ОформлениеСтроки.Ячейки;
			Если ПараметрыИзмерения.Свойство("ПорядокПредставление") Тогда
				ОформлениеЯчейки.Сортировка.ОтображатьТекст = Истина;
				ОформлениеЯчейки.Сортировка.УстановитьТекст(ПараметрыИзмерения["ПорядокПредставление"]);
			КонецЕсли;
			Если ПараметрыИзмерения.Свойство("ПоляПредставление") Тогда
				ОформлениеЯчейки.ДополнительныеПоля.УстановитьТекст(ПараметрыИзмерения["ПоляПредставление"]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// отключим редактирование ячеек для фиксированных измерений
	Попытка // Признак фиксированного измерения есть не во всех отчетах
		Если ДанныеСтроки.Фиксированное Тогда
			//ОформлениеСтроки.ЦветФона = Новый Цвет(234, 229, 216);
			ОформлениеСтроки.ЦветТекста = Новый Цвет(128, 128, 128);
			
			ОформлениеСтроки.Ячейки.Использование.Флажок = 2;
			ОформлениеСтроки.Ячейки.Сортировка.ГиперСсылка = Ложь;
			
			//ОформлениеСтроки.Ячейки.ДополнительныеПоля.ЦветФона = Новый Цвет();
			ОформлениеСтроки.Ячейки.ДополнительныеПоля.ЦветТекста = Новый Цвет();
			
			ОформлениеСтроки.Ячейки.Использование.ТолькоПросмотр = Истина;
			ОформлениеСтроки.Ячейки.Представление.ТолькоПросмотр = Истина;
			ОформлениеСтроки.Ячейки.ТипИзмерения.ТолькоПросмотр  = Истина;
			ОформлениеСтроки.Ячейки.Сортировка.ТолькоПросмотр    = Истина;
			
			ДанныеСтроки.Использование = Истина;
		КонецЕсли;
	Исключение КонецПопытки;
	
КонецПроцедуры	//	отИзмеренияПриВыводеСтроки()

// Процедура обработчик вывода строк измерений для строк и колонок построителя отчета
//
// Параметры:
//	ОтчетОбъект      - ОтчетОбъект                     - Объект отчета
//  ОформленияСтрок  - ОбластьЯчеекТабличногоДокумента - Оформление строки
//
Процедура отИзмеренияПриПолученииДанных(ОтчетОбъект, ОформленияСтрок) Экспорт
	
	// отключим редактирование ячеек для фиксированных измерений
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		
		Если ДанныеСтроки = Неопределено Тогда Возврат; КонецЕсли;
		Если ПустаяСтрока(ДанныеСтроки.ПутьКДанным) Тогда Возврат; КонецЕсли;
		
		ОформлениеСтроки.ЦветТекста = ?(ДанныеСтроки.Использование, Новый Цвет(), Новый Цвет(128, 128, 128));
		
		Если ОформлениеСтроки.Ячейки.Найти("Сортировка") <> Неопределено Тогда
			ИдентификаторПути = СтрЗаменить(ДанныеСтроки.ПутьКДанным, ".", "_");
			Если ОтчетОбъект.ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) и ОтчетОбъект.РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
				ПараметрыИзмерения = ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути];
				
				ОформлениеЯчейки = ОформлениеСтроки.Ячейки;
				Если ПараметрыИзмерения.Свойство("ПорядокПредставление") Тогда
					ОформлениеЯчейки.Сортировка.ОтображатьТекст = Истина;
					ОформлениеЯчейки.Сортировка.УстановитьТекст(ПараметрыИзмерения["ПорядокПредставление"]);
				КонецЕсли;
				Если ПараметрыИзмерения.Свойство("ПоляПредставление") Тогда
					ОформлениеЯчейки.ДополнительныеПоля.УстановитьТекст(ПараметрыИзмерения["ПоляПредставление"]);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Попытка // Признак фиксированного измерения есть не во всех отчетах
			Если ДанныеСтроки.Фиксированное Тогда
				ОформлениеСтроки.ЦветТекста = Новый Цвет(128, 128, 128);
				
				ОформлениеСтроки.Ячейки.Использование.Флажок = 2;
				ОформлениеСтроки.Ячейки.Сортировка.ГиперСсылка = Ложь;
				
				ОформлениеСтроки.Ячейки.ДополнительныеПоля.ЦветФона = Новый Цвет();
				ОформлениеСтроки.Ячейки.ДополнительныеПоля.ЦветТекста = Новый Цвет();
				
				ОформлениеСтроки.Ячейки.Использование.ТолькоПросмотр = Истина;
				ОформлениеСтроки.Ячейки.Представление.ТолькоПросмотр = Истина;
				ОформлениеСтроки.Ячейки.ТипИзмерения.ТолькоПросмотр  = Истина;
				ОформлениеСтроки.Ячейки.Сортировка.ТолькоПросмотр    = Истина;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// обработчик вывода строки показателей построителя отчета
//
// Параметры:
//	ОтчетОбъект      - ОтчетОбъект                     - Объект отчета
//  ОформлениеСтроки - ОбластьЯчеекТабличногоДокумента - Оформление строки
//	ДанныеСтроки	 - ТекущиеДанные					  - Данные строки
//
Процедура отПоказателиПриВыводеСтроки(ОтчетОбъект, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки <> Неопределено Тогда
		ОформлениеСтроки.ЦветТекста = ?(ДанныеСтроки.Использование, Новый Цвет(), Новый Цвет(128, 128, 128));
	КонецЕсли;
	
КонецПроцедуры	//	отПоказателиПриВыводеСтроки()

// обработчик вывода строки функций построителя отчета
//
// Параметры:
//	ОтчетОбъект      - ОтчетОбъект                     - Объект отчета
//  ОформлениеСтроки - ОбластьЯчеекТабличногоДокумента - Оформление строки
//	ДанныеСтроки	 - ТекущиеДанные					  - Данные строки
//
Процедура отФункцииПриВыводеСтроки(ОтчетОбъект, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки <> Неопределено Тогда
		ОформлениеСтроки.ЦветТекста = ?(ДанныеСтроки.Использование, Новый Цвет(), Новый Цвет(128, 128, 128));
	КонецЕсли;
	
КонецПроцедуры	//	отФункцииПриВыводеСтроки()

// обработчик вывода строки функций построителя отчета
//
// Параметры:
//	ОтчетОбъект      - ОтчетОбъект                     - Объект отчета
//  ОформлениеСтроки - ОбластьЯчеекТабличногоДокумента - Оформление строки
//	ДанныеСтроки	 - ТекущиеДанные					  - Данные строки
//
Процедура отОтборПриВыводеСтроки(ОтчетОбъект, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки <> Неопределено Тогда
		ОформлениеСтроки.ЦветТекста = ?(ДанныеСтроки.Использование, Новый Цвет(), Новый Цвет(128, 128, 128));
	КонецЕсли;
	
КонецПроцедуры	//	отОтборПриВыводеСтроки()

// Процедура пометки измерений
//
// Параметры:
//  ОтчетФорма – Форма	                    – Ссылка на форму отчета
//	Измерения  - ИзмеренияПостроителяОтчета - Список доступных измерений построителя отчета
//	Кнопка     - Кнопка                     - Кнопка формы
//
Процедура отИзмеренияПометить(ОтчетФорма, Измерения, Кнопка) Экспорт
	
	Использование = (Найти(Кнопка.Имя, "ПометитьВсе"));
	Для Каждого ТекИзмерение Из Измерения Цикл
		//Если строка фиксированная, то флаг нужно оставить
		Попытка // Признак фиксированного измерения есть не во всех отчетах
			ТекИзмерение.Использование = (Использование ИЛИ ТекИзмерение.Фиксированное);
		Исключение
			ТекИзмерение.Использование = Использование;
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры	//	отИзмеренияПометить()

// Процедура обработки измерений перед началом добавления
//
// Параметры:
//  ОтчетФорма  – Форма             – Ссылка на форму отчета
//	Элемент     - ЭлементУправления - Элемент управления формы
// 	Отказ       - Булево			- Признак отказа от записи объекта
// 	Копирование - Булево			- Признак копирования
// 	ИмяПолей    - Строка			- Имя полей
//
Процедура отИзмеренияПередНачаломДобавления(ОтчетФорма, Элемент, Отказ, Копирование, ИмяПолей) Экспорт
	
	Отказ = Истина;
	
	ДеревоВыбора = ОтчетФорма.ДеревоДоступныхПолей.Скопировать();
	СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Измерение", Истина);
	Пока СтрокаПоискаОтбора<>Неопределено Цикл
		ТекРодитель = ?(СтрокаПоискаОтбора.Родитель=Неопределено, ДеревоВыбора, СтрокаПоискаОтбора.Родитель);
		ТекРодитель.Строки.Удалить(СтрокаПоискаОтбора);
		СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Измерение", Истина);
	КонецЦикла;
	
	// для отчетов вида "Движения с остатками" уберем возможность выбора полей, зависящих от регистратора
	Если ОтчетФорма.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		РегистраторВладелец = ДеревоВыбора.Строки.Найти("ПериодРегистратор", "ПутьКДанным");
		Если РегистраторВладелец <> Неопределено Тогда
			к = 0;
			Пока к <= РегистраторВладелец.Строки.Количество() - 1 Цикл
				ТекСтрока = РегистраторВладелец.Строки.Получить(к);
				Если ТекСтрока.ПутьКДанным <> "ПериодРегистратор.Дата" Тогда
					РегистраторВладелец.Строки.Удалить(ТекСтрока);
					
				Иначе к = к + 1;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	
	ФормаВыбора = ПолучитьОбщуюФорму("ВыборИзДерева", ОтчетФорма);
	ФормаВыбора.ДеревоВыбора = ДеревоВыбора;
	ФормаВыбора.Заголовок = "Выбор поля";
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.СоздатьКолонки();
	Для Каждого ТекКолонка Из ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки Цикл
		ТекКолонка.Видимость = Ложь;
	КонецЦикла;
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки.Представление.Видимость = Истина;	
	РезультатОткрытия = ФормаВыбора.ОткрытьМодально();
	
	Если ТипЗнч(РезультатОткрытия) = Тип("СтрокаДереваЗначений") Тогда
		Если РезультатОткрытия.Имя="ВыбСвойства" Тогда
			Если Не отВыбратьСвойство(ОтчетФорма, , РезультатОткрытия) Тогда
				Возврат;
			КонецЕсли;
		ИначеЕсли РезультатОткрытия.ПутьКДанным = Неопределено Тогда Возврат; КонецЕсли;
		
		Представление = РезультатОткрытия.ПолноеПредставление;
		
		Если ОтчетФорма.ИзмеренияСтроки.Найти(РезультатОткрытия.ПутьКДанным, "ПутьКДанным") <> Неопределено Тогда
			Предупреждение("Измерение <" + Представление + "> уже введено!", 5);
			Возврат;
		КонецЕсли;
		Если ОтчетФорма.ИзмеренияКолонки.Найти(РезультатОткрытия.ПутьКДанным, "ПутьКДанным") <> Неопределено Тогда
			Предупреждение("Измерение <" + Представление + "> уже введено!", 5);
			Возврат;
		КонецЕсли;
		
		Измерения = ОтчетФорма[Элемент.Имя];
		
		Попытка
			Если Измерения.Получить(Измерения.Количество() - 1).Фиксированное Тогда
				ТекСтрока = Измерения.Вставить(Измерения.Количество() - 1);
			Иначе ТекСтрока = Измерения.Добавить();
			КонецЕсли;
		Исключение
			ТекСтрока = Измерения.Добавить();
		КонецПопытки;		
		
		ТекСтрока.Использование = Истина;
		ТекСтрока.ПутьКДанным   = РезультатОткрытия.ПутьКДанным;
		ТекСтрока.Представление = Представление;
		ТекСтрока.ТипИзмерения  = "Элементы";
		
		отУстановитьПараметрыИзмеренияПоУмолчанию(ОтчетФорма, ТекСтрока);
	КонецЕсли;
	
КонецПроцедуры	//	отИзмеренияПередНачаломДобавления()

// обработчик перед удалением строки табличной части
//
// Параметры:
//  ОтчетФорма  – Форма             – Ссылка на форму отчета
//	Элемент     - ЭлементУправления - Элемент управления формы
// 	Отказ       - Булево			- Признак отказа от записи объекта
//
Процедура отИзмеренияПередУдалением(ОтчетФорма, Элемент, Отказ) Экспорт
	
	Если Элемент.ТекущиеДанные.Фиксированное Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры	//	отИзмеренияПередУдалением()

// Процедура обработки измерения в начале выбора
//
// Параметры:
//  ОтчетФорма           – Форма             – Ссылка на форму отчета
//	Элемент              - ЭлементУправления - Элемент управления формы
// 	СтандартнаяОбработка - Булево			 - Признак использования стандартной обработки
// 	ИмяПолей             - Строка			 - Имя полей
//
Процедура отИзмеренияНачалоВыбора(ОтчетФорма, Элемент, СтандартнаяОбработка, ИмяПолей) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДеревоВыбора = ОтчетФорма.ДеревоДоступныхПолей.Скопировать();
	
	СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Измерение", Истина);
	Пока СтрокаПоискаОтбора<>Неопределено Цикл
		ТекРодитель = ?(СтрокаПоискаОтбора.Родитель=Неопределено, ДеревоВыбора, СтрокаПоискаОтбора.Родитель);
		ТекРодитель.Строки.Удалить(СтрокаПоискаОтбора);
		СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Измерение", Истина);
	КонецЦикла;
	
	// для отчетов вида "Движения с остатками" уберем возможность выбора полей, зависящих от регистратора
	Если ОтчетФорма.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
		РегистраторВладелец = ДеревоВыбора.Строки.Найти("ПериодРегистратор", "ПутьКДанным");
		Если РегистраторВладелец <> Неопределено Тогда
			к = 0;
			Пока к <= РегистраторВладелец.Строки.Количество() - 1 Цикл
				ТекСтрока = РегистраторВладелец.Строки.Получить(к);
				Если ТекСтрока.ПутьКДанным <> "ПериодРегистратор.Дата" Тогда
					РегистраторВладелец.Строки.Удалить(ТекСтрока);
				Иначе к = к + 1;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаИзмерений = ОтчетФорма.ЭлементыФормы[ИмяПолей];
	
	ФормаВыбора = ПолучитьОбщуюФорму("ВыборИзДерева", ОтчетФорма);
	ФормаВыбора.ДеревоВыбора    = ДеревоВыбора;
	ФормаВыбора.Заголовок       = "Выбор поля";
	ФормаВыбора.ТекущееЗначение = ТаблицаИзмерений.ТекущиеДанные.ПутьКДанным;
	ФормаВыбора.КолонкаЗначения = "ПутьКДанным";
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.СоздатьКолонки();
	Для Каждого ТекКолонка Из ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки Цикл
		ТекКолонка.Видимость = Ложь;
	КонецЦикла;
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки.Представление.Видимость = Истина;	
	РезультатОткрытия = ФормаВыбора.ОткрытьМодально();
	
	Если ТипЗнч(РезультатОткрытия) = Тип("СтрокаДереваЗначений") Тогда
		Если РезультатОткрытия.Имя="ВыбСвойства" Тогда
			Если Не отВыбратьСвойство(ОтчетФорма,,РезультатОткрытия) Тогда
				Возврат;
			КонецЕсли;
		ИначеЕсли РезультатОткрытия.ПутьКДанным = Неопределено Тогда Возврат; КонецЕсли;	
		Если ТаблицаИзмерений.ТекущиеДанные.ПутьКДанным <> РезультатОткрытия.ПутьКДанным Тогда
			Представление = РезультатОткрытия.ПолноеПредставление;
			Если ОтчетФорма.ИзмеренияСтроки.Найти(РезультатОткрытия.ПутьКДанным, "ПутьКДанным") <> Неопределено Тогда
				Предупреждение("Измерение <" + Представление + "> уже введено!", 5);
				Возврат;
			КонецЕсли;
			Если ОтчетФорма.ИзмеренияКолонки.Найти(РезультатОткрытия.ПутьКДанным, "ПутьКДанным") <> Неопределено Тогда
				Предупреждение("Измерение <" + Представление + "> уже введено!", 5);
				Возврат;
			КонецЕсли;
			
			ИдентификаторПоля = СтрЗаменить(ТаблицаИзмерений.ТекущиеДанные.ПутьКДанным, ".", "_");
			Если ОтчетФорма.ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПоля) Тогда
				ОтчетФорма.ПараметрыИзмеренийСтрок.Удалить(ИдентификаторПоля);
			КонецЕсли;
			
			ТекСтрока = ТаблицаИзмерений.ТекущаяСтрока;
			ТекСтрока.ПутьКДанным   = РезультатОткрытия.ПутьКДанным;
			ТекСтрока.Представление = Представление;
			
			отУстановитьПараметрыИзмеренияПоУмолчанию(ОтчетФорма, ТекСтрока);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры	//	отИзмеренияНачалоВыбора()

// Процедура обработки при изменении типа измерения
//
// Параметры:
//  ОтчетФорма           – Форма             – Ссылка на форму отчета
//	Элемент              - ЭлементУправления - Элемент управления формы
// 	СтандартнаяОбработка - Булево			 - Признак использования стандартной обработки
//
Процедура отИзмеренияТипИзмеренияНачалоВыбораИзСписка(ОтчетФорма, Элемент, СтандартнаяОбработка) Экспорт
	
	спВыбора = Элемент.СписокВыбора;
	Если спВыбора.Количество() = 0 Тогда
		спВыбора.Добавить("Элементы", "Элементы");
		спВыбора.Добавить("Иерархия", "Иерархия");
		спВыбора.Добавить("Только иерархия", "Только иерархия");
	КонецЕсли;
	
	ПутьКДанным = ОтчетФорма.ТекущийЭлемент.ТекущиеДанные.ПутьКДанным;
	Измерение = ОтчетФорма.ДеревоДоступныхПолей.Строки.Найти(ПутьКДанным, "ПутьКДанным", Истина);
	Если Измерение <> Неопределено Тогда
		ТипИзмерения = Измерение.ТипЗначения;
		Если НЕ Справочники.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) и
			НЕ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) Тогда
			СтандартнаяОбработка = Ложь;
			
		Иначе
			ТекОбъект = Новый (ТипИзмерения.Типы()[0]);
			Если НЕ ТекОбъект.Метаданные().Иерархический Тогда
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отИзмеренияТипИзмеренияНачалоВыбораИзСписка()

// обработчик выбора строки табличного поля "ИзмеренияСтроки", "ИзмеренияКолонки" или "Отбор" формы отчета
//
// Параметры:
// 	ОтчетОбъект			 - ОтчетОбъект           - Объект отчета
//  ОтчетФорма           – Форма                 – Ссылка на форму отчета
//	Элемент              - ЭлементУправления     - Элемент управления формы
//	ВыбраннаяСтрока      - СтрокаТабличногоПоля  - Строка табличного поля
//	Колонка				 - КолонкаТабличногоПоля - Колонка табличного поля
// 	СтандартнаяОбработка - Булево			     - Признак использования стандартной обработки
//
Процедура отИзмеренияСтрокиОтборВыбор(ОтчетОбъект, ОтчетФорма, Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка) Экспорт
	
	Если Колонка.Имя = "Имя" Тогда
		
		ДеревоВыбора = ОтчетФорма.ДеревоДоступныхПолей.Скопировать();
		
		СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Отбор", Истина);
		Пока СтрокаПоискаОтбора<>Неопределено Цикл
			ТекРодитель = ?(СтрокаПоискаОтбора.Родитель=Неопределено, ДеревоВыбора, СтрокаПоискаОтбора.Родитель);
			ТекРодитель.Строки.Удалить(СтрокаПоискаОтбора);
			СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Отбор", Истина);
		КонецЦикла;
		
		ФормаВыбора = ПолучитьОбщуюФорму("ВыборИзДерева", ОтчетФорма);
		ФормаВыбора.ДеревоВыбора    = ДеревоВыбора;
		ФормаВыбора.Заголовок       = "Выбор поля";
		ФормаВыбора.ТекущееЗначение = ВыбраннаяСтрока.ПутьКДанным;
		ФормаВыбора.КолонкаЗначения = "ПутьКДанным";
		ФормаВыбора.ЭлементыФормы.ДеревоВыбора.СоздатьКолонки();
		Для Каждого ТекКолонка Из ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки Цикл
			ТекКолонка.Видимость = Ложь;
		КонецЦикла;
		ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки.Представление.Видимость = Истина;	
		РезультатОткрытия = ФормаВыбора.ОткрытьМодально();
		
		Если ТипЗнч(РезультатОткрытия) = Тип("СтрокаДереваЗначений") Тогда
			Если РезультатОткрытия.Имя="ВыбСвойства" Тогда
				Если Не отВыбратьСвойство(ОтчетОбъект, ОтчетФорма, РезультатОткрытия) Тогда
					Возврат;
				Иначе
					НастройкиПостроителя = ОтчетОбъект.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Истина);
					ОтчетОбъект.ПостроительОтчета.Текст = ОтчетОбъект.ТекстЗапроса;
					ОтчетОбъект.ПостроительОтчета.ЗаполнитьНастройки();
					ОтчетОбъект.ПостроительОтчета.УстановитьНастройки(НастройкиПостроителя, Истина, Ложь, Ложь, Ложь, Истина);
				КонецЕсли;
			ИначеЕсли РезультатОткрытия.ПутьКДанным = Неопределено Тогда Возврат; КонецЕсли;
			
			Если ВыбраннаяСтрока.ПутьКДанным <> РезультатОткрытия.ПутьКДанным Тогда
				ПолеНайдено = Ложь;
				Отборы = ОтчетФорма.ПостроительОтчета.Отбор;
				ТекИндекс = Отборы.Индекс(ВыбраннаяСтрока);
				
				Для Каждого ТекСтрока Из Отборы Цикл
					Если ТекСтрока.ПутьКДанным = РезультатОткрытия.ПутьКДанным Тогда
						ПолеНайдено = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Представление = РезультатОткрытия.ПолноеПредставление;
				Если НЕ ПолеНайдено Тогда
					Отборы.Удалить(ТекИндекс);
					
					ТекОтбор = Элемент.ТекущаяСтрока;
					Попытка
						ТекОтбор = Отборы.Добавить(РезультатОткрытия.ПутьКДанным);
						ТекОтбор.Представление = Представление;
					Исключение КонецПопытки;
					
					Если Отборы.Количество() - 1 - ТекИндекс > 0 Тогда
						Отборы.Сдвинуть(Отборы.Количество() - 1, -(Отборы.Количество() - 1 - ТекИндекс));
					КонецЕсли;
					Элемент.ТекущаяСтрока = ТекОтбор;
					
				Иначе
					Предупреждение("Фильтр <" + Представление + "> уже введен!", 5);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Колонка.Имя = "ДополнительныеПоля" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПутьКДанным = ВыбраннаяСтрока.ПутьКДанным;
		
		ДоступныеПоля  = ОтчетФорма.ДеревоДоступныхПолей;
		СтрокаВладелец = ДоступныеПоля.Строки.Найти(ПутьКДанным, "ПутьКДанным", Истина);
		Если СтрокаВладелец = Неопределено Тогда Возврат; КонецЕсли;
		Если СтрокаВладелец.Строки.Количество() = 0 Тогда Возврат; КонецЕсли;
		
		Если ТипЗнч(ОтчетОбъект.ПараметрыИзмеренийСтрок) <> Тип("Структура") Тогда
			ОтчетОбъект.ПараметрыИзмеренийСтрок = Новый Структура();
		КонецЕсли;
		
		ИдентификаторПути = СтрЗаменить(ПутьКДанным, ".", "_");
		Если НЕ ОтчетОбъект.ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) Тогда
			ТаблицаПолей = Новый ТаблицаЗначений();
			ТаблицаПолей.Колонки.Добавить("Имя");
			ТаблицаПолей.Колонки.Добавить("ПутьКДанным");
			ТаблицаПолей.Колонки.Добавить("Представление");
			ОтчетОбъект.ПараметрыИзмеренийСтрок.Вставить(ИдентификаторПути, Новый Структура("Поля,ПоляПредставление", ТаблицаПолей, ""));
		КонецЕсли;
		
		Если НЕ ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Свойство("Поля") Тогда
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Поля", "");
		КонецЕсли;
		ДополнительныеПоля = ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути]["Поля"];
		Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
			ВыбранныеПоля = ДополнительныеПоля.Скопировать();
			
		Иначе
			ВыбранныеПоля = Новый ТаблицаЗначений();
			ВыбранныеПоля.Колонки.Добавить("Имя");
			ВыбранныеПоля.Колонки.Добавить("ПутьКДанным");
			ВыбранныеПоля.Колонки.Добавить("Представление");
		КонецЕсли;
		
		ФормаСпискаПолей = ПолучитьОбщуюФорму("РедактированиеДополнительныхПолей", ОтчетФорма);
		ФормаСпискаПолей.ВладелецПолей = СтрокаВладелец.Имя;
		ФормаСпискаПолей.ПутьКДанным   = ПутьКДанным;
		ФормаСпискаПолей.ВыбранныеПоля = ВыбранныеПоля;
		ФормаСпискаПолей.ДоступныеПоляПостроителя = ДоступныеПоля.Строки.Найти(СтрокаВладелец.ПутьКДанным, "ПутьКДанным", Истина).Строки;
		РезультатОткрытия = ФормаСпискаПолей.ОткрытьМодально();
		
		Если ТипЗнч(РезультатОткрытия) = Тип("ТаблицаЗначений") Тогда
			// Получим представления полей
			СтрПредставлений = "";
			Для Каждого ТекСтрока Из РезультатОткрытия Цикл
				СтрПредставлений = ?(СтрПредставлений = "", "", СтрПредставлений + ", ") + ТекСтрока.Представление;
			КонецЦикла;	
			
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Поля",              РезультатОткрытия);
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("ПоляПредставление", СтрПредставлений);
		КонецЕсли;
		
	ИначеЕсли Колонка.Имя = "Сортировка" Тогда
		СтандартнаяОбработка = Ложь;
		
		// получим признак фиксированного ("жесткого") измерения
		Попытка // на всякий случай через попытку, т.к. не во всех отчетах есть признак Фиксированное
			Если ВыбраннаяСтрока.Фиксированное Тогда
				СтандартнаяОбработка = Ложь;
				Возврат;
			КонецЕсли;
		Исключение КонецПопытки;
		
		ПутьКДанным = ВыбраннаяСтрока.ПутьКДанным;
		
		ДоступныеПоля  = ОтчетФорма.ДеревоДоступныхПолей.Скопировать();
		СтрокаВладелец = ДоступныеПоля.Строки.Найти(ПутьКДанным, "ПутьКДанным", Истина);
		Если СтрокаВладелец = Неопределено Тогда Возврат; КонецЕсли;
		Если СтрокаВладелец.Строки.Количество() = 0 Тогда Возврат; КонецЕсли;
		
		Если ТипЗнч(ОтчетОбъект.ПараметрыИзмеренийСтрок) <> Тип("Структура") Тогда
			ОтчетОбъект.ПараметрыИзмеренийСтрок = Новый Структура();
		КонецЕсли;
		
		ИдентификаторПути = СтрЗаменить(ПутьКДанным, ".", "_");
		Если НЕ ОтчетОбъект.ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) Тогда
			ТаблицаПолей = Новый ТаблицаЗначений();
			ТаблицаПолей.Колонки.Добавить("ПутьКДанным");
			ТаблицаПолей.Колонки.Добавить("Представление");
			ТаблицаПолей.Колонки.Добавить("Убывание", Новый ОписаниеТипов("Булево"));
			ОтчетОбъект.ПараметрыИзмеренийСтрок.Вставить(ИдентификаторПути, Новый Структура("Порядок,ПорядокПредставление", ТаблицаПолей, ""));
		КонецЕсли;
		
		Если НЕ ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Свойство("Порядок") Тогда
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Порядок", "");
		КонецЕсли;
		
		ДополнительныеПоля = ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути]["Порядок"];
		Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
			ВыбранныеПоля = ДополнительныеПоля.Скопировать();
			
		Иначе
			ВыбранныеПоля = Новый ТаблицаЗначений();
			ВыбранныеПоля.Колонки.Добавить("ПутьКДанным");
			ВыбранныеПоля.Колонки.Добавить("Представление");
			ВыбранныеПоля.Колонки.Добавить("Убывание", Новый ОписаниеТипов("Булево"));
		КонецЕсли;
		
		ФормаСпискаПолей = ПолучитьОбщуюФорму("РедактированиеДополнительныхПолей", ОтчетФорма);
		ФормаСпискаПолей.ВладелецПолей = СтрокаВладелец.Имя;
		ФормаСпискаПолей.ПутьКДанным   = ПутьКДанным;
		ФормаСпискаПолей.ВыбранныеПоля = ВыбранныеПоля;
		
		СтрокаПоискаОтбора = СтрокаВладелец.Строки.Найти(Ложь, "Порядок", Истина);
		Пока СтрокаПоискаОтбора<>Неопределено Цикл
			ТекРодитель = ?(СтрокаПоискаОтбора.Родитель=Неопределено, СтрокаВладелец, СтрокаПоискаОтбора.Родитель);
			ТекРодитель.Строки.Удалить(СтрокаПоискаОтбора);
			СтрокаПоискаОтбора = СтрокаВладелец.Строки.Найти(Ложь, "Порядок", Истина);
		КонецЦикла;
		
		Если СтрокаВладелец.Строки.Количество()=0 Тогда Возврат; КонецЕсли;
		ФормаСпискаПолей.ДоступныеПоляПостроителя = СтрокаВладелец.Строки;
		РезультатОткрытия = ФормаСпискаПолей.ОткрытьМодально();
		
		Если ТипЗнч(РезультатОткрытия) = Тип("ТаблицаЗначений") Тогда
			// Получим представления полей
			СтрПредставлений = "";
			Для Каждого ТекСтрока Из РезультатОткрытия Цикл
				СтрПредставлений = ?(СтрПредставлений = "", "", СтрПредставлений + ", ") + ТекСтрока.Представление + ?(ТекСтрока.Убывание, " (убыв)", "");
			КонецЦикла;	
			
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Порядок",              РезультатОткрытия);
			ОтчетОбъект.ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("ПорядокПредставление", СтрПредставлений);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отИзмеренияСтрокиОтборВыбор()

// процедура контролирует дублирование измерений и фильтра
Процедура отИзмеренияПриОкончанииРедактирования(ОтчетОбъект, Элемент, НаименованиеПолей) Экспорт
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	
	ПостроительОтчета = ОтчетОбъект.ПостроительОтчета;
	
	ПутьКДанным  = Элемент.ТекущиеДанные.ПутьКДанным;
	ВыбИзмерение = ПостроительОтчета[НаименованиеПолей].Найти(Элемент.ТекущиеДанные.Имя);
	Для Каждого ТекИзмерение Из ПостроительОтчета[НаименованиеПолей] Цикл
		Если ТекИзмерение.ПутьКДанным = ПутьКДанным и ТекИзмерение <> ВыбИзмерение Тогда
			Если НаименованиеПолей = "Отбор" Тогда
				ПостроительОтчета[НаименованиеПолей].Удалить(ПостроительОтчета[НаименованиеПолей].Индекс(ВыбИзмерение));
			Иначе ПостроительОтчета[НаименованиеПолей].Удалить(ВыбИзмерение);
			КонецЕсли;
			Сообщить("Удалено дублирующее значение <" + ПутьКДанным + ">!");
			
			Прервать;
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры	//	отИзмеренияПриОкончанииРедактирования()

// Процедура обработки начала перетаскивания
//
// Параметры:
// 	ОтчетОбъект			    - ОтчетОбъект             - Объект отчета
//	Элемент                 - ЭлементУправления       - Элемент управления формы
//	ПараметрыПеретаскивания - ПараметрыПеретаскивания - Параметры перетаскивания
// 	СтандартнаяОбработка    - Булево			      - Признак использования стандартной обработки
//
Процедура отИзмеренияНачалоПеретаскивания(ОтчетОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка) Экспорт
	
	// получим признак фиксированного ("жесткого") измерения
	Попытка // на всякий случай через попытку, т.к. не во всех отчетах есть признак Фиксированное
		Если ПараметрыПеретаскивания.Значение.Фиксированное Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
	Исключение КонецПопытки;
	
	ОтчетОбъект.ПриемникПеретаскивания = ?(Элемент.Имя = "ИзмеренияСтроки", "ИзмеренияКолонки", "ИзмеренияСтроки");
	
КонецПроцедуры	//	отИзмеренияНачалоПеретаскивания()

// Процедура проверки перетаскивания
//
// Параметры:
// 	ОтчетОбъект			    - ОтчетОбъект             - Объект отчета
//	Элемент                 - ЭлементУправления       - Элемент управления формы
//	ПараметрыПеретаскивания - ПараметрыПеретаскивания - Параметры перетаскивания
// 	СтандартнаяОбработка    - Булево			      - Признак использования стандартной обработки
//	Строка                  - СтрокаТабличногоПоля    - Строка табличного поля
//	Колонка				    - КолонкаТабличногоПоля   - Колонка табличного поля
//
Процедура отИзмеренияПроверкаПеретаскивания(ОтчетОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка) Экспорт
	
	Если Элемент.Имя = ОтчетОбъект.ПриемникПеретаскивания Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры	//	отИзмеренияПроверкаПеретаскивания()

// Процедура обработки перетаскивания измерения
//
// Параметры:
// 	ОтчетОбъект			    - ОтчетОбъект             - Объект отчета
//	Элемент                 - ЭлементУправления       - Элемент управления формы
//	ПараметрыПеретаскивания - ПараметрыПеретаскивания - Параметры перетаскивания
// 	СтандартнаяОбработка    - Булево			      - Признак использования стандартной обработки
//	Строка                  - СтрокаТабличногоПоля    - Строка табличного поля
//	Колонка				    - КолонкаТабличногоПоля   - Колонка табличного поля
//
Процедура отИзмеренияПеретаскивание(ОтчетОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка) Экспорт
	
	// получим признак фиксированного ("жесткого") измерения
	Попытка // на всякий случай через попытку, т.к. не во всех отчетах есть признак Фиксированное
		Если Строка.Фиксированное Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
	Исключение КонецПопытки;
	
	// запретим перемещение измерения ниже документа движения
	Попытка // на всякий случай через попытку, т.к. не во всех отчетах есть признак Фиксированное
		Если Строка = Неопределено Тогда
			Если НЕ Элемент.ТекущиеДанные.Фиксированное Тогда
				СтандартнаяОбработка = Ложь;
				Возврат;
			КонецЕсли;	
		КонецЕсли;
	Исключение КонецПопытки;	
	
	Если ОтчетОбъект.ПриемникПеретаскивания = Элемент.Имя Тогда
		СтрокаИсточник = ПараметрыПеретаскивания.Значение;
		
		ТекСтрока = ОтчетОбъект[ОтчетОбъект.ПриемникПеретаскивания].Добавить();
		ТекСтрока.Использование = СтрокаИсточник.Использование;
		ТекСтрока.ПутьКДанным   = СтрокаИсточник.ПутьКДанным;
		ТекСтрока.Представление = СтрокаИсточник.Представление;
		ТекСтрока.ТипИзмерения  = СтрокаИсточник.ТипИзмерения;
		
		ОтчетОбъект[?(Элемент.Имя = "ИзмеренияСтроки", "ИзмеренияКолонки", "ИзмеренияСтроки")].Удалить(СтрокаИсточник);
	КонецЕсли;
	
	ОтчетОбъект.ПриемникПеретаскивания = "";
	
КонецПроцедуры	//	отИзмеренияПеретаскивание()

// Процедура обработки сдвига строки измерения
//
// Параметры:
// 	ОтчетОбъект	- ОтчетОбъект          - Объект отчета
//	ТекСтрока   - СтрокаТабличногоПоля - Строка табличного поля
//	Кнопка      - Кнопка               - Кнопка на форме
//
Процедура отИзмеренияСдвинутьСтроку(ОтчетОбъект, ТекСтрока, Кнопка) Экспорт
	
	// проверим, а есть ли строка
	Если ТекСтрока = Неопределено Тогда Возврат; КонецЕсли;
	
	// запретим сдвиг, если текущее измерение фиксированное
	// получим признак фиксированного ("жесткого") измерения
	Попытка // на всякий случай через попытку, т.к. не во всех отчетах есть признак Фиксированное
		Если ТекСтрока.Фиксированное Тогда Возврат; КонецЕсли;
	Исключение КонецПопытки;
	
	ИзмеренияСтроки = ОтчетОбъект.ИзмеренияСтроки;
	
	ТекНомерСтроки = ТекСтрока.НомерСтроки-1; 
	
	Если ((Кнопка.Имя = "СдвинутьВниз") И (ТекНомерСтроки = ИзмеренияСтроки.Количество()-1)) ИЛИ 
		((Кнопка.Имя = "СдвинутьВверх") И (ТекНомерСтроки = 0)) Тогда
		Возврат;
	КонецЕсли;
	
	Если Кнопка.Имя = "СдвинутьВниз" Тогда
		СледНомер = ТекНомерСтроки+1;
		
		// запретим сдвиг, если следующее измерение фиксированное
		// получим признак фиксированного ("жесткого") измерения
		Попытка // на всякий случай через попытку, т.к. не во всех отчетах есть признак Фиксированное
			Если ИзмеренияСтроки.Получить(СледНомер).Фиксированное Тогда Возврат; КонецЕсли;
		Исключение КонецПопытки;
		
	Иначе
		СледНомер = ТекНомерСтроки-1;
	КонецЕсли;
	
	Попытка
		ТекПолеИзПараметрыИспользованияПолейИПоказателей = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ИзмеренияСтроки[ТекНомерСтроки].ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	Исключение
		ТекПолеИзПараметрыИспользованияПолейИПоказателей = Неопределено;
	КонецПопытки;
	
	Попытка
		СледПолеИзПараметрыИспользованияПолейИПоказателей = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ИзмеренияСтроки[СледНомер].ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	Исключение
		СледПолеИзПараметрыИспользованияПолейИПоказателей = Неопределено;
	КонецПопытки;
	ТекСчетчикОграничений=0;
	СледСчетчикОграничений=0;
	
	Для Каждого ТекФункция Из ОтчетОбъект.Функции Цикл
		Для Каждого ТекПоказатель Из ОтчетОбъект.Показатели Цикл
			ИмяПоказателя = ТекПоказатель.Имя + ТекФункция.Имя;
			Попытка 
				Если (ТекПолеИзПараметрыИспользованияПолейИПоказателей = Неопределено) Тогда
				ИначеЕсли НЕ ТекПолеИзПараметрыИспользованияПолейИПоказателей.Свойство("Использование") Тогда
				ИначеЕсли Не ТекПолеИзПараметрыИспользованияПолейИПоказателей["Использование"]["Ресурсы"][ИмяПоказателя] Тогда
					ТекСчетчикОграничений = ТекСчетчикОграничений+1;
				КонецЕсли;
			Исключение
			Конецпопытки;
			
			Попытка
				Если СледПолеИзПараметрыИспользованияПолейИПоказателей = Неопределено Тогда
				ИначеЕсли НЕ СледПолеИзПараметрыИспользованияПолейИПоказателей .Свойство("Использование") Тогда
				ИначеЕсли Не СледПолеИзПараметрыИспользованияПолейИПоказателей["Использование"]["Ресурсы"][ИмяПоказателя] Тогда
					СледСчетчикОграничений = СледСчетчикОграничений+1;
				КонецЕсли;
			Исключение
			Конецпопытки;
		КонецЦикла;
	КонецЦикла;
	
	Если ((Кнопка.Имя = "СдвинутьВниз") И (ТекСчетчикОграничений=0) И (СледСчетчикОграничений>0)) ИЛИ
		((Кнопка.Имя = "СдвинутьВверх") И (ТекСчетчикОграничений>0) И (СледСчетчикОграничений=0)) Тогда
		Возврат;
	КонецЕсли;
	
	Если Кнопка.Имя = "СдвинутьВниз" Тогда
		ИзмеренияСтроки.Сдвинуть(ТекНомерСтроки, 1);
	ИначеЕсли Кнопка.Имя = "СдвинутьВверх" Тогда
		ИзмеренияСтроки.Сдвинуть(ТекНомерСтроки, -1);
	КонецЕсли;
	
КонецПроцедуры // отИзмеренияКолонокПередОкончаниемРедактирования()

// Процедура отметки показателей
//	ОтчетФорма - Форма           - Форма отчета
//	Поля       - ТаблицаЗначений - Список полей
//	Кнопка     - Кнопка          - Кнопка на форме
//	
Процедура отФункцииПоказателиПометить(ОтчетФорма, Поля, Кнопка) Экспорт
	
	Для Каждого ТекСтрока Из Поля Цикл
		ТекСтрока.Использование = (Найти(Кнопка.Имя, "ПометитьВсе"));
	КонецЦикла;
	
КонецПроцедуры	//	отФункцииПоказателиПометить()

// Процедура отбора перед началом добавления
//
// Параметры:
//  ОтчетФорма  – Форма             – Ссылка на форму отчета
//	Элемент     - ЭлементУправления - Элемент управления формы
// 	Отказ       - Булево			- Признак отказа от записи объекта
// 	Копирование - Булево			- Признак копирования
//
Процедура отОтборПередНачаломДобавления(ОтчетФорма, Элемент, Отказ, Копирование) Экспорт
	
	Отказ = Истина;
	ДеревоВыбора = ОтчетФорма.ДеревоДоступныхПолей.Скопировать();
	СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Отбор", Истина);
	Пока СтрокаПоискаОтбора<>Неопределено Цикл
		ТекРодитель = ?(СтрокаПоискаОтбора.Родитель=Неопределено, ДеревоВыбора, СтрокаПоискаОтбора.Родитель);
		ТекРодитель.Строки.Удалить(СтрокаПоискаОтбора);
		СтрокаПоискаОтбора = ДеревоВыбора.Строки.Найти(Ложь, "Отбор", Истина);
	КонецЦикла;
	ФормаВыбора = ПолучитьОбщуюФорму("ВыборИзДерева", ОтчетФорма);
	ФормаВыбора.ДеревоВыбора = ДеревоВыбора;
	ФормаВыбора.Заголовок = "Выбор поля";
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.СоздатьКолонки();
	Для Каждого ТекКолонка Из ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки Цикл
		ТекКолонка.Видимость = Ложь;
	КонецЦикла;
	ФормаВыбора.ЭлементыФормы.ДеревоВыбора.Колонки.Представление.Видимость = Истина;	
	РезультатОткрытия = ФормаВыбора.ОткрытьМодально();
	
	Если ТипЗнч(РезультатОткрытия) = Тип("СтрокаДереваЗначений") Тогда
		Если РезультатОткрытия.Имя="ВыбСвойства" Тогда
			Если Не отВыбратьСвойство(ОтчетФорма, , РезультатОткрытия) Тогда
				Возврат;
			Иначе
				НастройкиПостроителя = ОтчетФорма.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Истина);
				ОтчетФорма.ПостроительОтчета.Текст = ОтчетФорма.ТекстЗапроса;
				ОтчетФорма.ПостроительОтчета.ЗаполнитьНастройки();
				ОтчетФорма.ПостроительОтчета.УстановитьНастройки(НастройкиПостроителя, Истина, Ложь, Ложь, Ложь, Истина);
			КонецЕсли;
		КонецЕсли;
		ПолеНайдено = Ложь;
		ПоляПостроителя = ОтчетФорма.ПостроительОтчета.Отбор;
		Для Каждого ТекПоле Из ПоляПостроителя Цикл
			Если ТекПоле.ПутьКДанным = РезультатОткрытия.ПутьКДанным Тогда
				ПолеНайдено = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Представление = РезультатОткрытия.ПолноеПредставление;
		
		Если НЕ ПолеНайдено Тогда
			Попытка
				ТекОтбор = ПоляПостроителя.Добавить(РезультатОткрытия.ПутьКДанным);
				ТекОтбор.Представление = Представление;
				
				ОтчетФорма.ЭлементыФормы.Отбор.ТекущаяСтрока = ТекОтбор;
			Исключение КонецПопытки;
			
		Иначе
			Предупреждение("Фильтр <" + Представление + "> уже введен!", 5);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отОтборПередНачаломДобавления()

// Процедура отбора перед началом изменения
//
// Параметры:
//  ОтчетФорма  – Форма             – Ссылка на форму отчета
//	Элемент     - ЭлементУправления - Элемент управления формы
// 	Отказ       - Булево			- Признак отказа от записи объекта
//
Процедура отОтборПередНачаломИзменения(ОтчетФорма, Элемент, Отказ) Экспорт
	
	Если Элемент.ТекущаяКолонка.Имя = "Значение" Тогда
		ТекЭлемент = Элемент.ТекущаяКолонка.ЭлементУправления;
		ТекЭлемент.ВыбиратьТип = Истина;
		
		ПутьКДанным = СтрЗаменить(Элемент.ТекущиеДанные.ПутьКДанным, ".", "_");
		Если ОтчетФорма.СтруктураСвязиСвойств.Свойство(ПутьКДанным) Тогда
			ТипЗначения = ОтчетФорма.СтруктураСвязиСвойств[ПутьКДанным].ТипЗначения;
		Иначе ТипЗначения = Элемент.ТекущиеДанные.ТипЗначения;
		КонецЕсли;
		
		ТекЭлемент.РедактированиеТекста = (ТипЗначения.СодержитТип(Тип("Строка")) или ТипЗначения.СодержитТип(Тип("Число")) или ТипЗначения.СодержитТип(Тип("Дата")));
		ТекЭлемент.ОграничениеТипа = Новый ОписаниеТипов(ТипЗначения);
		ТекЭлемент.Значение = ТекЭлемент.ОграничениеТипа.ПривестиЗначение(Неопределено);
		Если ТипЗначения.Типы().Количество() = 1 Тогда
			ТекЭлемент.ВыбиратьТип = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//	отОтборПередНачаломИзменения()

// Процедура отбора значения перед началом выбора
//
// Параметры:
//  ОтчетФорма           – Форма             – Ссылка на форму отчета
//	Элемент              - ЭлементУправления - Элемент управления формы
// 	СтандартнаяОбработка - Булево			 - Признак использования стандартной обработки
//
Процедура отОтборЗначениеНачалоВыбора(ОтчетФорма, Элемент, СтандартнаяОбработка, ИмяФормыВыбора = Неопределено, ДопПараметры = Неопределено, ДопОтборы = Неопределено, МенеджерПолученияФормы = Неопределено) Экспорт
	
	Попытка СтруктураСвязиСвойств = ОтчетФорма.СтруктураСвязиСвойств;
	Исключение КонецПопытки;
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	ПутьКДанным = СтрЗаменить(ОтчетФорма.ЭлементыФормы.Отбор.ТекущиеДанные.ПутьКДанным, ".", "_");
	
	Если СтруктураСвязиСвойств.Свойство(ПутьКДанным) Тогда
		СтруктураСвойства = СтруктураСвязиСвойств[ПутьКДанным];
		ТипЗначения = СтруктураСвойства.ТипЗначения;
		
		Если ТипЗнч(Элемент.Значение) = Тип("СписокЗначений") тогда
			СтандартнаяОбработка = Ложь;
			Форма = ПолучитьОбщуюФорму("РедактированиеСпискаЗначений",ОтчетФорма);
			Форма.ДопустимыеТипы = СтруктураСвойства.ТипЗначения;
			Форма.Владелец = СтруктураСвойства.Свойство;
			Форма.Значения = ОтчетФорма.ЭлементыФормы.Отбор.ТекущиеДанные.Значение;
			Форма.ОткрытьМодально();
			Возврат;
		КонецЕсли;
		
		Элемент.ОграничениеТипа = Новый ОписаниеТипов(ТипЗначения);
		Элемент.РедактированиеТекста = (ТипЗначения.СодержитТип(Тип("Строка")) или ТипЗначения.СодержитТип(Тип("Число")) или ТипЗначения.СодержитТип(Тип("Дата")));
		
		Если ТипЗначения.Типы().Количество() = 1 Тогда
			ТекЗн = Элемент.ОграничениеТипа.ПривестиЗначение("");
			Элемент.Значение = Элемент.ОграничениеТипа.ПривестиЗначение(Элемент.Значение);
			
			ЕстьВладелецСвойство = Ложь;
			Попытка
				МетаданныеЗначения   = Элемент.Значение.Метаданные();
				Если МетаданныеЗначения.Владельцы.Количество() = 1 и МетаданныеЗначения.Владельцы.Содержит(СтруктураСвойства.Свойство.Метаданные()) Тогда
					ЕстьВладелецСвойство = Истина;
				КонецЕсли;
			Исключение КонецПопытки;
			
			Если ЕстьВладелецСвойство Тогда
				Попытка
					ФормаВыбора = Справочники[МетаданныеЗначения.Имя].ПолучитьФорму(МетаданныеЗначения.ОсновнаяФормаДляВыбора.Имя, Элемент);
					ФормаВыбора.ПараметрОтборПоВладельцу = СтруктураСвойства.Свойство;
					ФормаВыбора.НачальноеЗначениеВыбора  = Элемент.Значение;
					ФормаВыбора.ЗакрыватьПриВыборе       = Истина;
					ФормаВыбора.РежимВыбора              = Истина;
					ФормаВыбора.Открыть();
					
					СтандартнаяОбработка = Ложь;
				Исключение КонецПопытки;	
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если Элемент.Значение = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Попытка
			СписокВладельцев	= Новый СписокЗначений;
			МетаданныеЗначения	= Неопределено;
			ЭтоХарактеристика	= Ложь;
			ЭтоЕдиницаИзмерения = Ложь;
			ТипЗначенияОтбора	= ТипЗнч(Элемент.Значение);
			ЗначениеЭтоСписок	= (ТипЗначенияОтбора = Тип("СписокЗначений"));
			Если ЗначениеЭтоСписок Тогда
				Если Элемент.Значение.ТипЗначения.Типы().Количество()=1 Тогда
					ТипПоля = Элемент.Значение.ТипЗначения.Типы()[0];
					МетаданныеЗначения  = Метаданные.НайтиПоТипу(ТипПоля);
					ЭтоХарактеристика	= (ТипПоля = Тип("СправочникСсылка.ХарактеристикиНоменклатуры"));
					ЭтоЕдиницаИзмерения	= (ТипПоля = Тип("СправочникСсылка.ЕдиницыИзмерения"));
				КонецЕсли;
			Иначе
				МетаданныеЗначения  = Элемент.Значение.Метаданные();
				ЭтоХарактеристика	= (ТипЗначенияОтбора = Тип("СправочникСсылка.ХарактеристикиНоменклатуры"));
				ЭтоЕдиницаИзмерения	= (ТипЗначенияОтбора = Тип("СправочникСсылка.ЕдиницыИзмерения"));
			КонецЕсли;
			
			Если МетаданныеЗначения <> Неопределено И МетаданныеЗначения.Владельцы.Количество() > 0 Тогда
				ТипСсылкиНоменклатуры	  = Тип("СправочникСсылка.Номенклатура");
				ТипСсылкиТипаНоменклатуры = Тип("СправочникСсылка.ТипыНоменклатуры");
				
				Для Каждого ТекОтбор Из ОтчетФорма.ПостроительОтчета.Отбор Цикл
					Если Не ТекОтбор.Использование ИЛИ Найти(ВРег(ТекОтбор.ВидСравнения), "НЕ")>0 Тогда Продолжить; КонецЕсли;
					
					СписокДоступныхТипов = Новый СписокЗначений;
					МассивТипов = ТекОтбор.ТипЗначения.Типы();
					Для Каждого ТекТип Из МассивТипов Цикл
						СсылкаВладельца = Новый (ТекТип);
						МетаданныеВладельца = СсылкаВладельца.Метаданные();
						Если МетаданныеЗначения.Владельцы.Содержит(МетаданныеВладельца) Тогда
							СписокДоступныхТипов.Добавить(ТекТип);
						КонецЕсли;
					КонецЦикла;							
					
					Если СписокДоступныхТипов.Количество() = 0 Тогда Продолжить; КонецЕсли;
					
					Если ТипЗнч(ТекОтбор.Значение) = Тип("СписокЗначений") Тогда
						Для Каждого ТекЗначениеВладельца Из ТекОтбор.Значение Цикл
							ТекЗначение = ТекЗначениеВладельца.Значение;
							ТекТипЗначения = ТипЗнч(ТекЗначение);
							
							Если СписокДоступныхТипов.НайтиПоЗначению(ТекТипЗначения) = Неопределено Тогда Продолжить; КонецЕсли;
							Если ЭтоХарактеристика Тогда
								//1 - Характеристики подчинены данному типу номенклатуры,
								//2 - Характеристики подчинены номенклатурным позициям, 
								//3 (и любой другой) - Учет по характеристикам не ведется
								Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
									Если ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик = 1 Тогда
										ТекЗначение = ТекЗначение.ТипНоменклатуры;
									ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик > 2 Тогда
										Продолжить;
									КонецЕсли;
								ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
									Если ТекЗначение.ИспользованиеХарактеристик > 1 Тогда
										Продолжить;
									КонецЕсли;
								КонецЕсли;
							ИначеЕсли ЭтоЕдиницаИзмерения Тогда
								//1 - Единицы измерения подчинены данному типу номенклатуры,
								//2 - Единицы подчинены номенклатурным позициям
								Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
									Если ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
										ТекЗначение = ТекЗначение.ТипНоменклатуры;
									ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения > 2 Тогда
										Продолжить;
									КонецЕсли;
								ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
									Если ТекЗначение.ИспользованиеЕдиницИзмерения > 1 Тогда
										Продолжить;
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
							
							ЭтоГруппа = Ложь;
							Попытка	ЭтоГруппа = ТекЗначение.ЭтоГруппа; Исключение КонецПопытки;
							Если ЭтоГруппа Тогда
								ОтДобавитьЗначениеКСпискуВладельцевПоИерархии(ТекЗначение, СписокВладельцев, ТекЗначение.Метаданные().Имя, ТипЗнч(ТекЗначение));
							Иначе
								Если СписокВладельцев.НайтиПоЗначению(ТекЗначение) = Неопределено Тогда
									СписокВладельцев.Добавить(ТекЗначение);
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					Иначе
						ТекЗначение = ТекОтбор.Значение;
						ТекТипЗначения = ТипЗнч(ТекЗначение);
						
						Если СписокДоступныхТипов.НайтиПоЗначению(ТекТипЗначения) = Неопределено Тогда Продолжить; КонецЕсли;
						Если ЭтоХарактеристика Тогда
							//1 - Характеристики подчинены данному типу номенклатуры,
							//2 - Характеристики подчинены номенклатурным позициям, 
							//3 (и любой другой) - Учет по характеристикам не ведется
							Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
								Если ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик = 1 Тогда
									ТекЗначение = ТекЗначение.ТипНоменклатуры;
								ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик > 2 Тогда
									Продолжить;
								КонецЕсли;
							ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
								Если ТекЗначение.ИспользованиеХарактеристик > 1 Тогда
									Продолжить;
								КонецЕсли;
							КонецЕсли;
						ИначеЕсли ЭтоЕдиницаИзмерения Тогда
							//1 - Единицы измерения подчинены данному типу номенклатуры,
							//2 - Единицы подчинены номенклатурным позициям
							Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
								Если ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
									ТекЗначение = ТекЗначение.ТипНоменклатуры;
								ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения > 2 Тогда
									Продолжить;
								КонецЕсли;
							ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
								Если ТекЗначение.ИспользованиеЕдиницИзмерения > 1 Тогда
									Продолжить;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
						ЭтоГруппа = Ложь;
						Попытка	ЭтоГруппа = ТекЗначение.ЭтоГруппа; Исключение КонецПопытки;
						Если ЭтоГруппа Тогда
							ОтДобавитьЗначениеКСпискуВладельцевПоИерархии(ТекЗначение, СписокВладельцев, ТекЗначение.Метаданные().Имя, ТипЗнч(ТекЗначение));
						Иначе
							Если СписокВладельцев.НайтиПоЗначению(ТекЗначение) = Неопределено Тогда
								СписокВладельцев.Добавить(ТекЗначение);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			ИспользоватьПриватнуюФорму = Не обЗначениеНеЗаполнено(ИмяФормыВыбора);
			
			Если ЗначениеЭтоСписок Тогда
				СтруктураПараметров = Новый Структура;
				Если ИспользоватьПриватнуюФорму Тогда
					СтруктураПараметров.Вставить("ИмяФормыВыбора", ИмяФормыВыбора);
					Если обЗначениеНеЗаполнено(МенеджерПолученияФормы) Тогда
						СтруктураПараметров.Вставить("МенеджерПолученияФормы", МенеджерПолученияФормы);
					КонецЕсли;
				КонецЕсли;
				
				Если ТипЗнч(ДопОтборы) = Тип("Структура") Тогда
					СтруктураПараметров.Вставить("ДопОтборы", ДопОтборы);
				КонецЕсли;
				
				Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
					СтруктураПараметров.Вставить("ДопПараметры", ДопПараметры);
				КонецЕсли;
				
				Если СписокВладельцев.Количество() > 0 Тогда
					СтруктураПараметров.Вставить("СписокВладельцев", СписокВладельцев);
				КонецЕсли;
				
				СтандартнаяОбработка = Ложь;
				Форма = ПолучитьОбщуюФорму("РедактированиеСпискаЗначений", ОтчетФорма);
				Форма.ДопустимыеТипы = ОтчетФорма.ЭлементыФормы.Отбор.ТекущиеДанные.ТипЗначения;
				Форма.Владелец = Неопределено;
				Форма.Значения = Элемент.Значение;
				Форма.СтруктураПараметров = СтруктураПараметров;
				Форма.ОткрытьМодально();
				Возврат;
			ИначеЕсли ИспользоватьПриватнуюФорму Тогда
				
				Если обЗначениеНеЗаполнено(МенеджерПолученияФормы) Тогда
					ФормаВыбора = МенеджерПолученияФормы.ПолучитьФорму(ИмяФормыВыбора, Элемент);
				Иначе
					ТекущийМенеджерТипа = Неопределено;
					Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=Справочники;
					ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=Документы;
					ИначеЕсли Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=Перечисления;
					ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=ПланыВидовХарактеристик;
					ИначеЕсли ПланыСчетов.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=ПланыСчетов;
					ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=ПланыВидовРасчета;
					ИначеЕсли ПланыОбмена.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=ПланыОбмена;
					ИначеЕсли БизнесПроцессы.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=БизнесПроцессы;
					ИначеЕсли Задачи.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
						ТекущийМенеджерТипа=Задачи;
					КонецЕсли;
					
					Если ТекущийМенеджерТипа=Неопределено Тогда Возврат; КонецЕсли;
					
					ФормаВыбора = ТекущийМенеджерТипа[МетаданныеЗначения.Имя].ПолучитьФорму(ИмяФормыВыбора, Элемент);
				КонецЕсли;
				
				Если СписокВладельцев.Количество() > 0 Тогда
					ФормаВыбора.Отбор.Владелец.ВидСравнения =  ВидСравнения.ВСписке;
					ФормаВыбора.Отбор.Владелец.Использование = Истина;
					ФормаВыбора.Отбор.Владелец.Значение = СписокВладельцев;
				КонецЕсли;
				
				Если ТипЗнч(ДопОтборы) = Тип("Структура") Тогда
					Для Каждого СтруктураОтбора Из ДопОтборы Цикл
						ТекОтбор = СтруктураОтбора.Значение;
						ФормаВыбора.Отбор[ТекОтбор.Имя].ВидСравнения  = ТекОтбор.ВидСравнения;
						ФормаВыбора.Отбор[ТекОтбор.Имя].Использование = Истина;
						ФормаВыбора.Отбор[ТекОтбор.Имя].Значение	  = ТекОтбор.Значение;
					КонецЦикла;
				КонецЕсли;
				
				Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
					Для Каждого ТекПараметр Из ДопПараметры Цикл
						ФормаВыбора[ТекПараметр.Ключ] = ТекПараметр.Значение;
					КонецЦикла;
				КонецЕсли;
				
				ФормаВыбора.НачальноеЗначениеВыбора  = Элемент.Значение;
				ФормаВыбора.ЗакрыватьПриВыборе       = Истина;
				ФормаВыбора.РежимВыбора              = Истина;
				ФормаВыбора.Открыть();
				СтандартнаяОбработка = Ложь;
				
			ИначеЕсли Не ИспользоватьПриватнуюФорму Тогда
				
				Если СписокВладельцев.Количество()=0 И ДопОтборы=Неопределено И ДопПараметры=Неопределено Тогда
					Возврат;
				КонецЕсли;
				
				ТекущийМенеджерТипа = Неопределено;
				Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=Справочники;
				ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=Документы;
				ИначеЕсли Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=Перечисления;
				ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=ПланыВидовХарактеристик;
				ИначеЕсли ПланыСчетов.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=ПланыСчетов;
				ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=ПланыВидовРасчета;
				ИначеЕсли ПланыОбмена.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=ПланыОбмена;
				ИначеЕсли БизнесПроцессы.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=БизнесПроцессы;
				ИначеЕсли Задачи.ТипВсеСсылки().СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
					ТекущийМенеджерТипа=Задачи;
				КонецЕсли;
				
				Если ТекущийМенеджерТипа=Неопределено Тогда Возврат; КонецЕсли;
				
				ФормаВыбора = ТекущийМенеджерТипа[МетаданныеЗначения.Имя].ПолучитьФорму(МетаданныеЗначения.ОсновнаяФормаДляВыбора.Имя, Элемент);
				
				Если СписокВладельцев.Количество() > 0 Тогда
					ФормаВыбора.Отбор.Владелец.ВидСравнения =  ВидСравнения.ВСписке;
					ФормаВыбора.Отбор.Владелец.Использование = Истина;
					ФормаВыбора.Отбор.Владелец.Значение = СписокВладельцев;
				КонецЕсли;
				
				Если ТипЗнч(ДопОтборы) = Тип("Структура") Тогда
					Для Каждого СтруктураОтбора Из ДопОтборы Цикл
						ТекОтбор = СтруктураОтбора.Значение;
						ФормаВыбора.Отбор[ТекОтбор.Имя].ВидСравнения  = ТекОтбор.ВидСравнения;
						ФормаВыбора.Отбор[ТекОтбор.Имя].Использование = Истина;
						ФормаВыбора.Отбор[ТекОтбор.Имя].Значение	  = ТекОтбор.Значение;
					КонецЦикла;
				КонецЕсли;
				
				Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
					Для Каждого ТекПараметр Из ДопПараметры Цикл
						ФормаВыбора[ТекПараметр.Ключ] = ТекПараметр.Значение;
					КонецЦикла;
				КонецЕсли;
				ФормаВыбора.НачальноеЗначениеВыбора  = Элемент.Значение;
				ФормаВыбора.ЗакрыватьПриВыборе       = Истина;
				ФормаВыбора.РежимВыбора              = Истина;
				ФормаВыбора.Открыть();
				СтандартнаяОбработка = Ложь;
			КонецЕсли;	
		Исключение
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры	//	отОтборЗначениеНачалоВыбора()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМ И МОДУЛЕЙ ОТЧЕТОВ

// Процедура стандартный обработчик инициализации объекта - отчета
//
// Параметры:
//  ЭтотОбъект – Объект – Отчет
//
Процедура отОтчетПриИнициализацииОбъекта(ЭтотОбъект) Экспорт
	// Установим период отчета по умолчанию
	Попытка
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ДатаКонца) Тогда
			ЭтотОбъект.ДатаКонца = РабочаяДата;
		КонецЕсли;
	Исключение
	КонецПопытки;
	Попытка
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ДатаНачала) Тогда
			ЭтотОбъект.ДатаНачала = НачалоМесяца(ЭтотОбъект.ДатаКонца);
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры // отОтчетПриИнициализацииОбъекта()

// Процедура обработчик изменения вида отчета (остатки/остатки и обороты),
// при использовании отчета на новом макете
//
// Параметры:
//  ОтчетФорма               – Форма             – Ссылка на форму отчета
// 	ВидОтчетаОстаткиИОбороты - Булево			 - Признак вида отчета ОстаткиИОбороты
//
Процедура отОтчетИзменитьВидОтчета(ЭтаФорма, ВидОтчетаОстаткиИОбороты = Истина) Экспорт
	
	ОтчетОбъект = ЭтаФорма.ЭтотОбъект;
	ЭтаФорма.Закрыть();
	
	СтруктураНастроекДляТекущейФормы = Новый Структура;
	отСтруктураСохраняемыхЗначений(ЭтаФорма, СтруктураНастроекДляТекущейФормы);
	СохранитьЗначение((ОтчетОбъект.Метаданные().Имя + "СтруктураНастроек"), СтруктураНастроекДляТекущейФормы);
	
	//параметры новой формы
	Если ТипЗнч(ВидОтчетаОстаткиИОбороты) = Тип("Булево") Тогда
		Попытка РежимНастройки = ОтчетОбъект.РежимНастройки; 
		Исключение РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт;
		КонецПопытки;
		Если РежимНастройки = Перечисления.РежимыНастройкиОтчетов.Эксперт Тогда
			ИмяФормыНастроек = ?(ВидОтчетаОстаткиИОбороты, "НастройкиОстатки",             "НастройкиОстаткиИОбороты");
		Иначе ИмяФормыНастроек = ?(ВидОтчетаОстаткиИОбороты, "НастройкиОстаткиПользователь", "НастройкиОстаткиИОборотыПользователь");
		КонецЕсли;
		
	Иначе ИмяФормыНастроек = ВидОтчетаОстаткиИОбороты;
	КонецЕсли;
	
	ФормаНастройки = ОтчетОбъект.ПолучитьФорму(ИмяФормыНастроек);
	ВосстановитьНастройкиДляТекущейФормы(ОтчетОбъект, ФормаНастройки);
	
	//переоткрываем
	ФормаНастройки.Открыть();
	
КонецПроцедуры	//	отОтчетИзменитьВидОтчета()

// Процедура стандартный обработчик изменения режима настройки(эксперт/стандарт)
//
// Параметры:
//  ЭтаФорма – Форма – Ссылка на форму отчета
//
Процедура отОтчетИзменитьРежим(ЭтаФорма) Экспорт
	
	ЭтаФорма.Закрыть();
	ОтчетОбъект = ЭтаФорма.ЭтотОбъект;
	СтруктураНастроекДляТекущейФормы = Новый Структура;
	отСтруктураСохраняемыхЗначений(ЭтаФорма, СтруктураНастроекДляТекущейФормы);
	СохранитьЗначение((ОтчетОбъект.Метаданные().Имя+"СтруктураНастроек"), СтруктураНастроекДляТекущейФормы);
	
	//параметры текущей формы
	ЭтаФормаЭксперт = (Найти(ОтчетОбъект.ИмяФормыНастроек, "Пользователь") = 0);
	
	//параметры новой формы
	ИмяФормыНастроек = СокрЛП(ОтчетОбъект.ИмяФормыНастроек);
	ИмяФормыНастроекНовое = ?(ЭтаФормаЭксперт, ИмяФормыНастроек + "Пользователь", Лев(ИмяФормыНастроек, СтрДлина(ИмяФормыНастроек) - СтрДлина("Пользователь")));
	ФормаНастройки = ОтчетОбъект.ПолучитьФорму(ИмяФормыНастроекНовое);
	СохранитьЗначение(ОтчетОбъект.Метаданные().Имя, ИмяФормыНастроекНовое);
	ВосстановитьНастройкиДляТекущейФормы(ОтчетОбъект, ФормаНастройки);
	
	//переоткрываем
	ФормаНастройки.Открыть();
	
КонецПроцедуры	//	отОтчетИзменитьРежим()

// Процедура стандартный обработчик предоткрытия формы отчета
//
// Параметры:
//  ЭтаФорма             – Форма        – Ссылка на форму отчета.
//	Отказ                - Булево       - Признак отказа от записи
// 	СтандартнаяОбработка - Булево       - Признак использования стандартной обработки
//  ПраваПользователя    - Неопределено - Права пользователя
//
Процедура отОтчетПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка, ПраваПользователя = Неопределено) Экспорт
	
	ОтчетОбъект = ЭтаФорма.ЭтотОбъект;
	//параметры текущей(открываемой) формы
	ЭтаФормаСтандартная = (Найти(ОтчетОбъект.ИмяФормыНастроек, "Настройки") <> 0);
	ЭтаФормаЭксперт     = (Найти(ОтчетОбъект.ИмяФормыНастроек, "Пользователь") = 0);
	
	//нестандартную форму открываем без разговоров
	Если НЕ ЭтаФормаСтандартная Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПоискаФормы = ?(ЭтаФормаЭксперт,ОтчетОбъект.ИмяФормыНастроек + "Пользователь",ОтчетОбъект.ИмяФормыНастроек);
	ЕстьСтандартнаяФормаПользователь = ОтчетОбъект.Метаданные().Формы.Найти(СтрокаПоискаФормы) <> Неопределено; 
	//проверим право на открытие эксперта
	ПравоЗапретитьЭксперт = обПраво("ЗапретитьРежимЭксперт", ПраваПользователя);
	
	//попытаемся восстановить предыдущую сохраненную форму
	ИмяФормыНастроекСохраненное = ВосстановитьЗначение(ОтчетОбъект.Метаданные().Имя);
	ИмяФормыНастроекСохраненное = ?(ИмяФормыНастроекСохраненное=Неопределено,ОтчетОбъект.ИмяФормыНастроек,ИмяФормыНастроекСохраненное);
	//параметры сохраненной формы
	СохраненнаяФормаЭксперт = (Найти(ИмяФормыНастроекСохраненное, "Пользователь") = 0);
	//присвоим значение формы в случае запрета эксперта
	Если СохраненнаяФормаЭксперт И ПравоЗапретитьЭксперт И ЕстьСтандартнаяФормаПользователь Тогда
		СохранитьЗначение(ОтчетОбъект.Метаданные().Имя, ИмяФормыНастроекСохраненное + "Пользователь");
		ИмяФормыНастроекСохраненное = ВосстановитьЗначение(ОтчетОбъект.Метаданные().Имя);
		СохраненнаяФормаЭксперт = (Найти(ИмяФормыНастроекСохраненное, "Пользователь") = 0);
	КонецЕсли;
	
	//проверяем соответсвие сохраненной формы и новой формы
	Попытка
		Если ИмяФормыНастроекСохраненное <> Неопределено Тогда
			Если СохраненнаяФормаЭксперт <> ЭтаФормаЭксперт Тогда
				Отказ = Истина;
				СтандартнаяОбработка = Ложь;
				ФормаНастройкиНовая = ОтчетОбъект.ПолучитьФорму(?(Найти(ОтчетОбъект.ИмяФормыНастроек, "Пользователь") = 0, ОтчетОбъект.ИмяФормыНастроек + "Пользователь", 
				Лев(ОтчетОбъект.ИмяФормыНастроек, СтрДлина(ОтчетОбъект.ИмяФормыНастроек) - 12)));
				ВосстановитьНастройкиДляТекущейФормы(ОтчетОбъект, ФормаНастройкиНовая);
				ФормаНастройкиНовая.Открыть();
			КонецЕсли;
		КонецЕсли;
	Исключение
		Отказ = Ложь;
		СтандартнаяОбработка = Истина;
	КонецПопытки;
	
	//проверяем на права открытия эксперта
	Если Найти(ОтчетОбъект.ИмяФормыНастроек, "Пользователь") = 0 И ЕстьСтандартнаяФормаПользователь Тогда
		Отказ = ПравоЗапретитьЭксперт;
		Если Отказ Тогда
			СтандартнаяОбработка = Ложь;
			ФормаНастройкиНовая = ОтчетОбъект.ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек + "Пользователь");
			ВосстановитьНастройкиДляТекущейФормы(ОтчетОбъект, ФормаНастройкиНовая);
			ФормаНастройкиНовая.Открыть();
		КонецЕсли;
	КонецЕсли;
	
	//красим кнопку Эксперта, если тек. форма пользовательская и эксперт запрещен
	Если НЕ ЭтаФормаЭксперт И ПравоЗапретитьЭксперт Тогда
		Попытка
			ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.ИзменитьРежим.Доступность = НЕ обПраво("ЗапретитьРежимЭксперт", ПраваПользователя);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры // отОтчетИзменитьВидОтчета()

// Процедура стандартный обработчик открытия формы отчета
//
// Параметры:
//  ЭтаФорма - Форма        – Ссылка на форму отчета.
//  ПраваПользователя  - Неопределено - Права пользователя
//
Процедура отОтчетФормаПриОткрытии(ЭтаФорма, ПраваПользователя = Неопределено) Экспорт
	
	Если Найти(ЭтаФорма.Метаданные().Имя, "Пользователь") > 0 Тогда
		Попытка
			ДоступностьЭксперт = НЕ обПраво("ЗапретитьРежимЭксперт", ПраваПользователя);
			ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.ИзменитьРежим.Доступность = ДоступностьЭксперт;
		Исключение КонецПопытки;
	КонецЕсли;
	
	Попытка
		спВыбора = ЭтаФорма.ЭлементыФормы.ИзмеренияСтроки.Колонки.ТипИзмерения.ЭлементУправления.СписокВыбора;
		спВыбора.Добавить("Элементы", "Элементы");
		спВыбора.Добавить("Иерархия", "Иерархия");
		спВыбора.Добавить("Только иерархия", "Только иерархия");
	Исключение КонецПопытки;
	// Зарегистрируем в диспетчере форм ПанелиАРМ                   
	МенеджерОбъекта = "Отчеты."+ЭтаФорма.ЭтотОбъект.Метаданные().Имя+".ПолучитьФорму("""+ЭтаФорма.ЭтотОбъект.ИмяФормыНастроек+""")";
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().ЗарегистрироватьФорму(ЭтаФорма, МенеджерОбъекта, "Отчеты");
	
КонецПроцедуры	//	отОтчетФормаПриОткрытии()

// Процедура стандартный обработчик открытия общей формы "Отчет"
//
// Параметры:
//  ЭтаФорма – Форма – Ссылка на форму отчета.
//
Процедура отОтчетПриОткрытии(ЭтаФорма) Экспорт
	
	ОтчетОбъект = ЭтаФорма.ОтчетОбъект;
	
	ЭтаФорма.ДатаНачала = ОтчетОбъект.ДатаНачала;
	ЭтаФорма.ДатаКонца = ОтчетОбъект.ДатаКонца;
	
	ЭтаФорма.ЭлементыФормы.тПериодС.Видимость=НЕ(ОтчетОбъект.ВидОтчета=Перечисления.ВидыОтчетов.Остатки);
	ЭтаФорма.ЭлементыФормы.ДатаНачала.Видимость=НЕ(ОтчетОбъект.ВидОтчета=Перечисления.ВидыОтчетов.Остатки);
	ЭтаФорма.ЭлементыФормы.кнВыборПериода.Видимость=НЕ(ОтчетОбъект.ВидОтчета=Перечисления.ВидыОтчетов.Остатки);
	ЭтаФорма.ЭлементыФормы.тПо.Заголовок=?(ОтчетОбъект.ВидОтчета=Перечисления.ВидыОтчетов.Остатки, "На дату", "по");
	
	// заполним варианты оформления сводной таблицы
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.БезОформления,"БезОформления");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Камень,"Камень");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Классика,"Классика");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Классика2,"Классика2");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Классика3,"Классика3");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Весна,"Весна");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Лето,"Лето");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Осень,"Осень");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Зима,"Зима");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Асфальт,"Асфальт");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Медь,"Медь");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Бронза,"Бронза");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Платина,"Платина");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Серебро,"Серебро");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Бирюза,"Бирюза");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Песок,"Песок");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Трава,"Трава");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Лед,"Лед");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Апельсин,"Апельсин");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Текстиль,"Текстиль");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Дерево,"Дерево");
	ЭтаФорма.ВариантыОформленияСводнойТаблицы.Добавить(СтандартноеОформление.Интерфейс,"Интерфейс");
	
	// заполним виды диаграммы
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.Круговая,"Круговая");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.КруговаяОбъемная,"Круговая объемная");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ГистограммаСНакоплением,"Гистограмма с накоплением");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ГистограммаСНакоплениемОбъемная,"Гистограмма с накоплением объемная");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.График,"График");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ГрафикСОбластямиИНакоплением,"График с областями и накоплением");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.Гистограмма,"Гистограмма");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ГистограммаОбъемная,"Гистограмма объемная");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ГистограммаГоризонтальная,"Гистограмма горизонтальная");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ГистограммаГоризонтальнаяОбъемная,"Гистограмма горизонтальная объемная");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.Изометрическая,"Изометрическая");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ИзометрическаяНепрерывная,"Изометрическая непрерывная");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ИзометрическаяЛента,"Изометрическая лента");
	ЭтаФорма.ВидыДиаграммы.Добавить(ТипДиаграммы.ИзометрическаяПирамида,"Изометрическая пирамида");
	//.....
	ЭтаФорма.ОтчетОбъект.РежимВыводаОтчета=Неопределено;
КонецПроцедуры // отОтчетФормаПриОткрытии()

// Процедура стандартный обработчик закрытия формы отчета
//
// Параметры:
//  ЭтаФорма – Форма – Ссылка на форму отчета.
//
Процедура отОтчетПриЗакрытии(ЭтаФорма) Экспорт
	ОтчетОбъект = ЭтаФорма.ЭтотОбъект;
	//сохраним имя формы настройки
	СохранитьЗначение(ОтчетОбъект.Метаданные().Имя, СокрЛП(ОтчетОбъект.ИмяФормыНастроек));
	// Удалим из диспечера форм ПанелиАРМ
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().УдалитьИзДереваФорм(ЭтаФорма);
КонецПроцедуры // отОтчетПриЗакрытии()

// Процедура стандартный обработчик предзакрытия формы отчета
//
// Параметры:
//  ЭтаФорма             – Форма        – Ссылка на форму отчета.
//	Отказ                - Булево       - Признак отказа от записи
// 	СтандартнаяОбработка - Булево       - Признак использования стандартной обработки
//
Процедура отОтчетПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
// зарезервировано на будущее	
КонецПроцедуры // отОтчетПередЗакрытием()

// Процедура обработчик нажатия кнопки изменения отчета 
//
// Параметры:
//  ЭтаФорма       – Форма – Ссылка на форму отчета.
//	ВыбОтчет       - Отчет - Выбранный отчет
//	ВыбФорма       - Форма - Выбранная форма
//	ФормаНастройки - Форма - Форма настройки
//
Процедура отОтчетОткрытьНовыйОтчет(ЭтаФорма, ВыбОтчет="", ВыбФорма="", ФормаНастройки=Неопределено) Экспорт
	
	Если обЗначениеНеЗаполнено(ФормаНастройки) Тогда
		Если обЗначениеНеЗаполнено(ВыбОтчет) Тогда Возврат; Конецесли;
		ФормаНастройки = Отчеты[ВыбОтчет].ПолучитьФорму(ВыбФорма,);
	КонецЕсли;
	
	ФормаНастройки.ЗаполнитьНачальныеНастройки(ФормаНастройки.ИмяМакета);
	
	МетаданныеФормыНастройки = ФормаНастройки.Метаданные();
	МетаданныеОтчетаОбъекта = ЭтаФорма.Метаданные();
	РеквизитыНовойФормы = МетаданныеФормыНастройки.Реквизиты;
	
	Для Каждого ТекРеквизит Из МетаданныеОтчетаОбъекта.Реквизиты Цикл
		Если Найти("@ПостроительОтчета@ВидОтчета@ИмяРегистра@ИмяФормыНастроек@","@"+ТекРеквизит.Имя+"@")>0 Тогда Продолжить; КонецЕсли;
		Если РеквизитыНовойФормы.Найти(ТекРеквизит.Имя) <> Неопределено Тогда
			ФормаНастройки[ТекРеквизит.Имя] = ЭтаФорма[ТекРеквизит.Имя];
		КонецЕсли;
	Конеццикла;
	
	Если ФормаНастройки.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.СводнаяТаблица И (ФормаНастройки.Метаданные().Реквизиты.Найти("ВариантОформленияСводнойТаблицы") = Неопределено) Тогда
		ФормаНастройки.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;	
	ИначеЕсли ФормаНастройки.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.Диаграмма И (ФормаНастройки.Метаданные().Реквизиты.Найти("ВыбТипДиаграммы") = Неопределено) тогда  
		ФормаНастройки.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
	ИначеЕсли ФормаНастройки.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.СводнаяДиаграмма И (ФормаНастройки.Метаданные().Реквизиты.Найти("ВыбТипСводнойДиаграммы") = Неопределено) тогда
		ФормаНастройки.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
	КонецЕсли;
	
	Попытка
		СохраненнаяСтруктураСвязиСвойств = ЭтаФорма.СтруктураСвязиСвойств;
	Исключение
		СохраненнаяСтруктураСвязиСвойств = Новый Структура;
	КонецПопытки;
	
	Если МетаданныеФормыНастройки.ТабличныеЧасти.Найти("ИзмеренияСтроки")<>Неопределено И МетаданныеОтчетаОбъекта.ТабличныеЧасти.Найти("ИзмеренияСтроки")<>Неопределено Тогда 
		
		ФормаНастройки.ИзмеренияСтроки.Очистить();
		Для Каждого ТекИзмерение Из ЭтаФорма.ИзмеренияСтроки Цикл
			ИмяИзмерения = СтрЗаменить(ТекИзмерение.ПутьКДанным,".","");
			ИмяИзмерения = ?(СохраненнаяСтруктураСвязиСвойств.Свойство(ИмяИзмерения), СохраненнаяСтруктураСвязиСвойств[ИмяИзмерения].ПутьКДаннымИзмерения, ТекИзмерение.ПутьКДанным);
			Если ФормаНастройки.ДеревоДоступныхПолей.Строки.Найти(ИмяИзмерения, "ПутьКДанным", Истина) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НовоеИзмерение = ФормаНастройки.ИзмеренияСтроки.Добавить();
			НовоеИзмерение.Использование=ТекИзмерение.Использование;
			НовоеИзмерение.Представление=ТекИзмерение.Представление;
			НовоеИзмерение.ПутьКДанным=ТекИзмерение.ПутьКДанным;
			НовоеИзмерение.ТипИзмерения=ТекИзмерение.ТипИзмерения;
		КонецЦикла;
		
	КонецЕсли;
	
	Если МетаданныеФормыНастройки.ТабличныеЧасти.Найти("ИзмеренияКолонки")<>Неопределено И МетаданныеОтчетаОбъекта.ТабличныеЧасти.Найти("ИзмеренияКолонки")<>Неопределено Тогда 
		
		ФормаНастройки.ИзмеренияКолонки.Очистить();
		Для Каждого ТекИзмерение Из ЭтаФорма.ИзмеренияКолонки Цикл
			ИмяИзмерения = СтрЗаменить(ТекИзмерение.ПутьКДанным,".","");
			ИмяИзмерения = ?(СохраненнаяСтруктураСвязиСвойств.Свойство(ИмяИзмерения), СохраненнаяСтруктураСвязиСвойств[ИмяИзмерения].ПутьКДаннымИзмерения, ТекИзмерение.ПутьКДанным);
			Если ФормаНастройки.ДеревоДоступныхПолей.Строки.Найти(ИмяИзмерения, "ПутьКДанным", Истина) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НовоеИзмерение = ФормаНастройки.ИзмеренияКолонки.Добавить();
			НовоеИзмерение.Использование=ТекИзмерение.Использование;
			НовоеИзмерение.Представление=ТекИзмерение.Представление;
			НовоеИзмерение.ПутьКДанным=ТекИзмерение.ПутьКДанным;
			НовоеИзмерение.ТипИзмерения=ТекИзмерение.ТипИзмерения;
		КонецЦикла;
		
	КонецЕсли;
	
	Если МетаданныеФормыНастройки.ТабличныеЧасти.Найти("Показатели")<>Неопределено И МетаданныеОтчетаОбъекта.ТабличныеЧасти.Найти("Показатели")<>Неопределено Тогда 
		
		Для Каждого ТекПоказатель Из ЭтаФорма.Показатели Цикл
			НайденныйПоказатель = ФормаНастройки.Показатели.Найти(ТекПоказатель.Имя, "Имя");
			Если НайденныйПоказатель = Неопределено Тогда Продолжить; КонецЕсли;
			НайденныйПоказатель.Использование=ТекПоказатель.Использование;
		КонецЦикла;
		
	КонецЕсли;
	
	ФормаНастройки.ПараметрыИзмеренийСтрок = ЭтаФорма.ПараметрыИзмеренийСтрок;
	
	отЗаполнитьНастройкиСвойствИОтборов(ФормаНастройки, , ЭтаФорма);
	
	Если ВыбФорма = "НастройкиДвиженияСОстатками" Тогда
		к = 0;
		Пока к <= ФормаНастройки.ИзмеренияСтроки.Количество() - 1 Цикл
			ТекИзмерение = ФормаНастройки.ИзмеренияСтроки.Получить(к);
			Попытка
				ФлОтсутствуетВОстатках = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекИзмерение.ПутьКДанным, ЭтаФорма.ПараметрыИспользованияПолейИПоказателей).Использование.ОтсутствуетВОстатках;
			Исключение
				ФлОтсутствуетВОстатках = Ложь;
			КонецПопытки;
			Если ТекИзмерение.ПутьКДанным = "ПериодДень" или
				ТекИзмерение.ПутьКДанным = "ПериодНеделя" или
				ТекИзмерение.ПутьКДанным = "ПериодМесяц" или
				ТекИзмерение.ПутьКДанным = "ПериодКвартал" или
				ТекИзмерение.ПутьКДанным = "ПериодГод" или
				(Найти(ТекИзмерение.ПутьКДанным, "ПериодРегистратор") = 1 и
				ТекИзмерение.ПутьКДанным <> "ПериодРегистратор.Дата") или
				ФлОтсутствуетВОстатках Тогда
				ФормаНастройки.ИзмеренияСтроки.Удалить(ТекИзмерение);
			Иначе
				к = к + 1;
			КонецЕсли;
		КонецЦикла;	
		
		ТекИзмерение = ФормаНастройки.ИзмеренияСтроки.Добавить();
		ТекИзмерение.ПутьКДанным   = "ПериодРегистратор";
		ТекИзмерение.ТипИзмерения  = ТипИзмеренияПостроителяОтчета.Элементы;
		ТекИзмерение.Представление = "Документ движения";
		ТекИзмерение.Использование = Истина;
		ТекИзмерение.Фиксированное = Истина;
	КонецЕсли;
	отФорматированиеПостроителяОтчета(ФормаНастройки);
	
	ФормаНастройки.ЗаполнитьНастройки = Ложь;
	ФормаНастройки.ВосстанавливатьЗначенияПриОткрытии = Ложь;
	ФормаНастройки.Открыть();
	
	Если ТипЗнч(ЭтаФорма)=Тип("Форма") Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры	//	отОтчетОткрытьНовыйОтчет()


// Открывает форму выбора всех полей отчета (прикрепляемое дерево).
// Если форма уже открыта, то закрывает её (меняя состояние триггерной кнопки)
//
// Параметры:
//  ЭтаФорма           – Форма  – Ссылка на форму отчета.
//	Кнопка             - Кнопка - Кнопка на форме
//	МассивДоступности - Массив - Массив доступностей
//
Процедура отДействияФормыПоказатьВсеПоля(ЭтаФорма, Кнопка, МассивДоступности=Неопределено) Экспорт
	ФормаПоляВыбораОтчета = ПолучитьОбщуюФорму("ПоляВыбораОтчета",ЭтаФорма);
	Если ФормаПоляВыбораОтчета.Открыта() Тогда
		ФормаПоляВыбораОтчета.Закрыть();
		Кнопка.Пометка = Ложь;
	Иначе
		Если ТипЗнч(МассивДоступности) = Тип("Массив") Тогда
			Инд=0;
			Пока Инд < МассивДоступности.Количество() Цикл
				ТекКнопка = ФормаПоляВыбораОтчета.ЭлементыФормы.КоманднаяПанель.Кнопки.Найти(МассивДоступности.Получить(Инд));
				Если ТекКнопка=Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ТекКнопка.Доступность=Ложь;
				Инд=Инд+1;
			КонецЦикла;
		КонецЕсли;
		
		ФормаПоляВыбораОтчета.Открыть();
		Кнопка.Пометка = Истина;
	КонецЕсли;
КонецПроцедуры // отОтчетОткрытьНовыйОтчет()

// открывает общую форму вывода для формирования отчета
// с использованием переданного объекта отчета из формы настройки
// сама форма настройки при этом закрывается
//
// Параметры:
//  ФормаНастройки – Форма  – Форма настройки
//	Кнопка         - Кнопка - Кнопка на форме
//
Процедура отДействияФормыСформировать(ФормаНастройки, Кнопка) Экспорт
	
	Если Не отКорректностьЗаполненияПередФормированием(ФормаНастройки.ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецФормы = ФормаНастройки.ВладелецФормы;
	// Общая форма настройки может быть создана новая, или получена
	// как уже существующая, в случае повторной настройки из общей формы.
	ФормаОтчета = ?(обЗначениеНеЗаполнено(ВладелецФормы), ПолучитьОбщуюФорму("Отчет",,ФормаНастройки), ВладелецФормы);
	
	ОтчетОбъект = ФормаНастройки.ЭтотОбъект;
	Попытка	
		Если ОтчетОбъект.ВидОтчета = Перечисления.ВидыОтчетов.Обороты Тогда
			Для каждого Функ из ОтчетОбъект.Функции Цикл
				Функ.Использование = Истина;
			КонецЦикла;
		КонецЕсли; 
	Исключение
	КонецПопытки;
	
	ФормаОтчета.ОтчетОбъект = ОтчетОбъект;
	
	// параметры переданы - форму настройки можно закрывать
	Если ФормаНастройки.Открыта() Тогда ФормаНастройки.Закрыть(); КонецЕсли;
	
	// при открытии общей формы отчета установим признак автоформирования отчета
	ФормаОтчета.Автоформирование = Ложь;
	ФормаОтчета.Открыть();
	
КонецПроцедуры // отДействияФормыСформировать()

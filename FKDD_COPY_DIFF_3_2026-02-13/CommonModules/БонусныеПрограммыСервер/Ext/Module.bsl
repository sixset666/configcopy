
// Получает текущее состояние бонусной программы
Функция БонуснаяПрограммаАктивна(БонуснаяПрограмма, Дата = Неопределено) Экспорт
	
	Если БонуснаяПрограмма.Пустая() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АктивныеБонусныеПрограммыСрезПоследних.Активна
	|ИЗ
	|	РегистрСведений.АктивныеБонусныеПрограммы.СрезПоследних(&Момент, БонуснаяПрограмма = &БонуснаяПрограмма) КАК АктивныеБонусныеПрограммыСрезПоследних";
	Запрос.УстановитьПараметр("Момент", ?(Дата <> Неопределено, Дата, ТекущаяДата()));
	Запрос.УстановитьПараметр("БонуснаяПрограмма", БонуснаяПрограмма);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать(); Выборка.Следующий();
	
	Возврат Выборка.Активна;
	
КонецФункции

// Устонавиливает активность программы
Процедура УстановитьАктивностьБонуснойПрограммы(БонуснаяПрограмма, Активность) Экспорт
	
	МенеджерЗаписей = РегистрыСведений.АктивныеБонусныеПрограммы.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписей.Активна = Активность;
	МенеджерЗаписей.Период  = ТекущаяДата();
	МенеджерЗаписей.БонуснаяПрограмма = БонуснаяПрограмма;
	
	Попытка
		МенеджерЗаписей.Записать(Истина);
	Исключение КонецПопытки;
	
КонецПроцедуры

#Область ОбщиеПроцедурыИФункции

Процедура РаспределитьСуммуПоТаблице(Таблица, СуммаРаспределения, КолонкаРаспределения, ВМинус = Истина) Экспорт
	
	СуммаВКолонке   = Таблица.Итог(КолонкаРаспределения);
	
	Если СуммаВКолонке = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СуммаЗаЕдиницу = СуммаРаспределения/СуммаВКолонке;
	
	ОсталосьРаспределить = СуммаРаспределения;
	
	СтрокаСМаксимальнойСуммой = Таблица[0]; СуммаСтрокиСМаксимальнойСуммой = Таблица[0][КолонкаРаспределения];
	Для Каждого Строка Из Таблица Цикл
		Если ОсталосьРаспределить = 0 Тогда Продолжить; КонецЕсли;
		
		Если Строка[КолонкаРаспределения] > СтрокаСМаксимальнойСуммой[КолонкаРаспределения] Тогда
			СтрокаСМаксимальнойСуммой = Строка;
			СуммаСтрокиСМаксимальнойСуммой = Строка[КолонкаРаспределения];
		КонецЕсли;
		
		Строка.РаспределенноеЗначение = Окр(СуммаЗаЕдиницу*Строка[КолонкаРаспределения], 2, РежимОкругления.Окр15как20);
		Строка[КолонкаРаспределения]  = Строка[КолонкаРаспределения] + ?(ВМинус, -Строка.РаспределенноеЗначение, Строка.РаспределенноеЗначение);
		
		ОсталосьРаспределить = ОсталосьРаспределить - Строка.РаспределенноеЗначение;
	КонецЦикла;
	
	Если ОсталосьРаспределить <> 0 Тогда
		СтрокаСМаксимальнойСуммой.РаспределенноеЗначение = СтрокаСМаксимальнойСуммой.РаспределенноеЗначение + ОсталосьРаспределить;
		СтрокаСМаксимальнойСуммой[КолонкаРаспределения]  = СтрокаСМаксимальнойСуммой[КолонкаРаспределения] + ?(ВМинус, -ОсталосьРаспределить, ОсталосьРаспределить);
	КонецЕсли;
	
КонецПроцедуры

Функция БаллыВВалюту(КоличествоБаллов, БонуснаяПрограмма, Валюта, Дата) Экспорт
	
	СуммаВВалютеБонуснойПрограммы = КоличествоБаллов*БонуснаяПрограмма.КратностьБонусов;
	Возврат обПересчет(СуммаВВалютеБонуснойПрограммы, БонуснаяПрограмма.ВалютаБонуса, Дата, Валюта, Дата, РежимОкругления.Окр15как20);
	
КонецФункции

// истина - есть остатки, ложь нет
//
Функция ОстаткиБалловБонуснойПрограммы(БонуснаяПрограмма) Экспорт
	
	ТекстЗапрос =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БонусныеБаллыОстатки.КоличествоОстаток,
	|	БонусныеБаллыОстатки.КоличествоКНачислениюОстаток
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.Остатки() КАК БонусныеБаллыОстатки
	|ГДЕ
	|	БонусныеБаллыОстатки.БонуснаяКарта.БонуснаяПрограмма = &БонуснаяПрограмма";
	
	Запрос = Новый Запрос(ТекстЗапрос);
	Запрос.УстановитьПараметр("БонуснаяПрограмма", БонуснаяПрограмма);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЗаблокированаКарточка(Карточка) Экспорт
	// Прочитаем штрих-код карточки
	ОбработкаОбъектШтрихКоды = Обработки.ШтрихКоды.Создать();
	ОбработкаОбъектШтрихКоды.Объект = Карточка;
	ОбработкаОбъектШтрихКоды.ПрочитатьЗаполнитьШтрихКоды();
	Если ОбработкаОбъектШтрихКоды.Состав.Количество()>0 Тогда
		Возврат ОбработкаОбъектШтрихКоды.Состав[0].Запрещен;
	Иначе
		Возврат Ложь;
	КонецЕсли; 
КонецФункции

#КонецОбласти

#Область РасчетБаллов

Процедура РасчитатьБонусныеБаллыКНачислению(Объект, БонуснаяПрограмма = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(БонуснаяПрограмма) Тогда
		Попытка
			БонуснаяПрограмма = Объект.Карточка.БонуснаяПрограмма;
		Исключение
		КонецПопытки;
		
		Если НЕ ЗначениеЗаполнено(БонуснаяПрограмма) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ БонуснаяПрограммаАктивна(БонуснаяПрограмма, Объект.Дата) Тогда
		Объект.КоличествоКНачислению = 0;
		Возврат;
	КонецЕсли;
	
	Если БонуснаяПрограмма.ЗапретНачисленияПриОплатеБонусами И Объект.КоличествоКСписанию <> 0 Тогда
		Объект.КоличествоКНачислению = 0;
		Возврат;
	КонецЕсли;
	
	// заполнить таблицу авторабот и товаров
	ТаблицаТоваров = ИнициализироватьТаблицуНоменклатур(Объект, БонуснаяПрограмма.УчитыватьОплатуБонусамиПриНачислении);
	
	// получим процент начисления для строки
	Для Каждого Строка Из ТаблицаТоваров Цикл
		ПолучитьПроцентНачисленияДляСтроки(Строка, БонуснаяПрограмма);
	КонецЦикла;
	
	ТаблицаТоваров.Свернуть("Процент", "Сумма");
	
	ВалютаДокумента = Объект.ВалютаДокумента;
	КурсДокумента   = Объект.КурсДокумента;
	
	ВалютаПрограммы = БонуснаяПрограмма.ВалютаБонуса;
	СтруктураКурса  = обКурсДляВалюты(ВалютаПрограммы, Объект.Дата);
	КурсПрограммы   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	СуммаБонусов = 0;
	Для Каждого Строка Из ТаблицаТоваров Цикл
		СуммаБонусов = СуммаБонусов + Строка.Сумма/100*Строка.Процент;
	КонецЦикла;
	
	СуммаБонусовВВал = обПересчет(СуммаБонусов, ВалютаДокумента, КурсДокумента, ВалютаПрограммы, КурсПрограммы, РежимОкругления.Окр15как20);
	КоличествоБаллов = ?(БонуснаяПрограмма.КратностьБонусов = 0, СуммаБонусовВВал, СуммаБонусовВВал/БонуснаяПрограмма.КратностьБонусов);
	
	Объект.КоличествоКНачислению = ОкруглитьКоличествоБаллов(КоличествоБаллов, БонуснаяПрограмма);
	Объект.ОбработкаРеквизита("КоличествоКНачислению");
	
КонецПроцедуры

Функция МаксимальноеКоличетсвоБоллов(Объект, БонуснаяПрограмма = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(БонуснаяПрограмма) Тогда
		Попытка
			БонуснаяПрограмма = Объект.Карточка.БонуснаяПрограмма;
		Исключение
		КонецПопытки;
		
		Если НЕ ЗначениеЗаполнено(БонуснаяПрограмма) Тогда
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	// заполнить таблицу авторабот и товаров
	ТаблицаТоваров = ИнициализироватьТаблицуНоменклатур(Объект, Ложь);
	
	// получим процент начисления для строки
	Для Каждого Строка Из ТаблицаТоваров Цикл
		ПолучитьПроцентНачисленияДляСтроки(Строка, БонуснаяПрограмма, Ложь);
	КонецЦикла;
	
	ТаблицаТоваров.Свернуть("Процент", "Сумма");
	
	ВалютаДокумента = Объект.ВалютаДокумента;
	КурсДокумента   = Объект.КурсДокумента;
	
	ВалютаПрограммы = БонуснаяПрограмма.ВалютаБонуса;
	СтруктураКурса  = обКурсДляВалюты(ВалютаПрограммы, Объект.Дата);
	КурсПрограммы   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	СуммаБонусов = 0;
	Для Каждого Строка Из ТаблицаТоваров Цикл
		СуммаБонусов = СуммаБонусов + Строка.Сумма/100*Строка.Процент;
	КонецЦикла;
	
	СуммаБонусовВВал = обПересчет(СуммаБонусов, ВалютаДокумента, КурсДокумента, ВалютаПрограммы, КурсПрограммы, РежимОкругления.Окр15как20);
	
	КоличествоБаллов = ?(БонуснаяПрограмма.КратностьБонусов = 0, СуммаБонусовВВал, СуммаБонусовВВал/БонуснаяПрограмма.КратностьБонусов);
	
	Возврат ОкруглитьКоличествоБаллов(КоличествоБаллов, БонуснаяПрограмма);
	
	//СуммаДокумента = Объект.Товары.Итог("СуммаВсего") + Объект.Товары.Итог("СуммаСкидкиБонусами");
	//Если обЭтотТип(Объект, "ДокументОбъект.Заказнаряд") Тогда
	//	СуммаДокумента = СуммаДокумента + Объект.Работы.Итог("СуммаВсего") + Объект.Работы.Итог("СуммаСкидкиБонусами");
	//КонецЕсли;
	//СуммаОплатыБонусами = БонуснаяПрограмма.МаксимальныйПроцентОплатыБонусами*СуммаДокумента/100;
	//СуммаВВалютеБонусов = обПересчет(СуммаОплатыБонусами, Объект.ВалютаДокумента, Объект.Дата, БонуснаяПрограмма.ВалютаБонуса, Объект.Дата);
	//
	//Возврат Цел(?(БонуснаяПрограмма.КратностьБонусов = 0, СуммаВВалютеБонусов, СуммаВВалютеБонусов/БонуснаяПрограмма.КратностьБонусов));
	
	Возврат 0;
	
КонецФункции

Функция КоличествоБалловВСуммеДокумента(Объект, БонуснаяПрограмма = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(БонуснаяПрограмма) Тогда
		Попытка
			БонуснаяПрограмма = Объект.Карточка.БонуснаяПрограмма;
		Исключение
		КонецПопытки;
		
		Если НЕ ЗначениеЗаполнено(БонуснаяПрограмма) Тогда
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	СуммаДокумента = Объект.Товары.Итог("СуммаВсего") + Объект.Товары.Итог("СуммаСкидкиБонусами");
	Если обЭтотТип(Объект, "ДокументОбъект.Заказнаряд") Тогда
		СуммаДокумента = СуммаДокумента + Объект.Работы.Итог("СуммаВсего") + Объект.Работы.Итог("СуммаСкидкиБонусами");
	КонецЕсли;
	
	СуммаВВалютеБонусов = обПересчет(СуммаДокумента, Объект.ВалютаДокумента, Объект.Дата, БонуснаяПрограмма.ВалютаБонуса, Объект.Дата);
	
	КоличествоБаллов = ?(БонуснаяПрограмма.КратностьБонусов = 0, СуммаВВалютеБонусов, СуммаВВалютеБонусов/БонуснаяПрограмма.КратностьБонусов);
	
	Возврат ОкруглитьКоличествоБаллов(КоличествоБаллов, БонуснаяПрограмма);
	
КонецФункции

#КонецОбласти

#Область ВнутренниеСлужебныеПроцедурыИФункции

Функция ИнициализироватьТаблицуНоменклатур(Объект, УчитыватьОплатуБонусамиПриНачислении)
	
	// описания типов
	ТипыНоменклатуры = Новый Массив;
	ТипыНоменклатуры.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ТипыНоменклатуры.Добавить(Тип("СправочникСсылка.Автоработы"));
	
	// получим таблицу авторабот с родителями
	ТаблицаНоменклатур = Новый ТаблицаЗначений;
	ТаблицаНоменклатур.Колонки.Добавить("Номенклатура"    , Новый ОписаниеТипов(ТипыНоменклатуры));
	ТаблицаНоменклатур.Колонки.Добавить("ТипНоменклатуры" , Новый ОписаниеТипов("СправочникСсылка.ТипыНоменклатуры"));
	ТаблицаНоменклатур.Колонки.Добавить("Сумма"           , Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15,2)));
	ТаблицаНоменклатур.Колонки.Добавить("Процент"         , Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15,2)));
	
	
	
	Для Каждого Строка Из Объект.Товары Цикл
		НоваяСтрока = ТаблицаНоменклатур.Добавить();
		НоваяСтрока.Номенклатура    = Строка.Номенклатура;
		НоваяСтрока.ТипНоменклатуры = Строка.Номенклатура.ТипНоменклатуры;
		НоваяСтрока.Сумма           = Строка.СуммаВсего + ?(УчитыватьОплатуБонусамиПриНачислении, 0, Строка.СуммаСкидкиБонусами);
		НоваяСтрока.Процент         = -1;
	КонецЦикла;
	
	Если обЭтотТип(Объект, "ДокументОбъект.ЗаказНаряд") Тогда
		Для Каждого Строка Из Объект.Работы Цикл
			НоваяСтрока = ТаблицаНоменклатур.Добавить();
			НоваяСтрока.Номенклатура    = Строка.Работа;
			НоваяСтрока.ТипНоменклатуры = Строка.Работа.Номенклатура.ТипНоменклатуры;
			НоваяСтрока.Сумма           = Строка.СуммаВсего + ?(УчитыватьОплатуБонусамиПриНачислении, 0, Строка.СуммаСкидкиБонусами);
			НоваяСтрока.Процент         = -1;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаНоменклатур;
	
КонецФункции

Процедура ПолучитьПроцентНачисленияДляСтроки(Строка, БонуснаяПрограмма, Начисление = Истина)
	
	// поиск по родителю
	Родитель = Строка.Номенклатура.Родитель;
	
	ПроцентыНачислений = ?(Начисление, БонуснаяПрограмма.ПравилаНачисленияБонусов, БонуснаяПрограмма.ПравилаСписанияБонусов);
	
	Пока НЕ Родитель.Пустая() Цикл
		НайденнаяСтрока = ПроцентыНачислений.Найти(Родитель, "Группа");
		Если НайденнаяСтрока <> Неопределено Тогда
			Строка.Процент = НайденнаяСтрока.Процент;
			Прервать;
		КонецЕсли;
		Родитель = Родитель.Родитель;
	КонецЦикла;
	
	Если Строка.Процент <> -1 Тогда
		Возврат;
	КонецЕсли;
	
	НайденнаяСтрока = ПроцентыНачислений.Найти(Строка.ТипНоменклатуры, "Группа");
	Если НайденнаяСтрока <> Неопределено Тогда
		Строка.Процент = НайденнаяСтрока.Процент;
	КонецЕсли;
	
	Если Строка.Процент <> -1 Тогда
		Возврат;
	КонецЕсли;
	
	Строка.Процент = ?(Начисление, БонуснаяПрограмма.ПроцентНачисленияБонусов, БонуснаяПрограмма.МаксимальныйПроцентОплатыБонусами);
	
КонецПроцедуры

Функция ОкруглитьКоличествоБаллов(КоличествоБалов, БонуснаяПрограмма) Экспорт
	
	КоличествоБалловОкругленная = КоличествоБалов;
	
	Если БонуснаяПрограмма.ОкруглятьПоАрифметическимПравилам Тогда
		КоличествоБалловОкругленная = Окр(КоличествоБалов, БонуснаяПрограмма.Точность, РежимОкругления.Окр15как10);
	Иначе
		КоличествоБалловОкругленная = Окр(КоличествоБалов, БонуснаяПрограмма.Точность, РежимОкругления.Окр15как20);
	КонецЕсли;
	
	Возврат КоличествоБалловОкругленная;
	
КонецФункции // ОкруглитьКоличествоБаллов()

#КонецОбласти
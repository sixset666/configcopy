
/////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ИНТЕГРАЦИИ С БАЗОЙ ДАННЫХ

// Функция выполняет поиск телефонного номера в регистре сведений КонтактнаяИнформация
//  и возвращает результат запроса.
// Внутренний - Булево, флаг определяет какой передан телефон, внутренний или внешний,
//  в зависимости от него будем искать внутренний или внешний соответственно
Функция CRMНайтиТелефонВКонтактнойИнформации(ВходящееИсходящееСобытие, ТелефонныйНомер, КоличествоХранимыхЦифрТелефона) Экспорт
	Если ТипЗнч(ТелефонныйНомер) = Тип("Структура") Тогда
		НомерТелефона = ТелефонныйНомер.НомерТелефона;
	Иначе
		НомерТелефона = ТелефонныйНомер;
	КонецЕсли;
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("НомерТелефона", CRMПреобразоватьНомерДляСохранения(НомерТелефона, КоличествоХранимыхЦифрТелефона));
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Объект,
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.ЗначениеПоУмолчанию
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И КонтактнаяИнформация.CRM_ПолеХраненияНомера = &НомерТелефона";
	Возврат Запрос.Выполнить();
КонецФункции

// Функция проверяет разрешено ли текущему пользователю работать с СофтФоном
// Параметры
//  ВыдаватьПредупреждение - Булево, Определяет выдавать сообщение пользователю, что работа с 
//                           СофтФоном не разрешена.
Функция CRMРаботаССофтФономРазрешена(ВыдаватьПредупреждение = Ложь,
									 ИспользоватьСофтФонТекущийПользователь,
									 ТекущийПользователь) Экспорт
	
	Если ВыдаватьПредупреждение И Не ИспользоватьСофтФонТекущийПользователь Тогда
		Предупреждение("Внимание!!! Работа СофтФона для пользователя"""+СокрЛП(ТекущийПользователь)+""" не разрешена."+Символы.ПС+
					   "Необходимо открыть настройки пользователя и установить флажок ""СофтФон управления звонками -> Использовать СофтФон""",
					   30, "Внимание!!!");
	КонецЕсли;
		
	Возврат ИспользоватьСофтФонТекущийПользователь;
КонецФункции

// Функция возвращает из справочника виды контактной информации
//  вид контактной, для указанного объекта контактной информации
// 	если не находит то возвращает пустую ссылку, если такая ссылка уже используется
//	в регистре, то создает новую присоединяя к наименованию 1
// Идентификатор - Определяет что именно ищем "ТелефонАОН", "ТелефонВнутренний" - Тип CRM_ВидыТелефонов
// ВидОбъекта - Определяет для какого объекта, перечисление виды объектов КИ
// Ссылка - объект ссылка на объект, Если неопределено - тогда возвращаем первый из справочника
Функция CRMВернутьВидКонтактнойИнформацииДляВидаОбъекта(Идентификатор, ВидОбъекта, Ссылка = Неопределено) Экспорт
	Если НЕ (Ссылка = Неопределено) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Тип",			Перечисления.ТипыКонтактнойИнформации.Телефон);
		Запрос.УстановитьПараметр("ВидОбъекта",		ВидОбъекта);
		Запрос.УстановитьПараметр("Идентификатор",	Идентификатор);
		Запрос.УстановитьПараметр("Ссылка",			Ссылка);
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		               |	КонтактнаяИнформация.Вид КАК Ссылка,
		               |	КонтактнаяИнформация.Вид.Наименование КАК Наименование
		               |ИЗ
		               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		               |ГДЕ
		               |	КонтактнаяИнформация.Объект = &Ссылка
		               |	И КонтактнаяИнформация.Вид В
		               |			(ВЫБРАТЬ
		               |				ВидыКонтактнойИнформации.Ссылка КАК Ссылка
		               |			ИЗ
		               |				Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		               |			ГДЕ
		               |				ВидыКонтактнойИнформации.Тип = &Тип
		               |				И ВидыКонтактнойИнформации.CRM_ИдентификаторВида = &Идентификатор
		               |				И ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъекта)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	КонтактнаяИнформация.Вид.Наименование УБЫВ";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			// Создаем новый объект вид КИ
			НовыйВидКИ = Справочники.ВидыКонтактнойИнформации.СоздатьЭлемент();
			НовыйВидКИ.Наименование = Выборка.Наименование + "1";
			НовыйВидКИ.CRM_ИдентификаторВида = Идентификатор;
			НовыйВидКИ.ВидОбъектаКонтактнойИнформации = ВидОбъекта;
			НовыйВидКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
			Попытка
				НовыйВидКИ.Записать();
				Возврат НовыйВидКИ.Ссылка;
			Исключение
				Предупреждение("Ошибка ввода нового вида контактной информации. Необходимо ввести в ручную.", 15, "Внимание");
				Возврат Выборка.Ссылка;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	// Если в регистре КИ не найдено, значит пробуем получить по умолчанию, для данного вида объекта
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Тип",			Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("ВидОбъекта",		ВидОбъекта);
	Запрос.УстановитьПараметр("Идентификатор",	Идентификатор);
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Тип = &Тип
	|	И ВидыКонтактнойИнформации.CRM_ИдентификаторВида = &Идентификатор
	|	И ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъекта";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;	
	Возврат Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
КонецФункции

// Функция проверяем по переданной таблице значений, количество найденных контактов
//  если он один, то возвращается сразу он. Если несколько и это связка контрагента и 
//  контактных лиц, то возвращается один из них.
// Параметры
//  ТаблицаЗначений  – Таблица значений – таблица содержащая найденные контакты
//  ЗначениеКонтакта  – Объект контактной информации - возвращаемое значение контакта.
// Возвращаемое значение:
//   Булево   – Истина, был найден один контакт или связка
Функция CRMОдинКонтактИлиСвязка(ТаблицаЗначений, ЗначениеКонтакта) Экспорт
	
	Результат = Ложь;
	Если ТаблицаЗначений.Количество() = 1 Тогда 
		СтруктураКонтакта = CRMПреобразоватьКонтактВСтруктуру(ТаблицаЗначений[0].Объект, ТаблицаЗначений[0].Поле1, ТаблицаЗначений[0].Поле2, ТаблицаЗначений[0].Поле3, ТаблицаЗначений[0].Поле4);
		ЗначениеКонтакта = СтруктураКонтакта;
		Результат = Истина;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции 

// Функция возвращает список значений в виде телефона и владельца данного телефона
//  используется когда нужно получить список номеров телефонов, например, по контрагентам
//  для визуализации их пользователю и выбора из списка нужного
//
// Параметры:
//  ОбъектКонтактнойИнформации  – Любой тип объекта, который определен для Измерения 
// 								  объект регистра КонтактнаяИнформация – значение для фильтрации
//   СписокТелефонов            – СписокЗначение, список значений, в который добавляются телефонные номера
Процедура CRMПолучитьСписокТелефоновСофтФон(ОбъектКонтактнойИнформации, СписокТелефонов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Объект,
	|	КонтактнаяИнформация.Тип,
	|	КонтактнаяИнформация.Вид,
	|	КонтактнаяИнформация.Представление,
	|	КонтактнаяИнформация.Поле1,
	|	КонтактнаяИнформация.Поле2,
	|	КонтактнаяИнформация.Поле3,
	|	КонтактнаяИнформация.Поле4,
	|	КонтактнаяИнформация.ЗначениеПоУмолчанию
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &ОбъектКонтактнойИнформации
	|	И КонтактнаяИнформация.Тип = &ТипКонтактнойИнформации";
	
	Запрос.УстановитьПараметр("ОбъектКонтактнойИнформации", ОбъектКонтактнойИнформации);
	Запрос.УстановитьПараметр("ТипКонтактнойИнформации",    Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	ТаблицаКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
	// Первый строкой выводим сам объект, а номер его заполняем значением по умолчанию
	Если (ТаблицаКонтактнойИнформации.Количество() > 0) Тогда
		
		НайденныеСтроки = ТаблицаКонтактнойИнформации.НайтиСтроки(Новый Структура("ЗначениеПоУмолчанию", Истина));
		Если НайденныеСтроки.Количество()>0 Тогда
			ТекущаяКонтактнаяИнформация = НайденныеСтроки[0];
		Иначе
			ТекущаяКонтактнаяИнформация = ТаблицаКонтактнойИнформации.Получить(0);
		КонецЕсли;
		
		СтруктураНомера = CRMСформироватьСтруктуруНомераИзПолей(ТекущаяКонтактнаяИнформация);
		СтруктураНомера.Вставить("Объект", ТекущаяКонтактнаяИнформация.Объект);
		СписокТелефонов.Добавить(СтруктураНомера, Строка(ТекущаяКонтактнаяИнформация.Объект));
	КонецЕсли;
	
	// Далее делаем подобие дерева с выделенными телефонными номерами для объекта
	Для каждого ТекущаяКонтактнаяИнформация Из ТаблицаКонтактнойИнформации Цикл
		СтруктураТелефона = CRMСформироватьСтруктуруНомераИзПолей(ТекущаяКонтактнаяИнформация);
		СтруктураТелефона.Вставить("Объект", ТекущаяКонтактнаяИнформация.Объект);
		Префикс = ?(ТекущаяКонтактнаяИнформация.ЗначениеПоУмолчанию, " • ", "   ");
		СписокТелефонов.Добавить(СтруктураТелефона, Префикс + СокрЛП(ТекущаяКонтактнаяИнформация.Вид) + ": " + ТекущаяКонтактнаяИнформация.Представление);
	КонецЦикла;
КонецПроцедуры

// По переданной структуре, определяет, кто это контакт, контрагент или контактное лицо
//  и в зависимости от этого возвращает структуру содержащую Контрагента, Контактное лицо и 
//  ответственного менеджера, если таковых получается определить.
// Так же содержит номер телефона, разложенный по полям.
Функция CRMВернутьПоНайденномуКонтактуИнформацию(СтруктураНайденногоКонтакта) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Контрагент", 			Неопределено);
	Результат.Вставить("КонтактноеЛицо", 		Неопределено);
	Результат.Вставить("СоединитьССотрудником",	Неопределено);
	Результат.Вставить("Поле1",					Неопределено);
	Результат.Вставить("Поле2",					Неопределено);
	Результат.Вставить("Поле3",					Неопределено);
	Результат.Вставить("Поле4",					Неопределено);
	
	// Если передали не структуру объекта и номера, то выходим с пустым результатом
	Если НЕ ТипЗнч(СтруктураНайденногоКонтакта) = Тип("Структура") Тогда Возврат Результат; КонецЕсли;
	
	Контакт = СтруктураНайденногоКонтакта.Объект;
	
	Если НЕ (Контакт = Неопределено) Тогда
		// Если был найден контрагент
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Контрагенты") Или ТипЗнч(Контакт) = Тип("СправочникОбъект.Контрагенты") Тогда
			Ведущий = РаботаСКонтактнымиЛицами.ПолучитьВедущегоКонтрагента(Контакт.Ссылка);
			
			Если ЗначениеЗаполнено(Ведущий) Тогда
				Результат.Контрагент     = Ведущий;
				Результат.КонтактноеЛицо = Контакт;
			Иначе	
				Результат.Контрагент	= Контакт;
			КонецЕсли;
		// Иначе кого бы не нашли, подставляем его в поле соединять с сотрудником
		Иначе
			Результат.СоединитьССотрудником = Контакт;
		КонецЕсли;
	КонецЕсли;
	Результат.Поле1 = СтруктураНайденногоКонтакта.Поле1;
	Результат.Поле2 = СтруктураНайденногоКонтакта.Поле2;
	Результат.Поле3 = СтруктураНайденногоКонтакта.Поле3;
	Результат.Поле4 = СтруктураНайденногоКонтакта.Поле4;
	
	Возврат Результат;
КонецФункции

// Функция определяет внутренний или внешний звонок исходя
//  из длины переданного номера телефона, и максимальной длины внутреннего номера 
//  телефона установленного в системе. Если длина номера > 0 И <= макс, то это внутренний
Функция CRMОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера) Экспорт
	НомерТелефона = CRMУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
	ДлинаНомераТелефона = СтрДлина(СокрЛП(НомерТелефона));
	Возврат (ДлинаНомераТелефона <= МаксимальнаяДлинаВнутреннегоНомера);
КонецФункции

// Процедура формирует структуру номера телефона в полях
// а может получат либо как строку либо как структуру номера телефона СофтФона
Процедура CRMЗаполнитьЗаписьРегистраСтруктуройТелефона(ЗаписьРегистра, НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера, КоличествоХранимыхЦифрТелефона) Экспорт
	// структура формируется при возврате из фукнкции ПреобразоватьНомерСУчетомПрефиксов
	Если ТипЗнч(НомерТелефона) = Тип("Структура") Тогда
		Если НЕ ПустаяСтрока(НомерТелефона.Внутренний) И ПустаяСтрока(НомерТелефона.НомерТелефона) Тогда
			ЗаписьРегистра.Поле4         = НомерТелефона.Внутренний;
			ЗаписьРегистра.Представление = НомерТелефона.Внутренний;
			ЗаписьРегистра.CRM_ПолеХраненияНомера = CRMПреобразоватьНомерДляСохранения(НомерТелефона.Внутренний, КоличествоХранимыхЦифрТелефона);
		Иначе
			ЗаписьРегистра.Поле1         = НомерТелефона.КодСтраны;
			ЗаписьРегистра.Поле2         = НомерТелефона.КодГорода;
			ЗаписьРегистра.Поле3         = НомерТелефона.НомерТелефона;
			ЗаписьРегистра.Поле4         = НомерТелефона.Внутренний;
			ЗаписьРегистра.Представление = НомерТелефона.Представление;
			ЗаписьРегистра.CRM_ПолеХраненияНомера = CRMПреобразоватьНомерДляСохранения(НомерТелефона.НомерТелефона, КоличествоХранимыхЦифрТелефона);
		КонецЕсли;
	Иначе
		Если CRMОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера) Тогда
			ЗаписьРегистра.Поле4         		= НомерТелефона;
			ЗаписьРегистра.Представление 		= НомерТелефона;
			ЗаписьРегистра.CRM_ПолеХраненияНомера 	= CRMПреобразоватьНомерДляСохранения(НомерТелефона, КоличествоХранимыхЦифрТелефона);
		Иначе
			ЗаписьРегистра.Поле3         		= НомерТелефона;
			ЗаписьРегистра.Представление 		= НомерТелефона;
			ЗаписьРегистра.CRM_ПолеХраненияНомера 	= CRMПреобразоватьНомерДляСохранения(НомерТелефона, КоличествоХранимыхЦифрТелефона);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура предназначена для управления кнопками Софтфона интегрированными в базу данных
//  например, позвонить и новый номер, во все объекты контактной информации
Процедура CRMДоступностьКнопокСофтФона(ТекущаяСтрока, кнПозвонить) Экспорт
	ТипТелефон = Перечисления.ТипыКонтактнойИнформации.Телефон;
	Попытка
		// Обрабатываем кнопку позвонить
		кнПозвонить.Доступность = ?(ТекущаяСтрока.Тип = ТипТелефон И НЕ ПустаяСтрока(ТекущаяСтрока.Представление), Истина, Ложь);
	Исключение
	КонецПопытки;
КонецПроцедуры

// Процедура подставляет в открываемую форму значения кодов города и страны по умолчанию.
//
// Параметры:
//  ЭтотОбъект  – Ссылка на обработку редактирование контактной информациию
//
Процедура CRMЗаполнитьКодыПоУмолчаниюДляЗаписи(СсылкаОбработку, КодСтраны, КодГорода) Экспорт
	Если СсылкаОбработку = Неопределено Тогда Возврат; КонецЕсли;
	
	// проверим вид КИ. Если выбран, любой вид отличный от внутреннего номера
	//  то по умолчанию при открытии подставим код страны и города
	// Но только при условии, что карточка открыта новая, т.е. не заполнен не номер
	//  не внутренний
	Если НЕ ТипЗнч(СсылкаОбработку.Вид) = Тип("Строка") И ЗначениеЗаполнено(СсылкаОбработку.Вид) Тогда
		Если СсылкаОбработку.Вид.CRM_ИдентификаторВида = Перечисления.CRM_ВидыТелефонов.ТелефонВнутренний Тогда Возврат; КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СсылкаОбработку.Поле3) И НЕ ЗначениеЗаполнено(СсылкаОбработку.Поле4) Тогда
	
		// Внутренний номер, к нему не надо добавлять коды
		Если НЕ ЗначениеЗаполнено(СсылкаОбработку.Поле4) Тогда
			// Если не заполнен код страны, то заполним его
			Если НЕ ЗначениеЗаполнено(СсылкаОбработку.Поле1) Тогда СсылкаОбработку.Поле1 = КодСтраны; КонецЕсли;
			// Если не заполнен код города, то заполним его
			Если НЕ ЗначениеЗаполнено(СсылкаОбработку.Поле2) Тогда СсылкаОбработку.Поле2 = КодГорода; КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

// Функция проверяет изменилось ли состояние звонка, и возвращает результат изменения
Функция CRMСостояниеЗвонкаИзменилось(ВидыСобытийСтанции, СостояниеПосле, СостояниеДо) Экспорт

	Результат = Ложь;
	Если (СостояниеДо = ВидыСобытийСтанции.CONNECTED 
	 Или СостояниеДо = ВидыСобытийСтанции.ACCEPTED
	 Или СостояниеДо = ВидыСобытийСтанции.OFFERING
	 Или СостояниеДо = ВидыСобытийСтанции.ONHOLD
	 Или СостояниеДо = ВидыСобытийСтанции.ONHOLDPENDTRANSFER
	 Или СостояниеДо = ВидыСобытийСтанции.ONHOLDPENDCONF
	 Или СостояниеДо = ВидыСобытийСтанции.RINGBACK
	 Или СостояниеДо = ВидыСобытийСтанции.CONFERENCED)
	 И НЕ СостояниеДо = СостояниеПосле Тогда
	 
	 	Результат = Истина;
		
	КонецЕсли;
	 
	Возврат Результат;
КонецФункции

// Триггер кнопки HOLD / UNHOLD
// В зависимости от текущего вида кнопки меняем на противоположный
Процедура CRMТриггерКнопкиHold(Кнопка, Заголовок) Экспорт
	Если Заголовок = "Hold" Тогда
		Кнопка.Заголовок = "UnHold";
	ИначеЕсли Заголовок = "UnHold" Тогда
		Кнопка.Заголовок = "Hold";
	КонецЕсли;
КонецПроцедуры

// Функция определяет можно ли использовать метод переданный в Метод, при переданном 
//  состоянии звонка, переменная Статус. Т.к. использование каждого метода возможно
//  при определенный состояниях.
// 
// Параметры
//  <Метод>  – <Строка> – имя метода станции
//  <Статус>  – <Структура ВидыСобытийСтанции> – текущее состояние звонка
// Возвращаемое значение:
//   <Булево>   – разрешено/запрещено использование метода
Функция CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, Метод, Статус, ДопПараметр="") Экспорт
	Результат = Ложь;
	
	// Ответить на вызов
	Если Метод = "AnswerCall" Тогда
	
 		Если    Статус = ВидыСобытийСтанции.OFFERING
	    	Или Статус = ВидыСобытийСтанции.RINGBACK  
	    	Или Статус = ВидыСобытийСтанции.ACCEPTED Тогда 
			Результат = Истина; 
		КонецЕсли;
 
	// HOLD
	ИначеЕсли Метод = "HoldCall" Тогда
		Если Статус = ВидыСобытийСтанции.CONNECTED Тогда Результат = Истина; КонецЕсли;
		
	// UnHold
	ИначеЕсли Метод = "UnHoldCall" Тогда
		Если Статус = ВидыСобытийСтанции.ONHOLD Или
			 Статус = ВидыСобытийСтанции.ONHOLDPENDTRANSFER Или
			 Статус = ВидыСобытийСтанции.ONHOLDPENDCONF Тогда 
			 
			Результат = Истина; 
		КонецЕсли;
		
	// Уничтожить звонок, отмена
	ИначеЕсли Метод = "DropCall" Тогда
		Если НЕ Статус = ВидыСобытийСтанции.IDLE Тогда Результат = Истина; КонецЕсли;
		
	// Безусловный перевод звонка
	ИначеЕсли Метод = "BlindTransferCall" Тогда
 		Если    Статус = ВидыСобытийСтанции.OFFERING
	    	Или Статус = ВидыСобытийСтанции.RINGBACK  
	    	Или Статус = ВидыСобытийСтанции.ACCEPTED Тогда 
			Результат = Истина; 
		КонецЕсли;
		
	// Условный перевод звонка
	ИначеЕсли Метод = "SetupTransferCall" Тогда
		Если Статус = ВидыСобытийСтанции.CONNECTED Тогда Результат = Истина; КонецЕсли;
		
	// Позвонить при гудке на линии
	ИначеЕсли Метод = "DialCall" Тогда
		
	// Подтвердить перевод звонка
	//  в ДопПараметр необходимо передать идентификатор консультационного звонка
	ИначеЕсли Метод = "CompleteTransferCall" Тогда
		Если (Статус = ВидыСобытийСтанции.CONNECTED Или
			 Статус = ВидыСобытийСтанции.RINGBACK Или
			 //Статус = ВидыСобытийСтанции.BUSY Или
			 Статус = ВидыСобытийСтанции.PROCEEDING Или  
			 Статус = ВидыСобытийСтанции.ONHOLD) И 
			 НЕ ПустаяСтрока(ДопПараметр) Тогда
			 
			 Результат = Истина; 
		 КонецЕсли;
		 
	КонецЕсли;
	
	Возврат Результат;
КонецФункции 

// Заполним КЭШ таблицу содержащую телефоны
//  Для ФизЛиц и Пользователей. Т.е. для сотрудников организации
//  далее эта таблица будет передана в форму Списка телефонов
Функция CRMЗаполниитьКЭШТаблицуТелефонныхНомеров() Экспорт
	
	Состояние("Заполнение таблицы телефонных номеров сотрудников");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Объект КАК Объект,
	               |	КонтактнаяИнформация.Поле1 КАК Поле1,
	               |	КонтактнаяИнформация.Поле2 КАК Поле2,
	               |	КонтактнаяИнформация.Поле3 КАК Поле3,
	               |	КонтактнаяИнформация.Поле4 КАК Поле4,
	               |	КонтактнаяИнформация.Представление КАК Представление
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Тип = &Тип
	               |	И ВЫРАЗИТЬ(КонтактнаяИнформация.Объект КАК Справочник.Сотрудники) В
	               |			(ВЫБРАТЬ
	               |				Сотрудники.Ссылка КАК Ссылка
	               |			ИЗ
	               |				Справочник.Сотрудники КАК Сотрудники)";
		
	// Список всех доступных типов
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);

	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

// Процедура обновляет данные в КЭШ таблице, в зависимости от изменений пользователя
Процедура CRMОбновитьКЭШТаблицуТелефонныхНомеров(ТаблицаКЭШТелефонныхНомеров, Ссылка, Отказ) Экспорт
	// Если отказ, то ничего вообще не делаем
	Если Отказ Тогда Возврат; КонецЕсли;
	
	// Если таблица неопределена, то загрузим ее заново
	Если ТаблицаКЭШТелефонныхНомеров = Неопределено Тогда
		ТаблицаКЭШТелефонныхНомеров = CRMЗаполниитьКЭШТаблицуТелефонныхНомеров();
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = ТаблицаКЭШТелефонныхНомеров.НайтиСтроки(Новый Структура("Объект", Ссылка));
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда
		// Удаляем текущие записи, затем заполним их новыми значениями
		Для Каждого ТекущаяСтрокаДляУдаления ИЗ НайденныеСтроки Цикл
			ТаблицаКЭШТелефонныхНомеров.Удалить(ТекущаяСтрокаДляУдаления);
		КонецЦикла;
	КонецЕсли;
	// зальем новые строки
	НаборКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	НаборКИ.Отбор.Объект.Установить(Ссылка);
	НаборКИ.Отбор.Тип.Установить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	НаборКИ.Прочитать();
		
	Для Каждого ТекущаяЗаписьКИ Из НаборКИ Цикл
		НоваяСтрока = ТаблицаКЭШТелефонныхНомеров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяЗаписьКИ, "Объект, Поле1, Поле2, Поле3, Поле4, Представление");
	КонецЦикла;
	
	Отказ = Ложь;
КонецПроцедуры

// Функция разбирает переданный номер, в структуру телефона и возвращает ее
Функция CRMРазобратьНомерТелефонаВСтруктуру(Знач ПозвонитьПоНомеру, глМаксимальнаяДлинаВнутреннегоНомера) Экспорт
	СтруктураНомераТелефона = Неопределено;
	
	Если НЕ ПустаяСтрока(ПозвонитьПоНомеру) Тогда
		СтруктураНомераТелефона = CRMСформироватьСтруктуруНомераИзПолей();
		
		// попытаемся разложить телефонный номер, если получится
		// на составляющие, если номер введен правильно
		// т.е. полностью 8(или 7, +7)903 7721122, то будем по нему звонить
		// номер должен прийти в следующем формате +7(903)7721122 или 8(903)7721122
		Если CRMОпределитьВнутреннийЗвонокПоНомеру(ПозвонитьПоНомеру, глМаксимальнаяДлинаВнутреннегоНомера) Тогда
			СтруктураНомераТелефона.Внутренний = ПозвонитьПоНомеру;
		Иначе
			// удалим + если есть
			ПозвонитьПоНомеру = СтрЗаменить(ПозвонитьПоНомеру, "+", "");
			НомКодГородаНачало = Найти(ПозвонитьПоНомеру, "(");
			НомКодГородаКонец = Найти(ПозвонитьПоНомеру, ")");
				
			// если был просто введен номер, то передадим его и позвоним
			//  если был введен как полный разберем
			Если НомКодГородаНачало = 0 И НомКодГородаКонец = 0 Тогда 
				СтруктураНомераТелефона.НомерТелефона = ПозвонитьПоНомеру;
			Иначе
				СтруктураНомераТелефона.КодСтраны = СокрЛП(Лев(ПозвонитьПоНомеру, НомКодГородаНачало-1));
				СтруктураНомераТелефона.КодГорода = СокрЛП(Сред(ПозвонитьПоНомеру, НомКодГородаНачало+1, НомКодГородаКонец-НомКодГородаНачало-1));
				СтруктураНомераТелефона.НомерТелефона = Прав(ПозвонитьПоНомеру, СтрДлина(ПозвонитьПоНомеру)-НомКодГородаКонец); 
			КонецЕсли;
				
		КонецЕсли;
				
		CRMСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураНомераТелефона);
	КонецЕсли;
	
	Возврат СтруктураНомераТелефона;
КонецФункции


Функция ПолучитьРеквизиты() Экспорт
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Наименование");
	
	ДобавитьРеквизит(Реквизиты, "Наименование");		
	ДобавитьРеквизит(Реквизиты, "Описание");		
	ДобавитьРеквизит(Реквизиты, "Артикул");		
	ДобавитьРеквизит(Реквизиты, "Характеристика");		
	ДобавитьРеквизит(Реквизиты, "ОстатокКоличество");		
	ДобавитьРеквизит(Реквизиты, "ОстатокМногоМало5");		
	ДобавитьРеквизит(Реквизиты, "ОстатокМногоМало10");		
	ДобавитьРеквизит(Реквизиты, "ОстатокМногоМало20");		
	
	Возврат Реквизиты; 
КонецФункции

Функция ПолучитьРеквизитыНоменклатуры() Экспорт
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Наименование");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_РеквизитыНоменклатурыДляШаблонаСписокПолей.ИмяПоля КАК ИмяПоля
	|ИЗ
	|	Справочник.ns_РеквизитыНоменклатурыДляШаблона.СписокПолей КАК ns_РеквизитыНоменклатурыДляШаблонаСписокПолей";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Реквизиты;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДобавитьРеквизит(Реквизиты, Выборка.ИмяПоля);	
	КонецЦикла; 
	
	Возврат Реквизиты; 
КонецФункции

Процедура ДобавитьРеквизит(Таблица, Значение)
	НовСтр = Таблица.Добавить();
	НовСтр.Наименование = Значение;
КонецПроцедуры

Функция ПолучитьЗначениеРеквизита(Номенклатура, ДопРеквизит, Таблица) Экспорт
	
	ДанныеВозврата = Неопределено;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Объект", Номенклатура);
	Отбор.Вставить("СвойствоНаименование", ДопРеквизит);
	Строки = Таблица.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		ДанныеВозврата = Строки[0].Значение;	
	КонецЕсли; 
	
	Возврат ДанныеВозврата;
КонецФункции

Функция СформироватьTitle(ШаблонЗаголовка, Заголовок, Номенклатура, Характеристика) Экспорт
	Наименование = ns_Переопределяемый.ПолучитьНаименованиеНоменклатуры(Номенклатура);
	
	Если ЗначениеЗаполнено(Характеристика) И ns_ОбщегоНазначенияПовтИсп.ПолучитьПодставлятьХарактеристикуВЗаголовок() Тогда
		Наименование = Лев(Наименование + " " + Строка(Характеристика), 50);
	Иначе
		Наименование = Лев(Наименование, 50);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		Возврат ВычислитьРегулярноеВыражение(ШаблонЗаголовка, Наименование, Характеристика);
	КонецЕсли; 
	
	ОбщийШаблонЗаголовка = ns_ОбщегоНазначенияПовтИсп.ПолучитьШаблонЗаголовка();
	Если ЗначениеЗаполнено(ОбщийШаблонЗаголовка) Тогда
		Возврат ВычислитьРегулярноеВыражение(ОбщийШаблонЗаголовка, Наименование, Характеристика);
	КонецЕсли; 
	
	Возврат Наименование;
КонецФункции

Функция ВычислитьРегулярноеВыражение(Шаблон, Наименование, Характеристика) Экспорт
	Результат = Наименование;
	
	Попытка
		RegExp = Новый COMОбъект("VBScript.RegExp");
		Если RegExp = Неопределено Тогда
			Возврат Результат;	
		КонецЕсли; 
		
		RegExp.IgnoreCase = Ложь;
		RegExp.Global = Истина;
		RegExp.MultiLine = Истина;
		
		RegExp.Pattern = Шаблон.РегулярноеВыражение;
		Если Шаблон.Replace И ЗначениеЗаполнено(Шаблон.ПодстрокаДляЗамены) Тогда
			РезультатРазбора = RegExp.Replace(Наименование, Шаблон.ПодстрокаДляЗамены);
		Иначе	
			РезультатРазбора = RegExp.Execute(Наименование);
		КонецЕсли; 
		
		Если ТипЗнч(РезультатРазбора) = Тип("Строка") Тогда
			Возврат РезультатРазбора;
		КонецЕсли; 
		
		Если РезультатРазбора.Count > 0 Тогда
			Для каждого ТекущийОбъект Из РезультатРазбора Цикл
				Если ТекущийОбъект.SubMatches.Count > 0 Тогда
					Для каждого Строка Из ТекущийОбъект.SubMatches Цикл
						Результат = Строка;	
					КонецЦикла; 
				КонецЕсли; 	
			КонецЦикла; 
		КонецЕсли; 
	Исключение
		ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
		Возврат Результат;
	КонецПопытки; 
	
	Возврат Результат;
КонецФункции

Функция СформироватьСтруктуруШаблона(Данные)
	СтруктуруШаблона = Новый Структура;
	СтруктуруШаблона.Вставить("ОбщийТекстОбъявлений", Данные.ОбщийТекстОбъявлений);
	СтруктуруШаблона.Вставить("Телефон", Данные.Телефон);
	СтруктуруШаблона.Вставить("Менеджер", Данные.Менеджер);
	СтруктуруШаблона.Вставить("Магазин", Данные.Магазин);
	СтруктуруШаблона.Вставить("Адрес", Данные.Адрес);
	Возврат СтруктуруШаблона;
КонецФункции
 
Функция ПолучитьСписокШаблонов()
	
	ДанныеШаблонизатора = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ШаблоныОбъявлений.Ссылка КАК Ссылка,
	|	ns_ШаблоныОбъявлений.ОбщийТекстОбъявлений КАК ОбщийТекстОбъявлений,
	|	ns_ШаблоныОбъявлений.Телефон КАК Телефон,
	|	ns_ШаблоныОбъявлений.Менеджер КАК Менеджер,
	|	ns_ШаблоныОбъявлений.Магазин КАК Магазин,
	|	ns_ШаблоныОбъявлений.Адрес КАК Адрес
	|ИЗ
	|	Справочник.ns_ШаблоныОбъявлений КАК ns_ШаблоныОбъявлений
	|ГДЕ
	|	ns_ШаблоныОбъявлений.ПометкаУдаления = ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДанныеШаблонизатора;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеШаблонизатора.Вставить(Выборка.Ссылка, СформироватьСтруктуруШаблона(Выборка));
	КонецЦикла; 
	
	Возврат ДанныеШаблонизатора;
КонецФункции

Функция ПолучитьДанныеДляШаблонизатора(Остатки, Номенклатура) Экспорт
	
	ДанныеДляШаблонизатора = Новый Структура;
	ДанныеДляШаблонизатора.Вставить("ДопРеквизиты", ns_Переопределяемый.ПолучитьЗначенияДополнительныхРеквизитов(Номенклатура));
	ДанныеДляШаблонизатора.Вставить("ИменаДопРеквизитов", СформироватьИменаДопРеквизитовПоШаблонам());
	ДанныеДляШаблонизатора.Вставить("Остатки", Остатки);
	ДанныеДляШаблонизатора.Вставить("РеквизитыНоменклатуры", СформироватьДанныеНоменклатуры(Номенклатура));
	ДанныеДляШаблонизатора.Вставить("Характеристики", ns_Переопределяемый.ПолучитьЗначенияХарактеристикДляШаблона(Номенклатура));

	Возврат ДанныеДляШаблонизатора;
КонецФункции

Функция ПолучитьЗначениеДопРеквизита(Номенклатура, ДопРеквизит, Таблица) Экспорт
	
	ДанныеВозврата = Неопределено;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Объект", Номенклатура);
	Отбор.Вставить("СвойствоНаименование", ДопРеквизит);
	Строки = Таблица.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		ДанныеВозврата = Строки[0].Значение;	
	КонецЕсли; 
	
	Возврат ДанныеВозврата;
КонецФункции

Функция СформироватьИменаДопРеквизитовПоШаблонам()
	
	ИменаДопРеквизитовКонфигурации = Новый СписокЗначений;
	ИменаДопРеквизитовПоШаблонам = Новый Соответствие;

	ns_Переопределяемый.ЗаполнитьДопРеквизиты(ИменаДопРеквизитовКонфигурации);
	СписокШаблонов = ПолучитьСписокШаблонов();
	
	Для каждого Шаблон Из СписокШаблонов Цикл
		ОбщийТекстОбъявлений = Шаблон.Значение.ОбщийТекстОбъявлений;
		СписокИменШаблона = Новый СписокЗначений;
		Для каждого Строка Из ИменаДопРеквизитовКонфигурации Цикл
			Если Найти(ОбщийТекстОбъявлений, "[" + Строка.Значение +"]") > 0 Тогда
				СписокИменШаблона.Добавить(Строка.Значение);
			КонецЕсли; 
		КонецЦикла;
		ИменаДопРеквизитовПоШаблонам.Вставить(Шаблон.Ключ, СписокИменШаблона);
	КонецЦикла;
	
	Возврат ИменаДопРеквизитовПоШаблонам;
КонецФункции

Процедура ЗаполнитьДанныеИзШаблона(ДанныеОбъявления, Шаблон, Магазин, ДанныеДляШаблонизатора, Адрес) Экспорт
	
	Если ЗначениеЗаполнено(Шаблон.Телефон) Тогда
		ДанныеОбъявления.ContactPhone = Шаблон.Телефон; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Адрес) Тогда
		ДанныеОбъявления.Address = Адрес; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Шаблон.Менеджер) Тогда
		ДанныеОбъявления.ManagerName = Шаблон.Менеджер; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Шаблон.ОбщийТекстОбъявлений) Тогда
		ОбщийТекстОбъявлений = ЗаменитьВШаблоне(Шаблон, ДанныеОбъявления, ДанныеДляШаблонизатора);
		ДанныеОбъявления.ТекстОбъявления = "<![CDATA[" + Лев(СокрЛП(ОбщийТекстОбъявлений), 4980) + "]]>";
	КонецЕсли; 
	
КонецПроцедуры

Функция ЗаменитьВШаблоне(Шаблон, Данные, ДанныеДляШаблонизатора)
	
	ОбщийТекстОбъявлений = Шаблон.ОбщийТекстОбъявлений;
	
	Параметры = Новый Соответствие;
	
	Если Найти(ОбщийТекстОбъявлений, "[Наименование]") > 0 Тогда
		Если ЗначениеЗаполнено(Данные.Характеристика) Тогда
			Параметры.Вставить("Наименование", Строка(Данные.Наименование) + " " + Строка(Данные.Характеристика));
		Иначе
			Параметры.Вставить("Наименование", Строка(Данные.Наименование));
		КонецЕсли; 
	КонецЕсли; 
	
	Если Найти(ОбщийТекстОбъявлений, "[Артикул]") > 0 Тогда
		Параметры.Вставить("Артикул", Строка(Данные.Артикул));
	КонецЕсли; 
	
	Если Найти(ОбщийТекстОбъявлений, "[Описание]") > 0 Тогда
		Параметры.Вставить("Описание", ЗаменитьПереводСтрокиНаBR(Данные.nsDescription));
	КонецЕсли; 
	
	ИменаДопРеквизитов = ДанныеДляШаблонизатора.ИменаДопРеквизитов[Шаблон];
	Если НЕ ИменаДопРеквизитов = Неопределено Тогда
		Для каждого ИмяРеквизита Из ИменаДопРеквизитов Цикл
			Если Найти(ОбщийТекстОбъявлений, "[" + ИмяРеквизита.Значение +"]") > 0 Тогда
				ЗначениеДопРеквизита = ПолучитьЗначениеДопРеквизита(Данные.Номенклатура, ИмяРеквизита.Значение, ДанныеДляШаблонизатора.ДопРеквизиты);
				// Если совпадают реквизиты и доп реквизиты, то надо проверить, возможно уже заполнено
				ЗначениеКлюча = Параметры.Получить(ИмяРеквизита.Значение);
				Если ЗначениеКлюча = Неопределено Тогда
					Параметры.Вставить("" + ИмяРеквизита.Значение + "", ?(ЗначениеЗаполнено(ЗначениеДопРеквизита), ЗначениеДопРеквизита, ""));
				Иначе 
					// Перезаполняем значение основного реквизита только в том случае, если заполнено значение в доп реквизитах
					Если ЗначениеЗаполнено(ЗначениеДопРеквизита) Тогда
						Параметры.Вставить("" + ИмяРеквизита.Значение + "", ЗначениеДопРеквизита);	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;

	Если Найти(ОбщийТекстОбъявлений, "[ОстатокКоличество]") > 0 Тогда
		Остаток = ДанныеДляШаблонизатора.Остатки[ПолучитьID(Данные)];
		Параметры.Вставить("ОстатокКоличество", ?(ЗначениеЗаполнено(Остаток), Остаток, ""));
	КонецЕсли; 
	
	Если Найти(ОбщийТекстОбъявлений, "[ОстатокМногоМало5]") > 0 Тогда
		Если НЕ Остаток = Неопределено Тогда
			Если Остаток > 5 Тогда
				СтрОстаток = "Много";
			Иначе
				СтрОстаток = "Мало";
			КонецЕсли;
		Иначе
			СтрОстаток = Неопределено;
		КонецЕсли;
		Параметры.Вставить("ОстатокМногоМало5", ?(ЗначениеЗаполнено(СтрОстаток), СтрОстаток, ""));
	КонецЕсли; 

	Если Найти(ОбщийТекстОбъявлений, "[ОстатокМногоМало10]") > 0 Тогда
		Остаток = ДанныеДляШаблонизатора.Остатки[ПолучитьID(Данные)];
		Если НЕ Остаток = Неопределено Тогда
			Если Остаток > 10 Тогда
				СтрОстаток = "Много";
			Иначе
				СтрОстаток = "Мало";
			КонецЕсли;
		Иначе
			СтрОстаток = Неопределено;
		КонецЕсли;
		Параметры.Вставить("ОстатокМногоМало10", ?(ЗначениеЗаполнено(СтрОстаток), СтрОстаток, ""));
	КонецЕсли; 

	Если Найти(ОбщийТекстОбъявлений, "[ОстатокМногоМало20]") > 0 Тогда
		Если НЕ Остаток = Неопределено Тогда
			Если Остаток > 20 Тогда
				СтрОстаток = "Много";
			Иначе
				СтрОстаток = "Мало";
			КонецЕсли;
		Иначе
			СтрОстаток = Неопределено;
		КонецЕсли;
		Параметры.Вставить("ОстатокМногоМало20", ?(ЗначениеЗаполнено(СтрОстаток), СтрОстаток, ""));
	КонецЕсли; 

	Если Найти(ОбщийТекстОбъявлений, "[Характеристика]") > 0 Тогда
		ЗначениеХарактеристики = ПолучитьЗначениеХарактеристикиНоменклатуры(Данные.Номенклатура, ДанныеДляШаблонизатора.Характеристики);
		Параметры.Вставить("Характеристика", ЗначениеХарактеристики);
	КонецЕсли; 
	
	РеквизитыНоменклатуры = ПолучитьРеквизитыНоменклатуры();
	Для каждого Реквизит Из РеквизитыНоменклатуры Цикл
		ИмяВШаблоне = "ном_" + Реквизит.Наименование; 
		Если Найти(ОбщийТекстОбъявлений, "[" + ИмяВШаблоне + "]") > 0 Тогда
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаНоменклатуры(Данные.Номенклатура, Реквизит.Наименование, ДанныеДляШаблонизатора.РеквизитыНоменклатуры);
			Параметры.Вставить(ИмяВШаблоне, ЗначениеРеквизита);
		КонецЕсли; 
	КонецЦикла; 
	
	Для Каждого Параметр Из Параметры Цикл
		ОбщийТекстОбъявлений = СтрЗаменить(ОбщийТекстОбъявлений, "[" + Параметр.Ключ + "]", СокрЛП(Параметр.Значение));
	КонецЦикла;
	
	ОбщийТекстОбъявлений = СтрЗаменить(ОбщийТекстОбъявлений, Символы.ВК, "");
	ОбщийТекстОбъявлений = СтрЗаменить(ОбщийТекстОбъявлений, Символы.ПС, "");
	ОбщийТекстОбъявлений = СтрЗаменить(ОбщийТекстОбъявлений, "<p></p>", "");

	Возврат ОбщийТекстОбъявлений;
КонецФункции

Функция ПолучитьID(Данные)
	ID = "";
	
	Если ЗначениеЗаполнено(Данные.Характеристика) Тогда
		ID = Строка(Данные.Номенклатура.УникальныйИдентификатор()) + "#" + Строка(Данные.Характеристика.УникальныйИдентификатор());	
	Иначе
		ID = Строка(Данные.Номенклатура.УникальныйИдентификатор());	
	КонецЕсли; 
	
	Возврат ID;
КонецФункции

Функция ЗаменитьПереводСтрокиНаBR(Текст)
	Возврат СтрЗаменить(Текст, Символы.ПС, "</br>");
КонецФункции

Функция СформироватьДанныеНоменклатуры(Номенклатура)
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Объект");
	ТаблицаЗначений.Колонки.Добавить("ИмяШаблона");
	ТаблицаЗначений.Колонки.Добавить("Значение");
	
	Реквизиты = ПолучитьРеквизитыНоменклатуры();
	Если Реквизиты.Количество() = 0 Тогда
		Возврат ТаблицаЗначений;
	КонецЕсли; 
	
	Поля = "";
	Для каждого Стр Из Реквизиты Цикл
		Поля = Поля + Стр.Наименование + ",";
	КонецЦикла; 
	Поля = Лев(Поля, СтрДлина(Поля) - 1);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|   %1
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Номенклатура)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", Поля);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаЗначений;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Для каждого Стр Из Реквизиты Цикл
			ИмяРеквизита = Стр.Наименование;
			ЗначениеРеквизита = Выборка[ИмяРеквизита];
			
			НовСтр = ТаблицаЗначений.Добавить();
			НовСтр.Объект = Выборка.Ссылка;
			НовСтр.ИмяШаблона = ИмяРеквизита;
			НовСтр.Значение = ЗначениеРеквизита;
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат ТаблицаЗначений;
КонецФункции

Функция ПолучитьЗначениеРеквизитаНоменклатуры(Номенклатура, ИмяШаблона, Таблица) Экспорт
	
	ДанныеВозврата = Неопределено;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Объект", Номенклатура);
	Отбор.Вставить("ИмяШаблона", ИмяШаблона);
	Строки = Таблица.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		ДанныеВозврата = Строки[0].Значение;	
	КонецЕсли; 
	
	Возврат ДанныеВозврата;
КонецФункции

Функция ПолучитьЗначениеХарактеристикиНоменклатуры(Номенклатура, Таблица) Экспорт
	
	ДанныеВозврата = Неопределено;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", Номенклатура);
	Строки = Таблица.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		ДанныеВозврата = Строки[0].ЗначениеДляШаблона;	
	КонецЕсли; 
	
	Возврат ДанныеВозврата;
КонецФункции


















 


// Определяет пройдена ли авторизации. При необходимости открывает форму авторизации
//
// Возвращаемое значение:
//  ПрошлиАвторизацию - Булево - Признак успешности прохождения авторизаци
//
Функция АвторизацияПройдена(ОткрыватьФормуАвторизации=Истина, Пинг=Ложь, Ошибка="", ЭтаФорма=Неопределено) Экспорт
	
	Если НЕ атСервер.СервисДоступен(Ошибка, Пинг) Тогда
		Если ОткрыватьФормуАвторизации И ЗначениеЗаполнено(Ошибка) Тогда
			Предупреждение(Ошибка);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	СтатусПользователя = ПараметрыСеанса.атКодСостояния%10;
	// Статусы пользователя
	//(0, "Запрещено использование");
	//(1, "Не указан логин");
	//(2, "Сохранен логин");
	//(3, "Авторизован"); // Ок
	//(4, "Отказ от авторизации");
	//(5, "Ошибка авторизации"); // Err
	
	ПрошлиАвторизацию = СтатусПользователя = 3;
	
	Если СтатусПользователя = 2 Тогда
		// Сохранен логин
		
		Логин = атСервер.ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Логин");
		Пароль = атСервер.ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Пароль");
		
		Если ПустаяСтрока(Пароль) Тогда
			АвторизацияУспешна = Ложь;
		Иначе
			АвторизацияУспешна = атСервер.ВыполнитьЗапросАвторизации(Логин, Пароль, Ошибка);
		КонецЕсли;
		
		Если Не АвторизацияУспешна И ОткрыватьФормуАвторизации Тогда
			
			ФормаАвторизации = Обработки.атПомощникПродаж.ПолучитьФорму("АвторизацияПользователя");
			Результат = ФормаАвторизации.ОткрытьМодально();
			
			Если Результат = Истина Тогда
				// Оповещение вызвано в форме авторизации
				ПрошлиАвторизацию = Истина;
			КонецЕсли;
			
		Иначе
			
			Если АвторизацияУспешна Тогда
				ПрошлиАвторизацию = Истина;
				// оповестим формы о результате авторизации
				Оповестить("атПомощникПродаж_Авторизация",, ЭтаФорма);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПрошлиАвторизацию;
	
КонецФункции

Процедура УстановитьСоответствие(СтрокаСловаря, ВыбранноеЗначение, Оповещать = Истина) Экспорт
	
	СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод");
	ЗаполнитьЗначенияСвойств(СтруктураСловаря, СтрокаСловаря);
	
	Если атСервер.СопоставитьСловарюСсылку(СтруктураСловаря, ВыбранноеЗначение) И Оповещать Тогда
		ПараметрыОповещения = Новый Структура("СсылкаСловаря, СсылкаИБ", СтрокаСловаря, ВыбранноеЗначение);
		Оповестить("атПомощникПродаж_УстановленоСоответствие", ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураСтрокиЗапроса() Экспорт
	Возврат Новый Структура("Идентификатор,
							|ДатаОбращения,
							|Описание,
							|СервисКод,
							|СервисНаименование,
							|КаталогКод,
							|КаталогНаименование,
							|МодельКод,
							|МодельНаименование,
							|МодификацияКод,
							|МодификацияНаименование,
							|Параметры");
КонецФункции

Функция СтруктураСтрокиНомераДетали() Экспорт
	Возврат Новый Структура("ДатаЗаписи,
							|Идентификатор,
							|ИдентификаторЗапроса,
							|ХешАртикулПроизводитель,
							|Артикул,
							|АртикулДляПоиска,
							|ПроизводительКод,
							|Производитель,
							|НоменклатураКод,
							|Номенклатура,
							|Наименование,
							|Количество,
							|ИмяНабора,
							|Параметры,
							|Комментарий");
КонецФункции

Функция НайтиПроизводителяСтроки(ТекСтрока, Создавать=Ложь) Экспорт
	
	Если ТипЗнч(ТекСтрока.Производитель) = Тип("СправочникСсылка.Производители") Тогда
		Возврат ТекСтрока.Производитель;
	ИначеЕсли ТипЗнч(ТекСтрока.Производитель) = Тип("Структура") Тогда
		ПроизводительСтроки = атСервер.ПолучитьСопоставленнуюСсылку(ТекСтрока.Производитель,, Ложь);
		Если ПроизводительСтроки = Неопределено Тогда
			// Первое обращение к производителю!
			ПроизводительСтроки = атСервер.НайтиСоответствиеЭлементу(ТекСтрока.Производитель);
			Если Не ЗначениеЗаполнено(ПроизводительСтроки) И Создавать Тогда
				ПроизводительСтроки = атСервер.СоздатьОбъектИБ("Производители", ТекСтрока.Производитель);
			КонецЕсли;
			Если ЗначениеЗаполнено(ПроизводительСтроки) Тогда
				атКлиент.УстановитьСоответствие(ТекСтрока.Производитель, ПроизводительСтроки, Истина);
			ИначеЕсли Не Создавать Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		Возврат ПроизводительСтроки;
	КонецЕсли;
	
	Попытка
		ПроизводительКод = ТекСтрока.ПроизводительКод;
	Исключение
		ПроизводительКод = Неопределено;
	КонецПопытки;
	Если ЗначениеЗаполнено(ПроизводительКод) Тогда
		СтруктураСловаря = Новый Структура("ЭтоСсылка, Тип, Код, Наименование", Истина, "Производители", ПроизводительКод, ""+ТекСтрока.Производитель);
		ПроизводительСтроки = атСервер.ПолучитьСопоставленнуюСсылку(СтруктураСловаря,, Ложь);
		Если ПроизводительСтроки = Неопределено Тогда
			// Первое обращение к производителю!
			ПроизводительСтроки = атСервер.НайтиСоответствиеЭлементу(СтруктураСловаря);
			Если Не ЗначениеЗаполнено(ПроизводительСтроки) И Создавать Тогда
				ПроизводительСтроки = атСервер.СоздатьОбъектИБ("Производители", СтруктураСловаря);
			КонецЕсли;
			Если ЗначениеЗаполнено(ПроизводительСтроки) Тогда
				атКлиент.УстановитьСоответствие(СтруктураСловаря, ПроизводительСтроки, Истина);
			ИначеЕсли Не Создавать Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		Возврат ПроизводительСтроки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекСтрока.Производитель) Тогда
		ПроизводительСтроки = Справочники.Номенклатура.НайтиПроизводителяПоНаименованию(ТекСтрока.Производитель);
		Если ЗначениеЗаполнено(ПроизводительСтроки) Тогда
			Возврат ПроизводительСтроки;
		КонецЕсли;
		Если Создавать Тогда
			// Нужно создать производителя!
			НовыйОбъект = Справочники.Производители.СоздатьЭлемент();
			НовыйОбъект.Заполнить(Неопределено);
			НовыйОбъект.УстановитьНовыйКод();
			НовыйОбъект.Наименование = ТекСтрока.Производитель;
			НовыйОбъект.АртикулыОбязательны = Истина;
			Попытка
				НовыйОбъект.Записать();
				Возврат НовыйОбъект.Ссылка;
			Исключение КонецПопытки;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	// Ничего в итоге не нашли и не вернулись!
	Возврат Справочники.Производители.ПустаяСсылка();
	
КонецФункции

Функция СоздатьНоменклатуруСтроки(ТекСтрока, ЭтаФорма, ВыборПользователя=Неопределено) Экспорт 
	
	// Создавать не будем, так как далее пользователь может отказаться от создания номенклатуры
	ПроизводительСтроки = НайтиПроизводителяСтроки(ТекСтрока, Ложь);
	
	Если ПроизводительСтроки <> Неопределено Тогда
		ТекСтрока.Производитель = ПроизводительСтроки;
		// Какой-то производитель есть в строке, попробуем найти номенклатуру
		НоменклатураСтроки = атСервер.НайтиНоменклатуру(ТекСтрока.АртикулДляПоиска, ПроизводительСтроки);
		Если ЗначениеЗаполнено(НоменклатураСтроки) Тогда
			Попытка
				ТекСтрока.Номенклатура = НоменклатураСтроки;
			Исключение КонецПопытки;
			Возврат НоменклатураСтроки;
		КонецЕсли;
	КонецЕсли;
	
	// Можно поискать через Словарь Номенклатура, если в строке есть НоменклатураКод!
	СтруктураСловаряНоменклатура = Неопределено;
	Попытка
		Если ЗначениеЗаполнено(ТекСтрока.НоменклатураКод) Тогда
			СтруктураСловаряНоменклатура = атСервер.ПолучитьОбъект(Новый Структура("ЭтоСсылка, Тип, Код, Наименование", Истина, "Номенклатура", ТекСтрока.НоменклатураКод, ТекСтрока.Номенклатура));
			НоменклатураСтроки = атСервер.НайтиСоответствиеЭлементу(СтруктураСловаряНоменклатура);
			Если ЗначениеЗаполнено(НоменклатураСтроки) Тогда
				ТекСтрока.Номенклатура = НоменклатураСтроки;
				атКлиент.УстановитьСоответствие(СтруктураСловаряНоменклатура, НоменклатураСтроки, Истина);
				Возврат НоменклатураСтроки;
			КонецЕсли;
		КонецЕсли;
	Исключение КонецПопытки;
	
	Если ВыборПользователя = "Отмена" Тогда
		// Те позиции которые нашлись ранее нас устраивают,
		// а дальше мы ничего не будем создавать!
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.Номенклатура) Тогда
		Предупреждение("Нет прав добавлять новые позиции номенклатуры");
		ВыборПользователя = "Отмена";
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВыборПользователя = Неопределено Тогда
		ВариантСоздания = обПраво("АвтоматическоеСозданиеНоменклатурыИзПрайсЛистовКонтрагентов", глПрава);
		Если ВариантСоздания = Перечисления.ВариантыОтветов.Спрашивать Тогда
			Ответ = Вопрос("Создать новый элемент Номенклатуры?
				|<Да> - Открыть форму элемента,
				|<Нет> - Создать автоматически,
				|<Отмена> - Отменить действие.",
				РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Отмена Тогда
				ВыборПользователя = "Отмена";
				Возврат Неопределено;
			КонецЕсли;
			ВыборПользователя = (Ответ = КодВозвратаДиалога.Да);
		ИначеЕсли ВариантСоздания = Перечисления.ВариантыОтветов.Да Тогда
			// Интерактивное создание
			ВыборПользователя = Ложь;
		Иначе
			ВыборПользователя = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ИнтерактивноеДобавлениеНоменклатуры = ВыборПользователя;
	
	Если ПроизводительСтроки = Неопределено Тогда
		// Создадим производителя и попробуем найти номенклатуру
		ПроизводительСтроки = НайтиПроизводителяСтроки(ТекСтрока, Истина);
		ТекСтрока.Производитель = ПроизводительСтроки;
		НоменклатураСтроки = атСервер.НайтиНоменклатуру(ТекСтрока.АртикулДляПоиска, ПроизводительСтроки);
		Если ЗначениеЗаполнено(НоменклатураСтроки) Тогда
			Попытка
				ТекСтрока.Номенклатура = НоменклатураСтроки;
			Исключение КонецПопытки;
			Возврат НоменклатураСтроки;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураСловаряНоменклатура <> Неопределено И Не ИнтерактивноеДобавлениеНоменклатуры Тогда
		// Это у нас номенклатура из сервиса, которую нужно создать автоматически
		НоменклатураСтроки = атСервер.СоздатьОбъектИБ("Номенклатура", СтруктураСловаряНоменклатура);
		Попытка
			ТекСтрока.Номенклатура = НоменклатураСтроки;
		Исключение КонецПопытки;
		УстановитьСоответствие(СтруктураСловаряНоменклатура, НоменклатураСтроки, Истина);
		Сообщить("Записан справочник номенклатуры <" + спПолучитьНаименование(НоменклатураСтроки) + ">");
		Оповестить("ЗаписанаНоменклатура", НоменклатураСтроки, ЭтаФорма);
		Возврат НоменклатураСтроки;
	КонецЕсли;
	
	// Ничего не нашлось ранее
	// Осталось только создать новую позицию номенклатуры
	
	НоменклатураОбъект = Справочники.Номенклатура.СоздатьЭлемент();
	НоменклатураОбъект.УстановитьНовыйКод();
	
	НоменклатураОбъект.Артикул          = ТекСтрока.Артикул;
	НоменклатураОбъект.АртикулДляПоиска = ТекСтрока.АртикулДляПоиска;
	НоменклатураОбъект.Производитель    = ПроизводительСтроки;
	
	Попытка
		НоменклатураОбъект.Наименование       = ТекСтрока.Наименование;
		НоменклатураОбъект.НаименованиеПолное = ТекСтрока.Наименование;
	Исключение
		НоменклатураОбъект.Наименование       = ТекСтрока.Номенклатура;
		НоменклатураОбъект.НаименованиеПолное = ТекСтрока.Номенклатура;
	КонецПопытки;
	
	Если ЗначениеЗаполнено(НоменклатураОбъект.Производитель) Тогда
		НоменклатураОбъект.СтранаПроисхождения = НоменклатураОбъект.Производитель.СтранаПроисхождения;
	КонецЕсли;
	
	Попытка
		Если НЕ ПустаяСтрока(ТекСтрока.НаименованиеИностранное) Тогда
			НоменклатураОбъект.НаименованиеИностранное = ТекСтрока.НаименованиеИностранное;
		КонецЕсли;
	Исключение КонецПопытки;
	
	Попытка
		НоменклатураОбъект.Вес = ТекСтрока.Вес;
	Исключение КонецПопытки;
	Попытка
		НоменклатураОбъект.Объем = ТекСтрока.Объем;
	Исключение КонецПопытки;
	
	Попытка
		Если ТипЗнч(ТекСтрока.Размещение) = Тип("СправочникСсылка.ПрайсЛистыКонтрагентов") Тогда
			НоменклатурнаяГруппа = ТекСтрока.Размещение.НоменклатурнаяГруппа;
		КонецЕсли;
	Исключение
		НоменклатурнаяГруппа = атСервер.ПолучитьНастройку("Номенклатура", "НоменклатурнаяГруппа");
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		НоменклатурнаяГруппа = Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
	НоменклатураОбъект.Родитель        = НоменклатурнаяГруппа;
	НоменклатураОбъект.ПроцентНаценки  = НоменклатураОбъект.Родитель.ПроцентНаценки;
	НоменклатураОбъект.ТипНоменклатуры = ?(обЗначениеНеЗаполнено(НоменклатурнаяГруппа.ТипНоменклатуры), Справочники.ТипыНоменклатуры.Штучный, НоменклатурнаяГруппа.ТипНоменклатуры);
	НоменклатураОбъект.СтавкаНДС       = ?(обЗначениеНеЗаполнено(НоменклатурнаяГруппа.СтавкаНДС), Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, НоменклатурнаяГруппа.СтавкаНДС);
	НоменклатураОбъект.ВалютаУчета     = ?(обЗначениеНеЗаполнено(НоменклатурнаяГруппа.ВалютаУчета), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), НоменклатурнаяГруппа.ВалютаУчета);
	НоменклатураОбъект.ВидНоменклатуры = ?(обЗначениеНеЗаполнено(НоменклатураОбъект.ТипНоменклатуры.ВидНоменклатуры), Перечисления.ВидыНоменклатуры.Товар, НоменклатураОбъект.ТипНоменклатуры.ВидНоменклатуры);
	НоменклатураОбъект.БазоваяЕдиницаИзмерения = ?(обЗначениеНеЗаполнено(НоменклатураОбъект.ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения), Константы.ОсновнаяЕдиницаИзмеренияКоличества.Получить(), НоменклатураОбъект.ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения);
	
	//Если флаг взведен, то открываем новую карточку в модальном режиме
	Если ИнтерактивноеДобавлениеНоменклатуры Тогда
		ФормаСправочника = НоменклатураОбъект.ПолучитьФорму(, ЭтаФорма);
		ФормаСправочника.ОткрытьМодально();
	Иначе
		//Если флаг не взведен, то просто сохраняем карточку
		Попытка
			НоменклатураОбъект.Записать();
			Сообщить("Записан справочник номенклатуры <" + спПолучитьНаименование(НоменклатураОбъект) + ">");
			Попытка // Возможна ошибка при записи основной единицы измерения (если не задана базовая)
				НоменклатураОбъект.ОсновнаяЕдиницаИзмерения = НоменклатураОбъект.ПолучитьЕдиницуИзмеренияПоБазовой();
				НоменклатураОбъект.Записать();
			Исключение
				Сообщить("Ошибка записи основной единицы измерения для номенклатуры <" + спПолучитьНаименование(НоменклатураОбъект) + ">");
			КонецПопытки;
		Исключение
			Сообщить("Ошибка записи номенклатуры <"+спПолучитьНаименование(НоменклатураОбъект)+">");
			// Откроем интерактивно, что бы пользователь мог исправить причину исключения
			ФормаСправочника = НоменклатураОбъект.ПолучитьФорму(, ЭтаФорма);
			ФормаСправочника.ОткрытьМодально();
		КонецПопытки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НоменклатураОбъект.Ссылка) Тогда
		//оповещение источника о записи, чтобы спозиционироваться на добавленной номенклатуре
		Попытка 
			ТекСтрока.Номенклатура = НоменклатураОбъект.Ссылка
		Исключение КонецПопытки;
		Оповестить("ЗаписанаНоменклатура", НоменклатураОбъект.Ссылка, ЭтаФорма);
	КонецЕсли;
	
	Возврат НоменклатураОбъект.Ссылка;
	
КонецФункции //СоздатьНоменклатуруСтроки()


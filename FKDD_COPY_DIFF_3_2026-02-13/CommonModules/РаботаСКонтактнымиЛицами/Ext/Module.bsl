
#Область ПроцедурыИФункцииПоРаботеСРегистромСведений

// проверка цепочки на цекличность
//
Функция ЗаписьВозможна(Ведомый, Ведущий) Экспорт
	
	Если Ведомый = Ведущий Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТаблицаВедущих = Неопределено;
	
	Если ПолучитьВедущих(Ведущий, ТаблицаВедущих) Тогда
		Для Каждого Строка Из ТаблицаВедущих Цикл
			Если НЕ ЗаписьВозможна(Ведомый, Строка.Ведущий) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ЗаписатьВедомых(Ссылка, Записи) Экспорт
	
	// создаем набор записей и устанавливаем отбор
	Набор = РегистрыСведений.КонтактныеЛицаКонтрагентов.СоздатьНаборЗаписей();
	Набор.Отбор.Ведущий.Установить(Ссылка);
	
	Набор.Прочитать(); Набор.Очистить();
	
	Для Каждого Запись Из Записи Цикл
		ЗаполнитьЗначенияСвойств(Набор.Добавить(), Запись);
	КонецЦикла;
	
	Попытка
		Набор.Записать();
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ЗаписатьВедущих(Ссылка, Записи) Экспорт
	
	// создаем набор записей и устанавливаем отбор
	Набор = РегистрыСведений.КонтактныеЛицаКонтрагентов.СоздатьНаборЗаписей();
	Набор.Отбор.Ведомый.Установить(Ссылка);
	
	Набор.Прочитать();
	
	Для Каждого Запись Из Записи Цикл
		ЗаполнитьЗначенияСвойств(Набор.Добавить(), Запись);
	КонецЦикла;
	
	Попытка
		Набор.Записать(Истина);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииОбновления

Функция ОбновитьКонтактныеЛица() Экспорт
	
	Набор = РегистрыСведений.КонтактныеЛицаКонтрагентов.СоздатьНаборЗаписей();
	Набор.Прочитать();
	
	Если Набор.Количество() > 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Набор.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.ВедушийКонтрагент КАК Ведущий,
	|	Контрагенты.Ссылка КАК Ведомый,
	|	Контрагенты.ВидОтношений КАК ВидОтношений,
	|	Контрагенты.Должность,
	|	ИСТИНА КАК Использование,
	|	ИСТИНА КАК Основной
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Ведущий) Тогда
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Набор.Записать(Истина);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначениия


// Создание прототипа таблицы записей
//
Функция ИнициализироватьТаблицуЗаписей() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Автомобили"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Сотрудники"));
	
	Таблица.Колонки.Добавить("Ведущий"              , Новый ОписаниеТипов(МассивТипов)                                  , "Ведущий");
	Таблица.Колонки.Добавить("Ведомый"              , Новый ОписаниеТипов(МассивТипов)                                  , "Ведомый");
	Таблица.Колонки.Добавить("Использование"        , Новый ОписаниеТипов("Булево")                                , "Использование");
	Таблица.Колонки.Добавить("Основной"             , Новый ОписаниеТипов("Булево")                                , "Основной");
	Таблица.Колонки.Добавить("ВидОтношений"         , Новый ОписаниеТипов("СправочникСсылка.ВидыВзаимоотношений")  , "Вид взаимоотношений");
	Таблица.Колонки.Добавить("Должность"            , Новый ОписаниеТипов("СправочникСсылка.Должности")            , "Должность");
	
	Возврат Таблица;
	
КонецФункции

// возвращает ведущих контрагентов
//
// Параметры:
//	Ведомый - ссылка на ведомого контрагента
//	Типы - типы получаемых ведомых
//	ВидыОтношений - виды получаемых отношений
//
// Возвращает таблицу с ведущими контрагентами
//
Функция ПолучитьВедущих(Ведомый, Результат, Типы = Неопределено, ВидыОтношений = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтактныеЛицаКонтрагентов.Ведущий,
	|	КонтактныеЛицаКонтрагентов.Основной,
	|	КонтактныеЛицаКонтрагентов.ВидОтношений,
	|	КонтактныеЛицаКонтрагентов.Должность
	|ИЗ
	|	РегистрСведений.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	|ГДЕ
	|	КонтактныеЛицаКонтрагентов.Ведомый = &Ведомый
	|	И КонтактныеЛицаКонтрагентов.Использование = ИСТИНА"
	+?(Типы <> Неопределено," И ТИПЗНАЧЕНИЯ(КонтактныеЛицаКонтрагентов.Ведущий) В (&Типы)","")
	+?(ВидыОтношений <> Неопределено, " И КонтактныеЛицаКонтрагентов.ВидОтношений  В (&ВидОтношений)", "")+"";
	
	Запрос.УстановитьПараметр("Ведомый", Ведомый);
	Запрос.УстановитьПараметр("ВидОтношений", ВидыОтношений);
	Запрос.УстановитьПараметр("Типы", Типы);
	
	РезультатЗапроса = Запрос.Выполнить();
	Результат = РезультатЗапроса.Выгрузить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// возвращает ведомых контрагентов
//
// Параметры:
//	Ведомый - ссылка на ведомого контрагента
//	Типы - типы получаемых ведомых
//	ВидыОтношений - виды получаемых отношений
//
// Возвращает таблицу с ведущими контрагентами
//
Функция ПолучитьВедомых(Ведущий, Результат, Типы = Неопределено, ВидыОтношений = Неопределено) Экспорт
	//БИЗТЕХ 18.12.2023 КОВАЛЬ А.Д.++
	//Добавлены строки в запрос
	//|	КонтактныеЛицаКонтрагентов.Доверенность,
	//|	КонтактныеЛицаКонтрагентов.СрокДействияС,
	//|	КонтактныеЛицаКонтрагентов.СрокДействияПо
	//БИЗТЕХ 18.12.2023 КОВАЛЬ А.Д.--

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтактныеЛицаКонтрагентов.Ведомый,
	|	КонтактныеЛицаКонтрагентов.Основной,
	|	КонтактныеЛицаКонтрагентов.ВидОтношений,
	|	КонтактныеЛицаКонтрагентов.Должность,
	|	КонтактныеЛицаКонтрагентов.Доверенность,
	|	КонтактныеЛицаКонтрагентов.СрокДействияС,
	|	КонтактныеЛицаКонтрагентов.СрокДействияПо
	|ИЗ
	|	РегистрСведений.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	|ГДЕ
	|	КонтактныеЛицаКонтрагентов.Ведущий = &Ведущий
	|	И КонтактныеЛицаКонтрагентов.Использование = ИСТИНА"
	+?(Типы <> Неопределено," И ТИПЗНАЧЕНИЯ(КонтактныеЛицаКонтрагентов.Ведомый) В (&Типы)","")
	+?(ВидыОтношений <> Неопределено, " И КонтактныеЛицаКонтрагентов.ВидОтношений  В (&ВидОтношений)", "");
	
	Запрос.УстановитьПараметр("Ведущий", Ведущий);
	Запрос.УстановитьПараметр("ВидОтношений", ВидыОтношений);
	Запрос.УстановитьПараметр("Типы", Типы);
	
	РезультатЗапроса = Запрос.Выполнить();
	Результат = РезультатЗапроса.Выгрузить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// попределение ведущего контрагента
Функция ПолучитьВедущегоКонтрагента(Ведомый) Экспорт
	
	ТаблицаВедущих = Неопределено;
	ТипыКонтрагентов = Новый Массив;
	ТипыКонтрагентов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	
	Если ПолучитьВедущих(Ведомый, ТаблицаВедущих, ТипыКонтрагентов) Тогда
		ТаблицаВедущих.Сортировать("Основной");
		
		Для Каждого Ведущий Из ТаблицаВедущих Цикл
			Если НЕ (Ведущий.Ведущий.ВидКонтрагента = Перечисления.ВидыКонтрагентов.КонтактноеЛицо) Тогда
				Возврат Ведущий.Ведущий;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если обЭтотТип(Ведомый, Тип("СправочникСсылка.Контрагенты")) И Ведомый.ВидКонтрагента <> Перечисления.ВидыКонтрагентов.КонтактноеЛицо Тогда
		Возврат Ведомый;
	Иначе
		Возврат Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// проверяет является ли контрагент ведущим для текущего
Функция ЭтоВедущий(ВедомыйКонтрагент, ВедущийКонтрагент, ТолькоОсновной = Ложь) Экспорт
	
	ТаблицаВедущих = Неопределено;
	Если ПолучитьВедущих(ВедомыйКонтрагент, ТаблицаВедущих) Тогда
		Поиск = Новый Структура("Ведущий", ВедущийКонтрагент);
		Если ТолькоОсновной Тогда
			Поиск.Вставить("Основной", Истина);
		КонецЕсли;
		
		Возврат (ТаблицаВедущих.НайтиСтроки(Поиск).Количество() > 0);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
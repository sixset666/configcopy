
// Записывает версию объекта в регистр сведений
Процедура ЗаписатьВерсиюОбъекта(знач Ссылка,
                                знач ЧислоВерсийОбъекта,
                                знач ХранилищеДанных) Экспорт  
 		МенеджерЗаписиВерсииОбъектов = РегистрыСведений.верс_ВерсииОбъектов.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписиВерсииОбъектов.Объект        	= Ссылка;
		МенеджерЗаписиВерсииОбъектов.ДатаВерсии    	= ТекущаяДата();
		МенеджерЗаписиВерсииОбъектов.ВерсияОбъекта 	= ХранилищеДанных;
		МенеджерЗаписиВерсииОбъектов.НомерВерсии	= ЧислоВерсийОбъекта + 1;
		МенеджерЗаписиВерсииОбъектов.АвторВерсии	= ПараметрыСеанса.Пользователь;
		МенеджерЗаписиВерсииОбъектов.УдалитьСжато 	= Истина;
		
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			МенеджерЗаписиВерсииОбъектов.СуммаДокумента	= Ссылка.СуммаДокумента; 
			МенеджерЗаписиВерсииОбъектов.СуммаТоваров	= Ссылка.СуммаНоменклатурыДокумента; 
			МенеджерЗаписиВерсииОбъектов.СуммаРабот		= Ссылка.СуммаРаботДокумента; 
		ИначеЕсли НЕ Ссылка.Метаданные().Реквизиты.Найти("СуммаДокумента") = Неопределено Тогда	
			МенеджерЗаписиВерсииОбъектов.СуммаДокумента	= Ссылка.СуммаДокумента; 
		КонецЕсли;
		
		Если верс_ВерсионированиеОбъектов.ЕстьРИБ() Тогда
			МенеджерЗаписиВерсииОбъектов.УзелРИБ = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Префикс;
		КонецЕсли;	
			
		МенеджерЗаписиВерсииОбъектов.Записать();  
	
КонецПроцедуры

// Возвращает количество версий объекта переданного по ссылке
Функция ПолучитьКоличествоВерсийОбъекта(Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	| ВЫБРАТЬ ЕСТЬNULL(МАКСИМУМ(НомерВерсии), 0) КАК НомерВерсии
	| ИЗ РегистрСведений.верс_ВерсииОбъектов
	| ГДЕ Объект = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	НомерВерсии = Выборка.НомерВерсии;
	
	Возврат НомерВерсии;
	
КонецФункции

Процедура ВыполнитьСжатиеВерсийОбъектовПоРегламентномуЗаданию() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ Объект,  НомерВерсии, ВерсияОбъекта 
	                | ИЗ РегистрСведений.верс_ВерсииОбъектов 
	                | ГДЕ УдалитьСжато = Ложь";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДвоичныеДанные  = Выборка.ВерсияОбъекта.Получить();
		ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		
		МенеджерЗаписиВерсииОбъектов = РегистрыСведений.верс_ВерсииОбъектов.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписиВерсииОбъектов.Объект        = Выборка.Объект;
		МенеджерЗаписиВерсииОбъектов.НомерВерсии   = Выборка.НомерВерсии;
		
		МенеджерЗаписиВерсииОбъектов.Прочитать();
		МенеджерЗаписиВерсииОбъектов.ВерсияОбъекта = ХранилищеДанных;
		МенеджерЗаписиВерсииОбъектов.УдалитьСжато  = Истина;
		МенеджерЗаписиВерсииОбъектов.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

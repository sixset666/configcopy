////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ СО СПРАВОЧНИКАМИ И ПЛАНАМИ ВИДОВ ХАРАКТЕРИСТИК
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Если Клиент Тогда
	
// Выводит вопрос на запись нового элемента и записывает его
// Вызывается перед попыткой ввести подчиненные (связанные) данные для элемента справочника
// Например, для контрагента при попытки ввести договор, счет, КИ и т.п.
// 
// Параметры:
//   ЭтотОбъект    - СправочникОбъект - записываемый объект
//   ФормаЭлемента - Форма - форма, в которой производится запись объекта
// 
// Возвращаемое значение:
//  Булево - признак того, за запись прошла успешно
//
Функция спЗаписатьОбъектПриВводеСвязанныхДанных(ЭтотОбъект, ФормаЭлемента) Экспорт
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Ответ = Вопрос("Элемент еще не записан, ввод связанных с ним данных не возможен. Записать элемент?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Попытка
				Отказ = НЕ ФормаЭлемента.ЗаписатьВФорме();
			Исключение
				Отказ = Истина;
			КонецПопытки;
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Ложь;
	КонецЕсли;

	Возврат Отказ;
КонецФункции // спЗаписатьОбъектПриВводеСвязанныхДанных()

#КонецЕсли

// Обработка изменения реквизитов справочников
// 
// Параметры:
//  ЭтотОбъект	 – СправочникОбъект	– Справочник, реквизит которого обрабатывается.
//  Имя			 – Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	 – Форма				– Ссылка на форму справочника. Если значение неопределено,
//										  производится программная обработка реквизитов.
//  ТекСтрока	 – СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры – Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// 
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) Экспорт
	
	Результат = Истина;
	
	//<< ЧалапАЮ БизТех 10.09.2024
	Если Имя = "верс_ИсторияИзменений" Тогда
		верс_ВерсионированиеОбъектов.ОткрытьОтчетИсторияИзмененийОбъектов(ЭтаФорма);
	КонецЕсли;
	// ЧалапАЮ БизТех 10.09.2024>>

	
	Если Имя="Печать" Тогда
		#Если Клиент Тогда   
			спОсновныеДействияФормыПечать(ЭтаФорма, ДопПараметры);
		#КонецЕсли
	ИначеЕсли Имя="кнЗаметки" Тогда
		#Если Клиент Тогда   
			спЗаметки(ЭтотОбъект.Ссылка,ЭтаФорма);
		#КонецЕсли
	ИначеЕсли Имя = "РедактироватьКод" Тогда
		#Если Клиент Тогда
			спДействияФормыРедактироватьКод(ЭтаФорма, ЭтотОбъект);
		#КонецЕсли
		
	ИначеЕсли Имя = "ПоискСсылок" Тогда
		#Если Клиент Тогда
			спДействияФормыПоискСсылок(ЭтаФорма, ЭтотОбъект);
		#КонецЕсли
		
	ИначеЕсли Имя = "ДобавитьВИзбранное" Тогда
		#Если Клиент Тогда
			Попытка	
				ИмяФормы = ДопПараметры.ИмяФормы;
			Исключение
				ИмяФормы = Неопределено;
			КонецПопытки;	
			спДействияФормыДобавитьВИзбранное(ЭтаФорма, ЭтотОбъект, ИмяФормы);
		#КонецЕсли
			
	ИначеЕсли Имя = "ВыгрузкаВБухгалтерию" Тогда
		#Если Клиент Тогда
			удВыгрузитьВбухгалтерию(ЭтаФорма,ЭтотОбъект);
		#КонецЕсли
		
	ИначеЕсли Имя = "ИсторияОбъекта" Тогда
		#Если Клиент Тогда
			спДействияФормыИсторияОбъекта(ЭтаФорма, ЭтотОбъект);
		#КонецЕсли
		
	ИначеЕсли Имя = "ВыгрузкаВТабличныйДокумент" Тогда
		#Если Клиент Тогда
			спДействияФормыВыгрузкаВТабличныйДокумент(ЭтаФорма, ЭтотОбъект);
		#КонецЕсли
			
	ИначеЕсли Имя = "СоздатьНапоминание" Тогда
		#Если Клиент Тогда
			спДействияФормыСоздатьНапоминание(ЭтаФорма, ЭтотОбъект);
		#КонецЕсли
		
	ИначеЕсли Имя = "СоздатьПисьмоВТехПоддержку" Тогда
		#Если Клиент Тогда
			элНаписатьПисьмоВСлужбуТехПоддержки(ЭтаФорма, ЭтотОбъект);
		#КонецЕсли
		
	ИначеЕсли Имя = "ЗаменитьНаОбъект" Тогда
		#Если Клиент Тогда
			Если НЕ ЭтотОбъект.ЭтоНовый() Тогда
				ФормаЗаписи = РегистрыСведений.ОбъектыДляЗамены.ПолучитьФормуРедактированияЗаписи();
				ФормаЗаписи.ИдентификаторЗаменяемого = ЗначениеВСтрокуВнутр(ЭтотОбъект.Ссылка);
				Если ФормаЗаписи.Открыта() Тогда
					ФормаЗаписи.Активизировать();
				Иначе
					ФормаЗаписи.Открыть();
				КонецЕсли;
			Иначе
				Сообщить("Невозможно создать запись о замене для нового объекта!");
			КонецЕсли;
		#КонецЕсли
		
	ИначеЕсли Имя = "ДополнительнаяФорма" Тогда
		#Если Клиент Тогда
			Если ЭтотОбъект.ЭтоГруппа Тогда
				НайденнаяФорма = ЭтотОбъект.Метаданные().Формы.Найти("ФормаГруппыДополнительная");
			Иначе
				НайденнаяФорма = ЭтотОбъект.Метаданные().Формы.Найти("ФормаЭлементаДополнительная");
			КонецЕсли;
			Если НайденнаяФорма <> Неопределено Тогда
				Форма = ЭтотОбъект.ПолучитьФорму(НайденнаяФорма.Имя);
				Форма.ВладелецФормы = ЭтаФорма;
				Форма.Открыть();		
			КонецЕсли;
		#КонецЕсли
		
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спОбработкаРеквизита()

// Возвращает нименование элемента из справочника "Номенклатура"
// 
// Параметры:
//  Элемент - Справочник 
// 
// Возвращаемое значение:
//  Строка - представление Номенклатуры, по возможности полное
//
Функция спПолучитьНаименование(Элемент) Экспорт
	Попытка
		Если НЕ обЗначениеНеЗаполнено(Элемент.НаименованиеПолное) Тогда
			Возврат СокрЛП(Элемент.НаименованиеПолное);
		КонецЕсли; 
	Исключение
	КонецПопытки;
	
	Попытка
		Возврат СокрЛП(Элемент.Наименование);
	Исключение
		Если ТипЗнч(Элемент) = Тип("ДокументСсылка.РеализацияТоваров") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.ЗакрытиеСмены") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.ВводОстатковТоваров") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.Инвентаризация") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.ВводОстатковТоваров") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.ВводОстатковТоваровВПроизводстве") ИЛИ 
			ТипЗнч(Элемент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			Возврат СокрЛП(Элемент.Ссылка);
		КонецЕсли;
		Возврат "";
	КонецПопытки; 
КонецФункции // ПолучитьНаименование()

// Возвращает последний документ переданного вида для объекта (организации, контрагента, сотрудника,
// контактного лица, номенклатуры)
// Параметры
//  Объект	        – Справочник                    - Владелец подтверждающего документа
//  ВидДокумента	– Перечисление.ВидыДокументов	– Вид подтверждающего документа 
// Возвращаемое значение:
//   Справочник.ПодтверждающиеДокументы   – Текущий подтверждающий документ
Функция спПолучитьПодтверждающийДокументОбъектаПоВиду(Объект,ВидДокумента = Неопределено) Экспорт
	ПодтверждающийДокумент = Справочники.ПодтверждающиеДокументы.ПустаяСсылка();
	ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПодтверждающиеДокументы.Ссылка КАК ПодтверждающийДокумент
	|ИЗ
	|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
	|ГДЕ
	|	ПодтверждающиеДокументы.Владелец = &Объект";
	Если ВидДокумента <> Неопределено Тогда 
		ТекстЗапроса = ТекстЗапроса + "
	|	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &ВидПодтверждающегоДокумента"
	КонецЕсли;
    ТекстЗапроса = ТекстЗапроса + "
	|	И ПодтверждающиеДокументы.Устаревший = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодтверждающиеДокументы.Текущий УБЫВ,
	|	ПодтверждающиеДокументы.ДатаВыдачи УБЫВ";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Объект",Объект);
	Запрос.УстановитьПараметр("ВидПодтверждающегоДокумента",ВидДокумента);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПодтверждающийДокумент = Выборка.ПодтверждающийДокумент			
	КонецЕсли;
	Возврат ПодтверждающийДокумент
КонецФункции // спПолучитьПодтверждающийДокументОбъектаПоВиду()

// Возвращает представление для Организаций или Контрагентов
// 
// Параметры
//  Элемент - Справочник.Организации, Справочник.Контрагенты>
//
// Возвращаемое значение:
//  Представление - Строка - Строка представления элемента справочника
//
Функция спПолучитьПредставление(Элемент,Знач СтруктураПараметров=Неопределено, ДополнительныеПараметры = Неопределено,РасчетныйСчетДокумента = Неопределено) Экспорт//бизтех
	
	Если ДополнительныеПараметры = Неопределено ИЛИ ТипЗнч(ДополнительныеПараметры) <> Тип("Структура") Тогда
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	КонецЕсли;
	
	ДляПечати = Ложь;
	ДополнительныеПараметры.Свойство("ДляПечати", ДляПечати);
	ДляПечати = ?(ДляПечати = Неопределено, Ложь, ДляПечати);
	РасчетныйСчетДокумента =Неопределено;
	ДополнительныеПараметры.Свойство("РасчетныйСчетДокумента", РасчетныйСчетДокумента);
	
	//Для начала посмотрим что за ссылку нам передали
	Если (ТипЗнч(Элемент)=Тип("СправочникСсылка.СкладыКомпании")) Тогда
		Если(НЕ Элемент.Организация = null) ИЛИ (НЕ Элемент.Организация = Неопределено) Тогда
			СкладНаименование = Строка(Элемент.Наименование);
			СкладОрганизация = Строка(Элемент.Организация);
			ПредставлениеСклад = Строка(СкладНаименование + Символы.ПС + "Организация: ");
			ПредставлениеСклад = СтрЗаменить(ПредставлениеСклад, " ,", ",");
					
				СкладЭлемент = Элемент.Организация;
				//Если структура параметров пустая, то будем действовать по умолчанию
				Если СтруктураПараметров=Неопределено Тогда
					СтруктураПараметров=Новый Структура("Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,ОКПО","","ИНН ","КПП ","","тел.: ","Код по ОКПО ");
				КонецЕсли;
				
				Представление="";
				
				//А теперь будем формировать представление согласно параметров
				Для каждого ПараметрПредставления Из СтруктураПараметров Цикл
					ЗначениеПараметра=Неопределено;
					Если ВРег(ПараметрПредставления.Ключ)="НАИМЕНОВАНИЕ" Тогда
						ЗначениеПараметра = спПолучитьНаименование(СкладЭлемент);
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="ИНН" Тогда
						Если ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.Организация.ИНН);
						Иначе
							ЗначениеПараметра=СокрЛП(СкладЭлемент.ИНН);
						КонецЕсли;
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="КПП" Тогда
						Если (ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.Организации")) ИЛИ
							((ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.Контрагенты"))И (СкладЭлемент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо)) Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.КПП);
						ИначеЕсли ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							Если ПустаяСтрока(СокрЛП(СкладЭлемент.КПП)) Тогда
								ЗначениеПараметра=СокрЛП(СкладЭлемент.Организация.КПП);
							Иначе
								ЗначениеПараметра=СокрЛП(СкладЭлемент.КПП);
							КонецЕсли; 
						КонецЕсли;
						
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="ОКПО" Тогда
						Если (ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.Организации")) ИЛИ
							((ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.Контрагенты"))И (СкладЭлемент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо)) Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.КодПоОКПО);
						ИначеЕсли ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.Организация.КодПоОКПО);
						КонецЕсли;
						
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="АДРЕСЮРИДИЧЕСКИЙ" ИЛИ
						ВРег(ПараметрПредставления.Ключ)="АДРЕСФАКТИЧЕСКИЙ" ИЛИ
						ВРег(ПараметрПредставления.Ключ)="ТЕЛЕФОНРАБОЧИЙ" Тогда
						ЗначениеПараметра=киПолучитьПредставлениеКИ(СкладЭлемент,Справочники.ВидыКонтактнойИнформации[ПараметрПредставления.Ключ]);
						Если обЗначениеНеЗаполнено(ЗначениеПараметра) И
							ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=киПолучитьПредставлениеКИ(СкладЭлемент.Организация,Справочники.ВидыКонтактнойИнформации[ПараметрПредставления.Ключ]);
						КонецЕсли; 
						
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="БАНКОВСКИЙСЧЕТ" Тогда
						Если ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.Организация.ОсновнойБанковскийСчет.НомерСчета);
						Иначе
							ЗначениеПараметра=СокрЛП(СкладЭлемент.ОсновнойБанковскийСчет.НомерСчета);
						КонецЕсли; 
						
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="БАНК" Тогда
						Если ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=СокрЛП(спПолучитьНаименование(СкладЭлемент.Организация.ОсновнойБанковскийСчет.Банк));
						Иначе
							ЗначениеПараметра=СокрЛП(спПолучитьНаименование(СкладЭлемент.ОсновнойБанковскийСчет.Банк));
						КонецЕсли; 
						
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="БИК" Тогда
						Если ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.Организация.ОсновнойБанковскийСчет.Банк.Код);
						Иначе
							ЗначениеПараметра=СокрЛП(СкладЭлемент.ОсновнойБанковскийСчет.Банк.Код);
						КонецЕсли;
						
					ИначеЕсли ВРег(ПараметрПредставления.Ключ)="КОРРСЧЕТ" Тогда
						Если ТипЗнч(СкладЭлемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
							ЗначениеПараметра=СокрЛП(СкладЭлемент.Организация.ОсновнойБанковскийСчет.Банк.КоррСчет);
						Иначе
							ЗначениеПараметра=СокрЛП(СкладЭлемент.ОсновнойБанковскийСчет.Банк.КоррСчет);
						КонецЕсли;
					КонецЕсли;
					
					Если обЗначениеНеЗаполнено(ЗначениеПараметра) Тогда
						Продолжить;
					КонецЕсли; 
					Если НЕ ПустаяСтрока(Представление) Тогда
						Представление=Представление+", ";
					КонецЕсли;
					Если ПараметрПредставления.Значение<>Неопределено Тогда
						Представление=Представление+ПараметрПредставления.Значение;
					КонецЕсли; 
					Представление=Представление+ЗначениеПараметра;
				КонецЦикла; 
				
				Возврат Строка(ПредставлениеСклад+Представление); 
		ИначеЕсли(Элемент.Организация = null) ИЛИ (Элемент.Организация = Неопределено) Тогда 
			Возврат Строка(Элемент.Наименование);
		КонецЕсли;
	ИначеЕсли (ТипЗнч(Элемент)<>Тип("СправочникСсылка.Организации")) И
		(ТипЗнч(Элемент)<>Тип("СправочникСсылка.ПодразделенияКомпании")) И
		(ТипЗнч(Элемент)<>Тип("СправочникСсылка.Контрагенты")) Тогда
		Возврат "Неправильный тип объекта!";
	КонецЕсли; 
	
	//Если структура параметров пустая, то будем действовать по умолчанию
	Если СтруктураПараметров=Неопределено Тогда
		СтруктураПараметров=Новый Структура("Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,ОКПО","","ИНН ","КПП ","","тел.: ","Код по ОКПО ");
	КонецЕсли;
	
	Представление="";
	
	//А теперь будем формировать представление согласно параметров
	Для каждого ПараметрПредставления Из СтруктураПараметров Цикл
		ЗначениеПараметра = Неопределено;
		Если ВРег(ПараметрПредставления.Ключ) = "НАИМЕНОВАНИЕ" Тогда
			Если ДляПечати И (ТипЗнч(Элемент) = Тип("СправочникСсылка.ПодразделенияКомпании")) Тогда
				ЗначениеПараметра = спПолучитьНаименование(Элемент.Организация);
			Иначе
				ЗначениеПараметра = спПолучитьНаименование(Элемент);
			КонецЕсли;
		ИначеЕсли ВРег(ПараметрПредставления.Ключ) = "ИНН" Тогда
			Если ТипЗнч(Элемент) = Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				ЗначениеПараметра = СокрЛП(Элемент.Организация.ИНН);
			ИначеЕсли ТипЗнч(Элемент) = Тип("СправочникСсылка.Контрагенты")
				И Элемент.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение Тогда
				ЗначениеПараметра = СокрЛП(Элемент.ГоловнойКонтрагент.ИНН);
			Иначе
				ЗначениеПараметра = СокрЛП(Элемент.ИНН);
			КонецЕсли;
		ИначеЕсли ВРег(ПараметрПредставления.Ключ) = "КПП" Тогда
			Если (ТипЗнч(Элемент) = Тип("СправочникСсылка.Организации")) Тогда
				Если ДополнительныеПараметры.Свойство("ИспользоватьКПППодразделения") Тогда
					Подразделение = ДополнительныеПараметры.ПодразделениеКомпании;
					Если НЕ обЗначениеНеЗаполнено(Подразделение) И НЕ обЗначениеНеЗаполнено(Подразделение.КПП) Тогда
						ЗначениеПараметра = СокрЛП(Подразделение.КПП);
					Иначе
						ЗначениеПараметра = СокрЛП(Элемент.КПП);
					КонецЕсли;
				Иначе
					ЗначениеПараметра = СокрЛП(Элемент.КПП);
				КонецЕсли;
			ИначеЕсли ТипЗнч(Элемент) = Тип("СправочникСсылка.Контрагенты")
				И (Элемент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо
					ИЛИ Элемент.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение) Тогда
				
				ЗначениеПараметра = СокрЛП(Элемент.КПП);
				
			ИначеЕсли ТипЗнч(Элемент) = Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				Если ПустаяСтрока(СокрЛП(Элемент.КПП)) Тогда
					ЗначениеПараметра = СокрЛП(Элемент.Организация.КПП);
				Иначе
					ЗначениеПараметра = СокрЛП(Элемент.КПП);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="ОКПО" Тогда
			Если (ТипЗнч(Элемент)=Тип("СправочникСсылка.Организации")) ИЛИ
				((ТипЗнч(Элемент)=Тип("СправочникСсылка.Контрагенты"))И (Элемент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо ИЛИ Элемент.ФормаСобственности=Перечисления.ФормыСобственности.ОбособленноеПодразделение)) Тогда
				ЗначениеПараметра=СокрЛП(Элемент.КодПоОКПО);
			ИначеЕсли ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				ЗначениеПараметра=СокрЛП(Элемент.Организация.КодПоОКПО);
			КонецЕсли;
			
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="АДРЕСЮРИДИЧЕСКИЙ" ИЛИ
			ВРег(ПараметрПредставления.Ключ)="АДРЕСФАКТИЧЕСКИЙ" Тогда
			ЗначениеПараметра=киПолучитьПредставлениеКИ(Элемент,Справочники.ВидыКонтактнойИнформации[ПараметрПредставления.Ключ]);
			//если не заполнен фактический адрес подразделения, необходимо взять юридический адрес организации
			//постановление Госкомстата РФ № 132 от 25.12.1998г. (о ТОРГ-12)
			Если обЗначениеНеЗаполнено(ЗначениеПараметра) И
				ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				ЗначениеПараметра=киПолучитьПредставлениеКИ(Элемент.Организация,Справочники.ВидыКонтактнойИнформации["АДРЕСЮРИДИЧЕСКИЙ"]);
			КонецЕсли; 
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="ТЕЛЕФОНРАБОЧИЙ" Тогда
			ЗначениеПараметра=киПолучитьПредставлениеКИ(Элемент,Справочники.ВидыКонтактнойИнформации[ПараметрПредставления.Ключ]);
			Если обЗначениеНеЗаполнено(ЗначениеПараметра) И
				ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				ЗначениеПараметра=киПолучитьПредставлениеКИ(Элемент.Организация,Справочники.ВидыКонтактнойИнформации[ПараметрПредставления.Ключ]);
			КонецЕсли; 
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="БАНКОВСКИЙСЧЕТ" Тогда
			Если обЗначениеНеЗаполнено(РасчетныйСчетДокумента) Тогда
				ЗначениеПараметра=СокрЛП(Элемент.ОсновнойБанковскийСчет.НомерСчета);
				Если обЗначениеНеЗаполнено(ЗначениеПараметра) И ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
					ЗначениеПараметра=СокрЛП(Элемент.Организация.ОсновнойБанковскийСчет.НомерСчета);
				КонецЕсли;
			Иначе
				ЗначениеПараметра=СокрЛП(РасчетныйСчетДокумента.НомерСчета);
			КонецЕсли;
			
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="БАНК" Тогда
			Если обЗначениеНеЗаполнено(РасчетныйСчетДокумента) Тогда
				ЗначениеПараметра=СокрЛП(спПолучитьНаименование(Элемент.ОсновнойБанковскийСчет.Банк));
				Если обЗначениеНеЗаполнено(ЗначениеПараметра) И ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
					ЗначениеПараметра=СокрЛП(спПолучитьНаименование(Элемент.Организация.ОсновнойБанковскийСчет.Банк));
				КонецЕсли;
			Иначе
				ЗначениеПараметра=СокрЛП(РасчетныйСчетДокумента.Банк);
			КонецЕсли;
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="БИК" Тогда
			Если обЗначениеНеЗаполнено(РасчетныйСчетДокумента) Тогда
				ЗначениеПараметра=СокрЛП(Элемент.ОсновнойБанковскийСчет.Банк.Код);
				Если обЗначениеНеЗаполнено(ЗначениеПараметра) И ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
					ЗначениеПараметра=СокрЛП(Элемент.Организация.ОсновнойБанковскийСчет.Банк.Код);
				КонецЕсли;
			Иначе
				ЗначениеПараметра=СокрЛП(РасчетныйСчетДокумента.Банк.Код);
			КонецЕсли;
		ИначеЕсли ВРег(ПараметрПредставления.Ключ)="КОРРСЧЕТ" Тогда
			Если обЗначениеНеЗаполнено(РасчетныйСчетДокумента) Тогда
				ЗначениеПараметра=СокрЛП(Элемент.ОсновнойБанковскийСчет.Банк.КоррСчет);
				Если обЗначениеНеЗаполнено(ЗначениеПараметра) И ТипЗнч(Элемент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
					ЗначениеПараметра=СокрЛП(Элемент.Организация.ОсновнойБанковскийСчет.Банк.КоррСчет);
				КонецЕсли;
			Иначе
				ЗначениеПараметра=СокрЛП(РасчетныйСчетДокумента.Банк.КоррСчет);
			КонецЕсли;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ЗначениеПараметра) Тогда
			Продолжить;
		КонецЕсли; 
		Если НЕ ПустаяСтрока(Представление) Тогда
			Представление=Представление+", ";
		КонецЕсли;
		Если ПараметрПредставления.Значение<>Неопределено Тогда
			Представление=Представление+ПараметрПредставления.Значение;
		КонецЕсли; 
		Представление=Представление+ЗначениеПараметра;
	КонецЦикла; 
	
	Возврат Представление;
КонецФункции // ПолучитьПредставление()

// Возвращает для печати КПП подразделения.
//
// Параметры:
//  Организация		- СправочникСсылка.Организация				- организации;
//  Подразделение	- СправочникСсылка.ПодразделениеКомпании	- подразделение документа.
//
// Возвращаемое значение:
//  Строка - Строка представления КПП.
//
Функция спПолучитьКППДляПечати(Организация, Подразделение) Экспорт
	
	Если НЕ обЗначениеНеЗаполнено(Подразделение) И НЕ обЗначениеНеЗаполнено(Подразделение.КПП) Тогда
		Возврат Подразделение.КПП;
	КонецЕсли;
	
	Возврат Организация.КПП;
	
КонецФункции // спПолучитьКППДляПечати()

// Возвращает необязательные параметры вызова функции спПолучитьПредставление()
//
// Возвращаемое значение:
//  Структура - параметры по умолчанию:
//    * ДляПечати - Булево - Истина, если получаем представление для подразделения
//                           и наименование нужно организации, а не подразделения,
//    * РасчетныйСчетДокумента - СправочникСсылка.РасчетныеСчета - расчетный счет для подстановки в представление,
//
Функция спСоздатьПараметрыПолученияПредставления () Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДляПечати", Ложь);
	Параметры.Вставить("РасчетныйСчетДокумента", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

// Проверяет корректность заполнения элемента
// по заполненности обязательных реквизитов и уникальности уникальных индексированных
//
// Параметры:
//  ЭтотОбъект   - СправочникОбъект - проверяемый объект
//  Ошибки       - строка - содержит описание ошибок заполнения
//  ДопРеквизиты - Структура - дополнительные реквизиты, обязательные для проверки. 
//  Заполнение   - Булево - если Истина, проверять на заполнение
//  Уникальность - Булево - если Истина, проверка уникальности реквизитов
//
// Возвращаемое значение:
//  Результат - Булево.
//
Функция спПроверитьКорректность(ЭтотОбъект, Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат=Истина;
	Если НЕ обПраво("ПроверкаЗаполненияСправочниковИДокументов",ЭтотОбъект.Права) Тогда
		Возврат Результат;
	КонецЕсли; 
	
	// Определимся это элемент или группа (для плана счетов всегда элемент)
	Попытка  ЭтоГруппа = ЭтотОбъект.ЭтоГруппа;
	Исключение ЭтоГруппа = Ложь;
	КонецПопытки;
	// сначала получим список обязательных у нашего объекта....
	Реквизиты = ЭтотОбъект.ПолучитьОбязательныеРеквизиты(НЕ ЭтоГруппа, ЭтоГруппа);
	// и его метаданные
	ЭтотОбъектМетаданные=ЭтотОбъект.Метаданные();
	
	РедактированиеКодов = обПраво("РазрешитьРедактированиеКодовСправочников",ЭтотОбъект.Права); 	
	
	// добавим дополнительные реквизиты
	Если ДопРеквизиты<>Неопределено Тогда
		Для каждого ДопРеквизит Из ДопРеквизиты Цикл
			Реквизиты.Вставить(ДопРеквизит.Ключ,ДопРеквизит.Значение);
		КонецЦикла;
	КонецЕсли;
	
	// Проверим установим заранее владельца, чтобы ниже в циклах этим не заниматься
	ЭтоСправочник=Ложь; Владелец=НЕОПРЕДЕЛЕНО;
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтотОбъект.Ссылка)) Тогда // Владелец только у справочников бывает
		ЭтоСправочник=Истина;
		Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.Владелец) Тогда
			Владелец=ЭтотОбъект.Владелец;
		КонецЕсли;
	КонецЕсли;
	
	// пройдемся по списку реквизитов с проверкой
	Для каждого Реквизит Из Реквизиты Цикл
		Если ТипЗнч(Реквизит.Значение)=Тип("Структура") Тогда
			// проверяем табличную часть
			ТабличнаяЧасть=ЭтотОбъект[Реквизит.Ключ];
			СписокНайденныхДублей=Новый СписокЗначений();
			// идем по табличной части
			Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
				// создадим структуру отбора для проверки уникальности реквизитов
				Если Уникальность Тогда СтруктураОтбора=Новый Структура(); КонецЕсли;
				// переберем обязательные реквизиты
				Для каждого РеквизитТаблицы Из Реквизит.Значение Цикл
					Если (Заполнение) И (РеквизитТаблицы.Значение<>2) И (РеквизитТаблицы.Значение>0) И обЗначениеНеЗаполнено(СтрокаТаблицы[РеквизитТаблицы.Ключ]) Тогда
						Результат=Ложь;
						Ошибки=Ошибки+"Таблица <"+обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,,Реквизит.Ключ)+"> значение колонки <"+обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,РеквизитТаблицы.Ключ,Реквизит.Ключ)+"> не заполнено ! Строка номер "+СокрЛП(СтрокаТаблицы.НомерСтроки)+Символы.ПС;
						Продолжить; // Если реквизит таблицы не заполнен, проверять его уникальность нет смысла
					КонецЕсли;
					// заполним структуру отбора для проверки уникальности реквизитов
					Если Уникальность И РеквизитТаблицы.Значение>1 Тогда
						СтруктураОтбора.Вставить(СокрЛП(РеквизитТаблицы.Ключ),СтрокаТаблицы[РеквизитТаблицы.Ключ]);
					КонецЕсли;
				КонецЦикла;
				// проверка уникальности
				Если Уникальность И СтруктураОтбора.Количество()>0 И СписокНайденныхДублей.НайтиПоЗначению(СтрокаТаблицы)=Неопределено Тогда
					// поищем строки удовлетворяющие структуре отбора
					НайденныеСтроки=ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора);
					// если нашли и их больше 1, то строки не уникальные
					Если НайденныеСтроки.Количество()>1 Тогда
						// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
						Результат=Ложь;
						ДублирующиесяСтроки=""; 
						// выведем строку сообщения...
						Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
							ДублирующиесяСтроки=ДублирующиесяСтроки+","+СокрЛП(НайденнаяСтрока.НомерСтроки);
							// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
							СписокНайденныхДублей.Добавить(НайденнаяСтрока);
						КонецЦикла;
						СтрокаРеквизитов="";
						Для каждого РеквизитТаблицы Из СтруктураОтбора Цикл
							СтрокаРеквизитов=СтрокаРеквизитов+?(ПустаяСтрока(СтрокаРеквизитов),"",",")+РеквизитТаблицы.Ключ;
						КонецЦикла;
						Ошибки=Ошибки+"Таблица <"+обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,,Реквизит.Ключ)+"> значения колонки <"+обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,РеквизитТаблицы.Ключ,Реквизит.Ключ)+"> не уникальны ! Строки: "+Сред(ДублирующиесяСтроки,2)+Символы.ПС;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		Иначе // обычный реквизит справочника / ПВХ (не табличная часть)
			// Сначала проверим заполнение (параметры структуры 1 или 3)
			Если Заполнение 
				и ((Реквизит.Значение=1) или (Реквизит.Значение=3) или (Реквизит.Значение=НЕОПРЕДЕЛЕНО))
				и обЗначениеНеЗаполнено(ЭтотОбъект[Реквизит.Ключ]) Тогда
				
				 // Реквизит "Код" не нужно проверять, когда запрещено редактирование кодов
				 // в этом случае код генерируется при записи объекта, на момент проверки его еще нет
				 Если Реквизит.Ключ="Код" Тогда
					 Если РедактированиеКодов = Перечисления.ВариантыОтветов.Нет 
						 ИЛИ РедактированиеКодов = Перечисления.ВариантыОтветов.Спрашивать Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;		
					
				Результат=Ложь;
				Ошибки=Ошибки+"Реквизит """+обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,Реквизит.Ключ)+""" не заполнен !"+Символы.ПС;
				Продолжить; // Если реквизит не заполнен, проверять его уникальность нет смысла
			КонецЕсли;
			// Теперь проверим уникальность (параметры структуры 2 или 3)
			// проверку сделаем прямым сравнением (а не >2) во избежании ошибки при нечисловом типе значения.
			Если (Уникальность) и ((Реквизит.Значение=2)ИЛИ(Реквизит.Значение=3)) Тогда
				ИмяОбъекта = ЭтотОбъект.Метаданные().Имя;
				Если Метаданные.Справочники.Найти(ИмяОбъекта) <> Неопределено Тогда
					//имеем справочник
					Менеджер=Справочники[ЭтотОбъект.Метаданные().Имя];
				ИначеЕсли Метаданные.ПланыВидовХарактеристик.Найти(ИмяОбъекта) <> Неопределено Тогда
					//имеем ПВХ
					Менеджер=ПланыВидовХарактеристик[ЭтотОбъект.Метаданные().Имя];
				ИначеЕсли Метаданные.ПланыВидовРасчета.Найти(ИмяОбъекта) <> Неопределено Тогда
					//имеем ПВР
					Менеджер=ПланыВидовРасчета[ЭтотОбъект.Метаданные().Имя];
				Иначе
					//имеем ... ничего не имеем
				КонецЕсли;
				
				Если Реквизит.Ключ="Наименование" Тогда
					Если ЭтоСправочник Тогда
						НайденнаяСсылка=Менеджер.НайтиПоНаименованию(ЭтотОбъект.Наименование,Истина,,Владелец);
					Иначе
						НайденнаяСсылка=Менеджер.НайтиПоНаименованию(ЭтотОбъект.Наименование,Истина);
					КонецЕсли;
				ИначеЕсли Реквизит.Ключ="Код" Тогда
					Если ЭтоСправочник Тогда
						//НайденнаяСсылка=Менеджер.НайтиПоКоду(ЭтотОбъект.Код,,,Владелец);
						Запрос = Новый Запрос;
						Запрос.Текст = 
						"ВЫБРАТЬ ПЕРВЫЕ 1
						|	"+ ИмяОбъекта + ".Ссылка
						|ИЗ
						|	Справочник."+ ИмяОбъекта + " КАК "+ ИмяОбъекта + "
						|ГДЕ
						|	"+ ИмяОбъекта + ".Код = &Код 
						|	И "+ ИмяОбъекта + ".Ссылка <> &Ссылка ";
						Если ЗначениеЗаполнено(Владелец) Тогда
							ЗАпрос.Текст = ЗАпрос.Текст + "
						    |	И "+ ИмяОбъекта + ".Владелец = &Владелец"
						КонецЕсли; 
						Запрос.УстановитьПараметр("Код",ЭтотОбъект.Код);
						Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
						Запрос.УстановитьПараметр("Владелец",Владелец);
						Выборка = Запрос.Выполнить().Выбрать();
						Если Выборка.Следующий() Тогда
						    НайденнаяСсылка = Выборка.Ссылка;
						Иначе	
						    НайденнаяСсылка = Менеджер.ПустаяСсылка();
						КонецЕсли; 
					Иначе
						НайденнаяСсылка=Менеджер.НайтиПоКоду(ЭтотОбъект.Код);
					КонецЕсли;
				Иначе // реквизит
					Если ЭтоСправочник Тогда
						НайденнаяСсылка=Менеджер.НайтиПоРеквизиту(Реквизит.Ключ,ЭтотОбъект[Реквизит.Ключ],,Владелец);
					Иначе
						НайденнаяСсылка=Менеджер.НайтиПоРеквизиту(Реквизит.Ключ,ЭтотОбъект[Реквизит.Ключ]);
					КонецЕсли;
				КонецЕсли;
				
				Если (НЕ обЗначениеНеЗаполнено(НайденнаяСсылка)) и (НайденнаяСсылка<>ЭтотОбъект.Ссылка) Тогда
					Результат=Ложь;
					Ошибки=Ошибки+"Реквизит """+обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,Реквизит.Ключ)+""" не уникален !";
					Если Не обЗначениеНеЗаполнено(Владелец) Тогда
						Ошибки=Ошибки+" (В пределах владельца: "+СокрЛП(Владелец)+")";
					КонецЕсли;
					Ошибки=Ошибки+Символы.ПС;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
 
		Если обПраво("КонтролироватьСоответствиеОрганизацииПодразделения", ЭтотОбъект.Права) Тогда
			Если ЭтотОбъектМетаданные.Реквизиты.Найти("Организация")<>Неопределено Тогда
				Если ЭтотОбъектМетаданные.Реквизиты.Найти("ПодразделениеКомпании")<>Неопределено Тогда
					Подразделение=ЭтотОбъект.ПодразделениеКомпании;
				ИначеЕсли ЭтотОбъектМетаданные.Реквизиты.Найти("Подразделение")<>Неопределено Тогда
					Подразделение=ЭтотОбъект.Подразделение;
				Иначе
					Подразделение=Неопределено;
				КонецЕсли; 
				Если НЕ обЗначениеНеЗаполнено(Подразделение) Тогда
					Если НЕ Подразделение.Организация = ЭтотОбъект.Организация Тогда
						СтрСообщения = "Подразделение <"+Подразделение.Наименование+"> не принадлежит организации <"+ЭтотОбъект.Организация.Наименование+">!";
						Сообщить(СтрСообщения,СтатусСообщения.Внимание);
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	
	Возврат Результат;
КонецФункции // спПроверитьКорректность()

// Проверяет справочники на право редактирования, чтения или доступности текущего
// вида справочника, а так же на право, если оно установлено, редактирования или
// чтения объекта текущего вида справочника, по ближайшему родителю, на которое
// установлено данное право  
//Параметры:
//  Объект       - текущий СправочникОбъект. Обязательный для заполнения.
//  Отказ        - результат работы процедуры, т.е. определяет текущую доступность данного объекта
//  ЭтаФорма     - форма текущего объекта. Передается для проверки на редактирование или чтение 
//                 объекта при открытии формы.
//  ЗначенияПрав - Соответствие, Кеш прав текущего пользователя.
//
Процедура спПроверкаПраваДоступа(Объект, Отказ, ЭтаФорма=Неопределено, ЗначенияПрав=Неопределено) Экспорт
	
	//если уже Отказ, то и проверять не надо
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	//если не справочник, то и делать нечего
	Если НЕ Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Объект.Ссылка)) Тогда	
		Возврат;
	КонецЕсли;
		
	Результат = обПраво("Право доступа " + Объект.Метаданные().Имя, ЗначенияПрав);
	
	Если Результат=Перечисления.ВидыПравДляСправочников.Редактирование Тогда
		// РЕДАКТИРОВАНИЕ 
	ИначеЕсли Результат=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
		Сообщить("Нет доступа к объектам вида "  + Объект.Метаданные().Представление(), СтатусСообщения.Важное); 
		Отказ=Истина;
	ИначеЕсли Результат=Перечисления.ВидыПравДляСправочников.Чтение Тогда
		// ЧТЕНИЕ
		Сообщить("Нет прав на редактирование объектов вида "  + Объект.Метаданные().Представление(), СтатусСообщения.Важное); 	
		// проверка на редактирование или просмотр объекта
		Если ЭтаФорма = Неопределено Тогда 
			Отказ = Истина; 
		Иначе
			Попытка
				//Если открываем форму справочника
				Если ЭтаФорма.ЭтоНовый() Тогда
					Отказ = Истина; 
				КонецЕсли; 
			Исключение
				Попытка
					//Исключение для открытия панели свойств
					Если ЭтаФорма.ТекОбъект.ЭтоНовый() Тогда
						Отказ = Истина; 
					КонецЕсли; 
				Исключение
				КонецПопытки; 
			КонецПопытки; 
			ЭтаФорма.ТолькоПросмотр=Истина; 
		КонецЕсли;
	ИначеЕсли Результат=Перечисления.ВидыПравДляСправочников.РедактированиеПоГруппам Тогда
		СоответствиеГруппДоступа = обПраво("Значения Право доступа " + Объект.Метаданные().Имя, ЗначенияПрав);
		Если СоответствиеГруппДоступа = Неопределено Тогда 
			// ЧТЕНИЕ
			Сообщить("Нет прав на редактирование объектов вида "  + Объект.Метаданные().Представление(), СтатусСообщения.Важное);
			// проверка на редактирование или просмотр объекта
			Если ЭтаФорма = Неопределено Тогда 
				Отказ = Истина; 
			Иначе 
				Попытка
					//Если открываем форму справочника
					Если ЭтаФорма.ЭтоНовый() Тогда
						Отказ = Истина; 
					КонецЕсли; 
				Исключение
					Попытка
						//Исключение для открытия панели свойств
						Если ЭтаФорма.ТекОбъект.ЭтоНовый() Тогда
							Отказ = Истина; 
						КонецЕсли; 
					Исключение
					КонецПопытки; 
				КонецПопытки; 
				ЭтаФорма.ТолькоПросмотр=Истина; 
			КонецЕсли;
		Иначе //если не в корне, то определяем по дереву доступа
			Если (Объект.ЭтоГруппа) и (НЕ Объект.Ссылка.Пустая())  Тогда
				Родитель = Объект.Ссылка;
			Иначе
				Родитель = Объект.Родитель;
			КонецЕсли;
			//если родитель пустой, то уровня нет
			Попытка Уровень = Родитель.Уровень() Исключение Уровень = 0 КонецПопытки;
			//получаем для текущего уровня
			ЗначениеДоступа=СоответствиеГруппДоступа[Родитель];
			// Перебираем всех родителей объекта, пока не находим
			// любое значение доступности
			Для Ном=1 по Уровень Цикл
				Если ЗначениеДоступа<>Неопределено Тогда
					Прервать;
				КонецЕсли;
				Родитель=Родитель.Родитель;
				ЗначениеДоступа=СоответствиеГруппДоступа[Родитель];
			КонецЦикла;
			
			//или нашли значение доступа, или вышли из цикла, ничего не найдя
			Если (ЗначениеДоступа=Неопределено) ИЛИ (НЕ ЗначениеДоступа) Тогда
				Если Родитель.Пустая() Тогда
					стрРодитель = "корня"
				Иначе
					стрРодитель = "группы "+СокрЛП(Родитель);
				КонецЕсли;
				Сообщить("Нет прав на редактирование " + стрРодитель + " объектов вида "  + Объект.Метаданные().Представление(), СтатусСообщения.Важное);
				// проверка на редактирование или просмотр объекта
				Если ЭтаФорма = Неопределено Тогда 
					Отказ = Истина; 
				Иначе 
					Попытка
						//Если открываем форму справочника
						Если ЭтаФорма.ЭтоНовый() Тогда
							Отказ = Истина; 
						КонецЕсли; 
					Исключение
						Попытка
							//Исключение для открытия панели свойств
							Если ЭтаФорма.ТекОбъект.ЭтоНовый() Тогда
								Отказ = Истина; 
							КонецЕсли; 
						Исключение
						КонецПопытки; 
					КонецПопытки; 
					ЭтаФорма.ТолькоПросмотр=Истина; 
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	//если значения права нет, то можно редактировать
КонецПроцедуры // спПроверкаПраваДоступа()

// Проверяет участие объекта в движениях регистра накопления
// 
// Параметры
//  ВидРегистра - строка - название регистра накопления как оно задано в конфигураторе
//  Измерение   - строка - название измерения регистра как оно задано в конфигураторе
//  Значение    - СправочникОбъект
//
// Возвращаемое значение:
//  Булево - признак наличия движений по регистру
//
Функция спУчастиеВДвижениях(ВидРегистра, Измерение, Значение) Экспорт
	МассивЗначений = Новый Массив();
	Если Не (ТипЗнч(Значение) = Тип("Массив")) Тогда
		МассивЗначений.Добавить(Значение);
	КонецЕсли;
	Запрос=Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	*
	|ИЗ
	|	РегистрНакопления."+ВидРегистра+" КАК Рег
	|ГДЕ
	|	Рег."+Измерение+" В (&Значение)
	|");
	Запрос.УстановитьПараметр("Значение",?(МассивЗначений.Количество()>0,МассивЗначений,Значение));
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции // спУчастиеВДвижениях()

// Проверяет наличие в строке не допустимых символов
//
// Параметры:
//	стрЗначение - строка для проверки
//	Права       - права пользователя
//	Автоработы  - флаг проверки для авторабот
//
// Возвращает истину если ошибок нет, иначе ложь
//
Функция спПроверкаНаНедопустимыеСимволы(стрЗначение,Права,Автоработы = Ложь) Экспорт
	Перем стрДопСимволов;
	Рез = "";
	
	Если Автоработы Тогда
		стрДопСимволов = обПраво("КонтрольВводаЗапрещенныхСимволовАртикулАвтоработы",Права);
	Иначе
		стрДопСимволов = обПраво("КонтрольВводаЗапрещенныхСимволовАртикулНоменклатуры",Права);
	КонецЕсли;
	
	// проверим есть ли вообще запрещенные символы
	Если обЗначениеНеЗаполнено(стрДопСимволов) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// обходим строку по символьно и запоминаем не допустимые символы
	Для Сч = 1 По СтрДлина(стрЗначение) Цикл
		Символ = Прав(Лев(стрЗначение,Сч),1);
		
		Если (Найти(стрДопСимволов,Символ)>0) Тогда
			Если Найти(Рез,Символ)=0 Тогда
				Рез = Рез + ?(ПустаяСтрока(Рез),"",", ") + Символ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(Рез) Тогда
		#Если Клиент Тогда
			Предупреждение("Были обнаружены следующие недопустимые символы: " + Символы.ПС + Рез,обПраво("ТаймаутДиалогов",Права));
		#КонецЕсли
	КонецЕсли;
	
	Возврат ПустаяСтрока(Рез);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ МОДУЛЕЙ СПРАВОЧНИКОВ И ПЛАНОВ ВИДОВ ХАРАКТЕРИСТИК

// Общий обработчик события ПриУстановкеНовогоКода элемента 
//
// Параметры:
//  ЭтотОбъект           - СправочникОбъект - обрабатываемый объект
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события.
//  Префикс              - Строка - Префикс, который будет использоваться для генерации кода. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Экспорт
	
	Результат = Истина;
	
	НовыйПрефикс = обПолучитьПрефиксОбъекта(ЭтотОбъект,Префикс);
	Если НовыйПрефикс = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
	Иначе
		Префикс = НовыйПрефикс;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции // спПриУстановкеНовогоКода()

// Общий обработчик события ПриКопировании элемента 
//
// Параметры:
//  ЭтотОбъект        - СправочникОбъект
//  ОбъектКопирования - СправочникОбъект - исходный элемент, который является источником копирования
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спПриКопировании(ЭтотОбъект, ОбъектКопирования) Экспорт
	
	Результат = Истина;
	
	Для каждого Реквизит Из ЭтотОбъект.Метаданные().Реквизиты Цикл
		Значение = ЭтотОбъект[Реквизит.Имя];
		Попытка
			Если ТипЗнч(Значение.Владелец) <> ТипЗнч(ЭтотОбъект.Ссылка) Тогда
				Продолжить;
			КонецЕсли;	
			Если Значение.Владелец <> Неопределено Тогда
				ЭтотОбъект[Реквизит.Имя] = Неопределено;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла; 
	
	//получим свойства объекта копирования и запишем их в переменную модуля объекта
	Попытка
		ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов = Обработки.ЗначенияСвойствОбъекта.Создать();
		ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ОбъектОтбораЗначений = ОбъектКопирования.Ссылка;
		ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ПрочитатьЗаполнитьСвойстваИЗначения();
		ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ОбъектОтбораЗначений = ЭтотОбъект.Ссылка;
	Исключение 
	КонецПопытки;
	
	Если ЭтотОбъект.Метаданные().Реквизиты.Найти("GUIDСайта") <> Неопределено И ЗначениеЗаполнено(ЭтотОбъект.GUIDСайта) Тогда
		Попытка
			ЭтотОбъект.GUIDСайта = "";
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спПриКопировании()

// Общий обработчик события ПриКопировании ОбработкаЗаполнения элемента 
// 
// Параметры:
//  ЭтотОбъект – СправочникОбъект
//  Основание  – Любая ссылка	– ссылка на объект - основание
// 
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спОбработкаЗаполнения(ЭтотОбъект, Основание) Экспорт
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Истина;
	
	Попытка
		Если обЗначениеНеЗаполнено(ЭтотОбъект.Организация) Тогда
			Попытка
				Организация = Основание.Организация;
			Исключение
				Организация = Неопределено;
			КонецПопытки; 
			Если обЗначениеНеЗаполнено(Организация) Тогда
				Организация = ПараметрыСеанса.Пользователь.Организация;
			КонецЕсли;
			ЭтотОбъект.Организация = Организация;
		КонецЕсли; 
	Исключение
	КонецПопытки; 
	Попытка
		Если обЗначениеНеЗаполнено(ЭтотОбъект.Подразделение) Тогда
			Попытка
				Подразделение = Основание.Подразделение;
			Исключение
				Попытка
					Подразделение = Основание.ПодразделениеКомпании;
				Исключение
					Подразделение = Неопределено;
				КонецПопытки; 
			КонецПопытки; 
			Если обЗначениеНеЗаполнено(Подразделение) Тогда
				Подразделение = ПараметрыСеанса.Пользователь.Подразделение;
			КонецЕсли;
			ЭтотОбъект.Подразделение = Подразделение;
		КонецЕсли; 
	Исключение
	КонецПопытки;
	
	Если ЭтотОбъект.Метаданные().Реквизиты.Найти("GUIDСайта") <> Неопределено И ЗначениеЗаполнено(ЭтотОбъект.GUIDСайта) Тогда
		Попытка
			ЭтотОбъект.GUIDСайта = "";
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // спОбработкаЗаполнения()

// Общий обработчик события ПередЗаписью элемента 
//
// Параметры:
//  ЭтотОбъект   - СправочникОбъект обрабатываемый объект
//  Отказ        - Булево - Признак отказа от записи элемента
//  ДопРеквизиты - Неопределено, Структура - содержит дополнительные реквизиты для проверки корректности заполнения
//  Заполнение   - Булево - Флаг проверки заполнения реквизитов
//  Уникальность - Булево - флаг проверки реквизитов на уникальность
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спПередЗаписью(ЭтотОбъект,Отказ,ДопРеквизиты=Неопределено,Заполнение=Истина,Уникальность=Истина) Экспорт
	
	Результат = Истина;
	
	Попытка
		// Для редких случаев, когда ЭтотОбъект неопределен
		// проверку на режим обмена данными делаем через попытку
		Загрузка = ЭтотОбъект.ОбменДанными.Загрузка;
		Если Загрузка Тогда
			// если текущий режим Загрузка, то производим минимум проверок
			// т.к. все проверки были произведены в ИБ источнике
			Возврат Ложь;
		КонецЕсли;
		ДопЗагрузка = Неопределено;
		Если НЕ Загрузка Тогда
			ЭтотОбъект.ДополнительныеСвойства.Свойство("Загрузка",ДопЗагрузка);
			Загрузка = ?(НЕ ДопЗагрузка = Неопределено,ДопЗагрузка,Загрузка); 
		КонецЕсли;
		Если Загрузка Тогда
			Возврат Ложь;
		КонецЕсли;
	Исключение КонецПопытки;
	
	ТекстОшибки="";
	
	// Выполним проверки прав доступа к элементу
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам",ЭтотОбъект.Права) Тогда
		
		// проверка доступа редактирования, при смене родителя объекта
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтотОбъект.Ссылка)) Тогда
			Если ЭтотОбъект.Ссылка.Родитель<>ЭтотОбъект.Родитель Тогда
				//сначала проверим можно ли редактировать старого родителя
				спПроверкаПраваДоступа(ЭтотОбъект,Отказ,,ЭтотОбъект.Права);
				спПроверкаПраваДоступа(ЭтотОбъект.Родитель,Отказ,,ЭтотОбъект.Права);
			Иначе
				// Проверка на допустимость редактирования справочника по текущему пользователю 
				спПроверкаПраваДоступа(ЭтотОбъект,Отказ,,ЭтотОбъект.Права);
			КонецЕсли;
		КонецЕсли;
		
		// Проверим элемент на использование его в константах
		Если ЭтотОбъект.ЭтоНовый() ИЛИ (НЕ ЭтотОбъект.Модифицированность()) ИЛИ
			обПраво("РедактированиеОбъектовЗначенийКонстант",ЭтотОбъект.Права) Тогда
			// тогда не нужно даже проверять
		Иначе
			ИмяКонстанты = "";
			Если обОбъектУказанВКонстантах(ЭтотОбъект,ИмяКонстанты) Тогда
				Отказ = Истина;
				ТекстОшибки = "Элемент не может быть изменен т.к. указан в значении константы """+ИмяКонстанты+"""";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Выполним проверку корректности заполнения
	Если НЕ ЭтотОбъект.ПометкаУдаления Тогда
		Отказ = Отказ ИЛИ НЕ ЭтотОбъект.ПроверитьКорректность(ТекстОшибки, ДопРеквизиты, Заполнение, Уникальность);
	КонецЕсли; 
	
	// Индикация ошибок
	Если (Отказ) И (НЕ ПустаяСтрока(ТекстОшибки)) Тогда
		Имя = ЭтотОбъект.Метаданные().Представление();
		Сообщить("Перед записью элемента - "+Имя+":"+СокрЛП(ЭтотОбъект)+" обнаружены ошибки :",СтатусСообщения.Внимание);
		Сообщить(ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спПередЗаписью()

// Общий обработчик события ПриЗаписи элемента 
//
// Параметры:
//  ЭтотОбъект - СправочникОбъект - обрабатываемый объект
//  Отказ      - Булево - Признак отказа от записи элемента
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спПриЗаписи(ЭтотОбъект, Отказ) Экспорт
	
	Результат = Истина;
	
	Загрузка = Ложь;
	Попытка
		// Для редких случаев, когда ЭтотОбъект неопределен
		// проверку на режим обмена данными делаем через попытку
		Загрузка = ЭтотОбъект.ОбменДанными.Загрузка;
		Если Загрузка Тогда
			// если текущий режим Загрузка, то производим минимум проверок
			// т.к. все проверки были произведены в ИБ источнике
			Возврат Ложь;
		КонецЕсли;
		ДопЗагрузка = Неопределено;
		Если НЕ Загрузка Тогда
			ЭтотОбъект.ДополнительныеСвойства.Свойство("Загрузка",ДопЗагрузка);
			Загрузка = ?(НЕ ДопЗагрузка = Неопределено,ДопЗагрузка,Загрузка); 
		КонецЕсли;
		Если Загрузка Тогда
			Возврат Ложь;
		КонецЕсли;
	Исключение КонецПопытки;
	
	Если НЕ Загрузка Тогда
		Попытка //Если есть свойства справочника, пронаследованные от основания или при копировании, запишем
			Если ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов <> Неопределено Тогда
				ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ОбъектОтбораЗначений = ЭтотОбъект.Ссылка;
				ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ЗаписатьВсеЗначенияСвойств();
				ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов = Неопределено;
			КонецЕсли;
		Исключение 
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спПриЗаписи()

// Общий обработчик события ПередУдалением элемента 
//
// Параметры:
//  ЭтотОбъект - СправочникОбъект - обрабатываемый объект
//  Отказ      - Булево - Признак отказа от удаления элемента
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спПередУдалением(ЭтотОбъект, Отказ) Экспорт
	
	Результат = Истина;
	
	Попытка
		// Для редких случаев, когда ЭтотОбъект неопределен
		// проверку на режим обмена данными делаем через попытку
		Загрузка = ЭтотОбъект.ОбменДанными.Загрузка;
		Если Загрузка Тогда
			// если текущий режим Загрузка, то производим минимум проверок
			// т.к. все проверки были произведены в ИБ источнике
			Возврат Ложь;
		КонецЕсли;
		ДопЗагрузка = Неопределено;
		Если НЕ Загрузка Тогда
			ЭтотОбъект.ДополнительныеСвойства.Свойство("Загрузка",ДопЗагрузка);
			Загрузка = ?(НЕ ДопЗагрузка = Неопределено,ДопЗагрузка,Загрузка); 
		КонецЕсли;
		Если Загрузка Тогда
			Возврат Ложь;
		КонецЕсли;
	Исключение КонецПопытки;
	
	// Выполним проверки прав доступа к элементу
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам",ЭтотОбъект.Права) Тогда
		// проверка доступа редактирования, при смене родителя объекта
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтотОбъект.Ссылка)) Тогда
			// Проверка на допустимость редактирования справочника по текущему пользователю 
			спПроверкаПраваДоступа(ЭтотОбъект,Отказ,,ЭтотОбъект.Права);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спПередУдалением()


#Если Клиент Тогда   
	
////////////////////////////////////////////////////////////////////////////////
// все дальнейшие процедуры относятся к интерфейсным
// и соответственно, на сервере и внешнем соединении исполняться не могут
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ РЕКВИЗИТОВ ФОРМ ЭЛЕМЕНТОВ СПРАВОЧНИКОВ И ПЛАНОВ ВИДОВ ХАРАКТЕРИСТИК

// Обработчик нажатия на гиперссылку с наименованием владельца
// на форме списка подчиненного справочника
//
// Параметры:
//  ЭтаФорма – Форма – Форма справочника элемент которой обрабатывается.
//  Элемент  - ЭлементФормы.
//
Процедура спСписокТВладелецНажатие(ЭтаФорма, Элемент) Экспорт
	Попытка
		ТекВладелец = ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные.Владелец;
		ВладелецОбъект = ТекВладелец.ПолучитьОбъект();
		ФормаВладельца = ВладелецОбъект.ПолучитьФорму("ФормаЭлемента",ЭтаФорма,);
		ФормаВладельца.Открыть();
	Исключение
		Попытка
			ФормаВладельца = ВладелецОбъект.ПолучитьФорму("ФормаСписка",ЭтаФорма,);
			ФормаВладельца.ПараметрТекущаяСтрока = ТекВладелец;
			ФормаВладельца.Открыть();
		Исключение
		КонецПопытки;
	КонецПопытки;
КонецПроцедуры // спСписокТВладелецНажатие()

// Обработчик нажатия на гиперссылку с наименованием владельца
// на форме элемента подчиненного справочника
//
// Параметры:
//  ЭтаФорма – Форма – Форма справочника элемент которой обрабатывается.
//  Элемент  - ЭлементФормы.
//
Процедура спЭлементТВладелецНажатие(ЭтаФорма, Элемент) Экспорт
	Попытка
		ФормаВладельца=ЭтаФорма.Владелец.ПолучитьФорму(,ЭтаФорма,);
		ФормаВладельца.Открыть();
	Исключение
	КонецПопытки;
КонецПроцедуры // спЭлементТВладелецНажатие()

// Обработчик автоподбора текста поля ввода справочника сотрудники
//
// Параметры:
//  Элемент  - ЭлементФормы. Поле ввода со ссылкой на справочник сотрудники.
//  Текст - Строка. Строка текста, введенная в поле ввода.
//  ТекстАвтоПодбора - Строка. Содержит текст для размещения в поле ввода.
//  СтандартнаяОбработка - Булево. Признак выполнения стандартной (системной) обработки события. 
//
Процедура спСотрудникАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, ДополнительныеОтборы = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Наименование ПОДОБНО &Текст
		|	И Сотрудники.ФлагУволен = ЛОЖЬ
		|	И Сотрудники.ЭтоГруппа = ЛОЖЬ
		|	//ДОПОЛНИТЕЛЬНЫЕОТБОРЫ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудники.Наименование";

	Если ДополнительныеОтборы <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ДОПОЛНИТЕЛЬНЫЕОТБОРЫ", "И " + ДополнительныеОтборы);
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Для Каждого ДополнительныйПараметр Из ДополнительныеПараметры Цикл 
			Запрос.УстановитьПараметр(ДополнительныйПараметр.Ключ, ДополнительныйПараметр.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Текст", Текст+"%");
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() = 1 Тогда
		ВыборкаДетальныеЗаписи.Следующий();
		ТекстАвтоПодбора = ВыборкаДетальныеЗаписи.Наименование;
	КонецЕсли;
КонецПроцедуры

// Обработчик окончания ввода текста поля ввода справочника сотрудники
//
// Параметры:
//  Элемент  - ЭлементФормы. Поле ввода со ссылкой на справочник сотрудники.
//  Текст - Строка. Строка текста, введенная в поле ввода.
//  Значение - СписокЗначений. Параметр содержит значение для размещения в поле
//                             ввода или список значений для последующего выбора
//                             одного из них и размещения в поле ввода.
//  СтандартнаяОбработка - Булево. Признак выполнения стандартной (системной) обработки события.
//	ДополнительныеОтборы - Строка. Дополнительные условия отбора сотрудников.
//	ДополнительныеПараметры - Структура. Структура дополнительных параметров отборов.
//
Процедура спСотрудникОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка, ДополнительныеОтборы = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 10
		|	Сотрудники.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	(Сотрудники.Наименование ПОДОБНО &Текст
		|			ИЛИ ВЫРАЗИТЬ(Сотрудники.Код КАК СТРОКА(10)) ПОДОБНО &Текст)
		|	И Сотрудники.ФлагУволен = ЛОЖЬ
		|	И Сотрудники.ЭтоГруппа = ЛОЖЬ
		|	//ДОПОЛНИТЕЛЬНЫЕОТБОРЫ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудники.Наименование";

	Если ДополнительныеОтборы <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ДОПОЛНИТЕЛЬНЫЕОТБОРЫ", "И " + ДополнительныеОтборы);
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Для Каждого ДополнительныйПараметр Из ДополнительныеПараметры Цикл 
			Запрос.УстановитьПараметр(ДополнительныйПараметр.Ключ, ДополнительныйПараметр.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Текст", Текст+"%");
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Количество() > 0 Тогда
		Значение = Новый СписокЗначений;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Значение.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ПАНЕЛЕЙ ФОРМ СПИСКОВ И ЭЛЕМЕНТОВ СПРАВОЧНИКОВ И ПЛАНОВ ВИДОВ ХАРАКТЕРИСТИК

// Нажатие кнопки "Выгрузить в табличный документ"
//
// Параметры:
//  ЭтаФорма - форма на которой все это происходит
//  Ссылка   - Кнопка - "Выгрузить в табличный документ" в верхней панели.
//
Процедура спДействияФормыВыгрузкаВТабличныйДокумент(ЭтаФорма,Ссылка) Экспорт
	Попытка	// Сначала попробуем для формы списка
		ТекущиеДанныеСписка=ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные;
		Если ТекущиеДанныеСписка=Неопределено Тогда
			Возврат;
		Иначе
			Объект = ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные.Ссылка;
		КонецЕсли;
	Исключение
		Попытка
			Объект = ЭтаФорма.Ссылка;
		Исключение	
			Если Не обЗначениеНеЗаполнено(Ссылка) Тогда
				Объект = Ссылка.Ссылка;
			Иначе
				Объект = Неопределено;
			КонецЕсли;
		КонецПопытки;	
	КонецПопытки;
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Объект) Тогда
		#Если Клиент Тогда
			Предупреждение("Элемент должен быть записан !");
			#КонецЕсли
		Возврат;
	КонецЕсли;
	
	// перед выгрузкой не помешало бы проверить, есть ли права на данный объект
	Результат = обПраво("Право доступа " + Объект.Метаданные().Имя);
	Если Результат=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
		Сообщить("Нет доступа к объектам вида "  + Объект.Метаданные().Представление(), СтатусСообщения.Важное); 
		Возврат;
    КонецЕсли;

	Если Не Объект.Пустая() Тогда 
		ОбъектВыгрузкаОбъекта = Обработки.ВыгрузкаВТабличныйДокумент.Создать();
		ОбъектВыгрузкаОбъекта.ОбъектСсылка = Объект;
		ОбъектВыгрузкаОбъекта.ВыгрузитьВТабличныйДокумент();
	КонецЕсли
КонецПроцедуры // спДействияФормыВыгрузкаВТабличныйДокумент()

// нажатие кнопки "ДобавитьВИзбранное" в верхней панели.
//
// Параметры:
//  ЭтаФорма - форма на которой все это происходит
//  Ссылка   - Кнопка - "ДобавитьВИзбранное" в верхней панели
//  ИмяФормы - Строка - Имя формы
//
Процедура спДействияФормыДобавитьВИзбранное(ЭтаФорма, Ссылка, ИмяФормы) Экспорт
	ЭтоФормаОбъекта = (Не Ссылка = Неопределено);
	Избранное = Обработки.Избранное.Создать();
	Если ЭтоФормаОбъекта Тогда
		Избранное.ДобавитьВИзбранное(Ссылка);
	Иначе
		Менеджер = "";
		Попытка
			Менеджер = "Справочники." + Сред(Строка(ЭтаФорма.СправочникСписок), СтрДлина("СправочникСписок") + 2);
		Исключение
			Менеджер = "";
		КонецПопытки;
		Если ПустаяСтрока(Менеджер) Тогда
			Попытка
				Менеджер = "Справочники." + Сред(Строка(ЭтаФорма.СправочникОбъект), СтрДлина("СправочникОбъект") + 2);
			Исключение
				Менеджер = "";
			КонецПопытки;
		КонецЕсли;
		Если Не ПустаяСтрока(Менеджер) Тогда
			Избранное.ДобавитьВИзбранное(ЭтаФорма.Заголовок, Менеджер, ИмяФормы);  
		Иначе	
			Попытка
				Менеджер = "ПланыВидовХарактеристик." + Сред(Строка(ЭтаФорма.ПланВидовХарактеристикСписок), СтрДлина("СправочникСписок") + 2);
			Исключение
				Менеджер = "";
			КонецПопытки;
			Если ПустаяСтрока(Менеджер) Тогда
				Попытка
					Менеджер = "ПланыВидовХарактеристик." + Сред(Строка(ЭтаФорма.ПланВидовХарактеристикОбъект), СтрДлина("СправочникОбъект") + 2);
				Исключение
					Менеджер = "";
				КонецПопытки;
			КонецЕсли;
			Если Не ПустаяСтрока(Менеджер) Тогда
				Избранное.ДобавитьВИзбранное(ЭтаФорма.Заголовок, Менеджер, ИмяФормы);  
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры // спДействияФормыДобавитьВИзбранное()

// Нажатие кнопки "История объекта"
//
// Параметры:
//  ЭтаФорма - форма на которой все это происходит
//  Ссылка   - Кнопка - кнопка "История объекта"  в верхней панели.
//
Процедура спДействияФормыИсторияОбъекта(ЭтаФорма,Ссылка) Экспорт
	Попытка	// Сначала попробуем для формы списка
		ТекущиеДанныеСписка=ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные;
		Если ТекущиеДанныеСписка=Неопределено Тогда
			Возврат;
		Иначе
			Объект = ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные.Ссылка;
		КонецЕсли;
	Исключение
		Попытка
			Объект = ЭтаФорма.Ссылка;
		Исключение	
			Если Не обЗначениеНеЗаполнено(Ссылка) Тогда
				Объект = Ссылка.Ссылка;
			Иначе
				Объект = Неопределено;
			КонецЕсли;
		КонецПопытки;	
	КонецПопытки;
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Объект) Тогда
		#Если Клиент Тогда
			Предупреждение("Элемент должен быть записан !");
			#КонецЕсли
		Возврат;
	КонецЕсли;
	Если Не Объект.Пустая() Тогда 
		ОбъектИсторияОбъекта = Обработки.ИсторияОбъекта.Создать();
		ОбъектИсторияОбъекта.ОбъектСсылка = Объект;
		ОбъектИсторияОбъекта.СформироватьЖурналРегистрации();
	КонецЕсли
КонецПроцедуры // спДействияФормыИсторияОбъекта()

// нажатие кнопки "ПоискСсылок" в верхней панели.
//
// Параметры:
//  ЭтаФорма           - форма на которой все это происходит
//  Ссылка             - Кнопка - "ПоискСсылок" в верхней панели 
//
Процедура спДействияФормыПоискСсылок(ЭтаФорма,Ссылка) Экспорт
	Попытка	// Сначала попробуем для формы списка
		ТекущиеДанныеСписка=ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные;
		Если ТекущиеДанныеСписка=Неопределено Тогда
			Возврат;
		Иначе
			Объект = ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные.Ссылка;
		КонецЕсли;
	Исключение
		Попытка
			Объект = ЭтаФорма.Ссылка;
		Исключение	
			Если Не обЗначениеНеЗаполнено(Ссылка) Тогда
				Объект = Ссылка.Ссылка;
			Иначе
				Объект = Неопределено;
			КонецЕсли;
		КонецПопытки;	
	КонецПопытки;
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Объект) Тогда
		#Если Клиент Тогда
			Предупреждение("Элемент должен быть записан !");
			#КонецЕсли
		Возврат;
	КонецЕсли;
	Если Не Объект.Пустая() Тогда 
		ОбъектПоискСсылок=Обработки.ПоискСсылок.Создать();
		ОбъектПоискСсылок.ОбъектСсылка=Объект;
		ОбъектПоискСсылок.НайтиВходящие();
		ОбъектПоискСсылок.НайтиИсходящие();
	КонецЕсли
КонецПроцедуры // спДействияФормыПоискСсылок()

// Событие нажатия кнопки "Свойства" в панели "ДействияФормы"
//
// Параметры:
//  ЭтаФорма         – Форма – форма на которой все это происходит
//  Кнопка           - кнопка на панели форм списков и элементов 
//  ИмяФормыСписка   - Строка - Имя формы списка по нему ищем в форме список, если его нет передана форма объекта.
//  КлючУникальности - Строка - Пользовательский идентификатор формы 
//
Процедура спДействияФормыСвойства(ЭтаФорма, Кнопка,  ИмяФормыСписка = "Список", КлючУникальности="") Экспорт
	// Сначала попробуем для формы списка, в противном случае - это форма объекта
	Список = ЭтаФорма.ЭлементыФормы.Найти(ИмяФормыСписка);
	Если Список = Неопределено Тогда
		Попытка
			//если форму открываем из формы объекта то и передаем в нее объект
			ТекОбъект = ЭтаФорма.ЭтотОбъект;
			ОткрытаИзФормыСписка = ЛОЖЬ;
		Исключение
			Возврат;
		КонецПопытки;
	Иначе
		ТекущиеДанныеСписка = Список.ТекущиеДанные;
		Если ТекущиеДанныеСписка = Неопределено Тогда
			Возврат;
		Иначе
			ТекОбъект = ТекущиеДанныеСписка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		ОткрытаИзФормыСписка = ИСТИНА;
	КонецЕсли;
		
	Если обЗначениеНеЗаполнено(ТекОбъект.Ссылка) Тогда
		Предупреждение("Элемент должен быть записан !");
		Возврат;
	КонецЕсли;
	// проверка на редактирование справочника
	Отказ=Ложь;
	
	//проверку прав доступа делаем только для справочников
	ЭтоСправочник = (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ТекОбъект.Ссылка)));
		
	// Проверка на допустимость редактирования справочника по текущему пользователю 	
	//ИсключениеНастроекСРМ
	Результат = НЕОПРЕДЕЛЕНО;
	Если ОткрытаИзФормыСписка Тогда
		Если ЭтоСправочник И обПраво("ПроверкаДоступаКСправочникамИДокументам") Тогда	
			Результат = обПраво("Право доступа " + ТекОбъект.Метаданные().Имя);
		КонецЕсли;
	Иначе
		Если ЭтоСправочник И обПраво("ПроверкаДоступаКСправочникамИДокументам", ТекОбъект.Права) Тогда	
			Результат = обПраво("Право доступа " + ТекОбъект.Метаданные().Имя, ТекОбъект.Права);
		КонецЕсли;
	КонецЕсли;
		
	Если Результат=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
		Сообщить("Нет доступа к форме свойств объекта - "  + ТекОбъект.Метаданные().Представление() + ": " + СокрЛП(ТекОбъект)+" запрет доступа", СтатусСообщения.Важное); 
		Возврат;
	КонецЕсли;
	
	//откроем панель свойств
	Показывать = НЕ Кнопка.Пометка;
	ОбработкаПанельСвойств = Обработки.ПанельСвойствОбъекта.Создать();
	Если НЕ обЗначениеНеЗаполнено(КлючУникальности) Тогда
		ОбработкаПанельСвойств.КлючУникальности=КлючУникальности;
	КонецЕсли;
	ОбработкаПанельСвойств.ОткрытьПанельСвойств(ТекОбъект, ЭтаФорма, Показывать, Кнопка, ОткрытаИзФормыСписка);
КонецПроцедуры // спДействияФормыСвойства()

// Событие нажатия на кнопку "Заметки" в панели "ДействияФормы"
//
// Параметры:
//  ЭтаФорма         – Форма – форма на которой все это происходит
//  обСсылка         - ссылка на выделенный объект свойства
//
Процедура спЗаметки(обСсылка,ЭтаФорма) Экспорт
	Если обСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаборРегистра = РегистрыСведений.ЗаметкиПользователей.СоздатьНаборЗаписей();
	ФормаРег = НаборРегистра.ПолучитьФорму("ФормаСписка",ЭтаФорма,ЭтаФорма);
	//ФормаРег.Отбор.Автор.ВидСравнения   = ВидСравнения.Равно;
	//ФормаРег.Отбор.Автор.Значение       = ПараметрыСеанса.Пользователь;
	//ФормаРег.Отбор.Автор.Использование  = Истина;
	ФормаРег.Отбор.Объект.ВидСравнения  = ВидСравнения.Равно;
	ФормаРег.Отбор.Объект.Значение      = обСсылка;
	ФормаРег.Отбор.Объект.Использование = Истина;
	ФормаРег.ЭлементыФормы.РегистрСведенийСписок.НастройкаОтбора.Объект.Доступность = Ложь;
	Если ФормаРег.Открыта() Тогда
		ФормаРег.Активизировать();
	Иначе
		ФормаРег.Открыть();
	КонецЕсли;
КонецПроцедуры

// Нажатие кнопки "Создать напоминание"
//
// Параметры:
//  ЭтаФорма - форма на которой все это происходит
//  Ссылка   - Кнопка - "Создать напоминание" в верхней панели.
//
Процедура спДействияФормыСоздатьНапоминание(ЭтаФорма, Ссылка) Экспорт
	опСоздатьОповещение(Ссылка.Ссылка,, ЭтаФорма);
КонецПроцедуры// спДействиеФормыСоздатьНапоминание()

// Событие нажатия кнопки "Печать" в панели "ОсновныеДействияФормы"
//
// Параметры:
//  ЭтаФорма           - форма на которой все это происходит
//  Кнопка             - Кнопка - на панели "ОсновныеДействияФормы" 
//
Процедура спОсновныеДействияФормыПечать(ЭтаФорма, Кнопка) Экспорт
	ОтчетПечатьСправочника = Отчеты.ПечатьСправочника.Создать();
	//передадим табличное поле и сам список справочника
	Попытка
		ОтчетПечатьСправочника.СправочникТабличноеПоле = ЭтаФорма.ЭлементыФормы["Список"];
	Исключение
		Возврат;
	КонецПопытки;
	Если ОтчетПечатьСправочника.СправочникТабличноеПоле.Данные = "СправочникСписок" Тогда
		ОтчетПечатьСправочника.СправочникСписок = ОтчетПечатьСправочника.СправочникТабличноеПоле.Значение;
		ОтчетПечатьСправочника.ИмяСправочника = СтрЗаменить(Строка(ЭтаФорма.СправочникСписок),"СправочникСписок.","");
		ОтчетПечатьСправочника.ПечатьСправочникаПВХ = 1;
		ОтчетПечатьСправочника.ПолучитьФорму("НастройкиПечатьСправочника").Открыть();
	ИначеЕсли ОтчетПечатьСправочника.СправочникТабличноеПоле.Данные = "ПланВидовХарактеристикСписок" Тогда
		ОтчетПечатьСправочника.СправочникСписок = ОтчетПечатьСправочника.СправочникТабличноеПоле.Значение;
		ОтчетПечатьСправочника.ИмяСправочника = СтрЗаменить(Строка(ЭтаФорма.ПланВидовХарактеристикСписок),"ПланВидовХарактеристикСписок.","");
		ОтчетПечатьСправочника.ПечатьСправочникаПВХ = 2;
		ОтчетПечатьСправочника.ПолучитьФорму("НастройкиПечатьСправочника").Открыть();
	ИначеЕсли ОтчетПечатьСправочника.СправочникТабличноеПоле.Данные = "ПланВидовРасчетаСписок" Тогда
		ОтчетПечатьСправочника.СправочникСписок = ОтчетПечатьСправочника.СправочникТабличноеПоле.Значение;
		ОтчетПечатьСправочника.ИмяСправочника = СтрЗаменить(Строка(ЭтаФорма.ПланВидовРасчетаСписок),"ПланВидовРасчетаСписок.","");
		ОтчетПечатьСправочника.ПечатьСправочникаПВХ = 3;
		ОтчетПечатьСправочника.ПолучитьФорму("НастройкиПечатьСправочника").Открыть();
	ИначеЕсли ОтчетПечатьСправочника.СправочникТабличноеПоле.Данные = "ПланСчетовСписок" Тогда
		ОтчетПечатьСправочника.СправочникСписок = ОтчетПечатьСправочника.СправочникТабличноеПоле.Значение;
		ОтчетПечатьСправочника.ИмяСправочника = СтрЗаменить(Строка(ЭтаФорма.ПланСчетовСписок),"ПланСчетовСписок.","");
		ОтчетПечатьСправочника.ПечатьСправочникаПВХ = 4;
		ОтчетПечатьСправочника.ПолучитьФорму("НастройкиПечатьСправочника").Открыть();
	КонецЕсли;
КонецПроцедуры // спОсновныеДействияФормыПечать()

// Отображает/скрывает элементы формы расположенные левее "ГлавногоЭлемента".
// При сокрытии элементов, главный элемент растягивает на всю форму.
// Параметры:
//  ЭтаФорма					- форма на которой все это происходит
//  Кнопка						- Кнопка на панели инструментов, нажатие на которую скрывает/отображает деревце
//  ИмяГлавногоЭлемента			- Строка. Имя того элемента ради которого жертвуем остальными. по умолчанию "Список"
//									(для справочников чаще всего это список)
//  ИмяГлавногоРазделитель		- Строка. Имя разделителя, разделяющего "главный элемент" и скрываемые элементы.
//
Процедура спСписокДействияФормыОтображатьДерево(ЭтаФорма,Кнопка,ИмяГлавногоЭлемента="Список",ИмяГлавногоРазделитель="РазделительДереваИСписка") Экспорт
		
	Попытка
		ГлавныйЭлемент=ЭтаФорма.ЭлементыФормы[ИмяГлавногоЭлемента];
		ГлавныйРазделитель=ЭтаФорма.ЭлементыФормы[ИмяГлавногоРазделитель];
	Исключение
		Предупреждение("Данный режим недоступен !",10);
		Кнопка.Доступноcть=Ложь; Возврат;
	КонецПопытки;
	// отступ элементов от края формы
	ОтступПоКраямФормы=4;
	Кнопка.Пометка=Не Кнопка.Пометка; // инвертируем флажок
	// проверяем чего делаем
	Если Кнопка.Пометка Тогда
		// надо отобразить скрытые элементы на форме и вернуть главному элементу прежний размер
			
		// инициализация переменных
		// пройдемся по элементам формы
		Для каждого ЭлементФормы Из ЭтаФорма.ЭлементыФормы Цикл
			// вычислим низ "главного элемента"
			НизСписка=ГлавныйЭлемент.Верх+ГлавныйЭлемент.Высота;
			// если рассматриваемый элемент правее главного и не ниже его и это не он сам и элемент не видим, то...
			Если	(ЭлементФормы<>ГлавныйЭлемент) 
				И	(ЭлементФормы.Лево>=ГлавныйЭлемент.Лево) 
				И	(Не(ЭлементФормы.Видимость)) и (ЭлементФормы.Верх<=НизСписка) Тогда
				// отобразим элементы элементы
				ЭлементФормы.Видимость=Истина; ЭлементФормы.Доступность=Истина;
			КонецЕсли;
		КонецЦикла;
		// ужимаем главный элемент по ширине 
		ГлавныйЭлементПраво=ГлавныйЭлемент.Лево+ГлавныйЭлемент.Ширина;
		ГлавныйЭлемент.Ширина=ГлавныйЭлементПраво-(ГлавныйРазделитель.Лево+ГлавныйРазделитель.Ширина);
		// устанавливаем левую границу главного элемента с отступом от элемента левой привязки
		ГлавныйЭлемент.Лево=ГлавныйРазделитель.Лево+ГлавныйРазделитель.Ширина;
		// установим левую привязку, что бы не было проблем с масштабированием и разделителями
		ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель,ГраницаЭлементаУправления.Право);
	Иначе  
		// надо скрыть побочные элементы на форме и развернуть главные на всю форму по ширине
		// обходим элементы формы
		Для каждого ЭлементФормы Из ЭтаФорма.ЭлементыФормы Цикл
			// если элемент расположен на пути разворота главного элемента и это не сам главный элемент, то...
			Если	(ЭлементФормы<>ГлавныйЭлемент) И (ЭлементФормы.Лево+ЭлементФормы.Ширина<=ГлавныйЭлемент.Лево) Тогда
				Если (((ЭлементФормы.Верх>=ГлавныйЭлемент.Верх) и (ЭлементФормы.Верх<=ГлавныйЭлемент.Верх+ГлавныйЭлемент.Высота))
					ИЛИ ((ЭлементФормы.Верх+ЭлементФормы.Высота<=ГлавныйЭлемент.Верх+ГлавныйЭлемент.Высота) и (ЭлементФормы.Верх+ЭлементФормы.Высота>=ГлавныйЭлемент.Верх))) Тогда 
					// скроем эти элементы
					ЭлементФормы.Видимость=Ложь;  ЭлементФормы.Доступность=Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		// запомним правую позицию
		ГлавныйЭлементПраво=ГлавныйЭлемент.Лево+ГлавныйЭлемент.Ширина-ОтступПоКраямФормы;
		// сбросим левую и правую привязку, что бы не иметь проблем с масштабированием и разделителями.
		ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево,Неопределено,ГраницаЭлементаУправления.Лево);
		// установим левый край главного элемента в начало формы
		ГлавныйЭлемент.Лево=ОтступПоКраямФормы;
		// установим ширину главного элемента
		ГлавныйЭлемент.Ширина=ГлавныйЭлементПраво;
	КонецЕсли;
	// сохраним текущую настройку
	СохранитьЗначение(СтрЗаменить(ЭтаФорма.СправочникСписок,".","_")+"_СкрытьДеревоГрупп",НЕ Кнопка.Пометка);
		
КонецПроцедуры // спСписокДействияФормыОтображатьДерево()
	
// Событие нажатия кнопки "Связь" в панели "ОсновныеДействияФормы"
//
// Параметры:
//  ЭтаФорма           - форма на которой все это происходит
//  Кнопка             - Кнопка 
//  Пометка            - Неопределено - передает состояние триггерной кнопки
//  ВызыватьОповещение - Булево - проводить оповещение (при Истина) или нет.
//
Процедура спСписокДействияФормыСвязь(ЭтаФорма, Кнопка, Пометка=Неопределено, ВызыватьОповещение = ложь) Экспорт
	зфСписокДействияФормыСвязь(ЭтаФорма, Кнопка, Пометка, ВызыватьОповещение);
КонецПроцедуры // спСписокДействияФормыСвязь()

// нажатие кнопки "Редактировать код" в верхней панели.
//
// Параметры:
//  ЭтаФорма           - форма на которой все это происходит
//  Ссылка             - Кнопка - "ПоискСсылок" в верхней панели 
//
Процедура спДействияФормыРедактироватьКод(ЭтаФорма,Ссылка) Экспорт
	
	Если ЭтаФорма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	//определим, это форма элемента или форма списка и получим элемент формы "код"
	Список = ЭтаФорма.ЭлементыФормы.Найти("Список");
	Если Список <> Неопределено И Тип(Список) = Тип("ТабличноеПоле") 
		И Список.СпособРедактирования = СпособРедактированияСписка.ВСписке Тогда
		Код = Список.Колонки.Найти("Код");
	КонецЕсли;
	Если Код = Неопределено Тогда
		Код = ЭтаФорма.ЭлементыФормы.Найти("Код");
	КонецЕсли;
	Если Код = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//получим кнопку
	Попытка
		Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Подменю.Кнопки.РедактироватьКод;
	Исключение
	КонецПопытки;
	Если Кнопка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//изменим доступность элемента формы и подсказку
	Если НЕ Кнопка.Пометка Тогда
		ТекстВопроса = "Код присваивается автоматически при записи элемента, самостоятельное его редактирование может привести к нарушению нумерации в системе. 
						|Вы действительно хотите установить код вручную?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	Код.ТолькоПросмотр = Кнопка.Пометка;
	Кнопка.Пометка = Не Кнопка.Пометка;

	//необходимо обновить автоотметки незаполненных реквизитов
	Попытка  ЭтоГруппа = ЭтаФорма.ЭтоГруппа;
	Исключение ЭтоГруппа = Ложь;
	КонецПопытки;
	Если Список <> Неопределено Тогда
		Попытка
			ТекСтрока =  Список.ТекущаяСтрока;
			Если ТекСтрока <> Неопределено Тогда
				СтруктураОбязательныхРеквизитов = ТекСтрока.ПолучитьОбъект().ПолучитьОбязательныеРеквизиты(Не(ЭтоГруппа),ЭтоГруппа);
			КонецЕсли;
		Исключение
		КонецПопытки;
	Иначе
		Попытка
			СтруктураОбязательныхРеквизитов = ЭтаФорма.ПолучитьОбязательныеРеквизиты(Не(ЭтоГруппа),ЭтоГруппа);
		Исключение
		КонецПопытки;
	КонецЕсли;
	Если СтруктураОбязательныхРеквизитов <> Неопределено Тогда 
		удРасставитьАвтоотметкиНезаполненного(ЭтаФорма, СтруктураОбязательныхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры // спДействияФормыПоискСсылок()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ФОРМ ЭЛЕМЕНТОВ СПРАВОЧНИКОВ И ПЛАНОВ ВИДОВ ХАРАКТЕРИСТИК

// Общий обработчик события ПередОткрытием формы элемента 
//
// Параметры:
// ЭтаФорма             - форма - переданная форма
// Отказ                - Булево - признак отказа от открытия формы
// СтандартнаяОбработка - Булево - в данный параметр передается признак выполнения стандартной (системной)
//									обработки события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт

	ВосстановитьРазмерыФормы(ЭтаФорма);
	
	Попытка
		МетаданныеОбъекта=Метаданные.НайтиПоТипу(ТипЗнч(ЭтаФорма.СправочникОбъект));
		ПодсистемаАвтосалон=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.НормативнаяПодсистема.Подсистемы.Автосалон;
		ПодсистемаТестДрайв=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.УчетныеБлоки.Подсистемы.ТестДрайв;
		Если ПодсистемаАвтосалон.Состав.Содержит(МетаданныеОбъекта) ИЛИ ПодсистемаТестДрайв.Состав.Содержит(МетаданныеОбъекта) Тогда
			Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон",Истина) Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		ПодсистемаУчетВзаимоотношенийСКлиентами=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.УчетныеБлоки.Подсистемы.УчетВзаимоотношенийСКлиентами;
		Если ПодсистемаУчетВзаимоотношенийСКлиентами.Состав.Содержит(МетаданныеОбъекта) Тогда
			Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаCRM",Истина) Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		ПодсистемаТехосмотр=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.Документооборот.Подсистемы.Техосмотр;
		Если ПодсистемаТехосмотр.Состав.Содержит(МетаданныеОбъекта) Тогда
			Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаТехосмотр",Истина) Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки; 
	
	// Проверка на допустимость редактирования справочника по текущему пользователю 
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам", ЭтаФорма.ЭтотОбъект.Права) Тогда	
		спПроверкаПраваДоступа(ЭтаФорма.ЭтотОбъект, Отказ, ЭтаФорма, ЭтаФорма.ЭтотОбъект.Права);	
	КонецЕсли;
		
	// Может быть отраслевое переопределение функции модуля...
	Результат = орФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
		
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	// Стандартная обработка
	удФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// заполним доп. свойства формы справочников
	удЗаполнитьДополнительныеДействияСправочников(ЭтаФорма);
		
	Объект=ЭтаФорма.ЭтотОбъект;
	Если Объект.ЭтоНовый() Тогда
		// Вызовем заполнение элемента по умолчанию
		Если (ЭтаФорма.ПараметрОснование = Неопределено) И (ЭтаФорма.ПараметрОбъектКопирования = Неопределено) Тогда
			Попытка // Вдруг не прописана или не экспортирована функция обработки заполнения
				Объект.Заполнить(Неопределено);
			Исключение КонецПопытки;
		КонецЕсли;
		// Запретим ввод подчиненного при неизвестном владельце
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Объект.Ссылка)) Тогда
			Если (Объект.Метаданные().Владельцы.Количество() > 0) И обЗначениеНеЗаполнено(Объект.Владелец) Тогда
				Отказ = Истина;
				Предупреждение("Не определен владелец !");
			КонецЕсли;  
		КонецЕсли;
	КонецЕсли;
	
	// установим доступность элемента формы "код" для формы элемента
	Код = ЭтаФорма.ЭлементыФормы.Найти("Код");
	Если Код <> Неопределено Тогда
		Попытка
			ЕстьАвтонумерация = (ЭтаФорма.Автонумерация = АвтонумерацияВФорме.Авто);
		Исключение
			ЕстьАвтонумерация=Ложь;
		КонецПопытки;
		Если ЕстьАвтонумерация Тогда
			РедактированиеКодов = обПраво("РазрешитьРедактированиеКодовСправочников",Объект.Права);
			Если РедактированиеКодов = Перечисления.ВариантыОтветов.Да Тогда
				Код.ТолькоПросмотр = Ложь;
				Если Объект.ЭтоНовый() Тогда
					Если СтрДлина(Код.Подсказка) = 0 Тогда
						Код.Подсказка =  "Код будет присвоен при записи объекта"; 
					Иначе 
						Код.Подсказка = Код.Подсказка + ". " + " Код будет присвоен при записи объекта"; 
					КонецЕсли;
				КонецЕсли;
				ЭтаФорма.Автонумерация = АвтонумерацияВФорме.Авто;
			ИначеЕсли РедактированиеКодов = Перечисления.ВариантыОтветов.Нет Тогда
				Код.ТолькоПросмотр = Истина;
				ЭтаФорма.Автонумерация = АвтонумерацияВФорме.НеИспользовать;
			ИначеЕсли РедактированиеКодов = Перечисления.ВариантыОтветов.Спрашивать Тогда
				Код.ТолькоПросмотр = Истина;
				Подсказка =  "Для включения редактирования кода используйте подменю ""Действия""";
				Если СтрДлина(Код.Подсказка) = 0 Тогда
					Код.Подсказка = Подсказка; 
				Иначе 
					Код.Подсказка = Код.Подсказка + ". " + Подсказка; 
				КонецЕсли;
				ЭтаФорма.Автонумерация = АвтонумерацияВФорме.НеИспользовать;
			КонецЕсли;	
		КонецЕсли; 
	КонецЕсли;
	
	верс_ВерсионированиеОбъектов.верс_удЗаполнитьДополнительныеДействияДокументов(ЭтаФорма); //ЧалапАю БизТех 10.09.2024 - версионирование

Возврат Результат;
	
КонецФункции // спДействияФормыСоздатьНапоминание()

// Общий обработчик события ПриОткрытии формы элемента 
//
// Параметры:
// ЭтаФорма          - форма - переданная форма
// ПовторноеОткрытие - Булево - признак повторного открытия формы элемента
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПриОткрытии(ЭтаФорма, ПовторноеОткрытие = Ложь) Экспорт
	
	Результат = Истина;
	
	ЭтотОбъект = ЭтаФорма.ЭтотОбъект;
	
	Если ЭтотОбъект.ЭтоНовый() ИЛИ НЕ(обПраво("ПроверкаДоступаКСправочникамИДокументам",ЭтотОбъект.Права)) Тогда
		// тогда не нужно даже проверять ничего
	Иначе
		// вывод кнопки заметок
		Попытка
			ТекСтрока = ЭтотОбъект.Ссылка;
		Исключение
			ТекСтрока = Неопределено;
		КонецПопытки;
		
		удОбновитьКартинкуЗаметок(ТекСтрока, ЭтаФорма);
		
		БлокироватьРеквизиты = Неопределено;
		Если ЭтотОбъект.ДоступностьИзменения(БлокироватьРеквизиты) Тогда
			// можно свободно редактировать объект  
		Иначе
			// объект участвовал в движениях 
			Сообщение="Текущий элемент участвовал в движениях.";
			МожноРедактировать=обПраво("РедактированиеНеизменяемыхРеквизитов",ЭтотОбъект.Права);
			ВыводитьСообщения=обПраво("ВыводитьСообщенияПроверкиПравДоступа",ЭтотОбъект.Права);
			// если нет списка блокируемых реквизитов то объект управляется на уровне формы целиком
			Если (БлокироватьРеквизиты=НЕОПРЕДЕЛЕНО) Тогда
				Если МожноРедактировать Тогда
					Если ВыводитьСообщения Тогда
						Сообщение=Сообщение+" Не рекомендуется его редактировать.";
						Сообщить(Сообщение,СтатусСообщения.Внимание);
					КонецЕсли;
				Иначе
					Сообщение=Сообщение+" Запрещается его редактирование.";
					ЭтаФорма.ТолькоПросмотр = Истина;
					Сообщить(Сообщение,СтатусСообщения.Важное);
				КонецЕсли;
			Иначе // будем запрещать отдельные реквизиты
				Если (МожноРедактировать) Тогда
					Если ВыводитьСообщения Тогда
						Сообщение=Сообщение+" Не рекомендуется изменять его поля:";
						Сообщить(Сообщение,СтатусСообщения.Внимание);
					КонецЕсли;
				Иначе
					Сообщение=Сообщение+" Запрещается изменять его поля:";
					Сообщить(Сообщение,СтатусСообщения.Важное);
				КонецЕсли;
				// понадобиться объект метаданных для получения "пользовательских имен" реквизитов				
				Если ВыводитьСообщения ИЛИ НЕ(МожноРедактировать) Тогда
					РеквизитыОбъекта = ЭтотОбъект.Метаданные().Реквизиты;
				КонецЕсли;
					
				Попытка // на случай если неправильно определена структура блокируемых реквизитов
					Для Каждого Реквизит Из БлокироватьРеквизиты Цикл
						ИмяРеквизита=Реквизит.Ключ;
						//получим представление реквизита если нужно его будет выводить
						Если НЕ(МожноРедактировать) ИЛИ ВыводитьСообщения Тогда
							МетоОписаниеРеквизита=РеквизитыОбъекта.Найти(ИмяРеквизита);
							Если МетоОписаниеРеквизита=НЕОПРЕДЕЛЕНО Тогда
								ПредставлениеРеквизита=ИмяРеквизита;
							Иначе	
								ПредставлениеРеквизита=МетоОписаниеРеквизита.Представление();
							КонецЕсли;
							ПредставлениеРеквизита = """"+ПредставлениеРеквизита+"""";
						КонецЕсли;
						Если МожноРедактировать Тогда
							Если ВыводитьСообщения Тогда
								Сообщить(ПредставлениеРеквизита);
							КонецЕсли;
						Иначе
							Если ЭтаФорма.ЭлементыФормы.Найти(ИмяРеквизита) <> Неопределено Тогда
								ЭтаФорма.ЭлементыФормы[ИмяРеквизита].Доступность = Ложь;
							КонецЕсли;
							Сообщить(ПредставлениеРеквизита);
						КонецЕсли;
					КонецЦикла;
				Исключение	// ошибка в структуре блокируемых реквизитов, значит блокируем всю форму !
					Сообщить("Ошибка при доступе к реквизитам формы: "+ОписаниеОшибки(),СтатусСообщения.Важное);
					Если МожноРедактировать Тогда
						// бывает что пользователю все-таки можно редактировать 
					Иначе
						ЭтаФорма.ТолькоПросмотр = Истина; 
					КонецЕсли;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	// Проверим на использование объекта в константах
	Если ЭтотОбъект.ЭтоНовый() ИЛИ ЭтаФорма.ТолькоПросмотр ИЛИ обПраво("РедактированиеОбъектовЗначенийКонстант",ЭтотОбъект.Права) Тогда
		// тогда не нужно даже проверять
	Иначе
		ИмяКонстанты = "";
		Если обОбъектУказанВКонстантах(ЭтотОбъект,ИмяКонстанты) Тогда
			ЭтаФорма.ТолькоПросмотр = Истина;
			Предупреждение("Текущий элемент используется как значение константы """+ ИмяКонстанты +"""
			|Редактирование данного элемента запрещено. Форма будет открыта в режиме просмотра.",30);
				
		КонецЕсли;
	КонецЕсли;
		
	// Определимся это элемент или группа (для ПВР всегда элемент)
	Попытка  ЭтоГруппа = ЭтаФорма.ЭтоГруппа;
	Исключение ЭтоГруппа = Ложь;
	КонецПопытки;
		
	// расставим автоотметки незаполненных реквизитов
	Если НЕ ЭтаФорма.ТолькоПросмотр Тогда
		СтруктураОбязательныхРеквизитов=ЭтотОбъект.ПолучитьОбязательныеРеквизиты(Не(ЭтоГруппа),ЭтоГруппа);
		удРасставитьАвтоотметкиНезаполненного(ЭтаФорма, СтруктураОбязательныхРеквизитов);
	КонецЕсли;
		
	// для стандартных командных панелей разберемся с отображением иконок
	ПанельОсновныеДействияФормы = ЭтаФорма.ЭлементыФормы.Найти("ОсновныеДействияФормы");
	Если (ПанельОсновныеДействияФормы<>Неопределено) Тогда
		Если обПраво("ПоказыватьИконкиНаКнопкахПанелей",ЭтотОбъект.Права) Тогда
			РежимОтображения=ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
		Иначе
			РежимОтображения=ОтображениеКнопкиКоманднойПанели.Надпись;
		КонецЕсли;
		Для Каждого КнопкаПанели Из ПанельОсновныеДействияФормы.Кнопки Цикл
			Если КнопкаПанели.ТипКнопки<>ТипКнопкиКоманднойПанели.Разделитель Тогда
				КнопкаПанели.Отображение=РежимОтображения;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	//добавим кнопку "ДополнительнаяФорма", если такая форма имеется в конфигурации у объекта
	ДобавлятьКнопкуДополнительнаяФорма = Ложь;
	Попытка
		ЭтотОбъектЭтоГруппа=ЭтотОбъект.ЭтоГруппа;
	Исключение
		ЭтотОбъектЭтоГруппа=Неопределено;
	КонецПопытки;
	Если ЭтотОбъектЭтоГруппа<>Неопределено Тогда
		Если ЭтотОбъект.ЭтоГруппа Тогда
			НайденнаяФорма = ЭтотОбъект.Метаданные().Формы.Найти("ФормаГруппыДополнительная");
		Иначе
			НайденнаяФорма = ЭтотОбъект.Метаданные().Формы.Найти("ФормаЭлементаДополнительная");
		КонецЕсли;
	Иначе
		НайденнаяФорма = Неопределено;
	КонецЕсли; 
	Если НайденнаяФорма <> Неопределено Тогда
		ДобавлятьКнопкуДополнительнаяФорма = Истина;
		//в саму дополнительную форму кнопку не добавляем
		Попытка
			Если ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("Форма") И
				ТипЗнч(ЭтаФорма.ВладелецФормы.ЭлементыФормы.ДействияФормы.Кнопки["ДополнительнаяФорма"])=Тип("КнопкаКоманднойПанели") Тогда
				ДобавлятьКнопкуДополнительнаяФорма = Ложь;	
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	Если ДобавлятьКнопкуДополнительнаяФорма Тогда
		Попытка
			КП = ЭтаФорма.ЭлементыФормы.ДействияФормы;
			НовоеДействие = Новый Действие("ДополнительныеДействияФормы");
			ТипКнопки = ТипКнопкиКоманднойПанели.Действие;
			НоваяКнопка = КП.Кнопки.Добавить("ДополнительнаяФорма", ТипКнопки, "Дополнительная форма", НовоеДействие);
			НоваяКнопка.Подсказка = "Открыть дополнительную форму";
			НоваяКнопка.Картинка = БиблиотекаКартинок.Формы;
		Исключение
		КонецПопытки;
	КонецЕсли;	
	
	//попробуем переназначить объект панели свойств, если открыта
	Обработки.ПанельСвойствОбъекта.Создать().ОткрытьПанельСвойств(ЭтаФорма.ЭтотОбъект, ЭтаФорма);
	
	// Если элемент помечен на удаление, добавим соответствующий заголовок
	Если ЭтаФорма.ЭтотОбъект.ПометкаУдаления Тогда
		ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + "Удален";
	КонецЕсли;
	
	// Зарегистрируем форму в ПанелеАРМ
	Наименование = ЭтаФорма.Наименование;
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтаФорма.Ссылка)) Тогда
		Если ЭтаФорма.СправочникОбъект.ЭтоНовый() Тогда
			Наименование = ЭтаФорма.СправочникОбъект.Метаданные().Синоним + " (Новый)";
		КонецЕсли;
		ФормаКартинка = "Справочник";
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтаФорма.Ссылка)) Тогда
		ФормаКартинка = "ПланВидовХарактеристик";
	ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтаФорма.Ссылка)) Тогда
		Если ЭтаФорма.ПланВидовРасчетаОбъект.ЭтоНовый() Тогда
			Наименование = ЭтаФорма.ПланВидовРасчетаОбъект.Метаданные().Синоним + " (Новый)";
		КонецЕсли;
		ФормаКартинка = "ПланВидовРасчета";
	КонецЕсли;
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().ЗарегистрироватьФорму(ЭтаФорма, ЭтаФорма.Ссылка, ФормаКартинка, Наименование);
	
	// Поиск в РегистреСведений.ОбъектыДляЗамены записей о замене
	#Если Клиент Тогда
		Если НЕ ЭтотОбъект.ЭтоНовый() Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			                      |	ОбъектыДляЗамены.ИдентификаторЗаменяемого
			                      |ИЗ
			                      |	РегистрСведений.ОбъектыДляЗамены КАК ОбъектыДляЗамены
			                      |ГДЕ
			                      |	ОбъектыДляЗамены.ИдентификаторЗаменяемого = &ИдентификаторЗаменяемого");
			Запрос.УстановитьПараметр("ИдентификаторЗаменяемого", ЗначениеВСтрокуВнутр(ЭтаФорма.Ссылка));
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Сообщить("Для данного объекта существует запись о замене на другой объект!");
			КонецЕсли;
		КонецЕсли;
	#КонецЕсли
	
	Возврат Результат;
КонецФункции // спЭлементПриОткрытии()

// Общий обработчик события ПриПовторномОткрытии формы элемента 
//
// Параметры:
// ЭтаФорма             - форма - переданная форма
// СтандартнаяОбработка - Булево - в данный параметр передается признак выполнения стандартной (системной)
//									обработки события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПриПовторномОткрытии(ЭтаФорма, СтандартнаяОбработка) Экспорт
	
	Результат = спЭлементПриОткрытии(ЭтаФорма, Истина);
	Возврат Результат;
	
КонецФункции // спЭлементПриПовторномОткрытии()

// Общий обработчик события ОбновлениеОтображения формы элемента 
//
// Параметры:
// ЭтаФорма - форма на которой все это происходит
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементОбновлениеОтображения(ЭтаФорма) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спЭлементОбновлениеОтображения()

// Общий обработчик события ПередЗакрытием формы элемента 
//
// Параметры:
// ЭтаФорма             - форма - переданная форма
// Отказ                - Булево - Признак отказа от закрытия формы.
// СтандартнаяОбработка - Булево - в данный параметр передается признак выполнения стандартной (системной)
//									обработки события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	СохранитьРазмерыФормы(ЭтаФорма);

	Результат = Истина;
	
	//Попытка // Проверим сами закрываемся или нас закрывают "снаружи"
	//	ВнешняяКомпонентаПодключена=Обработки.Защита.Создать().Компонента.УправлениеАктивно();
	//Исключение 
	//	ВнешняяКомпонентаПодключена=Ложь;
	//КонецПопытки;
	//Если НЕ ВнешняяКомпонентаПодключена Тогда
	//	СтандартнаяОбработка=Ложь; // Аварийное закрытие - нельзя что-либо спрашивать!
	//КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спЭлементПередЗакрытием()

// Общий обработчик события ПриЗакрытии формы элемента 
//
// Параметры:
// ЭтаФорма             - форма на которой все это происходит
// Отказ                - Булево - Признак отказа от закрытия формы
// СтандартнаяОбработка - Булево - в данный параметр передается признак выполнения стандартной (системной)
//									обработки события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПриЗакрытии(ЭтаФорма) Экспорт
	
	Результат = Истина;
	
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().УдалитьИзДереваФорм(ЭтаФорма);
	
	Возврат Результат;
	
КонецФункции // спЭлементПриЗакрытии()

// Общий обработчик ОбработкаОповещения формы элемента 
//
// Параметры:
//  ЭтаФорма   - форма - переданная форма
//  ИмяСобытия - Строка - Имя события. Может быть использовано для идентификации сообщений.
//  Параметр   - Произвольный - Параметр, переданный в сообщении
//  Источник   - Источник события, переданный в сообщении. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Результат = Истина;
	
	// Отбросим собственные оповещения
	Если ЭтаФорма=Источник Тогда Возврат Результат; КонецЕсли;
		
	Если ИмяСобытия = "ОбновитьПрава" Тогда
		Если Параметр=ПараметрыСеанса.Пользователь ИЛИ Параметр=ПараметрыСеанса.Пользователь.ШаблонПрав ИЛИ
			 Параметр=ПараметрыСеанса.ПодразделениеКомпании ИЛИ Параметр=ПараметрыСеанса.Организация Тогда
			// Были обновлены права пользователя (глПрава) - необходимо пересчитывание кэша прав объекта
			Попытка Состояние("Обновляю права для элемента "+ЭтаФорма.Заголовок);
				ЭтаФорма.Права = обПолучитьПраваИНастройкиПользователя(Параметр);
				ЭтаФорма.Обновить();
			Исключение КонецПопытки;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбновилисьЗаметки" Тогда
		Попытка
			ТекСтрока = ЭтаФорма.ЭтотОбъект.Ссылка;
			Если обЗначениеНеЗаполнено(ТекСтрока) Тогда
				ТекСтрока = Неопределено;
			КонецЕсли;
		Исключение
			ТекСтрока = Неопределено;
		КонецПопытки;
		
		удОбновитьКартинкуЗаметок(ТекСтрока, ЭтаФорма);
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции // спЭлементОбработкаОповещения()

// Общий обработчик ВнешнееСобытие формы элемента 
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//  Источник - Строка - Источник внешнего события
//  Событие  - Строка - Наименование события.
//  Данные   - Строка - Данные для события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементВнешнееСобытие(ЭтаФорма, Источник, Событие, Данные) Экспорт
	
	Результат = ЭтаФорма.ВводДоступен();
	Возврат Результат;
	
КонецФункции // спЭлементВнешнееСобытие()

// Общий обработчик события ПередЗаписью формы элемента 
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//  Отказ    - Булево - Признак отказа от записи элемента
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПередЗаписью(ЭтаФорма, Отказ) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спЭлементПередЗаписью()

// Общий обработчик события ПриЗаписи формы элемента 
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//  Отказ    - Булево - Признак отказа от записи
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПриЗаписи(ЭтаФорма, Отказ) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спЭлементПриЗаписи()

// Общий обработчик события ПослеЗаписи формы элемента 
//
// Параметры:
//  ЭтаФорма - форма на которой все это происходит
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спЭлементПослеЗаписи(ЭтаФорма) Экспорт
	
	Результат = Истина;
	
	// Обновим регистрацию формы в ПанелеАРМ
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтаФорма.Ссылка)) Тогда
		ФормаКартинка = "Справочник";
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтаФорма.Ссылка)) Тогда
		ФормаКартинка = "ПланВидовХарактеристик";
	ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭтаФорма.Ссылка)) Тогда
		ФормаКартинка = "ПланВидовРасчета";
	КонецЕсли;
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().ЗарегистрироватьФорму(ЭтаФорма, ЭтаФорма.Ссылка, ФормаКартинка, ЭтаФорма.Наименование);
	
	// доступность кнопки "Заметки"
	Попытка ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.кнЗаметки.Доступность = Истина; Исключение; КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // спЭлементПослеЗаписи()

Функция спЭлементПередУстановкойПометкиУдаления(ЭтаФорма, Элемент, Отказ, ПраваПользователя=Неопределено) Экспорт
	
	Результат = Истина;
	
	Если НЕ обПраво("УдалениеЧерезПометку", ПраваПользователя) Тогда
			Сообщить("Нет прав на изменение пометки удаления!",СтатусСообщения.Важное); 
			Отказ=Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спСписокПередУстановкойПометкиУдаления()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ТАБЛИЦЫ И ДЕРЕВА ФОРМ СПРАВОЧНИКОВ И ПЛАНОВ ВИДОВ ХАРАКТЕРИСТИК

// Общий обработчик события Выбор списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Элемент              - Элемент управления, список справочника
//  ВыбраннаяСтрока      - Выбранная строка списка справочника
//  Колонка              - Выбранная колонка списка справочника. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спДеревоВыбор(ЭтаФорма, Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка) Экспорт
	
	Результат = Истина;
	
	// Используется только для иерархических справочников и планов видов характеристик
	СтандартнаяОбработка=Ложь;
	ЭтаФорма.ЭлементыФормы.Список.ТекущийРодитель=?(обЗначениеНеЗаполнено(ВыбраннаяСтрока),Неопределено,ВыбраннаяСтрока);
	
	Возврат Результат;
	
КонецФункции // спДеревоВыбор()

// Общий обработчик события Выбор списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Элемент              - Элемент управления, список справочника
//  ВыбраннаяСтрока      - Выбранная строка списка справочника
//  Колонка              - Выбранная колонка списка справочника. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокВыбор(ЭтаФорма, Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спСписокВыбор()

// Общий обработчик события ПриАктивизацииСтроки списка элементов 
// Может обновлять комментарий в форме списка и оповещать связанные формы о своем позиционировании
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Элемент              - Элемент управления, список справочника
//  ОбновитьКомментарий  - Булево - Для разрешения / запрещения действий
//  ВызыватьОповещение   - Булево - Для разрешения / запрещения действий
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриАктивизацииСтроки(ЭтаФорма, Элемент, ОбновитьКомментарий=Истина, ВызыватьОповещение=Истина) Экспорт
	
	Результат = Истина;
	
	Попытка
		Если ОбновитьКомментарий и (ЭтаФорма.ЭлементыФормы.тКомментарий.ЦветТекста=ЦветаСтиля.ТекстИнформационнойНадписи) Тогда
			// покажем наименование элемента в комментарии
			Попытка
				Если Элемент.ТекущиеДанные<>Неопределено Тогда
					текстКомментарий = ?(Элемент.ТекущиеДанные.Ссылка.Владелец = Неопределено, Элемент.ТекущиеДанные.Ссылка.Наименование, "Владелец: " + СокрЛП(Элемент.ТекущиеДанные.Ссылка.Владелец.Наименование));
					ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок=СокрЛП(текстКомментарий);
				Иначе
					//попробуем считать владельца из отбора
					ЕстьВладелец = (ЭтаФорма.Отбор.Найти("Владелец") <> Неопределено) И (НЕ ЭтаФорма.Отбор.Владелец.Значение.Пустая());
					текстКомментарий = ?(ЕстьВладелец, "Владелец: " + СокрЛП(ЭтаФорма.Отбор.Владелец.Значение.Наименование),"");
					ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок=текстКомментарий;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
	Исключение
	КонецПопытки;
		
	Если (НЕ ЭтаФорма.МодальныйРежим) И (НЕ ЭтаФорма.РежимВыбора) И ВызыватьОповещение Тогда
		Попытка
			Оповестить("АктивизированЭлемент",Элемент.ТекущиеДанные.Ссылка,ЭтаФорма);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	
	// заметки
	Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Найти("кнЗаметки");
	Если Кнопка <> Неопределено И Элемент.ТекущаяСтрока <> Неопределено Тогда
		// проверим наличие заметок
		НаборРегистра = РегистрыСведений.ЗаметкиПользователей.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Объект.ВидСравнения  = ВидСравнения.Равно;
		НаборРегистра.Отбор.Объект.Значение      = Элемент.ТекущаяСтрока;
		НаборРегистра.Отбор.Объект.Использование = Истина;
		НаборРегистра.Прочитать();
		
		Если НаборРегистра.Количество() > 0 Тогда
			Кнопка.Картинка = БиблиотекаКартинок.ЗаметкиПерейти;
		Иначе
			Кнопка.Картинка = БиблиотекаКартинок.ЗаметкиСоздать;
		КонецЕсли;
		
		Оповестить("ОбновитьОтборЗаметок", Элемент.ТекущаяСтрока, ЭтаФорма);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции // спСписокПриАктивизацииСтроки()

// Общий обработчик события ПередНачаломДобавления списка элементов 
// Может обновлять комментарий в форме списка и оповещать связанные формы о своем позиционировании
// 
// Параметры:
// ЭтаФорма			 - Переданная форма.
// Элемент			 - Элемент управления, список справочника
// Отказ			 - Булево, признак отказа от добавления
//Копирование		 - Булево, признак копирования
// Родитель   	 	 - СправочникСсылка - родитель создаваемого элемента
// ЭтоГруппа	     - Булево, признак что создается группа
// ИмяФормы		     - Строка, имя открываемой формы
// ПраваПользователя - кеш прав пользователя
// ИмяДанныхСпискаЭлементов - строка, имя данных табличного поля списка справочника
// КлючУникальности  - Произвольный, ключ уникальности создаваемой формы
// ТиповоеОткрытиеФормы - Булево, флаг отменяющий стандартное открытие (форма будет открыта из этой процедуры)
//						  если передать Ложь, то отказа от стандартного открытия не будет, вызвавший код 
//						  может сам вмешаться в стандартное поведение и выполнить специфические действия
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередНачаломДобавления(ЭтаФорма, Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, ИмяФормы=Неопределено, ПраваПользователя=Неопределено, ИмяДанныхСпискаЭлементов = Неопределено, КлючУникальности = Неопределено, ТиповоеОткрытиеФормы = Истина) Экспорт
	
	Результат = Истина;
	
	// Проверка на допустимость редактирования справочника по текущему пользователю 
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам", ПраваПользователя) Тогда	
		Если Элемент.СпособРедактирования = СпособРедактированияСписка.ВСписке Тогда
			спПроверкаПраваДоступа(Элемент.ТекущийРодитель, Отказ,, ПраваПользователя);
			Если Отказ Тогда
				Возврат Результат;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	Если Копирование Тогда
		Возврат Результат;
	КонецЕсли;
	Попытка
		СписокСправочника = ?(ИмяДанныхСпискаЭлементов = Неопределено, ЭтаФорма.СправочникСписок, ЭтаФорма[ИмяДанныхСпискаЭлементов]);
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(СписокСправочника));
	Исключение
		Возврат Результат;
	КонецПопытки;
	//не нашли, выходим
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	//если редактируем в списке, выходим
	Если ОбъектМетаданных.СпособРедактирования = Метаданные.СвойстваОбъектов.СпособРедактирования.ВСписке Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТиповоеОткрытиеФормы Тогда
		Отказ = Истина; //откажемся от стандартного и сделаем свое типовое
		Если ЭтоГруппа Тогда
			НовыйЭлемент = Справочники[ОбъектМетаданных.Имя].СоздатьГруппу();
		Иначе
			НовыйЭлемент = Справочники[ОбъектМетаданных.Имя].СоздатьЭлемент();
		КонецЕсли;
		Если ОбъектМетаданных.Иерархический Тогда
			Если ОбъектМетаданных.ВидИерархии=Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов Тогда
				НовыйЭлемент.Родитель = ЭтаФорма.ТекущийЭлемент.ТекущаяСтрока;	
			Иначе
				НовыйЭлемент.Родитель = ЭтаФорма.ТекущийЭлемент.ТекущийРодитель;
			КонецЕсли;
		КонецЕсли;
		ИспользованиеРеквизита = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита;
		
		//если отбор установлен, будем заполнять реквизиты
		Для Каждого ЭлементОтбора Из СписокСправочника.Отбор Цикл
			Если (Не обЗначениеНеЗаполнено(ЭлементОтбора.Значение)) И (ЭлементОтбора.Использование) И (ЭлементОтбора.ВидСравнения = ВидСравнения.Равно) Тогда
				ИмяРеквизита = ЭлементОтбора.Имя;
				Если Найти("Код|Наименование|Владелец",ИмяРеквизита) > 0 Тогда
					НовыйЭлемент[ИмяРеквизита] = ЭлементОтбора.Значение;
					НовыйЭлемент.ОбработкаРеквизита(ИмяРеквизита);  
				Иначе
					Реквизит = ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита);
				КонецЕсли;
				Если Реквизит = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Если ((Реквизит.Использование = ИспользованиеРеквизита.ДляЭлемента) 
					ИЛИ (Реквизит.Использование = ИспользованиеРеквизита.ДляГруппыИЭлемента)) И (НЕ ЭтоГруппа) Тогда
					НовыйЭлемент[ИмяРеквизита] = ЭлементОтбора.Значение;
					НовыйЭлемент.ОбработкаРеквизита(ИмяРеквизита);  
				ИначеЕсли ((Реквизит.Использование = ИспользованиеРеквизита.ДляГруппы) 
					ИЛИ (Реквизит.Использование = ИспользованиеРеквизита.ДляГруппыИЭлемента)) И (ЭтоГруппа) Тогда
					НовыйЭлемент[ИмяРеквизита] = ЭлементОтбора.Значение;
					НовыйЭлемент.ОбработкаРеквизита(ИмяРеквизита);  
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
				
		ОтборВладелец = СписокСправочника.Отбор.Найти("Владелец");
		Если ОтборВладелец <> Неопределено Тогда
			//отбор владелец есть, но он сброшен
			Если обЗначениеНеЗаполнено(НовыйЭлемент.Владелец) Тогда
				//значение реквизита "Владелец" не заполнено,
				//тогда берем из текущей строки
				Попытка
					ВремВладелец = ЭтаФорма.ЭлементыФормы.Список.ТекущиеДанные.Владелец;
				Исключение
					ВремВладелец = "";
				КонецПопытки;
					
				СписокТиповВладельцев=Новый СписокЗначений;
				ТипВладельцаПоУмолчанию=Неопределено;
				
				Для каждого ТекВладелецМетаданные Из ОбъектМетаданных.Владельцы Цикл
					НовыйТипВладельца=СписокТиповВладельцев.Добавить(ТекВладелецМетаданные,ТекВладелецМетаданные.Синоним);
					Если ВремВладелец <> "" Тогда
						Если ТекВладелецМетаданные=ВремВладелец.Метаданные() Тогда
							ТипВладельцаПоУмолчанию=НовыйТипВладельца;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			
				ВыбранныйТипВладельца=Неопределено;
				Если СписокТиповВладельцев.Количество()>1 Тогда
					ВыбранныйТипВладельца=СписокТиповВладельцев.ВыбратьЭлемент("Выберите тип владельца",ТипВладельцаПоУмолчанию);
				Иначе
					ВыбранныйТипВладельца=СписокТиповВладельцев[0];
				КонецЕсли; 
				Если ВыбранныйТипВладельца=Неопределено Тогда
					Предупреждение("Тип владельца не определен!");
					Возврат Результат;
				КонецЕсли;
				ВыбранныйТипВладельца=ВыбранныйТипВладельца.Значение;
					
				ПолноеИмя  = ВыбранныйТипВладельца.ПолноеИмя();
				ЛеваяЧасть  = Лев(ПолноеИмя, Найти(ПолноеИмя, ".")-1);
				ПраваяЧасть = Сред(ПолноеИмя, Найти(ПолноеИмя, ".")+1);
				Если ЛеваяЧасть<>"" и ПраваяЧасть<>"" Тогда
					Если ЛеваяЧасть="Справочник" Тогда
						Менеджер = Справочники[ПраваяЧасть];   							
					ИначеЕсли ЛеваяЧасть="ПланВидовХарактеристик" Тогда
						Менеджер = ПланыВидовХарактеристик[ПраваяЧасть];								
					ИначеЕсли ЛеваяЧасть="ПланСчетов" Тогда
						Менеджер = ПланыСчетов[ПраваяЧасть];  							
					ИначеЕсли ЛеваяЧасть="ПланыВидовРасчета" Тогда
						Менеджер = ПланыВидовРасчета[ПраваяЧасть]; 							
					КонецЕсли;
							
					ФормаВыбора = Менеджер.ПолучитьФормуВыбора(, ЭтаФорма);
					ФормаВыбора.НачальноеЗначениеВыбора = ВремВладелец;
					ТекВладелец = ФормаВыбора.ОткрытьМодально();						
				КонецЕсли; 
					
				Если обЗначениеНеЗаполнено(ТекВладелец) Тогда
					Предупреждение("Владелец не определен!");
					Возврат Результат;
				Иначе				
					НовыйЭлемент.Владелец = ТекВладелец;		
				КонецЕсли;			
			КонецЕсли;
		КонецЕсли;
		// Откроем наконец форму
		Попытка
			ФормаНовогоЭлемента=НовыйЭлемент.ПолучитьФорму(ИмяФормы,Элемент,КлючУникальности);
		Исключение
			ФормаНовогоЭлемента=НовыйЭлемент.ПолучитьФорму(ИмяФормы,,КлючУникальности);
		КонецПопытки;
		ФормаНовогоЭлемента.Модифицированность = Ложь;
		ФормаНовогоЭлемента.Открыть();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спСписокПередНачаломДобавления()

// Общий обработчик события ПередНачаломИзменения списка элементов 
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  Элемент           - Элемент управления, список справочника
//  Отказ             - Булево - Признак отказа от изменения.
//  ПраваПользователя - Соответствие - кеш прав пользователя
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередНачаломИзменения(ЭтаФорма, Элемент, Отказ, ПраваПользователя=Неопределено) Экспорт
	
	Результат = Истина;
	
	Если Элемент.ТекущаяСтрока = Неопределено Тогда Возврат Результат; КонецЕсли;
	
	// Проверка на допустимость редактирования справочника по текущему пользователю 
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам", ПраваПользователя) Тогда	
		Если Элемент.СпособРедактирования = СпособРедактированияСписка.ВСписке Тогда
			спПроверкаПраваДоступа(Элемент.ТекущаяСтрока, Отказ,, ПраваПользователя);
		КонецЕсли;
	КонецЕсли;
	
	//запишем объект в панели свойств, если открыта во избежании разных версий объекта
	Попытка
		Показывать = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Свойства.Пометка;
		Если Показывать Тогда
			Обработки.ПанельСвойствОбъекта.Создать().ОткрытьПанельСвойств(Элемент.ТекущаяСтрока.ПолучитьОбъект(), ЭтаФорма, Показывать);
		КонецЕсли;
	Исключение КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // спСписокПередНачаломИзменения()

// Общий обработчик события ПередУдалением формы списка элементов 
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  Элемент           - Элемент управления, список справочника
//  Отказ             - Булево - Признак отказа от удаления объекта
//  ПраваПользователя - Соответствие - кеш прав пользователя
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередУдалением(ЭтаФорма, Элемент, Отказ, ПраваПользователя=Неопределено) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спСписокПередУдалением()

// Общий обработчик события ПриВыводеСтроки списка элементов 
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  Элемент           - Элемент управления, список справочника
//  ОформлениеСтроки  - ОформлениеСтроки - Содержит оформление строки (шрифт, цвет) и коллекцию оформлений ячеек.
//  ДанныеСтроки      - Данные выводимой строки - Параметр соответствует свойству ТекущиеДанные для выводимой строки. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриВыводеСтроки(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спСписокПриВыводеСтроки()

// Общий обработчик события ПриОкончанииРедактирования списка элементов 
// Параметры:
//  ЭтаФорма     - форма - переданная форма
//  Элемент      - Элемент управления, список справочника
//  НоваяСтрока  - Булево - Признак редактирования новой строки. Имеет значение Истина,
//							если строка была добавлена или скопирована. 
//  Отказ        - Булево - Истина, если произошла отмена редактирования. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриОкончанииРедактирования(ЭтаФорма, Элемент, НоваяСтрока, Отказ) Экспорт
	Результат = спСписокПриАктивизацииСтроки(ЭтаФорма,Элемент, Истина, Истина);
	Возврат Результат;
КонецФункции // спСписокПриОкончанииРедактирования()

// Общий обработчик события ОбработкаЗаписиНовогоОбъекта списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Элемент              - Элемент управления, список справочника
//  Объект               - Ссылка на объект, либо ТекущаяСтрока - Добавленный в подчиненной форме объект. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокОбработкаЗаписиНовогоОбъекта(ЭтаФорма, Элемент, Объект, СтандартнаяОбработка) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спСписокОбработкаЗаписиНовогоОбъекта()

// Общий обработчик события ПроверкаПеретаскивания списка элементов 
//
// Параметры:
//  Параметры перетаскивания - ПараметрыПеретаскивания,  содержит перетаскиваемое значение, 
//  тип действия и возможные действия при перетаскивании. 
//  Стандартная обработка    - Булево, в данный параметр передается признак выполнения 
//  стандартной (системной) обработки события. 
//  Строка   				- Строка, над которой находится объект. 
//  Колонка					- КолонкаТабличногоПоля, колонка, над которой находится объект. 
//
// Возврат
// Булево, результат проверки
Функция спСписокПроверкаПеретаскивания(ПараметрыПеретаскивания, Строка, Колонка) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат Истина
	КонецЕсли;	
	НедопустимоеПеретаскивание = Ложь;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для каждого ЭлементМассива Из ПараметрыПеретаскивания.Значение Цикл
			Если НЕ ЭлементМассива.Пустая() Тогда 	
				Если НЕ Строка.Пустая()  Тогда
					Если (Строка = ЭлементМассива) 
						ИЛИ	Строка.ПолучитьОбъект().ПринадлежитЭлементу(ЭлементМассива) Тогда
					НедопустимоеПеретаскивание = Истина;
					КонецЕсли;
				КонецЕсли;
			Иначе	
				НедопустимоеПеретаскивание = Истина;
			КонецЕсли;	
			Если  НедопустимоеПеретаскивание = Истина Тогда
				
				Возврат НедопустимоеПеретаскивание;
				
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если НЕ ПараметрыПеретаскивания.Значение.Пустая()  Тогда 
			Если НЕ Строка.Пустая()  Тогда
				НедопустимоеПеретаскивание = Строка.ПолучитьОбъект().ПринадлежитЭлементу(ПараметрыПеретаскивания.Значение);
			КонецЕСли;
		Иначе
			НедопустимоеПеретаскивание = Истина;
		КонецЕсли;	
		
		Возврат   НедопустимоеПеретаскивание;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции	//спСписокПроверкаПеретаскивания()

// Общий обработчик события Перетаскивание списка элементов 
// 
// Параметры:
// Элемент					- Элемент управления для которого вызван обработчик события.
// ПараметрыПеретаскивания	- ПараметрыПеретаскивания,  содержит перетаскиваемое значение,
//							  тип действия и возможные действия при перетаскивании.
// СтандартнаяОбработка		- Булево, в данный параметр передается признак выполнения стандартной (системной)
//							  обработки события.
// Строка   				- Строка, над которой находится объект.
// Колонка					- КолонкаТабличногоПоля, колонка, над которой находится объект.
// ПраваПользователя		- Соответствие, Кеш прав текущего пользователя.
// Вопрос					- Булево, если передано Ложь, тогда  не будет задан вопрос на перетаскивание.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка, ПраваПользователя, Вопрос = Истина) Экспорт
	
	Результат = Истина;
	
	// Откажемся от стандартного обработчика
	СтандартнаяОбработка = ЛОЖЬ;
	
	// Проверим есть ли что переносить
	Если обЗначениеНеЗаполнено(ПараметрыПеретаскивания.Значение) Тогда
		Сигнал();
		Возврат Результат;
	КонецЕсли;
	
	//Зададим вопрос
	Если Вопрос Тогда
		
		Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ПараметрыПеретаскивания.Значение.Количество() > 1 Тогда
			ТекстВопроса = "Перенести элементы в " + ?(НЕ Строка.Пустая(), "группу <" + СокрЛП(Строка(Строка)) + ">", "корень справочника") + "?";
		ИначеЕсли НЕ ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ПараметрыПеретаскивания.Значение.ЭтоГруппа Тогда
			Попытка 
				Наименование = ПараметрыПеретаскивания.Значение.Наименование;
			Исключение
				Наименование = СокрЛП(ПараметрыПеретаскивания.Значение.Код);
			КонецПопытки;
			ТекстВопроса = "Перенести группу <" + Наименование + "> в " + ?(НЕ Строка.Пустая(), "группу <" + СокрЛП(Строка(Строка)) + ">", "корень справочника") + "?";
		Иначе
			ТекстВопроса = "Перенести элемент в " + ?(НЕ Строка.Пустая(), "группу <" + СокрЛП(Строка(Строка)) + ">", "корень справочника") + "?";
		КонецЕсли; 
		Если Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,,"Перенос элемента") = КодВозвратаДиалога.Нет Тогда
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		// Переносим
		НачатьТранзакцию();
		Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
			
			Для каждого ТекСсылка Из ПараметрыПеретаскивания.Значение Цикл
				Если (ТекСсылка = Строка) ИЛИ (ТекСсылка.Родитель = Строка) Тогда
					Вопрос("Невозможно перенести в самого себя или в своего же родителя.",РежимДиалогаВопрос.ОК);
					ВызватьИсключение "";
				КонецЕсли;
				Объект = ТекСсылка.ПолучитьОбъект();
				Объект.Родитель = Строка;
				Объект.Записать();
			КонецЦикла;
			
		Иначе
			Ссылка = ПараметрыПеретаскивания.Значение;
			Если (Ссылка = Строка) ИЛИ (Ссылка.Родитель = Строка) Тогда
				Вопрос("Невозможно перенести в самого себя или в своего же родителя.",РежимДиалогаВопрос.ОК);
				ВызватьИсключение "";
			КонецЕсли;
			Объект = Ссылка.ПолучитьОбъект();
			Объект.Родитель = Строка;
			Объект.Записать();
		КонецЕсли;
		ЗафиксироватьТранзакцию();
		
	Исключение
		Сигнал();
		ОтменитьТранзакцию();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // спСписокПеретаскивание()

// Общий обработчик события ПередИзменениемРодителя списка элементов 
// Осуществляет "человеческий" перенос элемента в группу (как в 7.7)
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  Элемент           - Элемент управления, список справочника
//  Отказ             - Булево - Признак отказа от изменения родителя.
//  ПраваПользователя - Соответствие - кеш прав пользователя
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередИзменениемРодителя(ЭтаФорма, Элемент, Отказ, ПраваПользователя=Неопределено) Экспорт
	
	Результат = Истина;
	
	// Откажемся от стандартного обработчика
	Отказ = Истина;
	
	// Проверим есть ли что переносить
	Если Элемент.ТекущиеДанные = Неопределено И Элемент.ВыделенныеСтроки.Количество() < 1 Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Нужно поискать на форме дерево групп
	Если	(ЭтаФорма.ЭлементыФормы.Найти("Дерево")<>Неопределено)
		И	(ЭтаФорма.ЭлементыФормы.Дерево.Дерево)
		И	(ЭтаФорма.ЭлементыФормы.Дерево.ТекущаяСтрока<>Неопределено)
		И	(ЭтаФорма.ЭлементыФормы.Дерево.Видимость) Тогда
		
		// получим текущую группы выбранную в дереве групп
		Если ЭтаФорма.ЭлементыФормы.Дерево.ТекущиеДанные<>Неопределено Тогда
			ГруппаВДереве = ЭтаФорма.ЭлементыФормы.Дерево.ТекущиеДанные.Ссылка;
		Иначе
			ГруппаВДереве = Неопределено;
		КонецЕсли;
	Иначе
		Отказ = Ложь;
		Возврат Результат;
	КонецЕсли;
	
	// Зададим вопрос
	Если НЕ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		ТекстВопроса = "Перенести элемент в " + ?(ГруппаВДереве = Неопределено, "корень списка", "группу <" + СокрЛП(Строка(ГруппаВДереве)) + ">") + "?";
	Иначе
		Попытка 
			Наименование = Элемент.ТекущиеДанные.Наименование;
		Исключение
			Наименование = СокрЛП(Элемент.ТекущиеДанные.Код);
		КонецПопытки;
		ТекстВопроса = "Перенести группу <" + Наименование + "> в " + ?(ГруппаВДереве = Неопределено, "корень списка", "группу <" + СокрЛП(Строка(ГруппаВДереве)) + ">") + "?";
	КонецЕсли; 
	Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Перенос элемента") = КодВозвратаДиалога.Нет Тогда
		Возврат Результат;
	КонецЕсли;
	
	
	Попытка
		// Переносим
		НачатьТранзакцию();
		Если Элемент.ВыделенныеСтроки.Количество() > 1 Тогда
			
			Для каждого ТекСсылка Из Элемент.ВыделенныеСтроки Цикл
				Если (ТекСсылка = ГруппаВДереве) ИЛИ (ТекСсылка.Родитель = ГруппаВДереве) Тогда
					Вопрос("Невозможно перенести в самого себя или в своего же родителя.",РежимДиалогаВопрос.ОК);
					ВызватьИсключение "";
				КонецЕсли;
				Объект = ТекСсылка.ПолучитьОбъект();
				Объект.Родитель = ГруппаВДереве;
				Объект.Записать();
			КонецЦикла;
			
		Иначе
			Ссылка = Элемент.ТекущиеДанные.Ссылка;
			Если (Ссылка = ГруппаВДереве) ИЛИ (Ссылка.Родитель = ГруппаВДереве) Тогда
				Вопрос("Невозможно перенести в самого себя или в своего же родителя.",РежимДиалогаВопрос.ОК);
				ВызватьИсключение "";
			КонецЕсли;
			Объект = Ссылка.ПолучитьОбъект();
			Объект.Родитель = ГруппаВДереве;
			Объект.Записать();
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	ЭтаФорма.Обновить();
	
	Возврат Результат;
	
КонецФункции // спПроверкаПеретаскивания()

// Общий обработчик события ПередУстановкойПометкиУдаления списка элементов 
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  Элемент           - Элемент управления, список справочника
//  Отказ             - Булево - Признак отказа от изменения пометки удаления объекта
//  ПраваПользователя - Соответствие - кеш прав пользователя
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередУстановкойПометкиУдаления(ЭтаФорма, Элемент, Отказ, ПраваПользователя=Неопределено) Экспорт
	
	Результат = Истина;
	
	Если Элемент.ТекущаяСтрока.Предопределенный Тогда
		Если ПравоДоступа("Администрирование",Метаданные) Тогда
		    Если Вопрос("Установить пометку удаления предопределенного элемента справочника?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Пометка удаления предопределенного элемента")<>КодВозвратаДиалога.Да Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли; 
		Иначе
			Сообщить("Запрещено удаление предопределенных элементов справочников.",СтатусСообщения.Внимание);
			Отказ=Истина;
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам", ПраваПользователя) Тогда	
		Если НЕ обПраво("УдалениеЧерезПометку", ПраваПользователя) Тогда
			Сообщить("Нет прав на изменение пометки удаления!",СтатусСообщения.Важное); 
			Отказ=Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спСписокПередУстановкойПометкиУдаления()

// Общий обработчик события ПриИзмененииОтбора списка элементов 
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриИзмененииОтбора(ЭтаФорма, Элемент=Неопределено) Экспорт
	
	Результат = Истина;
	
	//возможность отборов
	флОтборОрганизация   = (ЭтаФорма.СправочникСписок.Отбор.Найти("Организация")  <>Неопределено);
	флОтборВладелец      = (ЭтаФорма.СправочникСписок.Отбор.Найти("Владелец")     <>Неопределено);
	флОтборПодразделение = (ЭтаФорма.СправочникСписок.Отбор.Найти("Подразделение")<>Неопределено);
		
	//отлавливаем всевозможные изменения значений и использований отборов
	Если Элемент = "СправочникСписок.Отбор.Владелец.Использование" Тогда
		Если НЕ ЭтаФорма.СправочникСписок.Отбор.Владелец.Использование Тогда
			Если НЕ ЭтаФорма.СправочникСписок.Отбор.Владелец.Значение=Неопределено и ТипЗнч(ЭтаФорма.СправочникСписок.Отбор.Владелец.Значение)<>Тип("СписокЗначений") Тогда
				ИмяСправочникаВладельца = ЭтаФорма.СправочникСписок.Отбор.Владелец.Значение.Метаданные().Имя;
				ЭтаФорма.СправочникСписок.Отбор.Владелец.ВидСравнения = ВидСравнения.Равно;
				ЭтаФорма.СправочникСписок.Отбор.Владелец.Значение = Справочники[ИмяСправочникаВладельца].ПустаяСсылка();
			КонецЕсли;	
		КонецЕсли; 
	КонецЕсли;
	Если Элемент = "СправочникСписок.Отбор.Организация.Использование" Тогда
		Если НЕ ЭтаФорма.СправочникСписок.Отбор.Организация.Использование Тогда
			ЭтаФорма.СправочникСписок.Отбор.Организация.ВидСравнения = ВидСравнения.Равно;
			ЭтаФорма.СправочникСписок.Отбор.Организация.Значение = Справочники.Организации.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли;
	Если Элемент = "СправочникСписок.Отбор.Подразделение.Использование" Тогда
		Если НЕ ЭтаФорма.СправочникСписок.Отбор.Подразделение.Использование Тогда
			ЭтаФорма.СправочникСписок.Отбор.Подразделение.ВидСравнения = ВидСравнения.Равно;
			ЭтаФорма.СправочникСписок.Отбор.Подразделение.Значение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли;
	Если Элемент = "СправочникСписок.Отбор.Владелец.Значение" Тогда
		ЭтаФорма.СправочникСписок.Отбор.Владелец.Использование = НЕ обЗначениеНеЗаполнено(ЭтаФорма.СправочникСписок.Отбор.Владелец.Значение);
	КонецЕсли;
	Если Элемент = "СправочникСписок.Отбор.Организация.Значение" Тогда
		Если ЭтаФорма.ЭлементыФормы.Найти("Организация") = Неопределено Тогда
			ЭтаФорма.СправочникСписок.Отбор.Организация.Использование = Истина;
		Иначе
			ЭтаФорма.СправочникСписок.Отбор.Организация.Использование = НЕ обЗначениеНеЗаполнено(ЭтаФорма.ЭлементыФормы.Организация.Значение);
		КонецЕсли;
	КонецЕсли;
	Если Элемент = "СправочникСписок.Отбор.Подразделение.Значение" Тогда
		Если ЭтаФорма.ЭлементыФормы.Найти("Подразделение") = Неопределено Тогда
			ЭтаФорма.СправочникСписок.Отбор.Подразделение.Использование = Истина;
		Иначе
			ЭтаФорма.СправочникСписок.Отбор.Подразделение.Использование = НЕ обЗначениеНеЗаполнено(ЭтаФорма.ЭлементыФормы.Подразделение.Значение);
		КонецЕсли;
	КонецЕсли;
		
	//вывод комментария (фильтра списка или гиперссылки на владельца)
	ПредставлениеОтбора = обПолучитьПредставлениеОтбора(ЭтаФорма.СправочникСписок.Отбор);
	Если ПустаяСтрока(ПредставлениеОтбора) Тогда
		СвязьПоВладельцу = Истина;
	Иначе
		Если (СтрЧислоВхождений(ПредставлениеОтбора,"=")=1 И Найти(ПредставлениеОтбора,"Владелец")<>0) Тогда
			СвязьПоВладельцу = Истина;
		Иначе
			СвязьПоВладельцу = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЭтаФорма.ЭлементыФормы.Найти("тКомментарий") = Неопределено Тогда
		Если СвязьПоВладельцу Тогда
			ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок = "";
			ЭтаФорма.ЭлементыФормы.тКомментарий.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
			ЭтаФорма.ЭлементыФормы.тКомментарий.Подсказка="При нажатии можно открыть форму элемента - владельца";
			ПустаяСсылка=Новый(ЭтаФорма.СправочникСписок.Отбор.Ссылка.ТипЗначения.Типы()[0]);
			МетаданныеСсылки=ПустаяСсылка.Метаданные();
			ЭтаФорма.ЭлементыФормы.тКомментарий.ГиперСсылка=(МетаданныеСсылки.Владельцы.Количество()>0);
			Попытка
				спСписокПриАктивизацииСтроки(ЭтаФорма, ЭтаФорма.ЭлементыФормы.Список, Истина, Истина);
			Исключение
			КонецПопытки;
		Иначе
			ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок = Лев(ПредставлениеОтбора, СтрДлина(ПредставлениеОтбора)-1);
			ЭтаФорма.ЭлементыФормы.тКомментарий.ЦветТекста = Новый Цвет(155,0,0);
			ЭтаФорма.ЭлементыФормы.тКомментарий.Подсказка="Текущий фильтр";
			ЭтаФорма.ЭлементыФормы.тКомментарий.ГиперСсылка=Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции // спСписокПриИзмененииОтбора()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ФОРМ СПИСКОВ СПРАВОЧНИКОВ И ПЛАНОВ ВИДОВ ХАРАКТЕРИСТИК

// Общий обработчик события ПередОткрытием формы списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Отказ                - Булево - Признак отказа от открытия формы. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Попытка
		МетаданныеОбъекта=Метаданные.НайтиПоТипу(ТипЗнч(ЭтаФорма.СправочникСписок));
		ПодсистемаАвтосалон=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.НормативнаяПодсистема.Подсистемы.Автосалон;
		ПодсистемаТестДрайв=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.УчетныеБлоки.Подсистемы.ТестДрайв;
		Если ПодсистемаАвтосалон.Состав.Содержит(МетаданныеОбъекта) ИЛИ ПодсистемаТестДрайв.Состав.Содержит(МетаданныеОбъекта) Тогда
			Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон",Истина) Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		ПодсистемаУчетВзаимоотношенийСКлиентами=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.УчетныеБлоки.Подсистемы.УчетВзаимоотношенийСКлиентами;
		Если ПодсистемаУчетВзаимоотношенийСКлиентами.Состав.Содержит(МетаданныеОбъекта) Тогда
			Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаCRM",Истина) Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		ПодсистемаТехосмотр=Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.Документооборот.Подсистемы.Техосмотр;
		Если ПодсистемаТехосмотр.Состав.Содержит(МетаданныеОбъекта) Тогда
			Если НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаТехосмотр",Истина) Тогда
				Отказ=Истина;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	// Может быть отраслевое переопределение функции модуля...
	Результат = орФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	Иначе
		Результат = Истина;
	КонецЕсли; 
	
	// Стандартная обработка
	удФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// заполним доп. свойства формы справочников
	удЗаполнитьДополнительныеДействияСправочников(ЭтаФорма);
	
	// установим доступность колонки "код" для формы списка
	Список = ЭтаФорма.ЭлементыФормы.Найти("Список");
	Если Список <> Неопределено И Тип(Список) = Тип("ТабличноеПоле") 
		И Список.СпособРедактирования = СпособРедактированияСписка.ВСписке Тогда
		Код = Список.Колонки.Найти("Код");
    КонецЕсли;
	Если Код <> Неопределено Тогда
		Попытка
			ЕстьАвтонумерация = (Список.Автонумерация = АвтонумерацияВФорме.Авто);
		Исключение
			ЕстьАвтонумерация=Ложь;
		КонецПопытки;
		Если ЕстьАвтонумерация Тогда
			РедактированиеКодов = обПраво("РазрешитьРедактированиеКодовСправочников");
			Если РедактированиеКодов = Перечисления.ВариантыОтветов.Да Тогда
				Код.ТолькоПросмотр = Ложь;
				Список.Автонумерация = АвтонумерацияВФорме.Авто;
			ИначеЕсли РедактированиеКодов = Перечисления.ВариантыОтветов.Нет Тогда
				Код.ТолькоПросмотр = Истина;
				Список.Автонумерация = АвтонумерацияВФорме.НеИспользовать;
			ИначеЕсли РедактированиеКодов = Перечисления.ВариантыОтветов.Спрашивать Тогда
				Код.ТолькоПросмотр = Истина;
				ПодсказкаВШапке =  "Для включения редактирования кода используйте подменю ""Действия""";
				Если СтрДлина(Код.ПодсказкаВШапке) = 0 Тогда
					Код.ПодсказкаВШапке = ПодсказкаВШапке; 
				Иначе 
					Код.ПодсказкаВШапке = Код.ПодсказкаВШапке + ". " + ПодсказкаВШапке; 
				КонецЕсли;
				Список.Автонумерация = АвтонумерацияВФорме.НеИспользовать;
			КонецЕсли;	
		КонецЕсли; 
	КонецЕсли;    
	
	верс_ВерсионированиеОбъектов.верс_удЗаполнитьДополнительныеДействияДокументов(ЭтаФорма); //ЧалапАю БизТех 10.09.2024 - версионирование
	
	Возврат Результат;
	
КонецФункции // спСписокПередОткрытием()

// Общий обработчик события ПриОткрытии формы списка элементов 
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  ПовторноеОткрытие - Булево - Признак повторного открытия формы 
//  СтруктураОтбора   - Структура - В данный параметр передается Структура для выполнения отбора.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриОткрытии(ЭтаФорма, ПовторноеОткрытие=Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	Результат = Истина;
	
	// общие действия форм списков при открытии
	удФормаПриОткрытии(ЭтаФорма);
		
	// Восстановим связь формы по владельцу
	Попытка
		Если ЭтаФорма.РежимВыбора Тогда 
			//Ложь, что кнопка осталась начатой
			Пометка = Ложь;
		Иначе
			Пометка = ВосстановитьЗначение("СвязьПоВладельцу" + Вычислить("ЭтаФорма.СправочникСписок"));
		КонецЕсли;
		Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь;
		Если (Пометка = Истина) И НЕ ПовторноеОткрытие Тогда
			// Надо восстановить привязку формы к оповещению о смене регистратора
			// Внимание: логическое выражение "Пометка = Истина" не упрощать - Пометка может не иметь булева типа !
			ЭтаФорма.ДействияФормыСвязь(Кнопка);
		КонецЕсли; 
		Если ЭтаФорма.РежимВыбора Тогда 
			Кнопка.Доступность = Ложь; 
		КонецЕсли;
	Исключение
		Кнопка = Неопределено;
	КонецПопытки;
		
	// Восстановим видимость дерева групп (для иерархических справочников)
	Попытка  // У неподчиненного справочника нет кнопки панели "ОтображатьДерево"
		Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.ОтображатьДерево; 
		Показать=ВосстановитьЗначение(СтрЗаменить(ЭтаФорма.СправочникСписок,".","_")+"_СкрытьДеревоГрупп");
		Показать=?(Показать=Неопределено,Ложь,Показать);
		Кнопка.Пометка=Показать;
		спСписокДействияФормыОтображатьДерево(ЭтаФорма, Кнопка);
	Исключение КонецПопытки;
		
	Попытка //Восстановим состояние иерархического просмотра
		ИмяПараметра=СокрЛП(ЭтаФорма.СправочникСписок)+".ИерархическийПросмотр";
		СостояниеИерархии=ВосстановитьЗначение(ИмяПараметра);
		Если СостояниеИерархии<>Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.Список.ИерархическийПросмотр=СостояниеИерархии;
		КонецЕсли; 
	Исключение КонецПопытки;
		
	Попытка
		ИмяПараметра=СокрЛП(ЭтаФорма.СправочникСписок)+".Порядок";
		ПорядокСписка=ВосстановитьЗначение(ИмяПараметра);
		Если ПорядокСписка<>Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.Список.Значение.Порядок.Очистить();
			ЭтаФорма.ЭлементыФормы.Список.Значение.Порядок.Установить(ПорядокСписка);
		КонецЕсли; 
	Исключение КонецПопытки;
		
	Попытка // Подключим обработку изменения отборов форм списков справочников (в попытке ввиду ПВХ)
		ЭтаФорма.ПодключитьОбработчикИзмененияДанных("СправочникСписок.Отбор","СписокПриИзмененииОтбора",Истина);
	Исключение КонецПопытки;
		
	// Установим отбор
	Если ЭтаФорма.РежимВыбора и (СтруктураОтбора <> Неопределено) Тогда
		РежимФильтрации = обПраво("ФильтрацияСправочниковПриВыборе");
		
		//Для выбора из документов подразделение компании исправим на подразделение
		Подразделение=Неопределено;
		Если СтруктураОтбора.Свойство("ПодразделениеКомпании",Подразделение) Тогда
			СтруктураОтбора.Удалить("ПодразделениеКомпании");
			Если НЕ СтруктураОтбора.Свойство("Подразделение") Тогда
				СтруктураОтбора.Вставить("Подразделение",Подразделение);
			КонецЕсли; 
		КонецЕсли; 
		
		Для каждого ПараметрОтбора Из СтруктураОтбора Цикл
			//Проверим необходимость фильтрации по организации и подразделению
			Если ПараметрОтбора.Ключ="Организация" Тогда
				Если НЕ ((РежимФильтрации = Перечисления.РежимФильтрации.ФильтроватьПоОрганизации) ИЛИ (РежимФильтрации = Перечисления.РежимФильтрации.ФильтроватьПоОрганизацииИПодразделению)) Тогда
					Продолжить;
				КонецЕсли; 
			ИначеЕсли ПараметрОтбора.Ключ="Подразделение" Тогда
				Если НЕ ((РежимФильтрации = Перечисления.РежимФильтрации.ФильтроватьПоПодразделению) ИЛИ (РежимФильтрации = Перечисления.РежимФильтрации.ФильтроватьПоОрганизацииИПодразделению)) Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли;
			
			//Проверим наличие такого отбора в справочнике
			Если ЭтаФорма.СправочникСписок.Отбор.Найти(ПараметрОтбора.Ключ)=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПараметрОтбора.Ключ = "ДатаНачала" Тогда
				ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].Значение = ПараметрОтбора.Значение;
				ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].ВидСравнения = ВидСравнения.МеньшеИлиРавно;
				ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].Использование = Истина;
				ЭтаФорма.СправочникСписок.Отбор.ДатаКонца.Значение = ПараметрОтбора.Значение;
				ЭтаФорма.СправочникСписок.Отбор.ДатаКонца.ВидСравнения = ВидСравнения.БольшеИлиРавно;
				ЭтаФорма.СправочникСписок.Отбор.ДатаКонца.Использование = Ложь;
				Продолжить;
			КонецЕсли;

			Если ТипЗнч(ПараметрОтбора.Значение)=Тип("СписокЗначений") Тогда
				ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].ВидСравнения = ВидСравнения.ВСписке;
			Иначе
				ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].ВидСравнения = ВидСравнения.Равно;
			КонецЕсли; 
			ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].Значение = ПараметрОтбора.Значение;
			ЭтаФорма.СправочникСписок.Отбор[ПараметрОтбора.Ключ].Использование = Истина;
		КонецЦикла;
		
		// Сбрасываем отборы
		СтруктураОтбора = Неопределено;
	КонецЕсли;
		
	Попытка  // Установим фокус ввода на список
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.ЭлементыФормы.Список;
	Исключение КонецПопытки;
	
	//Попытка
	//	ТекСтрока = ЭтаФорма.ЭлементыФормы.Список.ТекущаяСтрока;
	//Исключение
	//	ТекСтрока = Неопределено;
	//КонецПопытки;
	//
	//удОбновитьКартинкуЗаметок(ТекСтрока, ЭтаФорма);
	
	Возврат Результат;
	
КонецФункции // спСписокПриОткрытии()

// Общий обработчик события ПриПовторномОткрытии формы списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриПовторномОткрытии(ЭтаФорма,СтандартнаяОбработка) Экспорт
	 // зарезервировано на будущее
	 Результат = Истина;
	 Возврат Результат;
КонецФункции // спСписокПриПовторномОткрытии()

// Общий обработчик события ОбновлениеОтображения формы списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокОбновлениеОтображения(ЭтаФорма) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // спСписокОбновлениеОтображения()

// Общий обработчик события ПередЗакрытием формы списка элементов 
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Отказ                - Булево - Признак отказа от закрытия формы. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной)
//									обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Результат = Истина;
	
	//Попытка // Проверим сами закрываемся или нас закрывают "снаружи"
	//	ВнешняяКомпонентаПодключена=Обработки.Защита.Создать().Компонента.УправлениеАктивно();
	//Исключение 
	//	ВнешняяКомпонентаПодключена=Ложь;
	//КонецПопытки;
	//Если НЕ ВнешняяКомпонентаПодключена Тогда
	//	СтандартнаяОбработка=Ложь; // Аварийное закрытие - нельзя что-либо спрашивать!
	//КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спСписокПередЗакрытием()

// Общий обработчик события ПриЗакрытии формы списка элементов 
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокПриЗакрытии(ЭтаФорма) Экспорт
	
	Результат = Истина;
	
	Попытка //Запомним состояние иерархического просмотра
		СостояниеИерархии=ЭтаФорма.ЭлементыФормы.Список.ИерархическийПросмотр;
		ИмяПараметра=СокрЛП(ЭтаФорма.СправочникСписок)+".ИерархическийПросмотр";
		СохранитьЗначение(ИмяПараметра,СостояниеИерархии);
	Исключение КонецПопытки; 
		
	//сохраним состояние связи с владельцем
	Попытка	// а вдруг...кнопки нет
		//сохраняем только когда, когда не режим выбора
		Если НЕ ЭтаФорма.РежимВыбора Тогда 
			Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь;
				
			СохранитьЗначение("СвязьПоВладельцу" + Вычислить("ЭтаФорма.СправочникСписок"), Кнопка.Пометка);
				
			// Для случая выставленной пометки (оповещения работают) иммитируем отпускание кнопки, чтобы
			// глобальный обработчик переустановил переменную глОповещениеАктивации посредством опроса оставшихся форм
			Если Кнопка.Пометка Тогда
				ЭтаФорма.ДействияФормыСвязь(Кнопка);
			КонецЕсли; 
		КонецЕсли;
	Исключение КонецПопытки;
		
	Попытка
		ПорядокСписка="";
		Для каждого ЭлементПорядка Из ЭтаФорма.ЭлементыФормы.Список.Значение.Порядок Цикл
			ПорядокСписка=ПорядокСписка+ЭлементПорядка.ПутьКДанным;
			Если ЭлементПорядка.Направление=НаправлениеСортировки.Возр Тогда
				ПорядокСписка=ПорядокСписка+" Возр";
			ИначеЕсли ЭлементПорядка.Направление=НаправлениеСортировки.Убыв Тогда
				ПорядокСписка=ПорядокСписка+" Убыв";
			КонецЕсли; 
			ПорядокСписка=ПорядокСписка+",";
		КонецЦикла; 
		ПорядокСписка=Лев(ПорядокСписка,СтрДлина(ПорядокСписка)-1);
			
		ИмяПараметра=СокрЛП(ЭтаФорма.СправочникСписок)+".Порядок";
		СохранитьЗначение(ИмяПараметра,ПорядокСписка);
	Исключение КонецПопытки;
	
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().УдалитьИзДереваФорм(ЭтаФорма);	
	
	Возврат Результат;
	
КонецФункции // спСписокПриЗакрытии()

// Общий обработчик события ОбработкаЗаписиНовогоОбъекта формы списка элементов 
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//  Объект   - Ссылка на объект, либо ТекущаяСтрока - Добавленный в подчиненной форме объект. 
//  Источник - Форма - Форма - источник события. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спОбработкаЗаписиНовогоОбъекта(ЭтаФорма, Объект, Источник) Экспорт
	
	Результат = Истина;
	
	Попытка
		// Идентификатор может быть отличен от "Список",
		// позиционируемся на введенный объект через попытку
		ЭтаФорма.ЭлементыФормы.Список.ТекущаяСтрока = Объект.Ссылка;
	Исключение КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // спОбработкаЗаписиНовогоОбъекта()

// Общий обработчик события ОбработкаОповещения формы списка элементов 
//
// Параметры:
//  ЭтаФорма         - форма - переданная форма
//  ИмяСобытия       - Строка - Имя события. Может быть использовано для идентификации сообщений. 
//  Параметр         - Произвольный - Параметр, переданный в сообщении. 
//  Источник         - Источник события, переданный в сообщении. 
//  СвязьПоВладельцу - Булево.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник, СвязьПоВладельцу=Ложь) Экспорт
	
	Результат = Истина;
	
	// Отбросим собственные оповещения и оповещения без передачи параметра
	Если ЭтаФорма=Источник ИЛИ Параметр=Неопределено Тогда Возврат Результат; КонецЕсли;
		
	// Проверим оповещения смены владельцев формы
	Если ИмяСобытия = "АктивизированЭлемент" Тогда
		// Получим положение кнопки СвязьПоВладельцу - отрабатываем только если она выставлена
		Попытка // кнопки может не быть на форме
			Если НЕ ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь.Пометка Тогда Возврат Результат; КонецЕсли;
		Исключение КонецПопытки;
			
		//в попытке, а вдруг реквизит формы по другому называется
		Попытка
			ОтборВладелец = ЭтаФорма.СправочникСписок.Отбор.Владелец;
			ОтборВладелец.Значение = Параметр;
			ОтборВладелец.Использование = Истина;
			НастройкиОтбораВладелец = Вычислить("ЭтаФорма.ЭлементыФормы.Список").НастройкаОтбора.Владелец;
			НастройкиОтбораВладелец.Доступность = Ложь;
			ЭтаФорма.ПараметрОтборПоВладельцу = Параметр;
		Исключение КонецПопытки;
			
	ИначеЕсли ИмяСобытия = "ПроверкаОповещения" Тогда
		// Если мы получили это оповещение и у данной формы выставлена привязка (пометка),
		// мы должны установить глОповещениеАктивации = Истина, сделаем это через форму, аналогично нажатию кнопки..
		// такой рекурсивный вызов обработчиков формы применен для исключения передачи переменной модуля приложения
		// глОповещениеАктивации в параметрах обработчика.
		Попытка
			Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь;
			Если Кнопка.Пометка Тогда
				Кнопка.Пометка = Ложь;
				ЭтаФорма.ДействияФормыСвязь(Кнопка);
			КонецЕсли; 
		Исключение КонецПопытки;
	ИначеЕсли ИмяСобытия = "ОбновилисьЗаметки" Тогда
		Попытка
			ТекСтрока = ЭтаФорма.ЭлементыФормы.Список.ТекущаяСтрока;
		Исключение
			ТекСтрока = Неопределено;
		КонецПопытки;
		
		удОбновитьКартинкуЗаметок(ТекСтрока, ЭтаФорма);
	КонецЕсли;
		
	Попытка	// На случай отсутствия элемента диалога тКомментарий 
		Если (Не обЗначениеНеЗаполнено(ЭтаФорма.ПараметрОтборПоВладельцу)) И (обЗначениеНеЗаполнено(ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок)) Тогда
			ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок="Владелец: "+ЭтаФорма.Отбор.Владелец.Значение;
		КонецЕсли;
	Исключение КонецПопытки;		
	
	Возврат Результат;
	
КонецФункции // спСписокОбработкаОповещения()

// Общий обработчик события ВнешнееСобытие формы списка элементов 
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//  Источник - Строка - Источник внешнего события. 
//  Событие  - Строка - Наименование события. 
//  Данные   - Строка - Данные для события. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция спСписокВнешнееСобытие(ЭтаФорма, Источник, Событие, Данные) Экспорт
	
	Результат = ЭтаФорма.ВводДоступен();
	
	//защита от двойного срабатывания сканера штрихкодов
	Если Источник = "Сканер" И НЕ ЗначениеЗаполнено(Данные) Тогда 
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // спСписокВнешнееСобытие()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

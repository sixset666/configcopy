////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с контактной информацией для СофтФона

// Процедура формирует структуру номера телефона в полях
// а может получат либо как строку либо как структуру номера телефона СофтФона
Процедура кмсфпЗаполнитьЗаписьРегистраСтруктуройТелефона(ЗаписьРегистра, НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера, КоличествоХранимыхЦифрТелефона) Экспорт
	// структура формируется при возврате из функции ПреобразоватьНомерСУчетомПрефиксов
	Если ТипЗнч(НомерТелефона) = Тип("Структура") Тогда
		Если НЕ ПустаяСтрока(НомерТелефона.Внутренний) И ПустаяСтрока(НомерТелефона.НомерТелефона) Тогда
			ЗаписьРегистра.Поле4         = НомерТелефона.Внутренний;
			ЗаписьРегистра.Представление = НомерТелефона.Внутренний;
			ЗаписьРегистра.CRM_ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(НомерТелефона.Внутренний, КоличествоХранимыхЦифрТелефона);
		Иначе
			ЗаписьРегистра.Поле1         = НомерТелефона.КодСтраны;
			ЗаписьРегистра.Поле2         = НомерТелефона.КодГорода;
			ЗаписьРегистра.Поле3         = НомерТелефона.НомерТелефона;
			ЗаписьРегистра.Поле4         = НомерТелефона.Внутренний;
			ЗаписьРегистра.Представление = НомерТелефона.Представление;
			ЗаписьРегистра.CRM_ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(НомерТелефона.НомерТелефона, КоличествоХранимыхЦифрТелефона);
		КонецЕсли;
	Иначе
		Если кмсфпОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера) Тогда
			ЗаписьРегистра.Поле4         		= НомерТелефона;
			ЗаписьРегистра.Представление 		= НомерТелефона;
			ЗаписьРегистра.CRM_ПолеХраненияНомера 	= кмсфпПреобразоватьНомерДляСохранения(НомерТелефона, КоличествоХранимыхЦифрТелефона);
		Иначе
			ЗаписьРегистра.Поле3         		= НомерТелефона;
			ЗаписьРегистра.Представление 		= НомерТелефона;
			ЗаписьРегистра.CRM_ПолеХраненияНомера 	= кмсфпПреобразоватьНомерДляСохранения(НомерТелефона, КоличествоХранимыхЦифрТелефона);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Функция возвращает список значений в виде телефона и владельца данного телефона
//  используется когда нужно получить список номеров телефонов, например, по контрагентам
//  для визуализации их пользователю и выбора из списка нужного
//
// Параметры
//  ОбъектКонтактнойИнформации  – Любой тип объекта, который определен для Измерения 
// 								  объект регистра КонтактнаяИнформация – значение для фильтрации
//   СписокТелефонов            – СписокЗначение, список значений, в который добавляются телефонные номера
Процедура кмсфпПолучитьСписокТелефоновСофтФон(ОбъектКонтактнойИнформации, СписокТелефонов) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Объект,
	|	КонтактнаяИнформация.Тип,
	|	КонтактнаяИнформация.Представление,
	|	КонтактнаяИнформация.Поле1,
	|	КонтактнаяИнформация.Поле2,
	|	КонтактнаяИнформация.Поле3,
	|	КонтактнаяИнформация.Поле4,
	|	КонтактнаяИнформация.ЗначениеПоУмолчанию
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &ОбъектКонтактнойИнформации
	|	И КонтактнаяИнформация.Тип = &ТипКонтактнойИнформации";
	
	Запрос.УстановитьПараметр("ОбъектКонтактнойИнформации", ОбъектКонтактнойИнформации);
	Запрос.УстановитьПараметр("ТипКонтактнойИнформации",    Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	ТаблицаКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
	// Первый строкой выводим сам объект, а номер его заполняем значением по умолчанию
	Если (ТаблицаКонтактнойИнформации.Количество() > 0) Тогда
		
		НайденныеСтроки = ТаблицаКонтактнойИнформации.НайтиСтроки(Новый Структура("ЗначениеПоУмолчанию", Истина));
		Если НайденныеСтроки.Количество()>0 Тогда
			ТекущаяКонтактнаяИнформация = НайденныеСтроки[0];
		Иначе
			ТекущаяКонтактнаяИнформация = ТаблицаКонтактнойИнформации.Получить(0);
		КонецЕсли;
		
		СтруктураНомера = кмсфпСформироватьСтруктуруНомераИзПолей(ТекущаяКонтактнаяИнформация);
		СтруктураНомера.Вставить("Объект", ТекущаяКонтактнаяИнформация.Объект);
		СписокТелефонов.Добавить(СтруктураНомера, Строка(ТекущаяКонтактнаяИнформация.Объект));
	КонецЕсли;
	
	// Далее делаем подобие дерева с выделенными телефонными номерами для объекта
	Для каждого ТекущаяКонтактнаяИнформация Из ТаблицаКонтактнойИнформации Цикл
		СтруктураТелефона = кмсфпСформироватьСтруктуруНомераИзПолей(ТекущаяКонтактнаяИнформация);
		СтруктураТелефона.Вставить("Объект", ТекущаяКонтактнаяИнформация.Объект);
		Префикс = ?(ТекущаяКонтактнаяИнформация.ЗначениеПоУмолчанию, " • ", "   ");
		СписокТелефонов.Добавить(СтруктураТелефона, Префикс + " " + ТекущаяКонтактнаяИнформация.Представление);
	КонецЦикла;
КонецПроцедуры

//Процедура выполняет проверку и обновление регистра сведений 
//Контактная информация. Заполняется новое поле CRM_ПолеХраненияНомера
//которое служит аналогичным числовым полем либо Полю3 либо Полю4, с префиксом в начале
Процедура кмсфпЗаполнениеПоляХраненияНомераРегистраКИ(ЧислоХранимыхЦифрНомера, МаксимальнаяДлинаВнутреннегоНомера) Экспорт
	
	ОбновлениеЗавершено = Ложь; НомерВыборки = 1; ОбъемВыборки = 50000;
	// Определим количество
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|
	|ГДЕ
	|	КонтактнаяИнформация.Тип = &Тип И
	|	КонтактнаяИнформация.CRM_ПолеХраненияНомера = 0";
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда Возврат; КонецЕсли;
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	ОбщееКоличествоВыборок = Цел(Выборка.Количество / ОбъемВыборки) + 1;

	Пока НЕ ОбновлениеЗавершено Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ "+кмсфпФорматЧислоВСтроку(ОбъемВыборки)+"
		|	*
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|
		|ГДЕ
		|	КонтактнаяИнформация.Тип = &Тип И
		|	КонтактнаяИнформация.CRM_ПолеХраненияНомера = 0";
		
		Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
		Результат = Запрос.Выполнить();
		ТаблицаРезультат = Результат.Выгрузить();
		
		// Проверяем есть ли ошибочные строки в регистре. Если есть то их необходимо исправить
		ОбщееКоличество = ТаблицаРезультат.Количество();
		Если ОбщееКоличество = 0 Тогда
			ОбновлениеЗавершено = Истина;
		Иначе
			КоличествоВТранзакции = 10000; Ном = 0; НомерПоПорядку = 0;
			НачатьТранзакцию();
			Для Каждого ТекущаяСтрока Из ТаблицаРезультат Цикл
				НоваяЗапись = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСтрока);
				ТекущаяСтрока.Поле3         = кмсфпУбратьИзНомераТелефонаВсеПрефиксы(кмсфпУбратьИзНомераТелефонаВсеБуквы(ТекущаяСтрока.Поле3));
				ТекущаяСтрока.Поле4			= кмсфпУбратьИзНомераТелефонаВсеПрефиксы(кмсфпУбратьИзНомераТелефонаВсеБуквы(ТекущаяСтрока.Поле4));
				ТекущаяСтрока.Представление	= кмсфпУбратьИзНомераТелефонаВсеПрефиксы(кмсфпУбратьИзНомераТелефонаВсеБуквы(ТекущаяСтрока.Представление));
				Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Поле3) И НЕ ЗначениеЗаполнено(ТекущаяСтрока.Поле4) И ЗначениеЗаполнено(ТекущаяСтрока.Представление) Тогда
					ВремНомер = ТекущаяСтрока.Представление;
					Если СтрДлина(ВремНомер) <= МаксимальнаяДлинаВнутреннегоНомера Тогда
						НоваяЗапись.Поле4 = ВремНомер;
						НоваяЗапись.CRM_ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(НоваяЗапись.Поле4, ЧислоХранимыхЦифрНомера);
					Иначе
						НоваяЗапись.Поле3 = ВремНомер;
						НоваяЗапись.CRM_ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(НоваяЗапись.Поле3, ЧислоХранимыхЦифрНомера);
					КонецЕсли;
				ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.Поле3) Тогда
					НоваяЗапись.CRM_ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(ТекущаяСтрока.Поле3, ЧислоХранимыхЦифрНомера);
				ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.Поле4) Тогда
					НоваяЗапись.CRM_ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(ТекущаяСтрока.Поле4, ЧислоХранимыхЦифрНомера);
				Иначе
					НоваяЗапись.CRM_ПолеХраненияНомера = -1;
				КонецЕсли;
				Ном = Ном + 1;
				НоваяЗапись.Записать(Истина);
				// Если запись подошла к максимальной в транзакции, то выполним ее
				Если Ном = КоличествоВТранзакции Тогда
					// Фиксируем транзакцию по достижению нужного количества записей
					Попытка
						ЗафиксироватьТранзакцию();
						НачатьТранзакцию();
						Ном = 0;
					Исключение
						ОтменитьТранзакцию();
						Сообщить(ОписаниеОшибки());
						Прервать;
					КонецПопытки;
				КонецЕсли;
				
				// Информируем пользователя сколько осталось обработать
				НомерПоПорядку = НомерПоПорядку + 1;
				//Состояние("Обновление регистра сведений контактной информации (Общее количество
				//("+ОбщееКоличествоВыборок+"/"+НомерВыборки+"):Текущее количество ("+ОбщееКоличество+"/"+
				//НомерПоПорядку+"))");
			КонецЦикла;
			
			// фиксируем оставшуюся транзакцию
			Попытка
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				Сообщить(ОписаниеОшибки());
				Сообщить("Обновление регистра сведений контактной информации не выполнено, найдены ошибки !", 10);
				Возврат;
			КонецПопытки;

		КонецЕсли;
		НомерВыборки = НомерВыборки + 1;
		// Проверим зацикливание, если число выборок превышает максимальное, значит что то не так..
		//  надо выйти их цикла
		Если ОбщееКоличествоВыборок < НомерВыборки Тогда
			ОбновлениеЗавершено = Истина;
		КонецЕсли;
	КонецЦикла;
	Сообщить("Обновление регистра сведений контактная информация завершено успешно !");
	
КонецПроцедуры	

//Функция возвращает выборку владельцев из регистра КИ телефона
//
Функция кмсфпВладелецТелефона(Телефон,глКоличествоХранимыхЦифрТелефона) Экспорт
		
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Объект,
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
				   |ГДЕ
				   |	(КонтактнаяИнформация.CRM_ПолеХраненияНомера = &CRM_ПолеХраненияНомераТелефона
				   |	ИЛИ КонтактнаяИнформация.CRM_ПолеХраненияНомера = &CRM_ПолеХраненияНомераМобильного)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("CRM_ПолеХраненияНомераТелефона",кмсфпПреобразоватьНомерДляСохранения(Телефон, глКоличествоХранимыхЦифрТелефона));
	Запрос.УстановитьПараметр("CRM_ПолеХраненияНомераМобильного",кмсфпПреобразоватьНомерДляСохранения(Телефон, 10));
	
	Возврат Запрос.Выполнить();
	
КонецФункции	

//Функция возвращает первый найденный телефон заданного вида
//будет возращен основной телефон , иначе первый из списка
//
Функция кмсфпПолучитьТелефон(Объект,ВидыТелефонов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Объект,
	               |	КонтактнаяИнформация.Тип,
	               |	КонтактнаяИнформация.ЗначениеПоУмолчанию,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Представление
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |
	               |";
	
	Запрос.УстановитьПараметр("Объект",Объект);	

	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ЗаписьКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат ЗаписьКИ;
	КонецЕсли;	
	
	
	
	Пока Выборка.НайтиСледующий(Истина,"ЗначениеПоУмолчанию") Цикл	
		
		ЗаписьКИ.Объект = Выборка.Объект;
		ЗаписьКИ.Тип    = Выборка.Тип;
		ЗаписьКИ.Прочитать();
		
		Возврат ЗаписьКИ; 
		
	КонецЦикла;
	
	Если  Выборка.Количество() = 1 Тогда
		
		Пока Выборка.Следующий() Цикл
			ЗаписьКИ.Объект = Выборка.Объект;
			ЗаписьКИ.Тип    = Выборка.Тип;
			ЗаписьКИ.Прочитать();
			Возврат ЗаписьКИ;
		КонецЦикла;	
		
	КонецЕсли;
	
	Возврат ЗаписьКИ;
	
КонецФункции

//Функция возвращает список телефонов  переданного объекта 
//владельца контактной информации
//
Функция кмсфпПолучитьСписокТелефоновОбъекта(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Объект,
	               |	КонтактнаяИнформация.Тип,
	               |	КонтактнаяИнформация.ЗначениеПоУмолчанию,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Представление
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |";
	
	Запрос.УстановитьПараметр("Объект",Объект);	

	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	
	СписокТелефонов = Новый СписокЗначений;
	ТаблицаКонтактнойИнформации = Результат.Выгрузить();

	Если (ТаблицаКонтактнойИнформации.Количество() > 0) Тогда
		
		НайденныеСтроки = ТаблицаКонтактнойИнформации.НайтиСтроки(Новый Структура("ЗначениеПоУмолчанию", Истина));
		Если НайденныеСтроки.Количество()>0 Тогда
			ТекущаяКонтактнаяИнформация = НайденныеСтроки[0];
		Иначе
			ТекущаяКонтактнаяИнформация = ТаблицаКонтактнойИнформации.Получить(0);
		КонецЕсли;

		СтруктураНомера = кмсфпСформироватьСтруктуруНомераИзПолей(ТекущаяКонтактнаяИнформация);
		СтруктураНомера.Вставить("Объект", ТекущаяКонтактнаяИнформация.Объект);
		СписокТелефонов.Добавить(СтруктураНомера, СокрЛП(ТекущаяКонтактнаяИнформация.Представление));
		ТаблицаКонтактнойИнформации.Удалить(ТекущаяКонтактнаяИнформация);
	КонецЕсли;
	
	Для каждого ТекущаяКонтактнаяИнформация Из ТаблицаКонтактнойИнформации Цикл
		
		СтруктураТелефона = кмсфпСформироватьСтруктуруНомераИзПолей(ТекущаяКонтактнаяИнформация);
		СписокТелефонов.Добавить(СтруктураТелефона,СокрЛП(ТекущаяКонтактнаяИнформация.Представление));
		
	КонецЦикла;
		
	Возврат СписокТелефонов;
		
КонецФункции

// Процедура считывает контактную информацию из ИБ в набор записей регистра сведений.
//
// Параметры:
//  НаборЗаписей - набор записей регистра сведений
//  Ссылка - Объект, по которому необходимо заполнить КИ
//
Процедура кмсфпПрочитатьКонтактнуюИнформацию(НаборЗаписей, Ссылка) Экспорт

	Если ТипЗнч(НаборЗаписей) <> Тип("РегистрСведенийНаборЗаписей.КонтактнаяИнформация") Тогда
		НаборЗаписей = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	Иначе
		НаборЗаписей.Очистить();
	КонецЕсли; 
	
	НаборЗаписей.Отбор.Объект.Значение      = Ссылка;
	НаборЗаписей.Отбор.Объект.Использование = Истина;
	НаборЗаписей.Прочитать();
	
	ТаблицаНабора = НаборЗаписей.Выгрузить();
	ТаблицаНабора.Сортировать("Тип ВОЗР");
	НаборЗаписей.Загрузить(ТаблицаНабора);
	
КонецПроцедуры

// Процедура записывает контактную информацию в ИБ из набора записей регистра сведений.
//
// Параметры:
//  НаборЗаписей - набор записей регистра сведений
//  Ссылка - Объект, по которому необходимо записать КИ
//  Отказ - Булево
//
Процедура кмсфпЗаписатьКонтактнуюИнформацию(НаборЗаписей, Ссылка, Отказ) Экспорт

	ТаблицаНабора = НаборЗаписей.Выгрузить();
	
	Индекс = 0;
	Пока 1 = 1 Цикл
		
		Если Индекс > НаборЗаписей.Количество() - 1 Тогда
			Прервать;
		КонецЕсли; 
		
		Запись = НаборЗаписей[Индекс];
		
		Если НЕ ЗначениеЗаполнено(Запись.Представление) Тогда
			НаборЗаписей.Удалить(Запись);
			Продолжить;
		КонецЕсли;
		
		Запись.Объект = Ссылка;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	НаборЗаписей.Отбор.Объект.Значение      = Ссылка;
	НаборЗаписей.Отбор.Объект.Использование = Истина;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(ОписаниеОшибки()+Символы.ПС+"Элемент """ + СокрЛП(Ссылка) + """ не записан. Не записана контактная информация.");
		НаборЗаписей.Загрузить(ТаблицаНабора);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	кмсфпПрочитатьКонтактнуюИнформацию(НаборЗаписей, Ссылка)
	
КонецПроцедуры

// Процедура формирует строковое представление адреса.
Процедура кмсфпСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураТелефона) Экспорт

	// Если не заполнены поля НомерТелефона и Внутренний номер,
	//  то принудительно скидываем представление, потому что код страны и код города 
	//  это явно мало для создания номера.
	Если ПустаяСтрока(СтруктураТелефона.НомерТелефона) И ПустаяСтрока(СтруктураТелефона.Внутренний) Тогда
		СтруктураТелефона.КодСтраны = "";
		СтруктураТелефона.КодГорода = "";
		СтруктураТелефона.Представление = "";
		Возврат;
	КонецЕсли;

	// Подкорректируем номер телефона, если заполнен внутренний номер и не заполнен номер телефона,
	//  то это точно не добавочный номер, а следовательно удаляем код страны и код города
	Если ПустаяСтрока(СтруктураТелефона.НомерТелефона) И НЕ ПустаяСтрока(СтруктураТелефона.Внутренний) Тогда
		СтруктураТелефона.КодСтраны = "";
		СтруктураТелефона.КодГорода = "";
		СтруктураТелефона.Представление = "";
	КонецЕсли;
	
	СтруктураТелефона.Представление = СтруктураТелефона.КодСтраны;
	СтруктураТелефона.Представление = СтруктураТелефона.Представление + ?((Не ПустаяСтрока(СтруктураТелефона.КодГорода)),(кмсфпПроверкаПустойСтроки(СтруктураТелефона.Представление, Ложь)+"(" + СтруктураТелефона.КодГорода + ")"),"");
	СтруктураТелефона.Представление = СтруктураТелефона.Представление + ?((Не ПустаяСтрока(СтруктураТелефона.НомерТелефона)),(кмсфпПроверкаПустойСтроки(СтруктураТелефона.Представление, Ложь) + СтруктураТелефона.НомерТелефона),"");
	
	Если НЕ ПустаяСтрока(СтруктураТелефона.Представление) Тогда
		СтруктураТелефона.Представление = СтруктураТелефона.Представление + ?((Не ПустаяСтрока(СтруктураТелефона.Внутренний)),(кмсфпПроверкаПустойСтроки(СтруктураТелефона.Представление) + "доб. " + СтруктураТелефона.Внутренний),"");
	Иначе
		СтруктураТелефона.Представление = СтруктураТелефона.Внутренний;
	КонецЕсли;
	
КонецПроцедуры // СформироватьПредставление()

//Функция возвращает номер телефона в котором убраны все префиксы
Функция кмсфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона, СтрокаПрефиксов=" /*-+=_.\,!;%:?()") Экспорт
	Для НомерПрефикс = 1 По СтрДлина(СтрокаПрефиксов) Цикл
		ТекущийПрефикс = Сред(СтрокаПрефиксов, НомерПрефикс, 1);
		НомерТелефона = СтрЗаменить(НомерТелефона, ТекущийПрефикс, "");
	КонецЦикла;
	Возврат НомерТелефона;
КонецФункции

//Функция возвращает номер телефона в котором присутствуют только символы цифр
Функция кмсфпУбратьИзНомераТелефонаВсеБуквы(НомерТелефона) Экспорт
	ТолькоЦифрыНомера = "";
	Для а=1 По СтрДлина(НомерТелефона) Цикл
		Если СтрЧислоВхождений("1234567890",Сред(НомерТелефона,а,1)) > 0 Тогда
			ТолькоЦифрыНомера = ТолькоЦифрыНомера + Сред(НомерТелефона,а,1);
		КонецЕсли;
	КонецЦикла;
	Возврат ТолькоЦифрыНомера;
КонецФункции

//Функция возвращает номер телефона в формате который должен сохраняться для поиска
Функция кмсфпПреобразоватьНомерДляСохранения(Знач НомерТелефона, ЧислоСимволов) Экспорт
    //БИЗТЕХ КОВАЛЬ 01.07.2025 ++
	//Исправление ошибки конфигурации
	Если ЧислоСимволов = Неопределено Тогда ЧислоСимволов=10; КонецЕсли;
	//БИЗТЕХ КОВАЛЬ 01.07.2025 --
	Префикс = "1";
	НомерТелефона = кмсфпУбратьИзНомераТелефонаВсеБуквы(НомерТелефона);
	Возврат ?(НЕ ЗначениеЗаполнено(НомерТелефона), 0, Число(Префикс + Прав(СокрЛП(НомерТелефона), ЧислоСимволов)));
КонецФункции

// Функция преобразовывает переданный объект и поля телефонного номера в структуру
//  и возвращает ее.
Функция кмсфпПреобразоватьКонтактВСтруктуру(Объект, Поле1, Поле2, Поле3, Поле4) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Объект", 	Объект);
	Результат.Вставить("Поле1",		СокрЛП(Поле1));
	Результат.Вставить("Поле2", 	СокрЛП(Поле2));
	Результат.Вставить("Поле3", 	СокрЛП(Поле3));
	Результат.Вставить("Поле4", 	СокрЛП(Поле4));
	
	Возврат Результат;
	
КонецФункции

//Функция возвращает контактную информацию в которой присутсвует указанный номер телефона
Функция кмсфпНайтиТелефонВКонтактнойИнформации(ТелефонныйНомер, КоличествоХранимыхЦифрТелефона) Экспорт
	// Выполняем поиск
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|  	КонтактнаяИнформация.Объект,
	|  	КонтактнаяИнформация.Представление,
	|  	КонтактнаяИнформация.Поле1,
	|  	КонтактнаяИнформация.Поле2,
	|  	КонтактнаяИнформация.Поле3,
	|  	КонтактнаяИнформация.Поле4,
	|  	КонтактнаяИнформация.ЗначениеПоУмолчанию
	|ИЗ
	|  	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|  	КонтактнаяИнформация.Тип.Ссылка = &ТипСсылка И
	|  	КонтактнаяИнформация.CRM_ПолеХраненияНомера = &НомерТелефона";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТипСсылка", Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	// То это ситуация когда номер передан с добавочным
	Если ТипЗнч(ТелефонныйНомер) = Тип("Структура") Тогда
		Запрос.УстановитьПараметр("НомерТелефона", кмсфпПреобразоватьНомерДляСохранения(ТелефонныйНомер.НомерТелефона, КоличествоХранимыхЦифрТелефона));
	Иначе
		Запрос.УстановитьПараметр("НомерТелефона", кмсфпПреобразоватьНомерДляСохранения(ТелефонныйНомер, КоличествоХранимыхЦифрТелефона));
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить();
КонецФункции

// Формирует структуру телефонного звонка по записи регистра сведений контактная информация
//  если значение неопределено, то создает пустую структуру, сделано для удобства
//  создание структуры в одном месте
Функция кмсфпСформироватьСтруктуруНомераИзПолей(ТекущаяСтрока=Неопределено) Экспорт
	
	СтруктураТелефона = Новый Структура;
	Если ТекущаяСтрока=Неопределено Тогда
		СтруктураТелефона.Вставить("НомерТелефона", 	"");
		СтруктураТелефона.Вставить("Внутренний",    	"");
		СтруктураТелефона.Вставить("КодГорода",     	"");
		СтруктураТелефона.Вставить("КодСтраны",     	"");
		СтруктураТелефона.Вставить("Представление",		"");
	Иначе
		СтруктураТелефона.Вставить("КодСтраны",     ТекущаяСтрока.Поле1);
		СтруктураТелефона.Вставить("КодГорода",     ТекущаяСтрока.Поле2);
		СтруктураТелефона.Вставить("НомерТелефона", ТекущаяСтрока.Поле3);
		СтруктураТелефона.Вставить("Внутренний",    ТекущаяСтрока.Поле4);
		СтруктураТелефона.Вставить("Представление", "");
	КонецЕсли;
	кмсфпСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураТелефона);
	
	Возврат СтруктураТелефона;
КонецФункции	

// Функция разбирает переданный номер, в структуру телефона и возвращает ее
Функция кмсфпРазобратьНомерТелефонаВСтруктуру(Знач ПозвонитьПоНомеру, глМаксимальнаяДлинаВнутреннегоНомера) Экспорт
	СтруктураНомераТелефона = Неопределено;
	
	Если НЕ ПустаяСтрока(ПозвонитьПоНомеру) Тогда
		СтруктураНомераТелефона = кмсфпСформироватьСтруктуруНомераИзПолей();
		
		// попытаемся разложить телефонный номер, если получится
		//  на составляющие, если номер введен правильно
		// т.е. полностью 8(или 7, +7)903 7721122, то будем по нему звонить
		//  номер должен прийти в следующем формате +7(903)7721122 или 8(903)7721122
		Если кмсфпОпределитьВнутреннийЗвонокПоНомеру(ПозвонитьПоНомеру, глМаксимальнаяДлинаВнутреннегоНомера) Тогда
			СтруктураНомераТелефона.Внутренний = ПозвонитьПоНомеру;
		Иначе
			// удалим + если есть
			ПозвонитьПоНомеру = СтрЗаменить(ПозвонитьПоНомеру, "+", "");
			НомКодГородаНачало = Найти(ПозвонитьПоНомеру, "(");
			НомКодГородаКонец = Найти(ПозвонитьПоНомеру, ")");
				
			// если был просто введен номер, то передадим его и позвоним
			//  если был введен как полный разберем
			Если НомКодГородаНачало = 0 И НомКодГородаКонец = 0 Тогда 
				СтруктураНомераТелефона.НомерТелефона = ПозвонитьПоНомеру;
			Иначе
				СтруктураНомераТелефона.КодСтраны = СокрЛП(Лев(ПозвонитьПоНомеру, НомКодГородаНачало-1));
				СтруктураНомераТелефона.КодГорода = СокрЛП(Сред(ПозвонитьПоНомеру, НомКодГородаНачало+1, НомКодГородаКонец-НомКодГородаНачало-1));
				СтруктураНомераТелефона.НомерТелефона = Прав(ПозвонитьПоНомеру, СтрДлина(ПозвонитьПоНомеру)-НомКодГородаКонец); 
			КонецЕсли;
				
		КонецЕсли;
				
		кмсфпСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураНомераТелефона);
	КонецЕсли;
	
	Возврат СтруктураНомераТелефона;
КонецФункции

// По переданной структуре, определяет, кто это контакт, контрагент или контактное лицо
//  и в зависимости от этого возвращает структуру содержащую Контрагента, Контактное лицо и 
//  ответственного менеджера, если таковых получается определить.
// Так же содержит номер телефона, разложенный по полям.
Функция кмсфпВернутьПоНайденномуКонтактуИнформацию(СтруктураНайденногоКонтакта) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Контрагент", 			Неопределено);
	Результат.Вставить("КонтактноеЛицо", 		Неопределено);
	Результат.Вставить("СоединитьССотрудником",	Неопределено);
	Результат.Вставить("Поле1",					Неопределено);
	Результат.Вставить("Поле2",					Неопределено);
	Результат.Вставить("Поле3",					Неопределено);
	Результат.Вставить("Поле4",					Неопределено);
	
	// Если передали не структуру объекта и номера, то выходим с пустым результатом
	Если НЕ ТипЗнч(СтруктураНайденногоКонтакта) = Тип("Структура") Тогда Возврат Результат; КонецЕсли;
	
	Контакт = СтруктураНайденногоКонтакта.Объект;
	
	//разбор и поиск куратора и всего остального
	
	Если НЕ Контакт = Неопределено Тогда
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Сотрудники") 
			ИЛИ ТипЗнч(Контакт) = Тип("СправочникОбъект.Сотрудники") Тогда //Если телефон сотрудника то куратор = сотрудник
			Результат.СоединитьССотрудником = Контакт;
		ИначеЕсли (ТипЗнч(Контакт) = Тип("СправочникСсылка.Контрагенты") 
			Или ТипЗнч(Контакт) = Тип("СправочникОбъект.Контрагенты")) И Контакт.ВидКонтрагента=Перечисления.ВидыКонтрагентов.КонтактноеЛицо Тогда
			Результат.КонтактноеЛицо = Контакт;
			// поищем контрагента
			ТекстЗапроса=
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВзаимоотношенияКонтрагентов.Контрагент1 КАК Владелец
			|ИЗ
			|	РегистрСведений.ВзаимоотношенияКонтрагентов КАК ВзаимоотношенияКонтрагентов
			|ГДЕ
			|	ВзаимоотношенияКонтрагентов.Контрагент2 = &Контакт";
			Запрос=Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Контакт",Контакт.Ссылка);
			РезультатЗапроса=Запрос.Выполнить();
			Если НЕ РезультатЗапроса.Пустой() Тогда
				ТабРезульт=РезультатЗапроса.Выгрузить();
				Результат.Контрагент = ТабРезульт[0].Владелец;
			КонецЕсли;
			
			//Смотрим на регистр кураторы по КЛ и выбираем сначала куратора без договора, потом первого попавшегося
			ТекстЗапроса = "ВЫБРАТЬ
			|	Кураторы.Куратор,
			|	Кураторы.Договор
			|ИЗ
			|	РегистрСведений.Кураторы КАК Кураторы
			|ГДЕ
			|	Кураторы.Контрагент = &Контакт";
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапроса;
			Запрос.УстановитьПараметр("Контакт",Контакт.ссылка);
			ТЗКураторовКЛ = Запрос.Выполнить().Выгрузить();
			ТЗКураторовКЛ.Сортировать("Договор");
			Если ТЗКураторовКЛ.Количество()>0 Тогда
				Результат.СоединитьССотрудником = ТЗКураторовКЛ[0].Куратор;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Организации") 
			Или ТипЗнч(Контакт) = Тип("СправочникОбъект.Организации") Тогда //Если телефон организации тогда смотрим на его ответственных по порядку, как только находим первого соединяем с ним
			//Выберем всех ответственных по организации
			Куратор = Неопределено;
			//Чтение значения для руководителя
			СтруктураОтбора=Новый Структура("Организация,Объект",Контакт.Ссылка,Перечисления.ВидыОбъектовСведений.Руководитель);
			СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
			Руководитель=СтруктураСведений.Значение;
			Если ЗначениеЗаполнено(Руководитель) ТОгда
				Куратор = Руководитель
			КонецЕсли;
			Если Куратор=Неопределено ТОгда
				//Чтение значения для главного бухгалтера
				СтруктураОтбора=Новый Структура("Организация,Объект",Контакт.Ссылка,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
				СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
				ГлавныйБухгалтер=СтруктураСведений.Значение;
				Если ЗначениеЗаполнено(ГлавныйБухгалтер) Тогда
					Куратор = ГлавныйБухгалтер;
				КонецЕсли;
			КонецЕсли;
			Если Куратор=Неопределено ТОгда
				//Чтение значения для кассира
				СтруктураОтбора=Новый Структура("Организация,Объект",Контакт.Ссылка,Перечисления.ВидыОбъектовСведений.Кассир);
				СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
				Кассир=СтруктураСведений.Значение;
				Если ЗначениеЗаполнено(Кассир) Тогда			
					Куратор = Кассир;
				КонецЕсли;
			КонецЕсли;
			Если Куратор=Неопределено ТОгда
				//Чтение значения для МОЛ
				СтруктураОтбора=Новый Структура("Организация,Объект",Контакт.Ссылка,Перечисления.ВидыОбъектовСведений.МОЛ);
				СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
				МОЛ=СтруктураСведений.Значение;
				Если ЗначениеЗаполнено(МОЛ) Тогда			
					Куратор = МОЛ;
				КонецЕсли;
			КонецЕсли;
			Если НЕ Куратор = Неопределено Тогда
				Результат.СоединитьССотрудником = Куратор;	
			КонецЕсли;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.ПодразделенияКомпании") 
			Или ТипЗнч(Контакт) = Тип("СправочникОбъект.ПодразделенияКомпании") Тогда //если подразделение компании то смотрим на организацию и подставляем ответственного из организации
			Организация = Контакт.Организация;
			Куратор = Неопределено;
			//Чтение значения для руководителя
			СтруктураОтбора=Новый Структура("Организация,Объект",Организация.Ссылка,Перечисления.ВидыОбъектовСведений.Руководитель);
			СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
			Руководитель=СтруктураСведений.Значение;
			Если ЗначениеЗаполнено(Руководитель) ТОгда
				Куратор = Руководитель
			КонецЕсли;
			Если Куратор=Неопределено ТОгда
				//Чтение значения для главного бухгалтера
				СтруктураОтбора=Новый Структура("Организация,Объект",Организация.Ссылка,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
				СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
				ГлавныйБухгалтер=СтруктураСведений.Значение;
				Если ЗначениеЗаполнено(ГлавныйБухгалтер) Тогда
					Куратор = ГлавныйБухгалтер;
				КонецЕсли;
			КонецЕсли;
			Если Куратор=Неопределено ТОгда
				//Чтение значения для кассира
				СтруктураОтбора=Новый Структура("Организация,Объект",Организация.Ссылка,Перечисления.ВидыОбъектовСведений.Кассир);
				СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
				Кассир=СтруктураСведений.Значение;
				Если ЗначениеЗаполнено(Кассир) Тогда			
					Куратор = Кассир;
				КонецЕсли;
			КонецЕсли;
			Если Куратор=Неопределено ТОгда
				//Чтение значения для МОЛ
				СтруктураОтбора=Новый Структура("Организация,Объект",Организация.Ссылка,Перечисления.ВидыОбъектовСведений.МОЛ);
				СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ТекущаяДата(),СтруктураОтбора);
				МОЛ=СтруктураСведений.Значение;
				Если ЗначениеЗаполнено(МОЛ) Тогда			
					Куратор = МОЛ;
				КонецЕсли;
			КонецЕсли;
			Если НЕ Куратор = Неопределено Тогда
				Результат.СоединитьССотрудником = Куратор;	
			КонецЕсли;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Контрагенты") 
			Или ТипЗнч(Контакт) = Тип("СправочникОбъект.Контрагенты") Тогда //Если контрагент тогда смотрим на регистр кураторы и выбираем сначала без договора а потом без разницы
			Результат.Контрагент = Контакт;
			ТекстЗапроса = "ВЫБРАТЬ
			|	Кураторы.Куратор,
			|	Кураторы.Договор
			|ИЗ
			|	РегистрСведений.Кураторы КАК Кураторы
			|ГДЕ
			|	Кураторы.Контрагент = &Контрагент";
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент",Контакт.ссылка);
			ТЗКураторовКЛ = Запрос.Выполнить().Выгрузить();
			ТЗКураторовКЛ.Сортировать("Договор");
			Если ТЗКураторовКЛ.Количество()>0 Тогда
				Результат.СоединитьССотрудником = ТЗКураторовКЛ[0].Куратор;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Результат.Поле1 = СтруктураНайденногоКонтакта.Поле1;
	Результат.Поле2 = СтруктураНайденногоКонтакта.Поле2;
	Результат.Поле3 = СтруктураНайденногоКонтакта.Поле3;
	Результат.Поле4 = СтруктураНайденногоКонтакта.Поле4;
	
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////
//Вспомогательные процедуры и функции

//Функция возвращает список документов по методанным
Функция кмсфпСписокДокументов() Экспорт
	СписокВозврата = Новый СписокЗначений;
	Для каждого ТекДокум из Метаданные.Документы Цикл
		СписокВозврата.Добавить(ТекДокум.Имя,ТекДокум.Синоним)
	КонецЦикла;
	Возврат СписокВозврата;
КонецФункции

#Если Клиент Тогда
	
//Функция восстанавливает значения
функция кмсфпВостановитьЗначение(ИмяЗначения,ЗначениеПоУмолчанию,Права = Неопределено) Экспорт
	Если Права = Неопределено Тогда //если передаются права то берем из настроек и прав из регистра
		ВостЗначение = ВосстановитьЗначение(ИмяЗначения);
		Если ВостЗначение = Неопределено ТОгда
			Возврат ЗначениеПоУмолчанию
		Иначе
			Возврат ВостЗначение
		КонецЕсли;
	Иначе //берем из сохраненного значения если оно было
		НастройкаПраво = обПраво(ИмяЗначения,Права);
		Возврат НастройкаПраво;
	КонецЕсли;
КонецФункции

// Процедура производит запись права/настройки
// Параметры:
//	ОбъектНастройкиПрав - Пользователь или объект на который распространяется право/настройка
//	ПравоНастройка		- Право/настройка
//	Значение			- Значение права/настройки
//	права				- Набор прав пользователя
//
Процедура кмсфпЗаписатьПравоНастройку(ОбъектНастройкиПрав,ПравоНастройка,Значение,права=Неопределено) ЭКспорт
	Если НЕ права = Неопределено Тогда
		Рег=РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
		Если ТипЗнч (ОбъектНастройкиПрав) = Тип("СправочникСсылка.Пользователи") Тогда
			Рег.Объект=?(обЗначениеНеЗаполнено(ОбъектНастройкиПрав.ШаблонПрав), ОбъектНастройкиПрав, ОбъектНастройкиПрав.ШаблонПрав);
		Иначе
			Рег.Объект=ОбъектНастройкиПрав;
		КонецЕсли;
		Рег.ПравоНастройка=ПланыВидовХарактеристик.ПраваИНастройки[ПравоНастройка];
		Рег.Значение=Значение;
		Рег.Записать();
	Иначе
		СохранитьЗначение(ПравоНастройка,Значение)
	КонецЕсли;
КонецПроцедуры

#КонецЕсли

//Проверка на пустоту строки
Функция кмсфпПроверкаПустойСтроки(ВыбСтрока, ПризнакЗапятой=Истина) Экспорт

	Если ПустаяСтрока(ВыбСтрока) Тогда
		Возврат "";
	Иначе
		Возврат ?(ПризнакЗапятой,",","")+" ";
	КонецЕсли; 
	
КонецФункции // кмсфпПроверкаПустойСтроки()

//переводим число в строку
Функция кмсфпФорматЧислоВСтроку(Знач ЧислоДоФормата) Экспорт
	Возврат (СтрЗаменить(Формат(ЧислоДоФормата, "ЧРГ=' '"), " ", ""));
КонецФункции

//Функция проверяет есть ли хоть одна КИ с типом телефон, если есть то возвращает Ложь иначе Истина
Функция кмсфпДоступностьРедактированияКоличестваХранимыхЦифрТелефона() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Объект
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип = Тип";
	
	
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
КонецФункции

////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с софт фоном

// Заполним КЭШ таблицу содержащую телефоны
//  Для ФизЛиц и Пользователей. Т.е. для сотрудников организации
//  далее эта таблица будет передана в форму Списка телефонов
Функция кмсфпЗаполнитьКЭШТаблицуТелефонныхНомеров() Экспорт
	
	//Состояние("Заполнение таблицы телефонных номеров сотрудников");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Объект КАК Объект,
	|	КонтактнаяИнформация.Поле1 КАК Поле1,
	|	КонтактнаяИнформация.Поле2 КАК Поле2,
	|	КонтактнаяИнформация.Поле3 КАК Поле3,
	|	КонтактнаяИнформация.Поле4 КАК Поле4,
	|	КонтактнаяИнформация.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип = &Тип
	|	И ВЫРАЗИТЬ(КонтактнаяИнформация.Объект КАК Справочник.ФизическиеЛица) В
	|(
	|ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактнаяИнформация.Объект КАК Объект,
	|	КонтактнаяИнформация.Поле1 КАК Поле1,
	|	КонтактнаяИнформация.Поле2 КАК Поле2,
	|	КонтактнаяИнформация.Поле3 КАК Поле3,
	|	КонтактнаяИнформация.Поле4 КАК Поле4,
	|	КонтактнаяИнформация.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип = &Тип
	|	И ВЫРАЗИТЬ(КонтактнаяИнформация.Объект КАК Справочник.Пользователи) В
	|(
	|ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|)
	|";
		
	// Список всех доступных типов
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);

	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

// Процедура обновляет данные в КЭШ таблице, в зависимости от изменений пользователя
Процедура кмсфпОбновитьКЭШТаблицуТелефонныхНомеров(ТаблицаКЭШТелефонныхНомеров, Ссылка, Отказ) Экспорт
	// Если отказ, то ничего вообще не делаем
	Если Отказ Тогда Возврат; КонецЕсли;
	
	// Если таблица неопределена, то загрузим ее заново
	Если ТаблицаКЭШТелефонныхНомеров = Неопределено Тогда
		ТаблицаКЭШТелефонныхНомеров = кмсфпЗаполнитьКЭШТаблицуТелефонныхНомеров();
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = ТаблицаКЭШТелефонныхНомеров.НайтиСтроки(Новый Структура("Объект", Ссылка));
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда
		// Удаляем текущие записи, затем заполним их новыми значениями
		Для Каждого ТекущаяСтрокаДляУдаления ИЗ НайденныеСтроки Цикл
			ТаблицаКЭШТелефонныхНомеров.Удалить(ТекущаяСтрокаДляУдаления);
		КонецЦикла;
	КонецЕсли;
	// "зальем" новые строки
	НаборКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	НаборКИ.Отбор.Объект.Установить(Ссылка);
	НаборКИ.Отбор.Тип.Установить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	НаборКИ.Прочитать();
		
	Для Каждого ТекущаяЗаписьКИ Из НаборКИ Цикл
		НоваяСтрока = ТаблицаКЭШТелефонныхНомеров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяЗаписьКИ, "Объект, Поле1, Поле2, Поле3, Поле4, Представление");
	КонецЦикла;
	
	Отказ = Ложь;
КонецПроцедуры

// Функция определяет внутренний или внешний звонок исходя
//  из длины переданного номера телефона, и максимальной длины внутреннего номера 
//  телефона установленного в системе. Если длина номера > 0 И <= макс, то это внутренний
Функция кмсфпОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера) Экспорт
	ДлиннаТелефона = СтрДлина(СокрЛП(НомерТелефона));
	Если ДлиннаТелефона > 0 И ДлиннаТелефона <= МаксимальнаяДлинаВнутреннегоНомера Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

// Триггер кнопки HOLD / UNHOLD
// В зависимости от текущего вида кнопки меняем на противоположный
Процедура кмсфпТриггерКнопкиHold(Кнопка, Заголовок) Экспорт
	Если Заголовок = "Hold" Тогда
		Кнопка.Заголовок = "UnHold";
	ИначеЕсли Заголовок = "UnHold" Тогда
		Кнопка.Заголовок = "Hold";
	КонецЕсли;
КонецПроцедуры

// Функция проверяет изменилось ли состояние звонка, и возвращает результат изменения
Функция кмсфпСостояниеЗвонкаИзменилось(ВидыСобытийСтанции, СостояниеПосле, СостояниеДо) Экспорт

	Результат = Ложь;
	Если (СостояниеДо = ВидыСобытийСтанции.CONNECTED 
	 Или СостояниеДо = ВидыСобытийСтанции.ACCEPTED
	 Или СостояниеДо = ВидыСобытийСтанции.OFFERING
	 Или СостояниеДо = ВидыСобытийСтанции.ONHOLD
	 Или СостояниеДо = ВидыСобытийСтанции.ONHOLDPENDTRANSFER
	 Или СостояниеДо = ВидыСобытийСтанции.ONHOLDPENDCONF
	 Или СостояниеДо = ВидыСобытийСтанции.RINGBACK
	 Или СостояниеДо = ВидыСобытийСтанции.CONFERENCED)
	 И НЕ СостояниеДо = СостояниеПосле Тогда
	 
	 	Результат = Истина;
		
	КонецЕсли;
	 
	Возврат Результат;
КонецФункции

// Функция определяет можно ли использовать метод переданный в Метод, при переданном 
//  состоянии звонка, переменная Статус. Т.к. использование каждого метода возможно
//  при определенный состояниях.
// 
// Параметры
//  <Метод>  – <Строка> – имя метода станции
//  <Статус>  – <Структура ВидыСобытийСтанции> – текущее состояние звонка
// Возвращаемое значение:
//   <Булево>   – разрешено/запрещено использование метода
Функция кмсфпИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, Метод, Статус, ДопПараметр="") Экспорт
	Результат = Ложь;
	
	// Ответить на вызов
	Если Метод = "AnswerCall" Тогда
	
 		Если    Статус = ВидыСобытийСтанции.OFFERING
	    	Или Статус = ВидыСобытийСтанции.ACCEPTED Тогда 
			Результат = Истина; 
		КонецЕсли;
 
	// HOLD
	ИначеЕсли Метод = "HoldCall" Тогда
		Если Статус = ВидыСобытийСтанции.CONNECTED Тогда Результат = Истина; КонецЕсли;
		
	// UnHold
	ИначеЕсли Метод = "UnHoldCall" Тогда
		Если Статус = ВидыСобытийСтанции.ONHOLD Или
			 Статус = ВидыСобытийСтанции.ONHOLDPENDTRANSFER Или
			 Статус = ВидыСобытийСтанции.ONHOLDPENDCONF Тогда 
			 
			Результат = Истина; 
		КонецЕсли;
		
	// Уничтожить звонок, отмена
	ИначеЕсли Метод = "DropCall" Тогда
		Если НЕ Статус = ВидыСобытийСтанции.IDLE Тогда Результат = Истина; КонецЕсли;
		
	// Безусловный перевод звонка
	ИначеЕсли Метод = "BlindTransferCall" Тогда
		Если Статус = ВидыСобытийСтанции.CONNECTED Тогда Результат = Истина; КонецЕсли;
		
	// Условный перевод звонка
	ИначеЕсли Метод = "SetupTransferCall" Тогда
		Если Статус = ВидыСобытийСтанции.CONNECTED Тогда Результат = Истина; КонецЕсли;
		
	// Позвонить при гудке на линии
	ИначеЕсли Метод = "DialCall" Тогда
		
	// Подтвердить перевод звонка
	//  в ДопПараметр необходимо передать идентификатор консультационного звонка
	ИначеЕсли Метод = "CompleteTransferCall" Тогда
		Если (Статус = ВидыСобытийСтанции.CONNECTED Или
			 Статус = ВидыСобытийСтанции.RINGBACK Или
			 //Статус = ВидыСобытийСтанции.BUSY Или
			 Статус = ВидыСобытийСтанции.PROCEEDING) И 
			 НЕ ПустаяСтрока(ДопПараметр) Тогда
			 
			 Результат = Истина; 
		 КонецЕсли;
		 
	КонецЕсли;
	
	Возврат Результат;
КонецФункции 

#Если Клиент Тогда
	
// Функция проверяет разрешено ли текущему пользователю работать с СофтФоном
// Параметры
//  ВыдаватьПредупреждение - Булево, Определяет выдавать сообщение пользователю, что работа с 
//                           СофтФоном не разрешена.
Функция кмсфпРаботаССофтФономРазрешена(ВыдаватьПредупреждение=Ложь, ИспользоватьСофтФонТекущийПользователь) Экспорт
	
	Если ВыдаватьПредупреждение И Не ИспользоватьСофтФонТекущийПользователь Тогда
		Предупреждение("Внимание!!! Работа СофтФона для текущего пользователя"""+""+""" не разрешена."+Символы.ПС+
					   "Необходимо открыть настройки пользователя и установить флажок ""СофтФон управления звонками -> Использовать СофтФон""",
					   30, "Внимание!!!");
	КонецЕсли;
		
	Возврат ИспользоватьСофтФонТекущийПользователь;
КонецФункции

// Процедура создает событие о звонке и набирает номер
Процедура кмсфпСоздатьСобытиеИПозвонить(Данные) Экспорт
	// Создаем документ событие
	НовыйДокумент = Документы.Событие.СоздатьДокумент();
	НовыйДокумент.УстановитьВремя();
	НовыйДокумент.УстановитьНовыйНомер();
	НовыйДокумент.Автор					= сфпСофтФонПроСервер.сфпТекущийПользователь();
	НовыйДокумент.ВидСобытия			= Перечисления.ВидыСобытий.ТелефонныйЗвонок;
	НовыйДокумент.ТипСообщения			= Перечисления.ТипыСообщений.Исходящее;
	НовыйДокумент.Состояние				= Перечисления.СостоянияСобытий.Завершено;
	НовыйДокумент.ДатаНачала			= ТекущаяДата();
	НовыйДокумент.Тема					= "НЕ ОБРАБОТАНО!!!";
	НовыйДокумент.Контрагент			= Данные.Объект;
	НовыйДокумент.КонтактнаяИнформация	= Данные.Представление;
	// Заполняем контактных лиц
	МассивКонтактныхЛиц = сфпСофтФонПроСервер.сфпПолучитьСписокКонтактныхЛиц(Данные.Объект);
	ДлинаМассива = МассивКонтактныхЛиц.Количество();
	Если ДлинаМассива > 0 Тогда
		НовыйДокумент.КонтактноеЛицо = МассивКонтактныхЛиц[0];
		Для НомерСтрокиМассива = 0 По ДлинаМассива - 1 Цикл
			НоваяСтрока = НовыйДокумент.СторонниеЛица.Добавить();
			НоваяСтрока.ВладелецКИ		= МассивКонтактныхЛиц[НомерСтрокиМассива];
			НоваяСтрока.КонтактноеЛицо	= МассивКонтактныхЛиц[НомерСтрокиМассива];
		КонецЦикла;	
	КонецЕсли;	
	НовыйДокумент.ПолучитьФорму().Открыть();
	кмсфпПозвонитьПоНомеру(Данные);
КонецПроцедуры

// Процедура набора номера
Процедура кмсфпПозвонитьПоНомеру(Данные) Экспорт
	сфпСофтФонПроКлиент.сфпПозвонить(Данные.Представление, Данные.Объект);	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Работа с SMS

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для подсчета длины SMS

// Функция определяет возможно ли использовать семи битное сообщение
Функция кмИспользовать7БитСообщение(Сообщение)
	Результат = Истина;	
	Для к = 1 по СтрДлина(Сообщение) Цикл
		ТекСимвол = Сред(Сообщение,к,1);
  		Если (КодСимвола(ТекСимвол)>127)или(Найти("~`@$^&_{}[]",ТекСимвол)<>0) Тогда
  			Результат = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

// Процедура выводит длину сообщения в символах и рассчитывает число частей 
//на которое будет разбито сообщения при отправке 
//
Функция кмДлинаSMS(Сообщение) Экспорт
	Перем MAX_LENGTH_SMS_UCS2; //70;
	Перем MAX_LENGTH_SMS_UCS2_WITHOUT_UDH;
	Перем MAX_LENGTH_SMS_7bit; //160
	Перем MAX_LENGTH_SMS_7bit_WITHOUT_UDH;
	
	//{Максимальная длинна T-UD (User Data) в октетах}
	MAX_USER_DATA_LENGTH  =  140;
	//{Длинна заголовка пользовательских данных в октетах}
	LENGTH_USER_DATA_HEADER = 6;
	//{Максимальная длинна данных в UCS2 (16 бит представляются 2 откатами)}
	MAX_LENGTH_SMS_UCS2 = Цел(MAX_USER_DATA_LENGTH/2); //70;
	//{Максимальная длинна данных в UCS2 минус заголовок пользовательских данных}
	MAX_LENGTH_SMS_UCS2_WITHOUT_UDH =  Цел((MAX_USER_DATA_LENGTH  - LENGTH_USER_DATA_HEADER - 1)/2);
	//{Максимальная длинна данных в7 bit}
	MAX_LENGTH_SMS_7bit = MAX_USER_DATA_LENGTH + Цел(MAX_USER_DATA_LENGTH/7); //160
	//{Максимальная длинна данных в  8 bit минус заголовок пользовательских данных}
	MAX_LENGTH_SMS_7bit_WITHOUT_UDH = MAX_LENGTH_SMS_7bit - (LENGTH_USER_DATA_HEADER + 1);
	
	НадписьДлинаСообщения = "Симв.: "+Строка(СтрДлина(Сообщение));
	
	Alpha = кмИспользовать7БитСообщение(Сообщение);
	MsgLen = СтрДлина(Сообщение);
	Если Alpha Тогда
		//Определяем из скольких частей будет состоять SMS
		Если MsgLen > MAX_LENGTH_SMS_7bit Тогда 
			MaxLen = MAX_LENGTH_SMS_7bit_WITHOUT_UDH;
			Num = Цел(MsgLen/MaxLen);
			Если (MsgLen/MaxLen - Цел(MsgLen/MaxLen) > 0) Тогда 
				Num=Num+1 
			КонецЕсли;
		Иначе
			MaxLen = MAX_LENGTH_SMS_7bit;
			Num = 1;			
		КонецЕсли;
	Иначе
		//Определяем из скольких частей будет состоять SMS
		Если MsgLen > MAX_LENGTH_SMS_UCS2 Тогда
			MaxLen = MAX_LENGTH_SMS_UCS2_WITHOUT_UDH;
			Num = Цел(MsgLen/MaxLen);
			Если (MsgLen/MaxLen - Цел(MsgLen/MaxLen) > 0) Тогда
				Num=Num+1;
			КонецЕсли;
		Иначе
			MaxLen = MAX_LENGTH_SMS_UCS2;
			Num = 1;
		КонецЕсли;
	КонецЕсли;
	Если Num>1 Тогда
		НадписьДлинаСообщения = НадписьДлинаСообщения+" частей: "+Строка(Num);
	КонецЕсли;
	Возврат НадписьДлинаСообщения;
КонецФункции


///////////////////////////////////////////////////////////////////////////////////////
 // Процедуры и функции по обработке телефонов
 
 //Функция удаляет из переданной строки все символы кроме цифр 
Функция кмОчиститьТелефонОтРазделителей(Телефон) Экспорт
		
	НовыйТелефон = "";
	а = 1;
	Пока а <= СтрДлина(Телефон) Цикл
		Симв =  Сред(Телефон, а, 1);
		Если (КодСимвола(Симв) >= 48 И КодСимвола(Симв) <= 57) Тогда
			НовыйТелефон = НовыйТелефон + Симв;
		КонецЕсли;
		а = а + 1;
	КонецЦикла;
	
	Возврат НовыйТелефон;
	
КонецФункции	

//Функция формирует номер получателя из полей строки ТЧ документа 
//КодСтраны, КодОператора, НомерТелефона
//
Функция кмСформироватьНомерПолучателя(КонтактнаяИнформация)Экспорт
	
	НомерПолучателя = КонтактнаяИнформация;
	НомерПолучателя = кмОчиститьТелефонОтРазделителей(НомерПолучателя);
	Если СтрДлина(НомерПолучателя)=11 Тогда
		ТелефонныйНомер = "+"+НомерПолучателя;
	иначе
		ТелефонныйНомер = НомерПолучателя;
	КонецЕсли;
	Возврат ТелефонныйНомер
КонецФункции

//Функция производит поиск по контактной информации
//Телефон - номер телефона который ищем
Функция кмВернутьВладельцаНомераТелефона(Телефон)
	НомерТелефона = кмОчиститьТелефонОтРазделителей(Телефон);
	ТекстЗапроса = "ВЫБРАТЬ
	|КонтактнаяИнформация.Объект
	|ИЗ
	|РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|КонтактнаяИнформация.Тип = &ТипКИ
	|И КонтактнаяИнформация.Вид = &ВидКИ
	|И КонтактнаяИнформация.Поле1 + КонтактнаяИнформация.Поле2 + КонтактнаяИнформация.Поле3 = &Телефон";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТипКИ",Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("ВидКИ",Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("Телефон","+"+НомерТелефона);
	//переделать на запрос.Выгрузить();
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	Если ТаблицаРезультатаЗапроса.Количество()>0 Тогда
		Для каждого ТекСтрока из ТаблицаРезультатаЗапроса Цикл
			ВладелецТелефона = ТекСтрока.Объект;
			Прервать;
		КонецЦикла;
		Возврат ВладелецТелефона;
	Иначе
		Возврат Неопределено;	
	КонецЕсли;
КонецФункции

//Функция убирает лишние символы и разделители из строки с номером телефона
//Телефон - строка, в которой нужно убрать лишние символы
Функция кмУбратьЛишниеСимволыТелефона(Телефон) Экспорт
	// Сначала уберем все ненужные символы.
	ВремТелефон	= Телефон;
	ЕстьПлюс	= (Лев(Телефон, 1) = "+");
	НовыйТелефон = "";
	а = 1;
	Пока а <= СтрДлина(ВремТелефон) Цикл
		Симв =  Сред(ВремТелефон, а, 1);
		Если (КодСимвола(Симв) >= 48 И КодСимвола(Симв) <= 57) Тогда
			НовыйТелефон = НовыйТелефон + Симв;
		КонецЕсли;
		а = а + 1;
	КонецЦикла;
	// Проверим телефон который получили.
	ПервыйСимвол = Лев(НовыйТелефон,1);
	Если НЕ ПервыйСимвол = "+" Тогда
		ЭтоФедеральныйНомер =  СтрДлина(НовыйТелефон) > 7;
		Если  ЭтоФедеральныйНомер И НЕ ПервыйСимвол = "8" И НЕ ПервыйСимвол = "0" Тогда
			НовыйТелефон = НовыйТелефон;
		//Заменяем первый символ 8 на +7	
		ИначеЕсли ЭтоФедеральныйНомер И ПервыйСимвол = "8" И НЕ ПервыйСимвол = "0" И НЕ ЕстьПлюс Тогда 
			НовыйТелефон="7" + Сред(НовыйТелефон, 2, СтрДлина(НовыйТелефон));
		КонецЕсли;
		Если СокрЛП(НовыйТелефон) = "" Тогда
			Сообщить("В сообщении не выбран телефон получателя!");
			ЕстьОшибки = Истина;
		КонецЕсли;	
	КонецЕсли;
		//Сообщить(НовыйТелефон);	
	Возврат НовыйТелефон;
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции общего назначения
                                        
//Функция проверяет правильность заполнения табличной части документа События
Функция кмПроверитьТЧСторонниеЛица(Таблица)Экспорт
	ВсеОК = Истина;
	//Проверяем тип контактной информации и заполнение поля контактная информация
	Для Каждого ТекСтрока из Таблица Цикл
		Если НЕ ТекСтрока.ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда	
			Сообщить("В строке № "+ТекСтрока.НомерСтроки+" указана неправильная контактная информация!");
			ВсеОК=Ложь;
		Иначе
			Если ТекСтрока.КонтактнаяИнформация = "" Тогда
				Сообщить("В строке № "+ТекСтрока.НомерСтроки+" не указан номер телефона!");
				ВсеОК=Ложь;
			КонецЕсли;
			//проверим был ли ранее указан этот номер телефона
			НомерТелефона = ТекСтрока.КонтактнаяИнформация;
			НомерСтроки = ТекСтрока.НомерСтроки;
			Для Каждого ТекСтрока2 Из Таблица Цикл
				СтрокаНомерСтрокиСОшибкой = "";
				Если ТекСтрока2.НомерСтроки = НомерСтроки Тогда
					Продолжить;
				КонецЕсли;
				Если ТекСтрока2.КонтактнаяИнформация = НомерТелефона Тогда
					СтрокаНомерСтрокиСОшибкой = СтрокаНомерСтрокиСОшибкой +?(СтрДлина(СтрокаНомерСтрокиСОшибкой)>0,"; "+ТекСтрока2.НомерСтроки,ТекСтрока2.НомерСтроки);
					ВсеОК = Ложь;
				КонецЕсли;
			КонецЦикла;
			//сообщим если были совпадения
			Если НЕ СтрокаНомерСтрокиСОшибкой="" Тогда
				КоличествоСтрокТаблицы = Строка(Таблица.Количество());
				Если СтрДлина(СтрокаНомерСтрокиСОшибкой)>СтрДлина(КоличествоСтрокТаблицы) Тогда
					Сообщить("Номер телефона "+НомерТелефона+" указанный в строке № "+НомерСтроки+" повторяется в строках с № "+СтрокаНомерСтрокиСОшибкой);
				Иначе
					Сообщить("Номер телефона "+НомерТелефона+" указанный в строке № "+НомерСтроки+" повторяется в строке № "+СтрокаНомерСтрокиСОшибкой);	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат ВсеОК;
КонецФункции

//Функция проверяет права на работу с SMS и возвращает Истина если хоть одно право установлено
//глПрава - глобальная переменая "глПрава"
Функция кмПравоНаРаботуСSMS(глПрава) Экспорт
		Возврат Истина
КонецФункции

//Функция возвращает тему SMS
Функция кмВернутьТемуSMS(Содержание) Экспорт
	Если СтрДлина(Содержание)>70 Тогда
		Возврат Лев(Содержание,67)+"...";
	Иначе
		Возврат Содержание;
	КонецЕсли;	
КонецФункции

// Возвращает основной мобильный телефон объекта
Функция кмПолучитьОсновнойМобильныйТелефонОбъекта(Объект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактнаяИнформация.Объект,
	               |	КонтактнаяИнформация.Тип,
	               |	КонтактнаяИнформация.ЗначениеПоУмолчанию,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Представление
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |	И КонтактнаяИнформация.Тип = &Тип
	               |	И КонтактнаяИнформация.Вид = &Вид
	               |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = &ЗначениеПоУмолчанию";
	
	Запрос.УстановитьПараметр("Объект",Объект);
	Запрос.УстановитьПараметр("Тип",Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Вид",Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("ЗначениеПоУмолчанию",Истина);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество()>0 Тогда
		Возврат Результат[0].Представление;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции
///////////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции работы с оборудованием

//Функция проверяет состояние устройства и при необходимости включает его
Функция кмПроверитьЗапуститьОборудование(Оборудование,ТорговоеОборудование) Экспорт
	//Проверим оборудование на включенность
	СтрокаОшибки = "";
	ЕстьОшибкиВОборудовании = Ложь;
	СтатусТекОборудования = ТорговоеОборудование.СтатусОборудования(Оборудование,СтрокаОшибки);
	Если СтатусТекОборудования = Неопределено Тогда
		Сообщить("Ошибка при проверке статуса оборудования: "+СтрокаОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ (СтатусТекОборудования=Перечисления.СтатусыОборудования.Включено) ИЛИ НЕ(СтатусТекОборудования=Перечисления.СтатусыОборудования.Готово) Тогда
		СтрОшибки = "";
		КодОшибки = ТорговоеОборудование.ВключитьОборудование(Оборудование,СтрОшибки);
		Если НЕ КодОшибки = 0 Тогда
			Сообщить("Ошибка при включении оборудования: "+СтрОшибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

// Функция производит отправку SMS
// Параметры:
//	Получатели - Список получателей SMS
//	Сообщение  - текст сообщения
//	ТорговоеОборудование - оборудование для отправки
//	глПрава - права пользователя
//	Отправитель - отправитель сообщения
//	Оборудование - оборудование для отправки
//	Транслит - признак того, что сообщение написано транслитом
//	Отчет - признак отчета о доставке
//	СоздаватьСобытие - признак того, что нужно создать событие об отправке
//	СтрокаОшибкиФункции - строка ошибок
//
Функция кмОтправитьSMS(Получатели,Сообщение,ТорговоеОборудование=Неопределено,глПрава = Неопределено,Отправитель=Неопределено,Оборудование=Неопределено,Транслит=Неопределено,Отчет=Неопределено,СоздаватьСобытие = Неопределено,СтрокаОшибкиФункции = "") экспорт
	СписокВозврата = Новый СписокЗначений;
	//Получаем права если они не заданы
	Если глПрава = Неопределено Тогда
		ОбработкаЗащита = Обработки.Защита.Создать();
		глПрава = ОбработкаЗащита.Права
	КонецЕсли;
	
	//Проверка прав на отправку SMS
	Если НЕ кмПравоНаРаботуСSMS(глПрава) Тогда
		СтрокаОшибкиФункции = "Нет прав на отправку SMS...";
		Возврат СписокВозврата;
	КонецЕсли;
	
	Если СоздаватьСобытие = Неопределено Тогда
		СоздаватьСобытие = Перечисления.ВариантыОтветов.Нет
	КонецЕсли;
	
	Если ТипЗнч(Получатели)=Тип("Строка") Тогда
		СписокПолучателей = Новый СписокЗначений;
		СписокПолучателей.Добавить(Получатели);
	ИначеЕсли ТипЗнч(Получатели)=Тип("СписокЗначений") Тогда
		СписокПолучателей = Получатели;
	КонецЕсли;
	
	//Проверим что нужно сделать с событием
	Если СоздаватьСобытие = Перечисления.ВариантыОтветов.Нет Тогда //Не чего не делаем
		
	Иначе
		НовыйДокумент = Документы.SMS.СоздатьДокумент();
		НовыйДокумент.Заполнить(Неопределено);
		НовыйДокумент.Дата = ТекущаяДата();
		НовыйДокумент.УстановитьНовыйНомер();
		НовыйДокумент.НомерОтправителя = Константы.смсИмяПользователяПоУмолчанию.Получить();
		НовыйДокумент.Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;//"Исходящее";
		НовыйДокумент.ТекстСообщения = Сообщение;
		НовыйДокумент.Автор = ПараметрыСеанса.Пользователь;
		НовыйДокумент.ХозОперация = Справочники.ХозОперации.SMS;
		к=0;
		Для каждого ТекПолучатель из СписокПолучателей Цикл
			ВладелецТелефона = кмВернутьВладельцаНомераТелефона(кмОчиститьТелефонОтРазделителей(ТекПолучатель.Значение));
			Если ТипЗнч(ВладелецТелефона) = Тип("СправочникСсылка.Контрагенты")
				 И НЕ ВладелецТелефона.СогласиеНаПолучениеSMS Тогда
				#Если Клиент Тогда
					Если СоздаватьСобытие = Перечисления.ВариантыОтветов.Спрашивать Тогда
						Сообщить("Контрагент "+ВладелецТелефона+" не дал согласие на получение SMS!", СтатусСообщения.Важное);
					КонецЕсли;
				#КонецЕсли
			КонецЕсли;
			НоваяСтрока = НовыйДокумент.Получатели.Добавить();
			Если СоздаватьСобытие = Перечисления.ВариантыОтветов.Да Тогда
				НоваяСтрока.GUID_SMS = СписокВозврата[к].Представление;
			КонецЕсли;
			НоваяСтрока.КонтактнаяИнформация = ТекПолучатель.Значение;
			НоваяСтрока.ВладелецКИ = ВладелецТелефона;
			НоваяСтрока.Контрагент = ВладелецТелефона;
			НоваяСтрока.ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
			НоваяСтрока.НомерТелефона = НоваяСтрока.КонтактнаяИнформация;
			Если СоздаватьСобытие = Перечисления.ВариантыОтветов.Да Тогда
				НоваяСтрока.Статус = "В очереди";
				НоваяСтрока.ДатаЗавершения = ТекущаяДата();
			КонецЕсли;
			к=к+1;
		КонецЦикла;
		//Ставим статус документа
		НовыйДокумент.СтатусСтрокой = кмПолучитьСтатусSMS(НовыйДокумент);
		
		Если СоздаватьСобытие = Перечисления.ВариантыОтветов.Да Тогда //нужно создать и записать событие
			НовыйДокумент.Записать();	
		ИначеЕсли СоздаватьСобытие = Перечисления.ВариантыОтветов.Спрашивать Тогда //Нужно создать событие и открыть форму
			ФормаДокумента = НовыйДокумент.ПолучитьФорму();
			ФормаДокумента.Открыть();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат СписокВозврата;
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с входящими SMS

//Функция находит документ событие по реквизиту табличной части GUID сообщения и меняет статус сообщения
//Если статус изменен или новый статус совпадает со старым то возвращает истину иначе ложь.
//Если документ не найден с таким GUID то возвращается ложь
Функция кмИзменитьСтатусСообщения(GUID,Статус,ДатаЗавершения,Ошибка = "")Экспорт
	ТекстЗапроса = 	"ВЫБРАТЬ
	|	СобытиеСторонниеЛица.GUID_SMS,
	|	СобытиеСторонниеЛица.Ссылка
	|ИЗ
	|	Документ.SMS.Получатели КАК СобытиеСторонниеЛица
	|ГДЕ
	|	СобытиеСторонниеЛица.GUID_SMS = &GUID";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("GUID",GUID);
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	Если ТаблицаЗапроса.Количество()>0 Тогда 
		ДокументСсылка = ТаблицаЗапроса[0].Ссылка;
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		СтрокаДокумента = ДокументОбъект.Получатели.Найти(GUID,"GUID_SMS");
		Если НЕ СтрокаДокумента.Статус = Статус тогда
			СтрокаДокумента.Статус = Статус;
			СтрокаДокумента.ДатаЗавершения = ДатаЗавершения;
			СтрокаДокумента.ОписаниеОшибки = Ошибка;
			ДокументОбъект.СтатусСтрокой = кмПолучитьСтатусSMS(ДокументОбъект);
			Попытка
				//Сообщить("Попытка записать документ: "+ Строка(ТаблицаЗапроса[0].Ссылка));
				ДокументОбъект.Записать();
				//Сообщить("Документ записан: "+ Строка(ТаблицаЗапроса[0].Ссылка));
				Возврат Истина;
			Исключение
				Сообщить("Документ не записан: "+ Строка(ТаблицаЗапроса[0].Ссылка));
				Сообщить(ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки;
		Иначе
			Возврат Истина;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

//Процедура обрабатывает входящее SMS сообщение
Функция кмОбработатьСообщение(SMS)
	ТекстЗапроса = "ВЫБРАТЬ
	|	Событие.GUIDSMSСообщения,
	|	Событие.Ссылка
	|ИЗ
	|	Документ.SMS КАК Событие
	|ГДЕ
	|	Событие.GUIDSMSСообщения = &Сессия";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Если SMS.Количество>1 Тогда
		Запрос.УстановитьПараметр("Сессия",SMS.Очередь);
	иначе
		Запрос.УстановитьПараметр("Сессия",SMS.GUID);
	КонецЕсли;
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	Если ТаблицаРезультатаЗапроса.Количество()=0 тогда //если нет еще не одного документа с этой сессией
		Возврат кмСоздатьСобытие(SMS);
	Иначе //допишем и соберем что получилось
		Если SMS.Количество>1 Тогда
			Возврат кмОбновитьСобытие(ТаблицаРезультатаЗапроса[0].Ссылка,SMS);
		Иначе
			Возврат Истина
		КонецЕсли;
	КонецЕсли;
КонецФункции

//процедура создает новый документ событие
Функция кмСоздатьСобытие(SMS)Экспорт
	НовыйДокумент = Документы.SMS.СоздатьДокумент();
	НовыйДокумент.УстановитьНовыйНомер();
	НовыйДокумент.Дата = ТекущаяДата();
	НовыйДокумент.ХозОперация = Справочники.ХозОперации.SMS;
	НовыйДокумент.Входящее_Исходящее = Перечисления.ТипыСообщений.Входящее;
	НовыйДокумент.Организация = ПараметрыСеанса.Организация;
	НовыйДокумент.ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	НоваяСтрока = НовыйДокумент.Получатели.Добавить();
	НоваяСтрока.ДатаЗавершения = SMS.ДатаВремяЛогическогоЗавершения;
	НоваяСтрока.КонтактнаяИнформация = SMS.Отправитель_Получатель;
	НоваяСтрока.НомерТелефона = SMS.Отправитель_Получатель;
	ВладелецТелефона = кмВернутьВладельцаНомераТелефона(SMS.Отправитель_Получатель);
	НоваяСтрока.ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
	НоваяСтрока.ВладелецКИ = ВладелецТелефона;
	НоваяСтрока.Контрагент = ВладелецТелефона;
	ТекстСодержания = SMS.ТекстСообщения;
	ТекстСодержания = СтрЗаменить(ТекстСодержания,Символы.ПС,"<ENTER>");
	Если SMS.Количество=1 Тогда
		НовыйДокумент.GUIDSMSСообщения = SMS.GUID;

		НовыйДокумент.ТекстСообщения = ТекстСодержания;
	Иначе
		НовыйДокумент.GUIDSMSСообщения = SMS.Очередь;
		ТекстСообщение = "";
		КоличествоЧастей = SMS.Количество;
		НомерЧасти = SMS.ПорядковыйНомерВСоставе;
		Для к=1 По КоличествоЧастей Цикл
			Если к=НомерЧасти Тогда
				ТекстСообщение = ТекстСообщение+ТекстСодержания;
			Иначе
				ТекстСообщение = ТекстСообщение+"(...)";
			КонецЕсли;
			Если к<>КоличествоЧастей Тогда
				ТекстСообщение = ТекстСообщение+Символы.ПС;
			КонецЕсли;
		КонецЦикла;
		НовыйДокумент.ТекстСообщения = ТекстСообщение;
	КонецЕсли;	
	НовыйДокумент.Автор = ПараметрыСеанса.Пользователь;
	Попытка
		НовыйДокумент.Записать();
		Возврат Истина
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь
	КонецПопытки;
КонецФункции

//Процедура обновляет текст в документе событие
Функция кмОбновитьСобытие(ДокументСсылка,SMS)Экспорт
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ДокументОбъект.ТекстСообщения);
	//Добавить полученную часть в нужное место
	СтрокаСообщения = SMS.ТекстСообщения;
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, Символы.ПС, "<ENTER>");
	НомерЧасти = SMS.ПорядковыйНомерВСоставе;
	Текст.ЗаменитьСтроку(НомерЧасти, СтрокаСообщения);
	ТекстСообщения = "";
	КоличествоТекстовыхЧастей = 0;
	Для к = 1 По Текст.КоличествоСтрок() Цикл
		ТекстСообщения = ТекстСообщения+Текст.ПолучитьСтроку(к) + Символы.ПС;
		Если СокрЛП(Текст.ПолучитьСтроку(к)) <> "(...)" Тогда
			КоличествоТекстовыхЧастей = КоличествоТекстовыхЧастей + 1; 
		КонецЕсли;
	КонецЦикла;
	ВсегоЧастей = SMS.Количество;
	Если КоличествоТекстовыхЧастей = ВсегоЧастей Тогда
		ТекстСообщения = СтрЗаменить(ТекстСообщения, Символы.ПС, "");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "<ENTER>", Символы.ПС);
	КонецЕсли;
	ДокументОбъект.ТекстСообщения = ТекстСообщения;
	Попытка 
		ДокументОбъект.Записать();
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;	
КонецФункции

// Процедура открытия адресной книги
Процедура кмОткрытьАдреснуюКнигу() Экспорт
	Обработки.АдреснаяКнига.Создать().ПолучитьФорму().Открыть();
КонецПроцедуры

//Функция возвращает строковое представление статуса документа
//Документ - документ SMS в котором нужно изменить статус
Функция кмПолучитьСтатусSMS(Документ) Экспорт
	Статус = "";
	ВсегоКоличество = Документ.Получатели.Количество();
	КоличествоОтправленных = 0;
	КоличествоВОчереди = 0;
	КоличествоДоставленных = 0;
	КоличествоОшибок = 0;
	Для Каждого ТекСтрокаДокумента Из Документ.Получатели Цикл
		Если ТекСтрокаДокумента.Статус = "Отправлено" Тогда
			КоличествоОтправленных = КоличествоОтправленных+1;
		ИначеЕсли ТекСтрокаДокумента.Статус = "Доставлено" Тогда
			КоличествоДоставленных = КоличествоДоставленных+1;
			КоличествоОтправленных = КоличествоОтправленных+1;
		ИначеЕсли ТекСтрокаДокумента.Статус = "Ошибка" Тогда
			КоличествоОшибок = КоличествоОшибок+1;
		ИначеЕсли ТекСтрокаДокумента.Статус = "В очереди" Тогда
			КоличествоВОчереди = КоличествоВОчереди+1;
		КонецЕсли;
	КонецЦикла;
	Статус = "Всего: "+ВсегоКоличество+";"+?((КоличествоВОчереди=0)и(КоличествоОтправленных=0)и(КоличествоДоставленных=0)и(КоличествоОшибок=0),"SMS записано, но не отправлено"," из них:")+?(КоличествоВОчереди=0,""," в очереди-"+КоличествоВОчереди+";")+?(КоличествоОтправленных=0,""," отправлено-"+КоличествоОтправленных+";")+?(КоличествоДоставленных=0,""," доставлено-"+КоличествоДоставленных+";")+?(КоличествоОшибок=0,""," ошибок-"+КоличествоОшибок);
	Возврат Статус;
КонецФункции

//Функция проверяет есть ли документ с таким GUID-ом в БД или нет
//GUID - GUID сообщения которого нужно проверить
Функция кмПроверкаСуществованияSMS(Документ) Экспорт
	ТекстЗапроса = "ВЫБРАТЬ
	|	SMS.GUIDSMSСообщения,
	|	SMS.Ссылка
	|ИЗ
	|	Документ.SMS КАК SMS
	|ГДЕ
	|	SMS.GUIDSMSСообщения = &GUID
	|	И SMS.Ссылка <> &ДокСсылка";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("GUID",Документ.GUIDSMSСообщения);
	Запрос.УстановитьПараметр("ДокСсылка",Документ.Ссылка);
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	Если ТаблицаРезультатаЗапроса.Количество()>0 Тогда
		Возврат Истина
	Иначе
		Возврат Ложь
	КонецЕсли;
КонецФУнкции

////////////////////////////////////////////////////////////////////////////////////////
// Транслитирация текста

//Функция возвращает русский текст из транслитированного
Функция кмОбратнаяТранслитирация(Текст) Экспорт
	//Заполняем список соответствий
	СписокСоответствийТранслитерации = Новый СписокЗначений;
	СписокСоответствийТранслитерации.Добавить("y","й");
	СписокСоответствийТранслитерации.Добавить("tz","ц");
	СписокСоответствийТранслитерации.Добавить("u","у");
	СписокСоответствийТранслитерации.Добавить("k","к");
	СписокСоответствийТранслитерации.Добавить("e","е");
	СписокСоответствийТранслитерации.Добавить("n","н");
	СписокСоответствийТранслитерации.Добавить("g","г");
	СписокСоответствийТранслитерации.Добавить("sh","ш");
	СписокСоответствийТранслитерации.Добавить("sch","щ");
	СписокСоответствийТранслитерации.Добавить("z","з");
	СписокСоответствийТранслитерации.Добавить("h","х");
	СписокСоответствийТранслитерации.Добавить("f","ф");
	СписокСоответствийТранслитерации.Добавить("yi","ы");
	СписокСоответствийТранслитерации.Добавить("v","в");
	СписокСоответствийТранслитерации.Добавить("a","а");
	СписокСоответствийТранслитерации.Добавить("p","п");
	СписокСоответствийТранслитерации.Добавить("r","р");
	СписокСоответствийТранслитерации.Добавить("o","о");
	СписокСоответствийТранслитерации.Добавить("l","л");
	СписокСоответствийТранслитерации.Добавить("d","д");
	СписокСоответствийТранслитерации.Добавить("zh","ж");
	СписокСоответствийТранслитерации.Добавить("ye","э");
	СписокСоответствийТранслитерации.Добавить("ya","я");
	СписокСоответствийТранслитерации.Добавить("ch","ч");
	СписокСоответствийТранслитерации.Добавить("s","с");
	СписокСоответствийТранслитерации.Добавить("m","м");
	СписокСоответствийТранслитерации.Добавить("i","и");
	СписокСоответствийТранслитерации.Добавить("t","т");
	СписокСоответствийТранслитерации.Добавить("'","ь");
	СписокСоответствийТранслитерации.Добавить("b","б");
	СписокСоответствийТранслитерации.Добавить("yu","ю");
	
	ДлинаТекста = СтрДлина(Текст);
	РезультирующийТекст = "";
	
	к=1;
	Пока к<=ДлинаТекста Цикл
		//поищем сначала совпадения по 3-м символам
		Если НЕ к+2>ДлинаТекста Тогда
			Символ = Сред(Текст,к,3);
			ПервыйЗаглавный3 = КодСимвола(Лев(Символ,1))>=65 и КодСимвола(Лев(Символ,1))<=90;
			Перевод3 = СписокСоответствийТранслитерации.НайтиПоЗначению(Нрег(Символ));
		Иначе
			 Перевод3=Неопределено
		КонецЕсли;
		Если Перевод3=Неопределено Тогда
			//смотрим на 2 символа :)
			Если НЕ к+1>ДлинаТекста Тогда
				Символ = Сред(Текст,к,2);
				ПервыйЗаглавный2 = КодСимвола(Лев(Символ,1))>=65 и КодСимвола(Лев(Символ,1))<=90;
				Перевод2 = СписокСоответствийТранслитерации.НайтиПоЗначению(нРег(Символ));
			Иначе
				Перевод2 = Неопределено
			КонецЕсли;
			Если Перевод2 = Неопределено тогда
				//смотрим на один символ :))))
				Символ = Сред(Текст,к,1);
				ПервыйЗаглавный1 = КодСимвола(Лев(Символ,1))>=65 и КодСимвола(Лев(Символ,1))<=90;
				Перевод1 = СписокСоответствийТранслитерации.НайтиПоЗначению(Нрег(Символ));
				Если Перевод1 = Неопределено Тогда //ну тогда оставляем этот символ как есть
					РезультирующийТекст = РезультирующийТекст+Символ;
				Иначе
					Если ПервыйЗаглавный1 И Символ<>"'" Тогда
						РезПеревод = ВРег(Перевод1);
					Иначе
						РезПеревод = Перевод1
					КонецЕсли;
					РезультирующийТекст = РезультирующийТекст+РезПеревод;					
				КонецЕсли;
				к=к+1
			Иначе
				Если ПервыйЗаглавный2 Тогда
					РезПеревод = ВРег(Лев(Перевод2,1))+Сред(Перевод2,2);
				Иначе
					РезПеревод = Перевод2
				КонецЕсли;
				РезультирующийТекст = РезультирующийТекст+РезПеревод;
				
				к=к+2
			КонецЕсли;
		Иначе
			Если ПервыйЗаглавный3 Тогда
				РезПеревод = ВРег(Лев(Перевод3,1))+Сред(Перевод3,2);
			Иначе
				РезПеревод = Перевод3
			КонецЕсли;
			РезультирующийТекст = РезультирующийТекст+РезПеревод;
			к=к+3
		КонецЕсли;
	КонецЦикла;
	Возврат РезультирующийТекст;
КонецФункции

// Функция возвращает транслитерированный текст сообщения
Функция кмТранслитерация(Текст) Экспорт

	//Заполняем список соответствий
	СписокСоответствийТранслитерации = Новый СписокЗначений;
	СписокСоответствийТранслитерации.Добавить("й","y");
	СписокСоответствийТранслитерации.Добавить("ц","tz");
	СписокСоответствийТранслитерации.Добавить("у","u");
	СписокСоответствийТранслитерации.Добавить("к","k");
	СписокСоответствийТранслитерации.Добавить("е","e");
	СписокСоответствийТранслитерации.Добавить("н","n");
	СписокСоответствийТранслитерации.Добавить("г","g");
	СписокСоответствийТранслитерации.Добавить("ш","sh");
	СписокСоответствийТранслитерации.Добавить("щ","sch");
	СписокСоответствийТранслитерации.Добавить("з","z");
	СписокСоответствийТранслитерации.Добавить("х","h");
	СписокСоответствийТранслитерации.Добавить("ъ","'");
	СписокСоответствийТранслитерации.Добавить("ф","f");
	СписокСоответствийТранслитерации.Добавить("ы","yi");
	СписокСоответствийТранслитерации.Добавить("в","v");
	СписокСоответствийТранслитерации.Добавить("а","a");
	СписокСоответствийТранслитерации.Добавить("п","p");
	СписокСоответствийТранслитерации.Добавить("р","r");
	СписокСоответствийТранслитерации.Добавить("о","o");
	СписокСоответствийТранслитерации.Добавить("л","l");
	СписокСоответствийТранслитерации.Добавить("д","d");
	СписокСоответствийТранслитерации.Добавить("ж","zh");
	СписокСоответствийТранслитерации.Добавить("э","ye");
	СписокСоответствийТранслитерации.Добавить("я","ya");
	СписокСоответствийТранслитерации.Добавить("ч","ch");
	СписокСоответствийТранслитерации.Добавить("с","s");
	СписокСоответствийТранслитерации.Добавить("м","m");
	СписокСоответствийТранслитерации.Добавить("и","i");
	СписокСоответствийТранслитерации.Добавить("т","t");
	СписокСоответствийТранслитерации.Добавить("ь","'");
	СписокСоответствийТранслитерации.Добавить("б","b");
	СписокСоответствийТранслитерации.Добавить("ю","yu");
	СписокСоответствийТранслитерации.Добавить("ё","e");
	
	РезультирующийТекст = "";
	ДлинаТекста =  СтрДлина(Текст);
	
	Для Сч = 1 По  ДлинаТекста Цикл
		
		КодСимвола = КодСимвола(Текст,Сч);
		
		РусскаяЗаглавная = Ложь;
		Если  (КодСимвола>= 1040 И КодСимвола<=1071) ИЛИ КодСимвола = 1025 Тогда
			РусскаяЗаглавная = Истина;
		КонецЕсли;	
		
		Символ =  НРег(Сред(Текст,Сч,1));
		Соответствие = СписокСоответствийТранслитерации.НайтиПоЗначению(Символ);
		
		Если НЕ Соответствие = Неопределено Тогда
			Символ = Соответствие.Представление;
			Если РусскаяЗаглавная Тогда
				ПервыйСимвол = Сред(Символ,1,1);
				Символ = СтрЗаменить(Символ,ПервыйСимвол,ВРег(ПервыйСимвол));
			КонецЕсли;
		КонецЕсли;	
		
		РезультирующийТекст = РезультирующийТекст + Символ;
		
	КонецЦикла;
	
	Возврат  РезультирующийТекст;
	
КонецФункции	

Процедура КонтрольЭлементовФормы(Форма,ТипФормы) Экспорт
	ОбъектДокумент = Форма.ЭтотОбъект.Метаданные().Имя;
	ПраваПользователя = глПрава;
	ШаблонПравПользователя = ПараметрыСеанса.Пользователь.ШаблонПрав;	
	
	ШаблонПрав = новый СписокЗначений;
	ШаблонПрав.Добавить(ШаблонПравПользователя);
	ШаблонПрав.Добавить(Справочники.Пользователи.ПустаяСсылка());
	
	Настройки = ПолучитьНастройкиЭлементов(ОбъектДокумент,ТипФормы,ШаблонПрав);
	
	Для Каждого Настройка из Настройки Цикл
		
				
		ПроверкаПоШаблонуПрав = ?(ЗначениеЗаполнено(Настройка.ШаблонПрав) Или ЗначениеЗаполнено(Настройка.ШаблонПравИсключение),Истина,Ложь);
		ПроверкаПоПравам = ?(ЗначениеЗаполнено(Настройка.Право) Или ЗначениеЗаполнено(Настройка.ПравоИсключение),Истина,Ложь);
		ПравилоДляВсехБезИсключений = ?(ПроверкаПоШаблонуПрав = Ложь И ПроверкаПоПравам = Ложь,Истина,Ложь);
				
		//Проверка по шаблону
		Если НЕ ПравилоДляВсехБезИсключений И ПроверкаПоШаблонуПрав тогда
			//Проверка на конкретный шаблон
			Если ЗначениеЗаполнено(Настройка.ШаблонПрав) И ШаблонПравПользователя <> Настройка.ШаблонПрав Тогда
				ПРодолжить; //Правило не подходит
			КонецЕсли;
			//Проверка на Исключение
			Если ЗначениеЗаполнено(Настройка.ШаблонПравИсключение) И ШаблонПравПользователя = Настройка.ШаблонПравИсключение Тогда
				ПРодолжить; //Правило не подходит
			КонецЕсли;
		КонецЕсли;
		
		//Проверка по Правам
		Если НЕ ПравилоДляВсехБезИсключений И ПроверкаПоПравам тогда
			//Проверка на конкретное право
			Если ЗначениеЗаполнено(Настройка.Право) И ЗначениеЗаполнено(обПраво(Настройка.Право,ПраваПользователя)) И обПраво(Настройка.Право,ПраваПользователя) = Ложь Тогда
				ПРодолжить; //Правило не подходит
			КонецЕсли;
			//Проверка на Исключение
			Если ЗначениеЗаполнено(Настройка.ПравоИсключение) И ЗначениеЗаполнено(обПраво(Настройка.ПравоИсключение,ПраваПользователя)) И обПраво(Настройка.ПравоИсключение,ПраваПользователя) = Ложь Тогда
				ПРодолжить; //Правило не подходит
			КонецЕсли;
		КонецЕсли;
			
		
		ТабличнаяЧастьЛи = Настройка.ТабличнаяЧасть;
		НаимЭлемента = Настройка.Элемент;
		
		СвойствоЭлемента = Настройка.Свойство;
		ЗначениеСвойства = Настройка.Значение;
		
		Если НЕ ЗначениеЗаполнено(СвойствоЭлемента) ИЛИ НЕ ЗначениеЗаполнено(ЗначениеСвойства) Тогда
			Продолжить;		
		КонецЕсли;
		
		Если ЗначениеСвойства = "Истина" тогда
			ЗначениеСвойства = Истина;
		ИначеЕсли ЗначениеСвойства = "Ложь" Тогда
			ЗначениеСвойства = ложь;             
		Иначе
			ЗначениеСвойства = Неопределено;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СвойствоЭлемента) ИЛИ НЕ ЗначениеЗаполнено(ЗначениеСвойства) Тогда
			Продолжить;		
		КонецЕсли;
		
		Элемент = ПолучитьЭлементНаФорме(Форма,НаимЭлемента);
		
		Если Элемент = Ложь Тогда
			Продолжить;		
		КонецЕсли;
		
		Если ТабличнаяЧастьЛи Тогда
			ИмяКолонки = Настройка.КолонкаТаблЧасти;
			Элемент = ПолучитьКолонкуТаблицы(Элемент,ИмяКолонки)
		КонецЕсли;
		
		Если Элемент = Ложь Тогда
			Продолжить;		
		КонецЕсли;
		
		УстановитьЗначениеСвойстваЭлемента(Элемент,СвойствоЭлемента,ЗначениеСвойства);
			
	КонецЦикла;
	
	
	
	
КонецПроцедуры 

Функция ПолучитьНастройкиЭлементов(Объект, ТипФормы, ШаблонПрав)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтрольЭлементовФорм.ДокументОбъект КАК ДокументОбъект,
		|	КонтрольЭлементовФорм.Форма КАК Форма,
		|	КонтрольЭлементовФорм.Активно КАК Активно,
		|	КонтрольЭлементовФорм.Элемент КАК Элемент,
		|	КонтрольЭлементовФорм.ТабличнаяЧасть КАК ТабличнаяЧасть,
		|	КонтрольЭлементовФорм.КолонкаТаблЧасти КАК КолонкаТаблЧасти,
		|	КонтрольЭлементовФорм.Свойство КАК Свойство,
		|	КонтрольЭлементовФорм.Значение КАК Значение,
		|	КонтрольЭлементовФорм.Право КАК Право,
		|	КонтрольЭлементовФорм.ШаблонПрав КАК ШаблонПрав,
		|	КонтрольЭлементовФорм.ПравоИсключение КАК ПравоИсключение,
		|	КонтрольЭлементовФорм.ШаблонПравИсключение КАК ШаблонПравИсключение
		|ИЗ
		|	РегистрСведений.КонтрольЭлементовФорм КАК КонтрольЭлементовФорм
		|ГДЕ
		|	КонтрольЭлементовФорм.Активно = ИСТИНА
		|	И КонтрольЭлементовФорм.ДокументОбъект = &ДокументОбъект
		|	И КонтрольЭлементовФорм.Форма = &Форма
		|	И КонтрольЭлементовФорм.ШаблонПрав В(&ШаблонПрав)";
	
	Запрос.УстановитьПараметр("ДокументОбъект", Объект);
	Запрос.УстановитьПараметр("Форма", ТипФормы);
	Запрос.УстановитьПараметр("ШаблонПрав", ШаблонПрав);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции

Функция ПолучитьЭлементНаФорме(Форма,Элемент)
	Попытка
		Возврат Форма.ЭлементыФормы[Элемент];
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции

Функция ПолучитьКолонкуТаблицы(Таблица,ИмяКолонки)
	Попытка
		Возврат Таблица.Колонки[ИмяКолонки];
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции 

Функция УстановитьЗначениеСвойстваЭлемента(Элемент,Свойство,Значение)
	Попытка
		Элемент[Свойство] = Значение;
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции
//Бизтех Коваль 13.01.2024 ++
Функция ПриВызовеИзТаблицы(Документ,ИмяТаблицы, ВыбраннаяСтрока, Колонка) Экспорт
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат истина;
	конецЕсли;
	
	Попытка
		ИмяКолонки = Колонка.ТекущаяКолонка.Имя;
	Исключение
		Возврат Истина;
	КонецПопытки;
	
	БлокируемыеКолонки = Новый Массив;
	БлокируемыеКолонки.Добавить("Сумма");
	БлокируемыеКолонки.Добавить("СуммаВсего");
	БлокируемыеКолонки.Добавить("Нормочас");
	БлокируемыеКолонки.Добавить("ПроцентСкидки");
	БлокируемыеКолонки.Добавить("СуммаСкидки");
	
	Если ИмяТаблицы <> "Товары" Тогда
		БлокируемыеКолонки.Добавить("Цена");
	КОнецЕсли;
	
	
	ИсключенияРаботы = Новый Массив; 
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00015442")); //Лакокрасочные материалы
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00061220")); //СЛЕСАРНЫЕ РАБОТЫ 
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00082349")); //Малярно-кузовной ремонт
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00085402")); //Перешив руля
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00082170")); //Ремонт сидения
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00085406")); //Мойка автомобиля (Комплекс)
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00085405")); //Мойка автомобиля (кузов)
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00080973")); //Химчистка салона автомобиля
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00044658")); //Перетяжка кожей (субподряд)
	ИсключенияРаботы.Добавить(Справочники.Автоработы.НайтиПоКоду("ЦБ00085408")); //Перетяжка потолка
	
	ПраваПользователя = глПрава;
	ЕстьЛиПраво = обПраво(ПланыВидовХарактеристик.ПраваИНастройки.НайтиПоКоду("К0002"),ПраваПользователя);
	Если ЕстьЛиПраво <> Истина Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка
		ТекущаяНоменклатураАвторабота = ВыбраннаяСтрока.Работа;
	Исключение
		ТекущаяНоменклатураАвторабота = ВыбраннаяСтрока.Номенклатура;
	КонецПопытки;
	
	Если БлокируемыеКолонки.Найти(ИмяКолонки) = Неопределено Тогда
		Возврат Истина;
	Иначе
		Если ЗначениеЗаполнено(ТекущаяНоменклатураАвторабота) И ИсключенияРаботы.Найти(ТекущаяНоменклатураАвторабота) <> Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
//Бизтех Коваль 13.01.2024 --
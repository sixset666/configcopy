// Модуль отличий отраслевых решений

// Данный общий модуль существует во всех отраслевых конфигурациях, созданных
// на базе технологического ядра 1С-Рарус v8. Данный модуль не ставится на поддержку
// от технологического ядра, однако должен содержать одинаковые объявления
// общих процедур и функций. При необходимости внесения доработок отраслевого решения,
// в стандартное API объектов общего ядра, соответствующие исключения прописываются в этих функциях.
// При отсутствии объявления необходимой функции в данном модуле технологического ядра
// и наследников, объявления и вызов добавляется в ядро и все дочерние конфигурации.
// Общий принцип формирования переопределяющих функций - возвращать "Неопределено"
// при отсутствии отличий от стандартной функции или необходимости дальнейшей стандартной обработки.

///////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция возвращает таблицу долгов по предоплате для заказов покупателя и заказов на автомобили.
//
// Параметры:
//	ДокументОбъект            - ДокументСсылка - Документ, который вызвал функцию.
//	Заказ                     - ДокументСсылка или массив  - Заказ или заказы, для которых нужно получить долги по предоплатам.
//	ДополнительнаяСуммаЗаказа - Число - Дополнительная сумма заказа, например при корректировке.
//
// Возвращаемое значение:
//	Таблица значений - Таблица долгов в разрезе заказов.
//
Функция орПолучитьТаблицуДолговПоПредоплатам(ДокументОбъект, Заказ, ДополнительнаяСуммаЗаказа = 0) Экспорт
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Дата",ДокументОбъект.Дата);
	ДокументОбъектСтруктура.Вставить("МоментВремени",ДокументОбъект.МоментВремени());
	ДокументОбъектСтруктура.Вставить("Проведен",ДокументОбъект.Проведен);
	
	//ЗАЩИТА: Перед сборкой заменить на вызов функции из защищенного хранилища
	//Возврат Обработки.ЗащитаТОР.Создать().ПолучитьТаблицуДолговПоПредоплатам(ДокументОбъектСтруктура,Заказ,ДополнительнаяСуммаЗаказа);
	//ЗАЩИТА: Вызов функции из защищенного хранилища 
	Возврат зфЗащищенныеФункцииСервер.ПолучитьТаблицуДолговПоПредоплатам(ДокументОбъектСтруктура,Заказ,ДополнительнаяСуммаЗаказа);
	//ЗАЩИТА: Коней вызова функции из защищенного хранилища 
КонецФункции // орПолучитьТаблицуДолговПоПредоплатам()

// Функция возвращает цену товара. При отсутствии переопределения возвращает Неопределено
//
// Параметры:
//	ТипЦен       - СправочникСсылка - Тип цены
//	Номенклатура - СправочникСсылка  - Товар
//	НаМомент     - Дата				 - Момент времени
//	Контрагент   - СправочникСсылка	 - Контрагент
//	Валюта       - СправочникСсылка  - Валюта
//	Курс         - Число			 - Курс валюты
//	ХарактеристикаНоменклатуры	- СправочникСсылка  - Характеристика номенклатуры
//	ЕдиницаИзмерения			- СправочникСсылка  - Единица измерения номенклатуры
//	ПодразделениеКомпании		- СправочникСсылка  - Подразделение компании
//
// Возвращаемое значение:
//	Число - Цена товара
//
Функция орПолучитьЦену(ТипЦен, Номенклатура, Знач НаМомент, Контрагент, Валюта, Курс, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании, ПолучитьЦенуБазовойЕдиницы, ВариантПоставки=Неопределено) Экспорт
	Цена=Неопределено;
	
	// определим на какой момент получаем цену
	Если НаМомент=Неопределено Тогда
		#Если Клиент Тогда
			НаМомент=РабочаяДата;
		#Иначе
			НаМомент=ТекущаяДата();
		#КонецЕсли
		НаМомент=КонецДня(НаМомент);
	КонецЕсли;
	
	// проверим, а не на документ ли получаем цену
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(НаМомент)) Тогда
		// если документ еще не записан, то получим цену на дату документа, иначе на момент времени
		Если обЗначениеНеЗаполнено(НаМомент) Тогда
			#Если Клиент Тогда
				НаМомент=РабочаяДата;
			#Иначе
				НаМомент=ТекущаяДата();
			#КонецЕсли
			НаМомент=КонецДня(НаМомент);
		Иначе
			НаМомент=НаМомент.МоментВремени();
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Номенклатура)=Тип("СправочникСсылка.Номенклатура")Тогда
		
		Если ТипЦен.Рассчитывается Тогда
			//Если тип цен - расчетный, 
			//то проверим указан ли процент наценки для данного типа номенклатуры
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТипыЦенПроцентыСкидкиНаценки.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки
			|ИЗ
			|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК ТипыЦенПроцентыСкидкиНаценки
			|ГДЕ
			|	ТипыЦенПроцентыСкидкиНаценки.Ссылка.Ссылка = &ТипЦен
			|	И ТипыЦенПроцентыСкидкиНаценки.ТипНоменклатуры = &ТипНоменклатуры";
			Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
			Запрос.УстановитьПараметр("ТипНоменклатуры", Номенклатура.ТипНоменклатуры);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				// Указан Процент наценки для Типа номенклатуры
				ПроцентСкидкиНаценки = Выборка.ПроцентСкидкиНаценки;
			Иначе
				// Используем Процент наценки из типа цены
				ПроцентСкидкиНаценки = ТипЦен.ПроцентСкидкиНаценки;
			КонецЕсли;
			
			// получим цену базового типа
			БазоваяЦена = обПолучитьЦену(ТипЦен.БазовыйТипЦен, Номенклатура, НаМомент, Контрагент, Валюта, Курс, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании, ПолучитьЦенуБазовойЕдиницы, ВариантПоставки);
			Если БазоваяЦена = 0 Тогда Возврат 0; КонецЕсли;
			// рассчитаем цену по наценке
			Цена = БазоваяЦена + (БазоваяЦена*ПроцентСкидкиНаценки/100);
			// При необходимости выполним пересчет по валюте
			Если обЗначениеНеЗаполнено(Валюта) Тогда
				ВалютаБазовойЦены = обВалютаТипаЦены(Номенклатура, ТипЦен.БазовыйТипЦен);
				ВалютаЦены        = обВалютаТипаЦены(Номенклатура, ТипЦен);
				КурсЦены          = НаМомент;
				Цена              = обПересчет(Цена, ВалютаБазовойЦены, НаМомент, ВалютаЦены, КурсЦены);
			КонецЕсли;
			
		Иначе
			Если ТипЗнч(ВариантПоставки) = Тип("Структура") И ЗначениеЗаполнено(ВариантПоставки.Поставщик) Тогда
				
				// Если указан какой-то Контрагент или Прайс-лист контрагента, то поищем цены в прайс-листах
				ЦеныПоставщика = РегистрыСведений.ПрайсЛистыКонтрагентов.ПолучитьПрайсЛистыКонтрагентов(Номенклатура,, ВариантПоставки.Поставщик, НаМомент, ПодразделениеКомпании);
				Если ЦеныПоставщика <> Неопределено Тогда
					Если Не обЗначениеНеЗаполнено(ВариантПоставки.КлючСтрокиПоставщика) Тогда
						ВыбраннаяСтрока = ЦеныПоставщика.Найти(ВариантПоставки.КлючСтрокиПоставщика, "КлючСтрокиПоставщика");
					КонецЕсли;
					
					Если ВыбраннаяСтрока = Неопределено Тогда
						Если ЦеныПоставщика.Количество() = 1 Тогда
							ВыбраннаяСтрока = ЦеныПоставщика[0];
						ИначеЕсли ЦеныПоставщика.Количество() > 1 Тогда
							Если ВариантПоставки.ИнтерактивныйВвод Тогда
								#Если Клиент Тогда
									ВыбраннаяСтрока = ЦеныПоставщика.ВыбратьСтроку("Выбор позиции из прайс-листа поставщика");
								#Иначе
									ВыбраннаяСтрока = ЦеныПоставщика[0];
								#КонецЕсли
							Иначе
								ВыбраннаяСтрока = ЦеныПоставщика[0];
							КонецЕсли;
						Иначе
							// Проверим, есть ли вообще у контрагента прайс-листы
							Запрос = Новый Запрос;
							Запрос.Текст = 
							"ВЫБРАТЬ ПЕРВЫЕ 1
								|	ПрайсЛистыКонтрагентов.ПрайсЛист
								|ИЗ
								|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
								|ГДЕ
								|	ПрайсЛистыКонтрагентов.Контрагент = &Контрагент";
							Запрос.УстановитьПараметр("Контрагент", ВариантПоставки.Поставщик);
							РезультатЗапроса = Запрос.Выполнить().Выбрать();
							Если (РезультатЗапроса.Следующий() ИЛИ ТипЗнч(ВариантПоставки.Поставщик) <> Тип("СправочникСсылка.Контрагенты")) И ВариантПоставки.ИнтерактивныйВвод Тогда
								#Если Клиент Тогда
									Предупреждение("Номенклатура в прайс-листе поставщика не найдена!");
								#КонецЕсли
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если ВыбраннаяСтрока = Неопределено Тогда
					// В прайс-листах не найдено или отказ выбора позиции
					ВариантПоставки.КлючСтрокиПоставщика = "";
					ВариантПоставки.СрокПоставки         = "";
					// Просто получим цену по Типу цены
					Цена = обПолучитьЦену(ТипЦен, Номенклатура, НаМомент, Контрагент, Валюта, Курс, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании);
				Иначе
					// Что-то нашли и выбрали
					ВариантПоставки.Поставщик = ВыбраннаяСтрока.ПрайсЛист;
					ВариантПоставки.КлючСтрокиПоставщика = ВыбраннаяСтрока.КлючСтрокиПоставщика;
					ВариантПоставки.СрокПоставки = ВыбраннаяСтрока.СрокПоставки;
					// При необходимости выполним пересчет по валюте
					Если ЗначениеЗаполнено(Валюта) Тогда
						ВалютаЦены = Валюта;
						КурсЦены   = ?(Курс = 0, НаМомент, Курс);
					Иначе
						ВалютаЦены = обВалютаТипаЦены(Номенклатура, ТипЦен);
						КурсЦены   = НаМомент;
					КонецЕсли;
					
					ЦенаВВалюте = обПересчет(ВыбраннаяСтрока.ЦенаВПрайсЛисте, ВыбраннаяСтрока.Валюта, НаМомент, ВалютаЦены, КурсЦены);
					СтруктураЦены = Справочники.ПрайсЛистыКонтрагентов.РассчитатьЦеныПрайсЛиста(ВыбраннаяСтрока.ПрайсЛист, Истина, Истина, ТипЦен, ПодразделениеКомпании, НаМомент, ЦенаВВалюте, Номенклатура, ВыбраннаяСтрока.ТегПозиции, ВыбраннаяСтрока.Производитель);
					
					Если ВариантПоставки.Свойство("Закупка") И ВариантПоставки.Закупка Тогда
						Цена = СтруктураЦены.ЦенаПокупки;
					Иначе
						Цена = СтруктураЦены.ЦенаПродажи;
					КонецЕсли;
					
					Если ТипЦен.ЦенаВключаетНДС <> ВыбраннаяСтрока.ЦенаВключаетНДС Тогда
						Если ВариантПоставки.Свойство("СтавкаНДС") Тогда
							СтавкаНДС = ВариантПоставки.СтавкаНДС.Ставка;
						Иначе
							СтавкаНДС = Номенклатура.СтавкаНДС.Ставка;
						КонецЕсли;
						Если ТипЦен.ЦенаВключаетНДС Тогда
							// Добавим НДС
							Цена = Окр(Цена * (100 + СтавкаНДС) / 100, 2);
						Иначе
							// Уберем НДС
							Цена = Окр(Цена * 100 / (100 + СтавкаНДС), 2);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Номенклатура)=Тип("Структура") Тогда
		//Проверка на получение цены автомобиля или модели или опции
		//Пока будем считать что хочется получить именно цену автомобиля или модели
		СтруктураОтбора=Новый Структура("ТипЦен",ТипЦен);
		ВариантКомплектации=Справочники.ВариантыКомплектации.ПустаяСсылка();
		Опция=Справочники.Опции.ПустаяСсылка();
		ИмяРегистраЦен="ЦеныАвтомобилей";
		Попытка
			Автомобиль=Номенклатура.Автомобиль;
			Если обЗначениеНеЗаполнено(ТипЦен) ИЛИ обЗначениеНеЗаполнено(Автомобиль) Тогда Возврат 0; КонецЕсли;
			Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
				СтруктураОтбора.Вставить("Автомобиль",Автомобиль);
				СтруктураОтбора.Вставить("ВариантКомплектации",ВариантКомплектации);
			ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
				ВариантКомплектации=Номенклатура.ВариантКомплектации;
				Если ТипЗнч(ВариантКомплектации)<>Тип("СправочникСсылка.ВариантыКомплектации") Тогда
					Возврат Неопределено;
				КонецЕсли;
				СтруктураОтбора.Вставить("ВариантКомплектации",ВариантКомплектации);
				Попытка
					Опция=Номенклатура.Опция;
					Если ТипЗнч(Опция)<>Тип("СправочникСсылка.Опции") Тогда
						Возврат Неопределено;
					КонецЕсли;
					Если обЗначениеНеЗаполнено(Опция) Тогда Возврат 0; КонецЕсли;
					//Т.к. в структуре запрашивается опция, значит будем получать цену из регистра цен на опции
					ИмяРегистраЦен="ЦеныОпций";
					СтруктураОтбора.Вставить("Модель",Автомобиль);
					СтруктураОтбора.Вставить("Опция",Опция);
				Исключение
					СтруктураОтбора.Вставить("Автомобиль",Автомобиль);
				КонецПопытки; 
			Иначе
				Возврат Неопределено;
			КонецЕсли; 
		Исключение
			Возврат Неопределено;
		КонецПопытки; 
		// если тип цен - расчетный, то рассчитаем цену
		Если ТипЦен.Рассчитывается Тогда
			// получим цену базового типа
			БазоваяЦена=обПолучитьЦену(ТипЦен.БазовыйТипЦен,Номенклатура,НаМомент,Контрагент,Валюта,Курс,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,ПодразделениеКомпании,ПолучитьЦенуБазовойЕдиницы);
			Если БазоваяЦена=0 Тогда Возврат 0; КонецЕсли;
			
			//Добавим или вычтем НДС к/из базовой цене
			Если ТипЦен.ЦенаВключаетНДС<>ТипЦен.БазовыйТипЦен.ЦенаВключаетНДС Тогда
				Если ТипЦен.ЦенаВключаетНДС Тогда
					БазоваяЦена=БазоваяЦена*(1+(Справочники.СтавкиНДС.ОсновнаяСтавкаНДС.Ставка/100));
				Иначе
					БазоваяЦена=(100*БазоваяЦена)/(100+Справочники.СтавкиНДС.ОсновнаяСтавкаНДС.Ставка);
				КонецЕсли; 
			КонецЕсли; 
			// рассчитаем цену
			Цена=БазоваяЦена+(БазоваяЦена*ТипЦен.ПроцентСкидкиНаценки/100);
		Иначе
			// получаем таблицу цен
			СтруктураЦен=РегистрыСведений[ИмяРегистраЦен].ПолучитьПоследнее(НаМомент,СтруктураОтбора);
			// получаем цену
			Цена=СтруктураЦен.Цена;
			Если Цена=0 И ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
				//Если запрашивается цена автомобиля, а она не задана - получим соответствующую цену на модель
				Номенклатура.Автомобиль=Автомобиль.Модель;
				Номенклатура.ВариантКомплектации=Автомобиль.ВариантКомплектации;
				Цена=орПолучитьЦену(ТипЦен,Номенклатура,НаМомент,Контрагент,Валюта,Курс,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,ПодразделениеКомпании,ПолучитьЦенуБазовойЕдиницы);
			Иначе
				Если Цена<>0 Тогда
					// если указана валюта, то пересчитаем цену в нее
					Если НЕ обЗначениеНеЗаполнено(Опция) Тогда
						ВалютаТипаЦены = обВалютаТипаЦены(Опция, ТипЦен);
					ИначеЕсли НЕ обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
						ВалютаТипаЦены = обВалютаТипаЦены(ВариантКомплектации, ТипЦен);
					Иначе
						ВалютаТипаЦены = обВалютаТипаЦены(Автомобиль, ТипЦен);
					КонецЕсли; 
					Если НЕ обЗначениеНеЗаполнено(Валюта) И Валюта<>ВалютаТипаЦены Тогда
						Цена=обПересчет(Цена,ВалютаТипаЦены,НаМомент,Валюта,?(Курс=0,НаМомент,Курс));
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	Если Цена<>Неопределено Тогда
		// округлим цену
		Если ТипЦен.ОкруглятьВБольшуюСторону Тогда
			Цена = Окр(Цена,ТипЦен.Точность,1);
		Иначе
			Цена = Окр(Цена,ТипЦен.Точность,0);
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Цена;
КонецФункции // орПолучитьЦену()

// Функция возвращает цену автоработы. При отсутствии переопределения возвращает Неопределено
//
// Параметры:
//	ТипЦен       - СправочникСсылка - Тип цены
//	Номенклатура - СправочникСсылка  - Товар
//	НаМомент     - Дата				 - Момент времени
//	Контрагент   - СправочникСсылка	 - Контрагент
//	Валюта       - СправочникСсылка  - Валюта
//	Курс         - Число			 - Курс валюты
//	ХарактеристикаНоменклатуры	- СправочникСсылка  - Характеристика номенклатуры
//	ЕдиницаИзмерения			- СправочникСсылка  - Единица измерения номенклатуры
//	ПодразделениеКомпании		- СправочникСсылка  - Подразделение компании
//
// Возвращаемое значение:
//	Число - Цена товара
//
Функция орПолучитьЦенуАвтоработы(ТипЦен, Авторабота, Модель, ДоговорВзаиморасчетов, Цех, ВидРемонта, Знач НаДату = Неопределено, Валюта = Неопределено, Курс = Неопределено) Экспорт
	
	НормочасПоУмолчанию=обПраво("НормочасПоУмолчанию");
	
	СтруктураЦены = Новый Структура;
	СтруктураЦены.Вставить("Нормочас",     НормочасПоУмолчанию);
	СтруктураЦены.Вставить("НормаВремени", 0);
	СтруктураЦены.Вставить("Валюта",       Справочники.Валюты.ПустаяСсылка());
	СтруктураЦены.Вставить("Цена",         0);
	
	Если обЗначениеНеЗаполнено(ТипЦен) Тогда
		Возврат СтруктураЦены;
	КонецЕсли;
	
	Если ТипЦен.Рассчитывается Тогда
		
		СтруктураЦены = орПолучитьЦенуАвтоработы(ТипЦен.БазовыйТипЦен, Авторабота, Модель, ДоговорВзаиморасчетов, Цех, ВидРемонта, НаДату, Валюта, Курс);
		СтруктураЦены.Цена = СтруктураЦены.Цена + (СтруктураЦены.Цена*ТипЦен.ПроцентСкидкиНаценки/100);
		
	Иначе
		
		Если ТипЗнч(Модель) = Тип("СправочникСсылка.Модели") Тогда
			ВариантКомплектации = Модель;
		Иначе
			ВариантКомплектации = Модель;
			Модель              = Модель.Владелец;
		КонецЕсли;
		
		Если НаДату = Неопределено Тогда
			НаДату = ТекущаяДата();
		КонецЕсли;
		
		//Получим таблицу родителей автоработы с индексом иерархии
		ТаблицаРодителейАвтоработы = Новый ТаблицаЗначений;
		ТаблицаРодителейАвтоработы.Колонки.Добавить("Авторабота", Новый ОписаниеТипов("СправочникСсылка.Автоработы"));
		ТаблицаРодителейАвтоработы.Колонки.Добавить("Модель",     Новый ОписаниеТипов("СправочникСсылка.Модели"));
		ТаблицаРодителейАвтоработы.Колонки.Добавить("Приоритет",  Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 0)));
		
		ТекущийРодитель  = Авторабота;
		ИндексАвтоработы = 0;
		ПустаяРабота     = Справочники.Автоработы.ПустаяСсылка();
		Пока ТекущийРодитель<>ПустаяРабота Цикл
			ТекущаяМодель = Модель;
			ИндексМодели  = 0;
			Пока ТекущаяМодель<>Справочники.Модели.ПустаяСсылка() Цикл
				НоваяСтрока = ТаблицаРодителейАвтоработы.Добавить();
				НоваяСтрока.Авторабота = ТекущийРодитель;
				НоваяСтрока.Модель     = ТекущаяМодель;
				НоваяСтрока.Приоритет  = Формат(ИндексАвтоработы, "ЧН=0; ЧГ=0") + Формат(ИндексМодели, "ЧН=0; ЧГ=0");
				ТекущаяМодель = ТекущаяМодель.Родитель;
				ИндексМодели = ИндексМодели + 1;
			КонецЦикла;
			НоваяСтрока = ТаблицаРодителейАвтоработы.Добавить();
			НоваяСтрока.Авторабота = ТекущийРодитель;
			НоваяСтрока.Модель     = Справочники.Модели.ПустаяСсылка();
			НоваяСтрока.Приоритет  = Формат(ИндексАвтоработы, "ЧН=0; ЧГ=0") + Формат(ИндексМодели, "ЧН=0; ЧГ=0");
			
			ТекущийРодитель = ТекущийРодитель.Родитель;
			ИндексАвтоработы = ИндексАвтоработы + 1;
		КонецЦикла;
		
		ТекущаяМодель = Модель;
		ИндексМодели  = 0;
		Пока ТекущаяМодель<>Справочники.Модели.ПустаяСсылка() Цикл
			НоваяСтрока = ТаблицаРодителейАвтоработы.Добавить();
			НоваяСтрока.Авторабота = ПустаяРабота;
			НоваяСтрока.Модель     = ТекущаяМодель;
			НоваяСтрока.Приоритет  = Формат(ИндексАвтоработы, "ЧН=0; ЧГ=0") + Формат(ИндексМодели, "ЧН=0; ЧГ=0");
			ТекущаяМодель = ТекущаяМодель.Родитель;
			ИндексМодели = ИндексМодели + 1;
		КонецЦикла;
		НоваяСтрока = ТаблицаРодителейАвтоработы.Добавить();
		НоваяСтрока.Авторабота = ПустаяРабота;
		НоваяСтрока.Модель     = Справочники.Модели.ПустаяСсылка();
		НоваяСтрока.Приоритет  = Формат(ИндексАвтоработы, "ЧН=0; ЧГ=0") + Формат(ИндексМодели, "ЧН=0; ЧГ=0");
		
		//Получим таблицу родителей цехов с индексом иерархии
		ТаблицаРодителейЦеха  =Новый ТаблицаЗначений;
		ТаблицаРодителейЦеха.Колонки.Добавить("Цех",Новый ОписаниеТипов("СправочникСсылка.Цеха"));
		ТаблицаРодителейЦеха.Колонки.Добавить("Приоритет",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2)));
		
		ТекущийРодитель = Цех;
		ИндексЦеха      = 0;
		Пока ТекущийРодитель<>Справочники.Цеха.ПустаяСсылка() Цикл
			НоваяСтрока = ТаблицаРодителейЦеха.Добавить();
			НоваяСтрока.Цех       = ТекущийРодитель;
			НоваяСтрока.Приоритет = ИндексЦеха;
			ТекущийРодитель = ТекущийРодитель.Родитель;
			ИндексЦеха = ИндексЦеха+1;
		КонецЦикла;
		НоваяСтрока = ТаблицаРодителейЦеха.Добавить();
		НоваяСтрока.Цех       = Справочники.Цеха.ПустаяСсылка();
		НоваяСтрока.Приоритет = ИндексЦеха;
		
		//Поместим таблицу родителей в менеджер временных таблиц
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ТабРодителей.Авторабота КАК Авторабота,
		|	ТабРодителей.Модель КАК Модель,
		|	ТабРодителей.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ
		|	ТаблицаРодителейАвтоработы
		|ИЗ
		|	&ТабРодителей КАК ТабРодителей
		|;
		|
		|ВЫБРАТЬ
		|	ТабРодителей.Цех КАК Цех,
		|	ТабРодителей.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ
		|	ТаблицаРодителейЦеха
		|ИЗ
		|	&ТабЦехов КАК ТабРодителей
		|;
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЦеныАвтоработСрезПоследних.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ЦеныАвтоработСрезПоследних.ВидРемонта КАК ВидРемонта,
		|	ТаблицаРодителейЦеха.Приоритет КАК ПриоритетЦеха,
		|	ТаблицаРодителейАвтоработы.Приоритет КАК Приоритет,
		|	ЦеныАвтоработСрезПоследних.Нормочас КАК Нормочас,
		|	ЦеныАвтоработСрезПоследних.Валюта КАК Валюта,
		|	ЦеныАвтоработСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныАвторабот.СрезПоследних(
		|			&НаДату,
		|				ТипЦен = &ТипЦен И 
		|				(Авторабота, Модель) В (ВЫБРАТЬ Авторабота, Модель ИЗ ТаблицаРодителейАвтоработы) И 
		|				(ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов ИЛИ 
		|				ДоговорВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.ДоговорыВзаиморасчетов.ПустаяСсылка)) И 
		|				Цех В (ВЫБРАТЬ Цех ИЗ ТаблицаРодителейЦеха) И 
		|				(ВидРемонта = &ВидРемонта ИЛИ 
		|				ВидРемонта = ЗНАЧЕНИЕ(Справочник.ВидыРемонта.ПустаяСсылка))) КАК ЦеныАвтоработСрезПоследних
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаРодителейАвтоработы КАК ТаблицаРодителейАвтоработы
		|ПО
		|	ЦеныАвтоработСрезПоследних.Авторабота = ТаблицаРодителейАвтоработы.Авторабота И 
		|	ЦеныАвтоработСрезПоследних.Модель = ТаблицаРодителейАвтоработы.Модель
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаРодителейЦеха КАК ТаблицаРодителейЦеха
		|ПО
		|	ЦеныАвтоработСрезПоследних.Цех = ТаблицаРодителейЦеха.Цех
		|ГДЕ
		|	(ЦеныАвтоработСрезПоследних.Нормочас <> ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка) ИЛИ 
		|	ЦеныАвтоработСрезПоследних.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаРодителейАвтоработы.Приоритет,
		|	ВЫБОР
		|		КОГДА ЦеныАвтоработСрезПоследних.ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	ТаблицаРодителейЦеха.Приоритет,
		|	ВЫБОР
		|		КОГДА ЦеныАвтоработСрезПоследних.ВидРемонта = &ВидРемонта
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ
		|";
		Запрос.УстановитьПараметр("НаДату",                НаДату);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("ВидРемонта",            ВидРемонта);
		Запрос.УстановитьПараметр("ТабРодителей",          ТаблицаРодителейАвтоработы);
		Запрос.УстановитьПараметр("ТабЦехов",              ТаблицаРодителейЦеха);
		Запрос.УстановитьПараметр("ТипЦен",                ТипЦен);
		
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если НЕ обЗначениеНеЗаполнено(Выборка.Нормочас) Тогда
				СтруктураЦены.Вставить("Нормочас",     Выборка.Нормочас);
				СтруктураЦены.Вставить("НормаВремени", орНормаВремениАвтоработы(Авторабота, ВариантКомплектации));
				СтруктураЦены.Вставить("Цена",         Выборка.Нормочас.Цена);
				Если Валюта<>Неопределено И Курс<>Неопределено Тогда
					СтруктураЦены.Вставить("Цена",     обПересчет(СтруктураЦены.Цена, Выборка.Нормочас.Валюта, НаДату, Валюта,Курс));
				КонецЕсли; 
			ИначеЕсли НЕ обЗначениеНеЗаполнено(Выборка.Валюта) Тогда
				СтруктураЦены.Вставить("Нормочас",     Справочники.Нормочасы.Рубль);
				СтруктураЦены.Вставить("НормаВремени", 1);
				СтруктураЦены.Вставить("Валюта",       Выборка.Валюта);
				Если Валюта = Неопределено ИЛИ Курс = Неопределено Тогда
					СтруктураЦены.Вставить("Цена", Выборка.Цена);
				Иначе
					СтруктураЦены.Вставить("Цена", обПересчет(Выборка.Цена, Выборка.Валюта, НаДату, Валюта, Курс));
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтруктураЦены.Вставить("Нормочас",     НормочасПоУмолчанию);
			Если НормочасПоУмолчанию = Справочники.Нормочасы.Рубль Тогда
				СтруктураЦены.Вставить("НормаВремени", 1);
			Иначе
				СтруктураЦены.Вставить("НормаВремени", орНормаВремениАвтоработы(Авторабота, ВариантКомплектации));
			КонецЕсли;
			СтруктураЦены.Вставить("Цена",         НормочасПоУмолчанию.Цена);
			Если Валюта<>Неопределено И Курс<>Неопределено Тогда
				СтруктураЦены.Вставить("Цена", обПересчет(СтруктураЦены.Цена, НормочасПоУмолчанию.Валюта, НаДату, Валюта, Курс));
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	// Округлим цену.
	Если ТипЦен.ОкруглятьВБольшуюСторону Тогда
		СтруктураЦены.Цена = Окр(СтруктураЦены.Цена, ТипЦен.Точность, 1);
	Иначе
		СтруктураЦены.Цена = Окр(СтруктураЦены.Цена, ТипЦен.Точность, 0);
	КонецЕсли;
	
	Возврат СтруктураЦены;
	
КонецФункции // орПолучитьЦенуАвтоработы() 

// Получение нормы времени автоработы согласно класса автомобиля
Функция орНормаВремениАвтоработы(Авторабота, ВариантКомплектации) Экспорт
	
	НормаВремени = 0;
	
	Если НЕ ЗначениеЗаполнено(Авторабота) ИЛИ Авторабота.ЭтоГруппа Тогда
		Возврат НормаВремени;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
		
		ТаблицаПриоритетов = Новый ТаблицаЗначений;
		ТаблицаПриоритетов.Колонки.Добавить("Модель",    Новый ОписаниеТипов("СправочникСсылка.Модели"));
		ТаблицаПриоритетов.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		Если ТипЗнч(ВариантКомплектации) = Тип("СправочникСсылка.ВариантыКомплектации") Тогда
			Модель = ВариантКомплектации.Владелец;
			Запрос.УстановитьПараметр("Модель",              Модель);
			Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
		Иначе
			Модель = ВариантКомплектации.Родитель;
			Запрос.УстановитьПараметр("Модель",              ВариантКомплектации);
			Запрос.УстановитьПараметр("ВариантКомплектации", Справочники.ВариантыКомплектации.ПустаяСсылка());
		КонецЕсли;
		
		Приоритет = 1;
		Пока ЗначениеЗаполнено(Модель) Цикл
			НоваяСтрока = ТаблицаПриоритетов.Добавить();
			НоваяСтрока.Модель = Модель;
			НоваяСтрока.Приоритет = Приоритет;
			Приоритет = Приоритет + 1;
			Модель    = Модель.Родитель;
		КонецЦикла;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаПриоритетов.Модель КАК Модель,
		|	ТаблицаПриоритетов.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ
		|	ТаблицаПриоритетов
		|ИЗ
		|	&ТаблицаПриоритетов КАК ТаблицаПриоритетов
		|;
		|
		|ВЫБРАТЬ
		|	0 КАК Приоритет,
		|	НормыВремени.ВремяВыполнения КАК ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы.НормыВремени КАК НормыВремени
		|ГДЕ
		|	НормыВремени.Ссылка = &Авторабота И 
		|	НормыВремени.Модель              = &Модель И 
		|	НормыВремени.ВариантКомплектации = &ВариантКомплектации
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаПриоритетов.Приоритет,
		|	НормыВремени.ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы.НормыВремени КАК НормыВремени
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаПриоритетов КАК ТаблицаПриоритетов
		|ПО
		|	НормыВремени.Ссылка              = &Авторабота И 
		|	НормыВремени.Модель              = ТаблицаПриоритетов.Модель И 
		|	НормыВремени.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
		|ГДЕ
		|	НормыВремени.Ссылка              = &Авторабота И 
		|	НормыВремени.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	" + Формат(Приоритет, "ЧГ=0") + ",
		|	Автоработы.ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы КАК Автоработы
		|ГДЕ
		|	Автоработы.Ссылка = &Авторабота
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		
		Запрос.УстановитьПараметр("ТаблицаПриоритетов",  ТаблицаПриоритетов);
	Иначе
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Автоработы.ВремяВыполнения КАК ВремяВыполнения
		|ИЗ
		|	Справочник.Автоработы КАК Автоработы
		|ГДЕ
		|	Автоработы.Ссылка = &Авторабота";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Авторабота", Авторабота);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		НормаВремени = Выборка.ВремяВыполнения;
	КонецЕсли;
	
	Возврат НормаВремени; 
КонецФункции // орНормаВремениАвтоработы() 

// Функция получает сумму табличной части документа без скидки
//
// Параметры:
//	ЭтотОбъект	– ДокументОбъект – Документ, сумму которого необходимо получить
//  Товары		– ТабличнаяЧасть – Табличная часть документа, сумму по которой необходимо получить
//
// Возвращаемое значение:
//   Число		– Сумма документа без скидки
//
Функция орПолучитьСуммуДокументаБезСкидки(ЭтотОбъект,Товары=Неопределено, МассивСтрокСВытеснением=Неопределено) Экспорт
	Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаряд") Тогда
		Если Товары="Работы" Тогда
			
			ЕстьВытеснение = Ложь;
			Если ТипЗнч(МассивСтрокСВытеснением) = Тип("Массив") Тогда
				ЕстьВытеснение = Истина;
			КонецЕсли;
			
			ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
			
			СуммаДокументаБезСкидки = 0;
			Если ЕстьВытеснение Тогда
				КоличествоСтрок = ЭтотОбъект.Работы.Количество()-1;
				Для Инд = 0 По КоличествоСтрок Цикл
					Если НЕ МассивСтрокСВытеснением.Найти(Инд) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					ТекСтрока = ЭтотОбъект.Работы[Инд];
					Если ЦенаВключаетНДС Тогда
						// Получим сумму документу без учета НДС и скидки
						СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ТекСтрока.Сумма;
					Иначе
						//Если цена не включает НДС, то получим суммарную НДС по всем строкам
						СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ТекСтрока.Сумма + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
					КонецЕсли;
				КонецЦикла;
			Иначе
				СуммаДокументаБезСкидки = ЭтотОбъект.Работы.Итог("Сумма");
				
				Если НЕ ЦенаВключаетНДС Тогда
					Для каждого ТекСтрока Из ЭтотОбъект.Работы Цикл
						СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			Возврат СуммаДокументаБезСкидки;
			
		КонецЕсли; 
		
	ИначеЕсли Товары="Автомобиль" Тогда
		СуммаДокументаБезСкидки = ЭтотОбъект.ЦенаАвтомобиля;
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
		СебестоимостьАвтомобиля = 0;
		Попытка
			СебестоимостьАвтомобиля = ЭтотОбъект.СебестоимостьАвтомобиля;
		Исключение
		КонецПопытки;
		Если НЕ ЦенаВключаетНДС Тогда
			СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ((СуммаДокументаБезСкидки-СебестоимостьАвтомобиля)*ЭтотОбъект.СтавкаНДСНаАвтомобиль.Ставка)/100;
		КонецЕсли;
		Если ЭтотОбъект.Метаданные().ТабличныеЧасти.Найти("Опции")<>Неопределено Тогда
			СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ЭтотОбъект.Опции.Итог("Сумма");
			Если НЕ ЦенаВключаетНДС Тогда
				Для каждого ТекСтрока Из ЭтотОбъект.Опции Цикл
					СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли; 
		Возврат СуммаДокументаБезСкидки;
		
	ИначеЕсли Товары="Автомобили" Тогда
		СуммаДокументаБезСкидки = ЭтотОбъект.Автомобили.Итог("Сумма");
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
		Если НЕ ЦенаВключаетНДС Тогда
			Для каждого ТекСтрока Из ЭтотОбъект.Автомобили Цикл
				СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
			КонецЦикла;
		КонецЕсли;
		Возврат СуммаДокументаБезСкидки;
	КонецЕсли; 
	
	Возврат Неопределено;
КонецФункции // орПолучитьСуммуДокументаБезСкидки()

// Функция перерасчета абсолютной скидки или выбора максимальной автоматической скидки
//
// Параметры:
//	ЭтотОбъект - ДокументОбъект - Документ, для которого производится поиск скидки
//  Товары     - ТабличнаяЧасть - Табличная часть документа товаров.
//
// Возвращаемое значение:
//	СправочникСсылка.ТипыСкидок - ссылка на справочник типов скидок
//
Функция орПерерасчетСкидки(ЭтотОбъект,Товары=Неопределено) Экспорт
	Возврат Неопределено;
КонецФункции // орПерерасчетСкидки()

// Функция поиска максимальной скидки для документа
//
// Параметры:
//	ЭтотОбъект - ДокументОбъект - Документ, для которого производится поиск скидки
//  Товары     - ТабличнаяЧасть - Табличная часть документа товаров.
//				Если параметр равен "НЕОПРЕДЕЛЕНО", то сумма документа берется
//				из табличной части "ТОВАРЫ" или реквизита "СуммаДокумента"
//
// Возвращаемое значение:
//	СправочникСсылка.ТипыСкидок - ссылка на справочник типов скидок
//
Функция орПолучитьМаксимальнуюСкидку(ЭтотОбъект,Товары=Неопределено) Экспорт
	Возврат Неопределено;
КонецФункции // орПолучитьМаксимальнуюСкидку()

// Функция применяет полученные параметры скидки шапки к указанной табличной части документа
//
// Параметры:
//	ЭтотОбъект   - ДокументОбъект - Документ, для которого производится поиск скидки
//  ИмяТаблицы   – Строка         - Имя табличной части, к которой необходимо применить скидку
//  ЭтаФорма     - Форма          - Форма документа, для которого производится поиск скидки
//	ДопПараметры - Произвольный   - Дополнительные параметры, которые могут использоваться при расчете табличной части
//
// Возвращаемое значение:
//	Неопределено, БУЛЕВО – Признак успешного завершения действия
//
Функция орПрименитьСкидкуШапкиКТабличнойЧасти(ЭтотОбъект, ИмяТаблицы, ЭтаФорма=Неопределено, ДопПараметры=Неопределено) Экспорт
	Возврат Неопределено;
КонецФункции // орПрименитьСкидкуШапкиКТабличнойЧасти()

// Функция обработки изменения реквизитов документов (отраслевая)
//
// Параметры:
//	ЭтотОбъект	 – ДокументОбъект	    – Документ, реквизит которого обрабатывается.
//  Имя			 – Строка			    – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	 – Форма				– Ссылка на форму документа. Если значение неопределено,
//				 производится программная обработка реквизитов.
//  ТекСтрока	 – СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//				 Имеет смысл только для табличных частей документов.
//  ДопПараметры – Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция орОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Возврат Неопределено;
КонецФункции // орОбработкаРеквизита()

// Функция получения описания типов, содержащего типы сделок покупателя/поставщика
//
// Параметры:
//  ТипыСделокПокупателя - Булево - Признак получения типов сделок покупателя или поставщика
//
// Возвращаемое значение:
//   ОписаниеТипов - Типы сделок покупателя или поставщика
//
Функция орПолучитьТипыСделок(ТипыСделокПокупателя) Экспорт
	Если ТипыСделокПокупателя Тогда
		ТипыСделок=Новый ОписаниеТипов(
		"ДокументСсылка.ЗаказПокупателя,
		|ДокументСсылка.СчетНаОплату,
		|ДокументСсылка.РеализацияТоваров,
		|ДокументСсылка.ЧекНаОплату,
		|ДокументСсылка.ЗаказНаряд,
		|ДокументСсылка.ЗаявкаНаРемонт,
		|ДокументСсылка.АктРазногласий,
		|ДокументСсылка.ОтчетКомиссионера,
		|ДокументСсылка.ВозвратОтПокупателя,
		|ДокументСсылка.ЗаказНаАвтомобиль,
		|ДокументСсылка.СчетНаОплатуЗаАвтомобили,
		|ДокументСсылка.РеализацияАвтомобилей,
		|ДокументСсылка.КорректировкаЗаказаПокупателя,
		|ДокументСсылка.ЗаменаВЗаказеПокупателя,
		|ДокументСсылка.ОтчетКомиссионераЗаАвтомобили,
		|ДокументСсылка.ВозвратОтПокупателяАвтомобилей"
		);
	Иначе
		ТипыСделок=Новый ОписаниеТипов(
		"ДокументСсылка.ЗаказПоставщику,
		|ДокументСсылка.ПоступлениеТоваров,
		|ДокументСсылка.ПоступлениеДопРасходов,
		|ДокументСсылка.СчетОтПоставщика,
		|ДокументСсылка.АвансовыйОтчет,
		|ДокументСсылка.ОтчетКомитенту,
		|ДокументСсылка.ВозвратПоставщику,
		|ДокументСсылка.СчетОтПоставщикаЗаАвтомобили,
		|ДокументСсылка.ПоступлениеАвтомобилей,
		|ДокументСсылка.ЗаказПоставщикуНаАвтомобиль,
		|ДокументСсылка.КорректировкаЗаказаПоставщику,
		|ДокументСсылка.ОтчетКомитентуЗаАвтомобили,
		|ДокументСсылка.ВозвратПоставщикуАвтомобилей"
		);
	КонецЕсли; 
	Возврат ТипыСделок;
КонецФункции // орПолучитьТипыСделок()

// Функция возвращает массив партионных регистров
// состав этих регистров может отличаться в различных конфигурациях
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Массив	- Массив партионных регистров
//
Функция орПолучитьПартионныеРегистры() Экспорт
	МассивПартионныхРегистров=Новый Массив;
	МассивПартионныхРегистров.Добавить("ПартииТоваровКомпании");
	МассивПартионныхРегистров.Добавить("ПартииТоваровОтданные");
	МассивПартионныхРегистров.Добавить("ДоходыИРасходы");
	МассивПартионныхРегистров.Добавить("Продажи");
	МассивПартионныхРегистров.Добавить("РеализованныеТовары");
	МассивПартионныхРегистров.Добавить("ТоварыВПроизводстве");
	МассивПартионныхРегистров.Добавить("КомплектацияАвтомобилей");
	
	Возврат МассивПартионныхРегистров;
КонецФункции // орПолучитьПартионныеРегистры()

// функция проверяет входит ли документ в список "неоднозначных" партионных документов.
//
// Параметры:
//	МетаданныеДок	- ОбъектМетаданных	- Объект описания метаданных документа
//
// Возвращаемое значение:
//	Булево	- Признак проведения документа
//
Функция орДокументНеВсегдаПроводитьсяПоПартиям(МетаданныеДок) Экспорт
	Исключения = Новый Массив;
	Исключения.Добавить(Метаданные.Документы.Переоценка);
	Исключения.Добавить(Метаданные.Документы.Инвентаризация);
	Исключения.Добавить(Метаданные.Документы.ПоступлениеДопРасходов);
	Если Исключения.Найти(МетаданныеДок) <> Неопределено Тогда		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции // орДокументНеВсегдаПроводитьсяПоПартиям()

// функция рассчитывает дополнительные расходы при проведении по регистру "Партии товаров компании"
//
// Параметры:
//	ТаблицаТоваровДокумента	- ТабличнаяЧасть - Табличная часть документа
//	Ссылка					- ДокументСсылка - Документ
//
//	Возвращаемое значение:
//	Число	- сумма доп. расходов на себестоимость товаров
//
Функция орРасчетДополнительныхРасходов(ТаблицаТоваровДокумента, Ссылка, МассивОшибок=Неопределено, Режим=1) Экспорт
	Если МассивОшибок=Неопределено Тогда
		МассивОшибок = Новый Массив;	
	КонецЕсли;
	
	ВсегоДопРасходы = 0;
	
	// Скопируем таблицу товаров
	ВремТаблица = ТаблицаТоваровДокумента.Скопировать();
	//если необходимо очистим
	Если Режим = 2 Тогда
		ВремТаблица.Очистить();
	КонецЕсли;
	
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоСумме ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоСумме,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоСумме ТОГДА ДокументТовары.СуммаНДС ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоСуммеНДС,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоКоличеству ТОГДА ДокументТовары.СуммаНДС ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоКоличествуНДС,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоКоличеству ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоКоличеству,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоВесу ТОГДА ДокументТовары.СуммаНДС ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоВесуНДС,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоВесу ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоВесу,
	|	ДокументТовары.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ."+Ссылка.Метаданные().Имя+".Товары КАК ДокументТовары
	|ГДЕ
	|	  ДокументТовары.Ссылка=&Ссылка
	|	И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов<>ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.НаДоходыИРасходы)
	|	И ДокументТовары.Номенклатура.ВидНоменклатуры=ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.СтавкаНДС
	|");
	Запрос.УстановитьПараметр("Ссылка",       Ссылка);
	Запрос.УстановитьПараметр("ПоСумме",      Перечисления.СпособыРаспределенияДопРасходов.ПоСумме);
	Запрос.УстановитьПараметр("ПоКоличеству", Перечисления.СпособыРаспределенияДопРасходов.ПоКоличеству);
	Запрос.УстановитьПараметр("ПоВесу",       Перечисления.СпособыРаспределенияДопРасходов.ПоВесу);	
	РезультатДопРасходы = Запрос.Выполнить();
	
	Если РезультатДопРасходы.Пустой() Тогда
		ТаблицаТоваровДокумента=ВремТаблица;
		Возврат ВсегоДопРасходы;
	КонецЕсли;
	
	ВыборкаДопРасходы = РезультатДопРасходы.Выбрать();
	
	// Вычислм общую сумму
	ИтогДокументКоличество = ТаблицаТоваровДокумента.Итог("Количество");
	ИтогДокументСумма      = ТаблицаТоваровДокумента.Итог("СуммаВсего"); 
	ИтогДокументВес        = ТаблицаТоваровДокумента.Итог("Вес");
	
	Пока ВыборкаДопРасходы.Следующий() Цикл
		ДопРасходыПоКоличеству =  ?(ВыборкаДопРасходы.ПоКоличеству=NULL,0,ВыборкаДопРасходы.ПоКоличеству);
		ДопРасходыПоСумме      =  ?(ВыборкаДопРасходы.ПоСумме=NULL,0,ВыборкаДопРасходы.ПоСумме);
		ДопРасходыПоВесу       =  ?(ВыборкаДопРасходы.ПоВесу=NULL,0,ВыборкаДопРасходы.ПоВесу);
		
		ВсегоДопРасходы = ДопРасходыПоКоличеству + ДопРасходыПоСумме + ДопРасходыПоВесу;
		Если ВсегоДопРасходы=0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИтогДопРасходы    = ВсегоДопРасходы;
		ИтогДопРасходыНДС = ?(ВыборкаДопРасходы.ПоКоличествуНДС=NULL,0,ВыборкаДопРасходы.ПоКоличествуНДС)+?(ВыборкаДопРасходы.ПоСуммеНДС=NULL,0,ВыборкаДопРасходы.ПоСуммеНДС)+?(ВыборкаДопРасходы.ПоВесуНДС=NULL,0,ВыборкаДопРасходы.ПоВесуНДС);
		
		// Вычислим ситуацию, когда
		Если (ИтогДокументКоличество=0) И (ДопРасходыПоКоличеству<>0) Тогда
			МассивОшибок.Добавить("Количество прихода равно нулю. Распределение суммы дополнительных расходов на себестоимость прихода невозможно.");
		КонецЕсли;
		
		Если (ИтогДокументСумма=0) И (ДопРасходыПоСумме<>0) Тогда
			МассивОшибок.Добавить("Себестоимость прихода равна нулю. Распределение суммы дополнительных расходов на себестоимость прихода невозможно.");	
		КонецЕсли;	
		
		Если (ИтогДокументВес=0) И (ДопРасходыПоВесу<>0) Тогда
			МассивОшибок.Добавить("Вес прихода равен нулю. Распределение суммы дополнительных расходов на себестоимость прихода невозможно.");
		КонецЕсли;	
		
		Если МассивОшибок.Количество()<>0 Тогда
			Продолжить;
		КонецЕсли;
		
		// распредляем по товарам
		Для Каждого СтрокаТоварИсх Из ТаблицаТоваровДокумента Цикл
			СтрокаТовар = ВремТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовар,СтрокаТоварИсх); // всетаки падало при отсутствии колонок
			
			// Обнулим значения
			СтрокаТовар.Количество = 0;
			СтрокаТовар.Сумма      = 0;
			СтрокаТовар.СуммаВсего = 0;
			СтрокаТовар.СуммаНДС   = 0;
			
			СтрокаТовар.СтавкаНДС = ВыборкаДопРасходы.СтавкаНДС;
			СуммаДопРасходов=0; СуммаНДСДопРасходов=0;
			
			КоэфРаспределения    = ?(ИтогДокументКоличество=0, 0, СтрокаТоварИсх.Количество/ИтогДокументКоличество);
			СуммаДопРасходовК    = ВыборкаДопРасходы.ПоКоличеству    * КоэфРаспределения;
			СуммаНДСДопРасходовК = ВыборкаДопРасходы.ПоКоличествуНДС * КоэфРаспределения;
			
			КоэфРаспределения    = ?(ИтогДокументСумма=0, 0, СтрокаТоварИсх.СуммаВсего/ИтогДокументСумма);
			СуммаДопРасходовС    = ВыборкаДопРасходы.ПоСумме    * КоэфРаспределения;
			СуммаНДСДопРасходовС = ВыборкаДопРасходы.ПоСуммеНДС * КоэфРаспределения;
			
			КоэфРаспределения    = ?(ИтогДокументВес=0, 0, СтрокаТоварИсх.Вес/ИтогДокументВес);
			СуммаДопРасходовВ    = ВыборкаДопРасходы.ПоВесу    * КоэфРаспределения;
			СуммаНДСДопРасходовВ = ВыборкаДопРасходы.ПоВесуНДС * КоэфРаспределения;
			
			СтрокаТовар.СуммаВсего = СуммаДопРасходовК + СуммаДопРасходовС + СуммаДопРасходовВ;
			СтрокаТовар.СуммаНДС   = СуммаНДСДопРасходовК + СуммаНДСДопРасходовС + СуммаНДСДопРасходовВ;
			
			ИтогДопРасходы    = ИтогДопРасходы - (СтрокаТовар.СуммаВсего);
			ИтогДопРасходыНДС = ИтогДопРасходыНДС - (СтрокаТовар.СуммаНДС);
		КонецЦикла;
		
		// если осталась сумма от округления то закинем ее на последний
		Если ИтогДопРасходы<>0 ИЛИ ИтогДопРасходыНДС<>0 Тогда
			Попытка
				СтрокаТовар.СуммаВсего = СтрокаТовар.СуммаВсего + ИтогДопРасходы;
				СтрокаТовар.СуммаНДС   = СтрокаТовар.СуммаНДС + ИтогДопРасходыНДС;
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	//
	ТаблицаТоваровДокумента=ВремТаблица;
	Возврат ВсегоДопРасходы;
КонецФункции // орРасчетДополнительныхРасходов()

// Функция в зависимости от установленных подразделений и склада получает тип цен для документа
//
// Параметры:
//	ЭтотОбъект	 – ДокументОбъект	    – Документ, который обрабатывается.
//
// Возвращаемое значение:
//	СправочникСсылка - Тип цен. Если в документе "тип цен" не предусмотрен, возвращает "Неопределено"
//
Функция орПолучитьТипЦенДокумента(ЭтотОбъект) Экспорт
	Возврат Неопределено; 
КонецФункции // орПолучитьТипЦенДокумента()

// Функция отрабатывает изменение характеристики
//
// Параметры:
//	ЭтотОбъект	 – ДокументОбъект	    – Документ, который обрабатывается.
//  Имя			 – Строка			    – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ТекСтрока	 – СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//  ЭтаФорма	 – Форма				– Ссылка на форму документа.
//  ДопПараметры – Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Неопределено.
//
Функция орХарактеристикаНоменклатурыПриИзменении(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) Экспорт
	Возврат Неопределено;
КонецФункции // орХарактеристикаНоменклатурыПриИзменении()

// Функция определяет надо ли заблокировать документ если идет сеанс обмена
// Функция отрабатывает изменение характеристики
//
// Параметры:
//	ЭтотОбъект	    – ДокументОбъект           – Документ, который обрабатывается.
//	Отказ           - Булево		 	       - Признак отказа от записи
//	РежимЗаписи     - РежимЗаписиДокумента     - Режим записи документа
//	РежимПроведения - РежимПроведенияДокумента - Режим проведения документа
//
// Возвращаемое значение:
//   Неопределено.
//
Функция орБлокироватьДокументПриОбмене(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	Возврат Неопределено;
КонецФункции // орБлокироватьДокументПриОбмене()

// Функция получает дату запрета редактирования документа
//
// Параметры:
//	ЭтотОбъект	    – ДокументОбъект           – Документ, который обрабатывается.
//
// Возвращаемое значение:
//	Дата	- Дата запрета редактирования
//
Функция орПолучитьДатуЗапретаРедактирования(ЭтотОбъект) Экспорт
	ГрупповаяОбработкаДокументов=Ложь;
	Если НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ГрупповаяОбработкаДокументов",ГрупповаяОбработкаДокументов) Тогда
		ГрупповаяОбработкаДокументов=Ложь;
	КонецЕсли;
	Если ГрупповаяОбработкаДокументов=Истина Тогда
		Возврат Дата('00010101');
	КонецЕсли; 
	
	Если обПраво("РедактированиеДокументовВЗакрытомПериоде",ЭтотОбъект.Права,,ЭтотОбъект) Тогда
		Возврат Дата('00010101');
	КонецЕсли; 
	
	Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаряд") Тогда
		//Если заказ-наряд еще не закрыт, то он НЕ блокируется не зависимо от даты запрета редактирования
		Если ЭтотОбъект.Ссылка.Состояние<>Справочники.ВидыСостоянийЗаказНарядов.Закрыт И ЭтотОбъект.Состояние<>Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			Возврат Дата('00010101');
		КонецЕсли;
		//Исключения для прочих документов через расширение прав доступа "Редактирование документов в закрытом периоде"
		//ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаявкаНаРемонт") Тогда
		//	Возврат Дата('00010101');
	ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаАвтомобиль") Тогда
		//Если заказ еще не обслужен, то он НЕ блокируется не зависимо от даты запрета редактирования
		ОстаткиЗаказаНаАвтомобиль=РегистрыНакопления.ЗаказыАвтомобилей.Остатки(,Новый Структура("Заказ",ЭтотОбъект.Ссылка));
		Если ОстаткиЗаказаНаАвтомобиль.Итог("Количество")<>0 ИЛИ 
			ОстаткиЗаказаНаАвтомобиль.Итог("Резерв")<>0 Тогда
			Возврат Дата('00010101');
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат обПолучитьПраваИНастройкиПользователя(ЭтотОбъект.Организация,"ДатаЗапретаРедактирования",ЭтотОбъект);
КонецФункции // орПолучитьДатуЗапретаРедактирования()

// Функция получает интервал запрета редактирования документа
//
// Параметры:
//	ЭтотОбъект	    – ДокументОбъект – Документ, который обрабатывается.
//	ТекущееВремя    – Дата           – Текущее время.
//
// Возвращаемое значение:
//	СтруктураИнтервалаЗапрета	- Структура границ запрета редактирования
//
Функция орПолучитьИнтервалЗапретаРедактирования(ЭтотОбъект, ТекущееВремя=Неопределено) Экспорт
	
	ТекущееВремя = ?(ТекущееВремя=Неопределено, ТекущаяДата(), ТекущееВремя);
	Если ТипЗнч(ЭтотОбъект)=Тип("ДокументСсылка.ЗаказНаряд") ИЛИ ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаряд") Тогда
		Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка.ДатаЗакрытия) И ЗначениеЗаполнено(ЭтотОбъект.ДатаЗакрытия) Тогда
			ДатаДокумента = Мин(ЭтотОбъект.Ссылка.ДатаЗакрытия, ЭтотОбъект.ДатаЗакрытия);
		ИначеЕсли ЗначениеЗаполнено(ЭтотОбъект.Ссылка.ДатаЗакрытия) Тогда
			ДатаДокумента = ЭтотОбъект.Ссылка.ДатаЗакрытия;
		ИначеЕсли ЗначениеЗаполнено(ЭтотОбъект.ДатаЗакрытия) Тогда
			ДатаДокумента = ЭтотОбъект.ДатаЗакрытия;
		Иначе
			ДатаДокумента=ЭтотОбъект.Дата;
		КонецЕсли;
	Иначе
		ДатаДокумента=ЭтотОбъект.Дата;
	КонецЕсли; 
	СтруктураИнтервалаЗапрета = Новый Структура("НижнГраница, ВерхГраница", ДатаДокумента, ДатаДокумента);
	
	ГрупповаяОбработкаДокументов=Ложь;
	Если НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ГрупповаяОбработкаДокументов",ГрупповаяОбработкаДокументов) Тогда
		ГрупповаяОбработкаДокументов=Ложь;
	КонецЕсли;
	Если ГрупповаяОбработкаДокументов=Истина Тогда
		Возврат СтруктураИнтервалаЗапрета;
	КонецЕсли; 
	
	Если обПраво("РедактированиеДокументовВЗакрытомПериоде",ЭтотОбъект.Права,,ЭтотОбъект) Тогда
		Возврат СтруктураИнтервалаЗапрета;
	КонецЕсли; 
	
	Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаряд") Тогда
		//Если заказ-наряд еще не закрыт, то он НЕ блокируется не зависимо от даты запрета редактирования
		Если ЭтотОбъект.Ссылка.Состояние<>Справочники.ВидыСостоянийЗаказНарядов.Закрыт И ЭтотОбъект.Состояние<>Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			Возврат СтруктураИнтервалаЗапрета;
		КонецЕсли;
		//Исключения для прочих документов через расширение прав доступа "Редактирование документов в закрытом периоде"
		//ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаявкаНаРемонт") Тогда
		//	Возврат СтруктураИнтервалаЗапрета;
	ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаАвтомобиль") Тогда
		//Если заказ еще не обслужен, то он НЕ блокируется не зависимо от интервала запрета редактирования
		ОстаткиЗаказаНаАвтомобиль=РегистрыНакопления.ЗаказыАвтомобилей.Остатки(,Новый Структура("Заказ",ЭтотОбъект.Ссылка));
		Если ОстаткиЗаказаНаАвтомобиль.Итог("Количество")<>0 ИЛИ 
			ОстаткиЗаказаНаАвтомобиль.Итог("Резерв")<>0 Тогда
			Возврат СтруктураИнтервалаЗапрета;
		КонецЕсли; 
	КонецЕсли; 
	
	//bizteh
		Если ТипЗнч(ЭтотОбъект) = Тип("ДокументОбъект.ЭМ_КумулятивнаяМаржа") И НЕ обПраво("Директор") Тогда
			СтруктураИнтервалаЗапрета.Вставить("НижнГраница", НачалоДня(ТекущееВремя));
			СтруктураИнтервалаЗапрета.Вставить("ВерхГраница", КонецДня(ТекущееВремя));
			Возврат СтруктураИнтервалаЗапрета;
		КонецЕсли; 
	//bizteh
	
	Если обПраво("РедактироватьВЗаданномИнтервале", ЭтотОбъект.Права,,ЭтотОбъект) Тогда
		СтруктураИнтервалаЗапрета.Вставить("НижнГраница", НачалоДня(ТекущееВремя-обПраво("ДнейМеньше",ЭтотОбъект.Права,,ЭтотОбъект)*86400));
		СтруктураИнтервалаЗапрета.Вставить("ВерхГраница", КонецДня(ТекущееВремя+обПраво("ДнейБольше",ЭтотОбъект.Права,,ЭтотОбъект)*86400));
	КонецЕсли;
	
	Возврат СтруктураИнтервалаЗапрета;
	
КонецФункции // орПолучитьИнтервалЗапретаРедактирования()

#Если Клиент Тогда
	// Функция устанавливает текущий элемент формы
	//
	// Параметры:
	//  ЭтаФорма	 – Форма				– Ссылка на форму документа.
	//
	// Возвращаемое значение:
	//	Булево - установка текущего элемента формы	
	//
	Функция орУстановитьТекущийЭлементФормы(ЭтаФорма) Экспорт
		Возврат Ложь;
	КонецФункции // орУстановитьТекущийЭлементФормы()
	
	// Функция корректировки заполнения по умолчанию
	//
	// Параметры:
	//  ЭтаФорма	 – Форма				– Ссылка на форму документа.
	//
	// Возвращаемое значение:
	//	Неопределено
	//
	Процедура орКорректировкаЗаполнения(ЭтаФорма) Экспорт
		Возврат;
	КонецПроцедуры // орКорректировкаЗаполнения()
#КонецЕсли

// Функция изменения базового количества в документах
//
// Параметры:
//	ЭтотОбъект	 – ДокументОбъект	    – Документ, который обрабатывается.
//  ТекСтрока	 – СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//  ЭтаФорма	 – Форма				– Ссылка на форму документа.
//  ДопПараметры – Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Неопределено.
//
Функция орКоличествоБазовоеПриИзменении(ЭтотОбъект,ТекСтрока,ЭтаФорма,ДопПараметры) Экспорт
	Возврат Неопределено;
КонецФункции // орКоличествоБазовоеПриИзменении()

// Функция удаляет ненужную регистрацию изменений
//
// Параметры:
//	ЭтотОбъект	 – ДокументОбъект	    – Документ, который обрабатывается.
//
// Возвращаемое значение:
//   Неопределено.
//
Процедура орРегистрацияИзменений(ЭтотОбъект) Экспорт
	// зарезервировано на будущее	
КонецПроцедуры // орРегистрацияИзменений()

// Функция заполнения цветом строк таблицы товаров
//
// Параметры:
//  ЭтаФорма	      – Форма		      – Ссылка на форму документа.
//	ТабличноеПоле     - ТабличнаяЧасть    - Табличное поле документа
//	Элемент			  - ЭлементУправления - Элемент управления табличного поля
//	ОформлениеСтроки  - ОформлениеСтроки  - Оформление строки табличного поля
//	ДанныеСтроки      - ТекущиеДанные	  - Данные строки табличного поля
//	ПраваПользователя - Неопределено      - Право доступа	
//
// Возвращаемое значение:
//   Неопределено.
//
Функция орТоварыПриВыводеСтроки(ЭтаФорма,ТабличноеПоле,Элемент,ОформлениеСтроки,ДанныеСтроки,ПраваПользователя) Экспорт
	Возврат Неопределено;
КонецФункции // орТоварыПриВыводеСтроки()

// Функция возвращает информацию о текущем программном продукте. 
// Используется блоком поддержки.
// Обязательно необходимо указать свой код продукта, адрес техподдержки этого продукта, представление.
//
// КОДЫ:
//  330: 1С-Рарус:CRM Управление продажами 2.0 + Альфа-Авто:Автосалон + Автосервис + Автозапчасти, редакция 3
//  326: 1С-Рарус:CRM Управление продажами, редакция 2
//  348: 1С-Рарус:GAAP IV
//  415: 1С-Рарус:SMS и Факс Коммуникатор, редакция 1
//  350: 1С-Рарус:Автотранспорт, редакция 5, профессиональный вариант
//  351: 1С-Рарус:Автотранспорт, редакция 5, стандартный вариант, сетевая поставка
//  352: 1С-Рарус:Автохозяйство, редакция 1
//  381: 1С-Рарус:Администратор, редакция 1
//  353: 1С-Рарус:АЗК+Нефтебаза. Редакция 1
//  349: 1С-Рарус:Бэк-офис, редакция 4
//  354: 1С-Рарус:Бюджетное планирование, редакция 1
//  355: 1С-Рарус:Депозитарий, редакция 1
//  428: 1С-Рарус:Депозитарий, редакция 2
//  383: 1С-Рарус:Динамические отчеты. Интерфейс обмена с Контур Стандарт, редакция 1
//  367: 1С-Рарус:Магазин бытовой техники и средств связи, редакция 2
//  419: 1С-Рарус:Магазин одежды и спорт товаров, редакция 2.5
//  418: 1С-Рарус:Магазин парфюмерии и косметики, редакция 2.5
//  417: 1С-Рарус:Магазин строительных и отделочных материалов, редакция 2.5
//  364: 1С-Рарус:Магазин, редакция 1
//  366: 1С-Рарус:Магазин, редакция 2
//  365: 1С-Рарус:Магазин, украинская версия 1.0
//  378: 1С-Рарус:Мебельное предприятие, редакция 2
//  421: 1С-Рарус:Общепит, редакция  8, профессиональный вариант
//  427: 1С-Рарус:Торговый комплекс. Продовольственные товары, редакция 8
//  305: 1С-Рарус:Управление рестораном (бэк-офис), редакция 1
//  373: Альфа-Авто:Автозапчасти + Автосервис, редакция 3
//  375: Альфа-Авто:Автозапчасти + Автошины, редакция 3
//  377: Альфа-Авто:Автосалон + Автосервис + Автозапчасти, редакция 3 редакция 3
//  376: Альфа-Авто:Автосалон + Автосервис + Автозапчасти, редакция 3, для Украины
//  306: Альфа-Авто:Автосалон + Автосервис + Автозапчасти, редакция 4
//  412: Альфа-Авто:Автосалон + Автосервис + Автозапчасти, украинская версия 4.0
//  304: Альфа-Авто:Автосервис + Автозапчасти, редакция 4
//  411: Альфа-Авто:Автосервис + Автозапчасти, украинская версия 4.0             
//  500: Альфа-Авто:Автосалон + Автосервис + Автозапчасти ПРОФ, редакция 5.1
Функция орПолучитьИнформациюОПродукте(Структура=Неопределено) Экспорт
	Если Структура=Неопределено Тогда
		Структура = Новый Структура;	
	КонецЕсли;
	Структура.Вставить("КодПродукта", 500); // Впишите код продукта - Число
	
	// Здесь необходимо указать адрес техподдержки текущего программного продукта.
	// А также нужно задать представление для этого адреса.
	СтруктураАдрес = Новый Структура;
	СтруктураАдрес.Вставить("Адрес",         "alfa@rarus.ru"); // Например: alfa@rarus.ru
	СтруктураАдрес.Вставить("Представление", "Техподдержка решений направления ""Альфа-Авто"""); // Например: Техподдержка решений направления "Альфа-Авто".
	Структура.Вставить("АдресТехПоддержкиПродукта", СтруктураАдрес);	
	
	Возврат Структура;	
КонецФункции

// Функция проверки корректности заполнения свойств
// по заполненности обязательных реквизитов и уникальности уникальных индексированных
//
// Параметры:
//  ЭтотОбъект	 - ОбработкаОбъект, обрабатываемые свойства, обработка "ЗначенияСвойствОбъекта"
//  Ошибки    	 - строка, описание ошибок
//
// Возвращаемое значение:
//   булево - признак успешности проверки
//
Функция орПроверитьКорректностьСвойств(ЭтотОбъект, Ошибки="") Экспорт
	Возврат Истина;
КонецФункции

// После редактирования строки табличного поля.
//
// Параметры:
//  ЭтаФорма 			 - форма, форма переданного объекта
//  ТабличноеПоле 		 - редактируемое табличное поле
//  НоваяСтрока 		 - булево, признак редактирования новой строки
//  Копирование			 - булево, признак копирования
//
Процедура орТабличноеПолеСвойстваОбъектаПриНачалеРедактирования(ЭтаФорма, ТабличноеПоле, НоваяСтрока, Копирование) Экспорт
	Строка = ТабличноеПоле.ТекущиеДанные;
	Если НЕ Строка.Свойство.Пустая() Тогда
		Строка.Значение = Строка.Свойство.ТипЗначения.ПривестиЗначение(Строка.Значение);
		ТипыСвойства=Строка.Свойство.ТипЗначения.Типы();
		ТабличноеПоле.ТекущаяКолонка.ЭлементУправления.ВыбиратьТип=(ТипыСвойства.Количество()>1);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ФОРМ

// Функция обработки события ПередОткрытием всех форм
//
// Параметры:
//  ЭтаФорма	         – Форма  – Ссылка на форму документа.
//	Отказ                - Булево - Признак отказа от записи
//	СтандартнаяОбработка - Булево - Признак использования стандартной обработки события
//
// Возвращаемое значение:
//   Неопределено.
//
Функция орФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	Возврат Неопределено;
КонецФункции // орФормаПередОткрытием()

// Функция обработки события ОбработкаВыбора всех форм
//
// Параметры:
//  ЭтаФорма	   – Форма        – Ссылка на форму документа.
//	ЗначениеВыбора - Произвольный - Выбранное значение
//	Источник 	   - Произвольный - Источник данных
//
// Возвращаемое значение:
//   Неопределено.
//
Функция орФормаОбработкаВыбора(ЭтаФорма, ЗначениеВыбора, Источник) Экспорт
	//А не автомобиль ли выбирается?
	Если ТипЗнч(ЗначениеВыбора)=Тип("СправочникСсылка.Автомобили") И ЗначениеВыбора.ЭтоГруппа = Ложь Тогда
		ТабличноеПоле=ЭтаФорма.ЭлементыФормы.Найти("Автомобили");
		Если ТипЗнч(ТабличноеПоле)=Тип("ТабличноеПоле") Тогда
			ТабличнаяЧасть=ЭтаФорма[ТабличноеПоле.Данные];
			СтрокаАвтомобиля=ТабличнаяЧасть.Найти(ЗначениеВыбора,"Автомобиль");
			Если СтрокаАвтомобиля=Неопределено Тогда
				СтрокаАвтомобиля=ТабличнаяЧасть.Добавить();
				СтрокаАвтомобиля.Автомобиль=ЗначениеВыбора;
				Попытка
					СтрокаАвтомобиля.ИдентификаторАвтомобиля=Новый УникальныйИдентификатор;
				Исключение КонецПопытки; 
				ЭтаФорма.ОбработкаРеквизита("Автомобили.Автомобиль",СтрокаАвтомобиля,ЭтаФорма);
			КонецЕсли; 
			ТабличноеПоле.ТекущаяСтрока=СтрокаАвтомобиля;
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЗначениеВыбора)=Тип("СправочникСсылка.Опции") Тогда
		СтруктураПараметровПодбора=Неопределено;
		Попытка
			СтруктураПараметровПодбора=Источник.СтруктураПараметровФормы;
		Исключение
			СтруктураПараметровПодбора=Новый Структура();
		КонецПопытки; 
		ОбработкаВДокументе = Ложь;
		Если НЕ СтруктураПараметровПодбора.Свойство("ОбработкаВДокументе",ОбработкаВДокументе) Тогда
			ОбработкаВДокументе = Ложь;
			СтруктураПараметровПодбора.Вставить("ОбработкаВДокументе",ОбработкаВДокументе);
		КонецЕсли; 
		
		ТабличнаяЧасть = Неопределено;
		Попытка
			ТабличнаяЧасть = Источник.ВладелецФормы[СтруктураПараметровПодбора.ИмяТабличнойЧасти];
		Исключение
		КонецПопытки;
		Если Не ТабличнаяЧасть=Неопределено Тогда
			ОбработкаПодбор=Обработки.ПодборОпций.Создать();
			Если ЗначениеВыбора.ПризнакНабора Тогда
				ТаблицаНабора = ЗначениеВыбора.СоставНабора.Выгрузить();
				Для Каждого ЭлементИзНабора Из ТаблицаНабора Цикл
					ОбработкаПодбор.ДобавитьНоменклатуру(Источник,СтруктураПараметровПодбора,ЭлементИзНабора.Опция,Ложь,1);
				КонецЦикла;
			Иначе
				ОбработкаПодбор.ДобавитьНоменклатуру(Источник,СтруктураПараметровПодбора,ЗначениеВыбора,Ложь,1);
			КонецЕсли;
		КонецЕсли;    
	КонецЕсли; 
	
	Возврат Неопределено;
КонецФункции // орФормаОбработкаВыбора()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ МОДУЛЕЙ ДОКУМЕНТОВ

// Функция отраслевого обработчика ПередЗаписью документа
//
// Параметры:
//	ЭтотОбъект	    – ДокументОбъект           – Документ, который обрабатывается.
//	Отказ           - Булево		 	       - Признак отказа от записи
//	РежимЗаписи     - РежимЗаписиДокумента     - Режим записи документа
//	РежимПроведения - РежимПроведенияДокумента - Режим проведения документа
//  ДопРеквизиты    – Структура			       – Структура, содержащая дополнительные параметры обработки реквизита.
//	Заполнение      - Булево				   - Признак заполнения
//	Уникальность    - Булево				   - Признак уникальности
//	Маска           - Строка                   - Разрешение проверки прав 1 - проверять / 0 - не проверять
//					биты:
//					1 - Проверка периода редактирования 
//					2 - Запись раньше документа основания
//					3 - Редактирование проведенных документов
//					4 - Редактирование при наличие подчиненных
//					5 - Управление проведением
//
// Возвращаемое значение:
//	Неопределено
//
Функция орПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, ДопРеквизиты=Неопределено,Заполнение=Истина,Уникальность=Истина,Маска = "11111") Экспорт
	Если (ЭтотОбъект.Метаданные().Реквизиты.Найти("ДокументОснование")<>Неопределено) И 
		(НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		Если (ТипЗнч(ЭтотОбъект.ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") ИЛИ
			ТипЗнч(ЭтотОбъект.ДокументОснование)=Тип("ДокументСсылка.ЗаявкаНаРемонт")) И
			(ТипЗнч(ЭтотОбъект)<>Тип("ДокументОбъект.АктРазногласий")) Тогда
			Маска=Лев(Маска,1)+"0"+Сред(Маска,3);
		КонецЕсли; 
	КонецЕсли; 
	
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение И 
		(ТипЗнч(ЭтотОбъект.Ссылка)=Тип("ДокументСсылка.КорректировкаЗаказаПокупателя") ИЛИ 
		ТипЗнч(ЭтотОбъект.Ссылка)=Тип("ДокументСсылка.ПеремещениеТоваров") ИЛИ 
		ТипЗнч(ЭтотОбъект.Ссылка)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ 
		ТипЗнч(ЭтотОбъект.Ссылка)=Тип("ДокументСсылка.РезервированиеЗаказовПокупателя")) Тогда
		ЭтотОбъект.Движения.ЗаказыПокупателей.ИнтерактивноеПроведение = НЕ ЭтотОбъект.Проведен;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции // орПередЗаписью()

// Функция стандартного обработчика заполнения при вводе нового на основании
//
// Параметры:
//	ЭтотОбъект	– ДокументОбъект – Документ, который обрабатывается.
//	Основание   - ДокументОбъект - Документ-основание
//	Копирование - Булево         - Признак создания копированием
//
// Возвращаемое значение:
//	Неопределено
//
Функция орОбработкаЗаполнения(ЭтотОбъект,Основание,Копирование) Экспорт
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		//Установим флаг регламентированного учета
		дкОбработкаЗаполненияРегламентированныйУчет(ЭтотОбъект,Копирование);
		//Заполнение документов из заказ-наряда
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПокупателя") Тогда
			//Заполнение заказа покупателя из заказ-наряда
			Возврат орОбработкаЗаполнения_ЗаказНаряд_ЗаказПокупателя(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СнятиеРезервовЗаказовПокупателя") Тогда
			//Заполнение снятие резервов по заказ-наряду
			Возврат орОбработкаЗаполнения_ЗаказНаряд_СнятиеРезервовЗаказовПокупателя(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплату") Тогда
			//Заполнение счета на оплату из заказ-наряда
			Возврат орОбработкаЗаполнения_ЗаказНаряд_СчетНаОплату(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПриходныйКассовыйОрдер") Тогда
			//Заполнение ПКО из заказ-наряда
			Возврат орОбработкаЗаполнения_ЗаказНаряд_ПриходныйКассовыйОрдер(ЭтотОбъект,Основание,Копирование);
			//ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетФактураВыданный") Тогда
			//	//Заполнение счета-фактуры выданного из заказ-наряда
			//	Возврат орОбработкаЗаполнения_ЗаказНаряд_СчетФактураВыданный(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату по заказ-наряду
			Возврат орОбработкаЗаполнения_ЗаказНаряд_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ВозвратОтПокупателя") Тогда
			//Заполнение возврата от покупателя по заказ-наряду
			Возврат орОбработкаЗаполнения_ЗаказНаряд_ВозвратОтПокупателя(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СписаниеТоваров") Тогда
			//Заполнение списания товаров по заказ-наряду
			Возврат орОбработкаЗаполнения_ЗаказНаряд_СписаниеТоваров(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.SMS") Тогда
			//Заполнение SMS по заказ-наряду
			Возврат орОбработкаЗаполнения_SMSЗаказчику_ЗаказНаряд(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПоставщику") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказВнутренний") Тогда
			//Заполнение заказа поставщику или внутреннего заказа из заказ-наряда
			Возврат орОбработкаЗаполнения_ЗаказНаряд_ЗаказПоставщику(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплату") Тогда
			//Заполнение счета на оплату из заявки на ремонт
			Рез=орОбработкаЗаполнения_ЗаказНаряд_СчетНаОплату(ЭтотОбъект,Основание,Копирование);
			Возврат Рез;
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.SMS") Тогда
			//Заполнение SMS по заявке на ремонт
			Возврат орОбработкаЗаполнения_SMSЗаказчику(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.АктРазногласий") Тогда
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплату") Тогда
			//Заполнение счета на оплату из акта разногласий
			Рез=орОбработкаЗаполнения_АктРазногласий_СчетНаОплату(ЭтотОбъект,Основание,Копирование);
			Возврат Рез;
			//ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетФактураВыданный") Тогда
			//	//Заполнение счета-фактуры выданной из акта разногласий
			//	Рез=орОбработкаЗаполнения_АктРазногласий_СчетФактураВыданный(ЭтотОбъект,Основание,Копирование);
			//	Возврат Рез;	
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.СчетНаОплату") Тогда
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату на основании счета
			Возврат орОбработкаЗаполнения_СчетНаОплату_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		//Установим флаг регламентированного учета
		дкОбработкаЗаполненияРегламентированныйУчет(ЭтотОбъект,Копирование);
		//Заполнение документов из заказа на автомобиль
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПоступлениеАвтомобилей") Тогда
			//Заполнение поступления автомобилей из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказНаАвтомобиль_ПоступлениеАвтомобилей(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПоставщикуНаАвтомобиль") Тогда
			//Заполнение заказа поставщику на автомобиль из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказНаАвтомобиль_ЗаказПоставщикуНаАвтомобиль(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплатуЗаАвтомобили") Тогда
			//Заполнение счета на оплату за автомобили из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказНаАвтомобиль_СчетНаОплатуЗаАвтомобили(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.РеализацияАвтомобилей") Тогда
			//Заполнение реализации автомобилей из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказНаАвтомобиль_РеализацияАвтомобилей(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СнятиеРезервовАвтомобилей") Тогда
			//Заполнение снятия резервов автомобилей из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказНаАвтомобиль_СнятиеРезервовАвтомобилей(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказНаАвтомобиль_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.Взаимозачет") Тогда
			//Заполнение взаимозачета по Trade-In
			Возврат орОбработкаЗаполнения_TradeIn_Взаимозачет(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.SMS") Тогда
			//Заполнение SMS по заказу на автомобиль
			Возврат орОбработкаЗаполнения_SMSЗаказчику(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили") Тогда		
		//Установим флаг регламентированного учета
		дкОбработкаЗаполненияРегламентированныйУчет(ЭтотОбъект,Копирование);
		//Заполнение документов из счета на оплату за автомобили
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.РеализацияАвтомобилей") Тогда
			//Заполнение ПКО из заказа на автомобиль
			Возврат орОбработкаЗаполнения_СчетНаОплатуЗаАвтомобили_РеализацияАвтомобилей(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату из заказа на автомобиль
			Возврат орОбработкаЗаполнения_СчетНаОплатуЗаАвтомобили_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияАвтомобилей") ИЛИ
		ТипЗнч(Основание)=Тип("ДокументСсылка.ОтчетКомиссионераЗаАвтомобили") ИЛИ
		ТипЗнч(Основание)=Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей") Тогда
		//Установим флаг регламентированного учета
		дкОбработкаЗаполненияРегламентированныйУчет(ЭтотОбъект,Копирование);
		//Заполнение документов из реализации автомобилей
		//Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		//	Возврат орОбработкаЗаполнения_Автомобили_СчетФактураВыданный(ЭтотОбъект,Основание,Копирование);
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияАвтомобилей") И
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплатуЗаАвтомобили") Тогда
			//Заполнение счета на оплату за автомобили из реализации автомобиля
			Возврат орОбработкаЗаполнения_РеализацияАвтомобилей_СчетНаОплатуЗаАвтомобили(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияАвтомобилей") И
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПриходныйКассовыйОрдер") Тогда
			//Заполнение ПКО из реализации автомобиля
			Возврат орОбработкаЗаполнения_РеализацияАвтомобилей_ПриходныйКассовыйОрдер(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату из реализации автомобиля
			Если ТипЗнч(Основание)=Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей") Тогда
				Возврат орОбработкаЗаполнения_ВозвратПоставщикуАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
			Иначе
				Возврат орОбработкаЗаполнения_РеализацияАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеАвтомобилей") ИЛИ
		ТипЗнч(Основание)=Тип("ДокументСсылка.ОтчетКомитентуЗаАвтомобили") ИЛИ
		ТипЗнч(Основание)=Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей") Тогда
		//Установим флаг регламентированного учета
		дкОбработкаЗаполненияРегламентированныйУчет(ЭтотОбъект,Копирование);
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеАвтомобилей") Тогда
			Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.Взаимозачет") Тогда
				//Заполнение взаимозачета по Trade-In
				Возврат орОбработкаЗаполнения_TradeIn_Взаимозачет(ЭтотОбъект,Основание,Копирование);
			КонецЕсли;
		КонецЕсли;
		//Заполнение документов из поступления автомобилей
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетФактураПолученный") Тогда
			Возврат орОбработкаЗаполнения_Автомобили_СчетФактураПолученный(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату из поступления автомобиля
			Если ТипЗнч(Основание)=Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей") Тогда
				Возврат орОбработкаЗаполнения_ВозвратОтПокупателяАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
			Иначе
				Возврат орОбработкаЗаполнения_ПоступлениеАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		//Установим флаг регламентированного учета
		дкОбработкаЗаполненияРегламентированныйУчет(ЭтотОбъект,Копирование);
		//Заполнение документов из заказа на автомобиль
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПоступлениеАвтомобилей") Тогда
			// Заполнение документа поступление автомобилей на основании заказа поставщику на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ПоступлениеАвтомобилей(ЭтотОбъект,Основание,Копирование)
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетОтПоставщикаЗаАвтомобили") Тогда
			//Заполнение счета от поставщика за автомобили из заказа поставщику на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_СчетОтПоставщикаЗаАвтомобили(ЭтотОбъект,Основание,Копирование)
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
			//Заполнение РКО из заказа поставщику на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_РасходныйКассовыйОрдер(ЭтотОбъект,Основание,Копирование)
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПлатежноеПоручение") Тогда
			//Заполнение платежного поручения из заказа на автомобиль
			Возврат орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ПлатежноеПоручение(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.SMS") Тогда
			//Заполнение SMS по заказу поставщика на автомобиль
			Возврат орОбработкаЗаполнения_SMSЗаказчику(ЭтотОбъект,Основание,Копирование);
		ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату из поступления автомобиля
			Возврат орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		Если ТипЗнч(ЭтотОбъект) = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
			орОбработкаЗаполнения_КорректировкаРеализации_СчетФактураПолученный(ЭтотОбъект,Основание,Копирование);
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
		Если ТипЗнч(ЭтотОбъект) = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
			орОбработкаЗаполнения_КорректировкаПоступленияАвтомобили_СчетФактураПолученный(ЭтотОбъект,Основание,Копирование);
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили") Тогда
		Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЧекНаОплату") Тогда
			//Заполнение чека на оплату из поступления автомобиля
			Возврат орОбработкаЗаполнения_СчетОтПоставщикаЗаАвтомобили_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
		КонецЕсли;
	ИначеЕсли Основание=Неопределено Тогда
		дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
		Если
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказВнутренний") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПеремещениеТоваров") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПоставщику") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.КорректировкаЗаказаПоставщику") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПоступлениеТоваров") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.АвансовыйОтчет") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетФактураПолученный") Тогда
			Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
				ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенЗакупки",ЭтотОбъект.Права,,ЭтотОбъект);
				ЭтотОбъект.ВалютаДокумента=обВалютаТипаЦены(Неопределено,ЭтотОбъект.ТипЦен,Ложь);
			КонецЕсли;
		ИначеЕсли
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПокупателя") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.КорректировкаЗаказаПокупателя") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.РеализацияТоваров") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаряд") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаявкаНаРемонт") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПеремещениеТоваровВПроизводство") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ИзвлечениеТоваровИзПроизводства") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплату") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетФактураВыданный") Тогда
			Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
				ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенПродажи",ЭтотОбъект.Права,,ЭтотОбъект);
				ЭтотОбъект.ВалютаДокумента=обВалютаТипаЦены(Неопределено,ЭтотОбъект.ТипЦен,Ложь);
			КонецЕсли;
			
			Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаряд") ИЛИ 
				ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаявкаНаРемонт") Тогда
				Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦенРабот) Тогда
					ЭтотОбъект.ТипЦенРабот=обПраво("ОсновнойТипЦенРабот",ЭтотОбъект.Права,,ЭтотОбъект);
					ЭтотОбъект.ВалютаДокумента=обВалютаТипаЦены(Неопределено,ЭтотОбъект.ТипЦенРабот,Ложь);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.БюджетЗакупокАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ВводОстатковАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ВозвратПоставщикуАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПоставщикуНаАвтомобиль") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СписаниеАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетОтПоставщикаЗаАвтомобили") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ОтчетКомитентуЗаАвтомобили") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПеремещениеАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ПоступлениеАвтомобилей") Тогда
			Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
				ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенЗакупкиАвтомобилей",ЭтотОбъект.Права,,ЭтотОбъект);
				ЭтотОбъект.ВалютаДокумента=обВалютаТипаЦены(Неопределено,ЭтотОбъект.ТипЦен,Ложь);
			КонецЕсли;
		ИначеЕсли
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.БюджетПродажАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ВозвратОтПокупателяАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказНаАвтомобиль") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.РеализацияАвтомобилей") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.СчетНаОплатуЗаАвтомобили") ИЛИ
			ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ОтчетКомиссионераЗаАвтомобили") Тогда
			Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
				ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенПродажиАвтомобилей",ЭтотОбъект.Права,,ЭтотОбъект);
				ЭтотОбъект.ВалютаДокумента=обВалютаТипаЦены(Неопределено,ЭтотОбъект.ТипЦен,Ложь);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции // орОбработкаЗаполнения()

// Функция производит выборочную регистрацию изменений согласно правилам миграции документов
//
// Параметры:
//	ЭтотОбъект	 						- ДокументОбъект. Документ, который обрабатывается
//	ЭтоУдаление	 						- Булево, признак необходимости зарегистрировать удаление документа
//	ДоступныеУзлыРегистрацииИзменений	- Массив. Используется для возврата уже готового массива
//	узлов в вызывающую процедуру
//
// Возвращаемое значение:
//   Неопределено.
//
Процедура орРегистрацияИзмененийДокументов(ЭтотОбъект,ЭтоУдаление=Ложь,ДоступныеУзлыРегистрацииИзменений = Неопределено,
	ДопМассивПодразделений = Неопределено, УзелОтправитель = Неопределено,ПланОбменаМетаданные = Неопределено,
	МетаданныеОбъекта = Неопределено,МетаданныеДвижений = Неопределено,РежимЗаписи = Неопределено) Экспорт
	
	одОбменДанными.РегистрацияИзменений(ЭтотОбъект,ЭтоУдаление,ДоступныеУзлыРегистрацииИзменений,ДопМассивПодразделений,УзелОтправитель,
	ПланОбменаМетаданные,МетаданныеОбъекта,МетаданныеДвижений,РежимЗаписи);
	
КонецПроцедуры // орРегистрацияИзмененийДокументов()

// Функция получения отраслевых исключений из правил миграции объектов
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//   Структура   – Структура правил миграции объектов
//
Функция орПравилаМиграцииОбъектов() Экспорт
	// Сформируем структуру правил миграции объектов
	// По умолчанию исключений нет
	ПравилаМиграцииОбъектов = Новый Структура();
	//Пример заполнения структуры
	//ПравилаМиграцииОбъектов.Вставить("ИзменениеЦен", Новый Структура("Способ,Подразделение",
	//"ВсеИнформационныеБазы", ""));
	Возврат ПравилаМиграцииОбъектов;
КонецФункции // орПравилаМиграцииОбъектов()

// Функция возвращает маску файлов обмена (например, для удаления неактуальных)
//Отраслевое переопределение маски файлов обмена
//Параметры:
//	НастройкаДоставки - СправочникСсылка - настройка доставки
//	Выгрузка - Флаг того, что файл отправляем, если Ложь, то принимаем
Функция орМаскаФайлаДоставки(УзлОбмена,НастройкаДоставки, Выгрузка) Экспорт
	Рез = "";
	
	Возврат Рез;
КонецФункции

// Функция начала выбора значения свойства объектов в панели свойств
//
// Параметры:
//	Объект  			 – Объект	      - Объект, свойства которого редактируются (справочник, документ и т.д.)
//  СвойстваОбъекта  	 – ТабличнаяЧасть – Табличная часть свойств объекта
//  Свойство 			 – ПВХСсылка      – Ссылка на свойство объекта, значение которого редактируется
//  Элемент  			 – ПолеВвода      – Поле ввода, в котором осуществляется редактирование свойства
//  СтандартнаяОбработка – Булево         – Признак стандартной обработки начала выбора значения свойства
//	глСтруктураОтбора 	 - Переменная     - Глобальная переменная со структурой отбора для выбора
//
// Возвращаемое значение:
//	Булево - Результат выполнения отраслевой функции. Если отраслевая функция не выполняет никаких действий, 
//  	   возвращается Неопределено
//
Функция орСвойстваОбъектаЗначениеНачалоВыбора(Объект, СвойстваОбъекта, Свойство, Элемент, СтандартнаяОбработка, глСтруктураОтбора) Экспорт
	Результат=Неопределено;
	Возврат Результат;
КонецФункции // орСвойстваОбъектаЗначениеНачалоВыбора()

// Функция обработчик изменения значения свойства объекта в панели свойств
//
// Параметры:
//  Объект          – Объект         - Объект, свойства которого редактируются (справочник, документ и т.д.)
//  СвойстваОбъекта – ТабличнаяЧасть – Табличная часть свойств объекта
//  Свойство        – ПВХСсылка      – Ссылка на свойство объекта, значение которого редактируется
//  Элемент         – ПолеВвода      – Поле ввода, в котором осуществляется редактирование свойства
//
// Возвращаемое значение:
//	Булево - Результат выполнения отраслевой функции. Если отраслевая функция не выполняет
//		   никаких действий, возвращается Неопределено
//
Функция орСвойстваОбъектаЗначениеПриИзменении(Объект, СвойстваОбъекта, Свойство, Элемент) Экспорт
	Результат=Неопределено;
	
	Возврат Результат;
КонецФункции // орСвойстваОбъектаЗначениеПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// РАБОТА С КАЛЕНДАРНЫМ ПЛАНИРОВАНИЕМ

// Процедура отраслевой коррекции таблицы событий календаря
//
// Параметры:
//	РасписаниеСобытий - ТаблицаЗначений - Таблица расписаний событий
//
Процедура орКорректировкаРасписанияСобытий(РасписаниеСобытий) Экспорт
	МассивЗаявокНаРемонтДляУдаления=Новый Массив;
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл
		Если СтрокаРасписания.Напоминание Тогда
			СтрокаРасписания.ПредставлениеОбъекта = Формат(СтрокаРасписания.НачалоСобытия, "ДФ=ЧЧ:мм; ДП=00:00") + " " + СтрокаРасписания.ПредставлениеОбъекта;
		ИначеЕсли ТипЗнч(СтрокаРасписания.Событие)=Тип("ДокументСсылка.Событие") Тогда
			СтрокаРасписания.ПредставлениеОбъекта = Формат(СтрокаРасписания.Событие.ДатаНачала, "ДФ=ЧЧ:мм; ДП=00:00") + " " + СтрокаРасписания.ПредставлениеОбъекта;
		ИначеЕсли ТипЗнч(СтрокаРасписания.Событие)=Тип("ДокументСсылка.ЗаявкаНаРемонт") ИЛИ 
			ТипЗнч(СтрокаРасписания.Событие)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			ДатаНачала=СтрокаРасписания.Событие.ДатаНачала;
			Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
				Попытка
					ДатаНачала=СтрокаРасписания.Событие.ДатаСоздания;
				Исключение КонецПопытки; 
			КонецЕсли; 
			Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
				ДатаНачала=СтрокаРасписания.Событие.Дата;
			КонецЕсли;
			ДатаОкончания=СтрокаРасписания.Событие.ДатаОкончания;
			Если обЗначениеНеЗаполнено(ДатаОкончания) Тогда
				//Если дата окончания не задана - просуммируем время выполнения работ, нормированных по времени
				Запрос=Новый Запрос;
				Запрос.Текст="ВЫБРАТЬ
				|	ЕСТЬNULL(СУММА(Работы.Коэффициент),0) КАК Коэффициент
				|ИЗ
				|	Документ."+СтрокаРасписания.Событие.Метаданные().Имя+".Работы КАК Работы
				|ГДЕ
				|	Работы.Ссылка = &Ссылка
				|	И Работы.Нормочас.Цена <> 1";
				Запрос.УстановитьПараметр("Ссылка",СтрокаРасписания.Событие);
				Выборка=Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					ДатаОкончания=ДатаНачала+(Выборка.Коэффициент*60*60);
				КонецЕсли; 
			КонецЕсли; 
			Если ТипЗнч(СтрокаРасписания.Событие)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
				Если Документы.ЗаказНаряд.НайтиПоРеквизиту("ДокументОснование",СтрокаРасписания.Событие).Пустая() Тогда
					СтрокаРасписания.ПредставлениеОбъекта = Формат(ДатаНачала, "ДЛФ=ЧЧ:мм; ДП=00:00") + " - " + Формат(ДатаОкончания, "ДЛФ=ЧЧ:мм; ДП=00:00") + " Заявка.";
					СтрокаРасписания.ИмяИконки = "Настройка";
				Иначе
					МассивЗаявокНаРемонтДляУдаления.Добавить(СтрокаРасписания);
				КонецЕсли; 
			ИначеЕсли ТипЗнч(СтрокаРасписания.Событие)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
				СтрокаРасписания.ПредставлениеОбъекта = Формат(ДатаНачала, "ДЛФ=ЧЧ:мм; ДП=00:00") + " - " + Формат(ДатаОкончания, "ДЛФ=ЧЧ:мм; ДП=00:00") + " ЗН.";
				СтрокаРасписания.ИмяИконки = "Настройка";
			КонецЕсли;
		Иначе
			СтрокаРасписания.ПредставлениеОбъекта = Формат(СтрокаРасписания.Событие.ДатаНачала, "ДФ=ЧЧ:мм; ДП=00:00") + " " + СтрокаРасписания.ПредставлениеОбъекта;
		КонецЕсли;
	КонецЦикла;
	//Удалим заявки на ремонт, на основании которых введены ЗН
	Для каждого ЗаявкаНаРемонтДляУдаления Из МассивЗаявокНаРемонтДляУдаления Цикл
		РасписаниеСобытий.Удалить(ЗаявкаНаРемонтДляУдаления);
	КонецЦикла; 
КонецПроцедуры // орКорректировкаРасписанияСобытий()

// Функция отраслевой обработки вызова события
//
// Параметры:
//	ИмяСобытия  - Строка - Имя события
//	ДатаСобытия - Дата   - Дата события
//
// Возвращаемое значение:
//	Неопределено
//
Функция орОбработкаВызоваНовогоСобытия(ИмяСобытия, ДатаСобытия) Экспорт
	Возврат Неопределено;
КонецФункции // орОбработкаВызоваНовогоСобытия()

#Если Клиент Тогда
	
	// Функция добавляет в панель АРМ/календарик/контекстное меню дополнительные объекты.
	// Добавляемые объекты представляют элементы списка значений.
	// Значение - имя кнопки, представление - подсказка, картинка - картинка
	//
	// Параметры:
	//	Нет.
	//
	// Возвращаемое значение:
	//	ТаблицаЗначений - Список дополнительных объектов
	//
	Функция орПолучитьДопОбъектыКалендаряПанелиАРМ() Экспорт
		СписокДопОбъектов = Новый ТаблицаЗначений;
		СписокДопОбъектов.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
		СписокДопОбъектов.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
		СписокДопОбъектов.Колонки.Добавить("Тип", Новый ОписаниеТипов("Тип"));
		СписокДопОбъектов.Колонки.Добавить("Картинка");
		Возврат СписокДопОбъектов;
	КонецФункции // орПолучитьДопОбъектыКалендаряПанелиАРМ()
	
#КонецЕсли

// Процедура обработки нажатия отраслевой кнопки, добавленной согласно списку доп. объектов
// в функции орПолучитьДопОбъектыКалендаряПанелиАРМ()
//
// Параметры:
//	ВыбЗнач      - Тип, ОбъектСсылка - Значение для выбора. Если тип "Тип", тогда объект создается,
//				 если тип "Ссылка"   - открывается осн. форма
//	ДопПараметры - Структура        - Структура набора заполняемых реквизитов
//
Процедура орОбработкаВыбораДопОбъектаПанелиАРМ(ВыбЗнач, СтандартнаяОбработка = Истина, ДопПараметры = Неопределено) Экспорт
	// зарезервировано на будущее	
КонецПроцедуры // орОбработкаВыбораДопОбъектаПанелиАРМ()

// Процедура обработки выбора объекта планирования в календарном интерфейсе
//
// Параметры:
//	ВыбЗнач              - Тип, ОбъектСсылка - Значение для выбора. Если тип "Тип", тогда объект создается,
//						если тип "Ссылка" - открывается осн. форма
//	ВидПериода           - Строка            - Вид периода, из какого календарного представления был вызван
//												("День", "Неделя", "Месяц")
//	Форма                - Форма             - Форма календарного интерфейса
//	СтандартнаяОбработка - Булево            - Флаг стандартной обработки выбора объекта, если Ложь,
///						то станд. обработка выбора не будет выполнена
//	ДопПараметры         - Структура         - Включает свойство ТекущиеДанные, содержащее тек. строку/данные
//	объекта планирования
//
Процедура орКалендарьОбработкаВыбораСобытия(ВыбЗнач, ВидПериода, Форма, СтандартнаяОбработка, ДопПараметры = Неопределено) Экспорт
	Если (ТипЗнч(ВыбЗнач) <> Тип("Тип") И обЗначениеНеЗаполнено(ВыбЗнач)) ИЛИ обЗначениеНеЗаполнено(ВидПериода) ИЛИ обЗначениеНеЗаполнено(Форма) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // орКалендарьОбработкаВыбораСобытия()

////////////////////////////////////////////////////////////////////////////////
// РАБОТА СО СКИДКАМИ.

// Функция осуществляет поиск подходящей скидки в соответствии с передаваемыми параметрами.
// Параметры:
//  ДокументОбъект – "ДокументОбъект" - Произвольный документ, для которого производится поиск подходящей скидки.
//
//  ОбработкаОбъект - "ОбработкаОбъект" - Обработка расчета скидки
//
// Возвращаемое значение:
//   Булево - Истина: Необходим пересчет табличной части в документе; Ложь: Подобранная скидка не отличается от текущей.
// 
Функция орПолучитьСкидкуШапки(ДокументОбъект, ОбработкаОбъект) Экспорт
	Возврат Неопределено;		
КонецФункции // орПолучитьСкидкуШапки() 

// Функция осуществляет перерасчет сумм табличной части документа в соответствии с передаваемыми значениями скидки.
//
// Параметры:
//  ДокументОбъект – "ДокументОбъект" - Произвольный документ, для которого производится поиск подходящей скидки.
//
//  ОбработкаОбъект - "ОбработкаОбъект" - Обработка расчета скидки
//
// Возвращаемое значение:
//   Булево - Истина: Необходим пересчет табличной части в документе; Ложь: Подобранная скидка не отличается от текущей.
// 
Функция орПрименитьСкидкуШапки(ДокументОбъект, ОбработкаОбъект) Экспорт
	Если ТипЗнч(ДокументОбъект)=Тип("ДокументОбъект.ЗаказНаАвтомобиль") И
		ОбработкаОбъект.ИмяТабличнойЧасти="Опции" Тогда
		СкидкаОтносительная=ОбработкаОбъект.ТекущаяСкидкаНаценка.Пустая() ИЛИ (ОбработкаОбъект.ТекущаяСкидкаНаценка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная);
		// Выполняем расчет суммы скидки для каждой строки табличной части.
		Если СкидкаОтносительная Тогда
			// Скидки более 100% недопустимы.
			ПроцентСкидки=Мин(ОбработкаОбъект.ТекущееЗначениеСкидки,100); 		
		Иначе
			// Скидки более 100% недопустимы.
			КоэффициентСкидки=?(ОбработкаОбъект.БазаДляРасчетаСкидки=0,0,ОбработкаОбъект.ТекущаяСуммаСкидки/ОбработкаОбъект.БазаДляРасчетаСкидки);
			КоэффициентСкидки=?(КоэффициентСкидки>1,1,КоэффициентСкидки);
		КонецЕсли;
		//Установим скидку на автомобиль
		Если СкидкаОтносительная Тогда
			ДокументОбъект.ПроцентСкидкиНаАвтомобиль=ПроцентСкидки;
			ДокументОбъект.СуммаСкидкиНаАвтомобиль=ДокументОбъект.ПроцентСкидкиНаАвтомобиль/100*ДокументОбъект.СуммаВсегоНаАвтомобиль;
			ДокументОбъект.СуммаСкидкиНаАвтомобиль=дкОкруглитьСуммуСкидки(ДокументОбъект.СуммаСкидкиНаАвтомобиль,ОбработкаОбъект.ТекущаяСкидкаНаценка);
		Иначе
			ДокументОбъект.СуммаСкидкиНаАвтомобиль=КоэффициентСкидки*ДокументОбъект.СуммаВсегоНаАвтомобиль;
			ДокументОбъект.СуммаСкидкиНаАвтомобиль=дкОкруглитьСуммуСкидки(ДокументОбъект.СуммаСкидкиНаАвтомобиль,ОбработкаОбъект.ТекущаяСкидкаНаценка);
			ДокументОбъект.ПроцентСкидкиНаАвтомобиль=?(ДокументОбъект.СуммаСкидкиНаАвтомобиль=0,0,КоэффициентСкидки*100);
		КонецЕсли;
		// Рассчитываем сумму НДС и сумму всего.
		ДокументОбъект.ОбработкаРеквизита("СтавкаНДСНаАвтомобиль");
		// Проходимся по табличной части. 
		Для каждого ТекСтрока Из ДокументОбъект[ОбработкаОбъект.ИмяТабличнойЧасти] Цикл
			Если СкидкаОтносительная Тогда
				ТекСтрока.ПроцентСкидки=ПроцентСкидки;
				ТекСтрока.СуммаСкидки=ТекСтрока.ПроцентСкидки/100*ТекСтрока.СуммаВсего;
				ТекСтрока.СуммаСкидки=дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,ОбработкаОбъект.ТекущаяСкидкаНаценка);
			Иначе
				ТекСтрока.СуммаСкидки=КоэффициентСкидки*ТекСтрока.СуммаВсего;
				ТекСтрока.СуммаСкидки=дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,ОбработкаОбъект.ТекущаяСкидкаНаценка);
				ТекСтрока.ПроцентСкидки=?(ТекСтрока.СуммаСкидки=0,0,КоэффициентСкидки*100);
			КонецЕсли;
			// Рассчитываем сумму НДС и сумму всего.
			ДокументОбъект.ОбработкаРеквизита(ОбработкаОбъект.ИмяТабличнойЧасти+".СтавкаНДС",ТекСтрока);
		КонецЦикла;		
		// Убираем копейки от округления. 
		ОстатокСкидки=ОбработкаОбъект.ТекущаяСуммаСкидки-(ДокументОбъект.СуммаСкидкиНаАвтомобиль+ДокументОбъект[ОбработкаОбъект.ИмяТабличнойЧасти].Итог("СуммаСкидки"));
		Если ОстатокСкидки<>0 Тогда
			ДокументОбъект.СуммаСкидкиНаАвтомобиль=ДокументОбъект.СуммаСкидкиНаАвтомобиль+ОстатокСкидки;
			ДокументОбъект.ПроцентСкидкиНаАвтомобиль=?(ДокументОбъект.ЦенаАвтомобиля=0,0,100*ДокументОбъект.СуммаСкидкиНаАвтомобиль/ДокументОбъект.ЦенаАвтомобиля);
			ДокументОбъект.ОбработкаРеквизита("СтавкаНДСНаАвтомобиль");
		КонецЕсли;
		Возврат Истина;		
	КонецЕсли; 
	
	Возврат Неопределено;		
КонецФункции // орПрименитьСкидкуШапки() 

// РАБОТА СО СКИДКАМИ (КОНЕЦ).
////////////////////////////////////////////////////////////////////////////////

//Добавляет новую запись в журнал состояний
Функция орЖурналСостояний(ОбъектСсылка,Состояние=Неопределено) Экспорт
	НовоеСостояние=Состояние;
	Если НовоеСостояние=Неопределено Тогда
		НовоеСостояние=ОбъектСсылка.Состояние;
	КонецЕсли;
	
	ДобавитьЗаписьВЖурнал=Ложь;
	
	ТекДата=ТекущаяДата();
	ПоследнееСостояние=РегистрыСведений.ЖурналСостояний.СрезПоследних(ТекДата,Новый Структура("Объект",ОбъектСсылка));
	Если ПоследнееСостояние.Количество()=0 Тогда
		ДобавитьЗаписьВЖурнал=Истина;
	Иначе
		Если ПоследнееСостояние[0].Состояние<>НовоеСостояние Тогда
			ДобавитьЗаписьВЖурнал=Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ДобавитьЗаписьВЖурнал Тогда
		НоваяЗапись=РегистрыСведений.ЖурналСостояний.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период=ТекДата;
		НоваяЗапись.Объект=ОбъектСсылка;
		НоваяЗапись.Состояние=НовоеСостояние;
		НоваяЗапись.Автор=ПараметрыСеанса.Пользователь;
		НоваяЗапись.Компьютер=ПараметрыСеанса.Компьютер.Наименование;
		Попытка
			НоваяЗапись.Записать(Истина);
		Исключение
			Сообщить("Ошибка записи в журнал изменения состояния: "+ОписаниеОшибки(),СтатусСообщения.Обычное);
			Возврат Ложь;
		КонецПопытки; 
	КонецЕсли; 
	
	Возврат Истина;
КонецФункции // орЖурналСостояний() 

// Возвращает форматную строку года выпуска автомобиля
Функция орПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Права=Неопределено) Экспорт                    
	ФорматПредставленияГодаВыпускаАвтомобиля=обПраво("ФорматПредставленияГодаВыпускаАвтомобиля",Права);
	ФорматПредставления=Метаданные.Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.ЗначенияПеречисления[Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.Индекс(ФорматПредставленияГодаВыпускаАвтомобиля)];
	Возврат ФорматПредставления.Комментарий;	
КонецФункции // орПолучитьФорматПредставленияГодаВыпускаАвтомобиля() 

// Проверка необходимости проведения сервисной кампании для автомобиля
//
// Параметры
//  Автомобиль  – Справочники.Автомобили.Ссылка  – Ссылка на автомобиль, 
//													для которого необходимо проверить наличие сервисной кампании
// Возвращаемое значение:
//   Таблица значений   – Список сервисных кампаний, в которые включен данный автомобиль
//
Функция орПроверитьСервиснуюКампанию(Автомобиль,НаДату=Неопределено,ТолькоАктуальные=Истина,ЗаказНаряд=Неопределено) Экспорт
	Если ЗначениеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
		VIN     = Автомобиль.VIN;
		ОригVIN = Автомобиль.ОригинальныйVIN;
	ИначеЕсли ТипЗнч(Автомобиль) = Тип("Структура") Тогда
		VIN = " "; ОригинальныйVIN = " ";
		Если Автомобиль.Свойство("VIN") Тогда
			VIN = Автомобиль.VIN;
		КонецЕсли;
		
		Если Автомобиль.Свойство("ОригинальныйVIN") Тогда
			ОригVIN = Автомобиль.ОригинальныйVIN;
		КонецЕсли;
	Иначе
		VIN=" ";
		ОригVIN=" ";
	КонецЕсли;
	
	
	Если Не ЗначениеЗаполнено(VIN) Тогда
		Результат = Новый ТаблицаЗначений;
		Результат.Колонки.Добавить("СервиснаяКампания",,"СервиснаяКампания");
		Результат.Колонки.Добавить("ДатаНачала",,"ДатаНачала");
		Результат.Колонки.Добавить("ДатаОкончания",,"ДатаОкончания");
		Результат.Колонки.Добавить("ДатаВыполнения",,"ДатаВыполнения");
		Результат.Колонки.Добавить("ДокументВыполнения",,"ДокументВыполнения");
		Результат.Колонки.Добавить("ВидИсточника",,"ВидИсточника");
		
	Иначе
		
		Количество = СтрДлина(VIN);;
		Строка = "";
		Пока Количество > 0 Цикл
			Строка = Строка + "_";
			Количество = Количество - 1;
		КонецЦикла;
		
		КоличествоСимволов = СтрДлина(VIN);
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ОбъединенныйЗапрос.СервиснаяКампания КАК СервиснаяКампания,
		|	ОбъединенныйЗапрос.СервиснаяКампания.ДатаНачала КАК ДатаНачала,
		|	ОбъединенныйЗапрос.СервиснаяКампания.ДатаОкончания КАК ДатаОкончания,
		|	МАКСИМУМ(ОбъединенныйЗапрос.ДатаВыполнения) КАК ДатаВыполнения,
		|	МАКСИМУМ(ОбъединенныйЗапрос.ДокументВыполнения) КАК ДокументВыполнения,
		|	МАКСИМУМ(ОбъединенныйЗапрос.ВидИсточника) КАК ВидИсточника
		|ПОМЕСТИТЬ СервисныеКампанииСГруппировкой
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВыполнениеСервисныхКампаний.СервиснаяКампания КАК СервиснаяКампания,
		|		ВыполнениеСервисныхКампаний.СервиснаяКампания.ДатаНачала КАК ДатаНачала,
		|		ВыполнениеСервисныхКампаний.СервиснаяКампания.ДатаОкончания КАК ДатаОкончания,
		|		ВыполнениеСервисныхКампаний.ДатаВыполнения КАК ДатаВыполнения,
		|		ВыполнениеСервисныхКампаний.ДокументВыполнения КАК ДокументВыполнения,
		|		1 КАК ВидИсточника
		|	ИЗ
		|		РегистрСведений.ВыполнениеСервисныхКампаний КАК ВыполнениеСервисныхКампаний
		|	ГДЕ
		|		ВыполнениеСервисныхКампаний.VIN В(&VIN)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СервисныеКампанииДиапазоныVIN.Ссылка,
		|		СервисныеКампанииДиапазоныVIN.Ссылка.ДатаНачала,
		|		СервисныеКампанииДиапазоныVIN.Ссылка.ДатаОкончания,
		|		&ДатаВыполненияПустая,
		|		&ДокументВыполненияПустой,
		|		0
		|	ИЗ
		|		Справочник.СервисныеКампании.ДиапазоныVIN КАК СервисныеКампанииДиапазоныVIN
		|	ГДЕ
		|			ВЫБОР
		|					КОГДА &VIN1 МЕЖДУ СервисныеКампанииДиапазоныVIN.НачальноеЗначение И СервисныеКампанииДиапазоныVIN.КонечноеЗначение
		|							И СервисныеКампанииДиапазоныVIN.КонечноеЗначение ПОДОБНО """+Строка+"""
		|						ТОГДА ""Истина""
		|				КОНЕЦ <> """"
		|			И (СервисныеКампанииДиапазоныVIN.Ссылка.ДатаОкончания > &ДатаТекущая
		|				ИЛИ СервисныеКампанииДиапазоныVIN.Ссылка.ДатаОкончания = &ДатаВыполненияПустая)) КАК ОбъединенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъединенныйЗапрос.СервиснаяКампания,
		|	ОбъединенныйЗапрос.СервиснаяКампания.ДатаНачала,
		|	ОбъединенныйЗапрос.СервиснаяКампания.ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СервисныеКампанииСГруппировкой.СервиснаяКампания КАК СервиснаяКампания,
		|	СервисныеКампанииСГруппировкой.ДатаНачала КАК ДатаНачала,
		|	СервисныеКампанииСГруппировкой.ДатаОкончания КАК ДатаОкончания,
		|	СервисныеКампанииСГруппировкой.ДатаВыполнения КАК ДатаВыполнения,
		|	СервисныеКампанииСГруппировкой.ДокументВыполнения КАК ДокументВыполнения,
		|	СервисныеКампанииСГруппировкой.ВидИсточника КАК ВидИсточника
		|ИЗ
		|	СервисныеКампанииСГруппировкой КАК СервисныеКампанииСГруппировкой"; 
		
		Если ТолькоАктуальные Тогда
			Запрос.Текст=Запрос.Текст+"
			|	ГДЕ (СервисныеКампанииСГруппировкой.ДатаВыполнения = &ДатаВыполненияПустая 
			|			ИЛИ СервисныеКампанииСГруппировкой.ДокументВыполнения = &ЗаказНаряд)
			|	И (СервисныеКампанииСГруппировкой.ДокументВыполнения = &ДокументВыполненияПустой
			|			ИЛИ СервисныеКампанииСГруппировкой.ДокументВыполнения = &ЗаказНаряд)
			|	И (СервисныеКампанииСГруппировкой.СервиснаяКампания.ДатаНачала < &ДатаТекущая
			|			ИЛИ СервисныеКампанииСГруппировкой.СервиснаяКампания.ДатаНачала = &ДатаВыполненияПустая)
			|	И (СервисныеКампанииСГруппировкой.СервиснаяКампания.ДатаОкончания > &ДатаТекущая
			|			ИЛИ СервисныеКампанииСГруппировкой.СервиснаяКампания.ДатаОкончания = &ДатаВыполненияПустая)";
		КонецЕсли;
		// заполним список VIN
		СписокVIN = Новый СписокЗначений;
		СписокVIN.Добавить(VIN);
		СписокVIN.Добавить(ОригVIN);
		
		Запрос.УстановитьПараметр("VIN",СписокVIN);
		Запрос.УстановитьПараметр("VIN1",VIN);
		Запрос.УстановитьПараметр("ДатаВыполненияПустая",'00010101');
		Запрос.УстановитьПараметр("ДокументВыполненияПустой",Документы.ЗаказНаряд.ПустаяСсылка());
		Запрос.УстановитьПараметр("ЗаказНаряд",ЗаказНаряд);
		Запрос.УстановитьПараметр("ДатаТекущая",?(НаДату=Неопределено,ТекущаяДата(),НаДату));
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		Для Каждого Элемент Из Результат Цикл
			Если Элемент.ВидИсточника = 0 Тогда
				МенеджерЗаписи = РегистрыСведений.ВыполнениеСервисныхКампаний.СоздатьМенеджерЗаписи(); 
				МенеджерЗаписи.VIN = VIN; 
				МенеджерЗаписи.СервиснаяКампания = Элемент.СервиснаяКампания;
				МенеджерЗаписи.Прочитать();
				МенеджерЗаписи.VIN = VIN; 
				МенеджерЗаписи.СервиснаяКампания = Элемент.СервиснаяКампания;
				МенеджерЗаписи.Автор = ПараметрыСеанса.Пользователь; 
				МенеджерЗаписи.Записать(); 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат Результат;
КонецФункции // орПроверитьСервиснуюКампанию()

#Если Клиент Тогда
	
	// Процедура формирует отчет СостояниеЗаказовПокупателей,
	// с отбором по указанному документу
	Процедура орОтчетСостояниеЗаказовПокупателей(Заказ) Экспорт
		
		Если Заказ.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
		
		Отчет = Отчеты.СостояниеЗаказовПокупателей.Создать();
		Отчет.ЗаполнитьНачальныеНастройки();
		
		МетаданныеОтчета  = Отчет.Метаданные();
		ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.ДляЗаказа;
		НастройкиВарианта = ВариантОтчета.Настройки;
		
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",           НачалоДня(Заказ.Дата));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания",        КонецДня(ТекущаяДата()));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("РежимВывода",          1);
		
		Если ТипЗнч(Заказ.Ссылка)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Контрагент", Заказ.Ссылка.Контрагент);
		ИначеЕсли ТипЗнч(Заказ.Ссылка)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Контрагент", Заказ.Ссылка.ПодразделениеПолучатель);
		КонецЕсли;
		
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Заказ", Заказ.Ссылка);
		
		ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
		СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
		СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
		СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
		СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
		СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
		
		ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
		
	КонецПроцедуры // орОтчетСостояниеЗаказовПокупателей()
	
	// Процедура формирует отчет СостояниеЗаказовПоставщикам,
	// с отбором по указанному документу
	Процедура орОтчетСостояниеЗаказовПоставщикам(Заказ) Экспорт
		
		Если Заказ.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
		
		Отчет = Отчеты.СостояниеЗаказовПоставщикам.Создать();
		Отчет.ЗаполнитьНачальныеНастройки();
		
		МетаданныеОтчета  = Отчет.Метаданные();
		ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.ДляЗаказа;
		НастройкиВарианта = ВариантОтчета.Настройки;
		
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",             НачалоДня(Заказ.Ссылка.Дата));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания",          КонецДня(ТекущаяДата()));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ВыводитьЗакрытыеЗаказы", Истина);
		
		Если ТипЗнч(Заказ.Ссылка)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Контрагент", Заказ.Ссылка.Контрагент);
		ИначеЕсли ТипЗнч(Заказ.Ссылка)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Контрагент", Заказ.Ссылка.ПодразделениеПолучатель);
		КонецЕсли;
		
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Контрагент",      Заказ.Ссылка.Контрагент);
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "ЗаказПоставщику", Заказ.Ссылка);
		
		ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
		СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
		СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
		СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
		СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
		СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
		
		ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
		
	КонецПроцедуры // орОтчетСостояниеЗаказовПоставщикам()
	
	// Процедура формирует отчет ОстаткиИОборотыПартийТоваров
	// по указанному документу
	Процедура орОтчетОстаткиИОборотыПартийТоваров(ТекущийДокумент) Экспорт
		
		// заполним параметры отчета
		Отчет = Отчеты.ОстаткиИОборотыПартийТоваров.Создать();
		Отчет.ЗаполнитьНачальныеНастройки();
		
		МетаданныеОтчета  = Отчет.Метаданные();
		ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.Остатки;
		НастройкиВарианта = ВариантОтчета.Настройки;
		
		Если НЕ НастройкиВарианта.ПараметрыДанных.Элементы.Найти("ДатаНачала") = Неопределено Тогда
			НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",    ТекущийДокумент.Дата);
		КонецЕсли;
		
		Если НЕ НастройкиВарианта.ПараметрыДанных.Элементы.Найти("ДатаОкончания") = Неопределено Тогда
			НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ТекущаяДата()));
		КонецЕсли;
		
		ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
		
		Показатели      = НастройкиВарианта.Выбор;
		Отбор           = НастройкиВарианта.Отбор;
		СтруктураОтчета = НастройкиВарианта.Структура;
		
		Для Каждого ТекПоказатель Из Показатели.Элементы Цикл
			Если ТипЗнч(ТекПоказатель) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
				ТекПоказатель.Использование = (СокрЛП(ТекПоказатель.Поле) = "КонечныйОстаток.Количество");
			Иначе
				ТекПоказатель.Использование = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		// фильтры
		Отбор.Элементы.Очистить();
		
		ТекОтбор = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ТекОтбор.Использование  = Истина;
		ТекОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Партия");
		ТекОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ТекОтбор.ПравоеЗначение = ТекущийДокумент;
		
		СтруктураОтчета.Очистить();
		
		ТаблицаОтчета = СтруктураОтчета.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		
		// добавим измерение "Номенклатура"
		ГруппировкаНоменклатура = ОтчетыСервер.СКД_ДобавитьГруппировку(ТаблицаОтчета.Строки, "Номенклатура");
		ДопПоле = ГруппировкаНоменклатура.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ДопПоле.Поле = Новый ПолеКомпоновкиДанных("Номенклатура.Артикул");
		ДопПоле.Использование = Истина;
		
		НастройкиВарианта.ПараметрыВывода.УстановитьЗначениеПараметра("РасположениеРеквизитов", РасположениеРеквизитовКомпоновкиДанных.Отдельно);
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
		СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
		СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
		СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
		СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
		СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
		
		ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
		
	КонецПроцедуры // орОтчетОстаткиИОборотыПартийТоваров()
	
	// Процедура формирует отчет ДвижениеЗаказовПокупателей
	// по текущему документу
	Процедура орОтчетДвиженияЗаказовПокупателей(ТекущийДокумент) Экспорт
		
		Отчет = Отчеты.ОстаткиИОборотыЗаказовПокупателей.Создать();
		Отчет.ЗаполнитьНачальныеНастройки();
		
		МетаданныеОтчета  = Отчет.Метаданные();
		ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.РезервыПоПартиям;
		НастройкиВарианта = ВариантОтчета.Настройки;
		
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",    НачалоДня(ТекущийДокумент.Дата));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ТекущийДокумент.Дата));
		//НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("РежимВывода",   1);
		
		
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "ПериодРегистратор", ТекущийДокумент);
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "СкладКомпании", ТекущийДокумент.СкладКомпании);
		
		ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
		СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
		СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
		СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
		СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
		СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
		
		ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
		
	КонецПроцедуры // орОтчетДвиженияЗаказовПокупателей()
	
	// Процедура формирует отчет ОстаткиТоваровНаСкладах
	// по текущему документу
	Процедура орОтчетОстаткиТоваровНаСкладах(ТекущийДокумент) Экспорт
		
		// заполним параметры отчета
		Отчет=Отчеты.ОстаткиИОборотыТоваров.Создать();
		Отчет.ВидОтчета=Перечисления.ВидыОтчетов.Остатки;
		Отчет.ДатаНачала=НачалоДня(ТекущаяДата());
		Отчет.ДатаКонца=КонецДня(ТекущаяДата());
		Отчет.РежимВыводаОтчета=Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
		Отчет.РежимНастройки=Перечисления.РежимыНастройкиОтчетов.Эксперт;
		Отчет.ИмяФормыНастроек="НастройкиОстатки";
		Отчет.ЗаполнитьНачальныеНастройки("ТекстЗапросаОстатки");
		
		Отчет.ИзмеренияСтроки.Очистить();
		
		//Измерение Номенклатура
		НоваяСтрока=Отчет.ИзмеренияСтроки.Добавить();
		НоваяСтрока.Использование=Истина;
		НоваяСтрока.ПутьКДанным="Номенклатура";
		НоваяСтрока.Представление="Номенклатура";
		НоваяСтрока.ТипИзмерения="Элементы";
		
		//Колонка Склад
		НоваяСтрока=Отчет.ИзмеренияКолонки.Добавить();
		НоваяСтрока.Использование=Истина;
		НоваяСтрока.ПутьКДанным="СкладКомпании";
		НоваяСтрока.Представление="СкладКомпании";
		НоваяСтрока.ТипИзмерения="Элементы";
		
		//Дополнительное поле артикула номенклатуры
		ДопПоляНоменклатура=Неопределено;
		Если Отчет.ПараметрыИзмеренийСтрок.Свойство("Номенклатура",ДопПоляНоменклатура) Тогда
			ДопПоляНоменклатура.Вставить("ПоляПредставление","№ по каталогу");
			тзПоля=Новый ТаблицаЗначений;
			тзПоля.Колонки.Добавить("Имя");
			тзПоля.Колонки.Добавить("Представление");
			тзПоля.Колонки.Добавить("ПутьКДанным");
			НоваяСтрока=тзПоля.Добавить();
			НоваяСтрока.Имя="";
			НоваяСтрока.Представление="№ по каталогу";
			НоваяСтрока.ПутьКДанным="Номенклатура.Артикул";
			ДопПоляНоменклатура.Вставить("Поля",тзПоля);
		КонецЕсли; 
		
		//Расставим показатели
		Для каждого ОтчетПоказатель Из Отчет.Показатели Цикл
			ОтчетПоказатель.Использование=(ОтчетПоказатель.Имя="СвободныйОстаток");
		КонецЦикла; 
		
		//Расставим функции
		Для каждого ОтчетФункция Из Отчет.Функции Цикл
			ОтчетФункция.Использование=(ОтчетФункция.Имя="КонечныйОстаток");
		КонецЦикла;
		
		//Уберем лишние отборы
		Для каждого ОтчетОтбор Из Отчет.ПостроительОтчета.Отбор Цикл
			ОтчетОтбор.Использование=Ложь;
		КонецЦикла;
		
		СписокТоваров=Новый СписокЗначений();
		СписокТоваров.ЗагрузитьЗначения(ТекущийДокумент.Товары.ВыгрузитьКолонку("Номенклатура"));
		//Установим фильтр по заказ-наряду
		ОтборНоменклатура=Отчет.ПостроительОтчета.Отбор.Найти("Номенклатура");
		Если ОтборНоменклатура=Неопределено Тогда
			ОтборНоменклатура=Отчет.ПостроительОтчета.Отбор.Добавить("Номенклатура","Номенклатура","Номенклатура");
		КонецЕсли; 
		ОтборНоменклатура.ВидСравнения=ВидСравнения.ВСписке;
		ОтборНоменклатура.Значение=СписокТоваров;
		ОтборНоменклатура.Использование=Истина;
		
		//ДопПоля в отдельную колонку
		Отчет.ВыводитьДополнительныеПоляВОтдельнойКолонке=Истина;
		
		ФормаОтчета=ПолучитьОбщуюФорму("Отчет");
		ФормаОтчета.ОтчетОбъект=Отчет;
		ФормаОтчета.Заголовок="Наличие на складах";
		ФормаОтчета.Открыть();
		
	КонецПроцедуры
	
	// Процедура формирует отчет НаличиеВПрайсЛистахКонтрагентов
	// по текущему документу
	Процедура орОтчетНаличиеВПрайсЛистахКонтрагентов(ТекущийДокумент) Экспорт
		
		// заполним параметры отчета
		Отчет = Отчеты.ПрайсЛистыКонтрагентов.Создать();
		Отчет.ЗаполнитьНачальныеНастройки();
		
		МетаданныеОтчета  = Отчет.Метаданные();
		ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.НаличиеВПрайсЛистахКонтрагентов;
		НастройкиВарианта = ВариантОтчета.Настройки;
		
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания",  КонецДня(ТекущаяДата()));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ПоВалютеОтчета", Истина);
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ВалютаОтчета",   ТекущийДокумент.ВалютаДокумента);
		
		СписокТоваров = Новый СписокЗначений();
		МассивНоменклатуры = ТекущийДокумент.Товары.ВыгрузитьКолонку("Номенклатура");
		ИмяРеквизитаКода = "Артикул";
		
		Для Каждого ЭлементМассива Из МассивНоменклатуры Цикл
			АртикулЗначение = дкПолучитьЗначениеКолонкиКода(ЭлементМассива,ИмяРеквизитаКода,);
			СписокТоваров.Добавить(АртикулЗначение);
		КонецЦикла;
		
		//Установим фильтр по артикулам
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Артикул", СписокТоваров, ВидСравненияКомпоновкиДанных.ВСписке);
		
		ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
		СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
		СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
		СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
		СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
		СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
		
		ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
		
	КонецПроцедуры // орОтчетНаличиеВПрайсЛистахКонтрагентов()
	
	// Процедура формирует отчет НаличиеВоВнешнихПрайсЛистахКонтрагентов
	// по текущему документу
	Процедура орОтчетНаличиеВоВнешнихПрайсЛистахКонтрагентов(ТекущийДокумент) Экспорт
		
		// заполним параметры отчета
		Отчет=Отчеты.ВнешниеПрайсЛистыКонтрагентов.Создать();
		Отчет.ВидОтчета         = Перечисления.ВидыОтчетов.Остатки;
		Отчет.ДатаКонца         = КонецДня(ТекущаяДата());
		Отчет.РежимВыводаОтчета = Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
		Отчет.РежимНастройки    = Перечисления.РежимыНастройкиОтчетов.Эксперт;
		Отчет.ИмяФормыНастроек  = "НастройкиОстатки";
		Отчет.ЗаполнитьНачальныеНастройки("ТекстЗапросаОстатки");
		
		Отчет.ВыбиратьВалюту    = Истина;
		Отчет.ВалютаОтчета      = ТекущийДокумент.ВалютаДокумента;
		
		Отчет.ИзмеренияСтроки.Очистить();
		ПолеАртикул   = Отчет.ДеревоДоступныхПолей.Строки.Найти("Артикул", "ПутьКДанным");
		ПолеПрайсЛист = Отчет.ДеревоДоступныхПолей.Строки.Найти("ПрайсЛист", "ПутьКДанным");
		
		//Измерение Элемент прайс-листа
		НоваяСтрока = Отчет.ИзмеренияСтроки.Добавить();
		НоваяСтрока.Использование = Истина;
		НоваяСтрока.ПутьКДанным   = "Артикул";
		НоваяСтрока.Представление = ПолеАртикул.Представление;
		НоваяСтрока.ТипИзмерения  = "Элементы";
		
		//Измерение Прайс-лист
		НоваяСтрока=Отчет.ИзмеренияСтроки.Добавить();
		НоваяСтрока.Использование = Истина;
		НоваяСтрока.ПутьКДанным   = "ПрайсЛист";
		НоваяСтрока.Представление = ПолеПрайсЛист.Представление;
		НоваяСтрока.ТипИзмерения  = "Элементы";
		
		//Расставим показатели
		Для Каждого ОтчетПоказатель Из Отчет.Показатели Цикл
			ОтчетПоказатель.Использование=Истина;
		КонецЦикла; 
		
		//Расставим функции
		Для Каждого ОтчетФункция Из Отчет.Функции Цикл
			ОтчетФункция.Использование=Истина;
		КонецЦикла;
		
		//Уберем лишние отборы
		Для Каждого ОтчетОтбор Из Отчет.ПостроительОтчета.Отбор Цикл
			ОтчетОтбор.Использование=Ложь;
		КонецЦикла;
		
		СписокТоваров      = Новый СписокЗначений();
		МассивНоменклатуры = ТекущийДокумент.Товары.ВыгрузитьКолонку("Номенклатура");
		ИмяРеквизитаКода   = "Артикул";
		
		Для Каждого ЭлементМассива Из МассивНоменклатуры Цикл
			АртикулЗначение = дкПолучитьЗначениеКолонкиКода(ЭлементМассива, ИмяРеквизитаКода,); //ЭтотОбъект);
			СписокТоваров.Добавить(АртикулЗначение);
		КонецЦикла;
		
		//Установим фильтр по заказ-наряду
		ОтборАртикул = Отчет.ПостроительОтчета.Отбор.Найти("Артикул");
		Если ОтборАртикул = Неопределено Тогда
			ОтборАртикул  = Отчет.ПостроительОтчета.Отбор.Добавить("Артикул", "Артикул", ПолеАртикул.Представление);
		КонецЕсли;
		ОтборАртикул.ВидСравнения  = ВидСравнения.ВСписке;
		ОтборАртикул.Значение      = СписокТоваров;
		ОтборАртикул.Использование = Истина;
		
		ФормаОтчета = ПолучитьОбщуюФорму("Отчет");
		ФормаОтчета.ОтчетОбъект = Отчет;
		ФормаОтчета.Заголовок   = "Наличие во внешних прайс-листах контрагентов";
		ФормаОтчета.Открыть();
		
	КонецПроцедуры // орОтчетНаличиеВоВнешнихПрайсЛистахКонтрагентов()
	
#КонецЕсли

// Процедура формирует отчет ИсторияАвтомобиля
// Параметры:
//	Автомобиль - автомобиль по которому устанавливается отбор
//	ДатаНачала - дата начала отчета
//	ДатаКонца  - дата конца отчета
//
Процедура орОтчетИсторияАвтомобиля(Автомобиль,ДатаНачала=Неопределено,ДатаКонца=Неопределено) Экспорт
	Если ТипЗнч(Автомобиль)=Тип("Строка") Тогда
		АвтомобильСсылка=Справочники.Автомобили.НайтиПоРеквизиту("VIN",Автомобиль);
		Если АвтомобильСсылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
		АвтомобильОбъект=АвтомобильСсылка.ПолучитьОбъект();
	ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
		Если Автомобиль.Пустая() Тогда
			Возврат;
		КонецЕсли;
		АвтомобильОбъект=Автомобиль.ПолучитьОбъект();
	ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникОбъект.Автомобили") Тогда
		АвтомобильОбъект=Автомобиль;
	Иначе
		Возврат;
	КонецЕсли; 
	
	АвтомобильОбъект.ОтчетИсторияАвтомобиля(ДатаНачала,ДатаКонца);
КонецПроцедуры // орОтчетИсторияАвтомобиля() 

// Процедура добавляет свойства и значения
Процедура орДобавитьСвойстваИЗначения(Свойство,СвойстваИЗначения)
	СвойствоСтрока=СвойстваИЗначения.Найти(Свойство,"Свойство");
	Если СвойствоСтрока=Неопределено Тогда
		СвойствоСтрока=СвойстваИЗначения.Добавить();
		СвойствоСтрока.Активность=Ложь;
		СвойствоСтрока.Свойство=Свойство;
		СвойствоСтрока.СвойствоИзРегистра=ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка();
		СвойствоСтрока.Значение=ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка();
		СвойствоСтрока.Значение=СвойствоСтрока.Свойство.ТипЗначения.ПривестиЗначение(СвойствоСтрока.Значение);
	КонецЕсли; 
КонецПроцедуры // орДобавитьСвойстваИЗначения()

// Процедура заполнения свойств и значений
Процедура орЗаполнитьСвойстваИЗначения(ОбъектСсылка,СвойстваИЗначения) Экспорт
	Если ТипЗнч(ОбъектСсылка)=Тип("СправочникСсылка.Номенклатура") И (НЕ ОбъектСсылка.ЭтоГруппа) Тогда
		Если ОбъектСсылка.ТипНоменклатуры.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Шины Тогда
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныШирина,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныВысота,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныРадиус,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныПрофиль,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныНазначение,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныКонструкция,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСезонность,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексСкорости,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексНагрузки,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСпособГерметизации,СвойстваИЗначения);
		ИначеЕсли ОбъектСсылка.ТипНоменклатуры.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Диски Тогда
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиШирина,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныРадиус,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиМодель,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиТип,СвойстваИЗначения);			
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиКоличествоОтверстий,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиРасстояниеМеждуОтверстиями,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиВылет,СвойстваИЗначения);
			орДобавитьСвойстваИЗначения(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиДиаметрСтупицы,СвойстваИЗначения);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры // орЗаполнитьСвойстваИЗначения()

// Формирование графика работ ресурсов по заказ-наряду
Функция орГрафикРаботыРесурсовПоЗаказНаряду(Ссылка) Экспорт
	// Очистим регистр ГрафикРаботыРесурсов
	НаборЗаписейГрафикРаботыРесурсов = РегистрыСведений.ГрафикРаботыРесурсов.СоздатьНаборЗаписей();
	НаборЗаписейГрафикРаботыРесурсов.ДокументОбъект = Ссылка;
	НаборЗаписейГрафикРаботыРесурсов.ОтменаПроведения();
	
	// KRAA ++ Определим флаг для заявок на ремонт
	ДополнительныеДвижения = (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") И Ссылка.ХозОперация = Справочники.ХозОперации.ПланРемонта);	
	// KRAA -- Определим флаг для заявок на ремонт
	
	НаборЗаписей = Новый ТаблицаЗначений;
	НаборЗаписей.Колонки.Добавить("Ресурс1");
	НаборЗаписей.Колонки.Добавить("Ресурс2");
	НаборЗаписей.Колонки.Добавить("Объект");
	НаборЗаписей.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НаборЗаписей.Колонки.Добавить("НачалоРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("КонецРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("Продолжительность", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("КоличествоНатуральныхЕдиниц", Новый ОписаниеТипов("Число"));
	НаборЗаписей.Колонки.Добавить("НапомнитьЗа", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	
	// SHMI ++
	НаборЗаписей.Колонки.Добавить("Авторабота");
	// SHMI --
	
	// KRAA ++
	НаборЗаписей.Колонки.Добавить("ХозОперация", Новый ОписаниеТипов("СправочникСсылка.ХозОперации"));
	// KRAA --
	
	Если НЕ ДополнительныеДвижения Тогда
		//Запланируем исполнителей и цех из шапки
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Исполнители.Исполнитель,
		|	ВЫБОР
		|		КОГДА Исполнители.Цех=&ЦехПустаяСсылка ТОГДА
		|			Исполнители.Ссылка.Цех
		|		ИНАЧЕ
		|			Исполнители.Цех
		|		КОНЕЦ 
		|	КАК Цех 
		|ИЗ
		|	Документ."+Ссылка.Метаданные().Имя+".Исполнители КАК Исполнители
		|ГДЕ
		|	Исполнители.Ссылка = &Ссылка
		|";
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("ЦехПустаяСсылка",Справочники.Цеха.ПустаяСсылка());
		Выборка=Запрос.Выполнить().Выгрузить();
		СтрокаИсполнителей=Выборка.Добавить();
		СтрокаИсполнителей.Исполнитель=Справочники.Сотрудники.ПустаяСсылка();
		СтрокаИсполнителей.Цех=Ссылка.Цех;
		ДатаНачала=Ссылка.ДатаНачала;
		Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
			Попытка
				ДатаНачала=Ссылка.ДатаСоздания;
			Исключение КонецПопытки; 
		ИначеЕсли обЗначениеНеЗаполнено(НачалоДня(ДатаНачала)) Тогда
			Попытка
				ДатаНачала=НачалоДня(Ссылка.ДатаСоздания)+Час(ДатаНачала)*60*60+Минута(ДатаНачала)*60+Секунда(ДатаНачала);
			Исключение КонецПопытки; 
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
			ДатаНачала=Ссылка.Дата;
		ИначеЕсли обЗначениеНеЗаполнено(НачалоДня(ДатаНачала)) Тогда
			ДатаНачала=НачалоДня(Ссылка.Дата)+Час(ДатаНачала)*60*60+Минута(ДатаНачала)*60+Секунда(ДатаНачала);
		КонецЕсли;
		ДатаОкончания=Ссылка.ДатаОкончания;
		Если обЗначениеНеЗаполнено(ДатаОкончания) ИЛИ обЗначениеНеЗаполнено(НачалоДня(ДатаОкончания)) Тогда
			//Если дата окончания не задана - рассчитаем время выполнения работ согласно графика
			ДатаОкончания=орРассчитатьДатуОкончанияРабот(Ссылка.Цех.ГрафикРаботы,ДатаНачала,Ссылка,Ложь);
			Если обЗначениеНеЗаполнено(ДатаОкончания) Тогда
				ДатаОкончания=Ссылка.ДатаОкончания;
			КонецЕсли; 
		КонецЕсли; 
		Для каждого СтрокаИсполнителей Из Выборка Цикл
			Если ДатаОкончания=ДатаНачала Тогда Продолжить; КонецЕсли; 
			Если НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала) = 0 Тогда
				// Начало окончание в одном дне
				СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
				СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаИсполнителей.Цех;
				СтрокаГрафикаРаботыРесурсов.Ресурс2 = СтрокаИсполнителей.Исполнитель;
				СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
				СтрокаГрафикаРаботыРесурсов.Дата = НачалоДня(ДатаНачала);
				СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
				СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101' + (ДатаОкончания - НачалоДня(ДатаОкончания));
				СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени - СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени);
				// KRAA ++ Укажем хоз.операцию
				СтрокаГрафикаРаботыРесурсов.ХозОперация = ?(ДополнительныеДвижения,Справочники.ХозОперации.ЗаявкаНаРемонт,Ссылка.ХозОперация);
				// KRAA -- Укажем хоз.операцию
			Иначе
				Разница = (НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала))/60/60/24;
				ТекущийДень = НачалоДня(ДатаНачала);
				Пока Разница >= 0 Цикл
					СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
					СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаИсполнителей.Цех;
					СтрокаГрафикаРаботыРесурсов.Ресурс2 = СтрокаИсполнителей.Исполнитель;
					СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
					СтрокаГрафикаРаботыРесурсов.Дата = ТекущийДень;					
					// Установим НачалоРабочегоВремени 
					Если ТекущийДень = НачалоДня(ДатаНачала) Тогда
						СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
					Иначе
						СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101';
					КонецЕсли;
					// Установим КонецРабочегоВремени
					Если ТекущийДень = НачалоДня(ДатаОкончания) Тогда
						СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101' + (ДатаОкончания - НачалоДня(ДатаОкончания));
					Иначе
						СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101235959';
					КонецЕсли;
					// Посчитаем Продолжительность
					СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени - СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени);
					// KRAA ++ Укажем хоз.операцию
					СтрокаГрафикаРаботыРесурсов.ХозОперация = ?(ДополнительныеДвижения,Справочники.ХозОперации.ЗаявкаНаРемонт,Ссылка.ХозОперация);
					// KRAA -- Укажем хоз.операцию
					
					ТекущийДень = ТекущийДень + 1*60*60*24;
					Разница = Разница - 1;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//KRAA ++
	// Запишем плановые показатели загрузки рабочих мест по данным табличной части
	
	Если ДополнительныеДвижения И Ссылка.Планирование.Количество() > 0 Тогда                    
		
		Для Каждого СтрокаПланирования Из Ссылка.Планирование Цикл
			
			Если СтрокаПланирования.НачалоВыполнения = '00010101'
				ИЛИ СтрокаПланирования.ОкончаниеВыполнения = '00010101'
				ИЛИ СтрокаПланирования.НачалоВыполнения = СтрокаПланирования.ОкончаниеВыполнения Тогда
				Продолжить;
			КонецЕсли;
			
			ДеньНачалаПериода = НачалоДня(СтрокаПланирования.НачалоВыполнения);
			ДеньКонцаПериода = НачалоДня(СтрокаПланирования.ОкончаниеВыполнения);
			Если ДеньНачалаПериода = ДеньКонцаПериода Тогда
				СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
				СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаПланирования.РабочееМесто;
				СтрокаГрафикаРаботыРесурсов.Ресурс2 = СтрокаПланирования.Исполнитель;
				СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
				СтрокаГрафикаРаботыРесурсов.Дата = НачалоДня(СтрокаПланирования.НачалоВыполнения);
				СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаПланирования.ОкончаниеВыполнения-СтрокаПланирования.НачалоВыполнения);
				СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = СтрокаПланирования.НачалоВыполнения;
				СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = СтрокаПланирования.ОкончаниеВыполнения;
				СтрокаГрафикаРаботыРесурсов.КоличествоНатуральныхЕдиниц = 1;
				СтрокаГрафикаРаботыРесурсов.ХозОперация = Ссылка.ХозОперация;
				
				// SHMI ++
				СтрокаГрафикаРаботыРесурсов.Авторабота = СтрокаПланирования.Авторабота;
				// SHMI --
				
			Иначе
				// рассчитаем по дням
				ЧислоДнейВыполнения = Окр((КонецДня(СтрокаПланирования.ОкончаниеВыполнения) - НачалоДня(СтрокаПланирования.НачалоВыполнения))/86400);
				НачалоВыполненияСегодня = СтрокаПланирования.НачалоВыполнения;
				ОкончаниеВыполненияСегодня = СтрокаПланирования.ОкончаниеВыполнения;
				Для СчДней =1 По ЧислоДнейВыполнения Цикл
					Если СчДней = 1 Тогда
						ПродолжительностьВДень = КонецДня(СтрокаПланирования.НачалоВыполнения)-СтрокаПланирования.НачалоВыполнения;
						НачалоВыполненияСегодня = СтрокаПланирования.НачалоВыполнения;
						ОкончаниеВыполненияСегодня = КонецДня(СтрокаПланирования.НачалоВыполнения);
					ИначеЕсли СчДней = ЧислоДнейВыполнения Тогда
						ПродолжительностьВДень = СтрокаПланирования.ОкончаниеВыполнения-НачалоДня(СтрокаПланирования.ОкончаниеВыполнения);
						НачалоВыполненияСегодня = НачалоДня(СтрокаПланирования.ОкончаниеВыполнения);
						ОкончаниеВыполненияСегодня = СтрокаПланирования.ОкончаниеВыполнения;
					Иначе	
						ПродолжительностьВДень = 86399;
						НачалоВыполненияСегодня = НачалоДня(СтрокаПланирования.НачалоВыполнения+(СчДней-1)*86400);
						ОкончаниеВыполненияСегодня = КонецДня(СтрокаПланирования.НачалоВыполнения+(СчДней-1)*86400);
					КонецЕсли;
					СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
					СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаПланирования.РабочееМесто;
					СтрокаГрафикаРаботыРесурсов.Ресурс2 = СтрокаПланирования.Исполнитель;
					СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
					СтрокаГрафикаРаботыРесурсов.Дата = НачалоДня(СтрокаПланирования.НачалоВыполнения+((СчДней-1)*86400));
					СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + ПродолжительностьВДень;
					СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = НачалоВыполненияСегодня;
					СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = ОкончаниеВыполненияСегодня;
					СтрокаГрафикаРаботыРесурсов.КоличествоНатуральныхЕдиниц = 1;
					СтрокаГрафикаРаботыРесурсов.ХозОперация = Ссылка.ХозОперация;
					
					// SHMI ++
					СтрокаГрафикаРаботыРесурсов.Авторабота = СтрокаПланирования.Авторабота;
					// SHMI --
					
				КонецЦикла;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	// KRAA --
	
	НаборЗаписейГрафикРаботыРесурсов.НаборЗаписей = НаборЗаписей;
	НаборЗаписейГрафикРаботыРесурсов.Проведение();
	
	Возврат Истина;
КонецФункции // орГрафикРаботыРесурсовПоЗаказНаряду() 

// Расчет времени окончания работ по ЗН и ЗР
//	ГрафикРаботы - График работы ресурсов, по которому требуется выполнить расчет
//  ДатаНачала - Дата начала выполнения работ по ЗН
//	Работы - ТЧ выполняемых работ или ссылка на ЗН или ЗР
//	ЗапрашиватьВремяВыполнения - Флаг запросы времени выполнения работ
// Возвращаемое значение:
//   Дата   – Дата и время окончания работ
Функция орРассчитатьДатуОкончанияРабот(ГрафикРаботы,ДатаНачала,Работы,ЗапрашиватьВремяВыполнения=Ложь) Экспорт
	ВремяВыполнения=0;
	Если ТипЗнч(Работы)=Тип("ДокументСсылка.ЗаявкаНаРемонт") ИЛИ
		ТипЗнч(Работы)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		//Если передана ссылка на документ, то получим время выполнения запросом
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	Работы.Количество*Работы.Коэффициент КАК ВремяВыполнения
		|ИЗ
		|	Документ."+Работы.Метаданные().Имя+".Работы КАК Работы
		|ГДЕ
		|	Работы.Ссылка = &Ссылка
		|	И Работы.Нормочас.Цена <> 1";
		Запрос.УстановитьПараметр("Ссылка",Работы);
		Выборка=Запрос.Выполнить().Выгрузить();
		ВремяВыполнения=Выборка.Итог("ВремяВыполнения");
	Иначе
		//Расчет ориентировочного времени выполнения работ
		Для каждого СтрокаРабот Из Работы Цикл
			Если СтрокаРабот.Нормочас.Цена<>1 Тогда
				ВремяВыполнения=ВремяВыполнения+(СтрокаРабот.Коэффициент*СтрокаРабот.Количество);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
	#Если Клиент Тогда
		Если ЗапрашиватьВремяВыполнения Тогда
			//Запросим время выполнения работ
			Если НЕ ВвестиЧисло(ВремяВыполнения,"Введите время выполнения работ в часах",10,2) Тогда
				Возврат Неопределено;
			КонецЕсли; 
		КонецЕсли; 
	#КонецЕсли
	
	//Переведем время выполнения работ в секунды
	ВремяВыполнения=ВремяВыполнения*60*60;
	
	МассивВидовИнтервалов=Новый Массив;
	МассивВидовИнтервалов.Добавить(Справочники.ВидыИнтервалов.Работа);
	МассивВидовИнтервалов.Добавить(Справочники.ВидыИнтервалов.ПустаяСсылка());
	МассивВидовДней=Новый Массив;
	МассивВидовДней.Добавить(Перечисления.ВидДня.Рабочий);
	МассивВидовДней.Добавить(Перечисления.ВидДня.Предпраздничный);
	ДатаОкончания=кпПолучитьДатуОкончанияГрафика(ГрафикРаботы,ДатаНачала,ВремяВыполнения,МассивВидовИнтервалов,МассивВидовДней,Истина);
	
	Возврат ДатаОкончания;
КонецФункции // орРассчитатьДатуОкончанияРабот() 

// Заполнение таблицы кэширования остатков номенклатуры в производстве
// Номенклатура - Табличная часть товаров
// ЗаказНаряд - ДокументСсылка на ЗаказНаряд
// Цех - СправочникСсылка на Цеха
// Момент - Момент, на какой необходимо производить расчет
Процедура орЗаполнитьОстаткиНоменклатурыВПроизводстве(Документ,Номенклатура,ЗаказНаряд=Неопределено,Цех=Неопределено,Момент=Неопределено) Экспорт
	//Получим кеш остатков номенклатуры из дополнительных свойств
	ДополнительныеСвойства=Документ.ДополнительныеСвойства;
	
	КэшОстатковНоменклатурыВПроизводствеНаМомент=Момент;
	КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=ЗаказНаряд;
	Если КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=Документы.ЗаказНаряд.ПустаяСсылка() Тогда
		КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=Неопределено;
	КонецЕсли; 
	КэшОстатковНоменклатурыВПроизводствеПоЦеху=Цех;
	Если КэшОстатковНоменклатурыВПроизводствеПоЦеху=Справочники.Цеха.ПустаяСсылка() Тогда
		КэшОстатковНоменклатурыВПроизводствеПоЦеху=Неопределено;
	КонецЕсли; 
	МассивТиповНоменклатура=Новый Массив(1); МассивТиповНоменклатура[0]=Тип("СправочникСсылка.Номенклатура");
	ОписаниеТиповНоменклатура=Новый ОписаниеТипов(МассивТиповНоменклатура);
	МассивТиповХарактеристикиНоменклатуры=Новый Массив(1); МассивТиповХарактеристикиНоменклатуры[0]=Тип("СправочникСсылка.ХарактеристикиНоменклатуры");
	ОписаниеТиповХарактеристикиНоменклатуры=Новый ОписаниеТипов(МассивТиповХарактеристикиНоменклатуры);
	МассивТиповЧисло=Новый Массив(1); МассивТиповЧисло[0]=Тип("Число");
	ОписаниеТиповЧисло=Новый ОписаниеТипов(МассивТиповЧисло);
	КэшОстатковНоменклатурыВПроизводстве=Новый ТаблицаЗначений;
	КэшОстатковНоменклатурыВПроизводстве.Колонки.Добавить("Номенклатура",ОписаниеТиповНоменклатура);
	КэшОстатковНоменклатурыВПроизводстве.Колонки.Добавить("ХарактеристикаНоменклатуры",ОписаниеТиповХарактеристикиНоменклатуры);
	КэшОстатковНоменклатурыВПроизводстве.Колонки.Добавить("Количество",ОписаниеТиповЧисло);
	
	ТекстЗапроса="ВЫБРАТЬ
	|	"+?(КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду<>Неопределено,"ТоварыВПроизводствеОстатки.ЗаказНаряд КАК ЗаказНаряд,","")+"
	|	"+?(КэшОстатковНоменклатурыВПроизводствеПоЦеху<>Неопределено,"ТоварыВПроизводствеОстатки.Цех КАК Цех,","")+"
	|	ТоварыВПроизводствеОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ТоварыВПроизводствеОстатки.КоличествоОстаток) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве.Остатки("+?(КэшОстатковНоменклатурыВПроизводствеНаМомент<>Неопределено,"&НаМомент","")+", ИСТИНА "+?(КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду<>Неопределено," И ЗаказНаряд = &ЗаказНаряд ","")+?(КэшОстатковНоменклатурыВПроизводствеПоЦеху<>Неопределено," И Цех = &Цех","")+") КАК ТоварыВПроизводствеОстатки
	|СГРУППИРОВАТЬ ПО
	|	"+?(КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду<>Неопределено,"ТоварыВПроизводствеОстатки.ЗаказНаряд,","")+"
	|	"+?(КэшОстатковНоменклатурыВПроизводствеПоЦеху<>Неопределено,"ТоварыВПроизводствеОстатки.Цех,","")+"
	|	ТоварыВПроизводствеОстатки.Номенклатура,
	|	ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры";
	Запрос=Новый Запрос;
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("НаМомент",КэшОстатковНоменклатурыВПроизводствеНаМомент);
	Запрос.УстановитьПараметр("ЗаказНаряд",КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду);
	Запрос.УстановитьПараметр("Цех",КэшОстатковНоменклатурыВПроизводствеПоЦеху);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Номенклатура.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	РезультатЗапроса=Запрос.Выполнить();
	КэшОстатковНоменклатурыВПроизводстве=РезультатЗапроса.Выгрузить().Скопировать();
	
	ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводствеНаМомент",КэшОстатковНоменклатурыВПроизводствеНаМомент);
	ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду",КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду);
	ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводствеПоЦеху",КэшОстатковНоменклатурыВПроизводствеПоЦеху);
	ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводстве",КэшОстатковНоменклатурыВПроизводстве);
КонецПроцедуры

// Получить остатки номенклатуры в производстве
// Номенклатура - СправочникСсылка на номенклатуру
// ХарактеристикаНоменклатуры - СправочникСсылка на ХарактеристикаНоменклатуры
// ЗаказНаряд - ДокументСсылка на ЗаказНаряд
// Цех - СправочникСсылка на Цеха
// Момент - Момент, на какой необходимо производить расчет
// Возвращаемое значение Число - остаток номенклатуры
Функция орПолучитьОстаткиНоменклатурыВПроизводстве(ЭтаФорма,Номенклатура,ХарактеристикаНоменклатуры,ЗаказНаряд=Неопределено,Цех=Неопределено,Момент=Неопределено) Экспорт
	//Будем считать что остаток нулевой
	Количество=0;
	
	//Получим кеш остатков номенклатуры из дополнительных свойств
	ДополнительныеСвойства=ЭтаФорма.ДополнительныеСвойства;
	
	КэшОстатковНоменклатурыВПроизводствеНаМомент=Неопределено;
	ДополнительныеСвойства.Свойство("КэшОстатковНоменклатурыВПроизводствеНаМомент",КэшОстатковНоменклатурыВПроизводствеНаМомент);
	КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=Неопределено;
	ДополнительныеСвойства.Свойство("КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду",КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду);
	КэшОстатковНоменклатурыВПроизводствеПоЦеху=Неопределено;
	ДополнительныеСвойства.Свойство("КэшОстатковНоменклатурыВПроизводствеПоЦеху",КэшОстатковНоменклатурыВПроизводствеПоЦеху);
	КэшОстатковНоменклатурыВПроизводстве=Неопределено;
	ДополнительныеСвойства.Свойство("КэшОстатковНоменклатурыВПроизводстве",КэшОстатковНоменклатурыВПроизводстве);
	Если КэшОстатковНоменклатурыВПроизводстве=Неопределено Тогда
		МассивТиповНоменклатура=Новый Массив(1); МассивТиповНоменклатура[0]=Тип("СправочникСсылка.Номенклатура");
		ОписаниеТиповНоменклатура=Новый ОписаниеТипов(МассивТиповНоменклатура);
		МассивТиповХарактеристикиНоменклатуры=Новый Массив(1); МассивТиповХарактеристикиНоменклатуры[0]=Тип("СправочникСсылка.ХарактеристикиНоменклатуры");
		ОписаниеТиповХарактеристикиНоменклатуры=Новый ОписаниеТипов(МассивТиповХарактеристикиНоменклатуры);
		МассивТиповЧисло=Новый Массив(1); МассивТиповЧисло[0]=Тип("Число");
		ОписаниеТиповЧисло=Новый ОписаниеТипов(МассивТиповЧисло);
		КэшОстатковНоменклатурыВПроизводстве=Новый ТаблицаЗначений;
		КэшОстатковНоменклатурыВПроизводстве.Колонки.Добавить("Номенклатура",ОписаниеТиповНоменклатура);
		КэшОстатковНоменклатурыВПроизводстве.Колонки.Добавить("ХарактеристикаНоменклатуры",ОписаниеТиповХарактеристикиНоменклатуры);
		КэшОстатковНоменклатурыВПроизводстве.Колонки.Добавить("Количество",ОписаниеТиповЧисло);
	КонецЕсли; 
	
	Если ЗаказНаряд=Документы.ЗаказНаряд.ПустаяСсылка() Тогда
		ТекЗаказНаряд=Неопределено;
	Иначе
		ТекЗаказНаряд=ЗаказНаряд;
	КонецЕсли; 
	Если Цех=Справочники.Цеха.ПустаяСсылка() Тогда
		ТекЦех=Неопределено;
	Иначе
		ТекЦех=Цех;
	КонецЕсли; 
	
	Если Момент<>КэшОстатковНоменклатурыВПроизводствеНаМомент ИЛИ ТекЗаказНаряд<>КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду ИЛИ ТекЦех<>КэшОстатковНоменклатурыВПроизводствеПоЦеху Тогда
		//Если момент или цех изменены, очистим таблицу кэширования
		КэшОстатковНоменклатурыВПроизводстве.Очистить();
		КэшОстатковНоменклатурыВПроизводствеНаМомент=Момент;
		КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=ТекЗаказНаряд;
		КэшОстатковНоменклатурыВПроизводствеПоЦеху=ТекЦех;
		//Поместим параметры кеша в дополнительные свойства
		ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводствеНаМомент",КэшОстатковНоменклатурыВПроизводствеНаМомент);
		ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду",КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду);
		ДополнительныеСвойства.Вставить("КэшОстатковНоменклатурыВПроизводствеПоЦеху",КэшОстатковНоменклатурыВПроизводствеПоЦеху);
	КонецЕсли;
	//Попробуем найти номенклатуру в кэше
	МассивСтрок=КэшОстатковНоменклатурыВПроизводстве.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",Номенклатура,ХарактеристикаНоменклатуры));
	Если МассивСтрок.Количество()>0 Тогда
		//Такая номенклатура есть - вернем остаток из кеша
		Количество=МассивСтрок[0].Количество;
	Иначе
		//В кэше номенклатура не найдена - заведем для нее новую запись
		НоваяСтрокаКеша=КэшОстатковНоменклатурыВПроизводстве.Добавить();
		НоваяСтрокаКеша.Номенклатура=Номенклатура;
		НоваяСтрокаКеша.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры;
		НоваяСтрокаКеша.Количество=0;
		//Сформируем запрос на получение остатков по номенклатуре
		Запрос=Новый Запрос;
		ТекстЗапроса="
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ТоварыВПроизводствеОстатки.КоличествоОстаток),0) КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыВПроизводстве.Остатки("+?(КэшОстатковНоменклатурыВПроизводствеНаМомент=Неопределено,"","&НаМомент")+",Номенклатура=&Номенклатура"+?(ХарактеристикаНоменклатуры=Неопределено,""," И ХарактеристикаНоменклатуры=&ХарактеристикаНоменклатуры")+?(КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=Неопределено,""," И ЗаказНаряд=&ЗаказНаряд")+?(КэшОстатковНоменклатурыВПроизводствеПоЦеху=Неопределено,""," И Цех=&Цех")+") КАК ТоварыВПроизводствеОстатки
		|СГРУППИРОВАТЬ ПО
		|	"+?(КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду=Неопределено,"","ТоварыВПроизводствеОстатки.ЗаказНаряд,")+"
		|	"+?(КэшОстатковНоменклатурыВПроизводствеПоЦеху=Неопределено,"","ТоварыВПроизводствеОстатки.Цех,")+"
		|	ТоварыВПроизводствеОстатки.Номенклатура,
		|	"+?(ХарактеристикаНоменклатуры=Неопределено,"","ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры")+"
		|";
		Запрос.Текст=ТекстЗапроса;
		Запрос.УстановитьПараметр("НаМомент",КэшОстатковНоменклатурыВПроизводствеНаМомент);
		Запрос.УстановитьПараметр("ЗаказНаряд",КэшОстатковНоменклатурыВПроизводствеПоЗаказНаряду);
		Запрос.УстановитьПараметр("Цех",КэшОстатковНоменклатурыВПроизводствеПоЦеху);
		Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
		//Производим выборку
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			//Есть остатки по номенклатуре
			//Запишем остатки в кеш
			НоваяСтрокаКеша.Количество=Выборка.Количество;
			Количество=Выборка.Количество;
		КонецЕсли;
		//Поместим кеш в дополнительные свойства
		ДополнительныеСвойства.Вставить("КэшОстатковКэшОстатковНоменклатурыВПроизводствевНоменклатуры",КэшОстатковНоменклатурыВПроизводстве);
	КонецЕсли; 
	
	Возврат Количество;
КонецФункции

// Функция выполняет заполнение ЗаказПокупателя на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_ЗаказПокупателя(ЭтотОбъект,Основание,Копирование)
	
	// Заполнение шапки
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	// почистим товары
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенПродажи",ЭтотОбъект.Права,,ЭтотОбъект);
	
	Если Основание.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
		Основание.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
		//Сообщить("Заказ-наряд выполнен или закрыт. Вводить заказы покупателей запрещено.");
		Возврат Истина;
	КонецЕсли;
	
	ВклНДС=Истина;
	
	// попытаемся получить реальное вхождение налогов
	Если ЭтотОбъект.ТипЦен<>Неопределено Тогда
		ВклНДС = ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
	КонецЕсли;
	
	ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ЗаказПокупателя;
	
	////////////////////////////////////////////////////////////////////////////
	//Получим табличную часть заказ-наряда и движения по этому документу
	
	// фильтр по заказу
	ДопОтбор = "";
	ОснованиеЗН = Основание.ДокументОснование;
	Если ТипЗнч(ОснованиеЗН) = Тип("ДокументСсылка.ЗаказПокупателя") И ЗначениеЗаполнено(ОснованиеЗН) Тогда
		ДопОтбор = " ИЛИ Заказ = &ЗаказОснование";
	КонецЕсли;
	
	ЗапросПоЗаказам = Новый Запрос;
	ЗапросПоЗаказам.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗаказов.Номенклатура КАК Номенклатура,
	|	ТаблицаЗаказов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ТаблицаЗаказов.Заказано) КАК Заказано
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыПоставщикамОбороты.Номенклатура КАК Номенклатура,
	|		ЗаказыПоставщикамОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЗаказыПоставщикамОбороты.ЗаказаноОстаток КАК Заказано
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам.Остатки(, ) КАК ЗаказыПоставщикамОбороты
	|	ГДЕ
	|		ЗаказыПоставщикамОбороты.ЗаказПоставщику.ДокументОснование = &ЗаказНаряд
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыПокупателей.Номенклатура,
	|		ЗаказыПокупателей.ХарактеристикаНоменклатуры,
	|		ЗаказыПокупателей.ЗаказаноОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(, Заказ.ДокументОснование = &ЗаказНаряд"+ДопОтбор+") КАК ЗаказыПокупателей
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыВПроизводствеОстатки.Номенклатура,
	|		ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры,
	|		ТоварыВПроизводствеОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыВПроизводстве.Остатки(, ЗаказНаряд = &ЗаказНаряд) КАК ТоварыВПроизводствеОстатки) КАК ТаблицаЗаказов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказов.Номенклатура,
	|	ТаблицаЗаказов.ХарактеристикаНоменклатуры";
	ЗапросПоЗаказам.УстановитьПараметр("ЗаказНаряд",     Основание);
	ЗапросПоЗаказам.УстановитьПараметр("ЗаказОснование", ОснованиеЗН);
	ТаблицаПоЗаказам = ЗапросПоЗаказам.Выполнить().Выгрузить();
	
	Запрос=Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказНарядТовары.Номенклатура КАК Номенклатура,
	|	ЗаказНарядТовары.Количество КАК Количество,
	|	ЗаказНарядТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказНарядТовары.Коэффициент КАК Коэффициент,
	|	ЗаказНарядТовары.СкладКомпании КАК СкладКомпании,
	|	ЗаказНарядТовары.Цена КАК Цена,
	|	ЗаказНарядТовары.Сумма КАК Сумма,
	|	ЗаказНарядТовары.ПроцентСкидки КАК ПроцентСкидки,
	|	ЗаказНарядТовары.СуммаСкидки КАК СуммаСкидки,
	|	ЗаказНарядТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказНарядТовары.СуммаНДС КАК СуммаНДС,
	|	ЗаказНарядТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказНарядТовары.СуммаВсего КАК СуммаВсего,
	|	ЗаказНарядТовары.СкидкаНаТовар,
	|	ЗаказНарядТовары.ПроцентСкидкиСтроки,
	|	ЗаказНарядТовары.СуммаСкидкиСтроки,
	|	ЗаказНарядТовары.Поставщик,
	|	ЗаказНарядТовары.СрокПоставкиВСтроке,
	|	ЗаказНарядТовары.КлючСтрокиПоставщика
	|ИЗ
	|	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
	|ГДЕ
	|	ЗаказНарядТовары.Ссылка = &ЗаказНаряд";
	Запрос.УстановитьПараметр("ЗаказНаряд",Основание);
	ТабЗаказНаряда=Запрос.Выполнить().Выгрузить();
	
	//Поместим в табличную часть товаров
	Для каждого СтрокаЗаказНаряда Из ТабЗаказНаряда Цикл
		НоваяСтрока = ЭтотОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗаказНаряда);
	КонецЦикла;
	
	//Уменьшим количество на величину уже заказанного
	Для Каждого СтрокаЗаказов Из ТаблицаПоЗаказам Цикл
		Заказано           = СтрокаЗаказов.Заказано;
		МассивСтрокТоваров = ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаЗаказов.Номенклатура, СтрокаЗаказов.ХарактеристикаНоменклатуры));
		Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
			ЗаказаноВЕдиницахИзмерения = Заказано/СтрокаТоваров.Коэффициент;
			ЗаказаноПоСтроке           = Мин(ЗаказаноВЕдиницахИзмерения, СтрокаТоваров.Количество);
			СтрокаТоваров.Количество   = СтрокаТоваров.Количество-ЗаказаноПоСтроке;
			ЭтотОбъект.ОбработкаРеквизита("Товары.Количество", СтрокаТоваров);
			Если СтрокаТоваров.Резерв>СтрокаТоваров.Количество Тогда
				СтрокаТоваров.Резерв=СтрокаТоваров.Количество;
			КонецЕсли;
			Заказано = Заказано-(ЗаказаноПоСтроке*СтрокаТоваров.Коэффициент);
			Если Заказано=0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	//Удалим пустые строки из заказа
	МассивСтрокТоваров = ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Количество",0));
	Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
		ЭтотОбъект.Товары.Удалить(СтрокаТоваров);
	КонецЦикла;
	
	//Попробуем автоматически предложить сразу зарезервировать товар на складе
	ТабЗаказНаряда.Свернуть("СкладКомпании");
	Если ТабЗаказНаряда.Количество()=1 Тогда
		//У нас только один склад - попробуем заполнить резерв
		ЭтотОбъект.ХозОперация   = Справочники.ХозОперации.ЗаказРезервированиеПокупателя;
		ЭтотОбъект.СкладКомпании = ТабЗаказНаряда[0].СкладКомпании;
		ЭтотОбъект.ЗаполнитьРезервОстатками();
		Резерв = ЭтотОбъект.Товары.Итог("Резерв");
		Если Резерв=0 Тогда
			//Товара нет в наличии - будем только заказывать
			ЭтотОбъект.ХозОперация   = Справочники.ХозОперации.ЗаказПокупателя;
		КонецЕсли;
	КонецЕсли;
	
	//Перерассчитаем цены и ставки
	Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
		ЭтотОбъект.ОбработкаРеквизита("Товары.Цена",СтрокаТоваров);
	КонецЦикла;
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	//////////////////////////////////////////////////////////////////////////////		
	//Заполним ПроцентПредоплаты для нового документа
	
	СрокПоставкиДней = обПраво("СрокПоставкиПокупателюПоУмолчанию",ЭтотОбъект.Права,,ЭтотОбъект);
	#Если Клиент Тогда
		ЭтотОбъект.СрокПоставки = РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
	#Иначе
		ЭтотОбъект.СрокПоставки = КонецДня(ТекущаяДата());
	#КонецЕсли
	ЭтотОбъект.СрокПоставки = ЭтотОбъект.СрокПоставки+СрокПоставкиДней*60*60*24;
	//Заполнение предоплаты
	СуммаСоСкидкой = ЭтотОбъект.Товары.Итог("СуммаВсего");
	ЭтотОбъект.ПроцентПредоплаты = обПраво("МинимальныйПроцентПредоплаты", ЭтотОбъект.Права,,ЭтотОбъект);
	ЭтотОбъект.СуммаПредоплаты   = СуммаСоСкидкой*ЭтотОбъект.ПроцентПредоплаты/100;
	
	Возврат Истина;
	
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_ЗаказПокупателя()

// Функция выполняет заполнение СнятиеРезервовЗаказовПокупателя на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_СнятиеРезервовЗаказовПокупателя(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	
	ЭтотОбъект.Контрагент          = Основание.Контрагент;
	ЭтотОбъект.ХозОперация         = Справочники.ХозОперации.СнятиеРезерваПокупателя;
	ЭтотОбъект.КорректировкаЗаказа = Ложь;
	
	// фильтр по заказу
	ДопОтбор = "";
	ОснованиеЗН = Основание.ДокументОснование;
	Если ТипЗнч(ОснованиеЗН) = Тип("ДокументСсылка.ЗаказПокупателя") И ЗначениеЗаполнено(ОснованиеЗН) Тогда
		ДопОтбор = " ИЛИ Заказ = &ЗаказОснование";
	КонецЕсли;
	
	ЭтотОбъект.Товары.Очистить();
	//А теперь откорректируем ТЧ на предмет того, что в ней должны остаться только текущие резервы
	РазрешениеСнятияРезервов = обПраво("РазрешениеСнятияРезервов", ЭтотОбъект.Права,, ЭтотОбъект);
	Если НЕ РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.Запрещено Тогда
		ИмяРезерва = ?(РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.Разрешено, "РезервОстаток", "РезервСвободныйОстаток");
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.СкладКомпании КАК МестоРазмещения,
		|	ЗаказыПокупателейОстатки.Заказ КАК ЗаказПокупателя,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЕСТЬNULL(ЗаказыПокупателейОстатки."+ИмяРезерва+", 0) КАК Количество
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&НаМомент,Заказ.ДокументОснование = &ЗаказНаряд" + ДопОтбор + ") КАК ЗаказыПокупателейОстатки
		|";
		Запрос.УстановитьПараметр("НаМомент",       ТекущаяДата());
		Запрос.УстановитьПараметр("ЗаказНаряд",     ЭтотОбъект.ДокументОснование);
		Запрос.УстановитьПараметр("ЗаказОснование", ОснованиеЗН);
		
		ЭтотОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
		Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
			ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров);
		КонецЦикла;
	КонецЕсли;
	
	Основание=Неопределено;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_СнятиеРезервовЗаказовПокупателя()

// Функция выполняет заполнение СчетНаОплату на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_СчетНаОплату(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Если НЕ обЗначениеНеЗаполнено(Основание.Контрагент) Тогда
			Если Основание.Контрагент<>ЭтотОбъект.Контрагент Тогда
				ЭтотОбъект.Контрагент=Основание.Контрагент;
				ЭтотОбъект.ОбработкаРеквизита("Контрагент");
			КонецЕсли; 
		ИначеЕсли НЕ обЗначениеНеЗаполнено(Основание.Заказчик) Тогда
			Если Основание.Заказчик<>ЭтотОбъект.Контрагент Тогда
				ЭтотОбъект.Контрагент=Основание.Заказчик;
				ЭтотОбъект.ОбработкаРеквизита("Контрагент");
			КонецЕсли; 
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		Если НЕ обЗначениеНеЗаполнено(Основание.Контрагент) Тогда
			Если Основание.Контрагент<>ЭтотОбъект.Контрагент Тогда
				ЭтотОбъект.Контрагент=Основание.Контрагент;
				ЭтотОбъект.ОбработкаРеквизита("Контрагент");
			КонецЕсли; 
		ИначеЕсли ТипЗнч(Основание.Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда
			Если Основание.Заказчик<>ЭтотОбъект.Контрагент Тогда
				ЭтотОбъект.Контрагент=Основание.Заказчик;
				ЭтотОбъект.ОбработкаРеквизита("Контрагент");
			КонецЕсли;
		ИначеЕсли ТипЗнч(Основание.Заказчик)=Тип("Строка") Тогда
			Запрос=Новый Запрос("ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Контрагент
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Наименование ПОДОБНО &Наименование
			|	И Контрагенты.ЭтоГруппа = ЛОЖЬ
			|УПОРЯДОЧИТЬ ПО
			|	Контрагенты.Наименование");
			Запрос.УстановитьПараметр("Наименование",СокрЛП(Основание.Заказчик)+"%");
			ВыборкаЗапроса=Запрос.Выполнить().Выбрать();
			Если ВыборкаЗапроса.Следующий() Тогда
				Если ВыборкаЗапроса.Контрагент<>ЭтотОбъект.Контрагент Тогда
					ЭтотОбъект.Контрагент=ВыборкаЗапроса.Контрагент;
					ЭтотОбъект.ОбработкаРеквизита("Контрагент");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// перезаполним расчетный счет в соответствии с организацией
	Если НЕ ЭтотОбъект.Организация.Пустая() И (ЭтотОбъект.РасчетныйСчетОрганизации.Пустая()
		ИЛИ ЭтотОбъект.РасчетныйСчетОрганизации.Владелец <> ЭтотОбъект.Организация) Тогда
		ЭтотОбъект.РасчетныйСчетОрганизации=ЭтотОбъект.Организация.ОсновнойБанковскийСчет;
	КонецЕсли;
	
	ЭтотОбъект.СкидкаНаценка=Основание.СкидкаНаценка;
	
	ЭтотОбъект.Товары.Очистить();
	
	Для Каждого СтрокаТоваров Из Основание.Товары Цикл
		НоваяСтрокаСчета=ЭтотОбъект.Товары.Добавить();
		НоваяСтрокаСчета.Номенклатура=СтрокаТоваров.Номенклатура;
		НоваяСтрокаСчета.Количество=СтрокаТоваров.Количество;
		НоваяСтрокаСчета.ЕдиницаИзмерения=СтрокаТоваров.ЕдиницаИзмерения;
		НоваяСтрокаСчета.Коэффициент=СтрокаТоваров.Коэффициент;
		НоваяСтрокаСчета.Цена=СтрокаТоваров.Цена;
		НоваяСтрокаСчета.Сумма=СтрокаТоваров.Сумма;
		НоваяСтрокаСчета.СтавкаНДС=СтрокаТоваров.СтавкаНДС;
		НоваяСтрокаСчета.СуммаНДС=СтрокаТоваров.СуммаНДС;
		НоваяСтрокаСчета.ПроцентСкидки=СтрокаТоваров.ПроцентСкидки;
		НоваяСтрокаСчета.СуммаСкидки=СтрокаТоваров.СуммаСкидки;
		НоваяСтрокаСчета.СкидкаНаТовар=СтрокаТоваров.СкидкаНаТовар;
		НоваяСтрокаСчета.ПроцентСкидкиСтроки=СтрокаТоваров.ПроцентСкидкиСтроки;
		НоваяСтрокаСчета.СуммаСкидкиСтроки=СтрокаТоваров.СуммаСкидкиСтроки;
		НоваяСтрокаСчета.СуммаВсего=СтрокаТоваров.СуммаВсего;
		НоваяСтрокаСчета.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
		НоваяСтрокаСчета.Поставщик=СтрокаТоваров.Поставщик;
		НоваяСтрокаСчета.СрокПоставкиВСтроке=СтрокаТоваров.СрокПоставкиВСтроке;
		НоваяСтрокаСчета.КлючСтрокиПоставщика=СтрокаТоваров.КлючСтрокиПоставщика;
	КонецЦикла;
	
	ТребуетсяПересчетЦен = Ложь;
	ЦенаВключаетНДС = ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
	Если Основание.ТипЦен.ЦенаВключаетНДС <> Основание.ТипЦенРабот.ЦенаВключаетНДС Тогда
		ТребуетсяПересчетЦен = Истина;
	КонецЕсли;
	
	Для Каждого СтрокаРабот Из Основание.Работы Цикл
		НоваяСтрокаСчета=ЭтотОбъект.Товары.Добавить();
		НоваяСтрокаСчета.Номенклатура=СтрокаРабот.Работа;
		НоваяСтрокаСчета.Количество=СтрокаРабот.Количество;
		НоваяСтрокаСчета.ЕдиницаИзмерения=СтрокаРабот.Нормочас;
		НоваяСтрокаСчета.Коэффициент=СтрокаРабот.Коэффициент;
		НоваяСтрокаСчета.Цена=СтрокаРабот.Цена;
		НоваяСтрокаСчета.Сумма=СтрокаРабот.Сумма;
		НоваяСтрокаСчета.СтавкаНДС=СтрокаРабот.СтавкаНДС;
		НоваяСтрокаСчета.СуммаНДС=СтрокаРабот.СуммаНДС;
		НоваяСтрокаСчета.ПроцентСкидки=СтрокаРабот.ПроцентСкидки;
		НоваяСтрокаСчета.СуммаСкидки=СтрокаРабот.СуммаСкидки;
		НоваяСтрокаСчета.СкидкаНаТовар=СтрокаРабот.СкидкаНаТовар;
		НоваяСтрокаСчета.ПроцентСкидкиСтроки=СтрокаРабот.ПроцентСкидкиСтроки;
		НоваяСтрокаСчета.СуммаСкидкиСтроки=СтрокаРабот.СуммаСкидкиСтроки;
		НоваяСтрокаСчета.СуммаВсего=СтрокаРабот.СуммаВсего;
		НоваяСтрокаСчета.ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		Если ТребуетсяПересчетЦен Тогда
			ДопПараметры = Новый Структура("ТипЦен", Основание.ТипЦенРабот);
			СуммаБезСкидок = дкПолучитьСуммуСтрокиБезСкидки(Основание,СтрокаРабот,ДопПараметры);
			Если ЦенаВключаетНДС Тогда
				НоваяСтрокаСчета.Сумма = СуммаБезСкидок;
			Иначе
				НоваяСтрокаСчета.Сумма = Окр(СуммаБезСкидок*100 / (100 + НоваяСтрокаСчета.СтавкаНДС.Ставка),2);
			КонецЕсли;
			дкОбработкаРеквизита(ЭтотОбъект, "Товары.Сумма", НоваяСтрокаСчета);
		КонецЕсли;
		
	КонецЦикла;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_СчетНаОплату()

// Функция выполняет заполнение ПриходныйКассовыйОрдер на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_ПриходныйКассовыйОрдер(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.Товары.Очистить();
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	
	//Вычислим сумму задолженности по заказ-наряду
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот КАК Сумма,
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрОборот КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
	|				И Контрагент = &Контрагент
	|				И (Сделка = &ЗаказНаряд
	|					ИЛИ Сделка.ДокументОснование = &ЗаказНаряд
	|					ИЛИ Сделка = &Сделка)) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
	|ГДЕ
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.Регистратор <> &ЗаказНаряд";
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Контрагент",ЭтотОбъект.ДокументОснование.Контрагент);
	Запрос.УстановитьПараметр("ЗаказНаряд",ЭтотОбъект.ДокументОснование);
	Запрос.УстановитьПараметр("Сделка",ЭтотОбъект.ДокументОснование.ДокументОснование);
	тзДолги=Запрос.Выполнить().Выгрузить();
	#Если Клиент Тогда
		ПересчетНаДату=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
	#Иначе
		ПересчетНаДату=ТекущаяДата();
	#КонецЕсли
	Если ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ЭтотОбъект.ВалютаДокумента Тогда
		Долг=тзДолги.Итог("Сумма");
	Иначе
		Долг=обПересчет(тзДолги.Итог("Сумма"),ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ПересчетНаДату,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
	КонецЕсли;
	СуммаЗаказНаряда=обПересчет(ЭтотОбъект.ДокументОснование.СуммаДокумента,ЭтотОбъект.ДокументОснование.ВалютаДокумента,ПересчетНаДату,ЭтотОбъект.ВалютаДокумента,ПересчетНаДату);
	ЭтотОбъект.СуммаДокумента = Долг + СуммаЗаказНаряда;
	ЭтотОбъект.ОбработкаРеквизита("СуммаДокумента");
	
	//ЭтотОбъект.СуммаНДС=ЭтотОбъект.СуммаНДС+ЭтотОбъект.ДокументОснование.Товары.Итог("СуммаНДС")+ЭтотОбъект.ДокументОснование.Работы.Итог("СуммаНДС");
	//Статья ДДС - Оплата от покупателя
	ЭтотОбъект.СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;
	ЭтотОбъект.АвтоЗакрытиеСделок=ЭтотОбъект.ДокументОснование.АвтоЗакрытиеСделок;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_ПриходныйКассовыйОрдер()

// Функция выполняет заполнение ЧекНаОплату на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);
	
	ЭтотОбъект.Товары.Очистить();
	Для Каждого СтрокаТоваров Из Основание.Товары Цикл
		НоваяСтрокаСчета=ЭтотОбъект.Товары.Добавить();
		НоваяСтрокаСчета.Номенклатура=СтрокаТоваров.Номенклатура;
		НоваяСтрокаСчета.Количество=СтрокаТоваров.Количество;
		НоваяСтрокаСчета.ЕдиницаИзмерения=СтрокаТоваров.ЕдиницаИзмерения;
		НоваяСтрокаСчета.Коэффициент=СтрокаТоваров.Коэффициент;
		НоваяСтрокаСчета.Цена=СтрокаТоваров.Цена;
		НоваяСтрокаСчета.Сумма=СтрокаТоваров.Сумма;
		НоваяСтрокаСчета.СтавкаНДС=СтрокаТоваров.СтавкаНДС;
		НоваяСтрокаСчета.СуммаНДС=СтрокаТоваров.СуммаНДС;
		НоваяСтрокаСчета.ПроцентСкидки=СтрокаТоваров.ПроцентСкидки;
		НоваяСтрокаСчета.СуммаСкидки=СтрокаТоваров.СуммаСкидки;
		НоваяСтрокаСчета.СкидкаНаТовар=СтрокаТоваров.СкидкаНаТовар;
		НоваяСтрокаСчета.ПроцентСкидкиСтроки=СтрокаТоваров.ПроцентСкидкиСтроки;
		НоваяСтрокаСчета.СуммаСкидкиСтроки=СтрокаТоваров.СуммаСкидкиСтроки;
		НоваяСтрокаСчета.СуммаВсего=СтрокаТоваров.СуммаВсего;
		НоваяСтрокаСчета.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
		НоваяСтрокаСчета.СуммаСкидкиБонусами=СтрокаТоваров.СуммаСкидкиБонусами;
		Попытка
			НоваяСтрокаСчета.ПризнакПредметаРасчета = НоваяСтрокаСчета.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
		Исключение
		КонецПопытки;
		
	КонецЦикла;
	
	ТребуетсяПересчетЦен = Ложь;
	ЦенаВключаетНДС = ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
	Если Основание.ТипЦен.ЦенаВключаетНДС <> Основание.ТипЦенРабот.ЦенаВключаетНДС Тогда
		ТребуетсяПересчетЦен = Истина;
	КонецЕсли;
	
	Для Каждого СтрокаТоваров Из Основание.Работы Цикл
		НоваяСтрокаСчета=ЭтотОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСчета, СтрокаТоваров);
		Если ТребуетсяПересчетЦен Тогда
			ДопПараметры = Новый Структура("ТипЦен", Основание.ТипЦенРабот);
			СуммаБезСкидок = дкПолучитьСуммуСтрокиБезСкидки(Основание,СтрокаТоваров,ДопПараметры);
			Если ЦенаВключаетНДС Тогда
				НоваяСтрокаСчета.Сумма = СуммаБезСкидок;
			Иначе
				НоваяСтрокаСчета.Сумма = Окр(СуммаБезСкидок*100 / (100 + НоваяСтрокаСчета.СтавкаНДС.Ставка),2);
			КонецЕсли;
			дкОбработкаРеквизита(ЭтотОбъект, "Товары.Сумма", НоваяСтрокаСчета);
		КонецЕсли;
		НоваяСтрокаСчета.Номенклатура=СтрокаТоваров.Работа.Номенклатура;
		НоваяСтрокаСчета.ЕдиницаИзмерения=СтрокаТоваров.Работа.Номенклатура.ОсновнаяЕдиницаИзмерения;
		Попытка
			НоваяСтрокаСчета.ПризнакПредметаРасчета = НоваяСтрокаСчета.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
		Исключение
		КонецПопытки;
		//Создадим характеристику для автоработы
		АвтоработаТипНоменклатуры = Справочники.Номенклатура.Авторабота.ТипНоменклатуры;
		
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Строка(СтрокаТоваров.Работа), Истина,, АвтоработаТипНоменклатуры);
		Если ХарактеристикаНоменклатуры.Пустая() Тогда
			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
			ХарактеристикаНоменклатуры.УстановитьНовыйКод();
			ХарактеристикаНоменклатуры.Владелец=АвтоработаТипНоменклатуры;
			ХарактеристикаНоменклатуры.Наименование=Строка(СтрокаТоваров.Работа);
			ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
			Попытка
				ХарактеристикаНоменклатуры.Записать();
			Исключение
				Сообщить("Ошибка создания новой характеристики автоработы: "+ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;
		НоваяСтрокаСчета.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
	КонецЦикла;
	
	//Подсчет новой суммы документа назначения
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот КАК Сумма,
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрОборот КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
	|				И Контрагент = &Контрагент
	|				И (Сделка = &ЗаказНаряд
	|					ИЛИ Сделка.ДокументОснование = &ЗаказНаряд
	|					ИЛИ Сделка = &Сделка)) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
	|ГДЕ
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.Регистратор <> &ЗаказНаряд";
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Контрагент",ЭтотОбъект.ДокументОснование.Контрагент);
	Запрос.УстановитьПараметр("ЗаказНаряд",ЭтотОбъект.ДокументОснование);
	Запрос.УстановитьПараметр("Сделка", ЭтотОбъект.ДокументОснование.ДокументОснование);
	
	// 02.02.2025 - О2 API - Коваль А.Д.
	Если ТИПЗНЧ(ЭтотОбъект.ДокументОснование.ДокументОснование) = тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		ЭтоО2Заказ = О2_API_ОбщийМодуль.ЯвляетсяЛиЗаказО2(ЭтотОбъект.ДокументОснование.ДокументОснование);
		Если ЭтоО2Заказ = Истина Тогда
			// Нужно найти счет на оплату на основании заявки на ремонт и на основании данного счета найти предоплату
			СсылкаСчетаНаОплату = Документы.СчетНаОплату.НайтиПоРеквизиту("ДокументОснование", ЭтотОбъект.ДокументОснование.ДокументОснование); // У нас может быть только один счет
			// Меняем запрос
			Запрос.УстановитьПараметр("Сделка", СсылкаСчетаНаОплату);
		КонецЕсли;
	КонецЕсли;
	// 02.02.2025 - О2 API - Коваль А.Д.
	
	тзДолги=Запрос.Выполнить().Выгрузить();
	#Если Клиент Тогда
		ПересчетНаДату=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
	#Иначе
		ПересчетНаДату=ТекущаяДата();
	#КонецЕсли
	Если ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ЭтотОбъект.ВалютаДокумента Тогда
		Долг=тзДолги.Итог("Сумма");
	Иначе
		Долг=обПересчет(тзДолги.Итог("Сумма"),ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ПересчетНаДату,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
	КонецЕсли;
	СуммаЗаказНаряда=обПересчет(ЭтотОбъект.ДокументОснование.СуммаДокумента,ЭтотОбъект.ДокументОснование.ВалютаДокумента,ПересчетНаДату,ЭтотОбъект.ВалютаДокумента,ПересчетНаДату);
	ЭтотОбъект.СуммаДокумента = Долг + СуммаЗаказНаряда;
	//ЭтотОбъект.ОбработкаРеквизита("СуммаДокумента");
	
	Возврат Истина;
	
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_ЧекНаОплату()

// Функция выполняет заполнение ВозвратОтПокупателя на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_ВозвратОтПокупателя(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	
	ЭтотОбъект.Товары.Очистить();
	
	//Получим список деталей, израсходованных по ЗН
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.Партия,
	|	СУММА(ТоварыВПроизводстве.Количество) КАК Количество,
	|	СУММА(ТоварыВПроизводстве.СуммаУпр) КАК СуммаУпр,
	|	ТоварыВПроизводстве.ГТД
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыВПроизводстве.Партия,
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.ГТД";
	Запрос.УстановитьПараметр("Регистратор",ЭтотОбъект.ДокументОснование);
	ВыборкаДеталиВПроизводстве=Запрос.Выполнить().Выбрать();
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ЗаказНарядТовары.Номенклатура,
	|	ЗаказНарядТовары.Количество,
	|	ЗаказНарядТовары.ЕдиницаИзмерения,
	|	ЗаказНарядТовары.Коэффициент,
	|	ЗаказНарядТовары.Цена,
	|	ЗаказНарядТовары.Сумма,
	|	ЗаказНарядТовары.СтавкаНДС,
	|	ЗаказНарядТовары.СуммаНДС,
	|	ЗаказНарядТовары.ПроцентСкидки,
	|	ЗаказНарядТовары.СуммаСкидки,
	|	ЗаказНарядТовары.ХарактеристикаНоменклатуры,
	|	ЗаказНарядТовары.СуммаВсего,
	|	ЗаказНарядТовары.СкидкаНаТовар,
	|	ЗаказНарядТовары.ПроцентСкидкиСтроки,
	|	ЗаказНарядТовары.СуммаСкидкиСтроки
	|ИЗ
	|	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
	|ГДЕ
	|	ЗаказНарядТовары.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.ДокументОснование);
	ВыборкаДеталиЗаказНаряда=Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаДеталиВПроизводстве.Следующий() Цикл
		СтрокаТоваров=ЭтотОбъект.Товары.Добавить();
		СтрокаТоваров.Номенклатура=ВыборкаДеталиВПроизводстве.Номенклатура;
		СтрокаТоваров.ХарактеристикаНоменклатуры=ВыборкаДеталиВПроизводстве.ХарактеристикаНоменклатуры;
		ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров,,);
		СтрокаТоваров.Количество=ВыборкаДеталиВПроизводстве.Количество;
		ЭтотОбъект.ОбработкаРеквизита("Товары.Количество",СтрокаТоваров,,);
		ВыборкаДеталиЗаказНаряда.Сбросить();
		Если ВыборкаДеталиЗаказНаряда.НайтиСледующий(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ВыборкаДеталиВПроизводстве.Номенклатура,ВыборкаДеталиВПроизводстве.ХарактеристикаНоменклатуры)) Тогда
			Количество=ВыборкаДеталиЗаказНаряда.Количество*ВыборкаДеталиЗаказНаряда.Коэффициент;
			СтрокаТоваров.Цена=ВыборкаДеталиЗаказНаряда.Цена;
			СтрокаТоваров.Сумма=ВыборкаДеталиЗаказНаряда.Сумма*СтрокаТоваров.Количество/Количество;
			//СтрокаТоваров.ПроцентСкидки=ВыборкаДеталиЗаказНаряда.ПроцентСкидки;
			//СтрокаТоваров.СуммаСкидки=ВыборкаДеталиЗаказНаряда.СуммаСкидки*СтрокаТоваров.Количество/Количество;
			СтрокаТоваров.СуммаВсего=ВыборкаДеталиЗаказНаряда.СуммаВсего*СтрокаТоваров.Количество/Количество;
			СтрокаТоваров.СтавкаНДС=ВыборкаДеталиЗаказНаряда.СтавкаНДС;
			СтрокаТоваров.СуммаНДС=ВыборкаДеталиЗаказНаряда.СуммаНДС*СтрокаТоваров.Количество/Количество;
			// перенос скидок
			СтрокаТоваров.ПроцентСкидки=ВыборкаДеталиЗаказНаряда.ПроцентСкидки;
			ЭтотОбъект.ОбработкаРеквизита("Товары.ПроцентСкидки",СтрокаТоваров);
			
			СтрокаТоваров.СкидкаНаТовар=ВыборкаДеталиЗаказНаряда.СкидкаНаТовар;
			СтрокаТоваров.ПроцентСкидкиСтроки=ВыборкаДеталиЗаказНаряда.ПроцентСкидкиСтроки;
			ЭтотОбъект.ОбработкаРеквизита("Товары.ПроцентСкидкиСтроки",СтрокаТоваров);
		Иначе
			СтрокаТоваров.Цена=0;
			СтрокаТоваров.Сумма=0;
			СтрокаТоваров.ПроцентСкидки=0;
			СтрокаТоваров.СуммаСкидки=0;
			СтрокаТоваров.СуммаВсего=0;
			//СтрокаТоваров.СтавкаНДС=;
			СтрокаТоваров.СуммаНДС=0;
		КонецЕсли; 
		СтрокаТоваров.ДокументПродажи=ЭтотОбъект.ДокументОснование;
		СтрокаТоваров.Партия=ВыборкаДеталиВПроизводстве.Партия;
		СтрокаТоваров.ГТД=ВыборкаДеталиВПроизводстве.ГТД;
		СтрокаТоваров.Себестоимость=обПересчет(ВыборкаДеталиВПроизводстве.СуммаУпр,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ЭтотОбъект.КурсВалютыУпр,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
	КонецЦикла;
	
	Основание=Неопределено;
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_ВозвратОтПокупателя()

// Функция выполняет заполнение СписаниеТоваров на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_СписаниеТоваров(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.ХозОперация=Справочники.ХозОперации.СписаниеВПроизводство;
	ЭтотОбъект.ОбработкаРеквизита("ХозОперация");
	ЭтотОбъект.Контрагент=Справочники.Контрагенты.ПустаяСсылка();
	ЭтотОбъект.ОбработкаРеквизита("Контрагент");
	ЭтотОбъект.ДоговорВзаиморасчетов=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	ЭтотОбъект.ОбработкаРеквизита("ДоговорВзаиморасчетов");
	
	Основание=Неопределено;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_СписаниеТоваров()

// Функция выполняет заполнение ЗаказПоставщику на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_ЗаказНаряд_ЗаказПоставщику(ЭтотОбъект,Основание,Копирование)
	// Заполнение шапки
	//ЭтотОбъект.Автор=ЭтотОбъект.Права["Пользователь"];
	//ЭтотОбъект.ДокументОснование = Основание.Ссылка;
	//ЭтотОбъект.ВалютаДокумента = Основание.ВалютаДокумента;
	//ЭтотОбъект.КурсДокумента = Основание.КурсДокумента;
	//ЭтотОбъект.Организация = Основание.Организация;
	//ЭтотОбъект.ПодразделениеКомпании = Основание.ПодразделениеКомпании;
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.Товары.Очистить();
	ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенЗакупки",ЭтотОбъект.Права,,ЭтотОбъект);
	Если ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказПоставщику") Тогда
		ЭтотОбъект.ХозОперация=Справочники.ХозОперации.ЗаказПоставщику;	
	ИначеЕсли ТипЗнч(ЭтотОбъект)=Тип("ДокументОбъект.ЗаказВнутренний") Тогда
		ЭтотОбъект.ХозОперация=Справочники.ХозОперации.ЗаказВнутренний;	
	КонецЕсли;
	//////////////////////////////////////////////////////////////////////////////
	//Получим табличную часть заказ-наряда и движения по этому документу
	
	ДопОтбор = "";
	ОснованиеЗН = Основание.ДокументОснование;
	ЗапросПоЗаказам = Новый Запрос;
	Если ТипЗнч(ОснованиеЗН) = Тип("ДокументСсылка.ЗаказПокупателя") И ЗначениеЗаполнено(ОснованиеЗН) Тогда
		ДопОтбор = " ИЛИ Заказ = &ЗаказОснование";
		ЗапросПоЗаказам.УстановитьПараметр("ЗаказОснование", ОснованиеЗН);
	КонецЕсли;
	
	СтруктураОтбораТоваров = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	ЗапросПоЗаказам.Текст = "ВЫБРАТЬ
	|	ОбъединенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ОбъединенныйЗапрос.Заказано) КАК Заказано
	|ИЗ (ВЫБРАТЬ
	|		ЗаказыПоставщикамОбороты.Номенклатура КАК Номенклатура,
	|		ЗаказыПоставщикамОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЗаказыПоставщикамОбороты.ЗаказаноОстаток КАК Заказано
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам.Остатки(,) КАК ЗаказыПоставщикамОбороты
	|	ГДЕ
	|		ЗаказыПоставщикамОбороты.ЗаказПоставщику.ДокументОснование = &ЗаказНаряд
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыПокупателей.Номенклатура,
	|		ЗаказыПокупателей.ХарактеристикаНоменклатуры,
	|		ЗаказыПокупателей.ЗаказаноОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(, Заказ.ДокументОснование = &ЗаказНаряд"+ДопОтбор+") КАК ЗаказыПокупателей
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыВПроизводствеОстатки.Номенклатура,
	|		ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры,
	|		ТоварыВПроизводствеОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыВПроизводстве.Остатки(, ЗаказНаряд=&ЗаказНаряд) КАК ТоварыВПроизводствеОстатки
	|) КАК ОбъединенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ОбъединенныйЗапрос.Номенклатура,
	|	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры";
	ЗапросПоЗаказам.УстановитьПараметр("ЗаказНаряд", Основание);
	ТаблицаПоЗаказам = ЗапросПоЗаказам.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТоваров.Коэффициент КАК Коэффициент,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказНарядТовары.Номенклатура КАК Номенклатура,
	|		ЗаказНарядТовары.Количество КАК Количество,
	|		ЗаказНарядТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказНарядТовары.Коэффициент КАК Коэффициент,
	|		ЗаказНарядТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|	ИЗ
	|		Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
	|	ГДЕ
	|		ЗаказНарядТовары.Ссылка = &ЗаказНаряд И 
	|		(НЕ ЗаказНарядТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказНарядМатериалы.Номенклатура КАК Номенклатура,
	|		ЗаказНарядМатериалы.Количество КАК Количество,
	|		ЗаказНарядМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказНарядМатериалы.Коэффициент КАК Коэффициент,
	|		ЗаказНарядМатериалы.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|	ИЗ
	|		Документ.ЗаказНаряд.Материалы КАК ЗаказНарядМатериалы
	|	ГДЕ
	|		ЗаказНарядМатериалы.Ссылка = &ЗаказНаряд И 
	|		(НЕ ЗаказНарядМатериалы.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга))) КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.ЕдиницаИзмерения,
	|	ТаблицаТоваров.Коэффициент,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры
	|";
	Запрос.УстановитьПараметр("ЗаказНаряд",Основание);
	ВидНоменклатурыУслуги = Перечисления.ВидыНоменклатуры.Услуга;
	
	ТабЗаказНаряда = Запрос.Выполнить().Выгрузить();
	
	// Получим остатки запасов
	ТаблицаБезХарактеристик = ТабЗаказНаряда.СкопироватьКолонки("Номенклатура");
	ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	МассивРежимовСписания = Новый Массив;
	МассивРежимовСписания.Добавить(Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание);
	МассивРежимовСписания.Добавить(Перечисления.РежимыАвтоСписанияХарактеристик.ПустаяСсылка());
	
	МассивТоваров             = Новый Массив;
	МассивХарактеристик       = Новый Массив;
	МассивТоваровАвтоСписание = Новый Массив;
	Для Каждого ТекЭлемент Из ТабЗаказНаряда Цикл
		Если ТекЭлемент.ХарактеристикаНоменклатуры = ПустаяХарактеристика И 
			МассивРежимовСписания.Найти(ТекЭлемент.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик) = Неопределено Тогда
			Если МассивТоваровАвтоСписание.Найти(ТекЭлемент.Номенклатура) = Неопределено Тогда
				МассивТоваровАвтоСписание.Добавить(ТекЭлемент.Номенклатура);
			КонецЕсли;
		Иначе
			Если МассивТоваров.Найти(ТекЭлемент.Номенклатура) = Неопределено Тогда
				МассивТоваров.Добавить(ТекЭлемент.Номенклатура);
			КонецЕсли;
			Если МассивХарактеристик.Найти(ТекЭлемент.ХарактеристикаНоменклатуры) = Неопределено Тогда
				МассивХарактеристик.Добавить(ТекЭлемент.ХарактеристикаНоменклатуры);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЕстьАвтоСписаниеХарактеристик = МассивТоваровАвтоСписание.Количество()>0;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток
	|	КОНЕЦ КАК Остаток
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки
	|ГДЕ
	|	(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток)>0" + ?(ЕстьАвтоСписаниеХарактеристик, "
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	|	Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, Номенклатура В (&НоменклатураАвтоСписание)) КАК ОстаткиТоваровКомпанииОстатки
	|ГДЕ
	|	(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток)>0", "");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Номенклатура",               МассивТоваров);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", МассивХарактеристик);
	Запрос.УстановитьПараметр("НоменклатураАвтоСписание",   МассивТоваровАвтоСписание);
	
	ТаблицаЗапасов = Запрос.Выполнить().Выгрузить();
	ТаблицаЗапасов.Индексы.Добавить("Номенклатура");
	
	СтруктураОтбора = Новый Структура;
	
	//Поместим в табличную часть товаров
	Для Каждого СтрокаЗаказНаряда Из ТабЗаказНаряда Цикл
		
		СтруктураОтбора.Вставить("Номенклатура",               СтрокаЗаказНаряда.Номенклатура);
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаЗаказНаряда.ХарактеристикаНоменклатуры);
		
		НайденныеЗапасы = ТаблицаЗапасов.НайтиСтроки(СтруктураОтбора);
		Если НайденныеЗапасы.Количество()>0 Тогда
			Остаток = Окр(НайденныеЗапасы[0].Остаток/СтрокаЗаказНаряда.Коэффициент, 3);
		Иначе
			Остаток = 0;
		КонецЕсли;
		Если Остаток<СтрокаЗаказНаряда.Количество Тогда
			НоваяСтрока = ЭтотОбъект.Товары.Добавить();
			НоваяСтрока.Номенклатура               = СтрокаЗаказНаряда.Номенклатура;
			НоваяСтрока.Количество                 = СтрокаЗаказНаряда.Количество-Остаток;
			НоваяСтрока.ЕдиницаИзмерения           = СтрокаЗаказНаряда.ЕдиницаИзмерения;
			НоваяСтрока.Коэффициент                = СтрокаЗаказНаряда.Коэффициент;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаЗаказНаряда.ХарактеристикаНоменклатуры;
			ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	//Уменьшим количество на величину уже заказанного
	Для Каждого СтрокаЗаказов Из ТаблицаПоЗаказам Цикл
		Заказано = СтрокаЗаказов.Заказано;
		МассивСтрокТоваров=ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрокаЗаказов.Номенклатура,СтрокаЗаказов.ХарактеристикаНоменклатуры));
		Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
			ЗаказаноБаз = Окр(Заказано/СтрокаТоваров.Коэффициент,3);
			ЗаказаноПоСтроке=Мин(ЗаказаноБаз, СтрокаТоваров.Количество);
			СтрокаТоваров.Количество=СтрокаТоваров.Количество-ЗаказаноПоСтроке;
			Заказано = Заказано-ЗаказаноПоСтроке*СтрокаТоваров.Коэффициент;
			ЭтотОбъект.ОбработкаРеквизита("Товары.Количество",СтрокаТоваров);
			Если Заказано=0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	//Удалим пустые строки из заказа
	МассивСтрокТоваров=ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Количество",0));
	Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
		ЭтотОбъект.Товары.Удалить(СтрокаТоваров);
	КонецЦикла;
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаряд_ЗаказПоставщику()

// Функция выполняет заполнение SMSЗаказчику на основании ЗН
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_SMSЗаказчику(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.ДокументОснование  = Основание;
	ЭтотОбъект.Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;
	//Формирование списка номеров всех доступных отправителей
	ЭтотОбъект.НомерОтправителя = Константы.смсИмяПользователяПоУмолчанию.Получить();
	Если Не ЗначениеЗаполнено(ЭтотОбъект.НомерОтправителя) Тогда
		СтруктураПараметров = смсРаботаССообщениями.ПолучитьПараметры();
		Если СтруктураПараметров.Свойство("НомераОтправителя") Тогда
			СписокНомеровОтправителя = смсКоммуникатор.ПолучитьСписокНомеровИзСтроки(СтруктураПараметров.НомераОтправителя);
			Если СписокНомеровОтправителя.Количество()> 0 Тогда
				ЭтотОбъект.НомерОтправителя = СписокНомеровОтправителя[0];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Основание.Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда
		НоваяСтрока=ЭтотОбъект.Получатели.Добавить();
		НоваяСтрока.Контрагент=Основание.Заказчик;
		НоваяСтрока.ТипКонтактнойИнформации=Перечисления.ТипыКонтактнойИнформации.Телефон;
		//поищем основной мобильный телефон
		Попытка
			Если ЗначениеЗаполнено(Основание.КодРегиона) И ЗначениеЗаполнено(Основание.КодГорода) И ЗначениеЗаполнено(Основание.НомерТелефона) Тогда 
				НоваяСтрока.КонтактнаяИнформация = Основание.КодРегиона+" ("+Основание.КодГорода+")"+ Основание.НомерТелефона;
			Иначе
				НоваяСтрока.КонтактнаяИнформация = кмПолучитьОсновнойМобильныйТелефонОбъекта(НоваяСтрока.Контрагент);	
			КонецЕсли;
		Исключение
			НоваяСтрока.КонтактнаяИнформация = кмПолучитьОсновнойМобильныйТелефонОбъекта(НоваяСтрока.Контрагент);	
		КонецПопытки;
			
		НоваяСтрока.НомерТелефона=НоваяСтрока.КонтактнаяИнформация;
	Иначе
		НоваяСтрока=ЭтотОбъект.Получатели.Добавить();
		НоваяСтрока.Контрагент=Основание.Заказчик;
		НоваяСтрока.ТипКонтактнойИнформации=Перечисления.ТипыКонтактнойИнформации.Телефон;
		НоваяСтрока.НомерТелефона=Основание.ПредставлениеТелефона;
	КонецЕсли;
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		Если Основание.Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено Тогда
			ШаблонСообщенияПриОтмене = Основание.ВидРемонта.ШаблонПриОтменеЗаявки;
			Если ЗначениеЗаполнено(ШаблонСообщенияПриОтмене) Тогда
				ЭтотОбъект.ТекстСообщения = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(ШаблонСообщенияПриОтмене,Основание);
			КонецЕсли;
		Иначе
			ШаблонСообщенияПриЗаписи = Основание.ВидРемонта.ШаблонПриЗаписиНаРемонт;
			Если ЗначениеЗаполнено(ШаблонСообщенияПриЗаписи) Тогда
				ЭтотОбъект.ТекстСообщения = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(ШаблонСообщенияПриЗаписи,Основание);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ВидРемонта=Основание.ВидРемонта;
	Исключение
		ВидРемонта=Неопределено;
	КонецПопытки;
	Если ВидРемонта<>Неопределено Тогда
		ВремяНачалаОтправки = Основание.ВидРемонта.ВремяНачалаОтправки;
		ВремяОкончанияОтправки = Основание.ВидРемонта.ВремяОкончанияОтправки;
		Если НЕ (ВремяНачалаОтправки = Дата(1, 1, 1) ИЛИ ВремяОкончанияОтправки = Дата(1, 1, 1))  Тогда
			ЭтотОбъект.НеОтправлятьSMS = Истина;
			ЭтотОбъект.НачалоПериодаЗапрета = ВремяОкончанияОтправки;
			ЭтотОбъект.КонецПериодаЗапрета = ВремяНачалаОтправки;
		КонецЕсли;
	КонецЕсли;
	
	Основание=Неопределено;
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_SMSЗаказчику()

// Функция - Ор обработка заполнения SMSЗаказчику заказ наряд
//
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
// 
// Возвращаемое значение:
//  Булево - Признак удачного заполнения.
//
Функция орОбработкаЗаполнения_SMSЗаказчику_ЗаказНаряд(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.ДокументОснование  = Основание;
	ЭтотОбъект.Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;
	//Формирование списка номеров всех доступных отправителей
	ЭтотОбъект.НомерОтправителя = Константы.смсИмяПользователяПоУмолчанию.Получить();
	Если Не ЗначениеЗаполнено(ЭтотОбъект.НомерОтправителя) Тогда
		СтруктураПараметров = смсРаботаССообщениями.ПолучитьПараметры();
		Если СтруктураПараметров.Свойство("НомераОтправителя") Тогда
			СписокНомеровОтправителя = смсКоммуникатор.ПолучитьСписокНомеровИзСтроки(СтруктураПараметров.НомераОтправителя);
			Если СписокНомеровОтправителя.Количество()> 0 Тогда
				ЭтотОбъект.НомерОтправителя = СписокНомеровОтправителя[0];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Основание.Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда
		НоваяСтрока=ЭтотОбъект.Получатели.Добавить();
		НоваяСтрока.Контрагент=Основание.Заказчик;
		НоваяСтрока.ТипКонтактнойИнформации=Перечисления.ТипыКонтактнойИнформации.Телефон;
		//поищем основной мобильный телефон
		Если ЗначениеЗаполнено(Основание.КодРегиона) И ЗначениеЗаполнено(Основание.КодГорода) И ЗначениеЗаполнено(Основание.НомерТелефона) Тогда 
			НоваяСтрока.КонтактнаяИнформация = Основание.КодРегиона+" ("+Основание.КодГорода+")"+ Основание.НомерТелефона;
		Иначе
			НоваяСтрока.КонтактнаяИнформация = кмПолучитьОсновнойМобильныйТелефонОбъекта(НоваяСтрока.Контрагент);	
		КонецЕсли;
		НоваяСтрока.НомерТелефона=НоваяСтрока.КонтактнаяИнформация;
	Иначе
		НоваяСтрока=ЭтотОбъект.Получатели.Добавить();
		НоваяСтрока.Контрагент=Основание.Заказчик;
		НоваяСтрока.ТипКонтактнойИнформации=Перечисления.ТипыКонтактнойИнформации.Телефон;
		НоваяСтрока.НомерТелефона=Основание.ПредставлениеТелефона;
	КонецЕсли;
	
	РезультатПоиска=Основание.ВидРемонта.НастройкаУведомлений.Найти(Основание.Состояние,"СостояниеЗаказНаряда");
	Если РезультатПоиска<>Неопределено Тогда
		ЭтотОбъект.ТекстСообщения = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(РезультатПоиска.ШаблонСообщения,Основание);
	КонецЕсли;
			
	ВремяНачалаОтправки = Основание.ВидРемонта.ВремяНачалаОтправки;
	ВремяОкончанияОтправки = Основание.ВидРемонта.ВремяОкончанияОтправки;
	Если НЕ (ВремяНачалаОтправки = Дата(1, 1, 1) ИЛИ ВремяОкончанияОтправки = Дата(1, 1, 1))  Тогда
		ЭтотОбъект.НеОтправлятьSMS = Истина;
		ЭтотОбъект.НачалоПериодаЗапрета = ВремяОкончанияОтправки;
		ЭтотОбъект.КонецПериодаЗапрета = ВремяНачалаОтправки;
	КонецЕсли;
	
	Основание=Неопределено;
	Возврат Истина;
	
КонецФункции

// Функция выполняет заполнение ЧекНаОплату на основании СчетНаОплату
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_СчетНаОплату_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);
	
	ЭтотОбъект.Товары.Очистить();
	Для Каждого СтрокаСчета Из Основание.Товары Цикл
		СтрокаЧека = ЭтотОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЧека, СтрокаСчета);
		
		Если ТипЗнч(СтрокаСчета.Номенклатура) = Тип("СправочникСсылка.Автоработы") Тогда
			СтрокаЧека.ЕдиницаИзмерения = СтрокаСчета.Номенклатура.Номенклатура.ОсновнаяЕдиницаИзмерения;
			СтрокаЧека.Номенклатура = СтрокаСчета.Номенклатура.Номенклатура;
			
			// Создадим характеристику работы
			АвтоработаТипНоменклатуры = Справочники.Номенклатура.Авторабота.ТипНоменклатуры;
			ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Строка(СтрокаСчета.Номенклатура), Истина,, АвтоработаТипНоменклатуры);
			Если ХарактеристикаНоменклатуры.Пустая() Тогда
				ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
				ХарактеристикаНоменклатуры.УстановитьНовыйКод();
				ХарактеристикаНоменклатуры.Владелец=АвтоработаТипНоменклатуры;
				ХарактеристикаНоменклатуры.Наименование=Строка(СтрокаСчета.Номенклатура);
				ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
				Попытка
					ХарактеристикаНоменклатуры.Записать();
				Исключение
					Сообщить("Ошибка создания новой характеристики автоработы: "+ОписаниеОшибки());
					Возврат Ложь;
				КонецПопытки;
			КонецЕсли;
			
			Если Пересчет Тогда
				СтрокаЧека.Количество=СтрокаСчета.Количество*СтрокаСчета.Коэффициент;
				СтрокаЧека.СуммаНДС=ОбПересчет(СтрокаСчета.СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
				СтрокаЧека.СуммаСкидки=ОбПересчет(СтрокаСчета.СуммаСкидки,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
				СтрокаЧека.СуммаВсего=ОбПересчет(СтрокаСчета.СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
			КонецЕсли;
			
			СтрокаЧека.СтавкаНДС=?(ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС,Справочники.СтавкиНДС.БезНДС,СтрокаСчета.Номенклатура.Номенклатура.СтавкаНДС);
			СтрокаЧека.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры.Ссылка;
			СуммаБезСкидки = СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки;
			СтрокаЧека.ПроцентСкидки=100-?(СуммаБезСкидки=0,0,(СтрокаЧека.СуммаВсего*100/СуммаБезСкидки));
			ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека,,);
		КонецЕсли;
	КонецЦикла;
	////Удалим сначала пустые строки - это автоработы
	//СтрокаЧека=ЭтотОбъект.Товары.Найти(Справочники.Номенклатура.ПустаяСсылка(),"Номенклатура");
	//Пока СтрокаЧека<>Неопределено Цикл
	//	ЭтотОбъект.Товары.Удалить(СтрокаЧека);
	//	СтрокаЧека=ЭтотОбъект.Товары.Найти(Справочники.Номенклатура.ПустаяСсылка(),"Номенклатура");
	//КонецЦикла; 
	
	//ВалютаОснования = Основание.ВалютаДокумента;
	//КурсОснования   = Основание.КурсДокумента;
	//Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);
	
	//МассивСтрокРабот=Новый Массив;
	//Для Каждого СтрокаСчета Из Основание.Товары Цикл
	//	Если ТипЗнч(СтрокаСчета.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
	//		//СтрокаЧека=ЭтотОбъект.Товары.Найти(СтрокаСчета.Номенклатура.Номенклатура,"Номенклатура");
	//		//Если СтрокаЧека=Неопределено Тогда
	//		СтрокаЧека=ЭтотОбъект.Товары.Добавить();
	//		МассивСтрокРабот.Добавить(СтрокаЧека);
	//		СтрокаЧека.Номенклатура=СтрокаСчета.Номенклатура.Номенклатура;
	//		СтрокаЧека.СтавкаНДС=?(ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС,Справочники.СтавкиНДС.БезНДС,СтрокаСчета.Номенклатура.Номенклатура.СтавкаНДС);
	//		СтрокаЧека.ЕдиницаИзмерения=СтрокаСчета.Номенклатура.Номенклатура.ОсновнаяЕдиницаИзмерения;
	//		СтрокаЧека.Коэффициент=1;
	//		//КонецЕсли; 
	//		СтрокаЧека.Количество=СтрокаЧека.Количество+СтрокаСчета.Количество*СтрокаСчета.Коэффициент;
	//		СтрокаЧека.СуммаНДС=СтрокаЧека.СуммаНДС+?(Пересчет,ОбПересчет(СтрокаСчета.СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СтрокаСчета.СуммаНДС);
	//		СтрокаЧека.СуммаСкидки=СтрокаЧека.СуммаСкидки+?(Пересчет,ОбПересчет(СтрокаСчета.СуммаСкидки,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СтрокаСчета.СуммаСкидки);
	//		СтрокаЧека.СуммаВсего=СтрокаЧека.СуммаВсего+?(Пересчет,ОбПересчет(СтрокаСчета.СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СтрокаСчета.СуммаВсего);
	//		
	//		// Создадим характеристику работы
	//		АвтоработаТипНоменклатуры = Справочники.Номенклатура.Авторабота.ТипНоменклатуры;
	//		ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Строка(СтрокаСчета.Номенклатура), Истина,, АвтоработаТипНоменклатуры);
	//		Если ХарактеристикаНоменклатуры.Пустая() Тогда
	//			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
	//			ХарактеристикаНоменклатуры.УстановитьНовыйКод();
	//			ХарактеристикаНоменклатуры.Владелец=АвтоработаТипНоменклатуры;
	//			ХарактеристикаНоменклатуры.Наименование=Строка(СтрокаСчета.Номенклатура);
	//			ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
	//			Попытка
	//				ХарактеристикаНоменклатуры.Записать();
	//			Исключение
	//				Сообщить("Ошибка создания новой характеристики автоработы: "+ОписаниеОшибки());
	//				Возврат Ложь;
	//			КонецПопытки;
	//		КонецЕсли;
	//		СтрокаЧека.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры.Ссылка;
	//	КонецЕсли; 
	//	
	//КонецЦикла;
	//Для каждого СтрокаРабот Из МассивСтрокРабот Цикл
	//	СуммаБезСкидки=СтрокаРабот.СуммаВсего+СтрокаРабот.СуммаСкидки;
	//	СтрокаРабот.ПроцентСкидки=100-(СтрокаРабот.СуммаВсего*100/СуммаБезСкидки);
	//	ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаРабот,,);
	//КонецЦикла; 
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента = ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_СчетНаОплату_ЧекНаОплату()

// Функция выполняет заполнение СчетНаОплату на основании АктРазногласий
// Параметры:
//	ЭтотОбъект  - Документ для заполнения
//	Основание   - Документ основание
//	Копирование - Признак копирования
//
Функция орОбработкаЗаполнения_АктРазногласий_СчетНаОплату(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ПустыеСтроки=ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Количество",0));
	Для каждого ПустаяСтрока Из ПустыеСтроки Цикл
		ЭтотОбъект.Товары.Удалить(ПустаяСтрока);
	КонецЦикла; 
	
	Для Каждого СтрокаРабот Из Основание.Работы Цикл
		Если СтрокаРабот.Количество=0 Тогда Продолжить; КонецЕсли; 
		НоваяСтрокаСчета=ЭтотОбъект.Товары.Добавить();
		НоваяСтрокаСчета.Номенклатура=СтрокаРабот.Работа;
		НоваяСтрокаСчета.Количество=СтрокаРабот.Количество;
		НоваяСтрокаСчета.ЕдиницаИзмерения=СтрокаРабот.Нормочас;
		НоваяСтрокаСчета.Коэффициент=СтрокаРабот.Коэффициент;
		НоваяСтрокаСчета.Цена=СтрокаРабот.Цена;
		НоваяСтрокаСчета.Сумма=СтрокаРабот.Сумма;
		НоваяСтрокаСчета.СтавкаНДС=СтрокаРабот.СтавкаНДС;
		НоваяСтрокаСчета.СуммаНДС=СтрокаРабот.СуммаНДС;
		НоваяСтрокаСчета.ПроцентСкидки=СтрокаРабот.ПроцентСкидки;
		НоваяСтрокаСчета.СуммаСкидки=СтрокаРабот.СуммаСкидки;
		НоваяСтрокаСчета.СуммаВсего=СтрокаРабот.СуммаВсего;
		НоваяСтрокаСчета.ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЦикла;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_АктРазногласий_СчетНаОплату()

// Функция получает для списка заказ-нарядов таблицу данных оформления строк 
Функция орСписокЗаказНарядПолучитьТаблицуОформленияСтрок(ОформленияСтрок)
	
	ТаблицаОформленияСтрок = Новый ТаблицаЗначений();
	ТаблицаОформленияСтрок.Колонки.Добавить("Ссылка");
	ТаблицаОформленияСтрок.Колонки.Добавить("Состояние", Новый ОписаниеТипов("СправочникСсылка.ВидыСостоянийЗаказНарядов"));;	
	
	МассивЗаказНарядов = Новый Массив();
	МассивЗаявокНаРемонт = Новый Массив();
	МассивДругихДокументов = Новый Массив();
	
	Для Каждого ТекОформление Из ОформленияСтрок Цикл
		
		Если ТипЗнч(ТекОформление.ДанныеСтроки.Ссылка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			МассивЗаказНарядов.Добавить(ТекОформление.ДанныеСтроки.Ссылка);	
		ИначеЕсли ТипЗнч(ТекОформление.ДанныеСтроки.Ссылка)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
			МассивЗаявокНаРемонт.Добавить(ТекОформление.ДанныеСтроки.Ссылка);
		Иначе
			МассивДругихДокументов.Добавить(ТекОформление.ДанныеСтроки.Ссылка);
		КонецЕсли;
		
	КонецЦикла;	
	
	//Получим данные по Заказ нарядам и Заявкам на ремонт
	Запрос = Новый Запрос;	
	Запрос.Текст =  
		"ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ втЗаявкиНаРемонт
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Ссылка В(&МассивЗаявокНаРемонт)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК Ссылка,
		|	ЗаказНаряд.Состояние КАК Состояние
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Ссылка В(&МассивЗаказНарядов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втЗаявкиНаРемонт.Ссылка,
		|	ЗаказНаряд.Состояние
		|ИЗ
		|	втЗаявкиНаРемонт КАК втЗаявкиНаРемонт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаряд КАК ЗаказНаряд
		|		ПО втЗаявкиНаРемонт.Ссылка = ЗаказНаряд.ДокументОснование";
		
    Запрос.УстановитьПараметр("МассивЗаказНарядов", МассивЗаказНарядов);
	Запрос.УстановитьПараметр("МассивЗаявокНаРемонт", МассивЗаявокНаРемонт);
		
    Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		Стр = ТаблицаОформленияСтрок.Добавить();
	   	Стр.Ссылка 		= Выборка.Ссылка;
		Стр.Состояние 	= Выборка.Состояние;	
	КонецЦикла;
	
	//Получим данные по Другим документам
	ДругиеДокументы_ДокументОснование = обЗначениеРеквизитаОбъектов(МассивДругихДокументов, "ДокументОснование");
	МассивДокументовОснований = Новый Массив;	
	Для каждого Документ Из ДругиеДокументы_ДокументОснование Цикл
		Если ТипЗнч(Документ.Значение) <> Тип("ДокументСсылка.ЗаказНаряд") Тогда
			Продолжить;		
		КонецЕсли;
		МассивДокументовОснований.Добавить(Документ.Значение);		
	КонецЦикла;	
	ДругиеДокументы_ДокументОснование_Состояние = обЗначениеРеквизитаОбъектов(МассивДокументовОснований, "Состояние");
	
	Для каждого ДругойДокумент Из ДругиеДокументы_ДокументОснование Цикл
		
		ДокументОснование = ДругойДокумент.Значение;
		СостояниеДокументаОснования = ДругиеДокументы_ДокументОснование_Состояние.Получить(ДокументОснование);
		Если НЕ ЗначениеЗаполнено(СостояниеДокументаОснования) Тогда
			Продолжить;
		КонецЕсли;
		
		Стр = ТаблицаОформленияСтрок.Добавить();
	   	Стр.Ссылка 		= ДругойДокумент.Ключ;
		Стр.Состояние 	= СостояниеДокументаОснования;
		
	КонецЦикла;
	
	Возврат ТаблицаОформленияСтрок;
	
КонецФункции

// Функция получает цвет состояния заказ наряда из кеша
Функция обПолучитьЦветСостоянияЗаказНаряда(Состояние, КешЦветовСостоянийЗаказНарядов)

	//Получим цвета сразу по всем состояниям, так как справочник не велик
	Если КешЦветовСостоянийЗаказНарядов = Неопределено Тогда
		
		КешЦветовСостоянийЗаказНарядов = Новый Соответствие;

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВидыСостоянийЗаказНарядов.Ссылка КАК Ссылка,
			|	ВидыСостоянийЗаказНарядов.Цвет КАК Цвет
			|ИЗ
			|	Справочник.ВидыСостоянийЗаказНарядов КАК ВидыСостоянийЗаказНарядов
			|ГДЕ
			|	НЕ ВидыСостоянийЗаказНарядов.ПометкаУдаления";
		
		РезультатЗапроса = Запрос.Выполнить();		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ЦветФона = Выборка.Цвет.Получить();				
			КешЦветовСостоянийЗаказНарядов.Вставить(Выборка.Ссылка, ЦветФона);
			
		КонецЦикла;
	
	КонецЕсли;
	
	ЦветСостояния = КешЦветовСостоянийЗаказНарядов.Получить(Состояние);	
	Если ЦветСостояния = Неопределено Тогда
		
		ЦветСостояния = обЗначениеРеквизитаОбъекта(Состояние, "Цвет").Получить();
		КешЦветовСостоянийЗаказНарядов.Вставить(Состояние, ЦветСостояния);
		
	КонецЕсли;	
			
	Возврат ЦветСостояния;
	
КонецФункции

// Список "ЗаказНаряд" при получении данных
//
Процедура орСписокЗаказНарядПриПолученииДанныхДляСпискаСтрок(ОформленияСтрок, КешЦветовСостоянийЗаказНарядов) Экспорт
		
	ТаблицаДанныхОформленияСтрок = орСписокЗаказНарядПолучитьТаблицуОформленияСтрок(ОформленияСтрок);	
	
	Для Каждого ТекОформление Из ОформленияСтрок Цикл
		
		ДанныеОформленияСтроки = ТаблицаДанныхОформленияСтрок.Найти(ТекОформление.Данныестроки.Ссылка, "Ссылка");		
		
		Если ДанныеОформленияСтроки <> Неопределено Тогда
			
			
			ЦветФона = обПолучитьЦветСостоянияЗаказНаряда(ДанныеОформленияСтроки.Состояние, КешЦветовСостоянийЗаказНарядов);
			Если НЕ обЗначениеНеЗаполнено(ЦветФона) Тогда
			
				ТекОформление.ЦветФона = ЦветФона;	
				
			КонецЕсли;			
			
		КонецЕсли;				
		
	КонецЦикла;			
		
КонецПроцедуры

//Обработка заполнения для автосалона

// Заполнение документа счет фактура полученный
Функция орОбработкаЗаполнения_Автомобили_СчетФактураПолученный(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли;
	
	ТабТоваров = Неопределено;
	ТабГТД     = Неопределено;
	
	//Добавим в счет-фактуру автомобили из реализации
	Запрос = Новый Запрос;
	ИмяДокументаОснования=ЭтотОбъект.ДокументОснование.Метаданные().Имя;
	ЭтотОбъект.Грузоотправитель=Основание.Контрагент;
	Если ИмяДокументаОснования="ПоступлениеАвтомобилей" Тогда
		Запрос.Текст = "ВЫБРАТЬ
		|	ТабличнаяЧастьАвтомобили.Автомобиль,
		|	ТабличнаяЧастьАвтомобили.Количество,
		|	ТабличнаяЧастьАвтомобили.Цена+ЕСТЬNULL(ТабличнаяЧастьОпции.ЦенаОпции,0) КАК Цена,
		|	ТабличнаяЧастьАвтомобили.Сумма+ЕСТЬNULL(ТабличнаяЧастьОпции.СуммаОпции,0) КАК Сумма,
		|	ТабличнаяЧастьАвтомобили.СтавкаНДС КАК СтавкаНДС,
		|	ТабличнаяЧастьАвтомобили.СуммаНДС+ЕСТЬNULL(ТабличнаяЧастьОпции.СуммаНДСОпции,0) КАК СуммаНДС,
		|	ТабличнаяЧастьАвтомобили.СуммаВсего+ЕСТЬNULL(ТабличнаяЧастьОпции.СуммаВсегоОпции,0) КАК СуммаВсего,
		|	ТабличнаяЧастьАвтомобили.ГТД
		|ИЗ
		|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ТабличнаяЧастьАвтомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ТабличнаяЧастьОпции.ИдентификаторАвтомобиля КАК ИдентификаторАвтомобиля,
		|			СУММА(ТабличнаяЧастьОпции.Цена) КАК ЦенаОпции,
		|			СУММА(ТабличнаяЧастьОпции.Сумма) КАК СуммаОпции,
		|			СУММА(ТабличнаяЧастьОпции.СуммаНДС) КАК СуммаНДСОпции,
		|			СУММА(ТабличнаяЧастьОпции.СуммаВсего) КАК СуммаВсегоОпции
		|		ИЗ
		|			Документ.ПоступлениеАвтомобилей.Опции КАК ТабличнаяЧастьОпции
		|		ГДЕ
		|			ТабличнаяЧастьОпции.Ссылка = &ДокументОснование
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ТабличнаяЧастьОпции.ИдентификаторАвтомобиля) КАК ТабличнаяЧастьОпции
		|		ПО ТабличнаяЧастьАвтомобили.ИдентификаторАвтомобиля = ТабличнаяЧастьОпции.ИдентификаторАвтомобиля
		|ГДЕ
		|	ТабличнаяЧастьАвтомобили.Ссылка = &ДокументОснование";
		//|	Документ."+ИмяДокументаОснования+".Автомобили КАК ТабличнаяЧастьАвтомобили
	ИначеЕсли ИмяДокументаОснования="ОтчетКомитентуЗаАвтомобили" Тогда
		ИмяРегистраОстатковАвтомобилей="РеализованныеАвтомобили";
		Запрос.УстановитьПараметр("ВидДвиженияРегистраОстатков",ВидДвиженияНакопления.Расход);
		Запрос.Текст = "ВЫБРАТЬ
		|	ТабличнаяЧастьАвтомобили.Автомобиль,
		|	ТабличнаяЧастьАвтомобили.Количество,
		|	ТабличнаяЧастьАвтомобили.Цена,
		|	ТабличнаяЧастьАвтомобили.Сумма,
		|	ТабличнаяЧастьАвтомобили.СтавкаНДС,
		|	ТабличнаяЧастьАвтомобили.СуммаНДС,
		|	ТабличнаяЧастьАвтомобили.СуммаВсего,
		|	ПоступлениеАвтомобилейАвтомобили.ГТД
		|ИЗ
		|	Документ."+ИмяДокументаОснования+".Автомобили КАК ТабличнаяЧастьАвтомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления."+ИмяРегистраОстатковАвтомобилей+" КАК ОстаткиАвтомобилей
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
		|			ПО ОстаткиАвтомобилей.ДокументПередачи = ПоступлениеАвтомобилейАвтомобили.Ссылка И ОстаткиАвтомобилей.Автомобиль = ПоступлениеАвтомобилейАвтомобили.Автомобиль
		|		ПО ТабличнаяЧастьАвтомобили.Автомобиль = ОстаткиАвтомобилей.Автомобиль И (ОстаткиАвтомобилей.Регистратор = &ДокументОснование) И (ОстаткиАвтомобилей.ДокументПередачи ССЫЛКА Документ.ПоступлениеАвтомобилей) И (ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРегистраОстатков)
		|ГДЕ
		|	ТабличнаяЧастьАвтомобили.Ссылка = &ДокументОснование
		|";
	ИначеЕсли ИмяДокументаОснования="ВозвратОтПокупателяАвтомобилей" Тогда
		ИмяРегистраОстатковАвтомобилей="ОстаткиАвтомобилей";
		Запрос.УстановитьПараметр("ВидДвиженияРегистраОстатков",ВидДвиженияНакопления.Расход);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабличнаяЧастьАвтомобили.Автомобиль,
		|	ТабличнаяЧастьАвтомобили.Количество,
		|	ТабличнаяЧастьАвтомобили.Цена,
		|	ТабличнаяЧастьАвтомобили.Сумма,
		|	ТабличнаяЧастьАвтомобили.СтавкаНДС,
		|	ТабличнаяЧастьАвтомобили.СуммаНДС,
		|	ТабличнаяЧастьАвтомобили.СуммаВсего,
		|	ПоступлениеАвтомобилейАвтомобили.ГТД
		|ИЗ
		|	Документ."+ИмяДокументаОснования+".Автомобили КАК ТабличнаяЧастьАвтомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления."+ИмяРегистраОстатковАвтомобилей+" КАК ОстаткиАвтомобилей
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
		|			ПО ОстаткиАвтомобилей.Партия = ПоступлениеАвтомобилейАвтомобили.Ссылка И ОстаткиАвтомобилей.Автомобиль = ПоступлениеАвтомобилейАвтомобили.Автомобиль
		|		ПО ТабличнаяЧастьАвтомобили.Автомобиль = ОстаткиАвтомобилей.Автомобиль И (ОстаткиАвтомобилей.Регистратор = &ДокументОснование) И (ОстаткиАвтомобилей.Партия ССЫЛКА Документ.ПоступлениеАвтомобилей) И (ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРегистраОстатков)
		|ГДЕ
		|	ТабличнаяЧастьАвтомобили.Ссылка = &ДокументОснование
		|";
		
		// перенесем товары
		ЗапросПоТоварам   = Новый Запрос;
		ЗапросПоТоварам.Текст =
		"ВЫБРАТЬ
		|	ВозвратОтПокупателяАвтомобилейТовары.Номенклатура,
		|	ВозвратОтПокупателяАвтомобилейТовары.ХарактеристикаНоменклатуры,
		|	ВозвратОтПокупателяАвтомобилейТовары.СтавкаНДС,
		|	СУММА(ВозвратОтПокупателяАвтомобилейТовары.Количество) КАК Количество,
		|	СУММА(ВозвратОтПокупателяАвтомобилейТовары.СуммаВсего) КАК Сумма,
		|	СУММА(ВозвратОтПокупателяАвтомобилейТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВозвратОтПокупателяАвтомобилейТовары.Количество) КАК КоличествоОсталось,
		|	СУММА(ВозвратОтПокупателяАвтомобилейТовары.СуммаВсего) КАК СуммаОсталось,
		|	СУММА(ВозвратОтПокупателяАвтомобилейТовары.СуммаНДС) КАК СуммаНДСОсталось
		|ИЗ
		|	Документ.ВозвратОтПокупателяАвтомобилей.Товары КАК ВозвратОтПокупателяАвтомобилейТовары
		|ГДЕ
		|	ВозвратОтПокупателяАвтомобилейТовары.Ссылка = &ДокументОснование
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратОтПокупателяАвтомобилейТовары.Номенклатура,
		|	ВозвратОтПокупателяАвтомобилейТовары.ХарактеристикаНоменклатуры,
		|	ВозвратОтПокупателяАвтомобилейТовары.СтавкаНДС";
		
		ЗапросПоТоварам.УстановитьПараметр("ДокументОснование",Основание);
		ТабТоваров = ЗапросПоТоварам.Выполнить().Выгрузить();
		
		ЗапросПоТоварам.Текст =
		"ВЫБРАТЬ
		|	КомплектацияАвтомобилей.Номенклатура,
		|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
		|	КомплектацияАвтомобилей.ГТД,
		|	СУММА(-КомплектацияАвтомобилей.Количество) КАК Количество,
		|	СУММА(-КомплектацияАвтомобилей.Количество) КАК КоличествоОсталось
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &ДокументОснование
		|
		|СГРУППИРОВАТЬ ПО
		|	КомплектацияАвтомобилей.ГТД,
		|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
		|	КомплектацияАвтомобилей.Номенклатура";
		
		ТабГТД = ЗапросПоТоварам.Выполнить().Выгрузить();
	КонецЕсли; 
	Запрос.УстановитьПараметр("ДокументОснование",ЭтотОбъект.ДокументОснование);
	Выборка = Запрос.Выполнить().Выбрать();
	ЭтотОбъект.Товары.Очистить();
	Пока Выборка.Следующий() Цикл
		НайденныеСтроки=ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Номенклатура,ГТД",Выборка.Автомобиль,Выборка.ГТД));
		Если НайденныеСтроки.Количество()=0 Тогда
			СтрокаТоваров=ЭтотОбъект.Товары.Добавить();
		Иначе
			СтрокаТоваров=НайденныеСтроки[0];
		КонецЕсли;
		СтрокаТоваров.Номенклатура=Выборка.Автомобиль;
		СтрокаТоваров.Количество=СтрокаТоваров.Количество+Выборка.Количество;
		СтрокаТоваров.ЕдиницаИзмерения=Справочники.КлассификаторЕдиницИзмерения.шт;
		СтрокаТоваров.Коэффициент=1;
		СтрокаТоваров.Цена=Выборка.Цена;
		СтрокаТоваров.Сумма=СтрокаТоваров.Сумма+Выборка.Сумма;
		СтрокаТоваров.СтавкаНДС=Выборка.СтавкаНДС;
		СтрокаТоваров.СуммаНДС=СтрокаТоваров.СуммаНДС+Выборка.СуммаНДС;
		СтрокаТоваров.СуммаВсего=СтрокаТоваров.СуммаВсего+Выборка.СуммаВсего;
		СтрокаТоваров.ГТД=Выборка.ГТД;
	КонецЦикла;
	
	Если НЕ обЗначениеНеЗаполнено(ТабТоваров) Тогда
		Для Каждого стрМассива Из ТабТоваров Цикл
			Если ТабГТД <> Неопределено Тогда
				Если стрМассива.КоличествоОсталось = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Отбор = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",стрМассива.Номенклатура,стрМассива.ХарактеристикаНоменклатуры);
				МассивПоиска = ТабГТД.НайтиСтроки(Отбор);
				Для Каждого ТекСтрока Из МассивПоиска Цикл
					Если стрМассива.КоличествоОсталось = 0 Тогда
						Прервать;
					КонецЕсли;
					СписываемКоличество = Мин(стрМассива.КоличествоОсталось,ТекСтрока.КоличествоОсталось);
					СписываемСумма      = ?(стрМассива.КоличествоОсталось = СписываемКоличество, стрМассива.СуммаОсталось, Окр((стрМассива.Сумма/стрМассива.Количество)*СписываемКоличество,2));
					СписываемСуммаНДС   = ?(стрМассива.КоличествоОсталось = СписываемКоличество, стрМассива.СуммаНДСОсталось, Окр((стрМассива.СуммаНДС/стрМассива.Количество)*СписываемКоличество,2));
					
					НоваяСтрока = ЭтотОбъект.Товары.Добавить();
					НоваяСтрока.Номенклатура = стрМассива.Номенклатура;
					ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
					
					НоваяСтрока.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
					НоваяСтрока.Количество                 = СписываемКоличество;
					НоваяСтрока.ГТД                        = ТекСтрока.ГТД;
					НоваяСтрока.СтавкаНДС                  = стрМассива.СтавкаНДС;
					НоваяСтрока.СуммаВсего                 = СписываемСумма;
					ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
					
					НоваяСтрока.СуммаНДС                   = СписываемСуммаНДС;
					ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаНДС",НоваяСтрока);
					
					// уменьшим нераспределенное количество
					стрМассива.КоличествоОсталось = стрМассива.КоличествоОсталось - СписываемКоличество;
					ТекСтрока.КоличествоОсталось  = ТекСтрока.КоличествоОсталось - СписываемКоличество;
					стрМассива.СуммаОсталось      = стрМассива.СуммаОсталось - СписываемСумма;
					стрМассива.СуммаНДСОсталось   = стрМассива.СуммаНДСОсталось - СписываемСуммаНДС;
				КонецЦикла;
			КонецЕсли;
			
			// если осталось еще дпишем без гтд
			Если стрМассива.КоличествоОсталось > 0 Тогда
				НоваяСтрока = ЭтотОбъект.Товары.Добавить();
				НоваяСтрока.Номенклатура = стрМассива.Номенклатура;
				ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				
				НоваяСтрока.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
				НоваяСтрока.Количество                 = стрМассива.КоличествоОсталось;
				НоваяСтрока.ГТД                        = Справочники.ГТД.ПустаяСсылка();
				НоваяСтрока.СтавкаНДС                  = стрМассива.СтавкаНДС;
				НоваяСтрока.СуммаВсего                 = стрМассива.СуммаОсталось;
				ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
				
				НоваяСтрока.СуммаНДС                   = стрМассива.СуммаНДСОсталось;
				ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаНДС",НоваяСтрока);
				// если расхождения по сумма кинем х на последнюю строку
			ИначеЕсли стрМассива.СуммаОсталось <> 0 ИЛИ стрМассива.СуммаНДСОсталось <> 0 Тогда
				НоваяСтрока.СуммаВсего = НоваяСтрока.СуммаВсего + стрМассива.СуммаОсталось;
				СтараяСуммаНДС         = НоваяСтрока.СуммаНДС;
				ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
				
				НоваяСтрока.СуммаНДС   = СтараяСуммаНДС + стрМассива.СуммаНДСОсталось;
				ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаНДС",НоваяСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Основание=Неопределено;
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_Автомобили_СчетФактураПолученный()

// Заполнение документа платежное поручение на основании заказа
Функция орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ПлатежноеПоручение(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ПлатежноеПоручение() 

// Заполнение документа поступление автомобилей на основании заказа
Функция орОбработкаЗаполнения_ЗаказНаАвтомобиль_ПоступлениеАвтомобилей(ЭтотОбъект,Основание,Копирование)
	Если ЗначениеЗаполнено(Основание.Модель.Контрагент) Тогда
		ЭтотОбъект.Контрагент = Основание.Модель.Контрагент;
		ЭтотОбъект.ДоговорВзаиморасчетов = Основание.Модель.ДоговорВзаиморасчетов;
	Иначе
		ЭтотОбъект.Контрагент            = обПраво("ОсновнойПоставщик",,,ЭтотОбъект);
		ЭтотОбъект.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ЭтотОбъект.Контрагент, Перечисления.ВидыДоговоров.Покупка, ЭтотОбъект,, Истина, Ложь);
		
		ЭтотОбъект.ОбработкаРеквизита("ДоговорВзаиморасчетов");
	КонецЕсли;
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.ТипЦен = обПраво("ОсновнойТипЦенПродажиАвтомобилей",,,ЭтотОбъект);
	НоваяСтрока = ЭтотОбъект.Автомобили.Добавить();
	НоваяСтрока.Автомобиль = Основание.Автомобиль;
	НоваяСтрока.ИдентификаторАвтомобиля = Новый УникальныйИдентификатор;
	
	Для Каждого ТекЭлемент Из ЭтотОбъект.Опции Цикл
		ТекЭлемент.ИдентификаторАвтомобиля = НоваяСтрока.ИдентификаторАвтомобиля;
	КонецЦикла;
	
	ЭтотОбъект.ОбработкаРеквизита("Автомобили.Автомобиль", НоваяСтрока);
	
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.РассчитатьСуммуВсего();
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_ПоступлениеАвтомобилей() 

// Заполнение документа реализация автомобилей на основании заказа
Функция орОбработкаЗаполнения_ЗаказНаАвтомобиль_РеализацияАвтомобилей(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ЭтотОбъект.СкидкаНаценкаАвтомобили=Основание.СкидкаНаценкаНаАвтомобиль;
	ЭтотОбъект.ЗначениеСкидкиНаценкиАвтомобили=Основание.ЗначениеСкидкиНаценкиНаАвтомобиль;
	ЭтотОбъект.СуммаСкидкиНаценкиАвтомобили=Основание.СуммаСкидкиНаценкиНаАвтомобиль;
	
	ЭтотОбъект.Автомобили.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(, Заказ = &Заказ) КАК ЗаказыАвтомобилейОстатки
	|ГДЕ
	|	ЗаказыАвтомобилейОстатки.РезервОстаток = 1
	|";
	Запрос.УстановитьПараметр("Заказ", Основание);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		СтрокаАвтомобиля = ЭтотОбъект.Автомобили.Добавить();
		СтрокаАвтомобиля.ИдентификаторАвтомобиля = Новый УникальныйИдентификатор;
		Для Каждого стрТоваров Из ЭтотОбъект.Товары Цикл
			стрТоваров.ИдентификаторАвтомобиля = СтрокаАвтомобиля.ИдентификаторАвтомобиля;
		КонецЦикла;
		
		СтрокаАвтомобиля.Автомобиль = Выборка.Автомобиль;
		ЭтотОбъект.ОбработкаРеквизита("Автомобили.Автомобиль", СтрокаАвтомобиля);
		СтрокаАвтомобиля.ЗаказНаАвтомобиль = Основание;
		ЭтотОбъект.ОбработкаРеквизита("Автомобили.ЗаказНаАвтомобиль", СтрокаАвтомобиля);
		
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ОстаткиАвтомобилейОстатки.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(, Автомобиль = &Автомобиль) КАК ОстаткиАвтомобилейОстатки";
		Запрос.УстановитьПараметр("Автомобиль", Выборка.Автомобиль);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЭтотОбъект.СкладКомпании=Выборка.СкладКомпании;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_РеализацияАвтомобилей() 

// Заполнение документа счет на оплату за автомобили на основании заказа
Функция орОбработкаЗаполнения_ЗаказНаАвтомобиль_СчетНаОплатуЗаАвтомобили(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ЭтотОбъект.СкидкаНаценкаАвтомобили=Основание.СкидкаНаценкаНаАвтомобиль;
	ЭтотОбъект.ЗначениеСкидкиНаценкиАвтомобили=Основание.ЗначениеСкидкиНаценкиНаАвтомобиль;
	ЭтотОбъект.СуммаСкидкиНаценкиАвтомобили=Основание.СуммаСкидкиНаценкиНаАвтомобиль;
	
	ЭтотОбъект.Автомобили.Очистить();
	
	СтрокаАвтомобиля=ЭтотОбъект.Автомобили.Добавить();
	СтрокаАвтомобиля.ИдентификаторАвтомобиля=Новый УникальныйИдентификатор;
	АвтомобильЗаказа = Основание.Автомобиль;
	Если обЗначениеНеЗаполнено(АвтомобильЗаказа) Тогда
		СтрокаАвтомобиля.Автомобиль          = Основание.Модель;
		СтрокаАвтомобиля.ВариантКомплектации = Основание.ВариантКомплектации;
	Иначе
		СтрокаАвтомобиля.Автомобиль          = АвтомобильЗаказа;
		СтрокаАвтомобиля.ВариантКомплектации = АвтомобильЗаказа.ВариантКомплектации;
	КонецЕсли; 
	СтрокаАвтомобиля.Количество=1;
	СтрокаАвтомобиля.Цена=Основание.ЦенаАвтомобиля+Основание.Опции.Итог("Сумма");
	СтрокаАвтомобиля.Сумма=СтрокаАвтомобиля.Цена;
	СтрокаАвтомобиля.СуммаСкидки=Основание.СуммаСкидкиНаАвтомобиль+Основание.Опции.Итог("СуммаСкидки");
	СтрокаАвтомобиля.СуммаВсего=Основание.СуммаВсегоНаАвтомобиль+Основание.Опции.Итог("СуммаВсего");
	ЭтотОбъект.ОбработкаРеквизита("Автомобили.СуммаСкидки",СтрокаАвтомобиля);
	СтрокаАвтомобиля.СтавкаНДС=Основание.СтавкаНДСНаАвтомобиль;
	СтрокаАвтомобиля.СуммаНДС=Основание.СуммаНДСНаАвтомобиль+Основание.Опции.Итог("СуммаНДС");
	
	Для каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
		СтрокаТоваров.ИдентификаторАвтомобиля=СтрокаАвтомобиля.ИдентификаторАвтомобиля;		
	КонецЦикла; 
	
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.РассчитатьСуммуВсего();
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_СчетНаОплатуЗаАвтомобили() 

// Заполнение документа снятие резервов автомобилей на основании заказа
Функция орОбработкаЗаполнения_ЗаказНаАвтомобиль_СнятиеРезервовАвтомобилей(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ЭтотОбъект.Автомобили.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(, Заказ = &Заказ) КАК ЗаказыАвтомобилейОстатки
	|ГДЕ
	|	ЗаказыАвтомобилейОстатки.РезервОстаток = 1
	|";
	Запрос.УстановитьПараметр("Заказ", Основание);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		НовыйАвтомобиль = ЭтотОбъект.Автомобили.Добавить();
		НовыйАвтомобиль.Автомобиль = Выборка.Автомобиль;
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ОстаткиАвтомобилейОстатки.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(, Автомобиль = &Автомобиль) КАК ОстаткиАвтомобилейОстатки";
		Запрос.УстановитьПараметр("Автомобиль", Выборка.Автомобиль);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЭтотОбъект.СкладКомпании = Выборка.СкладКомпании;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_СнятиеРезервовАвтомобилей() 

// Заполнение документа чек на оплату на основании заказа
Функция орОбработкаЗаполнения_ЗаказНаАвтомобиль_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);
	
	ЭтотОбъект.СкидкаНаценка=Основание.СкидкаНаценкаНаАвтомобиль;
	ЭтотОбъект.ЗначениеСкидкиНаценки=Основание.ЗначениеСкидкиНаценкиНаАвтомобиль;
	ЭтотОбъект.СуммаСкидкиНаценки=Основание.СуммаСкидкиНаценкиНаАвтомобиль;
	
	//Если у автомобиля есть доп. свойство "Номенклатура" и оно заполнено, подставим его значение
	НоменклатураАвтомобиль = Справочники.Номенклатура.Автомобиль;
	Автомобиль = обПолучитьЗначениеСвойства(Основание.Автомобиль, "Номенклатура", НоменклатураАвтомобиль);
	Автомобиль = ?(ЗначениеЗаполнено(Автомобиль), Автомобиль, НоменклатураАвтомобиль);
	АвтомобильТипНоменклатуры=НоменклатураАвтомобиль.ТипНоменклатуры;
	ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	VIN = Основание.Автомобиль.VIN;
	//Если НЕ ПустаяСтрока(СокрЛП(Основание.VIN)) Тогда
	ХарактеристикаНоменклатурыНаименование="VIN "+?(ПустаяСтрока(СокрЛП(VIN)),"<Не задан>",СокрЛП(VIN));
	ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(ХарактеристикаНоменклатурыНаименование,Истина,,АвтомобильТипНоменклатуры);
	Если ХарактеристикаНоменклатуры.Пустая() Тогда
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
		ХарактеристикаНоменклатуры.УстановитьНовыйКод();
		ХарактеристикаНоменклатуры.Владелец=АвтомобильТипНоменклатуры;
		ХарактеристикаНоменклатуры.Наименование=ХарактеристикаНоменклатурыНаименование;
		ХарактеристикаНоменклатуры.СерийныйНомер=?(ПустаяСтрока(СокрЛП(VIN)),"<Не задан>",СокрЛП(VIN));
		ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
		Попытка
			ХарактеристикаНоменклатуры.Записать();
		Исключение
			Сообщить("Ошибка создания новой характеристики автомобиля: "+ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки; 
	КонецЕсли; 
	//КонецЕсли; 
	
	ЭтотОбъект.Товары.Очистить();
	СтрокаЧека=ЭтотОбъект.Товары.Добавить();
	СтрокаЧека.Номенклатура=Автомобиль;
	СтрокаЧека.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
	ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаЧека);
	СтрокаЧека.Количество=1;
	СуммаСкидки=Основание.СуммаСкидкиНаАвтомобиль+Основание.Опции.Итог("СуммаСкидки")+Основание.Товары.Итог("СуммаСкидки");
	СуммаСкидки=?(Пересчет,ОбПересчет(СуммаСкидки,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаСкидки);
	Цена=Основание.ЦенаАвтомобиля+Основание.Опции.Итог("Цена")+Основание.Товары.Итог("Цена");
	Цена=?(Пересчет,ОбПересчет(Цена,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Цена);
	Сумма=Основание.ЦенаАвтомобиля+Основание.Опции.Итог("Сумма")+Основание.Товары.Итог("Сумма");
	Сумма=?(Пересчет,ОбПересчет(Сумма,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Сумма);
	СуммаНДС=Основание.СуммаНДСНаАвтомобиль+Основание.Опции.Итог("СуммаНДС")+Основание.Товары.Итог("СуммаНДС");
	СуммаНДС=?(Пересчет,ОбПересчет(СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаНДС);
	СтрокаЧека.СуммаСкидки=СуммаСкидки;
	СтрокаЧека.СуммаВсего=?(Пересчет,ОбПересчет(Основание.СуммаДокумента,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Основание.СуммаДокумента);
	Если (СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки)=0 Тогда
		СтрокаЧека.ПроцентСкидки=0;
	Иначе
		СтрокаЧека.ПроцентСкидки=(1-(СтрокаЧека.СуммаВсего/(СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки)))*100;
	КонецЕсли; 
	ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека);
	СтрокаЧека.Цена=Цена;
	СтрокаЧека.Сумма=Сумма;
	СтрокаЧека.СуммаНДС=СуммаНДС;
	СтрокаЧека.СуммаСкидки=СуммаСкидки;
	
	Попытка
		СтрокаЧека.СебестоимостьАвтомобиля=Основание.СебестоимостьАвтомобиля;
	Исключение
	КонецПопытки;
	
	// Заполним комиссионера
	ДанныеДоговораВзаиморасчетов = дкЗаполнитьДоговорКомисси(Основание, Основание.Автомобиль);
	
	Если ДанныеДоговораВзаиморасчетов.Количество() > 0 Тогда
		ДоговорКомиссия = ДанныеДоговораВзаиморасчетов.Получить(Основание.Автомобиль);
		ЗаполнитьЗначенияСвойств(СтрокаЧека, ДоговорКомиссия);
	КонецЕсли;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_ЧекНаОплату()

// Заполнение документа поступление автомобилей на основании заказа
Функция орОбработкаЗаполнения_ЗаказНаАвтомобиль_ЗаказПоставщикуНаАвтомобиль(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ЭтотОбъект.ТипЦен                = обПраво("ОсновнойТипЦенЗакупки", ЭтотОбъект.Права,, ЭтотОбъект);
	ЭтотОбъект.ОбработкаРеквизита("ТипЦен");
	ЭтотОбъект.Контрагент            = Справочники.Контрагенты.ПустаяСсылка();
	ЭтотОбъект.ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	// подберем контрагента из модели
	ЭтотОбъект.ОбработкаРеквизита("Модель");
	ЭтотОбъект.ЦенаАвтомобиля        = обПолучитьЦену(ЭтотОбъект.ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации", Основание.Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),ТекущаяДата(),,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
	ЭтотОбъект.ОбработкаРеквизита("ЦенаАвтомобиля");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_ЗаказПоставщикуНаАвтомобиль() 

// Заполнение документа поступление автомобилей на основании заказа
Функция орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ПоступлениеАвтомобилей(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ЭтотОбъект.Опции.Очистить();
	Если ЗначениеЗаполнено(Основание.Автомобиль) Тогда
		СтрокаАвтомобиля = ЭтотОбъект.Автомобили.Добавить();
		СтрокаАвтомобиля.Автомобиль = Основание.Автомобиль;
		СтрокаАвтомобиля.ИдентификаторАвтомобиля = Новый УникальныйИдентификатор;
		ЭтотОбъект.ОбработкаРеквизита("Автомобили.Автомобиль", СтрокаАвтомобиля);
	КонецЕсли; 
	
	ЭтотОбъект.СуммаДокумента = ЭтотОбъект.РассчитатьСуммуВсего();
	
	Возврат Истина;
	
КонецФункции // орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ПоступлениеАвтомобилей() 

// Заполнение документа счет от поставщика за автомобили на основании заказа
Функция орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_СчетОтПоставщикаЗаАвтомобили(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.Автомобили.Очистить();
	ЭтотОбъект.Опции.Очистить();
	Если ЗначениеЗаполнено(Основание.Автомобиль) Тогда
		СтрокаАвтомобиля = ЭтотОбъект.Автомобили.Добавить();
		СтрокаАвтомобиля.Автомобиль              = Основание.Автомобиль;
		СтрокаАвтомобиля.ИдентификаторАвтомобиля = Новый УникальныйИдентификатор;
		СтрокаАвтомобиля.ВариантКомплектации     = Основание.ВариантКомплектации;
		СтрокаАвтомобиля.Количество              = 1;
		СтрокаАвтомобиля.Цена                    = Основание.ЦенаАвтомобиля;
		СтрокаАвтомобиля.Сумма                   = Основание.ЦенаАвтомобиля;
		СтрокаАвтомобиля.СтавкаНДС               = Основание.СтавкаНДСНаАвтомобиль;
		СтрокаАвтомобиля.СуммаНДС                = Основание.СуммаНДСНаАвтомобиль;
		СтрокаАвтомобиля.СуммаВсего              = Основание.СуммаВсегоНаАвтомобиль;
		
		Для Каждого СтрокаТЧ Из Основание.Опции Цикл
			НоваяСтрокаТЧ = ЭтотОбъект.Опции.Добавить();
			НоваяСтрокаТЧ.ИдентификаторАвтомобиля = СтрокаАвтомобиля.ИдентификаторАвтомобиля;
			НоваяСтрокаТЧ.Опция                   = СтрокаТЧ.Опция;
			НоваяСтрокаТЧ.Количество              = СтрокаТЧ.Количество;
			НоваяСтрокаТЧ.Цена                    = СтрокаТЧ.Цена;
			НоваяСтрокаТЧ.Сумма                   = СтрокаТЧ.Сумма;
			НоваяСтрокаТЧ.СтавкаНДС               = СтрокаТЧ.СтавкаНДС;
			НоваяСтрокаТЧ.СуммаНДС                = СтрокаТЧ.СуммаНДС;
			НоваяСтрокаТЧ.СуммаВсего              = СтрокаТЧ.СуммаВсего;
		КонецЦикла;
		
		ЭтотОбъект.СуммаДокумента = ЭтотОбъект.РассчитатьСуммуВсего();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_СчетОтПоставщикаЗаАвтомобили() 

// Заполнение документа расходный кассовый ордер на основании заказа на автомобиль
Функция орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_РасходныйКассовыйОрдер(ЭтотОбъект, Основание, Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	Если ЗначениеЗаполнено(Основание) И обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование) Тогда
		ЭтотОбъект.ДокументОснование = Основание;
	КонецЕсли;
	
	ЭтотОбъект.Сделка = Основание;
	
	//Статья ДДС - Оплата от поставщику
	ЭтотОбъект.СтатьяДДС                = Справочники.СтатьиДДС.ОплатаПоставщику;
	ЭтотОбъект.КурсВалютыВзаиморасчетов = дкПолучитьКурсВалютыВзаиморасчетов(ЭтотОбъект, ?(обЗначениеНеЗаполнено(ЭтотОбъект.ДоговорВзаиморасчетов), Неопределено, ЭтотОбъект.ДоговорВзаиморасчетов), ?(обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование), Неопределено, ЭтотОбъект.ДокументОснование));
	ЭтотОбъект.ОбработкаРеквизита("КассаКомпании",,,Новый Структура("ЗадаватьВопрос",Ложь));
	
	ОперативнаяОтметкаВремени = ТекущаяДата();
	
	//Проверим сумму предоплаты
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОстаток КАК Сумма,
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрОстаток КАК СуммаУпр,
	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаБазОстаток КАК СуммаБаз
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(,
	|		Контрагент = &Контрагент И
	|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
	|		Сделка = &Сделка
	|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
	|";
	Запрос.УстановитьПараметр("Контрагент",            Основание.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", Основание.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Сделка",                Основание);
	тзОплаты=Запрос.Выполнить().Выгрузить();
	
	СуммаДокумента = Основание.СуммаДокумента;
	Если Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ЭтотОбъект.ВалютаДокумента Тогда
		Оплачено = тзОплаты.Итог("Сумма");
	ИначеЕсли ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		Оплачено = тзОплаты.Итог("СуммаБаз");
	ИначеЕсли ЭтотОбъект.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
		Оплачено = тзОплаты.Итог("СуммаУпр");
	Иначе
		Оплачено = обПересчет(тзОплаты.Итог("Сумма"), Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ОперативнаяОтметкаВремени, ЭтотОбъект.ВалютаДокумента, ОперативнаяОтметкаВремени, РежимОкругления.Окр15как20);
	КонецЕсли;
	
	Если НЕ Основание.ВалютаДокумента = ЭтотОбъект.ВалютаДокумента Тогда
		СуммаДокумента = обПересчет(СуммаДокумента, Основание.ВалютаДокумента, Основание.Дата, ЭтотОбъект.ВалютаДокумента, ОперативнаяОтметкаВремени, РежимОкругления.Окр15как20);
	КонецЕсли;
	
	ЭтотОбъект.СуммаДокумента = СуммаДокумента - Оплачено;
	ЭтотОбъект.ОбработкаРеквизита("СуммаДокумента");
	
	Основание = Неопределено;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_ЗаказНаАвтомобиль_РасходныйКассовыйОрдер() 

// Заполнение реализации автомобилей на основании счета на оплату
Функция орОбработкаЗаполнения_СчетНаОплатуЗаАвтомобили_РеализацияАвтомобилей(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	//ЭтотОбъект.Товары.Очистить();
	//
	//Для каждого СтрокаАвтомобиля Из ЭтотОбъект.Автомобили Цикл
	//	Заказ=ЭтотОбъект.НайтиЗаказАвтомобиля(СтрокаАвтомобиля.Автомобиль);
	//	Если НЕ обЗначениеНеЗаполнено(Заказ) Тогда
	//		СтрокаАвтомобиля.ЗаказНаАвтомобиль=Заказ;
	//	КонецЕсли; 
	//	ЭтотОбъект.ДобавитьОборудованиеАвтомобиля(СтрокаАвтомобиля.Автомобиль);
	//КонецЦикла; 
	
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.РассчитатьСуммуВсего();
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_СчетНаОплатуЗаАвтомобили_РеализацияАвтомобилей() 

// Заполнение чека на оплату на основании счета на оплату
Функция орОбработкаЗаполнения_СчетНаОплатуЗаАвтомобили_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);
	
	НоменклатураАвтомобиль = Справочники.Номенклатура.Автомобиль;
	АвтомобильТипНоменклатуры = НоменклатураАвтомобиль.ТипНоменклатуры;
	
	Если НЕ обЗначениеНеЗаполнено(Основание.ДокументОснование)
		И ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
		ТаблицаГТД = РегистрыНакопления.ОстаткиАвтомобилей.ПолучитьГТДАвтомобилей(Основание.ДокументОснование);
	Иначе
		ТаблицаГТД = Неопределено;
	КонецЕсли;
	
	// Заполнение договора комиссионера.
	ДоговораКомиссии = дкЗаполнитьДоговорКомисси(
		?(ЗначениеЗаполнено(Основание.ДокументОснование), Основание.ДокументОснование, Основание),
		Основание.Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	ЭтотОбъект.Товары.Очистить();
	Для каждого СтрокаАвтомобиля Из Основание.Автомобили Цикл
		
		//Если у автомобиля есть доп. свойство "Номенклатура" и оно заполнено, подставим его значение
		Автомобиль = обПолучитьЗначениеСвойства(СтрокаАвтомобиля.Автомобиль, "Номенклатура", НоменклатураАвтомобиль);
		Автомобиль = ?(ЗначениеЗаполнено(Автомобиль), Автомобиль, НоменклатураАвтомобиль);
		
		//Создадим характеристику для автомобиля
		Если ТипЗнч(СтрокаАвтомобиля.Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			VIN=СтрокаАвтомобиля.Автомобиль.VIN;
		Иначе
			VIN=СтрокаАвтомобиля.Автомобиль.Наименование;
		КонецЕсли; 
		ХарактеристикаНоменклатурыНаименование="VIN "+VIN;
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(ХарактеристикаНоменклатурыНаименование,Истина,,АвтомобильТипНоменклатуры);
		Если ХарактеристикаНоменклатуры.Пустая() Тогда
			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
			ХарактеристикаНоменклатуры.УстановитьНовыйКод();
			ХарактеристикаНоменклатуры.Владелец=АвтомобильТипНоменклатуры;
			ХарактеристикаНоменклатуры.Наименование=ХарактеристикаНоменклатурыНаименование;
			ХарактеристикаНоменклатуры.СерийныйНомер=VIN;
			ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
			Попытка
				ХарактеристикаНоменклатуры.Записать();
			Исключение
				Сообщить("Ошибка создания новой характеристики автомобиля: "+ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки; 
		КонецЕсли; 
		Цена=СтрокаАвтомобиля.Сумма;
		Сумма=СтрокаАвтомобиля.Сумма;
		СуммаНДС=СтрокаАвтомобиля.СуммаНДС;
		СуммаСкидки=СтрокаАвтомобиля.СуммаСкидки;
		СуммаВсего=СтрокаАвтомобиля.СуммаВсего;
		СтрокиОборудования=Основание.Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",СтрокаАвтомобиля.ИдентификаторАвтомобиля));
		Для каждого СтрокаОборудования Из СтрокиОборудования Цикл
			Цена=Цена+СтрокаОборудования.Сумма;
			Сумма=Сумма+СтрокаОборудования.Сумма;
			СуммаНДС=СуммаНДС+СтрокаОборудования.СуммаНДС;
			СуммаСкидки=СуммаСкидки+СтрокаОборудования.СуммаСкидки;
			СуммаВсего=СуммаВсего+СтрокаОборудования.СуммаВсего;
		КонецЦикла; 
		Цена=?(Пересчет,ОбПересчет(Цена,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Цена);
		Сумма=?(Пересчет,ОбПересчет(Сумма,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Сумма);
		СуммаНДС=?(Пересчет,ОбПересчет(СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаНДС);
		СуммаСкидки=?(Пересчет,ОбПересчет(СуммаСкидки,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаСкидки);
		СуммаВсего=?(Пересчет,ОбПересчет(СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаВсего);
		
		СтрокаЧека=ЭтотОбъект.Товары.Добавить();
		СтрокаЧека.Номенклатура=Автомобиль;
		СтрокаЧека.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
		ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаЧека);
		СтрокаЧека.Количество=1;
		
		СтрокаЧека.СуммаСкидки=СуммаСкидки;
		СтрокаЧека.СуммаВсего=СуммаВсего;
		СтрокаЧека.ПроцентСкидки=(1-(СтрокаЧека.СуммаВсего/(СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки)))*100;
		ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека);
		СтрокаЧека.Цена=Цена;
		СтрокаЧека.Сумма=Сумма;
		СтрокаЧека.СуммаНДС=СуммаНДС;
		СтрокаЧека.СтавкаНДС=СтрокаАвтомобиля.СтавкаНДС;
		СтрокаЧека.СуммаСкидки=СуммаСкидки;
		Попытка
			СтрокаЧека.СебестоимостьАвтомобиля=СтрокаАвтомобиля.СебестоимостьАвтомобиля;
		Исключение
		КонецПопытки;
		
		Если НЕ ТаблицаГТД = Неопределено Тогда
			НайденнаяСтрокаГТД = ТаблицаГТД.Найти(СтрокаАвтомобиля.Автомобиль, "Автомобиль");
			Если НЕ НайденнаяСтрокаГТД = Неопределено Тогда
				СтрокаЧека.ГТД=НайденнаяСтрокаГТД.ГТД;
			КонецЕсли;
		КонецЕсли;
		
		ДоговорКомиссии = ДоговораКомиссии.Получить(СтрокаАвтомобиля.Автомобиль);
		Если НЕ ДоговорКомиссии = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаЧека, ДоговорКомиссии);
		КонецЕсли;
	КонецЦикла; 
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_СчетНаОплатуЗаАвтомобили_ЧекНаОплату()

// Заполнение документа счет на оплату на основании реализации автомобилей
Функция орОбработкаЗаполнения_РеализацияАвтомобилей_СчетНаОплатуЗаАвтомобили(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	//ЭтотОбъект.Товары.Очистить();
	//
	//Для каждого СтрокаАвтомобиля Из ЭтотОбъект.Автомобили Цикл
	//	СтрокаАвтомобиля.ИдентификаторАвтомобиля=Новый УникальныйИдентификатор;
	//	СтрокиТоваров=Основание.Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",
	//	СтрокаАвтомобиля.ИдентификаторАвтомобиля));
	//	Для каждого СтрокаТоваров Из СтрокиТоваров Цикл
	//		НоваяСтрокаТоваров=ЭтотОбъект.Товары.Добавить();
	//		НоваяСтрокаТоваров.ИдентификаторАвтомобиля=СтрокаАвтомобиля.ИдентификаторАвтомобиля;
	//		НоваяСтрокаТоваров.Номенклатура=СтрокаТоваров.Номенклатура;
	//		НоваяСтрокаТоваров.Количество=СтрокаТоваров.Количество;
	//		НоваяСтрокаТоваров.ЕдиницаИзмерения=СтрокаТоваров.ЕдиницаИзмерения;
	//		НоваяСтрокаТоваров.Коэффициент=СтрокаТоваров.Коэффициент;
	//		НоваяСтрокаТоваров.Цена=СтрокаТоваров.Цена;
	//		НоваяСтрокаТоваров.Сумма=СтрокаТоваров.Сумма;
	//		НоваяСтрокаТоваров.СтавкаНДС=СтрокаТоваров.СтавкаНДС;
	//		НоваяСтрокаТоваров.СуммаНДС=СтрокаТоваров.СуммаНДС;
	//		НоваяСтрокаТоваров.ПроцентСкидки=СтрокаТоваров.ПроцентСкидки;
	//		НоваяСтрокаТоваров.СуммаСкидки=СтрокаТоваров.СуммаСкидки;
	//		НоваяСтрокаТоваров.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
	//		НоваяСтрокаТоваров.СуммаВсего=СтрокаТоваров.СуммаВсего;
	//	КонецЦикла; 
	//КонецЦикла; 
	//
	//ЭтотОбъект.СуммаДокумента=ЭтотОбъект.РассчитатьСуммуВсего();
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_РеализацияАвтомобилей_СчетНаОплатуЗаАвтомобили() 

// Заполнение документа приходно кассовый ордер на основании реализации автомобилей
Функция орОбработкаЗаполнения_РеализацияАвтомобилей_ПриходныйКассовыйОрдер(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.Товары.Очистить();
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование)) Тогда
		ЭтотОбъект.ДокументОснование=Основание;
	КонецЕсли; 
	ОперативнаяОтметкаВремени=ТекущаяДата();
	СтруктураОтбора=Новый Структура(); 
	СтруктураОтбора.Вставить("Контрагент",ЭтотОбъект.ДокументОснование.Контрагент);
	СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов);
	СтруктураОтбора.Вставить("Сделка",ЭтотОбъект.ДокументОснование);
	тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ОперативнаяОтметкаВремени,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
	Если ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ЭтотОбъект.ВалютаДокумента Тогда
		ОстатокПоСделке=обПересчет(тзДолги.Итог("Сумма"),ЭтотОбъект.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ЭтотОбъект.ДокументОснование.КурсДокумента,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
	ИначеЕсли Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()=ЭтотОбъект.ВалютаДокумента Тогда
		ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
	Иначе
		ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ЭтотОбъект.ДокументОснование.КурсВалютыУпр,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента);
	КонецЕсли;
	
	ЭтотОбъект.СуммаДокумента=ОстатокПоСделке;
	ЭтотОбъект.ОбработкаРеквизита("СуммаДокумента");
	//Статья ДДС - Оплата от покупателя
	ЭтотОбъект.СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_РеализацияАвтомобилей_ПриходныйКассовыйОрдер() 

// Заполнение документа чек на оплату на основании реализации автомобилей
Функция орОбработкаЗаполнения_РеализацияАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);				
	
	НоменклатураАвтомобиль = Справочники.Номенклатура.Автомобиль;
	АвтомобильТипНоменклатуры = НоменклатураАвтомобиль.ТипНоменклатуры;
	
	ТаблицаГТД = РегистрыНакопления.ОстаткиАвтомобилей.ПолучитьГТДАвтомобилей(Основание);
	
	// Заполнение договора комиссионера.
	ДоговораКомиссии = дкЗаполнитьДоговорКомисси(Основание, Основание.Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	ЭтотОбъект.Товары.Очистить();
	Для каждого СтрокаАвтомобиля Из Основание.Автомобили Цикл
		
		//Если у автомобиля есть доп. свойство "Номенклатура" и оно заполнено, подставим его значение
		Автомобиль = обПолучитьЗначениеСвойства(СтрокаАвтомобиля.Автомобиль, "Номенклатура", НоменклатураАвтомобиль);
		Автомобиль = ?(ЗначениеЗаполнено(Автомобиль), Автомобиль, НоменклатураАвтомобиль);
		
		ПризнакПредметаРасчета = СтрокаАвтомобиля.Автомобиль.Модель.ПризнакПредметаРасчета;
		Если НЕ ЗначениеЗаполнено(ПризнакПредметаРасчета) Тогда
			ПризнакПредметаРасчета = Автомобиль.ТипНоменклатуры.ПризнакПредметаРасчета;
		КонецЕсли;
		
		//Создадим характеристику для автомобиля
		VIN=СтрокаАвтомобиля.Автомобиль.VIN;
		ХарактеристикаНоменклатурыНаименование="VIN "+VIN;
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(ХарактеристикаНоменклатурыНаименование,Истина,,АвтомобильТипНоменклатуры);
		Если ХарактеристикаНоменклатуры.Пустая() Тогда
			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
			ХарактеристикаНоменклатуры.УстановитьНовыйКод();
			ХарактеристикаНоменклатуры.Владелец=АвтомобильТипНоменклатуры;
			ХарактеристикаНоменклатуры.Наименование=ХарактеристикаНоменклатурыНаименование;
			ХарактеристикаНоменклатуры.СерийныйНомер=VIN;
			ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
			Попытка
				ХарактеристикаНоменклатуры.Записать();
			Исключение
				Сообщить("Ошибка создания новой характеристики автомобиля: "+ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки; 
		КонецЕсли; 
		Цена=СтрокаАвтомобиля.Сумма;
		Сумма=СтрокаАвтомобиля.Сумма;
		СуммаНДС=СтрокаАвтомобиля.СуммаНДС;
		СуммаСкидки=СтрокаАвтомобиля.СуммаСкидки;
		СуммаСкидкиСтроки=0;
		СуммаВсего=СтрокаАвтомобиля.СуммаВсего;
		СтрокиОборудования=Основание.Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",СтрокаАвтомобиля.ИдентификаторАвтомобиля));
		Для каждого СтрокаОборудования Из СтрокиОборудования Цикл
			Цена=Цена+СтрокаОборудования.Сумма;
			Сумма=Сумма+СтрокаОборудования.Сумма;
			СуммаНДС=СуммаНДС+СтрокаОборудования.СуммаНДС;
			СуммаСкидки=СуммаСкидки+СтрокаОборудования.СуммаСкидки;
			СуммаСкидкиСтроки=СуммаСкидкиСтроки+СтрокаОборудования.СуммаСкидкиСтроки;
			СуммаВсего=СуммаВсего+СтрокаОборудования.СуммаВсего;
		КонецЦикла; 
		Цена=?(Пересчет,ОбПересчет(Цена,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Цена);
		Сумма=?(Пересчет,ОбПересчет(Сумма,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Сумма);
		СуммаНДС=?(Пересчет,ОбПересчет(СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаНДС);
		СуммаСкидки=?(Пересчет,ОбПересчет(СуммаСкидки,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаСкидки);
		СуммаВсего=?(Пересчет,ОбПересчет(СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаВсего);
		
		СтрокаЧека=ЭтотОбъект.Товары.Добавить();
		СтрокаЧека.Номенклатура=Автомобиль;
		СтрокаЧека.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
		ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаЧека);
		СтрокаЧека.Количество=1;
		
		//<<БАА
		СтрокаЧека.СтавкаНДС = СтрокаАвтомобиля.СтавкаНДС;
		ЭтотОбъект.ОбработкаРеквизита("Товары.СтавкаНДС",СтрокаЧека);
		//>>БАА

		
		СтрокаЧека.СуммаСкидки=СуммаСкидки;
		СтрокаЧека.СуммаВсего=СуммаВсего;
		СтрокаЧека.СуммаСкидкиСтроки=СуммаСкидкиСтроки;
		Если (СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки+СтрокаЧека.СуммаСкидкиСтроки) <> 0 Тогда
			СтрокаЧека.ПроцентСкидки=(1-(СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидкиСтроки)/(СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки+СтрокаЧека.СуммаСкидкиСтроки))*100;
			СтрокаЧека.ПроцентСкидкиСтроки=(1-(СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидки)/(СтрокаЧека.СуммаВсего+СтрокаЧека.СуммаСкидкиСтроки+СтрокаЧека.СуммаСкидки))*100;
		КонецЕсли;
		ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека);
		СтрокаЧека.Цена=Цена;
		СтрокаЧека.Сумма=Сумма;
		СтрокаЧека.СуммаНДС=СуммаНДС;
		СтрокаЧека.СуммаСкидки=СуммаСкидки;
		СтрокаЧека.СуммаСкидкиСтроки=СуммаСкидкиСтроки;
		Попытка
			СтрокаЧека.СебестоимостьАвтомобиля=СтрокаАвтомобиля.СебестоимостьАвтомобиля;
		Исключение
		КонецПопытки;
		
		Если НЕ ТаблицаГТД = Неопределено Тогда
			НайденнаяСтрокаГТД = ТаблицаГТД.Найти(СтрокаАвтомобиля.Автомобиль, "Автомобиль");
			Если НЕ НайденнаяСтрокаГТД = Неопределено Тогда
				СтрокаЧека.ГТД=НайденнаяСтрокаГТД.ГТД;
			КонецЕсли;
		КонецЕсли;
		
		ДоговорКомиссии = ДоговораКомиссии.Получить(СтрокаАвтомобиля.Автомобиль);
		Если НЕ ДоговорКомиссии = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаЧека, ДоговорКомиссии);
		КонецЕсли;
		
		СтрокаЧека.ПризнакПредметаРасчета = ПризнакПредметаРасчета;
		
	КонецЦикла;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_РеализацияАвтомобилей_ЧекНаОплату()

// Заполнение документа чек на оплату на основании возврата поставщику автомобилей
Функция орОбработкаЗаполнения_ВозвратПоставщикуАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);				
	
	НоменклатураАвтомобиль = Справочники.Номенклатура.Автомобиль;
	АвтомобильТипНоменклатуры = НоменклатураАвтомобиль.ТипНоменклатуры;
	
	ТаблицаГТД = РегистрыНакопления.ОстаткиАвтомобилей.ПолучитьГТДАвтомобилей(Основание);
	
	// Заполнение договора комиссионера.
	ДоговораКомиссии = дкЗаполнитьДоговорКомисси(
		?(ЗначениеЗаполнено(Основание.ДокументОснование), Основание.ДокументОснование, Основание),
		Основание.Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	ЭтотОбъект.Товары.Очистить();
	Для каждого СтрокаАвтомобиля Из Основание.Автомобили Цикл
		
		//Если у автомобиля есть доп. свойство "Номенклатура" и оно заполнено, подставим его значение
		Автомобиль = обПолучитьЗначениеСвойства(СтрокаАвтомобиля.Автомобиль, "Номенклатура", НоменклатураАвтомобиль);
		Автомобиль = ?(ЗначениеЗаполнено(Автомобиль), Автомобиль, НоменклатураАвтомобиль);
		
		//Создадим характеристику для автомобиля
		VIN=СтрокаАвтомобиля.Автомобиль.VIN;
		ХарактеристикаНоменклатурыНаименование="VIN "+VIN;
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(ХарактеристикаНоменклатурыНаименование,Истина,,АвтомобильТипНоменклатуры);
		Если ХарактеристикаНоменклатуры.Пустая() Тогда
			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
			ХарактеристикаНоменклатуры.УстановитьНовыйКод();
			ХарактеристикаНоменклатуры.Владелец=АвтомобильТипНоменклатуры;
			ХарактеристикаНоменклатуры.Наименование=ХарактеристикаНоменклатурыНаименование;
			ХарактеристикаНоменклатуры.СерийныйНомер=VIN;
			ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
			Попытка
				ХарактеристикаНоменклатуры.Записать();
			Исключение
				Сообщить("Ошибка создания новой характеристики автомобиля: "+ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки; 
		КонецЕсли; 
		Цена=СтрокаАвтомобиля.Цена;
		Сумма=СтрокаАвтомобиля.Сумма;
		СуммаНДС=СтрокаАвтомобиля.СуммаНДС;
		СуммаВсего=СтрокаАвтомобиля.СуммаВсего;
		СтрокиОборудования=Основание.Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",СтрокаАвтомобиля.ИдентификаторАвтомобиля));
		Для каждого СтрокаОборудования Из СтрокиОборудования Цикл
			Цена=Цена+СтрокаОборудования.Цена;
			Сумма=Сумма+СтрокаОборудования.Сумма;
			СуммаНДС=СуммаНДС+СтрокаОборудования.СуммаНДС;
			СуммаВсего=СуммаВсего+СтрокаОборудования.СуммаВсего;
		КонецЦикла; 
		Цена=?(Пересчет,ОбПересчет(Цена,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Цена);
		Сумма=?(Пересчет,ОбПересчет(Сумма,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Сумма);
		СуммаНДС=?(Пересчет,ОбПересчет(СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаНДС);
		СуммаВсего=?(Пересчет,ОбПересчет(СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаВсего);
		
		СтрокаЧека=ЭтотОбъект.Товары.Добавить();
		СтрокаЧека.Номенклатура=Автомобиль;
		СтрокаЧека.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
		ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаЧека);
		СтрокаЧека.Количество=1;
		
		СтрокаЧека.СуммаВсего=СуммаВсего;
		ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека);
		СтрокаЧека.Цена=Цена;
		СтрокаЧека.Сумма=Сумма;
		СтрокаЧека.СуммаНДС=СуммаНДС;
		
		Если НЕ ТаблицаГТД = Неопределено Тогда
			НайденнаяСтрокаГТД = ТаблицаГТД.Найти(СтрокаАвтомобиля.Автомобиль, "Автомобиль");
			Если НЕ НайденнаяСтрокаГТД = Неопределено Тогда
				СтрокаЧека.ГТД=НайденнаяСтрокаГТД.ГТД;
			КонецЕсли;
		КонецЕсли;
		
		ДоговорКомиссии = ДоговораКомиссии.Получить(СтрокаАвтомобиля.Автомобиль);
		Если НЕ ДоговорКомиссии = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаЧека, ДоговорКомиссии);
		КонецЕсли;
		
	КонецЦикла;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
	
КонецФункции

// Заполнение документа чек на оплату на основании счет поставщика за автомобили
Функция орОбработкаЗаполнения_СчетОтПоставщикаЗаАвтомобили_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	
	Возврат орОбработкаЗаполнения_ПоступлениеАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
	
КонецФункции

// Заполнение документа чек на оплату на основании заказ поставщику автомобилей
Функция орОбработкаЗаполнения_ЗаказПоставщикуНаАвтомобиль_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);
	
	НоменклатураАвтомобиль = Справочники.Номенклатура.Автомобиль;
	АвтомобильТипНоменклатуры = НоменклатураАвтомобиль.ТипНоменклатуры;
	
	ЭтотОбъект.Товары.Очистить();
	
	//Если у автомобиля есть доп. свойство "Номенклатура" и оно заполнено, подставим его значение
	Автомобиль = обПолучитьЗначениеСвойства(Основание.Автомобиль, "Номенклатура", НоменклатураАвтомобиль);
	Автомобиль = ?(ЗначениеЗаполнено(Автомобиль), Автомобиль, НоменклатураАвтомобиль);
	
	//Создадим характеристику для автомобиля
	VIN=Основание.Автомобиль.VIN;
	ХарактеристикаНоменклатурыНаименование="VIN "+VIN;
	ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(ХарактеристикаНоменклатурыНаименование,Истина,,АвтомобильТипНоменклатуры);
	Если ХарактеристикаНоменклатуры.Пустая() Тогда
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
		ХарактеристикаНоменклатуры.УстановитьНовыйКод();
		ХарактеристикаНоменклатуры.Владелец=АвтомобильТипНоменклатуры;
		ХарактеристикаНоменклатуры.Наименование=ХарактеристикаНоменклатурыНаименование;
		ХарактеристикаНоменклатуры.СерийныйНомер=VIN;
		ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
		Попытка
			ХарактеристикаНоменклатуры.Записать();
		Исключение
			Сообщить("Ошибка создания новой характеристики автомобиля: "+ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки; 
	КонецЕсли; 
	Цена=Основание.ЦенаАвтомобиля;
	Сумма=Основание.ЦенаАвтомобиля;
	СуммаНДС=Основание.СуммаНДСНаАвтомобиль;
	СуммаВсего=Основание.СуммаВсегоНаАвтомобиль;
	
	Для каждого СтрокаОпция Из Основание.Опции Цикл
		Цена=Цена+СтрокаОпция.Цена;
		Сумма=Сумма+СтрокаОпция.Сумма;
		СуммаНДС=СуммаНДС+СтрокаОпция.СуммаНДС;
		СуммаВсего=СуммаВсего+СтрокаОпция.СуммаВсего;
	КонецЦикла; 
	
	Цена=?(Пересчет,ОбПересчет(Цена,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Цена);
	Сумма=?(Пересчет,ОбПересчет(Сумма,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Сумма);
	СуммаНДС=?(Пересчет,ОбПересчет(СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаНДС);
	СуммаВсего=?(Пересчет,ОбПересчет(СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаВсего);
	
	СтрокаЧека=ЭтотОбъект.Товары.Добавить();
	СтрокаЧека.Номенклатура=Автомобиль;
	СтрокаЧека.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
	ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаЧека);
	СтрокаЧека.Количество=1;
	СтрокаЧека.СтавкаНДС = Основание.СтавкаНДСНаАвтомобиль;
	СтрокаЧека.СуммаВсего=СуммаВсего;
	ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека);
	СтрокаЧека.Цена=Цена;
	СтрокаЧека.Сумма=Сумма;
	СтрокаЧека.СуммаНДС=СуммаНДС;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
	
КонецФункции

// Заполнение документа чек на оплату на основании возврат покупателя автомобилей
Функция орОбработкаЗаполнения_ВозвратОтПокупателяАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	
	Возврат орОбработкаЗаполнения_РеализацияАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование);
	
КонецФункции

// Заполнение документа чек на оплату на основании поступления автомобилей
Функция орОбработкаЗаполнения_ПоступлениеАвтомобилей_ЧекНаОплату(ЭтотОбъект,Основание,Копирование)
	
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	ВалютаОснования = Основание.ВалютаДокумента;
	КурсОснования   = Основание.КурсДокумента;
	Пересчет = (ВалютаОснования <> ЭтотОбъект.ВалютаДокумента)ИЛИ(КурсОснования <> ЭтотОбъект.КурсДокумента);				
	
	НоменклатураАвтомобиль = Справочники.Номенклатура.Автомобиль;
	АвтомобильТипНоменклатуры = НоменклатураАвтомобиль.ТипНоменклатуры;
	
	СделкаОснования = Неопределено;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили") Тогда
		СделкаОснования = Основание.ДокументОснование;
		Если ЗначениеЗаполнено(СделкаОснования) И ТипЗнч(СделкаОснования) = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
			СделкаОснования = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаГТД = РегистрыНакопления.ОстаткиАвтомобилей.ПолучитьГТДАвтомобилей(?(ЗначениеЗаполнено(СделкаОснования), СделкаОснования, Основание));
	
	// Заполнение договора комиссионера.
	ДоговораКомиссии = дкЗаполнитьДоговорКомисси(
		?(ЗначениеЗаполнено(СделкаОснования), СделкаОснования, Основание),
		Основание.Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	
	ЭтотОбъект.Товары.Очистить();
	Для каждого СтрокаАвтомобиля Из Основание.Автомобили Цикл
		
		//Если у автомобиля есть доп. свойство "Номенклатура" и оно заполнено, подставим его значение
		Автомобиль = обПолучитьЗначениеСвойства(СтрокаАвтомобиля.Автомобиль, "Номенклатура", НоменклатураАвтомобиль);
		Автомобиль = ?(ЗначениеЗаполнено(Автомобиль), Автомобиль, НоменклатураАвтомобиль);
		
		//Создадим характеристику для автомобиля
		VIN=СтрокаАвтомобиля.Автомобиль.VIN;
		ХарактеристикаНоменклатурыНаименование="VIN "+VIN;
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(ХарактеристикаНоменклатурыНаименование,Истина,,АвтомобильТипНоменклатуры);
		Если ХарактеристикаНоменклатуры.Пустая() Тогда
			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
			ХарактеристикаНоменклатуры.УстановитьНовыйКод();
			ХарактеристикаНоменклатуры.Владелец=АвтомобильТипНоменклатуры;
			ХарактеристикаНоменклатуры.Наименование=ХарактеристикаНоменклатурыНаименование;
			ХарактеристикаНоменклатуры.СерийныйНомер=VIN;
			ХарактеристикаНоменклатуры.ОбменДанными.Загрузка=Истина;
			Попытка
				ХарактеристикаНоменклатуры.Записать();
			Исключение
				Сообщить("Ошибка создания новой характеристики автомобиля: "+ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки; 
		КонецЕсли; 
		Цена=СтрокаАвтомобиля.Цена;
		Сумма=СтрокаАвтомобиля.Сумма;
		СуммаНДС=СтрокаАвтомобиля.СуммаНДС;
		СуммаВсего=СтрокаАвтомобиля.СуммаВсего;
		
		СтрокиОпции=Основание.Опции.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",СтрокаАвтомобиля.ИдентификаторАвтомобиля));
		Для каждого СтрокаОпция Из СтрокиОпции Цикл
			Цена=Цена+СтрокаОпция.Цена;
			Сумма=Сумма+СтрокаОпция.Сумма;
			СуммаНДС=СуммаНДС+СтрокаОпция.СуммаНДС;
			СуммаВсего=СуммаВсего+СтрокаОпция.СуммаВсего;
		КонецЦикла; 
		
		Цена=?(Пересчет,ОбПересчет(Цена,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Цена);
		Сумма=?(Пересчет,ОбПересчет(Сумма,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),Сумма);
		СуммаНДС=?(Пересчет,ОбПересчет(СуммаНДС,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаНДС);
		СуммаВсего=?(Пересчет,ОбПересчет(СуммаВсего,ВалютаОснования,КурсОснования,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.КурсДокумента),СуммаВсего);
		
		СтрокаЧека=ЭтотОбъект.Товары.Добавить();
		СтрокаЧека.Номенклатура=Автомобиль;
		СтрокаЧека.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры.Ссылка;
		ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаЧека);
		СтрокаЧека.Количество=1;
		СтрокаЧека.СтавкаНДС = СтрокаАвтомобиля.СтавкаНДС;
		СтрокаЧека.СуммаВсего=СуммаВсего;
		ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",СтрокаЧека);
		СтрокаЧека.Цена=Цена;
		СтрокаЧека.Сумма=Сумма;
		СтрокаЧека.СуммаНДС=СуммаНДС;
		
		Если НЕ ТаблицаГТД = Неопределено Тогда
			НайденнаяСтрокаГТД = ТаблицаГТД.Найти(СтрокаАвтомобиля.Автомобиль, "Автомобиль");
			Если НЕ НайденнаяСтрокаГТД = Неопределено Тогда
				СтрокаЧека.ГТД=НайденнаяСтрокаГТД.ГТД;
			КонецЕсли;
		КонецЕсли;
		
		ДоговорКомиссии = ДоговораКомиссии.Получить(СтрокаАвтомобиля.Автомобиль);
		Если НЕ ДоговорКомиссии = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаЧека, ДоговорКомиссии);
		КонецЕсли;
		
	КонецЦикла;
	
	//Подсчет новой суммы документа назначения
	ЭтотОбъект.СуммаДокумента=ЭтотОбъект.Товары.Итог("СуммаВсего");
	
	Возврат Истина;
	
КонецФункции

// Заполнение документа взаиморасчет по TradeIn
Функция орОбработкаЗаполнения_TradeIn_Взаимозачет(ЭтотОбъект,Основание,Копирование)
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Свойство",ПланыВидовХарактеристик.СвойстваОбъектов.ЗаказНаАвтомобиль);
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗначенияСвойствОбъектов.Объект КАК ПоступлениеАвтомобилей,
		|	ЗначенияСвойствОбъектов.Значение КАК ЗаказНаАвтомобиль
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ПоступлениеАвтомобилей
		|	И ЗначенияСвойствОбъектов.Свойство = &Свойство
		|	И ЗначенияСвойствОбъектов.Значение = &Значение";
		Запрос.УстановитьПараметр("Значение",Основание);
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеАвтомобилей") Тогда
		Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗначенияСвойствОбъектов.Объект КАК ПоступлениеАвтомобилей,
		|	ЗначенияСвойствОбъектов.Значение КАК ЗаказНаАвтомобиль
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Объект = &Объект
		|	И ЗначенияСвойствОбъектов.Свойство = &Свойство
		|	И ЗначенияСвойствОбъектов.Значение ССЫЛКА Документ.ЗаказНаАвтомобиль";
		Запрос.УстановитьПараметр("Объект",Основание);
	Иначе
		Возврат Неопределено;
	КонецЕсли; 
	Выборка=Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	
	СделкаЗаказНаАвтомобиль=Выборка.ЗаказНаАвтомобиль;
	СделкаПоступлениеАвтомобилей=Выборка.ПоступлениеАвтомобилей;
	СтруктураОтбора=Новый Структура();
	СтруктураОтбора.Вставить("Контрагент",СделкаПоступлениеАвтомобилей.Контрагент);
	СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",СделкаПоступлениеАвтомобилей.ДоговорВзаиморасчетов);
	тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
	Если ЭтотОбъект.ВалютаДокумента=СделкаПоступлениеАвтомобилей.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов Тогда
		ВзаиморасчетыПоПоступлениеАвтомобилей=обПересчет(тзДолги.Итог("Сумма"),СделкаПоступлениеАвтомобилей.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,СделкаПоступлениеАвтомобилей.Дата,ЭтотОбъект.ВалютаДокумента,СделкаПоступлениеАвтомобилей.Дата,РежимОкругления.Окр15как20);
	ИначеЕсли ЭтотОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ВзаиморасчетыПоПоступлениеАвтомобилей=тзДолги.Итог("СуммаБаз");
	Иначе
		ВзаиморасчетыПоПоступлениеАвтомобилей=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),СделкаПоступлениеАвтомобилей.Дата,ЭтотОбъект.ВалютаДокумента,СделкаПоступлениеАвтомобилей.Дата,РежимОкругления.Окр15как20);
	КонецЕсли;
	ВзаиморасчетыПоПоступлениеАвтомобилей=?(ВзаиморасчетыПоПоступлениеАвтомобилей<0,-ВзаиморасчетыПоПоступлениеАвтомобилей,0);
	
	СтрокаВзвимозачетов=ЭтотОбъект.Состав.Добавить();
	ЭтотОбъект.Дебитор=СделкаЗаказНаАвтомобиль.Контрагент;
	ЭтотОбъект.Кредитор=СделкаПоступлениеАвтомобилей.Контрагент;
	СтрокаВзвимозачетов.ДоговорВзаиморасчетовДебитор=СделкаЗаказНаАвтомобиль.ДоговорВзаиморасчетов;
	СтрокаВзвимозачетов.СделкаДебитор=СделкаЗаказНаАвтомобиль;
	СтрокаВзвимозачетов.ДоговорВзаиморасчетовКредитор=СделкаПоступлениеАвтомобилей.ДоговорВзаиморасчетов;
	СтрокаВзвимозачетов.СделкаКредитор=СделкаПоступлениеАвтомобилей;
	СтрокаВзвимозачетов.Сумма=ВзаиморасчетыПоПоступлениеАвтомобилей;
	
	Возврат Истина;
КонецФункции // орОбработкаЗаполнения_TradeIn_Взаимозачет() 

Функция орОбработкаЗаполнения_КорректировкаРеализации_СчетФактураПолученный(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.ХозОперация = Справочники.ХозОперации.СчетФактураПолученныйКорректировка;
	ЭтотОбъект.Товары.Очистить();
	
	Для Каждого стрОснования Из Основание.Товары Цикл
		Если стрОснования.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если стрОснования.Подтверждение = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект.Товары.Добавить(), стрОснования);
	КонецЦикла;
	
	// закроем дальнейшее заполнение
	ЭтотОбъект.ДокументОснование = Основание;
	//Основание = Неопределено;
КонецФункции

Процедура орОбработкаЗаполнения_КорректировкаПоступленияАвтомобили_СчетФактураПолученный(ЭтотОбъект,Основание,Копирование)
	дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект, Основание, Копирование);
	ЭтотОбъект.ХозОперация = Справочники.ХозОперации.СчетФактураПолученныйКорректировка;
	ЭтотОбъект.Товары.Очистить();
	
	Для Каждого стрОснования Из Основание.Автомобили Цикл
		
		Если стрОснования.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ЭтотОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, стрОснования);
		НоваяСтрока.Номенклатура = стрОснования.Автомобиль;
		НоваяСтрока.Количество   = 1;
		НоваяСтрока.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;
		НоваяСтрока.Коэффициент = 1;
	КонецЦикла;
	
	// закроем дальнейшее заполнение
	ЭтотОбъект.ДокументОснование = Основание;
КонецПроцедуры

// Список "РабочиеЛисты" при получении данных
//
Процедура орСписокРабочиеЛистыПриПолученииДанных(ОформлениеСтроки, ДанныеСтроки) Экспорт
	СтатусРабочегоЛиста = Справочники.СтатусыРабочегоЛиста.Подготовка;
	Если ТипЗнч(ДанныеСтроки.Ссылка)=Тип("ДокументСсылка.РабочийЛист") ИЛИ 
		ТипЗнч(ДанныеСтроки.Ссылка)=Тип("ДокументСсылка.РабочийЛистКредитногоОтдела") ИЛИ 
		ТипЗнч(ДанныеСтроки.Ссылка)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
		СтатусРабочегоЛиста = ДанныеСтроки.Ссылка.Статус;
	Иначе
		Попытка
			СтатусРабочегоЛиста = ДанныеСтроки.Ссылка.ДокументОснование.Статус;
		Исключение
		КонецПопытки; 
	КонецЕсли; 
	Если НЕ обЗначениеНеЗаполнено(СтатусРабочегоЛиста) Тогда
		ЦветФона=СтатусРабочегоЛиста.Цвет.Получить();
		Если НЕ обЗначениеНеЗаполнено(ЦветФона) Тогда
			ОформлениеСтроки.ЦветФона=ЦветФона;
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры //орСписокЗаказНарядПриПолученииДанных()

// Функция для получения видов документов, которые используются для взаимодействия
//
Функция орПолучитьВидыДокументовВзаимодействия()
	
	МассивВидовДокументов = Новый Массив();
	МассивВидовДокументов.Добавить("РабочийЛистКредитногоОтдела");
	МассивВидовДокументов.Добавить("РабочийЛистОтделаСтрахования");
	
	Возврат МассивВидовДокументов;
	
КонецФункции // орПолучитьВидыДокументовВзаимодействия()

// Процедура для получения взаимодействий документов
//
Функция орПолучитьВзаимодействияПодчиненныхДокументов(РабочийЛист, ВыводитьЗапланированные = Истина) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РабочийЛист) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивВидов = орПолучитьВидыДокументовВзаимодействия();
	
	МассивПодчиненныхДокументов = Новый Массив;
	
	МассивПодчиненных = дкПолучитьМассивПодчиненных(РабочийЛист, МассивВидов);
	// объекдиним массивы
	Для Каждого ТекДокумент Из МассивПодчиненных Цикл
		Если МассивПодчиненныхДокументов.Найти(ТекДокумент) = Неопределено Тогда
			МассивПодчиненныхДокументов.Добавить(ТекДокумент);
		КонецЕсли;
	КонецЦикла;
	
	// Добавим также события самого документа
	МассивПодчиненныхДокументов.Добавить(РабочийЛист);
	
	Если МассивПодчиненныхДокументов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Событие.Ссылка КАК Событие,
	|	Событие.Дата,
	|	Событие.ВидСобытия,
	|	Событие.Результат,
	|	Событие.Комментарий,
	|	Событие.Содержание,
	|	Событие.ДатаСледующегоСобытия,
	|	Событие.ВидСледующегоСобытия,
	|	Событие.Состояние,
	|	Событие.ДатаНачала,
	|	Событие.Менеджер,
	|	Событие.ДокументОснование
	|ИЗ
	|	Документ.Событие КАК Событие
	|ГДЕ
	|	Событие.ДокументОснование В(&ДокументОснование)
	|	"+?(ВыводитьЗапланированные," И Событие.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСобытий.Запланировано)", "")+"";
	Если ЗначениеЗаполнено(РабочийЛист.ДокументОснование) И обЭтотТип(РабочийЛист.ДокументОснование, Тип("ДокументСсылка.Событие")) Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Событие.Ссылка,
		|	Событие.Дата,
		|	Событие.ВидСобытия,
		|	Событие.Результат,
		|	Событие.Комментарий,
		|	Событие.Содержание,
		|	Событие.ДатаСледующегоСобытия,
		|	Событие.ВидСледующегоСобытия,
		|	Событие.Состояние,
		|	Событие.ДатаНачала,
		|	Событие.Менеджер,
		|	Событие.ДокументОснование
		|ИЗ
		|	Документ.Событие КАК Событие
		|ГДЕ
		|	Событие.Ссылка = &ДокументСобытиеОснование";
		Запрос.УстановитьПараметр("ДокументСобытиеОснование", РабочийЛист.ДокументОснование);
		Если ЗначениеЗаполнено(РабочийЛист.ДокументОснование.ДокументОснование) Тогда
			ИмяДокумента = РабочийЛист.ДокументОснование.ДокументОснование.Метаданные().Имя;
			Если РабочийЛист.ДокументОснование.ДокументОснование.Метаданные().Реквизиты.Найти("ДокументОснование")=Неопределено Тогда
				ЕстьДокументОснование=Ложь;
			Иначе
				ЕстьДокументОснование=Истина;
			КонецЕсли;
			Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Документ.Ссылка,
			|	Документ.Дата,
			|	"""",
			|	"""",
			|	Документ.Комментарий,
			|	"""",
			|	"""",
			|	"""",
			|	"""",
			|	Документ.Дата,
			|	"+?(РабочийЛист.ДокументОснование.ДокументОснование.Метаданные().Реквизиты.Найти("Менеджер") <> Неопределено , "Документ.Менеджер", """""") +",
			|	"+?(ЕстьДокументОснование,"Документ.ДокументОснование","NULL")+"
			|ИЗ
			|	Документ."+ИмяДокумента+" КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &ОснованиеСобытия";
			Запрос.УстановитьПараметр("ОснованиеСобытия", РабочийЛист.ДокументОснование.ДокументОснование);
		КонецЕсли;
	КонецЕсли;
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	Запрос.УстановитьПараметр("ДокументОснование", МассивПодчиненныхДокументов);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // орПолучитьВзаимодействияДокументов()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТОВ		

#Если Клиент Тогда
	
	//приводит макет печ. формы в соответствие с данными документа
	//Макет - макет печ. формы
	//возвращает область шапки таблицы с заполненными параметрами
	Функция орПривестиМакетПечатнойФормы(ЭтотОбъект, Макет) Экспорт
		
		//получим права
		Попытка ПраваПользователя = ЭтотОбъект.Права;
		Исключение ПраваПользователя = Неопределено;
		КонецПопытки;
		
		ОбластьТовар = Макет.Область("Товар");
		//определяем есть ли скидки
		ЕстьСкидка = Ложь;
		Попытка
			Если ЭтотОбъект.Товары.Итог("СуммаСкидки") <> 0 Тогда
				ЕстьСкидка = Истина;
			КонецЕсли;
		Исключение КонецПопытки;
		//определяем есть ли скидки в автомобилях	
		Попытка
			Если ЭтотОбъект.Автомобили.Итог("СуммаСкидки") <> 0 Тогда
				ЕстьСкидка = Истина;
			КонецЕсли;
		Исключение КонецПопытки;
		
		Попытка
			Если Не ЕстьСкидка Тогда
				ОбластьСкидка = Макет.Область("Скидка");
				ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьСкидка.ШиринаКолонки;
				Макет.УдалитьОбласть(ОбластьСкидка,ТипСмещенияТабличногоДокумента.ПоВертикали);
			КонецЕсли;
		Исключение КонецПопытки;
		
		
		//теперь запишем параметры шапки
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		
		//заполняем заголовок колонки НДС по типу цен
		Попытка
			ОбластьМакета.Параметры.НДС = "НДС";
			ТипЦен = ЭтотОбъект.ТипЦен;
			Если ТипЦен.ЦенаВключаетНДС Тогда	// Если НДС включен
				Если НЕ (ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС) Тогда	// Однако не все организации - плательщики НДС
					ОбластьМакета.Параметры.НДС = "в т.ч. НДС";
				КонецЕсли; 
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Возврат ОбластьМакета;
	КонецФункции // орПривестиМакетПечатнойФормы()
	
	//функция, подготавливает структуру строки документа для вывода в печ. формы
	//формат структуры:
	//НомерСтроки
	//АвтомобильНаименование - полное наименование товара с характеристикой, если есть
	//Автомобиль - значение номенклатуры для расшифровки
	//VIN - уникальный код автомобиля
	//НомерДвигателя
	//НомерКузова
	//НомерШасси
	//Количество - если Количество*Коэффициент <> КоличествоБазовое, то выводится базовое
	//Цена
	//СуммаНДС
	//СуммаВсего
	//СуммаБезНДС
	//ЦенаБезНДС
	//ЦенаРозничная
	//СуммаРозничная
	//ПредставлениеСкидки - определяется настройкой пользователя
	Функция орПолучитьПредставлениеДанныхТоварнойСтроки(ТекСтрока,ДокументОбъект) Экспорт
		Попытка
			ПраваПользователя=ДокументОбъект.Права;
			ЭтотОбъект=ДокументОбъект;
		Исключение
			ПраваПользователя=ДокументОбъект;
			ЭтотОбъект=Неопределено;
		КонецПопытки; 
		
		СтруктураСтроки = Новый Структура;
		//заполняем структуру строки
		СтруктураСтроки.Вставить("НомерСтроки",Символы.НПП+ТекСтрока.НомерСтроки);
		Попытка
			СтруктураСтроки.Вставить("АвтомобильНаименование",спПолучитьНаименование(ТекСтрока.Автомобиль));
			//для расшифровки
			СтруктураСтроки.Вставить("Автомобиль",ТекСтрока.Автомобиль);
		Исключение
			СтруктураСтроки.Вставить("АвтомобильНаименование",Неопределено);
			СтруктураСтроки.Вставить("Автомобиль",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("ОпцияНаименование",спПолучитьНаименование(ТекСтрока.Опция));
			//для расшифровки
			СтруктураСтроки.Вставить("Опция",ТекСтрока.Опция);
		Исключение
			СтруктураСтроки.Вставить("ОпцияНаименование",Неопределено);
			СтруктураСтроки.Вставить("Опция",Неопределено);
		КонецПопытки;
		
		//номера
		СтруктураСтроки.Вставить("VIN",Неопределено);
		СтруктураСтроки.Вставить("НомерДвигателя",Неопределено);
		СтруктураСтроки.Вставить("НомерКузова",Неопределено);
		СтруктураСтроки.Вставить("НомерШасси",Неопределено);
		Попытка
			СтруктураСтроки.Вставить("VIN",Символы.НПП+ТекСтрока.Автомобиль.VIN);
			СтруктураСтроки.Вставить("НомерДвигателя",Символы.НПП+ТекСтрока.Автомобиль.НомерДвигателя);
			СтруктураСтроки.Вставить("НомерКузова",Символы.НПП+ТекСтрока.Автомобиль.НомерКузова);
			СтруктураСтроки.Вставить("НомерШасси",Символы.НПП+ТекСтрока.Автомобиль.НомерШасси);
		Исключение
		КонецПопытки;
		
		//форматы вывода
		ФорматВыводаКоличества	= обПраво("ФорматВыводаКоличества",	ПраваПользователя,,ЭтотОбъект);
		ФорматВыводаСуммы		= обПраво("ФорматВыводаСуммы",		ПраваПользователя,,ЭтотОбъект);
		
		//если количество не равно базовому, корректируем вывод
		Попытка
			СтруктураСтроки.Вставить("Количество",Формат(ТекСтрока.Количество,ФорматВыводаКоличества));
		Исключение
			СтруктураСтроки.Вставить("Количество",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("Сумма",Формат(ТекСтрока.Сумма,ФорматВыводаСуммы));
		Исключение
			СтруктураСтроки.Вставить("Сумма",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("СтавкаНДС",дкПолучитьПредставлениеСтавкиНДС(ТекСтрока.СтавкаНДС));
		Исключение
			СтруктураСтроки.Вставить("СтавкаНДС",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("СуммаНДС",Формат(ТекСтрока.СуммаНДС,ФорматВыводаСуммы));
			Если ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС Тогда	// вместо пустой суммы выведем прочерк
				СтруктураСтроки.СуммаНДС = "-    ";
			КонецЕсли;
		Исключение
			СтруктураСтроки.Вставить("СуммаНДС",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("СуммаВсего",Формат(ТекСтрока.СуммаВсего,ФорматВыводаСуммы));
		Исключение
			СтруктураСтроки.Вставить("СуммаВсего",Неопределено);
		КонецПопытки;
		Попытка
			СуммаБезНДС = (ТекСтрока.СуммаВсего) - (ТекСтрока.СуммаНДС);
			СтруктураСтроки.Вставить("СуммаБезНДС",Формат(СуммаБезНДС,ФорматВыводаСуммы));
		Исключение
			Попытка
				СуммаБезНДС = (ТекСтрока.СуммаВсего);
				СтруктураСтроки.Вставить("СуммаБезНДС",Формат(СуммаБезНДС,ФорматВыводаСуммы));
			Исключение
				Попытка 
					СуммаБезНДС = ТекСтрока.Сумма;
					СтруктураСтроки.Вставить("СуммаБезНДС",Формат(СуммаБезНДС,ФорматВыводаСуммы));
				Исключение
					СтруктураСтроки.Вставить("СуммаБезНДС",Неопределено);
				КонецПопытки;
			КонецПопытки;
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("Цена",Формат(ТекСтрока.Цена,ФорматВыводаСуммы));
		Исключение
			СтруктураСтроки.Вставить("Цена",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("ЦенаБезНДС", СтруктураСтроки.СуммаБезНДС);
		Исключение
			СтруктураСтроки.Вставить("ЦенаБезНДС", Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("ЦенаРозничная",Формат(ТекСтрока.ЦенаРозничная,ФорматВыводаСуммы));
		Исключение
			СтруктураСтроки.Вставить("ЦенаРозничная",Неопределено);
		КонецПопытки;
		Попытка
			СтруктураСтроки.Вставить("СуммаРозничная",Формат(ТекСтрока.СуммаРозничная,ФорматВыводаСуммы));
		Исключение
			СтруктураСтроки.Вставить("СуммаРозничная",Неопределено);
		КонецПопытки;
		ПредставлениеСкидки = дкПолучитьПредставлениеСкидкиДляПечати(ТекСтрока, ПраваПользователя);
		СтруктураСтроки.Вставить("ПредставлениеСкидки",Формат(ПредставлениеСкидки,ФорматВыводаСуммы));
		Возврат СтруктураСтроки;
	КонецФункции // орПолучитьПредставлениеДанныхТоварнойСтроки()
	
	//получает схему ТС из регистра КартинкиИФайлы по модели автомобиля
	//Модель - макет печ. формы
	//ИдентификаторОбъекта - имя картинки в регистре, где содержится схема
	//возвращает найденную картинку или неопределено
	Функция орПолучитьСхемуТС(Модель, ИдентификаторОбъекта) Экспорт
		
		ГруппаМодели = Неопределено;
		Если (НЕ обЗначениеНеЗаполнено(Модель)) И (НЕ обЗначениеНеЗаполнено(Модель.Родитель)) Тогда
			ГруппаМодели = Модель.Родитель.Ссылка;
		КонецЕсли;	
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КартинкиИФайлы.ИмяФайла,
		|	КартинкиИФайлы.Данные,
		|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
		|	0 КАК ПорядокСортировки
		|ИЗ
		|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		|ГДЕ
		|	КартинкиИФайлы.Объект = &Модель
		|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта
		|
		| //ОбъединениеГруппы
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПорядокСортировки");
		Запрос.УстановитьПараметр("Модель",Модель.Ссылка);
		Запрос.УстановитьПараметр("ИдентификаторОбъекта",ИдентификаторОбъекта);
		Если ГруппаМодели <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"//ОбъединениеГруппы", "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	КартинкиИФайлы.ИмяФайла,
			|	КартинкиИФайлы.Данные,
			|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
			|	1
			|ИЗ
			|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
			|ГДЕ
			|	КартинкиИФайлы.Объект = &ГруппаМодели
			|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта");
			Запрос.УстановитьПараметр("ГруппаМодели",ГруппаМодели);
		КонецЕсли;
		ДанныеРегистра = Запрос.Выполнить().Выбрать();
		
		Если ДанныеРегистра.Количество() > 0 Тогда
			ДанныеРегистра.Следующий();
			Попытка
				Если ДанныеРегистра.ДанныеВоВнешнемФайле Тогда
					СхемаТС = Новый Картинка(ДанныеРегистра.ИмяФайла,Ложь);
				Иначе
					СхемаТС = ДанныеРегистра.Данные.Получить();
				КонецЕсли;	
			Исключение
			КонецПопытки;
			Если СхемаТС <> Неопределено И ТипЗнч(СхемаТС)=Тип("Картинка") Тогда
				Возврат СхемаТС;				
			КонецЕсли;
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецФункции // орПолучитьСхемуТС()
	
#КонецЕсли

//получает схему ТС из регистра КартинкиИФайлы по модели автомобиля
//Модель - макет печ. формы
//ИдентификаторОбъекта - имя картинки в регистре, где содержится схема
//возвращает найденную картинку или неопределено
Функция орПолучитьСхемуТССервер(Модель, ИдентификаторОбъекта) Экспорт
	
	ГруппаМодели = Неопределено;
	Если (НЕ обЗначениеНеЗаполнено(Модель)) И (НЕ обЗначениеНеЗаполнено(Модель.Родитель)) Тогда
		ГруппаМодели = Модель.Родитель.Ссылка;
	КонецЕсли;	
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КартинкиИФайлы.ИмяФайла,
	|	КартинкиИФайлы.Данные,
	|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
	|	0 КАК ПорядокСортировки
	|ИЗ
	|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	|ГДЕ
	|	КартинкиИФайлы.Объект = &Модель
	|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта
	|
	| //ОбъединениеГруппы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокСортировки");
	Запрос.УстановитьПараметр("Модель",Модель.Ссылка);
	Запрос.УстановитьПараметр("ИдентификаторОбъекта",ИдентификаторОбъекта);
	Если ГруппаМодели <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"//ОбъединениеГруппы", "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КартинкиИФайлы.ИмяФайла,
		|	КартинкиИФайлы.Данные,
		|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
		|	1
		|ИЗ
		|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		|ГДЕ
		|	КартинкиИФайлы.Объект = &ГруппаМодели
		|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта");
		Запрос.УстановитьПараметр("ГруппаМодели",ГруппаМодели);
	КонецЕсли;
	ДанныеРегистра = Запрос.Выполнить().Выбрать();
	
	Если ДанныеРегистра.Количество() > 0 Тогда
		ДанныеРегистра.Следующий();
		Попытка
			Если ДанныеРегистра.ДанныеВоВнешнемФайле Тогда
				СхемаТС = Новый Картинка(ДанныеРегистра.ИмяФайла,Ложь);
			Иначе
				СхемаТС = ДанныеРегистра.Данные.Получить();
			КонецЕсли;	
		Исключение
		КонецПопытки;
		Если СхемаТС <> Неопределено  И Тип(СхемаТС)=Тип("Картинка") Тогда
			Возврат СхемаТС;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // орПолучитьСхемуТС()

// Возвращает использование тахографа для Технического диагностирования
Функция орМодельИспользуетТахограф(Модель) Экспорт
	// Поиск признака использования тахографа по макету Спр. "Требования к транспортным средствам"
	// На данный момент номер требования 83
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТребованияКТранспортнымСредствамДиапазонЗначений.Ссылка КАК ТребованиеКТранспортномуСредству
	               |ИЗ
	               |	Справочник.ТребованияКТранспортнымСредствам.ДиапазонЗначений КАК ТребованияКТранспортнымСредствамДиапазонЗначений
	               |ГДЕ
	               |	ТребованияКТранспортнымСредствамДиапазонЗначений.КатегорияТранспортногоСредства = &КатегорияТранспортногоСредства
	               |	И ТребованияКТранспортнымСредствамДиапазонЗначений.Ссылка.Номер = &Номер
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТребованиеКТранспортномуСредству";
	Запрос.УстановитьПараметр("КатегорияТранспортногоСредства",Модель.КатегорияТС);
	Запрос.УстановитьПараметр("Номер", 83);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции


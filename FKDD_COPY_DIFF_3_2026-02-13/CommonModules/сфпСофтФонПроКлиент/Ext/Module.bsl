
// Процедура - Начать прослушивание записи разговора
//
// Параметры:
//  ПараметрыЗвонка	 - Структура - Структура параметров
//  Форма			 - ФормаКлиентскогоПриложения	 - Форма из которой производится вызов процедуры
//  Элемент			 - ЭлементФормы		 - Кнопка вызова процедуры
//
Процедура НачатьПрослушиваниеЗаписиРазговора(ПараметрыЗвонка, Форма, Элемент) Экспорт
	
	#Если ВебКлиент Тогда
		Состояние(НСтр("ru='Работа СофтФона невозможна в web-клиенте!'"));
		Возврат;
	#КонецЕсли

	// Прежде, чем искать запись мы должны выяснить, есть ли права на прослушку у данного пользователя
	ТекстПредупреждения = Нстр("ru = 'У данного пользователя нет прав на прослушивание разговора пользователя '");
	
	ТекПользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();
	МассивПрослушиваемыхПользователей = сфпСофтФонПроСервер.сфпПолучитьМассивПрослушиваемыхПользователей(ТекПользователь);
	Если МассивПрослушиваемыхПользователей.Количество() > 0 Тогда
		Если МассивПрослушиваемыхПользователей.Найти(ПараметрыЗвонка.Ответственный) = Неопределено Тогда
			ТекстПредупреждения = ТекстПредупреждения + """" + ПараметрыЗвонка.Ответственный + """" + ".";
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;

	Иначе				
		ТекстПредупреждения = ТекстПредупреждения + """" + ПараметрыЗвонка.Ответственный + """" + ".";
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	//ВерсияСофтФон = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользуемаяВерсияСофтФон");
	//ПараметрыЗвонка.Вставить("ВерсияСофтФон", ВерсияСофтФон);
	
	СписокВыбора = Новый СписокЗначений();
	СписокВыбора.Добавить("Прослушать");
	СписокВыбора.Добавить("Скачать");
		
	//Если ВерсияСофтФон = ПредопределенноеЗначение("Перечисление.сфпВерсииСофтФон.СофтФотPROSTO") Тогда
	//	Если ЗначениеЗаполнено(ПараметрыЗвонка.ИдентификаторЗаписи) Тогда
	//		#Если ВебКлиент Тогда
	//			ВыбранныйЭлемент = СписокВыбора.НайтиПоЗначению("Скачать");
	//			ПродолжитьПрослушиваниеЗаписиРазговора(ВыбранныйЭлемент, ПараметрыЗвонка);
	//		#Иначе
	//			Оповещение = Новый ОписаниеОповещения("ПродолжитьПрослушиваниеЗаписиРазговора", сфпСофтФонПроКлиент, ПараметрыЗвонка);
	//			Форма.ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элемент); 
	//		#КонецЕсли
	//	КонецЕсли;
	//	
	//Иначе
		ВыбранныйЭлемент = СписокВыбора.НайтиПоЗначению("Прослушать");
		ПродолжитьПрослушиваниеЗаписиРазговора(ВыбранныйЭлемент, ПараметрыЗвонка);
	//КонецЕсли;
	
КонецПроцедуры

// Процедура - Продолжить прослушивание записи разговора
//
// Параметры:
//  ВыбранныйЭлемент		 - ЭлементФормы	 - Элемент вызова процедуры
//  ДополнительныеПараметры	 - Структура	 - Структура параметров
//
Процедура ПродолжитьПрослушиваниеЗаписиРазговора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		//Если ДополнительныеПараметры.ВерсияСофтФон = ПредопределенноеЗначение("Перечисление.сфпВерсииСофтФон.СофтФотPROSTO") Тогда
		//	НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(ДополнительныеПараметры.НомерТелефона);
		//	ИмяФайла = Формат(ДополнительныеПараметры.ДатаНачала, "ДФ=yyyyMMdd_HHmmss") + "_" + НомерТелефона;
		//	Если ВыбранныйЭлемент.Значение = "Прослушать" Тогда
		//		ОбновитьДанныеЗвонка = Неопределено;
		//		сфпСофтФонПроКлиент.ПрослушатьЗаписьРазговора(
		//			ДополнительныеПараметры.ИдентификаторЗвонка, ДополнительныеПараметры.ИдентификаторЗаписи, ОбновитьДанныеЗвонка, "play", ИмяФайла);
		//		
		//	ИначеЕсли ВыбранныйЭлемент.Значение = "Скачать" Тогда
		//		ОбновитьДанныеЗвонка = Неопределено;
		//		сфпСофтФонПроКлиент.ПрослушатьЗаписьРазговора(
		//			ДополнительныеПараметры.ИдентификаторЗвонка, ДополнительныеПараметры.ИдентификаторЗаписи, ОбновитьДанныеЗвонка, "download", ИмяФайла);
		//	КонецЕсли;
		//		
		//Иначе		
			Если НЕ ПустаяСтрока(ДополнительныеПараметры.ИдентификаторЗаписи) Тогда
				сфпСофтФонПроКлиент.сфпПолучитьФайлРазговора(ДополнительныеПараметры.ИдентификаторЗаписи)
			КонецЕсли;
		//КонецЕсли;
	КонецЕсли;

КонецПроцедуры


/////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура выполняет инициализацию подсистемы СофтФон
//
// Параметры:
//	Нет.
//
Процедура сфпПодключитьСофтФон() Экспорт
	
	Если НЕ сфпСофтФонПроСервер.сфпИспользоватьСофтФон() Тогда
		Возврат;

	ИначеЕсли НЕ сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпИспользоватьСофтФон") Тогда
		Возврат;
	КонецЕсли;

	сфпДобавитьГлобальныйПараметр("сфпЗакрыватьПанельПриЗавершенииРаботы", сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпЗакрыватьПанельПриЗавершенииРаботы"));

	#Если ВебКлиент Тогда
		Состояние(НСтр("ru='Работа СофтФона невозможна в web-клиенте!'"));
	#Иначе
		сфпПодключиться();
	#КонецЕсли

КонецПроцедуры // сфпПодключитьСофтФон()

// Процедура очищает глобальные переменные и закрывает панель
//
// Параметры:
//	Нет.
//
Процедура сфпОтключитьСофтФон(Отказ = Неопределено) Экспорт
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;

	сфпObjCLON = Неопределено;
	сфпСтруктураЗвонков	= Новый Структура();
	сфпДанныеЗаполнения	= Неопределено;		
	сфпСоответствиеЛинийИСтатусов = Неопределено;
	
	Если сфпПанельУправления <> Неопределено Тогда
		#Если НЕ ВебКлиент Тогда
			Если сфпПолучитьГлобальныйПараметр("сфпЗакрыватьПанельПриЗавершенииРаботы") = Истина Тогда
				сфпПанельУправления.PanelExit();
			КонецЕсли;
		#КонецЕсли
		
		сфпПанельУправления = Неопределено;
	КонецЕсли;

КонецПроцедуры // сфпОтключитьСофтФон()

// Функция проверяет доступность использования СофтФон
//
// Параметры:
//	ВыводитьПредупреждения	- Булево	- Признак вывода предупреждений
//
// Возвращаемое значение:
//	Булево	- Доступность СофтФона
//
Функция сфпПроверитьДоступностьСофтФон(ВыводитьПредупреждения = Истина) Экспорт
	#Если ВебКлиент Тогда
		Возврат Ложь;
	#Иначе
		Если НЕ сфпСофтФонПроСервер.сфпИспользоватьСофтФон() Тогда
			Если ВыводитьПредупреждения Тогда
				ПоказатьПредупреждение(, НСтр("ru='В настройках системы отключено использование СофтФон'"), 5);
			КонецЕсли;	
			Возврат Ложь;
		ИначеЕсли НЕ сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпИспользоватьСофтФон") Тогда	
			Если ВыводитьПредупреждения Тогда
				ПоказатьПредупреждение(, НСтр("ru='В персональных настройках пользователя отключено использование СофтФон'"), 5);
			КонецЕсли;	
			Возврат Ложь;
		ИначеЕсли сфпПанельУправления = Неопределено Тогда
			Если ВыводитьПредупреждения Тогда
				ПоказатьПредупреждение(, НСтр("ru='Отсутствует подключение к серверу СофтФона'"), 5);
			КонецЕсли;	
			Возврат Ложь;
		КонецЕсли;	
		Возврат Истина;
	#КонецЕсли
КонецФункции // сфпПроверитьДоступностьСофтФон()

// Процедура заново заполняет регистр поиска по номерам
//
// Параметры:
//	Нет.
//
Процедура сфпПерезаполнитьРегистрПоискаПоНомерам() Экспорт
	
	ТипыОбъектовДляПерезаполнения = сфпСофтФонПроСервер.сфпПолучитьТипыОбъектовДляПерезаполненияПоНомерам();
	Если ТипыОбъектовДляПерезаполнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШагИндикатора = (100 / ТипыОбъектовДляПерезаполнения.Количество());
	Индикатор = ШагИндикатора;
	Для Сч = 0 по ТипыОбъектовДляПерезаполнения.Количество() - 1 Цикл
		//+АА
		//Состояние(НСтр("ru='Выполняется обновление регистра поиска по номеру телефона'"), Индикатор,, БиблиотекаКартинок.ДлительнаяОперация48);
		Состояние(НСтр("ru='Выполняется обновление регистра поиска по номеру телефона'"), Индикатор,, БиблиотекаКартинок.ДлительныеОперации);
		//-АА
		ТипОбъекта = ТипыОбъектовДляПерезаполнения[Сч];
		сфпСофтФонПроСервер.сфпЗаполнитьРегистрПоискаНомеровПоТипуОбъекта(ТипОбъекта);
		
		Индикатор = Индикатор + ШагИндикатора;
	КонецЦикла;	
	
	Если сфпСофтФонПроСервер.сфпИспользоватьМаршрутизацию() Тогда
		// Если включена маршрутизация, то заменяем таблицу маршрутизации
		СтарыйНабор = Новый Массив;
		НовыйНабор = сфпСофтФонПроСервер.сфпПолучитьТаблицуМаршрутизации();
		СписокМаршрутизации = сфпСофтФонПроСервер.сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
		сфпСофтФонПроСервер.сфпЗаменитьМаршрутизациюВАТС(СписокМаршрутизации);
	КонецЕсли;	
		
КонецПроцедуры

// Процедура выполняет необходимые действия после записи телефонного звонка в базу
//
// Параметры:
//	НовыйЗвонок		- ДокументСсылка 	- Ссылка на записанный звонок
//	СтруктураЗвонка	- Структура			- Структура данных звонка
//
Процедура сфпВыполнитьДействияПослеЗаписиТелефонногоЗвонка(НовыйЗвонок, СтруктураЗвонка) Экспорт
	Если ЗначениеЗаполнено(НовыйЗвонок) Тогда
		// Добавляем звонок в структуру
		СтруктураЗвонка["НовыйЗвонок"] = НовыйЗвонок;
		сфпДобавитьДанныеВСтруктуру(СтруктураЗвонка.hCall, "НовыйЗвонок", НовыйЗвонок);
		Если сфпСофтФонПроСервер.сфпИспользоватьCoMagic() Тогда
			Если сфпДанныеЗаполнения = Неопределено Тогда
				сфпПанельУправления.GetCallInfoCoMagic(СтруктураЗвонка.LineName, СтруктураЗвонка.CallerID);
			ИначеЕсли НЕ сфпДанныеЗаполнения.Свойство("СтруктураCoMagic") Тогда
				сфпПанельУправления.GetCallInfoCoMagic(СтруктураЗвонка.LineName, СтруктураЗвонка.CallerID);
			КонецЕсли;	
		КонецЕсли;
		Если ЗначениеЗаполнено(СтруктураЗвонка.Контакт) Тогда
			// Изменяем таблицу маршрутизации
			сфпСофтФонПроСервер.сфпОбновитьТаблицуМаршрутизации(НовыйЗвонок);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры	

// Процедура заполняет соответствие сфпСоответствиеЛинийИСтатусов по данным, полученных с сервера СФ
//
// Параметры:
//  МассивНомеров	 - Массив	 - Массив, содержащий таблицу состояний и номеров
//
Процедура сфпПерезаполнитьСоответствиеСостоянияЛиний(МассивНомеров) Экспорт
	Если сфпСоответствиеЛинийИСтатусов = Неопределено Тогда Возврат; КонецЕсли;
	Для Каждого ЭлементМассива Из МассивНомеров Цикл
		Если ПустаяСтрока(ЭлементМассива.Number) Тогда Продолжить КонецЕсли;
		ОчищенныйНомер = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(ЭлементМассива.Number);
		Если СтрДлина(ОчищенныйНомер) = СтрДлина(ЭлементМассива.Number) Тогда
			Если ТипЗнч(ЭлементМассива.LineState) = Тип("Число") Тогда
				сфпСоответствиеЛинийИСтатусов.Вставить(ЭлементМассива.Number, ЭлементМассива.LineState - 1);
			ИначеЕсли ТипЗнч(ЭлементМассива.LineState) = Тип("Строка") Тогда
				Если ЭлементМассива.LineState = "free" Тогда 
					LineState = 0;
				ИначеЕсли ЭлементМассива.LineState = "busy"	Тогда
					LineState = 1;
				Иначе
					LineState = 2;
				КонецЕсли;		
				сфпСоответствиеЛинийИСтатусов.Вставить(ЭлементМассива.Number, LineState);
			КонецЕсли;				
		КонецЕсли;
	КонецЦикла;		
	Оповестить("сфпПерезаполнитьСтатусыЛиний");
КонецПроцедуры

// Функция получает версию используемой панели Софтфона
//
Функция сфпПолучитьВерсиюПанели() Экспорт
	
	ВерсияПанели = "0.0.0.0";
	
	Если сфпПанельУправления <> Неопределено Тогда
		Попытка    ВерсияПанели	= сфпПанельУправления.Release();
		Исключение ВерсияПанели = "0.0.0.0";
		КонецПопытки;
	КонецЕсли;
	
	Возврат ВерсияПанели;

КонецФункции	

// Функция обрабатывает внешнее событие, которое приходит от нативной компоненты Софтфона
//
// Параметры:
//	Источник 	- Строка 	- Источник внешнего события
//  Событие 	- Строка	- Имя события источника
//	Данные		- JSON		- Данные события
//
Процедура сфпВнешнееСобытияСофтфона(Источник, Событие, Данные) Экспорт
	#Если НЕ Вебклиент Тогда
		Если Источник = "SoftPhone" Тогда
			МассивСоответствия = сфпСофтФонПроСервер.UnJSON(ЗаменитьНедопустимыеСимволыXML(Данные));
			Если МассивСоответствия = Неопределено Тогда
				Возврат;
			ИначеЕсли НЕ (ТипЗнч(МассивСоответствия)  = Тип("Соответствие")) Тогда
				Возврат;
			ИначеЕсли МассивСоответствия.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			СтруктураДанных = Новый Структура();
			Для Каждого ЭлементМассива Из МассивСоответствия Цикл
				СтруктураДанных.Вставить(ЭлементМассива.Ключ, ЭлементМассива.Значение); 
			КонецЦикла;
			Если НЕ СтруктураДанных.Свойство("NumberOnLine") Тогда
				СтруктураДанных.Вставить("NumberOnLine", "");
			КонецЕсли;
			Если Событие = "OnCallInfoEx" Тогда
				сфпOnCallInfo(СтруктураДанных["hCall"], СтруктураДанных["LineName"],СтруктураДанных["LineType"], СтруктураДанных["CallerID"],
				СтруктураДанных["CallerInfoName"], СтруктураДанных["CalledId"], СтруктураДанных["CalledInfoName"], СтруктураДанных["State"],
				СтруктураДанных["Origin"], СтруктураДанных["DopInfo"], СтруктураДанных["AvailableActions"], СтруктураДанных["AppValue"],
				СтруктураДанных["ImageData"],СтруктураДанных["ContactID"], СтруктураДанных["Caller_Destination_Number"], СтруктураДанных["NumberOnLine"]);
			ИначеЕсли  Событие = "PutSettingsJSON" Тогда	
				сфпПараметрыСервера = сфпСофтФонПроСервер.сфпПараметрыСервера();
				ТребуетсяОбновитьРегистр = НЕ (СтруктураДанных["LastNumberCount"] = сфпПараметрыСервера.ПоследниеЦифрыТелефонногоНомера);
				
				сфпПараметрыСервера.ПрефиксВыходаВГород					= СтруктураДанных["PrefCity"];
				сфпПараметрыСервера.ПрефиксВыходаВМежгород				= СтруктураДанных["PrefContry"];
				сфпПараметрыСервера.ПрефиксВыходаНаМеждународную		= СтруктураДанных["PrefWorld"];
				сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров	= СтруктураДанных["InternalNumMaxLen"];
				сфпПараметрыСервера.ПоследниеЦифрыТелефонногоНомера		= СтруктураДанных["LastNumberCount"];
				сфпПараметрыСервера.СтрокаПодключенияИстории			= СтруктураДанных["SQLConnectionString"];
				сфпПараметрыСервера.КодСтраны                           = СтруктураДанных["ContryCode"];
				сфпПараметрыСервера.КодГорода							= СтруктураДанных["CityCode"];
				сфпСофтФонПроСервер.сфпЗаписатьПараметрыСервера(СтруктураДанных);
				
				Если ТребуетсяОбновитьРегистр Тогда
					сфпСофтФонПроКлиент.сфпПерезаполнитьРегистрПоискаПоНомерам();
				КонецЕсли;
				
				Если НЕ ПустаяСтрока(СтруктураДанных["LocalPhoneNum"]) Тогда
					// Если изменился текущий внутренний номер пользователя
					Если НЕ (СтруктураДанных["LocalPhoneNum"] = сфпСофтФонПроСервер.сфпТекущийВнутреннийНомер()) Тогда
						// Запишем внутренний номер для текущего пользователя
						ТекущийПользователь	= сфпСофтФонПроСервер.сфпТекущийПользователь();
						МассивПользователей = сфпСофтФонПроСервер.сфпЗаписатьНомерПользователю(СтруктураДанных["LocalPhoneNum"], ТекущийПользователь);
						Если сфпСофтФонПроСервер.сфпИспользоватьМаршрутизацию() и Не сфпСофтФонПроСервер.сфпИспользоватьМаршрутизациюПоНомеруИзКИПользователя() Тогда
							// Изменим маршрутизацию в АТС
							СтарыйНабор	= сфпСофтФонПроСервер.сфпПолучитьТаблицуМаршрутизации(, ТекущийПользователь);
							Для Каждого ПользовательМассива Из МассивПользователей Цикл
								НаборПользователя	= сфпСофтФонПроСервер.сфпПолучитьТаблицуМаршрутизации(, ПользовательМассива);
								Для Каждого СтрокаНабора Из НаборПользователя Цикл
									СтарыйНабор.Добавить(СтрокаНабора);
								КонецЦикла;	
							КонецЦикла;	
							НовыйНабор	= сфпСофтФонПроСервер.сфпПолучитьТаблицуМаршрутизации(, ТекущийПользователь);
							// Изменяем внутренний номер на новый
							Для Каждого СтрокаНабора Из НовыйНабор Цикл
								СтрокаНабора.ВнутреннийНомер = СтруктураДанных["LocalPhoneNum"];
							КонецЦикла;	
							СписокМаршрутизации = сфпСофтФонПроСервер.сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
							сфпСофтФонПроСервер.сфпИзменитьМаршрутизациюВАТС(СписокМаршрутизации);
							сфпСофтФонПроСервер.сфпПерезаписатьНомерЛинииТекущегоПользователяВРегистреПоиска(ТекущийПользователь, СтруктураДанных["LocalPhoneNum"]);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;

				Если СтруктураДанных.Свойство("UseRouter") И НЕ (СтруктураДанных["UseRouter"]  = Неопределено) Тогда
					Если НЕ (СтруктураДанных["UseRouter"] = сфпПараметрыСервера.ИспользоватьМаршрутизацию) Тогда
						сфпПараметрыСервера.ИспользоватьМаршрутизацию	= СтруктураДанных["UseRouter"];
						ОбновитьИнтерфейс();	
					КонецЕсли;	
				ИначеЕсли СтруктураДанных.Свойство("HistoryOn") И НЕ (СтруктураДанных["HistoryOn"]  = Неопределено) Тогда
					// Если изменилась видимость отчетов
					Если НЕ (СтруктураДанных["HistoryOn"] = сфпПараметрыСервера.ИспользоватьИсториюЗвонков) Тогда
						сфпПараметрыСервера.ИспользоватьИсториюЗвонков = СтруктураДанных["HistoryOn"];
						ОбновитьИнтерфейс();
					КонецЕсли;	
				ИначеЕсли СтруктураДанных.Свойство("UseHistory") И НЕ (СтруктураДанных["UseHistory"]  = Неопределено) Тогда
					Если НЕ (СтруктураДанных["UseHistory"] = сфпПараметрыСервера.ИспользоватьИсториюЗвонков) Тогда
						сфпПараметрыСервера.ИспользоватьИсториюЗвонков = СтруктураДанных["UseHistory"]; 
						ОбновитьИнтерфейс();
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли  Событие = "OnEvent" Тогда
				сфпOnEvent(СтруктураДанных["EventType"], СтруктураДанных["Origin"], СтруктураДанных["ContactID"],
				СтруктураДанных["PhoneNumber"]);
			ИначеЕсли  Событие = "OnEventData" Тогда
				сфпOnEventData(СтруктураДанных["LineName"], СтруктураДанных["DataType"], СтруктураДанных["StrData"]);
			ИначеЕсли  Событие = "OnAllLinesInfo" Тогда
				сфпOnAllLinesInfo(СтруктураДанных["items"]);
			ИначеЕсли  Событие = "OnLinesStatus" Тогда	
				// Только для CRM 3.0 пока
				//сфпOnLinesStatus(Данные);
			ИначеЕсли  Событие = "OnRecordInfo" Тогда	
				сфпOnRecordInfo(СтруктураДанных["RecordEventType"], СтруктураДанных["hCall"], СтруктураДанных["LineID"],
				СтруктураДанных["RecordID"], СтруктураДанных["TimeStart"], СтруктураДанных["DurationTalk"],
				СтруктураДанных["FileName"], СтруктураДанных["ResultDescription"]);
			ИначеЕсли  Событие = "OnResultInfo" Тогда
				сфпOnResultInfo(СтруктураДанных["OperationName"], СтруктураДанных["OperationResult"],
				СтруктураДанных["OperationResultDescription"], СтруктураДанных["DopInfo"]);
			ИначеЕсли  Событие = "OnCallInfoCoMagic" Тогда
				сфпOnCallInfoCoMagic(СтруктураДанных["LineName"], СтруктураДанных["CallInfo"]);
			КонецЕсли;
		ИначеЕсли Источник = "DbControl" Тогда
			// Ожидаем окончания сохранения файла
			Если (Событие = "OnLoadProgress") И (Данные = "100") Тогда
				FileName = сфпСофтФонПроКлиент.сфпПолучитьИмяФайлаЗаписиРазговора();
				Попытка
					ЗапуститьПриложение(FileName);
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;	
	#КонецЕсли
КонецПроцедуры	

Процедура сфпДобавитьГлобальныйПараметр(ИмяПараметра, ЗначениеПараметра) Экспорт
	
	Если сфпГлобальныеПараметры = Неопределено Тогда
		сфпГлобальныеПараметры = Новый Структура();
	КонецЕсли;
	сфпГлобальныеПараметры.Вставить(ИмяПараметра, ЗначениеПараметра);
	
КонецПроцедуры

Функция сфпПолучитьГлобальныйПараметр(ИмяПараметра) Экспорт
	
	Если сфпГлобальныеПараметры = Неопределено Тогда
		сфпГлобальныеПараметры = Новый Структура();
	КонецЕсли;
	Если сфпГлобальныеПараметры.Свойство(ИмяПараметра) Тогда
		Возврат сфпГлобальныеПараметры[ИмяПараметра];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДКЛЮЧЕНИЯ К СОФТФОН

// Функция выполняет подключение к серверу СофтФона
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Результат подключения
//
Процедура сфпПодключиться()
	сфпСофтФонПроКлиентНативнаяКомпонента.сфпПодключитьсяНативнаяКомпонента(Истина);
КонецПроцедуры // сфпПодключиться()

// Процедура заполняет префиксы и настройки
//
// Параметры:
//	Нет.
//
Процедура сфпЗаполнитьПрефиксыИНастройки() Экспорт
	сфпСофтФонПроКлиентНативнаяКомпонента.сфпЗаполнитьПрефиксыИНастройкиНативнаяКомпонента();
КонецПроцедуры // сфпЗаполнитьПрефиксыИНастройки()	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ НАПРАВЛЕНИЯ ЗВОНКА

// Функция определяет внутренний или внешний звонок
//
// Параметры:
//	НаправлениеЗвонка	- Число		- Направление звонка
//	НомерТелефона		- Строка	- Номер телефона
//
// Возвращаемое значение:
//	Булево	- Признак внешнего звонка
//
Функция сфпОпределитьВнешнийЗвонок(НаправлениеЗвонка, НомерТелефона) Экспорт 
	Если НаправлениеЗвонка = 3 Тогда // Внешний входящий
		Возврат Истина;
	ИначеЕсли НаправлениеЗвонка = 6 Тогда // Внешний исходящий
		Возврат Истина;
	ИначеЕсли ЗначениеЗаполнено(НомерТелефона) Тогда                  
		НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
		сфпПараметрыСервера = сфпСофтФонПроСервер.сфпПараметрыСервера();
		Если ПустаяСтрока(НомерТелефона) Тогда
			Возврат Истина;	
		ИначеЕсли сфпОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров) Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;	
		КонецЕсли;	
	ИначеЕсли НаправлениеЗвонка = 1 Тогда // Внутренний
		Возврат Ложь;
	ИначеЕсли НаправлениеЗвонка = 0 Тогда  // Внешний
		Возврат Истина;	
	Иначе
		Возврат Ложь;  // 4, 5
	КонецЕсли;
КонецФункции // сфпОпределитьВнешнийЗвонок()

// Функция определяет входящий или исходящий звонок
//
// Параметры:
//	НомерСобытия	- Число	- Состояние звонка
//
// Возвращаемое значение:
//	Булево	- Признак входящего звонка
//
Функция сфпОпределитьВходящийЗвонок(НаправлениеЗвонка) Экспорт
	Если НаправлениеЗвонка = 1 Тогда // Внутренний исходящий
		Возврат Ложь;
	ИначеЕсли НаправлениеЗвонка = 6 Тогда // Внешний исходящий
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции // сфпОпределитьВходящийЗвонок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ СО СТРУКТУРОЙ ЗВОНКА

// Функция формирует структуру с данными звонка
//
// Параметры:
//	hCall				- Число			- Идентификатор звонка.
//	LineName			- Строка		- Имя линии.
//	CallerID			- Строка		- Номер звонящего.
//	CallerInfoName		- Строка		- Представление звонящего.
//	CalledId			- Строка		- Номер принимающего звонок.
//	CalledInfoName		- Строка		- Представление принимающего звонок.
//	State				- Число			- Состояние звонка.
//	Origin				- Число			- Направление звонка звонка. (0 – Неопределенно; 1 – Внутренний исходящий;
//											2 – Внутренний входящий; 3 – Внешний входящий; 4 – Недоступно;
//											5 – Конференция; 6 – Входящий)
//	AvailableActions	- Число			- Доступные действия со звонком. Битовая маска (aa_Drop	= $00000001; aa_Answer = $00000002;
//											aa_Hold = $00000004; aa_UnHold = $00000008; aa_Redirect = $00000010; aa_Transfer = $00000020;
//											aa_CompleteTransfer = $00000040; aa_CancelTransfer = $00000080)
//	ContactID			- Строка		- Идентификатор контакта во внешней учетной системе. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	Контакт				- СправочникСсылка	- Ссылка на контакт в ИБ
//	ВходящийЗвонок		- Булево			- Признак входящего звонка
//	ВнешнийЗвонок		- Булево			- Признак внешнего звонка
//	НомерТелефона		- Строка			- Очищенный номер телефона
//	НовыйЗвонок			- ДокументСсылка	- Ссылка на документ "Телефонный звонок"
//	МассивЗвонящих		- Массив			- Массив контактов, найденных по номеру телефона
//
// Возвращаемое значение:
//	Структура	- Структура с данными звонка
//
Функция сфпСформироватьСтруктуруЗвонка(hCall, LineName, CallerID, CallerInfoName, CalledId, CalledInfoName, DopInfo, State,
		Origin, AvailableActions, ContactID, Caller_Destination_Number, Контакт, ВходящийЗвонок, ВнешнийЗвонок, НомерТелефона, НовыйЗвонок,
		МассивЗвонящих) Экспорт
	СтруктураЗвонка = Новый Структура;
	СтруктураЗвонка.Вставить("hCall",						hCall);
	СтруктураЗвонка.Вставить("LineName",					LineName);
	СтруктураЗвонка.Вставить("CallerID",					CallerID);
	СтруктураЗвонка.Вставить("CallerInfoName",				CallerInfoName);
	СтруктураЗвонка.Вставить("CalledId",					CalledId);
	СтруктураЗвонка.Вставить("CalledInfoName",				CalledInfoName);
	СтруктураЗвонка.Вставить("DopInfo",						DopInfo);
	СтруктураЗвонка.Вставить("State",						State);
	СтруктураЗвонка.Вставить("Origin",						Origin);
	СтруктураЗвонка.Вставить("AvailableActions",			AvailableActions);
	СтруктураЗвонка.Вставить("ContactID",					ContactID);
	СтруктураЗвонка.Вставить("Caller_Destination_Number",	Caller_Destination_Number);
	СтруктураЗвонка.Вставить("Контакт",						Контакт);
	СтруктураЗвонка.Вставить("ВходящийЗвонок",				ВходящийЗвонок);
	СтруктураЗвонка.Вставить("ВнешнийЗвонок",				ВнешнийЗвонок);
	СтруктураЗвонка.Вставить("НомерТелефона",				НомерТелефона);
	СтруктураЗвонка.Вставить("НовыйЗвонок",					НовыйЗвонок);
	СтруктураЗвонка.Вставить("МассивЗвонящих",				МассивЗвонящих);
	СтруктураЗвонка.Вставить("ИдентификаторЗаписи",			"");			
	СтруктураЗвонка.Вставить("ЗаписьЗапущена",				Ложь);			
	Возврат СтруктураЗвонка;
КонецФункции // сфпСформироватьСтруктуруЗвонка() 	

// Функция возвращает структуру звонка
//
// Параметры:
//	hCall	- Число	- Идентификатор звонка.
//
// Возвращаемое значение:
//	Структура	- Структура данных звонка
//
Функция сфпНайтиДанныеЗвонка(hCall, ВходящийЗвонок = Неопределено, CallerID = Неопределено, CalledID = Неопределено) Экспорт
	
	Если сфпСтруктураЗвонков = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	КлючПоиска = "_" + Формат(hCall, "ЧГ=0"); 
	НайденныйЗвонок	= Неопределено;
	сфпСтруктураЗвонков.Свойство(КлючПоиска, НайденныйЗвонок);
	
	Если НайденныйЗвонок = Неопределено И ВходящийЗвонок <> Неопределено Тогда
		Если СтрДлина("" + hCall) > 2 Тогда
			Для к = 1 По 10 Цикл
				КлючПоискаSIP = "_" + Формат(к, "ЧГ=0");
				НайденныйЗвонокSIP = Неопределено;
				сфпСтруктураЗвонков.Свойство(КлючПоискаSIP, НайденныйЗвонокSIP);
				Если НайденныйЗвонокSIP <> Неопределено Тогда
					ЗаменитьЗвонок = Ложь;
					
					Если НайденныйЗвонокSIP.ВходящийЗвонок = ВходящийЗвонок Тогда
						Если Прав(НайденныйЗвонокSIP.CallerID, 10) = Прав(CallerID, 10) Тогда
							ЗаменитьЗвонок = Истина;
						КонецЕсли;

					Иначе	
						Если Прав(НайденныйЗвонокSIP.CalledID, 10) = Прав(CalledID, 10) Тогда
							ЗаменитьЗвонок = Истина;
						КонецЕсли;
					КонецЕсли;
					
					Если ЗаменитьЗвонок Тогда
						УдаляемыйhCall = НайденныйЗвонокSIP.hCall;
						НайденныйЗвонокSIP.hCall = hCall;
						сфпСтруктураЗвонков.Вставить(КлючПоиска, НайденныйЗвонокSIP);
						сфпУдалитьДанныеЗвонка(УдаляемыйhCall);
						
						Возврат НайденныйЗвонокSIP;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Возврат НайденныйЗвонок;

КонецФункции // сфпНайтиДанныеЗвонка()

// Функция возвращает структуру звонка
//
// Параметры:
//	Origin		- Число		- Идентификатор звонка.
//	ContactID	- Строка	- Идентификатор контакта.
//	PhoneNumber	- Число		- Номер телефона.
//
// Возвращаемое значение:
//	Структура	- Структура данных звонка
//
Функция сфпНайтиЗвонокПоВспомогательнымДанным(Origin, ContactID, PhoneNumber) Экспорт
	НайденныйЗвонок	= Неопределено;
	Если сфпСтруктураЗвонков = Неопределено Тогда
		Возврат НайденныйЗвонок;
	КонецЕсли;
	Если сфпСтруктураЗвонков.Количество() = 0 Тогда
		Возврат НайденныйЗвонок;
	КонецЕсли;
	Для Каждого ЭлементСписка Из сфпСтруктураЗвонков Цикл
		СтруктураЗвонка	= ЭлементСписка.Значение;
		Если СтруктураЗвонка.ContactID = ContactID Тогда
			НайденныйЗвонок	= СтруктураЗвонка;
			Прервать;
		ИначеЕсли сфпОпределитьВходящийЗвонок(Origin) Тогда
			Если Прав(СтруктураЗвонка.CallerID, 10) = Прав(PhoneNumber, 10) Тогда
				НайденныйЗвонок	= СтруктураЗвонка;
				Прервать;
			КонецЕсли;
		Иначе
			Если СтруктураЗвонка.CalledID = PhoneNumber Тогда
				НайденныйЗвонок	= СтруктураЗвонка;
				Прервать;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
	Возврат НайденныйЗвонок;
КонецФункции // сфпНайтиЗвонокПоВспомогательнымДанным()	

// Процедура обновляет или добавляет данные звонка
//
// Параметры:
//	СтруктураЗвонка	- Структура	- Структура с данными звонка
//
Процедура сфпОбновитьДанныеЗвонка(СтруктураЗвонка) Экспорт
	
	КлючПоиска = "_" + Формат(СтруктураЗвонка.hCall, "ЧГ=0"); 
	НайденныйЗвонок	= Неопределено;
	Если сфпСтруктураЗвонков = Неопределено Тогда
		сфпСтруктураЗвонков	= Новый Структура();
	КонецЕсли;	
	Если сфпСтруктураЗвонков.Свойство(КлючПоиска, НайденныйЗвонок) Тогда
		// Обновляем данные звонка
		Для Каждого КлючИЗначение Из СтруктураЗвонка Цикл
			Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				Если КлючИЗначение.Ключ = "ВнешнийЗвонок" Тогда
					НайденныйЗвонок[КлючИЗначение.Ключ] = КлючИЗначение.Значение;

				ИначеЕсли КлючИЗначение.Ключ = "ВходящийЗвонок" Тогда
					НайденныйЗвонок[КлючИЗначение.Ключ] = КлючИЗначение.Значение;

				ИначеЕсли КлючИЗначение.Ключ = "State" Тогда
					НайденныйЗвонок[КлючИЗначение.Ключ] = КлючИЗначение.Значение;

				ИначеЕсли НЕ ЗначениеЗаполнено(НайденныйЗвонок[КлючИЗначение.Ключ]) Тогда
					НайденныйЗвонок[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
				КонецЕсли;
		    КонецЕсли;
		КонецЦикла;

		// Оповещаем внутреннюю панель
		Оповестить("СофтФон_ОбновитьЗвонок", НайденныйЗвонок);          

	ИначеЕсли СтруктураЗвонка.State < 11 Тогда
		// Добавляем новый звонок
		сфпСтруктураЗвонков.Вставить(КлючПоиска, СтруктураЗвонка);
		
		// Оповещаем внутреннюю панель
		Оповестить("СофтФон_ОбновитьЗвонок", СтруктураЗвонка);          
	КонецЕсли;

КонецПроцедуры	// сфпОбновитьДанныеЗвонка()

// Процедура удаляет данные звонка
//
// Параметры:
//	hCall	- Число	- Идентификатор звонка.
//
Процедура сфпУдалитьДанныеЗвонка(hCall) Экспорт
	КлючПоиска		= "_" + Формат(hCall,"ЧГ=0"); 
	НайденныйЗвонок	= Неопределено;
	Если сфпСтруктураЗвонков.Свойство(КлючПоиска, НайденныйЗвонок) Тогда
		сфпСтруктураЗвонков.Удалить(КлючПоиска);
		
		// Оповещаем внутреннюю панель
		Оповестить("СофтФон_УдалитьЗвонок", hCall); 
	КонецЕсли;
КонецПроцедуры // сфпУдалитьДанныеЗвонка()

// Процедура добавляет данные в структуру звонка
//
// Параметры:
//	hCall		- Число			- Идентификатор звонка.
//	Ключ		- Строка		- Имя элемента данных
//	Значение	- Произвольный	- Значение элемента данных
//
Процедура сфпДобавитьДанныеВСтруктуру(hCall, Ключ, Значение)
	КлючПоиска		= "_" + Формат(hCall, "ЧГ=0"); 
	НайденныйЗвонок	= Неопределено;
	Если сфпСтруктураЗвонков.Свойство(КлючПоиска, НайденныйЗвонок) Тогда
		Если НайденныйЗвонок.Свойство(Ключ) Тогда
			НайденныйЗвонок[Ключ] = Значение;
		Иначе
			НайденныйЗвонок.Вставить(Ключ, Значение);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // сфпДобавитьДанныеВСтруктуру()

Процедура сфпЗаполнитьПараметрыЗвонка(ВходящийЗвонок, НомерТелефона, МассивЗвонящих, Контакт, CallerInfoName, CalledInfoName, DopInfo, AppValue, ImageData, ContactID)
	
	Если ЗначениеЗаполнено(НомерТелефона) Тогда
		Если ЗначениеЗаполнено(СокрЛП(AppValue)) Тогда
			Текст = Новый ТекстовыйДокумент;
			Текст.УстановитьТекст(AppValue);
			СтарыйНомер = Текст.ПолучитьСтроку(3);
			Если НЕ ПустаяСтрока(СтарыйНомер) И НЕ (СтарыйНомер = НомерТелефона) Тогда
				Если (СтрДлина(СтарыйНомер) > 7) И (Найти(НомерТелефона, СтарыйНомер) > 0) Тогда
					НомерТелефона = СтарыйНомер;

				Иначе	
					AppValue = "";
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
		
		Контакт = Неопределено;
		ПредставлениеКонтакта = Неопределено;
		ПредставлениеВладельцаКонтакта = Неопределено;
		
		Если ЗначениеЗаполнено(СокрЛП(AppValue)) Тогда
			Текст = Новый ТекстовыйДокумент();
			Текст.УстановитьТекст(AppValue);
			ПредставлениеКонтакта = Текст.ПолучитьСтроку(1);

		ИначеЕсли НЕ ПустаяСтрока(ContactID) Тогда
			Контакт = сфпСофтФонПроСервер.сфпНайтиКонтактПоGUID(ContactID);

		Иначе
			МассивВладельцевКонтактов = Неопределено;
			МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(НомерТелефона, МассивВладельцевКонтактов);
			Если МассивЗвонящих.Количество() = 1 Тогда
				Контакт	= МассивЗвонящих[0];

			ИначеЕсли МассивЗвонящих.Количество() > 1 Тогда
				ПредставлениеКонтакта = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(МассивЗвонящих[0]);
				ПредставлениеКонтакта = НСтр("ru='Первый найденный контакт: '") + Символы.ПС + ПредставлениеКонтакта + Символы.ПС;
				ПредставлениеВладельцаКонтакта = Нстр("ru = 'Есть и другие совпадения'");
				
				ПредставлениеВладельца = "";
				Если МассивВладельцевКонтактов.Количество() = 1 Тогда
					ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(МассивВладельцевКонтактов[0], ПредставлениеВладельца);

				ИначеЕсли МассивВладельцевКонтактов.Количество() > 1 Тогда
					ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(МассивЗвонящих[0], ПредставлениеВладельца);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПредставлениеВладельца) Тогда
					ПредставлениеВладельцаКонтакта = "" + ПредставлениеВладельца;
				КонецЕсли;
						
				// Составим многострочный текст из контактов для показа в панели, первая строка будет проигнорирована
				Сч = 1;
				Для Каждого СтрокаМассива Из МассивЗвонящих Цикл
					Если Сч = 5 Тогда
						ПредставлениеКонтакта = ПредставлениеКонтакта + "...";
						Прервать;
					КонецЕсли;
					
					ПредставлениеКонтакта = ПредставлениеКонтакта + ?(Сч = 1, "", ", ") + Строка(СтрокаМассива);
					
					Сч = Сч + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если Контакт <> Неопределено Тогда
			Если ТипЗнч(Контакт) = Тип("Строка") Тогда
				ПредставлениеКонтакта = Контакт;
				
			Иначе	
				ПредставлениеКонтакта = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(Контакт);
				ContactID = сфпСофтФонПроСервер.сфпПолучитьИдентификаторКонтакта(Контакт);
				ПредставлениеВладельца = "";
				ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(Контакт, ПредставлениеВладельца);
				ПредставлениеВладельцаКонтакта = "" + ПредставлениеВладельца;
				ImageData = сфпСофтФонПроСервер.сфпПолучитьАватарКонтакта(Контакт, Истина);
			КонецЕсли;
		КонецЕсли;
		
		Если ПредставлениеВладельцаКонтакта <> Неопределено Тогда
			DopInfo = ПредставлениеВладельцаКонтакта;
			
			Если ПредставлениеКонтакта <> Неопределено Тогда
				AppValue = ПредставлениеКонтакта + Символы.ПС + ПредставлениеВладельцаКонтакта + Символы.ПС + НомерТелефона;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПредставлениеКонтакта) Тогда
			Если ВходящийЗвонок Тогда
				  CallerInfoName = ПредставлениеКонтакта;
			Иначе CalledInfoName = ПредставлениеКонтакта;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Функция создает и заполняет структуру звонка
Функция сфпИнициализироватьСтруктуруЗвонка(hCall, LineName, LineType, CallerID, CallerInfoName, CalledId, CalledInfoName, State, Origin, DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number)
	
	МассивЗвонящих = Неопределено;
	Контакт = Неопределено;
	ВходящийЗвонок = сфпОпределитьВходящийЗвонок(Origin);
	НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(?(ВходящийЗвонок, CallerID, CalledId));
	ВнешнийЗвонок = сфпОпределитьВнешнийЗвонок(Origin, НомерТелефона);
	
	сфпЗаполнитьПараметрыЗвонка(ВходящийЗвонок, НомерТелефона, МассивЗвонящих, Контакт, CallerInfoName, CalledInfoName, DopInfo, AppValue, ImageData, ContactID);
	
	СтруктураЗвонка = сфпСформироватьСтруктуруЗвонка(hCall, LineName, CallerID, CallerInfoName, CalledId, CalledInfoName, DopInfo,
		State, Origin, AvailableActions, ContactID, Caller_Destination_Number, Контакт, ВходящийЗвонок, ВнешнийЗвонок, НомерТелефона, Неопределено,
		МассивЗвонящих);
		
	Возврат СтруктураЗвонка;
			
КонецФункции

Функция сфпИнициализироватьСтруктуруЗвонкаОффЛайн(Origin, ContactID, PhoneNumber)
	
	Контакт	= Неопределено;
	МассивЗвонящих = Неопределено;
	ВходящийЗвонок = сфпОпределитьВходящийЗвонок(Origin);
	НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(PhoneNumber);
	ВнешнийЗвонок = сфпОпределитьВнешнийЗвонок(Origin, НомерТелефона);
	
	КонтактИнформация = "";
	КонтактПредставление = "";
			
	Если ЗначениеЗаполнено(НомерТелефона) Тогда
		Если НЕ ПустаяСтрока(ContactID) Тогда
			Контакт = сфпСофтФонПроСервер.сфпНайтиКонтактПоGUID(ContactID);
			Если ЗначениеЗаполнено(Контакт) Тогда
				ИмяКонтакта	= сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(Контакт);
				ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(Контакт);
				Если ЗначениеЗаполнено(ВладелецКонтакта) Тогда
					КонтактИнформация = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(ВладелецКонтакта);
				КонецЕсли;
				
				КонтактПредставление = ИмяКонтакта;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Контакт) Тогда
			МассивВладельцевКонтактов = Неопределено;
			МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(НомерТелефона, МассивВладельцевКонтактов);
			Если МассивЗвонящих.Количество() = 1 Тогда
				Контакт	= МассивЗвонящих[0];
				ИмяКонтакта	= сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(Контакт);
				ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(Контакт);
				Если ЗначениеЗаполнено(ВладелецКонтакта) Тогда
					КонтактИнформация = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(ВладелецКонтакта);
				КонецЕсли;	
				
				КонтактПредставление = ИмяКонтакта;
			
			ИначеЕсли МассивЗвонящих.Количество() > 1 Тогда
				ПервыйНайденный	= МассивЗвонящих[0];
				ИмяКонтакта	= сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(ПервыйНайденный);
				КонтактИнформация = Нстр("ru = 'Есть и другие совпадения'");
				КонтактПредставление = НСтр("ru='Первый найденный контакт: '") + Символы.ПС + ИмяКонтакта;
								
				// Составим многострочный текст из контактов для показа в панели, первая строка будет проигнорирована
				Сч = 0;
				Для Каждого СтрокаМассива Из МассивЗвонящих Цикл
					Сч = Сч + 1;
					Если Сч = 5 Тогда
						КонтактПредставление = КонтактПредставление + "...";
												
						Прервать;
					КонецЕсли;
					
					КонтактПредставление = КонтактПредставление + ?(Сч = 1, "", ", ") + Строка(СтрокаМассива); 
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураЗвонка = Новый Структура();
	СтруктураЗвонка.Вставить("НомерТелефона", НомерТелефона);
	СтруктураЗвонка.Вставить("ВходящийЗвонок", ВходящийЗвонок);
	СтруктураЗвонка.Вставить("ВнешнийЗвонок", ВнешнийЗвонок);
	СтруктураЗвонка.Вставить("Контакт", Контакт);
	СтруктураЗвонка.Вставить("КонтактИнформация", КонтактИнформация);
	СтруктураЗвонка.Вставить("КонтактПредставление", КонтактПредставление);
	СтруктураЗвонка.Вставить("МассивЗвонящих", МассивЗвонящих);
	СтруктураЗвонка.Вставить("ContactID", ContactID);
			
	Возврат СтруктураЗвонка;
			
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ВНЕШНЕЙ ПАНЕЛИ

// Динамически подключаемый обработчик события "OnCallInfo" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	hCall				- Число			- Идентификатор линии.
//	LineName			- Строка		- Имя линии.
//	LineType			- Строка		- Тип линии.
//	CallerID			- Строка		- Номер звонящего.
//	CallerInfoName		- Строка		- Представление звонящего. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	CalledId			- Строка		- Номер принимающего звонок.
//	CalledInfoName		- Строка		- Представление принимающего звонок. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	State				- Число			- Состояние звонка.
//	Origin				- Число			- Направление звонка звонка. (0 – Неопределенно; 1 – Внутренний исходящий;
//											2 – Внутренний входящий; 3 – Внешний входящий; 4 – Недоступно;
//											5 – Конференция; 6 – Входящий)
//	DopInfo				- Строка		- Дополнительная информация о звонке. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	AvailableActions	- Число			- Доступные действия со звонком. Битовая маска (aa_Drop	= $00000001; aa_Answer = $00000002;
//											aa_Hold = $00000004; aa_UnHold = $00000008; aa_Redirect = $00000010; aa_Transfer = $00000020;
//											aa_CompleteTransfer = $00000040; aa_CancelTransfer = $00000080)
//	AppValue			- Произвольный	- Произвольная, служебная информация, хранимая приложением. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	ImageData			- Строка		- Картинка контакта в формате Base64. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	ContactID			- Строка		- Идентификатор контакта во внешней учетной системе. Возвращаемый параметр.
//											Значение может быть изменено приложением.
//	Caller_Destination_Number	- Строка - Номер, на который пришел входящий (например городской номер организации, на который звонит клиент).
//											
Процедура сфпOnCallInfo(hCall, LineName, LineType, CallerID, CallerInfoName, CalledId, CalledInfoName, State, Origin, DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number = "", NumberOnLine = "") Экспорт
	
	ТекстЗаписи = "hCall: " + hCall + "; State: " + State + "; LineName: " + LineName + "; NumberOnLine: " + NumberOnLine + "; CallerID: " + CallerID + "; CalledId: " + CalledId + "; Origin: " + Origin;
	сфпСофтФонПроСервер.сфпЗаписьЖурналаРегистрации("OnCallInfo", ТекстЗаписи);
	
	ВходящийЗвонок = сфпОпределитьВходящийЗвонок(Origin);
	
	Если State = 0 Тогда
		СтруктураЗвонка	= сфпНайтиДанныеЗвонка(hCall);
		Если СтруктураЗвонка <> Неопределено Тогда
			Если ЗначениеЗаполнено(СтруктураЗвонка.НовыйЗвонок) Тогда
				// Оповещаем об окончании разговора
				СтруктураОповещения = Новый Структура();
				СтруктураОповещения.Вставить("Звонок", СтруктураЗвонка.НовыйЗвонок);
				
				НомерЛинии = ?(ЗначениеЗаполнено(NumberOnLine), NumberOnLine, LineName);
				НомерЛинии = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерЛинии);
				Если ЗначениеЗаполнено(НомерЛинии) Тогда
					  НовыйОтветственный = сфпСофтФонПроСервер.сфпНайтиОтветственного(НомерЛинии);
				Иначе НовыйОтветственный = сфпСофтФонПроСервер.сфпТекущийПользователь();
				КонецЕсли;
				Если ЗначениеЗаполнено(НовыйОтветственный) И НовыйОтветственный <> сфпСофтФонПроСервер.сфпПолучитьЗначениеРеквизита(СтруктураЗвонка.НовыйЗвонок, "Ответственный") Тогда
					СтруктураОповещения.Вставить("НовыйОтветственный", НовыйОтветственный);
				КонецЕсли;

				Оповестить("СофтФон_КонецРазговора", СтруктураОповещения);
				
				ПараметрыФормы = Новый Структура("Ключ", СтруктураЗвонка.НовыйЗвонок);
				
				ИмяФормыЗвонка = сфпСофтФонПроСервер.сфпИмяФормыДокументаТелефонныйЗвонок();
				Если ЗначениеЗаполнено(ИмяФормыЗвонка) Тогда
					ФормаЗвонка = ПолучитьФорму(ИмяФормыЗвонка, ПараметрыФормы);
					Если НЕ ФормаЗвонка.Открыта() Тогда
						// Записываем в документ время окончания разговора
						Если СтруктураОповещения.Свойство("НовыйОтветственный") Тогда
							  сфпСофтФонПроСервер.сфпЗаписатьОкончаниеЗвонка(СтруктураЗвонка.НовыйЗвонок, НовыйОтветственный);
						Иначе сфпСофтФонПроСервер.сфпЗаписатьОкончаниеЗвонка(СтруктураЗвонка.НовыйЗвонок);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				// Записываем окончание разговора в регистр
				сфпСофтФонПроСервер.сфпЗаписатьОкончаниеЗвонкаВРегистр(СтруктураЗвонка.НовыйЗвонок, hCall);
			КонецЕсли;	
			
			// Удаляем звонок
			сфпУдалитьДанныеЗвонка(hCall);
		КонецЕсли;
		
		сфпДанныеЗаполнения	= Неопределено;

	ИначеЕсли State = 8 ИЛИ State = 15 Тогда
		ТекНомерЛинии = сфпСофтФонПроСервер.сфпТекущийВнутреннийНомер(сфпСофтФонПроСервер.сфпТекущийПользователь());		
		НомерЛинии 	  = ?(ЗначениеЗаполнено(NumberOnLine), NumberOnLine, LineName);
		НомерЛинии 	  = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерЛинии);
		НомерCallerID = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(CallerID);
		НомерCalledId = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(CalledId);
		
		Если hCall > 11 И (НЕ ЗначениеЗаполнено(НомерCallerID) ИЛИ НЕ ЗначениеЗаполнено(НомерCalledId)) Тогда
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НомерЛинии) И (ТекНомерЛинии <> НомерЛинии И Origin = 3) Тогда
			// звонок не нам
			Возврат;
		КонецЕсли;
		
		СтруктураЗвонка	= сфпНайтиДанныеЗвонка(hCall);
		Если СтруктураЗвонка = Неопределено Тогда
			СтруктураЗвонка = сфпИнициализироватьСтруктуруЗвонка(hCall, LineName, LineType, CallerID, CallerInfoName, CalledId, CalledInfoName, State, Origin, DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number);
			сфпОбновитьДанныеЗвонка(СтруктураЗвонка);
			
		ИначеЕсли ЗначениеЗаполнено(СтруктураЗвонка.НовыйЗвонок) Тогда
			// Оповещаем внутреннюю панель
			СтруктураЗвонка.State = State;
			СтруктураЗвонка.AvailableActions = AvailableActions;
			Оповестить("СофтФон_ОбновитьЗвонок", СтруктураЗвонка);

			Возврат;	
		КонецЕсли;		
		
		ВнешнийЗвонок = сфпОпределитьВнешнийЗвонок(Origin, СтруктураЗвонка.НомерТелефона);

		// Для случая, когда Origin меняется в ходе коммуникации (в случае перевода между своими номерами)
		Если СтруктураЗвонка <> Неопределено И (СтруктураЗвонка.ВнешнийЗвонок <> ВнешнийЗвонок ИЛИ СтруктураЗвонка.ВходящийЗвонок <> ВходящийЗвонок) Тогда	
			СтруктураЗвонка.ВнешнийЗвонок = ВнешнийЗвонок;
			СтруктураЗвонка.ВходящийЗвонок = ВходящийЗвонок;
			сфпПараметрыСервера = сфпСофтФонПроСервер.сфпПараметрыСервера();
			сфпМаксимальнаяДлинаВнутреннегоНомера = сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров;
			Если НЕ сфпОпределитьВнутреннийЗвонокПоНомеру(CallerID, сфпМаксимальнаяДлинаВнутреннегоНомера) Тогда
				МассивВладельцевКонтактов = Неопределено;
				
				СтруктураЗвонка.НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(CallerID);
				СтруктураЗвонка.МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(СтруктураЗвонка.НомерТелефона, МассивВладельцевКонтактов);
				
				Контакт = Неопределено;

				сфпЗаполнитьПараметрыЗвонка(ВходящийЗвонок, СтруктураЗвонка.НомерТелефона, СтруктураЗвонка.МассивЗвонящих, Контакт, CallerInfoName, CalledInfoName, DopInfo, AppValue, ImageData, ContactID);
			КонецЕсли;
				
			сфпПанельУправления.SetCallInfo(hCall, LineName, LineType, CallerID, CallerInfoName, CalledId, CalledInfoName, State,Origin, DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number);
						
			сфпОбновитьДанныеЗвонка(СтруктураЗвонка);
		КонецЕсли;

		Если СтруктураЗвонка <> Неопределено Тогда
			// изменился номер телефона, нужно изменить по нему данные в структуре
			Если ПустаяСтрока(СтруктураЗвонка.НомерТелефона) ИЛИ ((СтруктураЗвонка.НомерТелефона <> CallerID) И (СтруктураЗвонка.НомерТелефона <> CalledId)) Тогда
				Контакт	= Неопределено;
				НовыйЗвонок	= Неопределено;
				ВходящийЗвонок = сфпОпределитьВходящийЗвонок(Origin);
				НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(?(ВходящийЗвонок, CallerID, CalledId));
				ВнешнийЗвонок = сфпОпределитьВнешнийЗвонок(Origin, НомерТелефона);

				Если ЗначениеЗаполнено(НомерТелефона) Тогда
					МассивВладельцевКонтактов = Неопределено;
					МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(НомерТелефона, МассивВладельцевКонтактов);
					
					сфпЗаполнитьПараметрыЗвонка(ВходящийЗвонок, НомерТелефона, МассивЗвонящих, Контакт, CallerInfoName, CalledInfoName, DopInfo, AppValue, ImageData, ContactID);
					
					СтруктураЗвонка = сфпСформироватьСтруктуруЗвонка(hCall, LineName, CallerID, CallerInfoName, CalledId,
						CalledInfoName, DopInfo, State, Origin, AvailableActions, ContactID, Caller_Destination_Number, Контакт, ВходящийЗвонок, ВнешнийЗвонок,
						НомерТелефона, НовыйЗвонок, МассивЗвонящих);

					сфпОбновитьДанныеЗвонка(СтруктураЗвонка);

					сфпПанельУправления.SetCallInfo(hCall, LineName, LineType, CallerID, CallerInfoName, CalledId, CalledInfoName, State,Origin, DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number);
				КонецЕсли;
			КонецЕсли;

			Если СтруктураЗвонка.ВнешнийЗвонок Тогда
				Если НЕ ЗначениеЗаполнено(СтруктураЗвонка["НовыйЗвонок"]) Тогда
					Если сфпСофтФонПроСервер.сфпИспользоватьЗаписьПереговоров() Тогда
						Если сфпСофтФонПроСервер.сфпИспользоватьСпрут7() Тогда
							// Передаем команду на запись разговора
							Если НЕ СтруктураЗвонка.ЗаписьЗапущена Тогда
								сфпПанельУправления.StartRecord(СтруктураЗвонка.LineName, СтруктураЗвонка.hCall);	
								СтруктураЗвонка.Вставить("ЗаписьЗапущена", Истина);
							КонецЕсли;								
						КонецЕсли;	
					КонецЕсли;																											
					
					НовыйЗвонок	= сфпСофтФонПроСервер.сфпСоздатьТелефонныйЗвонок(СтруктураЗвонка, сфпДанныеЗаполнения);
					Если ЗначениеЗаполнено(НовыйЗвонок) Тогда
						сфпВыполнитьДействияПослеЗаписиТелефонногоЗвонка(НовыйЗвонок, СтруктураЗвонка);
						сфпСофтФонПроКлиентПереопределяемый.сфпВыполнитьАвтоматическоеДействие(СтруктураЗвонка);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			// Оповещаем внутреннюю панель
			СтруктураЗвонка.State = State;
			СтруктураЗвонка.AvailableActions = AvailableActions;
			Оповестить("СофтФон_ОбновитьЗвонок", СтруктураЗвонка);          
		КонецЕсли;

	Иначе
		СтруктураЗвонка	= сфпНайтиДанныеЗвонка(hCall, ВходящийЗвонок, CallerID, CalledID);
		Если СтруктураЗвонка = Неопределено Тогда
			СтруктураЗвонка = сфпИнициализироватьСтруктуруЗвонка(hCall, LineName, LineType, CallerID, CallerInfoName, CalledId, CalledInfoName, State, Origin, DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number);
			
		Иначе
			СтруктураЗвонка.State = State;
			СтруктураЗвонка.AvailableActions = AvailableActions;
		КонецЕсли;
		сфпОбновитьДанныеЗвонка(СтруктураЗвонка);
		
		сфпПанельУправления.SetCallInfo(hCall, LineName, LineType, CallerID, СтруктураЗвонка.CallerInfoName, CalledId, СтруктураЗвонка.CalledInfoName, State, Origin, СтруктураЗвонка.DopInfo, AvailableActions, AppValue, ImageData, ContactID, Caller_Destination_Number);
	КонецЕсли;

КонецПроцедуры // сфпOnCallInfo()

// Динамически подключаемый обработчик события "OnEvent" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	EventType	- Число		- Тип события (-1 – Таймер; -2 – Изменились настройки; 0 – Автоматический при ответе на звонок;
//										1 – Ручной вызов для создания события;	Прочие при регистрации событий из 1С)
//	Origin		- Число		- Направление звонка звонка (0 – Неопределенно; 1 – Внутренний исходящий; 2 – Внутренний входящий;
//										3 – Внешний входящий; 4 – Недоступно; 5 – Конференция; 6 – Входящий)
//	ContactID	- Строка	- Идентификатор контакта во внешней учетной системе
//	PhoneNumber	- Строка	- Номер телефона контакта
//
Процедура сфпOnEvent(EventType, Origin, ContactID, PhoneNumber) Экспорт
	Если EventType = -2 Тогда
		// Событие изменения номера основной линии
		сфпЗаполнитьПрефиксыИНастройки();
	ИначеЕсли EventType = -1 Тогда
		// Событие таймера, для обновления истории звонков
	ИначеЕсли EventType = 0 Тогда
		// Событие ответа на звонок панели управления
		СтруктураЗвонка	= сфпНайтиЗвонокПоВспомогательнымДанным(Origin, ContactID, PhoneNumber);
	ИначеЕсли EventType = 1 Тогда
		// Событие нажатия кнопки "Передать в 1С" панели управления
		СтруктураЗвонка	= сфпНайтиЗвонокПоВспомогательнымДанным(Origin, ContactID, PhoneNumber);
		сфпСофтФонПроКлиентПереопределяемый.сфпВыполнитьАвтоматическоеДействие(СтруктураЗвонка, Истина);
	Иначе
		// Прочие кнопки, определенные пользователем
		СтруктураЗвонка	= сфпНайтиЗвонокПоВспомогательнымДанным(Origin, ContactID, PhoneNumber);
		Если СтруктураЗвонка = Неопределено Тогда
			СтруктураЗвонка	= сфпИнициализироватьСтруктуруЗвонкаОффЛайн(Origin, ContactID, PhoneNumber);
		КонецЕсли;
		Если ТипЗнч(СтруктураЗвонка) = Тип("Структура") Тогда
			СтруктураЗвонка.Вставить("ФормаПолучена", Ложь);
		КонецЕсли;
		сфпСофтФонПроКлиентПереопределяемый.сфпВыполнитьДоступноеДействие(EventType, СтруктураЗвонка);
	КонецЕсли;
КонецПроцедуры // сфпOnEvent()

// Динамически подключаемый обработчик события "OnRecordInfo" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	RecordEventType		- Число		- Тип события
// 	hCall				- Число		- Идентификатор звонка. Может быть = 0.
//	LineID				- Строка	- Имя линии, на которой происходит звонок. Может быть = "".
//	RecordID			- Строка	- Идентификатор записи
//	TimeStart			- ДатаВремя	- Время начала записи
//	DurationTalk		- Число		- Продолжительность записи в секундах.
//	FileName			- Строка	- Имя файла звонка. Может быть = "".
//	ResultDescription	- Строка	- Описание ошибки. Может быть = "".
//	
Процедура сфпOnRecordInfo(RecordEventType, hCall, LineID, RecordID, TimeStart, DurationTalk, FileName, ResultDescription) Экспорт
	Если НЕ сфпСофтФонПроСервер.сфпИспользоватьЗаписьПереговоров() Тогда Возврат; КонецЕсли;
	Если RecordEventType = 0 Тогда
		// Запись остановлена
		ПоказатьПредупреждение(, ResultDescription, 5);
	ИначеЕсли RecordEventType = 1 Тогда
		// Запись запущена
		СтруктураЗвонка	= сфпНайтиДанныеЗвонка(hCall);
		Если НЕ (СтруктураЗвонка = Неопределено) Тогда
			// Записываем в регистр истории звонков идентификатор записи
			сфпСофтФонПроСервер.сфпЗаписатьИдентификаторЗаписиВРегистр(СтруктураЗвонка.НовыйЗвонок, hCall, RecordID);			
			// Для системы записей CLON передаём в структуру идентификатор
			СтруктураЗвонка.Вставить("ИдентификаторЗаписи", RecordID);
		КонецЕсли;
	ИначеЕсли RecordEventType = 2 Тогда
		// Файл записи сохранен
		Если ПустаяСтрока(FileName) Тогда
			FileName = сфпПолучитьИмяФайлаЗаписиРазговора();
		КонецЕсли;	
		Попытка
			НачатьЗапускПриложения(Новый ОписаниеОповещения("ОбработчикОповещенияБезДействия", сфпСофтФонПроКлиент), FileName);
		Исключение КонецПопытки;
	КонецЕсли;	
КонецПроцедуры // сфпOnRecordInfo()

// Динамически подключаемый обработчик события "OnEvent" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	SA	- COMSafeArray	- Массив описания состояния линий
//
Процедура сфпOnAllLinesInfo(ПараметрыЛиний) Экспорт
	сфпСофтФонПроКлиентНативнаяКомпонента.сфпOnAllLinesInfoНативнаяКомпонента(ПараметрыЛиний);
КонецПроцедуры // сфпOnAllLinesInfo()

// Динамически подключаемый обработчик события "OnResultInfo" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	OperationName				- Строка	- Наименование операции
//	OperationResult				- Число		- Результат. Успешное выполнение, если = 0
//  OperationResultDescription	- Строка	- Описание результата
//	DopInfo						- Строка	- Дополнительная информация
//
Процедура сфпOnResultInfo(OperationName, OperationResult, OperationResultDescription, DopInfo) Экспорт
	Если OperationName = "Autorization" Тогда
		Если OperationResult = 0 Тогда	// Нет ошибки
			сфпЗаполнитьПрефиксыИНастройки(); // Получаем настройки сервера СофтФон
		ИначеЕсли OperationResult = 1 Тогда	// Неверный пароль
			сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(OperationResultDescription);
		ИначеЕсли OperationResult = 2 Тогда	// Пользователь не найден
			сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(OperationResultDescription);
			Если сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпПривязатьВнутреннийНомер") Тогда
				ЛогинСофтфон 	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпЛогинНаСерверСофтФон");
				ПарольСофтфон 	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпПарольНаСерверСофтФон");
				НомерСофтфон	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпТекущийВнутреннийНомер");
				сфпПанельУправления.Registration(ЛогинСофтфон, ПарольСофтфон, НомерСофтфон,"","",1);
				сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(Нстр("ru='Регистрация нового пользователя...';en='New User Registration...'"));
			КонецЕсли;
		ИначеЕсли OperationResult = 3 Тогда	// Отказано в самостоятельной регистрации пользователя
			сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(OperationResultDescription);
		ИначеЕсли OperationResult = 4 Тогда	// Пользователь уже зарегистрирован		
			сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(OperationResultDescription);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // Подключаемый_OnResultInfo()

// Динамически подключаемый обработчик события "OnEventData" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	LineName	- Строка	- Имя линии
//  DataType	- Число		- Тип данных
//	StrData		- Строка	- Строковые данные
//
Процедура сфпOnEventData(LineName, DataType, StrData) Экспорт
	
	Если (DataType = 51) ИЛИ (DataType = 52) Тогда
		Если сфпСофтФонПроСервер.сфпИспользоватьCoMagic() Тогда
			Если ПустаяСтрока(StrData) Тогда
				Возврат;
			КонецЕсли;
			
			СтруктураВнешнихДанных = сфпСофтФонПроКлиент.сфпПолучитьСтруктуруВнешнихДанных(StrData);
			Origin = 0;
			ContactID = "";
			PhoneNumber = Прав(СтруктураВнешнихДанных.ani, 10);
			СтруктураЗвонка	= сфпНайтиЗвонокПоВспомогательнымДанным(Origin, ContactID, PhoneNumber);
			Если СтруктураЗвонка = Неопределено Тогда
				// Сохраняем данные CoMagic в структуре заполнения
				Если сфпДанныеЗаполнения = Неопределено Тогда
					сфпДанныеЗаполнения = Новый Структура;
					сфпДанныеЗаполнения.Вставить("СтруктураCoMagic", СтруктураВнешнихДанных);

				Иначе
					Если сфпДанныеЗаполнения.Свойство("СтруктураCoMagic") Тогда
						сфпДанныеЗаполнения.Удалить("СтруктураCoMagic");
					КонецЕсли;

					сфпДанныеЗаполнения.Вставить("СтруктураCoMagic", СтруктураВнешнихДанных);
				КонецЕсли;	

			Иначе	
				// Оповещаем о структуре CoMagic
				СтруктураОповещения = Новый Структура();
				СтруктураОповещения.Вставить("Звонок", СтруктураЗвонка.НовыйЗвонок);
				СтруктураОповещения.Вставить("СтруктураCoMagic", СтруктураВнешнихДанных);
				Оповестить("Софтфон_CoMagic", СтруктураОповещения);
				
				// Записываем структуру CoMagic в телефонный звонок
				ИмяФормыДокументаТелефонныйЗвонок = сфпСофтФонПроСервер.сфпИмяФормыДокументаТелефонныйЗвонок();
				Если ЗначениеЗаполнено(ИмяФормыДокументаТелефонныйЗвонок) Тогда
					ПараметрыФормы = Новый Структура("Ключ", СтруктураЗвонка.НовыйЗвонок);
					ФормаЗвонка = ПолучитьФорму(ИмяФормыДокументаТелефонныйЗвонок, ПараметрыФормы);
					Если НЕ ФормаЗвонка.Открыта() Тогда
						сфпСофтФонПроСервер.сфпЗаписатьСтруктуруCoMagic(СтруктураЗвонка.НовыйЗвонок, СтруктураВнешнихДанных);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры // сфпOnEventData()

// Динамически подключаемый обработчик события "OnCallInfoCoMagic" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	LineName	- Строка	- Имя линии
//	StrData		- Строка	- Строковые данные
//
Процедура сфпOnCallInfoCoMagic(LineName, StrData) Экспорт
	
	Если сфпСофтФонПроСервер.сфпИспользоватьCoMagic() Тогда
		Если ПустаяСтрока(StrData) Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураВнешнихДанных = сфпСофтФонПроКлиент.сфпПолучитьСтруктуруВнешнихДанных(StrData);
		Origin = 0;
		ContactID = "";
		PhoneNumber = Прав(СтруктураВнешнихДанных.ani, 10);
		СтруктураЗвонка	= сфпНайтиЗвонокПоВспомогательнымДанным(Origin, ContactID, PhoneNumber);
		Если СтруктураЗвонка = Неопределено Тогда
			// Сохраняем данные CoMagic в структуре заполнения
			Если сфпДанныеЗаполнения = Неопределено Тогда
				сфпДанныеЗаполнения = Новый Структура;
				сфпДанныеЗаполнения.Вставить("СтруктураCoMagic", СтруктураВнешнихДанных);
				
			Иначе
				Если сфпДанныеЗаполнения.Свойство("СтруктураCoMagic") Тогда
					сфпДанныеЗаполнения.Удалить("СтруктураCoMagic");
				КонецЕсли;	
				
				сфпДанныеЗаполнения.Вставить("СтруктураCoMagic", СтруктураВнешнихДанных);
			КонецЕсли;	

		Иначе	
			// Оповещаем о структуре CoMagic
			СтруктураОповещения = Новый Структура();
			СтруктураОповещения.Вставить("Звонок", СтруктураЗвонка.НовыйЗвонок);
			СтруктураОповещения.Вставить("СтруктураCoMagic", СтруктураВнешнихДанных);
			Оповестить("Софтфон_CoMagic", СтруктураОповещения);
			
			ИмяФормыДокументаТелефонныйЗвонок = сфпСофтФонПроСервер.сфпИмяФормыДокументаТелефонныйЗвонок();
			Если ЗначениеЗаполнено(ИмяФормыДокументаТелефонныйЗвонок) Тогда
				ПараметрыФормы = Новый Структура("Ключ", СтруктураЗвонка.НовыйЗвонок);
				ФормаЗвонка	= ПолучитьФорму(ИмяФормыДокументаТелефонныйЗвонок, ПараметрыФормы);
				Если НЕ ФормаЗвонка.Открыта() Тогда
					// Записываем в документ структуру CoMagic
					сфпСофтФонПроСервер.сфпЗаписатьСтруктуруCoMagic(СтруктураЗвонка.НовыйЗвонок, СтруктураВнешнихДанных);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // сфпOnCallInfoCoMagic()

// Динамически подключаемый обработчик события "сфпOnLinesStatus" внешней панели
// Отсутствие ссылок на процедуру не является ошибкой!
//
// Параметры:
//	StrData		- Строка	- Строковые данные
//
Процедура сфпOnLinesStatus(StrData) Экспорт
	РезультатСоответствие = сфпСофтФонПроСервер.UnJSON(ЗаменитьНедопустимыеСимволыXML(StrData));
	Если РезультатСоответствие = Неопределено Тогда Возврат; КонецЕсли;
	МассивРезультата = РезультатСоответствие.Получить("lines");
	Если МассивРезультата = Неопределено Тогда Возврат; КонецЕсли;
	МассивИзменений = Новый Массив;
	Для Каждого ЭлементМассива Из МассивРезультата Цикл
		СтруктураКлючей = Новый Структура();
		Для Каждого	ЭлементСоответствия Из ЭлементМассива Цикл
			СтруктураКлючей.Вставить(ЭлементСоответствия.Ключ, ЭлементСоответствия.Значение); 
		КонецЦикла;	
		МассивИзменений.Добавить(СтруктураКлючей);
	КонецЦикла;	
	Если МассивИзменений.Количество() > 0 Тогда
		сфпПерезаполнитьСоответствиеСостоянияЛиний(МассивИзменений);
	КонецЕсли;
КонецПроцедуры // сфпOnLinesStatus()

/////////////////////////////////////////////////
// ПРОЦЕДУРЫ ВЫПОЛНЕНИЯ ЗВОНКА 

// Функция разбирает переданную структуру номера телефона и возвращает строку, которую будет набирать АТС
//
// Параметры:
//	СтруктураНомера	- Структура	- Структура телефонного номера
//
// Возвращаемое значение:
//	Строка	- Строка для набора АТС
//
Функция сфпПреобразоватьНомерСУчетомПрефиксов(СтруктураНомера)
	сфпПараметрыСервера	= сфпСофтФонПроСервер.сфпПараметрыСервера();
	Если ПустаяСтрока(СтруктураНомера.НомерТелефона) Тогда
		Если ПустаяСтрока(СтруктураНомера.Добавочный) Тогда
			// Номера нет.
			РезультатНомер = "";
		Иначе
			// Внутренний номер.
			РезультатНомер = СтруктураНомера.Добавочный;
		КонецЕсли;			
	// проверяем длину номеру вместе с длиной кода города, т.к. в ряде компаний внутренний 
	// номер может быть достаточно длинным, до 9 символов
	// у внутренних же номеров код города отсутствует
	ИначеЕсли СтрДлина(СтруктураНомера.НомерТелефона)+СтрДлина(СтруктураНомера.КодГорода) > сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров Тогда
		КодСтраныНомера	= СокрЛП(СтрЗаменить(СтруктураНомера.КодСтраны, "+", ""));
		КодСтраныНомера	= ?(КодСтраныНомера = "8", "7", КодСтраныНомера);
		КодСтраныПользователя	= СокрЛП(СтрЗаменить(сфпПараметрыСервера.КодСтраны, "+", ""));
		КодСтраныПользователя	= ?(КодСтраныПользователя = "8", "7", КодСтраныПользователя);
		Если ПустаяСтрока(КодСтраныНомера) Тогда
			// Номер нашей страны.
			РезультатНомер = СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		ИначеЕсли ПустаяСтрока(КодСтраныПользователя) Тогда
			// Номер нашей страны.
			РезультатНомер = СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		ИначеЕсли (КодСтраныНомера	= КодСтраныПользователя) Тогда
			// Номер нашей страны.
			РезультатНомер = СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		Иначе
			// Номер друой страны.
			РезультатНомер = СтруктураНомера.КодСтраны + СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		КонецЕсли;	
	Иначе
		// Внутренний номер.
		РезультатНомер = СтруктураНомера.НомерТелефона;
	КонецЕсли;	
	Возврат РезультатНомер
КонецФункции // ПреобразоватьНомерСУчетомПрефиксов()

// Процедура выполняет звонок через панель СофтФон
//
// Параметры:
//	ДополнительныеПараметры	- Структура	- Структура дополнительных параметров
//
Процедура сфпВыполнитьЗвонок(ДополнительныеПараметры)
	
	Если ПустаяСтрока(ДополнительныеПараметры.Телефон) Тогда
		Возврат;
	КонецЕсли;
	
	ContactID = "";
	ContactName = "";
	ContactDopInfo = "";
	ContactImage = ""; 
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.СсылкаНаОбъект) Тогда
		Если ТипЗнч(ДополнительныеПараметры.СсылкаНаОбъект) <> Тип("Строка") Тогда
			ContactName = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(ДополнительныеПараметры.СсылкаНаОбъект);
			ContactID = сфпСофтФонПроСервер.сфпПолучитьИдентификаторКонтакта(ДополнительныеПараметры.СсылкаНаОбъект);
			ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(ДополнительныеПараметры.СсылкаНаОбъект);
			
			ContactDopInfo = "";
			Если ЗначениеЗаполнено(ВладелецКонтакта) Тогда
				ContactDopInfo = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(ВладелецКонтакта);
			КонецЕсли;
			
			ContactImage = сфпСофтФонПроСервер.сфпПолучитьАватарКонтакта(ДополнительныеПараметры.СсылкаНаОбъект, Истина);
			
		Иначе
			ContactID = ДополнительныеПараметры.СсылкаНаОбъект;
		КонецЕсли;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("МеждународныйЗвонок") и ДополнительныеПараметры.МеждународныйЗвонок Тогда
		ДополнительныеПараметры.Телефон = СтрЗаменить(ДополнительныеПараметры.Телефон, "+", "");
	КонецЕсли;				
	
	Попытка
		Результат = сфпПанельУправления.MakeCall("", ДополнительныеПараметры.Телефон, ContactID, ContactName, ContactDopInfo, ContactImage);
		сфпДанныеЗаполнения	= ДополнительныеПараметры.ДанныеЗаполнения;
	Исключение
		сфпДанныеЗаполнения	= Неопределено;
		Состояние(НСтр("ru='Не удалось создать звонок'"));	
	КонецПопытки;

КонецПроцедуры // сфпВыполнитьЗвонок()

// Процедура - обработчик ответа на вопрос о совершении международного звонка
//
// Параметры:
//	ОтветНаВопрос			- КодВозвратаДиалога	- Ответ на вопрос
//	ДополнительныеПараметры	- Структура				- Структура дополнительных параметров
//
Процедура сфпВыполнитьМеждународныйЗвонок(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда	
		ДополнительныеПараметры.Вставить("МеждународныйЗвонок", Истина);
		сфпВыполнитьЗвонок(ДополнительныеПараметры);
	КонецЕсли;	
КонецПроцедуры // ВыполнитьМеждународныйЗвонок()

// Функция выполняет звонок по номеру телефона
//
// Параметры:
//	Телефон					- Строка			- Номер телефона
//	Объект					- СправочникСсылка	- Контакт
//	ДанныеЗаполнения		- Структура			- Данные для заполнения создаваемого телефонного звонка
//
Функция сфпПозвонить(Телефон, Объект = Неопределено, ДанныеЗаполнения = Неопределено) Экспорт
	
	Если ПустаяСтрока(Телефон) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран номер телефона!'"), 5);		
		Возврат Ложь;

	ИначеЕсли НЕ сфпПроверитьДоступностьСофтФон(Истина) Тогда
		Возврат Ложь;

	ИначеЕсли Телефон = сфпСофтФонПроСервер.сфпТекущийВнутреннийНомер() Тогда
		Возврат Ложь;
	КонецЕсли;

	сфпЗаполнитьПрефиксыИНастройки();

	ContactID		= "";
	ContactName		= "";
	ContactDopInfo	= "";
	ContactImage	= "";
	
	СтруктураНомера	= сфпСофтФонПроСервер.сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Телефон);
	Телефон = сфпПреобразоватьНомерСУчетомПрефиксов(СтруктураНомера);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Телефон", Телефон);
	ДополнительныеПараметры.Вставить("СсылкаНаОбъект", Объект);
	ДополнительныеПараметры.Вставить("ДанныеЗаполнения", ДанныеЗаполнения);
	
	Если СтрДлина(Телефон) > 10 Тогда
		ОповещениеОтвета = Новый ОписаниеОповещения("сфпВыполнитьМеждународныйЗвонок", сфпСофтФонПроКлиент, ДополнительныеПараметры);
		ТекстЗаголовка = НСтр("ru='ВНИМАНИЕ!!! Выполняется звонок в другую страну'");
		ТекстВопроса = НСтр("ru='ВОЗМОЖНО!!! Телефонный номер набран неверно"  + Символы.ПС + "Подтвердите выполнение международного звонка'");
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Набрать номер'"));
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отменить'"));
		
		ПоказатьВопрос(ОповещениеОтвета, ТекстВопроса, СписокКнопок, 30, КодВозвратаДиалога.Нет, ТекстЗаголовка, КодВозвратаДиалога.Нет);

	Иначе
		сфпВыполнитьЗвонок(ДополнительныеПараметры);		
	КонецЕсли;

	Возврат Истина;

КонецФункции // сфпПозвонить()

// Процедура - обработчик оповещения для выполнения звонка
//
// Параметры:
//	ВыбранныйНомер			- ЭлементСпискаЗначений	- Выбранный номер
//	ДополнительныеПараметры	- Структура				- Структура дополнительных параметров
//
Процедура сфпВыполнитьЗвонокПоНомеру(ВыбранныйНомер, ДополнительныеПараметры) Экспорт
	Если ВыбранныйНомер = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран номер телефона!'"), 5);
		Возврат;
	КонецЕсли;
	сфпПозвонить(ВыбранныйНомер.Значение.Телефон, ВыбранныйНомер.Значение.Контакт, ДополнительныеПараметры.ДанныеЗаполнения);
КонецПроцедуры // сфпВыполнитьЗвонокПоНомеру()

// Процедура выполняет звонок после выбора номера телефона из списка номеров
//
// Параметры:
//	СписокОбъектов			- СписокЗначений	- Список объектов, для которых выбираются телефоны
//	ДанныеЗаполнения		- Структура			- Данные для заполнения создаваемого события
//
Процедура сфпПозвонитьВыбравТелефон(СписокОбъектов, ДанныеЗаполнения = Неопределено) Экспорт
	Если СписокОбъектов.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран абонент'"), 5);
		Возврат;
	КонецЕсли;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеЗаполнения",	ДанныеЗаполнения);
	
	СписокТелефонов = сфпЗаполнитьСписокТелефонов(СписокОбъектов);	
	
	Если СписокТелефонов.Количество() = 0 Тогда
		Телефон = Строка(ДанныеЗаполнения.Основание.ПредставлениеТелефона);
		сфпПозвонить(Телефон, , ДанныеЗаполнения);
	ИначеЕсли СписокТелефонов.Количество() = 2 Тогда
		сфпВыполнитьЗвонокПоНомеру(СписокТелефонов[0], ДополнительныеПараметры);
	Иначе	
		ОписаниеВыбора = Новый ОписаниеОповещения("сфпВыполнитьЗвонокПоНомеру", сфпСофтФонПроКлиент, ДополнительныеПараметры); 
		СписокТелефонов.ПоказатьВыборЭлемента(ОписаниеВыбора, НСтр("ru='Выбор телефона'"));
	КонецЕсли;
	
КонецПроцедуры // сфпПозвонитьВыбравТелефон()	

/////////////////////////////////////////////////
// ЗВОНОК

// Функция проверяет строку на пустоту
//
// Параметры:
//	ВыбСтрока		- Строка	- Проверяемая строка
//	ПризнакЗапятой  - Булево	- Признак добавления запятой
//
// Возвращаемое значение:
//	Строка	- Строка после проверки
//
Функция сфпПроверкаПустойСтроки(ВыбСтрока, ПризнакЗапятой = Истина) Экспорт
	Если ПустаяСтрока(ВыбСтрока) Тогда
		Возврат "";
	Иначе
		Возврат ?(ПризнакЗапятой, ",", "") + " ";
	КонецЕсли; 
КонецФункции // сфпПроверкаПустойСтроки()

// Функция определяет внутренний или внешний звонок исходя
//  из длины переданного номера телефона, и максимальной длины внутреннего номера 
//  телефона установленного в системе. Если длина номера > 0 И <= макс, то это внутренний
//
// Параметры:
//	НомерТелефона						- Строка	- Номер телефона
//	МаксимальнаяДлинаВнутреннегоНомера	- Число		- Максимальная длина внутреннего номера
//
// Возвращаемое значение:
//	Булево	- Признак внутреннего звонка
//
Функция сфпОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, МаксимальнаяДлинаВнутреннегоНомера) Экспорт
	НомерТелефона = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
	ДлинаНомераТелефона = СтрДлина(НомерТелефона);
	Возврат (ДлинаНомераТелефона <= МаксимальнаяДлинаВнутреннегоНомера);
КонецФункции // сфпОпределитьВнутреннийЗвонокПоНомеру()

// Функция формирует структуру телефонного звонка по контактной информации,
//  если значение неопределено, то создает пустую структуру, сделано для удобства
//  создание структуры в одном месте
//
// Параметры:
//	ТекущаяСтрока	- СтрокаТабличнойЧасти	- Строка контактной информации
//
// Возвращаемое значение:
//	СтруктураНомера	- Структура телефонного номера
//
Функция сфпСформироватьСтруктуруНомераИзПолей(ТекущаяСтрока = Неопределено) Экспорт
	СтруктураТелефона = Новый Структура;
	Если ТекущаяСтрока = Неопределено Тогда
		СтруктураТелефона.Вставить("НомерТелефона", 	"");
		СтруктураТелефона.Вставить("Добавочный",    	"");
		СтруктураТелефона.Вставить("КодГорода",     	"");
		СтруктураТелефона.Вставить("КодСтраны",     	"");
		СтруктураТелефона.Вставить("Представление",		"");
	Иначе
		СтруктураТелефона = сфпПреобразоватьСтрокуВСписокПолей(ТекущаяСтрока.ЗначенияПолей);
	КонецЕсли;
	сфпСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураТелефона);
	Возврат СтруктураТелефона;
КонецФункции // сфпСформироватьСтруктуруНомераИзПолей()	

// Функция преобразовывает строку полей в список значений
//
// Параметры:
//	СтрокаПолей	- Строка	- Строка, содержащая контактную информацию
//
// Возвращаемое значение:
//	СписокЗначений	- Список полей контактной информации
//
Функция сфпПреобразоватьСтрокуВСписокПолей(СтрокаПолей) Экспорт 
	Результат = Новый СписокЗначений;
	ПоследнийЭлемент = Неопределено;
	Для Сч = 1 По СтрЧислоСтрок(СтрокаПолей) Цикл
		Стр = СтрПолучитьСтроку(СтрокаПолей, Сч);
		Если Лев(Стр, 1) = Символы.Таб Тогда
			Если ПоследнийЭлемент <> Неопределено Тогда
				ПоследнийЭлемент.Значение = ПоследнийЭлемент.Значение + Символы.ПС + Сред(Стр, 2);
			КонецЕсли;
		Иначе
			Поз = Найти(Стр, "=");
			Если Поз <> 0 Тогда
				ПоследнийЭлемент = Результат.Добавить(Сред(Стр, Поз+1), Лев(Стр, Поз-1));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции // сфпПреобразоватьСтрокуВСписокПолей()

// Процедура формирует строковое представление адреса
//
// Параметры:
//	СтруктураТелефона	- Структура	- Структура телефонного номера
//
Процедура сфпСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураТелефона) Экспорт
	Если ПустаяСтрока(СтруктураТелефона.НомерТелефона) Тогда
		// Если не заполнено поле НомерТелефона, то принудительно скидываем представление,
		// потому что код страны и код города явно мало для создания номера.
		СтруктураТелефона.КодСтраны = "";
		СтруктураТелефона.КодГорода = "";
		СтруктураТелефона.Представление = "";
	Иначе	
		СтруктураТелефона.Представление = СокрЛП(СтруктураТелефона.КодСтраны);
		Если НЕ ПустаяСтрока(СтруктураТелефона.КодГорода) Тогда
			СтруктураТелефона.Представление = СтруктураТелефона.Представление + ?(ПустаяСтрока(СтруктураТелефона.Представление), "", " ") + "(" + СокрЛП(СтруктураТелефона.КодГорода) + ")";
		КонецЕсли;
		Если НЕ ПустаяСтрока(СтруктураТелефона.НомерТелефона) Тогда
			СтруктураТелефона.Представление = СтруктураТелефона.Представление + ?(ПустаяСтрока(СтруктураТелефона.Представление), "", " ") + СокрЛП(СтруктураТелефона.НомерТелефона);
		КонецЕсли;
		Если НЕ ПустаяСтрока(СтруктураТелефона.Добавочный) Тогда
			СтруктураТелефона.Представление = СтруктураТелефона.Представление + ?(ПустаяСтрока(СтруктураТелефона.Представление), "", ", ") + "доб. " + СокрЛП(СтруктураТелефона.Добавочный);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // сфпСформироватьПредставлениеТелефонаПоСтруктуре()

/////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С КОНТАКТНОЙ ИНФОРМАЦИЕЙ

// Функция возвращает список телефонов объектов из списка
//
// Параметры:
//	СписокОбъектов	- СписокЗначений	- Список объектов, для которых выбираются телефоны
//
// Возвращаемое значение:
//	СписокЗначений	- Список телефонов объектов
//
Функция сфпЗаполнитьСписокТелефонов(СписокОбъектов) Экспорт
	СписокТелефонов = Новый СписокЗначений;
	ТекКонтакт = "";
	Для Каждого СтрокаКонтакт Из СписокОбъектов Цикл
		Если ТипЗнч(СтрокаКонтакт.Значение) = Тип("Строка") Тогда
			Продолжить;
		КонецЕсли;
		ТелефоныКонтакта = сфпСофтФонПроСервер.сфпПолучитьМассивТелефоновИФаксов(СтрокаКонтакт.Значение);
		Для Каждого Телефон Из ТелефоныКонтакта Цикл
			Значение = Новый Структура;
			Значение.Вставить("Контакт", СтрокаКонтакт.Значение);
			Значение.Вставить("Телефон", Телефон.Представление);
			Если НЕ (ТекКонтакт = СтрокаКонтакт.Значение)Тогда
				СписокТелефонов.Добавить(Значение, СокрЛП(СтрокаКонтакт.Значение));
				ТекКонтакт = СтрокаКонтакт.Значение;
			КонецЕсли;
			СписокТелефонов.Добавить(Значение, "  " + СокрЛП(Телефон.Вид) + ": " + Телефон.Представление);
		КонецЦикла;
	КонецЦикла;
	Возврат СписокТелефонов;
КонецФункции // сфпЗаполнитьСписокТелефонов()

/////////////////////////////////////////////////
// ОБЩИЕ ТЕЛЕФОННЫЕ КНИГИ СЕРВЕРА СОФТФОН

// Процедура записывает телефонную книгу на сервере СофтФон
//
// Параметры:
//	СтруктураКниги	- Структура	- Структура телефонной книги
//
Процедура сфпЗаписатьТелефоннуюКнигу(СтруктураКниги) Экспорт
	Если НЕ сфпПроверитьДоступностьСофтФон(Ложь) Тогда Возврат; КонецЕсли;
	ОписаниеТелефоннойКниги = сфпСофтФонПроСервер.сфпСформироватьОписаниеТелефоннойКниги(СтруктураКниги, Ложь);
	Если ПустаяСтрока(ОписаниеТелефоннойКниги) Тогда Возврат; КонецЕсли;
	ОписаниеОшибки = "";
	Попытка
		сфпПанельУправления.PutAddressBooks(ОписаниеТелефоннойКниги, ОписаниеОшибки);
	Исключение
	КонецПопытки;
	Если НЕ (ОписаниеОшибки = "Ok") Тогда
		сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(ОписаниеОшибки);
	КонецЕсли;	
КонецПроцедуры // сфпЗаписатьТелефоннуюКнигу()

// Процедура удаляет телефонную книгу на сервере СофтФон
//
// Параметры:
//	СтруктураКниги	- Структура	- Структура телефонной книги
//
Процедура сфпУдалитьТелефоннуюКнигу(СтруктураКниги) Экспорт
	Если НЕ сфпПроверитьДоступностьСофтФон(Ложь) Тогда Возврат; КонецЕсли;
	ОписаниеТелефоннойКниги = сфпСофтФонПроСервер.сфпСформироватьОписаниеТелефоннойКниги(СтруктураКниги, Истина);
	ОписаниеОшибки = "";
	Попытка
		сфпПанельУправления.PutAddressBooks(ОписаниеТелефоннойКниги, ОписаниеОшибки);
	Исключение
	КонецПопытки;
	Если НЕ (ОписаниеОшибки = "Ok") Тогда
		сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(ОписаниеОшибки);
	КонецЕсли;	
КонецПроцедуры // сфпУдалитьТелефоннуюКнигу()

/////////////////////////////////////////////////
// СОСТОЯНИЯ ЛИНИЙ

// Процедура получает состояния внутренних линий
//
// Параметры:
//	Нет.
//
Процедура сфпПолучитьСостоянияЛиний() Экспорт
	Если НЕ сфпПроверитьДоступностьСофтФон(Ложь) Тогда Возврат; КонецЕсли;
	#Если Вебклиент Тогда
		Состояние(НСтр("ru='Работа СофтФона невозможна в web-клиенте!'"));
	#Иначе
		Если сфпПанельУправления = Неопределено Тогда Возврат; КонецЕсли;
		Попытка
			сфпПанельУправления.GetAllLinesInfo();
		Исключение
		КонецПопытки;
	#КонецЕсли
КонецПроцедуры // сфпПолучитьСостоянияЛиний()

/////////////////////////////////////////////////
// МАСТЕР НАСТРОЙКИ IP-АТС AGAT UX

// Процедура выполняет запуск мастера настройки IP-АТС AgatUX
//
// Параметры:
//	Нет.
//
Процедура сфпЗапуститьМастерНастройкиAgatUX() Экспорт
	#Если Вебклиент Тогда
		ПоказатьПредупреждение(, НСтр("ru='Работа мастера настройки AgatUX возможна только в тонком клиенте 1С'"), 5);
	#Иначе
		// Формируем и сохраняем список пользователей
		ФайлСпискаПользователей = Новый ТекстовыйДокумент;	
		МассивПользователей = сфпСофтФонПроСервер.сфпПолучитьМассивПользователей();
		Для Каждого ЭлементМассива Из МассивПользователей Цикл
			ФайлСпискаПользователей.ДобавитьСтроку(ЭлементМассива);
		КонецЦикла;
		Попытка
			ФайлСпискаПользователей.Записать(КаталогВременныхФайлов() + "personal.txt"); 
		Исключение
		КонецПопытки;	
		// Извлекаем и сохраняем файл настроек мастера настройки Agat UX
		Попытка 
			АдресХранилища = сфпСофтФонПроСервер.сфпВернутьХранилищеФайлаНастройкиМастера();
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
			ВременныйФайл = КаталогВременныхФайлов() + "SetupSettings.ini"; 
			ДвоичныеДанные.Записать(ВременныйФайл);
		Исключение
		КонецПопытки;
		// Извлекаем и запускаем мастер настройки Agat UX
		Попытка 
			АдресХранилища = сфпСофтФонПроСервер.сфпВернутьХранилищеМастераНастройки();
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
			ВременныйФайл = ПолучитьИмяВременногоФайла("exe"); 
			ДвоичныеДанные.Записать(ВременныйФайл);
			ЗапуститьПриложение(ВременныйФайл);
		Исключение
			сфпОбщегоНазначенияКлиентСервер.сфпСообщитьПользователю(НСтр("ru='Не удалось запустить установку мастера настройки AgatUX'"));
		КонецПопытки;
	#КонецЕсли
КонецПроцедуры // сфпЗапуститьМастерНастройкиAgatUX()

/////////////////////////////////////////////////
// ЗАПИСЬ ТЕЛЕФОННЫХ ПЕРЕГОВОРОВ

// Функция возвращает имя временного файла, в который выгружается запись разговора,
// полученная с сервера СофтФона
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Строка	- Имя файла для сохранения записи разговора
//
Функция сфпПолучитьИмяФайлаЗаписиРазговора() Экспорт
	// Определяем путь к файлу для сохранение записи разговора
	ИмяКаталога = "c:\";
	#Если НЕ ВебКлиент Тогда
	ИмяКаталога = КаталогВременныхФайлов();
	#КонецЕсли
	Возврат  (ИмяКаталога + "softphone.wav");
КонецФункции // сфпПолучитьИмяФайлаЗаписиРазговора()		

// Процедура получает файл записи разговора
//
// Параметры:
//	ИдентификаторЗаписи	- Строка	- ИдентификаторЗаписи
//
Процедура сфпПолучитьФайлРазговора(ИдентификаторЗаписи) Экспорт
	
	#Если Вебклиент Тогда
		Состояние(НСтр("ru='Работа СофтФона невозможна в web-клиенте!'"));
	#Иначе
		Если сфпПанельУправления = Неопределено Тогда
			Возврат;

		ИначеЕсли НЕ сфпСофтФонПроСервер.сфпИспользоватьЗаписьПереговоров() Тогда
			Возврат;
		КонецЕсли;

		FileName = сфпПолучитьИмяФайлаЗаписиРазговора();
		Если сфпСофтФонПроСервер.сфпИспользоватьСпрут7() Тогда
			Попытка
				сфпПанельУправления.SaveRecordedFile(, ИдентификаторЗаписи, FileName);
			Исключение КонецПопытки;
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры // сфпПолучитьФайлРазговора()

////////////////////////////////////////////////////////////////////////////////
// КОНТЕКСТНАЯ РЕКЛАМА COMAGIC

// Функция вычисляет степень числа
//
// Параметры:
//	_База	- Число	- Число, возводимое в степень
//	_Степ	- Число	- Степень числа
//
// Возвращаемое значение:
//	Число	- Число, возведенное в степень
//
Функция сфпСтепень(_База, _Степ)
	Результат = 1;
	Для К = 1 По _Степ Цикл
		Результат = Результат *_База;		
	КонецЦикла;
	Возврат Результат;
КонецФункции // сфпСтепень()

// Функция переводит шестнадцатеричное число в десятичное
//
// Параметры:
//	_Hex	- Строка	- Шестнадцатеричное число
//
// Возвращаемое значение:
//	Число	- Десятичное число
//
Функция сфпHexToDec(Знач _Hex)
	База = 16;
	_Hex = СокрЛП(_Hex);
	СтаршаяСтепень = СтрДлина(_Hex) - 1;
	Результат = 0;
	счСимволов = 1;
	Пока СтаршаяСтепень >=0 Цикл
		_HexСимвол = Сред(_Hex, счСимволов, 1);
		Представление = Найти("0123456789ABCDEF", _HexСимвол) - 1;
		Результат = Результат + Представление * сфпСтепень(База, СтаршаяСтепень);
		СтаршаяСтепень = СтаршаяСтепень - 1;
		СчСимволов = СчСимволов + 1;
	КонецЦикла;	
	Возврат Результат;
КонецФункции // сфпHexToDec()

// Функция преобразовает строку из UTF-8
//
// Параметры:
//	Стр	- Строка	- Строка с шестнадцатеричными символами
//
// Возвращаемое значение:
//	Строка	- Строка после преобразования
//
Функция сфпПреобразоватьСтрокуИзУТФ8(Стр)
	РабочаяСтрока = Стр;
	Пока Истина Цикл
		ПозицияНачала = Найти(РабочаяСтрока, "\u");
		Если ПозицияНачала = 0 Тогда Прервать; КонецЕсли;
		СтрокаСимвола = Сред(РабочаяСтрока, ПозицияНачала, 6); 	
		НовыйСимвол = Символ(сфпHexToDec(ВРег(Сред(СтрокаСимвола, 3))));
		РабочаяСтрока = СтрЗаменить(РабочаяСтрока, СтрокаСимвола, НовыйСимвол);
	КонецЦикла;	
	Возврат РабочаяСтрока; 
КонецФункции // сфпПреобразоватьСтрокуИзУТФ8()

// Функция переводит строку внешних данных в структуру
//
// Параметры:
//	СтрокаДанных	- Строка	- Строка внешних данных
//
// Возвращаемое значение:
//	Структура	- Структура внешних данных
//
Функция сфпПолучитьСтруктуруВнешнихДанных(СтрокаВнешнихДанных) Экспорт
	СтруктураВнешнихДанных = Новый Структура;
	// Убираем символы перевода строк
	СтрокаПоиска = СтрЗаменить(СтрокаВнешнихДанных, Символы.ПС, "");
	// Убираем скобки
	СтрокаПоиска = СтрЗаменить(СтрокаПоиска, "{", "");
	СтрокаПоиска = СтрЗаменить(СтрокаПоиска, "}", "");
	// Убираем проблеы слева и справа
	СтрокаПоиска = СокрЛП(СтрокаПоиска);
	// Ищем тег "comagic_context"
	ПозицияРазделителя = Найти(СтрокаПоиска, """" + "comagic_context" + """" + ":");
	ПерваяСтрока = СокрЛП(Лев(СтрокаПоиска, ПозицияРазделителя - 1));
	ВтораяСтрока = СокрЛП(Сред(СтрокаПоиска, ПозицияРазделителя + 18));
	// Разбираем первую строку
	Пока Истина Цикл
		ПозицияРазделителя = Найти(ПерваяСтрока, ",");
		Если ПозицияРазделителя = 0 Тогда
			РабочаяСтрока	= СтрокаПоиска;
			ПерваяСтрока	= "";	
		Иначе
			РабочаяСтрока	= СокрЛП(Лев(ПерваяСтрока, ПозицияРазделителя - 1));
			ПерваяСтрока	= СокрЛП(Сред(ПерваяСтрока, ПозицияРазделителя + 1));	
		КонецЕсли;
		ПозицияРазделителя = Найти(РабочаяСтрока, ":");
		СтрокаКлюч = СокрЛП(Лев(РабочаяСтрока, ПозицияРазделителя - 1));
		СтрокаКлюч = Сред(СтрокаКлюч, 2, СтрДлина(СтрокаКлюч) - 2);
		СтрокаЗначение	= СокрЛП(Сред(РабочаяСтрока, ПозицияРазделителя + 1));
		СтрокаЗначение = Сред(СтрокаЗначение, 2, СтрДлина(СтрокаЗначение) - 2);
		СтруктураВнешнихДанных.Вставить(СтрокаКлюч, СтрокаЗначение);
		Если СтрДлина(ПерваяСтрока) = 0 Тогда Прервать; КонецЕсли;
	КонецЦикла;
	// Разбираем вторую строку
	СтруктураЗначений = Новый Структура;
	Пока Истина Цикл
		ПозицияРазделителя = Найти(ВтораяСтрока, ",");
		Если ПозицияРазделителя = 0 Тогда
			РабочаяСтрока	= ВтораяСтрока;
			ВтораяСтрока	= "";	
		Иначе
			РабочаяСтрока	= СокрЛП(Лев(ВтораяСтрока, ПозицияРазделителя - 1));
			ВтораяСтрока	= СокрЛП(Сред(ВтораяСтрока, ПозицияРазделителя + 1));	
		КонецЕсли;
		ПозицияРазделителя = Найти(РабочаяСтрока, ":");
		СтрокаКлюч = СокрЛП(Лев(РабочаяСтрока, ПозицияРазделителя - 1));
		СтрокаКлюч = Сред(СтрокаКлюч, 2, СтрДлина(СтрокаКлюч) - 2);
		СтрокаЗначение	= СокрЛП(Сред(РабочаяСтрока, ПозицияРазделителя + 1));
		Если НЕ ПустаяСтрока(СтрокаЗначение) Тогда
			Если СтрокаЗначение = "null" Тогда
				СтрокаЗначение = "";
			Иначе
				Если НЕ СтрокаКлюч = "visitor_id" Тогда
					СтрокаЗначение = Сред(СтрокаЗначение, 2, СтрДлина(СтрокаЗначение) - 2);
					СтрокаЗначение = сфпПреобразоватьСтрокуИзУТФ8(СтрокаЗначение);
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	
		СтруктураЗначений.Вставить(СтрокаКлюч, СтрокаЗначение);
		Если СтрДлина(ВтораяСтрока) = 0 Тогда Прервать; КонецЕсли;
	КонецЦикла;
	СтруктураВнешнихДанных.Вставить("comagic_context", СтруктураЗначений);
	Возврат СтруктураВнешнихДанных;
КонецФункции // сфпПолучитьСтруктуруВнешнихДанных()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ИСТОРИЕЙ ЗВОНКОВ

// Процедура открывает список записей регистра истории звонков
//
// Параметры:
//	Ссылка	- ДокументСсылка	- Ссылка на телефонный звонок
//
Процедура сфпОткрытьИсториюРазговора(Ссылка) Экспорт
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Звонок", Ссылка);
	ОткрытьФорму("РегистрСведений.сфпИсторияЗвонков.ФормаСписка", ПараметрыФормы);	
КонецПроцедуры // сфпОткрытьИсториюРазговора()

// Процедура открывает список записей регистра истории звонков
//
// Параметры:
//	Ссылка	- ДокументСсылка	- Ссылка на телефонный звонок
//
Процедура сфпОткрытьИсториюТелефонныхЗвонков(Ссылка) Экспорт
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контакт", Ссылка);
	ОткрытьФорму("РегистрСведений.сфпИсторияЗвонков.ФормаСписка", ПараметрыФормы);	
КонецПроцедуры // сфпОткрытьИсториюТелефонныхЗвонков()

Процедура ПоказатьРасписаниеРегламентногоЗадания(ИмяЗадания) Экспорт
	
	Расписание = сфпСофтФонПроСервер.ПолучитьРасписаниеРегламентногоЗадания(ИмяЗадания);
	Если Расписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметры	= Новый Структура("ИмяЗадания", ИмяЗадания);
	Оповещение = Новый ОписаниеОповещения("ПриЗакрытииДиалогаРасписания", сфпСофтФонПроКлиент, ДополнительныеПараметры);
	
	ДиалогРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ДиалогРасписания.Показать(Оповещение);
	
КонецПроцедуры

Процедура ПриЗакрытииДиалогаРасписания(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	сфпСофтФонПроСервер.УстановитьРасписаниеРегламентногоЗадания(Расписание, ДополнительныеПараметры.ИмяЗадания);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С JSON

// Заменяет недопустимые символы в XML-строке на заданные символы
//
// Параметры:
//   Текст – Строка – строка, в которой требуется выполнить замену недопустимых символов.
//   СимволЗамены – Строка – строка, на которую требуется выполнить замену недопустимого символа в XML-строке
// 
//  Возвращаемое значение:
//    Строка - cтрока, полученная заменой недопустимых символов в XML-строке.
//
Функция ЗаменитьНедопустимыеСимволыXML(Знач Текст, СимволЗамены = " ") Экспорт
	
#Если НЕ ВебКлиент Тогда
	ПозицияНачала = 1;
	Пока Истина Цикл
		Позиция = НайтиНедопустимыеСимволыXML(Текст, ПозицияНачала);
		Если Позиция = 0 Тогда
			Прервать;
		КонецЕсли;
		Если Позиция > 1 Тогда
			НедопустимыйСимвол = Сред(Текст, Позиция - 1, 1);
			Если НайтиНедопустимыеСимволыXML(НедопустимыйСимвол) > 0 Тогда
				Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
			КонецЕсли;
		КонецЕсли;
		НедопустимыйСимвол = Сред(Текст, Позиция, 1);
		Если НайтиНедопустимыеСимволыXML(НедопустимыйСимвол) > 0 Тогда
			Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
		КонецЕсли;
		ПозицияНачала = Позиция + 1;
	КонецЦикла;
#КонецЕсли

	Возврат Текст;
КонецФункции

// Процедура программно создаёт дополнительные кнопки системы Софтфон у элементов справочников и документа "Событие"
//
// Параметры:
//	ЭтаФорма - ФормаЭлемента
//
Процедура сфпДобавитьКнопкиНаКоманднуюПанельФормЭлементов(ЭтаФорма) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		КнопкиКоманднойПанели = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки;
		
		ДействиеКнопки = Новый Действие("сфпПодключаемыйОбработчик");
		
		Если сфпСофтФонПроКлиент.сфпПроверитьДоступностьСофтФон(Ложь) Тогда
			КнопкиКоманднойПанели.Добавить("сфпРазделитель", ТипКнопкиКоманднойПанели.Разделитель);
			
			// Добавляем кнопку "Позвонить"
			НоваяКнопка = КнопкиКоманднойПанели.Добавить("сфпПозвонить", ТипКнопкиКоманднойПанели.Действие,, ДействиеКнопки);
			НоваяКнопка.Картинка = БиблиотекаКартинок.сфпТелефонныйЗвонок;
			НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			НоваяКнопка.Подсказка = Нстр("ru = 'Позвонить'");
			
			МетаданныеОбъекта = ЭтаФорма.ЭтотОбъект.Ссылка.Метаданные();
			ИмяОбъекта = МетаданныеОбъекта.Имя;
			
			Если ИмяОбъекта = "Контрагенты" ИЛИ ИмяОбъекта = "КонтактныеЛица" ИЛИ ИмяОбъекта = "КонтактныеЛицаКонтрагентов" Тогда
				Подменю = КнопкиКоманднойПанели.Добавить("сфпПодменю", ТипКнопкиКоманднойПанели.Подменю, Нстр("ru = 'СофтФон'"));
				Подменю.Картинка = БиблиотекаКартинок.сфпСофтФон48;
				Подменю.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Подменю.Подсказка = Нстр("ru = 'Команды сервиса СофтФон'");
				
				Если сфпСофтФонПроСервер.сфпИспользоватьМаршрутизацию() Тогда
					// Добавляем кнопку "Перевод звонков"
					НоваяКнопка = Подменю.Кнопки.Добавить("сфпПереводЗвонков", ТипКнопкиКоманднойПанели.Действие, Нстр("ru = 'Перевод звонков'"), ДействиеКнопки);	
					НоваяКнопка.Картинка = БиблиотекаКартинок.ЖурналРегистрацииПоПользователю;
					НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					НоваяКнопка.Подсказка = Нстр("ru = 'Перевод входящих звонков на выбранного пользователя'");
					НоваяКнопка.Текст = сфпСофтФонПроСервер.сфпПолучитьТекстКнопкиПереводЗвонков(ЭтаФорма.ЭтотОбъект.Ссылка);
				КонецЕсли;
				
				Если МетаданныеОбъекта.Реквизиты.Найти("сфпCoMagicID") <> Неопределено И сфпСофтФонПроСервер.сфпИспользоватьCoMagic() Тогда
					// Добавляем кнопку "Карточка CoMagic"
					НоваяКнопка = Подменю.Кнопки.Добавить("сфпКарточкаCoMagic", ТипКнопкиКоманднойПанели.Действие, Нстр("ru = 'Карточка CoMagic'"), ДействиеКнопки);
					НоваяКнопка.Картинка = БиблиотекаКартинок.сфпCoMagic;
					НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					НоваяКнопка.Подсказка = Нстр("ru = 'Открыть карточку контакта в сервисе CoMagic'");
					НоваяКнопка.Текст = Нстр("ru = 'Карточка CoMagic'");
				КонецЕсли;
				
				// Добавляем кнопку "История телефонных звонков"
				НоваяКнопка = Подменю.Кнопки.Добавить("сфпИсторияРазговораКонтакта", ТипКнопкиКоманднойПанели.Действие, Нстр("ru = 'История телефонных звонков'"), ДействиеКнопки);
				НоваяКнопка.Картинка = БиблиотекаКартинок.ТелефонныйЗвонок;
				НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				НоваяКнопка.Подсказка = Нстр("ru = 'Открыть историю телефонных звонков'");
				НоваяКнопка.Текст = Нстр("ru = 'История телефонных звонков'");
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяОбъекта = "Пользователи" Тогда
			// Добавляем кнопку "Персональные настройки СофтФон"
			НоваяКнопка = КнопкиКоманднойПанели.Добавить("сфпПерсональныеНастройки", ТипКнопкиКоманднойПанели.Действие, Нстр("ru = 'Настройки Софтфон'"), ДействиеКнопки);	
			НоваяКнопка.Картинка = БиблиотекаКартинок.сфпСофтФон48;
			НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
			НоваяКнопка.Подсказка = Нстр("ru = 'Открыть персональные настройки СофтФон'"); 
			
			// Добавляем кнопку "Прослушиваемые пользователи"
			НоваяКнопка = КнопкиКоманднойПанели.Добавить("сфпПрослушиваемыеПользователи", ТипКнопкиКоманднойПанели.Действие, Нстр("ru = 'Прослушиваемые пользователи'"), ДействиеКнопки);	
			НоваяКнопка.Картинка = БиблиотекаКартинок.сфпПереключатель;
			НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
			НоваяКнопка.Подсказка = Нстр("ru = 'Настройка прослушиваемых пользователей'"); 		
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

// Функция возвращает текст кнопки перевода звонков
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт
//
// Возвращаемое значение:
//	Строка	- Текст кнопки перевода звонков
//
Функция сфпПолучитьТекстКнопкиПереводЗвонков(Контакт)
	Возврат сфпСофтФонПроСервер.сфпПолучитьТекстКнопкиПереводЗвонков(Контакт);
КонецФункции // сфпПолучитьТекстКнопкиПереводЗвонков()

// Процедура - обработчик подключаемых команд
//
Процедура сфпПодключаемыйОбработчик(ЭтаФорма, Кнопка) Экспорт
	
	Если Кнопка.Имя = "сфпПозвонить" Тогда
		сфпСофтФонПроКлиентПереопределяемый.сфпОбработкаКомандыПозвонить(ЭтаФорма.Ссылка);

	ИначеЕсли Кнопка.Имя = "сфпПереводЗвонков" Тогда
		ПараметрыВыполненияКоманды = Новый Структура("Источник", ЭтаФорма);
		сфпСофтФонПроКлиентПереопределяемый.сфпОбработкаКомандыПереводЗвонков(ЭтаФорма.Ссылка, ПараметрыВыполненияКоманды);

	ИначеЕсли Кнопка.Имя = "сфпКарточкаCoMagic" Тогда
		сфпСофтФонПроКлиентПереопределяемый.сфпОбработкаКомандыКарточкаCoMagic(ЭтаФорма.Ссылка);
		
	ИначеЕсли Кнопка.Имя = "сфпИсторияТелефонныхЗвонков" Тогда
		сфпСофтФонПроКлиентПереопределяемый.сфпОбработкаКомандыИсторияТелефонныхЗвонков(ЭтаФорма.Ссылка);
		
	//+АА	
	//ИначеЕсли Кнопка.Имя = "сфпПерсональныеНастройки" Тогда
	//	ПараметрыФормы = Новый Структура("Пользователь", ЭтаФорма.Ссылка);
	//	ОткрытьФорму("ОбщаяФорма.сфпПерсональныеНастройки", ПараметрыФормы, ЭтаФорма);
	//-АА

	ИначеЕсли Кнопка.Имя = "сфпПрослушиваемыеПользователи" Тогда
		ПараметрыФормы = Новый Структура("Владелец", ЭтаФорма.Ссылка);
		ОткрытьФорму("РегистрСведений.сфпДоступныеДляПрослушиванияПользователи.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры // сфпПодключаемыйОбработчик()

// Заглушка для методов типа НачатьЗапускПриложения, с обязательным ОписаниеОповещения
Процедура ОбработчикОповещенияБезДействия(Результат, ДополнительныеПараметры) Экспорт
КонецПроцедуры
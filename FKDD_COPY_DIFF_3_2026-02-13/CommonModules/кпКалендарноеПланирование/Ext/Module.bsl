////////////////////////////////////////////////////////////////////////////////
// В ДАННЫЙ МОДУЛЬ ВЫНЕСЕНЫ ФУНКЦИИ, СВЯЗАННЫЕ С ПОДСИСТЕМОЙ
// 					"КАЛЕНДАРНОЕ ПЛАНИРОВАНИЕ"
////////////////////////////////////////////////////////////////////////////////


// Получение продолжительности графика в секундах
//
// Параметры:
//  График 		     - ТаблицаЗначений - График по которому вычисляется продолжительность
//  НачалоГрафика    - Дата			   - Дата начала вычисления продолжительности
//  ОкончаниеГрафика - Дата            - Дата окончания вычисления продолжительности
//  ВидыИнтервалов   - Массив          - Массив либо элемент перечисления вид интервала,
//					 для которого вычисляется продолжительность
//	ВидыДней         - Массив          - Массив либо элемент перечисления вид дня,
//					 для которого вычисляется продолжительность
//
// Возвращаемое значений:
//	Число - Продолжительность графика
//
Функция кпПолучитьПродолжительностьГрафика(График, НачалоГрафика, ОкончаниеГрафика, ВидыИнтервалов = Неопределено, ВидыДней = Неопределено) Экспорт
	Продолжительность = 0;
	
	ГрафикТаблица = кпПолучитьГрафик(График, НачалоГрафика, ОкончаниеГрафика, ВидыИнтервалов, ВидыДней);
	Для Каждого ТекСтрокаГрафика Из ГрафикТаблица Цикл
		Продолжительность = Продолжительность + (ТекСтрокаГрафика.Продолжительность - '00010101');
	КонецЦикла;
	
	Возврат Продолжительность;
КонецФункции

// Получение ТЗ описывающую полную таблицу графика
//
// Параметры:
//  График 		     - ТаблицаЗначений - График по которому вычисляется продолжительность
//  НачалоГрафика    - Дата			   - Дата начала вычисления продолжительности
//  ОкончаниеГрафика - Дата            - Дата окончания вычисления продолжительности
//  ВидыИнтервалов   - Массив          - Массив либо элемент перечисления вид интервала,
//					 для которого вычисляется продолжительность
//	ВидыДней         - Массив          - Массив либо элемент перечисления вид дня,
//					 для которого вычисляется продолжительность
// Возвращаемое значение:	
//	ТаблицаЗначений - Таблица графика
//
Функция кпПолучитьГрафик(График, Знач НачалоГрафика, Знач ОкончаниеГрафика, Знач ВидыИнтервалов = Неопределено, Знач ВидыДней = Неопределено) Экспорт
	ТаблицаГрафика = Новый ТаблицаЗначений;
	ТаблицаГрафика.Колонки.Добавить("Дата",					 Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаГрафика.Колонки.Добавить("НачалоРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	ТаблицаГрафика.Колонки.Добавить("КонецРабочегоВремени",  Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	ТаблицаГрафика.Колонки.Добавить("Продолжительность",	 Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	ТаблицаГрафика.Колонки.Добавить("Смена",				 Новый ОписаниеТипов("СправочникСсылка.Смены"));
	ТаблицаГрафика.Колонки.Добавить("ВидДня",				 Новый ОписаниеТипов("ПеречислениеСсылка.ВидДня"));
	ТаблицаГрафика.Колонки.Добавить("ВидИнтервала",			 Новый ОписаниеТипов("СправочникСсылка.ВидыИнтервалов"));
	ТаблицаГрафика.Колонки.Добавить("Банковский",			 Новый ОписаниеТипов("Булево"));
	ТаблицаГрафика.Колонки.Добавить("Комментарий",			 Новый ОписаниеТипов("Строка"));
		
	// Проверка корректности параметров
	Если обЗначениеНеЗаполнено(График) ИЛИ обЗначениеНеЗаполнено(НачалоГрафика)
		ИЛИ обЗначениеНеЗаполнено(ОкончаниеГрафика)	ИЛИ График.Периодичность.Пустая() Тогда
		
		Возврат ТаблицаГрафика;
	КонецЕсли;
	
	//приводим виды интервалов и дней к массиву
	Если ТипЗнч(ВидыИнтервалов) <> Тип("Массив") И ВидыИнтервалов <> Неопределено Тогда
		ВидыИнтервалов_ = ВидыИнтервалов;
		ВидыИнтервалов = Новый Массив;
		ВидыИнтервалов.Добавить(ВидыИнтервалов_);
	КонецЕсли;
	Если ТипЗнч(ВидыДней) <> Тип("Массив") И ВидыДней<>Неопределено Тогда
		ВидыДней_ = ВидыДней;
		ВидыДней = Новый Массив;
		ВидыДней.Добавить(ВидыДней_);
	КонецЕсли;
	ОтборПоВидамИнтервалов = (ТипЗнч(ВидыИнтервалов) = Тип("Массив"));
	ОтборПоВидамДней = (ТипЗнч(ВидыДней) = Тип("Массив"));
	
	НачалоГрафика = НачалоДня(НачалоГрафика);
	ОкончаниеГрафика = НачалоДня(ОкончаниеГрафика);
		
	Если График.Фиксированный Тогда	//заполняем по таблице смещения справочника ГрафикиРаботы
		
		Если График.Смещение.Количество() = 0 Тогда
			Возврат ТаблицаГрафика;
		КонецЕсли;
		
		ПериодичностьГрафика = Строка(График.Периодичность);
		
		ДатаНачалаШаблонаОбязательна = (ПериодичностьГрафика = "Произвольный"
			ИЛИ ПериодичностьГрафика = "Неделя"
			ИЛИ ПериодичностьГрафика = "Декада");
			
		Если ДатаНачалаШаблонаОбязательна И График.ДатаНачала = '00010101' Тогда
			Возврат ТаблицаГрафика;
		КонецЕсли;
		
		//расчет максимального смещения - максимальный номер дня в ТЧ Смещение шаблона
		Если ДатаНачалаШаблонаОбязательна Тогда
			НачальноеСмещение = (НачалоГрафика - График.ДатаНачала)/60/60/24;
			Если ПериодичностьГрафика = "Произвольный" Тогда
				ТаблицаСмещения = График.Смещение.Выгрузить();
				ТаблицаСмещения.Сортировать("НомерДня Убыв");
				МаксимальноеСмещение = ТаблицаСмещения[0].НомерДня + 1;
			ИначеЕсли ПериодичностьГрафика = "Неделя" Тогда
				МаксимальноеСмещение = 7;
			ИначеЕсли ПериодичностьГрафика = "Декада" Тогда
				МаксимальноеСмещение = 10;
			КонецЕсли;
		Иначе
			Если ПериодичностьГрафика = "Год" Тогда
				НачальноеСмещение = НачалоГрафика - НачалоГода(НачалоГрафика);
				МаксимальноеСмещение = 366;
			ИначеЕсли ПериодичностьГрафика = "Квартал" Тогда
				НачальноеСмещение = НачалоГрафика - НачалоКвартала(НачалоГрафика);
				МаксимальноеСмещение = 92;
			ИначеЕсли ПериодичностьГрафика = "Месяц" Тогда
				НачальноеСмещение = НачалоГрафика - НачалоМесяца(НачалоГрафика);
				МаксимальноеСмещение = 31;
			КонецЕсли;
		КонецЕсли;
		
		НачальноеСмещение = НачальноеСмещение - МаксимальноеСмещение * Цел(НачальноеСмещение/МаксимальноеСмещение);
		Если НачальноеСмещение < 0 Тогда
			НачальноеСмещение = МаксимальноеСмещение + НачальноеСмещение;
		КонецЕсли;
		
		МаксимальноеСмещение = МаксимальноеСмещение - 1;
		
		ТекущееСмещение = НачальноеСмещение;
		РазницаДней = (ОкончаниеГрафика - НачалоГрафика)/60/60/24;
		Для ПеременнаяРазницаДней = 0 По РазницаДней Цикл
			ДатаЦикла = НачалоГрафика + ПеременнаяРазницаДней*60*60*24;
			
			//корректировка тек. смещения под исключения (високосный год, длина месяцев квартала и т.п.)
			//добавляем разницу между макс. колич. дней в месяце (31) и текущим количеством дней в предыдущем
			//месяце (перепрыгиваем доп. число строк)
			ЭтоВисокосныйГод = (ДеньГода(КонецГода(ДатаЦикла)) = 366);
			Если ПериодичностьГрафика = "Год" Тогда
				Если (НЕ ЭтоВисокосныйГод) И ((Месяц(ДатаЦикла)=3 И День(ДатаЦикла)=1)
					ИЛИ (ПеременнаяРазницаДней = 0 И Месяц(ДатаЦикла)>=3 И День(ДатаЦикла)>=1)) Тогда
					ТекущееСмещение = ТекущееСмещение + 1;
				КонецЕсли;
				
			ИначеЕсли ПериодичностьГрафика = "Квартал" Тогда
				ТекущееСмещение = (НачалоДня(ДатаЦикла) - НачалоКвартала(ДатаЦикла))/24/3600;
				
			ИначеЕсли ПериодичностьГрафика = "Месяц" Тогда
				ТекущееСмещение = День(ДатаЦикла)-1;
				
			КонецЕсли;
			
			СтрокаГрафика = График.Смещение.Найти(ТекущееСмещение, "НомерДня");
			
			Если СтрокаГрафика <> Неопределено Тогда
				Если ОтборПоВидамДней И ВидыДней.Найти(СтрокаГрафика.ВидДня) = Неопределено Тогда Продолжить; КонецЕсли;
				Если СтрокаГрафика.Смена.Пустая() Тогда	//Заполняем ТЗ по интервалам смещения
					СтрокаТаблицыГрафика = ТаблицаГрафика.Добавить();
					СтрокаТаблицыГрафика.Дата = НачалоГрафика + (ПеременнаяРазницаДней*60*60*24);
					СтрокаТаблицыГрафика.НачалоРабочегоВремени = СтрокаГрафика.НачалоРабочегоВремени;
					СтрокаТаблицыГрафика.КонецРабочегоВремени = СтрокаГрафика.КонецРабочегоВремени;
					СтрокаТаблицыГрафика.Продолжительность = СтрокаГрафика.Продолжительность;
					СтрокаТаблицыГрафика.Смена = Справочники.Смены.ПустаяСсылка();
					СтрокаТаблицыГрафика.ВидДня = СтрокаГрафика.ВидДня;
					СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ПустаяСсылка();
					СтрокаТаблицыГрафика.Банковский = СтрокаГрафика.Банковский;
					СтрокаТаблицыГрафика.Комментарий = СтрокаГрафика.Комментарий;
					
				Иначе //Заполняем ТЗ по интервалам смены
					Для Каждого ИнтервалСмены Из СтрокаГрафика.Смена.Интервалы Цикл	
						Если ОтборПоВидамИнтервалов И ВидыИнтервалов.Найти(ИнтервалСмены.ВидИнтервала) = Неопределено Тогда Продолжить; КонецЕсли;
						СтрокаТаблицыГрафика = ТаблицаГрафика.Добавить();
						СтрокаТаблицыГрафика.Дата = НачалоГрафика + (ПеременнаяРазницаДней*60*60*24);
						СтрокаТаблицыГрафика.НачалоРабочегоВремени = ИнтервалСмены.НачалоРабочегоВремени;
						СтрокаТаблицыГрафика.КонецРабочегоВремени = ИнтервалСмены.КонецРабочегоВремени;
						СтрокаТаблицыГрафика.Продолжительность = ИнтервалСмены.Продолжительность;
						СтрокаТаблицыГрафика.Смена = СтрокаГрафика.Смена;
						СтрокаТаблицыГрафика.ВидДня = СтрокаГрафика.ВидДня;
						СтрокаТаблицыГрафика.ВидИнтервала = ИнтервалСмены.ВидИнтервала;
						СтрокаТаблицыГрафика.Банковский = СтрокаГрафика.Банковский;
						СтрокаТаблицыГрафика.Комментарий = СтрокаГрафика.Комментарий;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			Если МаксимальноеСмещение <= ТекущееСмещение Тогда
				ТекущееСмещение = 0;
			Иначе
				ТекущееСмещение = ТекущееСмещение + 1;
			КонецЕсли;
		КонецЦикла;
	Иначе
		// Не фиксированный график, таблицу получаем из регистра
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ГрафикРаботКалендарный.Дата,
		|	ГрафикРаботКалендарный.НачалоРабочегоВремени,
		|	ГрафикРаботКалендарный.КонецРабочегоВремени,
		|	ГрафикРаботКалендарный.Продолжительность,
		|	ГрафикРаботКалендарный.Смена,
		|	ГрафикРаботКалендарный.ВидДня,
		|	ГрафикРаботКалендарный.ВидИнтервала,
		|	ГрафикРаботКалендарный.Банковский,
		|	ГрафикРаботКалендарный.Комментарий
		|ИЗ
		|	РегистрСведений.ГрафикРаботКалендарный КАК ГрафикРаботКалендарный
		|ГДЕ
		|	ГрафикРаботКалендарный.График = &График
		|	"+?(ОтборПоВидамДней,"И ГрафикРаботКалендарный.ВидДня В (&ВидыДней)","")+"
		|	"+?(ОтборПоВидамИнтервалов,"И ГрафикРаботКалендарный.ВидИнтервала В (&ВидыИнтервалов)","")+"
		|	И (ГрафикРаботКалендарный.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)");
				
		Запрос.УстановитьПараметр("График", График);
		Запрос.УстановитьПараметр("ВидыИнтервалов", ВидыИнтервалов);
		Запрос.УстановитьПараметр("ВидыДней", ВидыДней);
		Запрос.УстановитьПараметр("ДатаНачала", НачалоГрафика);
		Запрос.УстановитьПараметр("ДатаОкончания", ОкончаниеГрафика);
				
		ТаблицаГрафика = Запрос.Выполнить().Выгрузить();
	КонецЕсли;

	Возврат ТаблицаГрафика;
КонецФункции

// Функция возвращает таблицу объектов из регистров сведений "ГрафикРаботыРесурсов" и, возможно, "Напоминания"
//
// Параметры:
//   Ресурс1 				   – Ссылка - Значение первого ресурса для отбора
//   Ресурс2 				   – Ссылка - Значение второго ресурса для отбора
//   УчитыватьПорядокРесурсов  – Булево - Определяет, будет ли учитываться порядок значений ресурсов при выборке.
//                              Если равен "Ложь", то будут выбираться все записи, где есть значение Ресурс1
//								(и возможно Ресурс2) независимо от того, где находится это значение в записи
//								регистра - в измерении "Ресурс1" или в "Ресурс2" 
//   ДатаНачала  			   - Дата   - Дата начала выборки объектов
//   ДатаОкончания 			   - Дата   - Дата окончания выборки объектов
//   ВыполнятьПолноеСоединение - Булево - Признак выполнения полного соединения с регистром "Напоминания".
//								 (Только в том в случае если в ресурс передана ссылка на пользователя!)
//   ГруппироватьПоДате 	   - Булево - Признак группировки результата запроса по дате
//   АктуальныеЗавершенные 	   - Число - 0: все, 1: только актуальные, 2: только завершенные
//
// Возвращаемое значение:
//   ТаблицаЗначений   – Таблица или дерево полученных данных (в зависимости от использования группировки)
//
Функция кпПолучитьОбъектыИзГрафикаРаботыРесурсов(Ресурс1, Ресурс2 = Неопределено, УчитыватьПорядокРесурсов = Истина,
	ДатаНачала = Неопределено, ДатаОкончания = Неопределено, ВыполнятьПолноеСоединение = Истина, ГруппироватьПоДате = Истина, АктуальныеЗавершенные = 1, МенеджерВременныхТаблицДляЗаполнения = Неопределено, СтруктураИндексов=Неопределено) Экспорт
	
	ИспользоватьМенеджерВременныхТаблиц = (ТипЗнч(МенеджерВременныхТаблицДляЗаполнения)=Тип("МенеджерВременныхТаблиц") И Не ГруппироватьПоДате);
	
	Если ИспользоватьМенеджерВременныхТаблиц Тогда
		ОтрицательныйРезультат = Ложь;
	Иначе
		ОтрицательныйРезультат = Новый ТаблицаЗначений; 
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Ресурс1) Тогда
		Возврат ОтрицательныйРезультат;
	КонецЕсли;
	
	Если ДатаНачала <> Неопределено И ДатаОкончания <> Неопределено Тогда
		Если ДатаОкончания < ДатаНачала Тогда
			Возврат ОтрицательныйРезультат;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
				   
	ДелатьСоединение = (ТипЗнч(Ресурс1) = Тип("СправочникСсылка.Пользователи") ИЛИ ТипЗнч(Ресурс2) = Тип("СправочникСсылка.Пользователи")) И ВыполнятьПолноеСоединение;
	
	// если у нас есть переданный пользователь, то сделаем полное соединение с регистром напоминаний по объекту
	Если ДелатьСоединение Тогда
		ТекстЗапроса = "ВЫБРАТЬ 
					   |	ВЫБОР 
					   |		КОГДА ГрафикРаботыРесурсов.Объект ЕСТЬ NULL ТОГДА ВложенныйЗапрос.ДатаОповещения
					   |		ИНАЧЕ ГрафикРаботыРесурсов.Дата
					   |	КОНЕЦ КАК Дата,
		               |	ГрафикРаботыРесурсов.Объект,
					   |	ВЫБОР 
					   |		КОГДА ГрафикРаботыРесурсов.Ресурс1 = &ПереданныйПользователь ТОГДА ГрафикРаботыРесурсов.Ресурс2
					   |		ИНАЧЕ ГрафикРаботыРесурсов.Ресурс1
					   |	КОНЕЦ КАК Ресурс,
					   |	ЕСТЬNULL(ГрафикРаботыРесурсов.НачалоРабочегоВремени, ВложенныйЗапрос.РеальнаяДатаОповещения) КАК НачалоРабочегоВремени,
		               |	ГрафикРаботыРесурсов.КонецРабочегоВремени,
		               |	ГрафикРаботыРесурсов.Продолжительность,
		               |	ГрафикРаботыРесурсов.Смена,
		               |	ВложенныйЗапрос.ОбъектНапоминания,
					   |	ВЫБОР 
					   |		КОГДА НЕ ВложенныйЗапрос.ОбъектНапоминания ЕСТЬ NULL ТОГДА Истина
					   |		ИНАЧЕ Ложь
					   |	КОНЕЦ КАК ЗаписьСодержитНапоминание,
		               |	ВложенныйЗапрос.РеальнаяДатаОповещения,
					   |	ВложенныйЗапрос.Тема,
					   |	ВложенныйЗапрос.Автор,
					   |	ВложенныйЗапрос.Пользователь,
					   |	ВЫБОР 
					   |		КОГДА ГрафикРаботыРесурсов.Объект ЕСТЬ NULL ТОГДА ВложенныйЗапрос.Завершено
					   |		ИНАЧЕ Ложь
					   |	КОНЕЦ КАК НапоминаниеЗавершено"+?(ИспользоватьМенеджерВременныхТаблиц,Символы.ПС+" ПОМЕСТИТЬ ГрафикиРаботРесурсов","")+"
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		               |	ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |		Напоминания.Объект КАК ОбъектНапоминания,
					   |		Напоминания.Пользователь,
					   |		Напоминания.Автор,
					   |		Напоминания.Тема,
					   |		Напоминания.Завершено,
					   |		НАЧАЛОПЕРИОДА(Напоминания.РеальнаяДатаОповещения, День) Как ДатаОповещения,
		               |		Напоминания.РеальнаяДатаОповещения КАК РеальнаяДатаОповещения
		               |	ИЗ
		               |		РегистрСведений.Напоминания КАК Напоминания"
					   + ?(АктуальныеЗавершенные = 0, "", "
		               |	ГДЕ" + ?(АктуальныеЗавершенные = 1, "
		               |		(НЕ Напоминания.Завершено)", "
					   |		Напоминания.Завершено"))
					   + ") КАК ВложенныйЗапрос
					   |  ПО ГрафикРаботыРесурсов.Объект = ВложенныйЗапрос.ОбъектНапоминания И ГрафикРаботыРесурсов.Дата = ВложенныйЗапрос.ДатаОповещения
					   |ГДЕ"
					   + ?(АктуальныеЗавершенные = 0, "", "
		               |	((ГрафикРаботыРесурсов.Объект ЕСТЬ NULL) ИЛИ ГрафикРаботыРесурсов.Объект.Состояние В(&мсвСостоянияСобытия)) И");

	Иначе
		ТекстЗапроса = "ВЫБРАТЬ 
		               |	ГрафикРаботыРесурсов.Дата,
		               |	ГрафикРаботыРесурсов.Объект,
					   |	ВЫБОР 
					   |		КОГДА ГрафикРаботыРесурсов.Ресурс1 = &ПереданныйПользователь ТОГДА ГрафикРаботыРесурсов.Ресурс2
					   |		ИНАЧЕ ГрафикРаботыРесурсов.Ресурс1
					   |	КОНЕЦ КАК Ресурс,
		               |	ГрафикРаботыРесурсов.НачалоРабочегоВремени,
		               |	ГрафикРаботыРесурсов.КонецРабочегоВремени,
		               |	ГрафикРаботыРесурсов.Смена,
		               |	ГрафикРаботыРесурсов.Продолжительность"+?(ИспользоватьМенеджерВременныхТаблиц,Символы.ПС+" ПОМЕСТИТЬ ГрафикиРаботРесурсов","")+"
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
					   |ГДЕ"
					   + ?(АктуальныеЗавершенные = 0, "", "
		               |	(ГрафикРаботыРесурсов.Объект.Состояние В(&мсвСостоянияСобытия)) И");
	КонецЕсли;
				   
	Запрос.УстановитьПараметр("ПереданныйПользователь", ?(ТипЗнч(Ресурс1) = Тип("СправочникСсылка.Пользователи"), Ресурс1, Ресурс2));
	
	Если ИспользоватьМенеджерВременныхТаблиц Тогда
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблицДляЗаполнения;
	КонецЕсли;
	
	//массив отбора состояний событий
	мсвСостояний = Новый Массив;
	Если АктуальныеЗавершенные = 1 Тогда
		мсвСостояний.Добавить(Перечисления.СостоянияСобытий.Запланировано);
	Иначе
		мсвСостояний.Добавить(Перечисления.СостоянияСобытий.Завершено);
		мсвСостояний.Добавить(Перечисления.СостоянияСобытий.Отменено);
	КонецЕсли;
	Запрос.УстановитьПараметр("мсвСостоянияСобытия", мсвСостояний);
	
	// заполняем условия по ресурсам и датам			   
	ТекстЗапроса = ТекстЗапроса + "
				   |( " + ?(НЕ УчитыватьПорядокРесурсов, "(", "") + "(ГрафикРаботыРесурсов.Ресурс1 = &Ресурс1" +
				   ?(Ресурс2 = Неопределено, "", " И ГрафикРаботыРесурсов.Ресурс2 = &Ресурс2") + ")";
				   
	Если НЕ УчитыватьПорядокРесурсов Тогда
		
		ТекстЗапроса = ТекстЗапроса + " ИЛИ 
					   |	(ГрафикРаботыРесурсов.Ресурс2 = &Ресурс1" +
					   ?(Ресурс2 = Неопределено, "", " И ГрафикРаботыРесурсов.Ресурс1 = &Ресурс2") + ") )";
	КонецЕсли;					   
	
	// если у нас полное соединение - то вставим условия по событиям
	Если ДелатьСоединение Тогда
		ТекстЗапроса = ТекстЗапроса + "
					   | ИЛИ 
					   |   ВложенныйЗапрос.Пользователь = " + ?(ТипЗнч(Ресурс1) = Тип("СправочникСсылка.Пользователи"),"&Ресурс1", "&Ресурс2") + ")";
	Иначе
		ТекстЗапроса = ТекстЗапроса + ")";
	КонецЕсли;		
				   
	Если ДатаНачала <> Неопределено Или ДатаОкончания <> Неопределено Тогда
		Если ДатаНачала <> Неопределено И ДатаОкончания <> Неопределено Тогда
			ТекстЗапроса = ТекстЗапроса + "
						   |И
						   | ((ГрафикРаботыРесурсов.Дата >= &ДатаНачала И ГрафикРаботыРесурсов.Дата <= &ДатаОкончания)"  + ?(ДелатьСоединение,"",")");
			Если ДелатьСоединение Тогда
				ТекстЗапроса = ТекстЗапроса + "
							   | ИЛИ 
							   |   (ВложенныйЗапрос.ДатаОповещения >= &ДатаНачала И ВложенныйЗапрос.ДатаОповещения<= &ДатаОкончания))";
			КонецЕсли;		
		ИначеЕсли ДатаНачала <> Неопределено Тогда
			ТекстЗапроса = ТекстЗапроса + "
						   |И 
						   | (ГрафикРаботыРесурсов.Дата >= &ДатаНачала" + ?(ДелатьСоединение,"",")");
			Если ДелатьСоединение Тогда
				ТекстЗапроса = ТекстЗапроса + "
							   | ИЛИ 
							   |   ВложенныйЗапрос.ДатаОповещения >= &ДатаНачала)";
			КонецЕсли;		
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
						   |И
						   | (ГрафикРаботыРесурсов.Дата <= &ДатаОкончания" + ?(ДелатьСоединение,"",")");
			Если ДелатьСоединение Тогда
				ТекстЗапроса = ТекстЗапроса + "
							   | ИЛИ 
							   |   ВложенныйЗапрос.ДатаОповещения <= &ДатаОкончания)";
			КонецЕсли;		
		КонецЕсли;	
	КонецЕсли;
	
	Если ИспользоватьМенеджерВременныхТаблиц Тогда
		Если ТипЗнч(СтруктураИндексов)=Тип("Структура") И СтруктураИндексов.Количество()>0 Тогда
			ТекстИндексируемыхПолей = "";
			Для Каждого ТекПоле Из СтруктураИндексов Цикл
				ТекстИндексируемыхПолей = ТекстИндексируемыхПолей + ?(ТекстИндексируемыхПолей="","",","+Символы.ПС)+ТекПоле.Ключ;
			КонецЦикла;
		ТекстЗапроса = ТекстЗапроса + "
				|ИНДЕКСИРОВАТЬ ПО
				|	"+ТекстИндексируемыхПолей;
        КонецЕсли;
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
				|УПОРЯДОЧИТЬ ПО
				|	Дата,
				|	НачалоРабочегоВремени";
	КонецЕсли;			   
	
	Если ГруппироватьПоДате Тогда
		ТекстЗапроса = ТекстЗапроса + "
					   |ИТОГИ ПО
					   |	Дата";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Если Ресурс1 <> Неопределено Тогда
		Запрос.УстановитьПараметр("Ресурс1", Ресурс1);
	КонецЕсли;	
	Если Ресурс2 <> Неопределено Тогда
		Запрос.УстановитьПараметр("Ресурс2", Ресурс2);
	КонецЕсли;
	Если ДатаНачала <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	КонецЕсли;
	Если ДатаОкончания <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	КонецЕсли;

	РезультатЗапроса = Запрос.Выполнить();
	Если ГруппироватьПоДате Тогда
		ВыборкаЗапроса = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ИначеЕсли ИспользоватьМенеджерВременныхТаблиц Тогда
		ВыборкаЗапроса = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Количество")[0]>0;
	Иначе	
		ВыборкаЗапроса = РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
	Возврат ВыборкаЗапроса;
КонецФункции

// Возвращает ближайшее незапланированное время по пользователю, его графику работы и дате нового события
//Если график не задан, то берется основной график компании, по умолчанию рабочий интервал - 10:00...19:00
//Если не указана дата, берется текущая дата
//Если не указан пользователь, берется текущий пользователь
//Если не указан квант времени, то 1 час (квант времени указывается в секундах!)
//Смысл - получить ближайшее незанятое, незапланированное по календарю время пользователя для текущей даты
//
// Параметры:
//
//  НаДату  	 - Дата   			- Дата начала выборки объектов
//  Ресурс1 	 – Ссылка 			- Значение первого ресурса для отбора
//  Ресурс2 	 – Ссылка 			- Значение второго ресурса для отбора
//	График 		 - ТаблицаЗначений 	- График по которому вычисляется продолжительность
//	КвантВремени - Число			- Квант времени
//
// Возвращаемое значение:
//	Дата - время нового события
//
Функция кпПолучитьВремяНовогоСобытия(НаДату = Неопределено, Ресурс1 = Неопределено, Ресурс2 = Неопределено,
									ГрафикРаботы = Неопределено, КвантВремени = Неопределено) Экспорт
									
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДата();
	КонецЕсли;
	НаДату = НачалоДня(НаДату);
	
	Если Ресурс1 = Неопределено Тогда
		Ресурс1 = ПараметрыСеанса.Пользователь;
	КонецЕсли;
	
	Если ГрафикРаботы = Неопределено Тогда
		ГрафикРаботы = Справочники.ГрафикиРаботы.ПустаяСсылка();
	КонецЕсли;
	//нужно ли по ресурсу получать график? хотя вроде бы логично..
	Если ТипЗнч(Ресурс1) = Тип("СправочникСсылка.Пользователи") Тогда
		ГрафикРаботы = Ресурс1.ГрафикРаботы;
	КонецЕсли;
	Если ГрафикРаботы.Пустая() Тогда
		ГрафикРаботы = Справочники.ГрафикиРаботы.ГрафикРаботыКомпании;
	КонецЕсли;
	
	Если КвантВремени = Неопределено Тогда
		КвантВремени = 3600;
	КонецЕсли;
	
	//запрос по календарю
	ТекстЗапроса = "ВЫБРАТЬ
	|	ГрафикРаботКалендарный.НачалоРабочегоВремени КАК Начало,
	|	ГрафикРаботКалендарный.КонецРабочегоВремени КАК Окончание
	|ИЗ
	|	РегистрСведений.ГрафикРаботКалендарный КАК ГрафикРаботКалендарный
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ГрафикРаботКалендарный.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&НаДату, ДЕНЬ)
	|	И ГрафикРаботКалендарный.График = &ГрафикРаботы
	|	И ГрафикРаботКалендарный.ВидИнтервала.РабочийИнтервал = Истина
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начало,
	|	Окончание";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("ГрафикРаботы", ГрафикРаботы);
	РезЗапросаКалендарь = Запрос.Выполнить();
	тблИнтерваловКалендаря = РезЗапросаКалендарь.Выгрузить();
	флЕстьКалендарь = (тблИнтерваловКалендаря.Количество() > 0);
	
	//запрос по ресурсам
	ТекстЗапроса = "ВЫБРАТЬ
	|	ГрафикРаботыРесурсов.НачалоРабочегоВремени КАК Начало,
	|	ГрафикРаботыРесурсов.КонецРабочегоВремени КАК Окончание
	|ИЗ
	|	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ГрафикРаботыРесурсов.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&НаДату, ДЕНЬ)
	|	И ГрафикРаботыРесурсов.Ресурс1 = &Ресурс1" + ?(обЗначениеНеЗаполнено(Ресурс2), "", "
	|	И ГрафикРаботыРесурсов.Ресурс2 = &Ресурс2") + "
	|УПОРЯДОЧИТЬ ПО
	|	Начало,
	|   Окончание";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("Ресурс1", Ресурс1);
	Запрос.УстановитьПараметр("Ресурс2", Ресурс2);
	РезЗапросаРесурсы = Запрос.Выполнить();
	тблИнтерваловПоРесурсам = РезЗапросаРесурсы.Выгрузить();
	флЕстьЗанятостьПоРесурсам = (тблИнтерваловПоРесурсам.Количество() > 0);
	 
	//календарь по умолчанию 10:00 - 19:00 (подставляется также в случае нерабочего дня)
	Если НЕ флЕстьКалендарь Тогда
		ИнтервалПоУмолчанию = тблИнтерваловКалендаря.Добавить();
		ИнтервалПоУмолчанию.Начало = '00010101100000';
		ИнтервалПоУмолчанию.Окончание = '00010101190000';
	КонецЕсли;
	
	//перебираем рабочие интервалы календаря
	флОтметкаНайдена = Ложь;
	НомИнтервала = 1;
	КоличИнтерваловКалендаря = тблИнтерваловКалендаря.Количество();
	Для Каждого ТекИнтервал Из тблИнтерваловКалендаря Цикл
		Если флЕстьЗанятостьПоРесурсам Тогда //если есть занятые интервалы
			СчВремя = ТекИнтервал.Начало;
			Пока СчВремя <= ТекИнтервал.Окончание Цикл //идем по календарю от начала до конца интервала с шагом в квант
				флИнтервалЗанят = Ложь;
				Для Каждого ТекИнтервалЗанятости Из тблИнтерваловПоРесурсам Цикл //перебираем занятые интервалы
					Если СчВремя < ТекИнтервалЗанятости.Начало
						ИЛИ СчВремя > ТекИнтервалЗанятости.Окончание
						ИЛИ (СчВремя = ТекИнтервалЗанятости.Окончание
							И ТекИнтервалЗанятости.Начало <> ТекИнтервалЗанятости.Окончание) Тогда //тек. отметка свободна
						
					Иначе //занято
						флИнтервалЗанят = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ флИнтервалЗанят Тогда
					Рез = НаДату + (СчВремя - '00010101000000');
					флОтметкаНайдена = Истина;
				КонецЕсли;
				
				Если флОтметкаНайдена Тогда
					Прервать;
				КонецЕсли;
					
				СчВремя = СчВремя + КвантВремени;
			КонецЦикла;
			
		Иначе //иначе берем начало 1 интервала календаря (при этом квант не учитывается)
			Рез = НаДату + (ТекИнтервал.Начало - '00010101000000');
			флОтметкаНайдена = Истина;
		КонецЕсли;
		
		Если флОтметкаНайдена Тогда
			Прервать;
		КонецЕсли;
		
		//проверяем, если не найдена отметка (всё уже расписано), то считаем отметкой конец последнего интервала
		Если КоличИнтерваловКалендаря = НомИнтервала И (НЕ флОтметкаНайдена) Тогда
			Рез = НаДату + (ТекИнтервал.Окончание - '00010101000000');
		КонецЕсли;
		
		НомИнтервала = НомИнтервала + 1;
	КонецЦикла;
		
	Возврат Рез;
КонецФункции // кпПолучитьВремяНовогоСобытия()

// Возвращает дату окончания графика по дате начала и продолжительности
//
// Параметры:
//  График 		      - ТаблицаЗначений - График по которому рассчитывается дата окончания
//  НачалоГрафика     - Дата			- Дата начала для расчета даты окончания
//  Продолжительность - Число           - Продолжительность в секундах для расчета даты окончания
//  ВидыИнтервалов    - Массив          - Массив либо элемент перечисления вид интервала,
//					    для которого рассчитывается дата окончания
//	ВидыДней          - Массив          - Массив либо элемент перечисления вид дня,
//					    для которого рассчитывается дата окончания
//	ДобавлятьОставшуюсяПродолжительность - Булево - Если Истина, то если продолжительность графика меньше заданной,
//													то дополнять оставшимся временем без учета графика (считать 24 часа в сутки)
//                                                  если Ложь, то если продолжительность графика меньше заданной,
//													то возвращать дату окончания графика
// Возвращаемое значение:	
//	ДатаОкончания - Дата окончания графика
Функция кпПолучитьДатуОкончанияГрафика(График, Знач НачалоГрафика, Знач Продолжительность, Знач ВидыИнтервалов = Неопределено, Знач ВидыДней = Неопределено, ДобавлятьОставшуюсяПродолжительность = Ложь) Экспорт
	ОкончаниеГрафика = НачалоГрафика;
	
	// если время работ 0 тогда возьмем наду начала
	Если Продолжительность = 0 Тогда
		Возврат ОкончаниеГрафика;
	КонецЕсли;
	
	// Проверка корректности параметров
	Если обЗначениеНеЗаполнено(График) ИЛИ обЗначениеНеЗаполнено(НачалоГрафика) Тогда
		ОкончаниеГрафика=НачалоГрафика+Продолжительность;
		Возврат ОкончаниеГрафика;
	КонецЕсли;
	
	//приводим виды интервалов и дней к массиву
	Если ТипЗнч(ВидыИнтервалов) <> Тип("Массив") Тогда
		ВидыИнтервалов_ = ВидыИнтервалов;
		ВидыИнтервалов = Новый Массив;
		Если ВидыИнтервалов_ = Неопределено Тогда //по умолчанию - все виды интервалов
			ВыборкаИнтервалов = Справочники.ВидыИнтервалов.Выбрать();
			Пока ВыборкаИнтервалов.Следующий() Цикл
				ВидыИнтервалов.Добавить(ВыборкаИнтервалов.Ссылка);
			КонецЦикла;
			ВидыИнтервалов.Добавить(Справочники.ВидыИнтервалов.ПустаяСсылка());
		Иначе
			ВидыИнтервалов.Добавить(ВидыИнтервалов_);
		КонецЕсли;
	КонецЕсли;
	Если ТипЗнч(ВидыДней) <> Тип("Массив") Тогда
		ВидыДней_ = ВидыДней;
		ВидыДней = Новый Массив;
		Если ВидыДней_ = Неопределено Тогда //по умолчанию используем все виды дней
			Для Каждого ТекЗначПеречисления Из Метаданные.Перечисления.ВидДня.ЗначенияПеречисления Цикл
				ВидыДней.Добавить(Перечисления.ВидДня[ТекЗначПеречисления.Имя]);
			КонецЦикла;
			ВидыДней.Добавить(Перечисления.ВидДня.ПустаяСсылка());
		Иначе
			ВидыДней.Добавить(ВидыДней_);
		КонецЕсли;
	КонецЕсли;
	
	Если График.Фиксированный Тогда	//заполняем по таблице смещения справочника ГрафикиРаботы
		Если График.Смещение.Количество() = 0 Тогда
			Если ДобавлятьОставшуюсяПродолжительность Тогда
				ОкончаниеГрафика = НачалоГрафика + Продолжительность;
			КонецЕсли;
			
			Возврат ОкончаниеГрафика;
		КонецЕсли;
		
		ПериодичностьГрафика = Строка(График.Периодичность);
		
		ДатаНачалаШаблонаОбязательна = (ПериодичностьГрафика = "Произвольный"
			ИЛИ ПериодичностьГрафика = "Неделя"
			ИЛИ ПериодичностьГрафика = "Декада");
			
		Если ДатаНачалаШаблонаОбязательна И График.ДатаНачала = '00010101' Тогда
			Возврат ОкончаниеГрафика;
		КонецЕсли;
		
		//расчет максимального смещения - максимальный номер дня в ТЧ Смещение шаблона
		Если ДатаНачалаШаблонаОбязательна Тогда
			НачальноеСмещение = (НачалоГрафика - График.ДатаНачала)/60/60/24;
			Если ПериодичностьГрафика = "Произвольный" Тогда
				ТаблицаСмещения = График.Смещение.Выгрузить();
				ТаблицаСмещения.Сортировать("НомерДня Убыв");
				МаксимальноеСмещение = ТаблицаСмещения[0].НомерДня + 1;
			ИначеЕсли ПериодичностьГрафика = "Неделя" Тогда
				МаксимальноеСмещение = 7;
			ИначеЕсли ПериодичностьГрафика = "Декада" Тогда
				МаксимальноеСмещение = 10;
			КонецЕсли;
		Иначе
			Если ПериодичностьГрафика = "Год" Тогда
				НачальноеСмещение = (НачалоГрафика - НачалоГода(НачалоГрафика))/60/60/24;
				МаксимальноеСмещение = 366;
			ИначеЕсли ПериодичностьГрафика = "Квартал" Тогда
				НачальноеСмещение = (НачалоГрафика - НачалоКвартала(НачалоГрафика))/60/60/24;
				МаксимальноеСмещение = 92;
			ИначеЕсли ПериодичностьГрафика = "Месяц" Тогда
				НачальноеСмещение = (НачалоГрафика - НачалоМесяца(НачалоГрафика))/60/60/24;
				МаксимальноеСмещение = 31;
			КонецЕсли;
		КонецЕсли;
		
		НачальноеСмещение = НачальноеСмещение - МаксимальноеСмещение * Цел(НачальноеСмещение/МаксимальноеСмещение);
		Если НачальноеСмещение < 0 Тогда
			НачальноеСмещение = НачальноеСмещение + МаксимальноеСмещение;
		КонецЕсли;
		
		МаксимальноеСмещение = МаксимальноеСмещение - 1;
		
		ТекущееСмещение = НачальноеСмещение;
		ПеременнаяРазницаДней = 0;
		
		// проверим корректность заполнения графика
		КорректныеСтрокиГрафика = График.Смещение.Количество();
		Для Каждого СтрокаГрафика Из График.Смещение Цикл
			// если заполнена смена
			Если НЕ СтрокаГрафика.Смена.Пустая() И СтрокаГрафика.Смена.Интервалы.Количество() > 0 Тогда
				// проверяем все интервалы
				КорректныеСтрокиГрафика = КорректныеСтрокиГрафика - 1;
				Для Каждого СтрокаИнтервала Из СтрокаГрафика.Смена.Интервалы Цикл
					Если (ВидыИнтервалов.Найти(СтрокаИнтервала.ВидИнтервала) <> Неопределено) Тогда
						КорректныеСтрокиГрафика = КорректныеСтрокиГрафика + 1;
						Если СтрокаИнтервала.КонецРабочегоВремени - СтрокаИнтервала.НачалоРабочегоВремени <= 0 Тогда
							КорректныеСтрокиГрафика = КорректныеСтрокиГрафика - 1;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если СтрокаГрафика.КонецРабочегоВремени - СтрокаГрафика.НачалоРабочегоВремени <= 0 Тогда
					КорректныеСтрокиГрафика = КорректныеСтрокиГрафика - 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// если есть рабочее время в графике - выполним цикл, если нет - прибавим к текущей дате
		Если КорректныеСтрокиГрафика > 0 Тогда
			Пока Истина Цикл
				ДатаЦикла = НачалоГрафика + (ПеременнаяРазницаДней*60*60*24);
				ВремяЦикла = ('00010101' + (ДатаЦикла - НачалоДня(ДатаЦикла))); //время, прошедшее с начала дня цикла (заданное начальное время)
				
				//корректировка тек. смещения под исключения (високосный год, длина месяцев квартала и т.п.)
				//добавляем разницу между макс. колич. дней в месяце (31) и текущим количеством дней в предыдущем
				//месяце (перепрыгиваем доп. число строк)
				ЭтоВисокосныйГод = (ДеньГода(КонецГода(ДатаЦикла)) = 366);
				Если ПериодичностьГрафика = "Год" Тогда
					Если (НЕ ЭтоВисокосныйГод) И ((Месяц(ДатаЦикла)=3 И День(ДатаЦикла)=1)
						ИЛИ (ПеременнаяРазницаДней = 0 И Месяц(ДатаЦикла)>=3 И День(ДатаЦикла)>=1)) Тогда
						ТекущееСмещение = ТекущееСмещение + 1;
					КонецЕсли;
					
				ИначеЕсли ПериодичностьГрафика = "Квартал" Тогда
					//Если Месяц(ДатаЦикла)=3 И День(ДатаЦикла)=1 Тогда //1 марта
					//	Если ЭтоВисокосныйГод Тогда
					//		ТекущееСмещение = ТекущееСмещение + 2;
					//	Иначе
					//		ТекущееСмещение = ТекущееСмещение + 3;
					//	КонецЕсли;
					//ИначеЕсли Месяц(ДатаЦикла)=5 И День(ДатаЦикла)=1 Тогда //1 мая
					//	ТекущееСмещение = ТекущееСмещение + 1;
					//ИначеЕсли Месяц(ДатаЦикла)=12 И День(ДатаЦикла)=1 Тогда //1 декабря
					//	ТекущееСмещение = ТекущееСмещение + 1;
					//КонецЕсли;
					ТекущееСмещение = (НачалоДня(ДатаЦикла) - НачалоКвартала(ДатаЦикла))/24/3600;
					
				ИначеЕсли ПериодичностьГрафика = "Месяц" Тогда
					ТекущееСмещение = День(ДатаЦикла)-1;
					
				КонецЕсли;
				
				СтрокаГрафика = График.Смещение.Найти(Цел(ТекущееСмещение), "НомерДня");
				
				Если СтрокаГрафика <> Неопределено Тогда
					Если (ВидыДней.Найти(СтрокаГрафика.ВидДня) <> Неопределено) Тогда
						Если НЕ СтрокаГрафика.Смена.Пустая() И СтрокаГрафика.Смена.Интервалы.Количество() > 0 Тогда // по интервалам смены
							Для Каждого ИнтервалСмены Из СтрокаГрафика.Смена.Интервалы Цикл
								Если (ВидыИнтервалов.Найти(ИнтервалСмены.ВидИнтервала) <> Неопределено) Тогда
									ОкончаниеГрафика = НачалоДня(ДатаЦикла) + (ИнтервалСмены.НачалоРабочегоВремени - '00010101');
									СтрокаГрафикаПродолжительность = ИнтервалСмены.Продолжительность - '00010101';
									
									Если ПеременнаяРазницаДней = 0 Тогда
										ПродолжительностьУжеПрошло = 0;
										Если (ВремяЦикла >= ИнтервалСмены.НачалоРабочегоВремени) И (ВремяЦикла <= ИнтервалСмены.КонецРабочегоВремени) Тогда
											ПродолжительностьУжеПрошло = (ВремяЦикла - ИнтервалСмены.НачалоРабочегоВремени);
										ИначеЕсли ВремяЦикла > ИнтервалСмены.КонецРабочегоВремени Тогда
											ПродолжительностьУжеПрошло = СтрокаГрафикаПродолжительность;
										КонецЕсли;
										ОкончаниеГрафика = Макс(ОкончаниеГрафика, НачалоГрафика);
										СтрокаГрафикаПродолжительность = СтрокаГрафикаПродолжительность - ПродолжительностьУжеПрошло;
									КонецЕсли;
									
									СтрокаГрафикаПродолжительность = Мин(СтрокаГрафикаПродолжительность, Продолжительность);
									ОкончаниеГрафика = ОкончаниеГрафика + СтрокаГрафикаПродолжительность;
									
									Продолжительность = Продолжительность - СтрокаГрафикаПродолжительность;
									Если Продолжительность = 0 Тогда
										Прервать;
									КонецЕсли;
								КонецЕсли;
							КонецЦикла;
							
						Иначе // по интервалам смещения
							ЭтоНочнаяСмена = ((СтрокаГрафика.КонецРабочегоВремени - СтрокаГрафика.НачалоРабочегоВремени) < 0); //признак ночной смены
							ОкончаниеГрафика = НачалоДня(ДатаЦикла) + (СтрокаГрафика.НачалоРабочегоВремени - '00010101');
							СтрокаГрафикаПродолжительность = СтрокаГрафика.Продолжительность - '00010101';
							Если ПеременнаяРазницаДней = 0 Тогда
								ПродолжительностьУжеПрошло = 0;
								Если ЭтоНочнаяСмена Тогда
									Если ВремяЦикла >= СтрокаГрафика.НачалоРабочегоВремени Тогда
										ПродолжительностьУжеПрошло = (ВремяЦикла - СтрокаГрафика.НачалоРабочегоВремени);
									ИначеЕсли ВремяЦикла <= СтрокаГрафика.КонецРабочегоВремени Тогда
										ПродолжительностьУжеПрошло = (СтрокаГрафика.КонецРабочегоВремени - ВремяЦикла);
									КонецЕсли;
								Иначе
									Если (ВремяЦикла >= СтрокаГрафика.НачалоРабочегоВремени) И (ВремяЦикла <= СтрокаГрафика.КонецРабочегоВремени) Тогда
										ПродолжительностьУжеПрошло = (ВремяЦикла - СтрокаГрафика.НачалоРабочегоВремени);
									ИначеЕсли ВремяЦикла > СтрокаГрафика.КонецРабочегоВремени Тогда
										ПродолжительностьУжеПрошло = СтрокаГрафикаПродолжительность;
									КонецЕсли;
								КонецЕсли;
								
								ОкончаниеГрафика = Макс(ОкончаниеГрафика, НачалоГрафика);
								
								СтрокаГрафикаПродолжительность = СтрокаГрафикаПродолжительность - ПродолжительностьУжеПрошло;
							КонецЕсли;
							
							СтрокаГрафикаПродолжительность = Мин(СтрокаГрафикаПродолжительность, Продолжительность);
							ОкончаниеГрафика = ОкончаниеГрафика + СтрокаГрафикаПродолжительность;
							
							Продолжительность = Продолжительность - СтрокаГрафикаПродолжительность;
							Если Продолжительность = 0 Тогда
								Прервать;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если Продолжительность = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если ТекущееСмещение >= МаксимальноеСмещение Тогда
					ТекущееСмещение = ТекущееСмещение-МаксимальноеСмещение;
				Иначе
					ТекущееСмещение = ТекущееСмещение + 1;
				КонецЕсли;
				
				ПеременнаяРазницаДней = ПеременнаяРазницаДней + 1;
			КонецЦикла;
		Иначе
			ОкончаниеГрафика = НачалоГрафика + Продолжительность;
			Сообщить("Возможно, график работы цеха не заполнен или заполнен некорректно.", СтатусСообщения.Информация);
		КонецЕсли;
	
	Иначе // Не фиксированный график: таблицу получаем из регистра в цикле порциями по месяцу
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ГрафикРаботКалендарный.Дата,
		|	ГрафикРаботКалендарный.Продолжительность,
		|	ГрафикРаботКалендарный.НачалоРабочегоВремени,
		|	ГрафикРаботКалендарный.КонецРабочегоВремени
		|ИЗ
		|	РегистрСведений.ГрафикРаботКалендарный КАК ГрафикРаботКалендарный
		|ГДЕ
		|	ГрафикРаботКалендарный.График = &График
		|	И ГрафикРаботКалендарный.ВидДня В (&ВидыДней)
		|	" + ?(График.СменаПоУмолчанию.Пустая(), "", "И ГрафикРаботКалендарный.ВидИнтервала В (&ВидыИнтервалов)
		|") + "	И (ДОБАВИТЬКДАТЕ(ГрафикРаботКалендарный.Дата, СЕКУНДА, ЧАС(ГрафикРаботКалендарный.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботКалендарный.КонецРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботКалендарный.КонецРабочегоВремени))) >= &ДатаНачала
		|УПОРЯДОЧИТЬ ПО
		|	ГрафикРаботКалендарный.Дата,
		|	ГрафикРаботКалендарный.НачалоРабочегоВремени");
		Запрос.УстановитьПараметр("График", График);
		Запрос.УстановитьПараметр("ВидыИнтервалов", ВидыИнтервалов);
		Запрос.УстановитьПараметр("ВидыДней", ВидыДней);
		Запрос.УстановитьПараметр("ДатаНачала", НачалоГрафика);
		тблГрафика = Запрос.Выполнить().Выгрузить();
		
		Для каждого СтрокаГрафика Из тблГрафика Цикл
			Если (СтрокаГрафика.Дата + (СтрокаГрафика.НачалоРабочегоВремени-'00010101')) < НачалоГрафика Тогда
				НачалоРабочегоВремени = НачалоГрафика - НачалоДня(НачалоГрафика);
				СтрокаГрафика.НачалоРабочегоВремени = (СтрокаГрафика.Дата + НачалоРабочегоВремени);
				СтрокаГрафика.Продолжительность = '00010101' + (СтрокаГрафика.КонецРабочегоВремени - СтрокаГрафика.НачалоРабочегоВремени);
			КонецЕсли; 
			ВремяВИнтервале = (СтрокаГрафика.Продолжительность - '00010101');
			ВремяВИнтервале = Мин(ВремяВИнтервале, Продолжительность);
			ОкончаниеГрафика = СтрокаГрафика.Дата + (СтрокаГрафика.НачалоРабочегоВремени + ВремяВИнтервале - '00010101');
			Продолжительность = Продолжительность - ВремяВИнтервале;
			Если Продолжительность = 0 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		//Если график "закончился", то прибавим оставшееся время
		Если ДобавлятьОставшуюсяПродолжительность Тогда
			ОкончаниеГрафика = ОкончаниеГрафика + Продолжительность;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОкончаниеГрафика;
КонецФункции

Процедура УстановитьТипСклад(Элементы) Экспорт
	ОписаниеТипа = ns_ОбщегоНазначенияВызовСервера.ПолучитьТипСклад(); 
	Элементы.Склад.ОграничениеТипа = ОписаниеТипа;
	Элементы.Склад.ВыбиратьТип = Ложь;
КонецПроцедуры

Процедура ОбработкаВыбора(Элементы, ВыбранноеЗначение, ИсточникВыбора, ИмяСправочника) Экспорт
	Если ТипЗнч(ИсточникВыбора) = Тип("ФормаКлиентскогоПриложения") Тогда
		Если ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ФормаОтметкиПоДокументу" Тогда
			ns_ОбщегоНазначенияВызовСервера.УстановитьВыгружатьПоДокументуНаСервере(ВыбранноеЗначение, ИмяСправочника);
		КонецЕсли; 
	КонецЕсли; 	
	Элементы.Список.Обновить();
КонецПроцедуры

Процедура УстановитьВыгружать(Элементы, Значение = Истина, ИмяСправочника) Экспорт
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	ns_ОбщегоНазначенияВызовСервера.УстановитьВыгружатьНаСервере(ВыделенныеСтроки, Значение, ИмяСправочника);
	Элементы.Список.Обновить();
КонецПроцедуры

Процедура ПоказатьТолькоВыгружаемые(Элементы, Список) Экспорт
	
	ОтборВыгружать = Неопределено;
	ПолеВыгружать = Новый ПолеКомпоновкиДанных("Выгружать");

	Отбор = Список.КомпоновщикНастроек.Настройки.Отбор;
	Если Отбор.Элементы.Количество() > 0 Тогда
		Для каждого ЭлементОтбора Из Отбор.Элементы Цикл
			Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
				ЭлементОтбора.ЛевоеЗначение = ПолеВыгружать Тогда
					ОтборВыгружать = ЭлементОтбора;
					Прервать;
			КонецЕсли; 	
		КонецЦикла; 
	Иначе
		Отбор = Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
		Для каждого ЭлементОтбора Из Отбор.Элементы Цикл
			Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
				ЭлементОтбора.ЛевоеЗначение = ПолеВыгружать Тогда
					ОтборВыгружать = ЭлементОтбора;
					Прервать;
			КонецЕсли; 	
		КонецЦикла; 
	КонецЕсли; 
	
	Если ОтборВыгружать = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОтборВыгружать.ПравоеЗначение = Истина;
	ОтборВыгружать.Использование = НЕ ОтборВыгружать.Использование;
	
	Элементы.ФормаПоказатьТолькоВыгружаемые.Пометка = ОтборВыгружать.Использование;
	
	Если ОтборВыгружать.Использование Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	Иначе
		Элементы.Список.Отображение = ОтображениеТаблицы.ИерархическийСписок;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработатьКомандуОткрытияФормы(СсылкаНаОбъект, ИмяСправочника, ПараметрыВыполненияКоманды) Экспорт 
	КлючФормы = ns_ОбщегоНазначенияВызовСервера.ПолучитьКлючФормы(СсылкаНаОбъект, ИмяСправочника);
	
	Если КлючФормы = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ПараметрыФормы = Новый Структура("Ключ", КлючФормы);

	ИмяФормы = "Справочник." + ИмяСправочника + ".ФормаОбъекта";
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
КонецПроцедуры
 
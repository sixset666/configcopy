/////////////////////////////////////////////////////////////////////////////////////////////////////
// ЗАЩИЩЕННЫЕ ФУНКЦИИ КЛИЕНТСКОГО КОНТЕКСТА
/////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик оповещения.
//
// Параметры:
//  Результат     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ОбработчикПродолжения - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
Процедура ОбработатьЗакрытиеФормыСостоянияСистемыЗащиты(Результат, ОбработчикПродолжения) Экспорт
	Отказ = ?(Результат = Неопределено,Истина,Результат);
	
	Если Отказ Тогда
		ОписаниеОшибки = "";
		ЛицензированиеСервер.ЗавершениеРаботыСистемыЛицензирования(ОписаниеОшибки);
		ЗавершитьРаботуСистемы();
	Иначе
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения, ИСТИНА);
	КонецЕсли;
	
КонецПроцедуры

// Эта процедура выполняется после старта системы защиты в обычном приложении
//
// Параметры:
//  РезультатЗапуска     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
Процедура ПослеСтартаСистемыЗащитыОбычноеПриложение(РезультатЗапуска, ДополнительныеПараметры) Экспорт
	//
	// Начальная инициализация рабочих переменных	
	ОбнаруженПервыйЗапуск			= Ложь;
	НеобходимоОбновлениеБазыДанных	= Ложь;
	ПараметрыКоманднойСтроки		= ПараметрЗапуска;
	ОбработкаСтартСистемы			= Обработки.СтартСистемы.Создать();
	
	ПраваСуперПользователя = (ПравоДоступа("Администрирование", Метаданные) И ПравоДоступа("МонопольныйРежим", Метаданные) 
	 И ПравоДоступа("Использование",    Метаданные.Обработки.ОбновлениеИнформационнойБазы)) ИЛИ ПравоДоступа("ОбновлениеКонфигурацииБазыДанных", Метаданные);
	
	Если НЕ ОбработкаСтартСистемы.ПервоначальнаяИнициализация(ПраваСуперПользователя,ОбнаруженПервыйЗапуск,НеобходимоОбновлениеБазыДанных) Тогда
		ЗавершитьРаботуСистемы(Ложь); Возврат;
	КонецЕсли;
	
	// Обработаем параметры запуска "срочных" команд
	Если ПраваСуперПользователя И сфОбработатьПараметрЗапуска(ПараметрыКоманднойСтроки,Истина) Тогда
		Возврат; // Параметры обработаны значит все сделано - сразу и выходим
	КонецЕсли;
	
	// Проверка необходимости и возможности обновить информационную базу
	Если ОбнаруженПервыйЗапуск ИЛИ НеобходимоОбновлениеБазыДанных Тогда
		Если ОбнаруженПервыйЗапуск Тогда
			ТекстСообщения ="  ВНИМАНИЕ! Обнаружен первый запуск программы!
							|Необходимо произвести первоначальное заполнение базы";
		Иначе
			ТекстСообщения = "  ВНИМАНИЕ! Обнаружено изменение конфигурации!
							|Необходимо произвести обновление информационной базы";
		КонецЕсли;
		Если ПраваСуперПользователя Тогда
			Если (ПараметрыСеанса.Пользователь<>Справочники.Пользователи.Робот) И Вопрос(ТекстСообщения+"
						|
					 	| Желаете выполнить эту операцию в настоящий момент?
						|
						|(При ответе ""Нет"" работа программы будет завершена)",РежимДиалогаВопрос.ДаНет,25,КодВозвратаДиалога.Да)=КодВозвратаДиалога.Нет Тогда
				ЗавершитьРаботуСистемы(Ложь); Возврат;	// Отказались обновлять - завершаем работу
			КонецЕсли;
		Иначе
			Предупреждение(ТекстСообщения+"
						|
						|Ваших прав доступа недостаточно для выполнения операции.
						|Сообщите об этой ситуации администратору базы данных.
						|
						|Сейчас Ваш сеанс будет завершен автоматически, извините.",25);
				ЗавершитьРаботуСистемы(Ложь); Возврат;	// Нет прав администратора - однозначно завершаем работу
		КонецЕсли;
	КонецЕсли;
	
	// Выполним обновление базы данных в дочернем узле
	Если (НеобходимоОбновлениеБазыДанных И НЕ ОбнаруженПервыйЗапуск И ЗначениеЗаполнено(ПланыОбмена.ГлавныйУзел()))
		ИЛИ Найти(Врег(ПараметрыКоманднойСтроки), Врег("UpdateIB")) > 0 
		ИЛИ Найти(Врег(ПараметрыКоманднойСтроки), Врег("SendExchangeMessage")) > 0 Тогда
		ОбработкаСтартСистемы.ЗавершитьОбновлениеБазыДочернегоУзла(ПараметрыКоманднойСтроки);
	Иначе
		// Отключим проверки и сообщения на время обновления
		Если НЕ ЗначениеЗаполнено(глПрава) Тогда // Если еще не инициализированы
			Состояние("Получение прав и настроек текущего пользователя...");
			глПрава = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
		КонецЕсли;
	КонецЕсли;
	
	// Выполним обновление по полной программе (первый запуск или "корневая" БД)
	Если ОбнаруженПервыйЗапуск ИЛИ НеобходимоОбновлениеБазыДанных Тогда
		Если НЕ ОбработкаСтартСистемы.ВыполнитьОбновлениеИнформационнойБазы() Тогда
			ЗавершитьРаботуСистемы(Ложь); Возврат;	// Если не получилось, то все 
		КонецЕсли;
	КонецЕсли;
	
	//anton//bizteh
	// Проверка заполнения констант
	//Если НЕ ОбработкаСтартСистемы.ВыполнитьПроверкуЗаполненияКонстант(ПраваСуперПользователя,ОбнаруженПервыйЗапуск) Тогда
	//	ЗавершитьРаботуСистемы(Ложь); Возврат;		// Если не получилось, то завершаем работу
	//КонецЕсли;
	//anton
	
	// Подключаем функции внешних компонент
	Если НЕ ОбработкаСтартСистемы.ИнициализацияФункцийВнешнихКомпонент() Тогда
		ЗавершитьРаботуСистемы(Ложь); Возврат;		// Если не получилось, то завершаем работу
	КонецЕсли;
	Если глЗавершениеРаботыСистемы Тогда
    	Возврат;
	КонецЕсли; 
	
	// Обработаем параметры запуска "дополнительных" команд
	Если ПраваСуперПользователя И сфОбработатьПараметрЗапуска(ПараметрыКоманднойСтроки,Ложь) Тогда
		Возврат; // Параметры обработаны значит все сделано - сразу и выходим
	КонецЕсли;
	
	// Для выполнения регламентных операций иногда нужно просить пользователей выйти из базы данных
	сфУстановитьКонтрольРежимаЗавершенияРаботыПользователей();
	
	// Почистим данные о необходимости обновления прав и настроек (это уже выполнилось)
	// причем необходимо удалить только те соединения текущего пользователя, которые уже не активны
	УдалитьДанныеСеансаОбработчикаЗаданий(,,3,Истина);
	ПодключитьОбработчикОжидания("глОбновитьПраваИНастройки", 45); 
	
	Если ПараметрыСеанса.Пользователь<>Справочники.Пользователи.Робот Тогда
		// С Роботом не нужно производить никакого интерактивного взаимодействия (спрашивать и т.п.)
		
		// Заполняем рабочие структуры данных
		Если НЕ ОбработкаСтартСистемы.ПроверкаЗаполненияРабочихСтруктурДанных(ОбнаруженПервыйЗапуск,НеобходимоОбновлениеБазыДанных) Тогда
			ЗавершитьРаботуСистемы(Ложь); Возврат;		// Если не получилось, то завершаем работу
		КонецЕсли;
		
		//anton//bizteh
		// !!!_Переход на ставку НДС 20%
		//Если НЕ РаботаСоСтавкамиНДСКлиентСервер.ПроверкаОбИзмененииСтавкиНДС() Тогда
		//	ЗавершитьРаботуСистемы(Ложь);
		//	Возврат;
		//КонецЕсли;
		//anton
		
		// Получим значение права "РаботаСФронтомКассира"
		Если обПраво("РаботаСФронтомКассира",глПрава)=Перечисления.РаботаСФронтомКассира.ТолькоСФронтом Тогда
			РаботаТолькоСФронтомКассира = Истина;
		Иначе
			РаботаТолькоСФронтомКассира = Ложь;
		КонецЕсли;
		
		РаботатьТолькоСАРМомСотрудника = Ложь;
		
		// получим значение права работать только с АРМ-ом сотрудника цеха
		Если НЕ РаботаТолькоСФронтомКассира Тогда
			Если зфЗащищенныеФункцииСервер.ПроверитьБитМаски(1) Тогда
				РаботатьТолькоСАРМомСотрудника = обПраво("УРВ_РаботатьТолькоСАРМомСотрудникаЦеха",глПрава);
			КонецЕсли;
		КонецЕсли;
		
		// Пересчитаем итоги регистров если это нужно
		ОпределитьНеобходимостьПерестановкиГраницыИтогов();
		
		// Проверим наличие файлов с данными о документах, обработаем восстановление документов
		МассивФайлов = НайтиФайлы(ПараметрыСеанса.Компьютер.ЛокальныйКаталог, "*.dct");
		GUIDКонфиграции = СтрЗаменить(Сред(ЗначениеВСтрокуВнутр(Метаданные),6,36),"-","");
		Если МассивФайлов.Количество()<>0 Тогда
			// сначала проверим, а есть ли файлы для нашего пользователя
			ЕстьМоиФайлы=Ложь; КодПользователя = СокрЛП(ПараметрыСеанса.Пользователь.Код);
			Для Каждого ТекФайл Из МассивФайлов Цикл
				Если Лев(ТекФайл.Имя,32)=GUIDКонфиграции И Найти(ТекФайл.Имя,"_"+КодПользователя+".")>0 Тогда
					ЕстьМоиФайлы=Истина; Прервать;
				КонецЕсли;
			КонецЦикла;
			
			//спросим пользователя, что ему делать с этими файлами
			Если ЕстьМоиФайлы Тогда
				Если Вопрос("В локальном каталоге присутствуют несохраненные файлы-документов.
					|Восстановить эти документы? (При отказе ВСЕ файлы будут удалены!)", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
					//ответ: не восстанавливать
					Для Каждого ТекФайл Из МассивФайлов Цикл
						Если Найти(ТекФайл.Имя,"_"+КодПользователя+".")>0 Тогда
							// удаляем файл
							УдалитьФайлы(ТекФайл.ПолноеИмя);
						КонецЕсли;
					КонецЦикла;
				Иначе
					//ответ: восстановить
					Для каждого ТекФайл Из МассивФайлов Цикл
						// пропустим не свои файлы
						Если Найти(ТекФайл.Имя,"_"+КодПользователя+".")=0 Тогда Продолжить; КонецЕсли;
						//получим структуру файла
						Попытка
							СтруктураФайла = ЗначениеИзФайла(ТекФайл.ПолноеИмя);
						Исключение
							Сообщить("Ошибка считывания файла: " + ОписаниеОшибки());
							Прервать;
						КонецПопытки;
						//попытаемся найти документ в базе
						Попытка
							ТекДокСсылка = ЗначениеИзСтрокиВнутр(СтруктураФайла.Ссылка);
						Исключение
							ТекДокСсылка = Документы[СтруктураФайла.ВидДокумента].ПустаяССылка();
						КонецПопытки;
		 				Если ТекДокСсылка.Пустая() Тогда 
							//если не найден - создадим новый
							ТекДокумент = Документы[СтруктураФайла.ВидДокумента].СоздатьДокумент();
						Иначе
							//если найден - получим из базы существующий
							ТекДокумент = ТекДокСсылка.ПолучитьОбъект();
						КонецЕсли;
						Если обЗначениеНеЗаполнено(ТекДокумент) Тогда
							Продолжить;
						КонецЕсли; 
										
						// получим коллекции реквизитов шапки и табличных частей документа
						ДокументРеквизитыШапки = Метаданные.Документы[СтруктураФайла.ВидДокумента].Реквизиты;
						ДокументТабличныеЧасти = Метаданные.Документы[СтруктураФайла.ВидДокумента].ТабличныеЧасти;
			
						//перенесем реквизиты документа из структуры файла в текущий объект
						ТекДокумент.Номер = СтруктураФайла.Номер;
						ТекДокумент.Дата  = СтруктураФайла.Дата;
						Для каждого ТекРеквизитШапки Из ДокументРеквизитыШапки Цикл
							Если СтруктураФайла.Свойство(ТекРеквизитШапки.Имя) Тогда
								ТекДокумент[ТекРеквизитШапки.Имя] = СтруктураФайла[ТекРеквизитШапки.Имя];
							КонецЕсли;
						КонецЦикла;
						Для каждого ТекТабличнаяЧасть Из ДокументТабличныеЧасти Цикл
							Если СтруктураФайла.Свойство(ТекТабличнаяЧасть.Имя) Тогда
								ТекТаблицаЗначений = СтруктураФайла[ТекТабличнаяЧасть.Имя];
								ТекДокумент[ТекТабличнаяЧасть.Имя].Загрузить(ТекТаблицаЗначений);
							КонецЕсли;
						КонецЦикла;
						
						// откроем форму сохраненного документа
						ФормаДокумента = ТекДокумент.ПолучитьФорму("ФормаДокумента");
						ФормаДокумента.Открыть();
						ФормаДокумента.Модифицированность = Истина;
						
						// удаляем файл
						УдалитьФайлы(ТекФайл.ПолноеИмя);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
		// Запуск фронта кассира в модальном режиме
		Если РаботаТолькоСФронтомКассира Тогда
			ФронтКассира=Обработки.ФронтКассира.ПолучитьФорму();
			ФронтКассира.ОткрытьМодально();
			ЗавершитьРаботуСистемы(Ложь);
			Возврат;
		КонецЕсли;
		
		// запуск АРМа сотрудника цеха
		Если РаботатьТолькоСАРМомСотрудника Тогда
			АрмСотрудника = Обработки.АРМСотрудникЦеха.ПолучитьФорму("ИнтерфейсСотрудникЦеха");
			АрмСотрудника.ОткрытьМодально();
			ЗавершитьРаботуСистемы(Ложь);
			Возврат;
		КонецЕсли;
		
		// Приветствие при старте системы
		Если обПраво("ПоказыватьПриветствиеПриСтарте",глПрава) 
			И Не РаботаТолькоСФронтомКассира
			И (ПараметрыСеанса.Пользователь<>Справочники.Пользователи.Робот) Тогда
			// Приветствие пользователя
			Пользователь = ПараметрыСеанса.Пользователь;
			Если обЗначениеНеЗаполнено(Пользователь.Сотрудник) Тогда
				ИмяПользователя = СокрЛП(Пользователь.Наименование);
			Иначе
				ИмяПользователя = СокрЛП(Пользователь.Сотрудник.Наименование);
			КонецЕсли;
			Предупреждение("Здравствуйте уважаемый пользователь """+ИмяПользователя+"""
						   |Система готова к работе.", 2);
		КонецЕсли;
		
		// Проверим нужно ли запустить пусковую панель
		Если обПраво("ОткрыватьПриВходеАРМ",глПрава) Тогда
			Обработки.ФункциональнаяПанель.ПолучитьФорму().Открыть();
		КонецЕсли;
		
	КонецЕсли; // Если НЕ Робот
	
	// SMS4B +
	ЗапуститьРоботаРеглЗаданий();
	// SMS4B -

	ОбработкаПланировщикаЗадач(Перечисления.ПериодыЗапускаЗадачПланировщика.ПриЗапускеСистемы);
	ПодключитьОбработчикОжидания("глОбработкаПланировщикаЗадач",10);
	
	глПроверитьПодключениеНапоминания();
	
	// создадим таблицу учетных записей автоматического транспорта электронной почты
	глУчетныеЗаписиАвтоматическогоТранспортаПисем = Новый ТаблицаЗначений();
	глУчетныеЗаписиАвтоматическогоТранспортаПисем.Колонки.Добавить("УчетнаяЗапись");
	глУчетныеЗаписиАвтоматическогоТранспортаПисем.Колонки.Добавить("ИнтервалОбновления");
	глУчетныеЗаписиАвтоматическогоТранспортаПисем.Колонки.Добавить("АдресЭлектроннойПочты");
	глУчетныеЗаписиАвтоматическогоТранспортаПисем.Колонки.Добавить("Действие");
	глУчетныеЗаписиАвтоматическогоТранспортаПисем.Колонки.Добавить("ВремяПоследнегоПолучения");
	эпПодключитьАвтоматическийТранспортПочты(глУчетныеЗаписиАвтоматическогоТранспортаПисем);
	
	глИзбранное=Обработки.Избранное.Создать();
	
	//+СофтФон
	CRMУстановитьЗначенияПоУмолчанию(сфпСофтФонПроСервер.сфпТекущийПользователь(), глКодГорода, глКодСтраны,
		глКоличествоХранимыхЦифрТелефона, глПрефиксВыходаВГород, глПрефиксВыходаНаМежгород, глМаксимальнаяДлинаВнутреннегоНомера);
	глОбработаноВнешнееСобытие = Ложь;
	Если сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпИспользоватьСофтФон") Тогда
		ТекАТС = Константы.CRM_ИспользуемыйСофтФон.Получить();
		Если ТекАТС = Перечисления.CRM_ИспользуемыйСофтФон.СофтФон3 Тогда
			сфпСофтФонПроКлиент.сфпПодключитьСофтФон();

		ИначеЕсли ТекАТС = Перечисления.CRM_ИспользуемыйСофтФон.СофтФонПроф Тогда
			Попытка
				РарусСофтФонПроф = Обработки.CRM_СофтФонПроф.Создать();
				РарусСофтФонПроф.ПодключитьСофтФон();
			Исключение
				Сообщить("Ошибка подключения " + ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	//-СофтФон
	
	// Помощник продаж +
	атСервер.ИнициализироватьКодСостояния();
	// Помощник продаж -
КонецПроцедуры

// Эта процедура выполняется после успешного старта системы лицензирования
//
// Параметры:
//  РезультатЗапуска     - Произвольный - Результат выполнения операции в подчиненной форме.
//  ДополнительныеПараметры - Произвольный - Значение, которое было указано при создании объекта описания оповещения.
//
Процедура ПослеСтартаСистемыЗащиты(РезультатЗапуска, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатЗапуска=ИСТИНА Тогда
		ЗавершитьРаботуСистемы();
	КонецЕсли;
	
	// прочий код
	
КонецПроцедуры

// Обработчик при начале работы системы.
//
// Параметры:
//  ОбработчикПродолжения - ОписаниеОповещения - Обработчик продолжения выполнения обработки.
//
Процедура ПриНачалеРаботыСистемы(ОбработчикПродолжения) Экспорт
	
	#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
		
		СписокРешений = ЛицензированиеПоддержка.СписокРешений();
		РезультатыЗапуска = Новый Массив;
		РезультатыЗапускаПоВсемСерверам = Новый Массив;
		Ошибка = Ложь;
		
		АдресаСерверовЛицензирования = ЛицензированиеСервер.СписокАдресовСервераЛицензирования();
		
		КоличествоСерверов = АдресаСерверовЛицензирования.Количество();
		Сч = 1;
		Для Каждого АдресСервера Из АдресаСерверовЛицензирования Цикл
			
			ЛицензированиеСервер.УстановитьПараметрСеансаТекущийАдресСервераЛицензирования(АдресСервера);
			
			Для Каждого Решение Из СписокРешений Цикл
				ОписаниеОшибки = "";
				КодОшибки = 0;
				ЛицензированиеСервер.НачалоРаботыСистемыЛицензирования(Решение.Ключ, ОписаниеОшибки, КодОшибки);
				Если ОписаниеОшибки<>"" Тогда
					Ошибка = Истина;
				КонецЕсли;
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("ОписаниеОшибки",ОписаниеОшибки);
				ПараметрыФормы.Вставить("КодОшибки",КодОшибки);
				ПараметрыФормы.Вставить("ПрограммноеОткрытиеФормы",Истина);
				ПараметрыФормы.Вставить("ИмяОбработки", Решение.Ключ);
				ПараметрыФормы.Вставить("ИмяРешения", Решение.Значение);
				РезультатыЗапуска.Добавить(ПараметрыФормы); 
				РезультатыЗапускаПоВсемСерверам.Добавить(ПараметрыФормы);
			КонецЦикла;
			
			Если Ошибка Тогда // В случае нескольких серверов лицензирования - ошибка запуска на последнем сервере  
				
				Если ЛицензированиеСервер.ЕстьОшибкаСоединенияССервером(РезультатыЗапуска) И Сч < КоличествоСерверов Тогда
					// Если ошибка соединения с сервером, на сервере не найден ключ и список серверов не исчерпан, то пробуем
					// подключиться к другому серверу.
					Ошибка = Ложь;
					РезультатыЗапуска.Очистить();
					Сч = Сч + 1;
					Продолжить;
				КонецЕсли;
				
				ПараметрыФормы = Новый Структура("РезультатыЗапуска,ПрограммноеОткрытиеФормы", РезультатыЗапускаПоВсемСерверам, ИСТИНА);
				Оповещение = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСостоянияСистемыЗащиты", ЛицензированиеКлиентСобытия, ОбработчикПродолжения);
				ОткрытьФорму("Обработка.УправлениеЛицензированием.Форма.СостояниеСистемыЗащиты",ПараметрыФормы, , , , , Оповещение);
				
			Иначе
				ВыполнитьОбработкуОповещения(ОбработчикПродолжения, ИСТИНА);
				Прервать; // Прекратить перебор серверов
			КонецЕсли;
			
		КонецЦикла;
		
	#КонецЕсли
	
КонецПроцедуры

// Обработчик при начале работы системы (обычное приложение).
//
// Параметры:
//  ОбработчикПродолжения - ОписаниеОповещения - Обработчик продолжения выполнения обработки.
//
Процедура ПриНачалеРаботыСистемыОбычноеПриложение(ОбработчикПродолжения) Экспорт
	
	ОбработкаСтартСистемы = Обработки.СтартСистемы.Создать();
	ТребуетсяПерезапуск=Ложь;
	Если НЕ ОбработкаСтартСистемы.СоздатьПредопределенныхПользователейИнформационнойБазы(ТребуетсяПерезапуск) Тогда
		ЗавершитьРаботуСистемы(Ложь); Возврат;
	КонецЕсли;
	Если ТребуетсяПерезапуск Тогда
		ЗавершитьРаботуСистемы(Ложь); Возврат;
	КонецЕсли;
		
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		СписокРешений = ЛицензированиеПоддержка.СписокРешений();
		РезультатыЗапуска = Новый Массив;
		РезультатыЗапускаПоВсемСерверам = Новый Массив;
		Ошибка = Ложь;
		
		АдресаСерверовЛицензирования = ЛицензированиеСервер.СписокАдресовСервераЛицензирования();
		
		КоличествоСерверов = АдресаСерверовЛицензирования.Количество();
		Сч = 1;
		
		Для Каждого АдресСервера Из АдресаСерверовЛицензирования Цикл
			
			ЛицензированиеСервер.УстановитьПараметрСеансаТекущийАдресСервераЛицензирования(АдресСервера);
			
			Для Каждого Решение Из СписокРешений Цикл
				ОписаниеОшибки = "";
				КодОшибки = 0;
				
				// Проверка и разбор длинного имени
				ИмяОбработки = Решение.Ключ;
				ИмяМакетаХранилища = "";
				ИмяМакетаПараметровЛицензирования = "";
				КороткоеИмяОбработки = "";
				Если НЕ ЛицензированиеСервер.РазделитьИмяОбработки(ИмяОбработки, ИмяМакетаХранилища, ИмяМакетаПараметровЛицензирования, КороткоеИмяОбработки) Тогда
					ОписаниеОшибки = НСтр("ru = 'Неверный формат имени обработки:'") + Символы.ПС + ИмяОбработки;
					КодОшибки = 1;
					Возврат;
				КонецЕсли;	
				КлючПродукта = ИмяМакетаХранилища+"."+ИмяМакетаПараметровЛицензирования;
				
				
				Если НЕ ЛицензированиеКлиент.ВыполнитьПривязкуКлиента(КлючПродукта, ОписаниеОшибки) Тогда
					ПоказатьПредупреждение(,ОписаниеОшибки);
				КонецЕсли;
				
				ОписаниеОшибки = "";
				КодОшибки = 0;
				
				ЛицензированиеСервер.НачалоРаботыСистемыЛицензирования(Решение.Ключ, ОписаниеОшибки, КодОшибки);
				Если ОписаниеОшибки<>"" Тогда
					Ошибка = Истина;
				КонецЕсли;
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("ОписаниеОшибки",ОписаниеОшибки);
				ПараметрыФормы.Вставить("КодОшибки",КодОшибки);
				ПараметрыФормы.Вставить("ПрограммноеОткрытиеФормы",Истина);
				ПараметрыФормы.Вставить("ИмяОбработки", Решение.Ключ);
				ПараметрыФормы.Вставить("ИмяРешения", Решение.Значение);
				РезультатыЗапуска.Добавить(ПараметрыФормы); 
				РезультатыЗапускаПоВсемСерверам.Добавить(ПараметрыФормы);
			КонецЦикла;
			
			Если Ошибка Тогда // В случае нескольких серверов лицензирования - ошибка запуска на последнем сервере
				
				Если ЛицензированиеСервер.ЕстьОшибкаСоединенияССервером(РезультатыЗапуска) И Сч < КоличествоСерверов Тогда
					// Если ошибка соединения с сервером, на сервере не найден ключ и список серверов не исчерпан, то пробуем
					// подключиться к другому серверу.
					Ошибка = Ложь;
					РезультатыЗапуска.Очистить();
					Сч = Сч + 1;
					Продолжить;
				КонецЕсли;
				
				ПараметрыФормы = Новый Структура("РезультатыЗапуска,ПрограммноеОткрытиеФормы", РезультатыЗапускаПоВсемСерверам, ИСТИНА);
				Оповещение = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСостоянияСистемыЗащиты", ЛицензированиеКлиентСобытия, ОбработчикПродолжения);
				ОткрытьФорму("Обработка.УправлениеЛицензированием.Форма.СостояниеСистемыЗащиты",ПараметрыФормы, , , , , Оповещение);
				
			Иначе
				ВыполнитьОбработкуОповещения(ОбработчикПродолжения, ИСТИНА);
				Прервать; // Прекратить перебор серверов
			КонецЕсли;
			
		КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

//Процедура проверяет, есть ли необходимость пересчета регистров
Процедура ОпределитьНеобходимостьПерестановкиГраницыИтогов()
	
	ДеньМесяцаНачалаНапоминания = 5;
	ДеньМесяцаБезусловногоПересчета = 10;
	
	ТекущийДеньМесяца = День(ТекущаяДата());	
	НадоПересчитывать = Истина;
	
	Если ТекущийДеньМесяца<ДеньМесяцаНачалаНапоминания Тогда
		НадоПересчитывать = Ложь;
	Иначе
		Если ТекущийДеньМесяца<ДеньМесяцаБезусловногоПересчета Тогда
			ЕстьПользователиВБазе = Ложь;
			Попытка
				УстановитьМонопольныйРежим(Истина);
			Исключение
				ЕстьПользователиВБазе = Истина;
			КонецПопытки;
			Если Не ЕстьПользователиВБазе Тогда
				УстановитьМонопольныйРежим(Ложь);
			КонецЕсли;
			НадоПересчитывать = Не ЕстьПользователиВБазе;
		Иначе
			НадоПересчитывать = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не НадоПересчитывать Тогда
		Возврат;
	КонецЕсли;
	
	ПредпочтительнаяДатаРасчетаИтогов = НачалоМесяца(ТекущаяДата())-1;
	СписокРегистровКРасчету= Новый СписокЗначений;
	Для Каждого Регистр ИЗ РегистрыНакопления Цикл                               
		МетаданныеРегистра = Метаданные.НайтиПоТипу(Тип(Регистр));
		Если МетаданныеРегистра.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			Если ПравоДоступа("УправлениеИтогами", МетаданныеРегистра) И ПравоДоступа("Чтение", МетаданныеРегистра) Тогда
				Если Регистр.ПолучитьПериодРассчитанныхИтогов()<ПредпочтительнаяДатаРасчетаИтогов  Тогда
					СписокРегистровКРасчету.Добавить(Регистр, "Регистр накопления "+МетаданныеРегистра.Синоним);
				КонецЕсли;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;	
	
	Если СписокРегистровКРасчету.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Вопрос("Для повышения производительности рекомендуется выполнить"+Символы.ПС+
				"расчет итогов регистров по "+Формат(ПредпочтительнаяДатаРасчетаИтогов, "ДФ=дд.ММ.гггг" )+". Выполнить его сейчас?", 
			РежимДиалогаВопрос.ДаНет, 25 , КодВозвратаДиалога.Нет) <> КодВозвратаДиалога.Да Тогда
			Возврат;
	КонецЕсли;

	Для Каждого РегистрИзСписка Из СписокРегистровКРасчету Цикл
		РегистрИзСписка.Значение.УстановитьПериодРассчитанныхИтогов(ПредпочтительнаяДатаРасчетаИтогов );		
		МетаданныеРегистра = Метаданные.НайтиПоТипу(ТипЗнч(РегистрИзСписка.Значение));
		Если Метаданные.РегистрыНакопления.Содержит(МетаданныеРегистра) Тогда
			ТипРегистра = " накопления ";
		КонецЕсли;
		Сообщить("Итоги регистра"+ТипРегистра+""""+МетаданныеРегистра.Синоним+""""+" были рассчитаны по: "+Формат(ПредпочтительнаяДатаРасчетаИтогов, "ДФ=дд.ММ.гггг" ));
	КонецЦикла;
	
КонецПроцедуры


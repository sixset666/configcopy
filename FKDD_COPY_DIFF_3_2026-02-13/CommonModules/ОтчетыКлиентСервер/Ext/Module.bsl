
#Область Общие

Функция ПолучитьЗаголовокПоля(ДоступноеПоле) Экспорт
	
	ЗаголовокПоля = "";
	Если ДоступноеПоле.Родитель = Неопределено Тогда
		ЗаголовокПоля     = ДоступноеПоле.Заголовок;
	Иначе
		ЗаголовокРодителя = ДоступноеПоле.Родитель.Заголовок;
		ЗаголовокПоля     = СтрЗаменить(ДоступноеПоле.Заголовок, ЗаголовокРодителя+".", "")+ " (" + (ЗаголовокРодителя)+ ")";
	КонецЕсли;
	
	Возврат ЗаголовокПоля;
	
КонецФункции

// Формирует краткое представление ошибки.
//
// Параметры:
//   ИнформацияОбОшибке - ИнформацияОбОшибке - Информация об ошибке.
//
// Возвращаемое значение:
//   Строка - Краткое представление ошибки.
//
Функция КраткоеПредставлениеОшибкиФормированияОтчета(ИнформацияОбОшибке) Экспорт
	
	ОписаниеОшибки = ИнформацияОбОшибке.Описание;
	ПричинаОшибки = ИнформацияОбОшибке.Причина;
	Пока ПричинаОшибки <> Неопределено Цикл
		ОписаниеОшибки = ПричинаОшибки.Описание;
		ПричинаОшибки = ПричинаОшибки.Причина;
	КонецЦикла;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

#КонецОбласти


#Область Период_Отчета

Процедура УстановитьПометкуНаПериод(ФормаОтчета, ИмяКоманды) Экспорт
	
	Элементы = ФормаОтчета.Элементы;
	
	Для Каждого ТекущаяКоманда Из Элементы.ПодменюВариантовПериода.ПодчиненныеЭлементы Цикл
		Если ТекущаяКоманда.ИмяКоманды = ИмяКоманды Тогда
			ТекущаяКоманда.Пометка = Истина;
			Элементы.ПодменюВариантовПериода.Заголовок = ТекущаяКоманда.Заголовок;
		Иначе
			ТекущаяКоманда.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьВариантыПериодов(ФормаОтчета) Экспорт
	
	Элементы   = ФормаОтчета.Элементы;
	ВидПериода = ФормаОтчета.ВидПериода;
	
	СписокВыбора = Элементы.ВариантПериода.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если ВидПериода = "День" Тогда
		
		ФормаОтчета.КонецПериода  = КонецДня(ФормаОтчета.КонецПериода);
		ФормаОтчета.НачалоПериода = НачалоДня(ФормаОтчета.КонецПериода);
		
		СекундЗаДень  = (60*60*24);
		ТекущийПериод = ФормаОтчета.НачалоПериода - СекундЗаДень*4;
		
		Элементы.ВариантПериода.ФорматРедактирования = "ДФ=дд.ММ.гггг";
		
		Для Инд=0 По 8 Цикл
			СписокВыбора.Добавить(ТекущийПериод, Формат(ТекущийПериод, "ДФ=дд.ММ.гггг"));
			ТекущийПериод = ТекущийПериод+СекундЗаДень;
		КонецЦикла;
		
	ИначеЕсли ВидПериода = "Неделя" Тогда
		
		ФормаОтчета.КонецПериода   = КонецНедели(ФормаОтчета.КонецПериода);
		ФормаОтчета.НачалоПериода  = НачалоНедели(ФормаОтчета.КонецПериода);
		СекундЗаНеделю = (60*60*24)*7;
		ТекущийПериод  = ФормаОтчета.НачалоПериода - СекундЗаНеделю*4;
		
		Элементы.ВариантПериода.ФорматРедактирования = "ДФ='""с"" дд.ММ.гггг'";
		
		Для Инд=0 По 8 Цикл
			СписокВыбора.Добавить(ТекущийПериод, Формат(ТекущийПериод, "ДФ='""Неделя с"" дд.ММ.гггг'"));
			ТекущийПериод = ТекущийПериод+СекундЗаНеделю;
			
		КонецЦикла;
		
	ИначеЕсли ВидПериода = "Месяц" Тогда
		
		ФормаОтчета.КонецПериода  = КонецМесяца(ФормаОтчета.КонецПериода);
		ФормаОтчета.НачалоПериода = НачалоМесяца(ФормаОтчета.КонецПериода);
		ТекущийПериод = ДобавитьМесяц(ФормаОтчета.НачалоПериода, -4);
		
		Элементы.ВариантПериода.ФорматРедактирования = "ДФ='"""+Формат(ФормаОтчета.НачалоПериода, "ДФ='ММММ'")+""" гггг'";
		
		Для Инд=0 По 8 Цикл
			СписокВыбора.Добавить(ТекущийПериод, Формат(ТекущийПериод, "ДФ='ММММ гггг'"));
			ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 1);
		КонецЦикла;
		
	ИначеЕсли ВидПериода = "Квартал" Тогда
		
		ФормаОтчета.КонецПериода  = КонецКвартала(ФормаОтчета.КонецПериода);
		ФормаОтчета.НачалоПериода = НачалоКвартала(ФормаОтчета.КонецПериода);
		ТекущийПериод = ДобавитьМесяц(ФормаОтчета.НачалоПериода, -3*4);
		
		Элементы.ВариантПериода.ФорматРедактирования = "ДФ=' к ""кв."" гггг'";
		
		Для Инд=0 По 8 Цикл
			СписокВыбора.Добавить(ТекущийПериод, Формат(ТекущийПериод, "ДФ=' к ""квартал"" гггг'"));
			ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 3);
		КонецЦикла;
		
	ИначеЕсли ВидПериода = "Год" Тогда
		
		ФормаОтчета.КонецПериода  = КонецГода(ФормаОтчета.КонецПериода);
		ФормаОтчета.НачалоПериода = НачалоГода(ФормаОтчета.КонецПериода);
		ТекущийПериод = ДобавитьМесяц(ФормаОтчета.НачалоПериода, -12*4);
		
		Элементы.ВариантПериода.ФорматРедактирования = "ДФ='гггг'";
		
		Для Инд=0 По 8 Цикл
			СписокВыбора.Добавить(ТекущийПериод, Формат(ТекущийПериод, "ДФ='гггг'"));
			ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 12);
		КонецЦикла;
		
	КонецЕсли;
	
	ФормаОтчета.ВариантПериода = ФормаОтчета.НачалоПериода;
	
КонецПроцедуры

Процедура СместитьПериод(ФормаОтчета, Направление) Экспорт
	
	ВидПериода = ФормаОтчета.ВидПериода;
	
	Если ВидПериода = "День" Тогда
		
		СекундЗаДень  = (60*60*24);
		ФормаОтчета.КонецПериода = ФормаОтчета.КонецПериода + СекундЗаДень*Направление;
		
	ИначеЕсли ВидПериода = "Неделя" Тогда
		
		СекундЗаНеделю = (60*60*24)*7;
		ФормаОтчета.КонецПериода  = ФормаОтчета.КонецПериода + СекундЗаНеделю*Направление;
		
	ИначеЕсли ВидПериода = "Месяц" Тогда
		
		ФормаОтчета.КонецПериода = ДобавитьМесяц(ФормаОтчета.КонецПериода, 1*Направление);
		
	ИначеЕсли ВидПериода = "Квартал" Тогда
		
		ФормаОтчета.КонецПериода = ДобавитьМесяц(ФормаОтчета.КонецПериода, 3*Направление);
		
	ИначеЕсли ВидПериода = "Год" Тогда
		
		ФормаОтчета.КонецПериода = ДобавитьМесяц(ФормаОтчета.КонецПериода, 12*Направление);
		
	КонецЕсли;
	
	ОбновитьВариантыПериодов(ФормаОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ПолучитьПредставлениеЭлементаОтбора(ПредставлениеПоля, ВидСравнения)
	
	ПредставлениеСравнения = "";
	// Производим формирование секции выдов сравнения
	Если ВидСравнения=Неопределено ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.Равно ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.Содержит ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.ВИерархии ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.ВСписке ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
		// Формирование представлений для прямых видов условий производить не будем
		
	ИначеЕсли ВидСравнения=ВидСравненияКомпоновкиДанных.Больше Тогда
		ПредставлениеСравнения = "(>)";
	ИначеЕсли ВидСравнения=ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
		ПредставлениеСравнения = "(>=)";
	ИначеЕсли ВидСравнения=ВидСравненияКомпоновкиДанных.Меньше Тогда
		ПредставлениеСравнения = "(<)";
	ИначеЕсли ВидСравнения=ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
		ПредставлениеСравнения = "(<=)";
	ИначеЕсли ВидСравнения=ВидСравненияКомпоновкиДанных.НеРавно ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.НеВИерархии ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.НеВСписке ИЛИ ВидСравнения=ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		ПредставлениеСравнения = "(НЕ)";
	Иначе
		ПредставлениеСравнения = "("+Строка(ВидСравнения)+")";
	КонецЕсли;
	
	ПредставлениеСравнения = ПредставлениеПоля + ПредставлениеСравнения;
	
	// Возвращаем сформированное представление текущего элемента отбора
	Возврат ПредставлениеСравнения;
	
КонецФункции // ПолучитьПредставлениеЭлементаОтбора()

// Настройки формы отчета по умолчанию.
//
// Возвращаемое значение:
//   Структура - Настройки формы отчета.
//       
//       * ФормироватьСразу - Булево - Значение по умолчанию для флажка "Формировать сразу".
//           Когда флажок включен, то отчет будет формироваться:
//             - После открытия;
//             - После выбора пользовательских настроек;
//             - После выбора другого варианта отчета.
//       
//       * ВыводитьСуммуВыделенныхЯчеек - Булево - Если Истина, то в отчете будет выводиться поле автосуммы.
//       
//       * СоответствиеПериодичностиПараметров - Соответствие - Ограничение списка выбора полей "СтандартныйПериод".
//           ** Ключ - ПараметрКомпоновкиДанных - Имя параметра отчета, к которому применятся ограничение.
//           ** Значение - ПеречислениеСсылка.ДоступныеПериодыОтчета - Ограничение периода отчета "снизу".
//       
//       * Печать - Структура - Параметры печати табличного документа "по умолчанию".
//           ** ПолеСверху - Число - Отступ сверху при печати (в миллиметрах).
//           ** ПолеСлева  - Число - Отступ слева  при печати (в миллиметрах).
//           ** ПолеСнизу  - Число - Отступ снизу  при печати (в миллиметрах).
//           ** ПолеСправа - Число - Отступ справа при печати (в миллиметрах).
//           ** ОриентацияСтраницы - ОриентацияСтраницы - "Портрет" или "Ландшафт".
//           ** АвтоМасштаб - Булево - Автоматически подгонять масштаб под размер страницы.
//           ** МасштабПечати - Число - Масштаб изображения (в процентах).
//       
//       * События - Структура - События, для которых определены обработчики в модуле объекта отчета.
//           
//           ** ПриСозданииНаСервере - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//               //
//               // Параметры:
//               //   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//               //   Отказ - Передается из параметров обработчика "как есть".
//               //   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//               //
//               // См. также:
//               //   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//               //
//               Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ПередЗагрузкойВариантаНаСервере - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//               //
//               // Параметры:
//               //   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//               //   НовыеНастройкиКД - НастройкиКомпоновкиДанных - Настройки для загрузки в компоновщик настроек.
//               //
//               // См. также:
//               //   "Расширение управляемой формы для отчета.ПриЗагрузкеВариантаНаСервере" в синтакс-помощнике.
//               //
//               Процедура ПередЗагрузкойВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ПриЗагрузкеВариантаНаСервере - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//               //
//               // Параметры:
//               //   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//               //   НовыеНастройкиКД - НастройкиКомпоновкиДанных - Настройки для загрузки в компоновщик настроек.
//               //
//               // См. также:
//               //   "Расширение управляемой формы для отчета.ПриЗагрузкеВариантаНаСервере" в синтакс-помощнике.
//               //
//               Процедура ПриЗагрузкеВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ПриЗагрузкеПользовательскихНастроекНаСервере - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//               //
//               // Параметры:
//               //   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//               //   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных -
//               //       Пользовательские настройки для загрузки в компоновщик настроек.
//               //
//               // См. также:
//               //   "Расширение управляемой формы для отчета.ПриЗагрузкеПользовательскихНастроекНаСервере" в синтакс-помощнике.
//               //
//               Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ПередЗаполнениемПанелиБыстрыхНастроек - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается до перезаполнения панели настроек формы отчета.
//               //
//               // Параметры:
//               //   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//               //   ПараметрыЗаполнения - Структура - Параметры, которые будут загружены в отчет.
//               //
//               Процедура ПередЗаполнениемПанелиБыстрыхНастроек(Форма, ПараметрыЗаполнения) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ПослеЗаполненияПанелиБыстрыхНастроек - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается после перезаполнения панели настроек формы отчета.
//               //
//               // Параметры:
//               //   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//               //   ПараметрыЗаполнения - Структура - Параметры, которые будут загружены в отчет.
//               //
//               Процедура ПослеЗаполненияПанелиБыстрыхНастроек(Форма, ПараметрыЗаполнения) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** КонтекстныйВызовСервера - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Обработчик контекстного вызова сервера.
//               //   Позволяет выполнить контекстный вызов сервера когда это требуется из клиентского общего модуля.
//               //   Например, из ФормаОтчетаКлиентПереопределяемый.ОбработчикКоманды().
//               //
//               // Параметры:
//               //   Форма  - ФормаКлиентскогоПриложения
//               //   Ключ      - Строка    - Ключ операции, которую необходимо выполнить в контекстном вызове.
//               //   Параметры - Структура - Параметры вызова сервера.
//               //   Результат - Структура - Результат работы сервера, возвращается на клиент.
//               //
//               // См. также:
//               //   ОбщаяФорма.ФормаОтчета.ВыполнитьКонтекстныйВызовСервера().
//               //
//               Процедура КонтекстныйВызовСервера(Форма, Ключ, Параметры, Результат) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ПриОпределенииПараметровВыбора - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Вызывается в форме отчета перед выводом настройки.
//               //   Подробнее - см. ФормаОтчетаПереопределяемый.ПриОпределенииПараметровВыбора()
//               //
//               Процедура ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//           
//           ** ДополнитьСвязиОбъектовМетаданных - Булево - Если Истина, то в модуле объекта отчета
//               следует определить обработчик события по шаблону:
//               
//               // Дополнительные связи настроек этого отчета.
//               //   Подробнее - см. ФормаОтчетаПереопределяемый.ДополнитьСвязиОбъектовМетаданных()
//               //
//               Процедура ДополнитьСвязиОбъектовМетаданных(СвязиОбъектовМетаданных) Экспорт
//               	// Обработка события.
//               КонецПроцедуры
//
Функция ПолучитьНастройкиОтчетаПоУмолчанию() Экспорт
	
	//Печать = Новый Структура;
	//Печать.Вставить("ПолеСверху", 10);
	//Печать.Вставить("ПолеСлева", 10);
	//Печать.Вставить("ПолеСнизу", 10);
	//Печать.Вставить("ПолеСправа", 10);
	//Печать.Вставить("ОриентацияСтраницы", ОриентацияСтраницы.Портрет);
	//Печать.Вставить("АвтоМасштаб", Истина);
	//Печать.Вставить("МасштабПечати", Неопределено);
	
	События = Новый Структура;
	События.Вставить("ПриСозданииНаСервере",             Ложь);
	События.Вставить("ПриЗагрузкеВариантаНаСервере",     Ложь);
	События.Вставить("ПриОпределенииПараметровВыбора",   Ложь);
	События.Вставить("ДополнитьСвязиОбъектовМетаданных", Ложь);
	
	// Параметры формы
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "");
	ПараметрыФормы.Вставить("Расшифровка",                 Неопределено);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",     Ложь);
	ПараметрыФормы.Вставить("ОтчетСсылка",                 Неопределено);
	ПараметрыФормы.Вставить("ЭкспертнаяНастройка",         Ложь);
	
	// параметры отображения элементов формы
	ОтображениеФормы = Новый Структура;
	ОтображениеФормы.Вставить("ВидимостьГруппаОтборы",     Истина);
	ОтображениеФормы.Вставить("ВидимостьГруппаСтроки",     Истина);
	ОтображениеФормы.Вставить("ВидимостьГруппаПараметры",  Истина);
	ОтображениеФормы.Вставить("ВидимостьГруппаКолонки",    Истина);
	ОтображениеФормы.Вставить("ВидимостьГруппаПоказатели", Истина);
	ОтображениеФормы.Вставить("Расположение",              "Сверху");
	
	Настройки = Новый Структура;
	//Настройки.Вставить("Печать",       Печать);
	Настройки.Вставить("События",   События);
	Настройки.Вставить("Параметры", ПараметрыФормы);
	Настройки.Вставить("Видимость", ОтображениеФормы);
	
	Возврат Настройки;
	
КонецФункции

Процедура ВзвестиМодифицированностьОтчета(ФормаОтчета, УстановитьИзменения = Ложь, ТекстСообщения = "Настройки отчета были изменены. Необходимо переформировать отчет.") Экспорт
	
	Элементы = ФормаОтчета.Элементы;
	
	Если УстановитьИзменения Тогда
		Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния.Текст     = ТекстСообщения;
		Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния.Видимость = Истина;
		Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния.Картинка  = БиблиотекаКартинок.СтатусыСобытийЖурнала_Предупреждение;
	Иначе
		Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
		Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ДвухмернаяДиаграмма(ТипДиаграммыОтчета) Экспорт
	
	ОдномернаяДиаграмма = (ТипДиаграммыОтчета = "Круговая" ИЛИ 
			ТипДиаграммыОтчета = "КруговаяОбъемная" ИЛИ 
			ТипДиаграммыОтчета = "Измерительная" ИЛИ 
			ТипДиаграммыОтчета = "Воронка" ИЛИ 
			ТипДиаграммыОтчета = "ВоронкаОбъемная" ИЛИ 
			ТипДиаграммыОтчета = "ВоронкаНормированная" ИЛИ 
			ТипДиаграммыОтчета = "ВоронкаНормированнаяОбъемная");
	
	Возврат (НЕ ОдномернаяДиаграмма);
	
КонецФункции

Процедура ЗаполнитьНастройкиОтчета(ФормаОтчета, КоллекцияЭлементов, ОбъектНастройки, ДоступныеНастройки = Неопределено, РодительЭлементов = Неопределено) Экспорт
	
	ТипНастройки = ТипЗнч(ОбъектНастройки);
	ТипЭлементов = ТипЗнч(КоллекцияЭлементов);
	
	Если ТипНастройки = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов.Элементы.Очистить();
		
		Для Каждого ЭлементОтбора Из ОбъектНастройки Цикл
			
			Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				
				ГруппаОтборов = КоллекцияЭлементов.Добавить();
				ГруппаОтборов.Использование = ЭлементОтбора.Использование;
				ГруппаОтборов.ТипГруппы     = ЭлементОтбора.ТипГруппы;
				ГруппаОтборов.Папка         = Истина;
				
				ЗаполнитьНастройкиОтчета(ФормаОтчета, ГруппаОтборов.ПолучитьЭлементы(), ЭлементОтбора.Элементы, ДоступныеНастройки);
				
			Иначе
				
				ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ЭлементОтбора.ЛевоеЗначение);
				// Имя поля изменилось или поле было удалено.
				Если ДоступноеПоле = Неопределено Тогда
					ЗаголовокПоля = СокрЛП(ЭлементОтбора.ЛевоеЗначение);
				Иначе
					ЗаголовокПоля = ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПоле);
				КонецЕсли;
				
				НовыйОтбор = КоллекцияЭлементов.Добавить();
				НовыйОтбор.Использование  = ЭлементОтбора.Использование;
				НовыйОтбор.ЛевоеЗначение  = ЭлементОтбора.ЛевоеЗначение;
				НовыйОтбор.ВидСравнения   = ЭлементОтбора.ВидСравнения;
				НовыйОтбор.ПравоеЗначение = ЭлементОтбора.ПравоеЗначение;
				НовыйОтбор.Заголовок      = ПолучитьПредставлениеЭлементаОтбора(ЗаголовокПоля, ЭлементОтбора.ВидСравнения);
				НовыйОтбор.Папка          = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов.Очистить();
		
		Для Каждого ЭлементПараметров Из ОбъектНастройки Цикл
			
			ИмяПараметра = Строка(ЭлементПараметров.Параметр);
			
			Если ИмяПараметра = "ДатаНачала" ИЛИ ИмяПараметра = "НачалоПериода" Тогда
				ФормаОтчета.ЕстьНачалоПериода = ЭлементПараметров.Использование;
				Продолжить;
			КонецЕсли;
			
			Если ИмяПараметра = "ДатаОкончания" ИЛИ ИмяПараметра = "ДатаОкончанияГраница" ИЛИ ИмяПараметра = "Период" ИЛИ ИмяПараметра = "КонецПериода" Тогда
				ФормаОтчета.ЕстьКонецПериода = ЭлементПараметров.Использование;
				Продолжить;
			КонецЕсли;
			
			// Имя параметра изменилось или параметр был удален
			ДоступныйПараметр = ДоступныеНастройки.НайтиПараметр(ЭлементПараметров.Параметр);
			Если ДоступныйПараметр = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйПараметр = КоллекцияЭлементов.Добавить();
			НовыйПараметр.Параметр      = ЭлементПараметров.Параметр;
			НовыйПараметр.Значение      = ЭлементПараметров.Значение;
			НовыйПараметр.Использование = ЭлементПараметров.Использование;
			НовыйПараметр.Заголовок     = ДоступныйПараметр.Заголовок;
			
			Если ЭлементПараметров.ЗначенияВложенныхПараметров.Количество()>0 Тогда
				ЗаполнитьНастройкиОтчета(ФормаОтчета, НовыйПараметр.ПолучитьЭлементы(), ЭлементПараметров.ЗначенияВложенныхПараметров, ДоступныеНастройки);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияВыбранныхПолейКомпоновкиДанных") И (ТипЭлементов = Тип("ВыбранныеПоляКомпоновкиДанных") ИЛИ ТипЭлементов = Тип("ГруппаВыбранныхПолейКомпоновкиДанных")) Тогда
		
		//Если РодительЭлементов = Неопределено Тогда
		//	СложноеПредставление = Ложь;
		//Иначе
		//	СложноеПредставление = (РодительЭлементов.ПоляГруппировки.Элементы.Количество()>1);
		//КонецЕсли;
		
		КоллекцияЭлементов.Элементы.Очистить();
		
		ПредставлениеПолей = "";
		Для Каждого ЭлементПоказателей Из ОбъектНастройки Цикл
			ТипПоля = ТипЗнч(ЭлементПоказателей);
			
			ЗаголовокПоля = "";
			Если ТипПоля = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
				
				ЗаголовокПоля = ЭлементПоказателей.Заголовок;
				
				Если ЗаголовокПоля = "" Тогда
					
					Попытка
						// Если группа входит в состав доступных полей
						ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ЭлементПоказателей.Поле);
					Исключение
						ДоступноеПоле = Неопределено;
					КонецПопытки;
					
					// Имя параметра изменилось или параметр был удален.
					Если НЕ ДоступноеПоле = Неопределено Тогда
						ЗаголовокПоля = ДоступноеПоле.Заголовок;
					КонецЕсли;
				КонецЕсли;
				
				ГруппаПоказателей = КоллекцияЭлементов.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(ГруппаПоказателей, ЭлементПоказателей);
				ГруппаПоказателей.Заголовок = ЗаголовокПоля;
				
				ЗаполнитьНастройкиОтчета(ФормаОтчета, ГруппаПоказателей, ЭлементПоказателей.Элементы, ДоступныеНастройки);
				
			ИначеЕсли ТипПоля = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
				
				ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ЭлементПоказателей.Поле);
				// Имя поля изменилось или поле было удалено.
				Если ДоступноеПоле = Неопределено Тогда
					ЗаголовокПоля = СокрЛП(ЭлементПоказателей.Поле);
				Иначе
					ЗаголовокПоля = ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПоле);
				КонецЕсли;
				
				НовыйПоказатель = КоллекцияЭлементов.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовыйПоказатель, ЭлементПоказателей);
				
			КонецЕсли;
			
			ПредставлениеПолей = ПредставлениеПолей + ?(ПредставлениеПолей = "", "", ", ") + ЗаголовокПоля;
			
		КонецЦикла;
		
		Если НЕ РодительЭлементов = Неопределено Тогда
			РодительЭлементов.ПредставлениеПолей = ПредставлениеПолей;
		КонецЕсли;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияВыбранныхПолейКомпоновкиДанных") И ТипЭлементов = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		
		//Если РодительЭлементов = Неопределено Тогда
		//	СложноеПредставление = Ложь;
		//Иначе
		//	СложноеПредставление = (РодительЭлементов.ПоляГруппировки.Элементы.Количество()>1);
		//КонецЕсли;
		
		КоллекцияЭлементов.Очистить();
		
		ПредставлениеПолей = "";
		Для Каждого ЭлементПоказателей Из ОбъектНастройки Цикл
			ТипПоля = ТипЗнч(ЭлементПоказателей);
			
			ЗаголовокПоля = "";
			Если ТипПоля = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
				
				ЗаголовокПоля = ЭлементПоказателей.Заголовок;
				Если ЗаголовокПоля = "" Тогда
					Попытка
						// Если группа входит в состав доступных полей
						ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ЭлементПоказателей.Поле);
					Исключение
						ДоступноеПоле = Неопределено;
					КонецПопытки;
					// Имя параметра изменилось или параметр был удален.
					Если НЕ ДоступноеПоле = Неопределено Тогда
						ЗаголовокПоля = ДоступноеПоле.Заголовок;
					КонецЕсли;
				КонецЕсли;
				
				ГруппаПоказателей = КоллекцияЭлементов.Добавить();
				ЗаполнитьЗначенияСвойств(ГруппаПоказателей, ЭлементПоказателей);
				ГруппаПоказателей.Заголовок    = ЗаголовокПоля;
				ГруппаПоказателей.Папка        = Истина;
				ГруппаПоказателей.КартинкаПоля = БиблиотекаКартинок.СправочникГруппа;
				
				ЗаполнитьНастройкиОтчета(ФормаОтчета, ГруппаПоказателей.ПолучитьЭлементы(), ЭлементПоказателей.Элементы, ДоступныеНастройки);
				
			ИначеЕсли ТипПоля = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
				
				НовыйПоказатель = КоллекцияЭлементов.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйПоказатель, ЭлементПоказателей);
				НовыйПоказатель.Папка = Ложь;
				
				ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ЭлементПоказателей.Поле);
				// Имя поля изменилось или поле было удалено.
				Если ДоступноеПоле = Неопределено Тогда
					ЗаголовокПоля = СокрЛП(ЭлементПоказателей.Поле);
					НовыйПоказатель.Заголовок    = ЗаголовокПоля;
					НовыйПоказатель.КартинкаПоля = БиблиотекаКартинок.Нет;
				Иначе
					ЗаголовокПоля = ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПоле);
					НовыйПоказатель.Заголовок    = ЗаголовокПоля;
					НовыйПоказатель.КартинкаПоля = ?(ДоступноеПоле.Ресурс, БиблиотекаКартинок.Ресурс, БиблиотекаКартинок.Реквизит);
				КонецЕсли;
				
			КонецЕсли;
			
			ПредставлениеПолей = ПредставлениеПолей + ?(ПредставлениеПолей = "", "", ", ") + ЗаголовокПоля;
			
		КонецЦикла;
		
		Если НЕ РодительЭлементов = Неопределено Тогда
			РодительЭлементов.ПредставлениеПолей = ПредставлениеПолей;
		КонецЕсли;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияПолейГруппировкиКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов.Элементы.Очистить();
		
		ЗаголовокГруппировки = "";
		Для Каждого ПолеГруппировки Из ОбъектНастройки Цикл
			
			Если ТипЗнч(ПолеГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ПолеГруппировки.Поле);
			// Имя поля изменилось или поле было удалено.
			Если ДоступноеПоле = Неопределено Тогда
				ЗаголовокПоля = СокрЛП(ПолеГруппировки.Поле);
			Иначе
				ЗаголовокПоля = ?(ЗаголовокГруппировки = "", "", ", ") + ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПоле);
			КонецЕсли;
			
			НовоеПолеГруппировки = КоллекцияЭлементов.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовоеПолеГруппировки, ПолеГруппировки);
			
			
			ЗаголовокГруппировки = ЗаголовокГруппировки + ЗаголовокПоля + ?(ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы, "", " " + Строка(ПолеГруппировки.ТипГруппировки));
			
		КонецЦикла;
		
		Если НЕ РодительЭлементов = Неопределено Тогда
			РодительЭлементов.Заголовок = ?(ЗаголовокГруппировки = "", "Детальные записи", ЗаголовокГруппировки);
		КонецЕсли;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовПорядкаКомпоновкиДанных") Тогда
		
		//Если РодительЭлементов = Неопределено Тогда
		//	СложноеПредставление = Ложь;
		//Иначе
		//	СложноеПредставление = (РодительЭлементов.ПоляГруппировки.Элементы.Количество()>1);
		//КонецЕсли;
		
		КоллекцияЭлементов.Элементы.Очистить();
		
		// Порядок группировок
		ПредставлениеПорядка = "";
		Для Каждого ПолеПорядка Из ОбъектНастройки Цикл
			
			Если НЕ ТипЗнч(ПолеПорядка) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			ДоступноеПоле = ДоступныеНастройки.НайтиПоле(ПолеПорядка.Поле);
			// Имя поля изменилось или поле было удалено.
			Если ДоступноеПоле = Неопределено Тогда
				ЗаголовокПоля = СокрЛП(ПолеПорядка.Поле);
			Иначе
				ЗаголовокПоля = ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПоле);
			КонецЕсли;
			
			НовоеПолеПорядка = КоллекцияЭлементов.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовоеПолеПорядка, ПолеПорядка);
			
			ЗаголовокПоля = ?(ПредставлениеПорядка = "", "", ", ") + ЗаголовокПоля;
			
			ПредставлениеПорядка = ПредставлениеПорядка + ЗаголовокПоля + ?(ПолеПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр, " Возр", " Убыв");
			
		КонецЦикла;
		
		Если НЕ РодительЭлементов = Неопределено Тогда
			РодительЭлементов.ПредставлениеПорядка = ?(ПредставлениеПорядка = "", "Авто", ПредставлениеПорядка);
		КонецЕсли;
		
	ИначеЕсли ТипНастройки = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
		
		ЗаполнитьНастройкиОтчета(ФормаОтчета, КоллекцияЭлементов, ОбъектНастройки.Элементы, ОбъектНастройки.ДоступныеПоляВыбора);
		
	ИначеЕсли ТипНастройки = Тип("ОтборКомпоновкиДанных") Тогда
		
		ЗаполнитьНастройкиОтчета(ФормаОтчета, КоллекцияЭлементов, ОбъектНастройки.Элементы, ОбъектНастройки.ДоступныеПоляОтбора);
		
	ИначеЕсли ТипНастройки = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") ИЛИ 
			ТипНастройки = Тип("ЗначенияПараметровВыводаКомпоновкиДанных") ИЛИ 
			ТипНастройки = Тип("ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных") ИЛИ 
			ТипНастройки = Тип("ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных") ИЛИ 
			ТипНастройки = Тип("ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных") ИЛИ 
			ТипНастройки = Тип("ЗначенияПараметровВыводаДиаграммыКомпоновкиДанных") ИЛИ 
			ТипНастройки = Тип("ЗначенияПараметровВыводаТаблицыКомпоновкиДанных") Тогда
			
			ЗаполнитьНастройкиОтчета(ФормаОтчета, КоллекцияЭлементов, ОбъектНастройки.Элементы, ОбъектНастройки.ДоступныеПараметры);
			
			
	ИначеЕсли ТипНастройки = Тип("ДанныеФормыКоллекцияЭлементовДерева") И ТипЭлементов = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		
		КоллекцияЭлементов.Очистить();
		
		Для Каждого ТекущаяСтрока Из ОбъектНастройки Цикл
			
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
			ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяСтрока.ПолучитьЭлементы(), ТекущаяСтрока.ПолучитьЭлементы());
			
		КонецЦикла;
	
	ИначеЕсли ТипНастройки = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		
		КоллекцияЭлементов.Элементы.Очистить();
		
		Для Каждого ТекущаяСтрока Из ОбъектНастройки Цикл
			
			Если ТекущаяСтрока.Папка Тогда
				ГруппаПоказателей = КоллекцияЭлементов.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(ГруппаПоказателей, ТекущаяСтрока);
				ЗаполнитьНастройкиОтчета(ФормаОтчета, ГруппаПоказателей, ТекущаяСтрока.ПолучитьЭлементы(), ДоступныеНастройки);
			Иначе
				НовыйПоказатель = КоллекцияЭлементов.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовыйПоказатель, ТекущаяСтрока);
			КонецЕсли;
		
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияСтрокДереваЗначений") Тогда
		
		КоллекцияЭлементов.Очистить();
		
		Для Каждого ТекущаяСтрока Из ОбъектНастройки Цикл
			
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			Если НоваяСтрока.Свойство("КартинкаПоля") И НоваяСтрока.Свойство("Папка") Тогда
				НоваяСтрока.КартинкаПоля  = ?(НоваяСтрока.Папка, БиблиотекаКартинок.СправочникГруппа, БиблиотекаКартинок.Ресурс);
			КонецЕсли;
			
			ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяСтрока.ПолучитьЭлементы(), ТекущаяСтрока.Строки);
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("ТаблицаЗначений") Тогда
		
		Для Каждого ТекущаяСтрока Из ОбъектНастройки Цикл
			
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
	// Добавление группы ресурсов
	ИначеЕсли ТипНастройки = Тип("КоллекцияДоступныхПолейКомпоновкиДанных") Тогда
		
		Для Каждого ТекущаяСтрока Из ОбъектНастройки Цикл
			
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
			НоваяСтрока.Использование = Истина;
		
			Если ТекущаяСтрока.Папка Тогда
				ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяСтрока.ПолучитьЭлементы(), ТекущаяСтрока.Элементы);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиСКД(ОбъектНастройки, КоллекцияЭлементов = Неопределено) Экспорт
	
	ТипНастройки = ТипЗнч(ОбъектНастройки);
	ТипЭлементов = ТипЗнч(КоллекцияЭлементов);
	
	// Параметры
	Если ТипНастройки = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") И ТипЭлементов = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаПараметров Из КоллекцияЭлементов Цикл
			
			ПараметрСКД = ОбъектНастройки.Найти(СтрокаПараметров.Параметр);
			Если ПараметрСКД = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПараметрСКД.Использование = СтрокаПараметров.Использование;
			ПараметрСКД.Значение      = СтрокаПараметров.Значение;
			
			Если ПараметрСКД.ЗначенияВложенныхПараметров.Количество()>0 Тогда
				ЗаполнитьНастройкиСКД(ПараметрСКД.ЗначенияВложенныхПараметров, СтрокаПараметров.ЗначенияВложенныхПараметров);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаПараметров Из КоллекцияЭлементов Цикл
			
			ПараметрСКД = ОбъектНастройки.Найти(СтрокаПараметров.Параметр);
			Если ПараметрСКД = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПараметрСКД.Использование = СтрокаПараметров.Использование;
			ПараметрСКД.Значение      = СтрокаПараметров.Значение;
			
			Если ПараметрСКД.ЗначенияВложенныхПараметров.Количество()>0 Тогда
				ЗаполнитьНастройкиСКД(ПараметрСКД.ЗначенияВложенныхПараметров, СтрокаПараметров.ПолучитьЭлементы());
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияВыбранныхПолейКомпоновкиДанных") И ТипЭлементов = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		
		ОбъектНастройки.Очистить();
		
		Для Каждого СтрокаПоказателей Из КоллекцияЭлементов Цикл
			
			Если СтрокаПоказателей.Папка Тогда
				
				ГруппаПоказателей = ОбъектНастройки.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
				ГруппаПоказателей.Использование = СтрокаПоказателей.Использование;
				ГруппаПоказателей.Заголовок     = СтрокаПоказателей.Заголовок;
				ГруппаПоказателей.Расположение  = СтрокаПоказателей.Расположение;
				
				//Если ТипЗнч(СтрокаПоказателей) = Тип("СтрокаДереваЗначений") Тогда
				//	ПодчиненныеЭлементы = СтрокаПоказателей.Строки;
				//Иначе
				//	ПодчиненныеЭлементы = СтрокаПоказателей.ПолучитьЭлементы();
				//КонецЕсли;
				
				ЗаполнитьНастройкиСКД(ГруппаПоказателей.Элементы, СтрокаПоказателей.ПолучитьЭлементы());
			Иначе
				ЭлементПоказателей = ОбъектНастройки.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				//ЗаполнитьЗначенияСвойств(ЭлементПоказателей, СтрокаПоказателей);
				ЭлементПоказателей.Использование  = СтрокаПоказателей.Использование;
				ЭлементПоказателей.Поле           = СтрокаПоказателей.Поле;
			КонецЕсли;
			
		КонецЦикла;
		
	// Показатели и реквизиты
	ИначеЕсли ТипНастройки = Тип("КоллекцияВыбранныхПолейКомпоновкиДанных") Тогда
		
		ОбъектНастройки.Очистить();
		
		Для Каждого СтрокаПоказателей Из КоллекцияЭлементов Цикл
			
			Если ТипЗнч(СтрокаПоказателей) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(СтрокаПоказателей) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
				ГруппаПоказателей = ОбъектНастройки.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(ГруппаПоказателей, СтрокаПоказателей);
				//ГруппаПоказателей.Использование = СтрокаПоказателей.Использование;
				//ГруппаПоказателей.Заголовок     = СтрокаПоказателей.Заголовок;
				//ГруппаПоказателей.Расположение  = СтрокаПоказателей.Расположение;
				
				//Если ТипЗнч(СтрокаПоказателей) = Тип("СтрокаДереваЗначений") Тогда
				//	ПодчиненныеЭлементы = СтрокаПоказателей.Строки;
				//Иначе
				//	ПодчиненныеЭлементы = СтрокаПоказателей.ПолучитьЭлементы();
				//КонецЕсли;
				
				ЗаполнитьНастройкиСКД(ГруппаПоказателей.Элементы, СтрокаПоказателей.Элементы);
			Иначе
				ЭлементПоказателей = ОбъектНастройки.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(ЭлементПоказателей, СтрокаПоказателей);
				//ЭлементПоказателей.Использование  = СтрокаПоказателей.Использование;
				//ЭлементПоказателей.Поле           = СтрокаПоказателей.Поле;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияПолейГруппировкиКомпоновкиДанных") Тогда
		
		// Состав группировок строки
		Для Каждого ПолеГруппировки Из КоллекцияЭлементов Цикл
			
			ПолеГруппировкиСКД = ОбъектНастройки.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировкиСКД.Использование  = ПолеГруппировки.Использование;
			ПолеГруппировкиСКД.Поле           = ПолеГруппировки.Поле;
			ПолеГруппировкиСКД.ТипГруппировки = ПолеГруппировки.ТипГруппировки;
			ПолеГруппировкиСКД.ТипДополнения  = ПолеГруппировки.ТипДополнения;
			ПолеГруппировкиСКД.НачалоПериода  = ПолеГруппировки.НачалоПериода;
			ПолеГруппировкиСКД.КонецПериода   = ПолеГруппировки.КонецПериода;
			
		КонецЦикла;
		
	// Копирование отбора
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") И ТипЗнч(КоллекцияЭлементов) = ТипНастройки Тогда
		
		ТипЭлементыОтбора = Тип("ЭлементОтбораКомпоновкиДанных");
		
		Для Каждого СтрокаОтбора Из КоллекцияЭлементов Цикл
			
			Если ТипЗнч(СтрокаОтбора) = ТипЭлементыОтбора Тогда
				ЭлементОтбора = ОбъектНастройки.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.Использование  = СтрокаОтбора.Использование;
				ЭлементОтбора.ЛевоеЗначение  = СтрокаОтбора.ЛевоеЗначение;
				ЭлементОтбора.ВидСравнения   = СтрокаОтбора.ВидСравнения;
				ЭлементОтбора.ПравоеЗначение = СтрокаОтбора.ПравоеЗначение;
			Иначе
				ГруппаОтбора = ОбъектНастройки.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтбора.Использование = СтрокаОтбора.Использование;
				ГруппаОтбора.ТипГруппы     = СтрокаОтбора.ТипГруппы;
				
			Попытка
				ЗаполнитьНастройкиСКД(ГруппаОтбора.Элементы, СтрокаОтбора.ПолучитьЭлементы());
			Исключение
				ЗаполнитьНастройкиСКД(ГруппаОтбора.Элементы, СтрокаОтбора.Элементы);
			КонецПопытки;
			КонецЕсли;
			
		КонецЦикла;
		
	// Отбор
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаОтбора Из КоллекцияЭлементов Цикл
			
			Если СтрокаОтбора.Папка Тогда
				ГруппаОтбора = ОбъектНастройки.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтбора.Использование = СтрокаОтбора.Использование;
				ГруппаОтбора.ТипГруппы     = СтрокаОтбора.ТипГруппы;
				
				ЗаполнитьНастройкиСКД(ГруппаОтбора.Элементы, СтрокаОтбора.ПолучитьЭлементы());
			Иначе
				ЭлементОтбора = ОбъектНастройки.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.Использование  = СтрокаОтбора.Использование;
				ЭлементОтбора.ЛевоеЗначение  = СтрокаОтбора.ЛевоеЗначение;
				ЭлементОтбора.ВидСравнения   = СтрокаОтбора.ВидСравнения;
				ЭлементОтбора.ПравоеЗначение = СтрокаОтбора.ПравоеЗначение;
			КонецЕсли;
			
		КонецЦикла;
		
	// Порядок
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовПорядкаКомпоновкиДанных") Тогда
		
		// Автоматический порядок
		ПолеПорядкаСКД = ОбъектНастройки.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		ПолеПорядкаСКД.Использование = Истина;
		
		// Порядок группировок
		Для Каждого ПолеПорядка Из КоллекцияЭлементов Цикл
			
			Если ТипЗнч(ПолеПорядка) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			ПолеПорядкаСКД = ОбъектНастройки.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
			ПолеПорядкаСКД.Использование     = ПолеПорядка.Использование;
			ПолеПорядкаСКД.Поле              = ПолеПорядка.Поле;
			ПолеПорядкаСКД.ТипУпорядочивания = ПолеПорядка.ТипУпорядочивания;
			
		КонецЦикла;
		
	// Элементы таблицы отчета
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		
		ВетвьСтруктуры = ОбъектНастройки;
		Для Каждого СтрокаГруппировки Из КоллекцияЭлементов Цикл
			
			// Нельзя добавлять не используемые группировки иначе СКД отсечет всю цепочку
			//Если НЕ СтрокаГруппировки.Использование Тогда
			//	Продолжить;
			//КонецЕсли;
			
			ЭлементСтруктуры = ВетвьСтруктуры.Добавить();
			ЭлементСтруктуры.Использование = СтрокаГруппировки.Использование;
			
			ЗаполнитьНастройкиСКД(ЭлементСтруктуры, СтрокаГруппировки);
			
			ВетвьСтруктуры = ЭлементСтруктуры.Структура;
			
		КонецЦикла;
		
	// Элементы диаграммы
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаГруппировки Из КоллекцияЭлементов Цикл
			
			// Нельзя добавлять не используемые группировки иначе СКД отсечет всю цепочку
			//Если НЕ СтрокаГруппировки.Использование Тогда
			//	Продолжить;
			//КонецЕсли;
			
			ЭлементСтруктуры = ОбъектНастройки.Добавить();
			ЭлементСтруктуры.Использование = СтрокаГруппировки.Использование;
			
			ЗаполнитьНастройкиСКД(ЭлементСтруктуры, СтрокаГруппировки);
			
			Прервать;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияОформляемыхПолейКомпоновкиДанных") Тогда
		
		Для Каждого ПолеОформления Из КоллекцияЭлементов Цикл
			
			Если НЕ ПолеОформления.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйЭлемент = ОбъектНастройки.Добавить();
			НовыйЭлемент.Использование = ПолеОформления.Использование;
			НовыйЭлемент.Поле          = ПолеОформления.Поле;
			
		КонецЦикла;
		
	// Группировка таблицы отчета
	ИначеЕсли ТипНастройки = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		
		// Заполнение полей группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.ПоляГруппировки.Элементы, КоллекцияЭлементов.ПоляГруппировки.Элементы);
		
		// Заполнение Реквизитов группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.Выбор.Элементы, КоллекцияЭлементов.Выбор.Элементы);
		
		// Автоматическое поле
		ПолеРеквизитаСКД = ОбъектНастройки.Выбор.Элементы.Вставить(0, Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		ПолеРеквизитаСКД.Использование = Истина;
		
		// Заполнение Порядка группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.Порядок.Элементы, КоллекцияЭлементов.Порядок.Элементы);
		
		// Заполнение Параметров вывода группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.ПараметрыВывода.Элементы, КоллекцияЭлементов.ПараметрыВывода.ПолучитьЭлементы());
		
		// Заполнение Отборов группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.Отбор.Элементы, КоллекцияЭлементов.Отбор.Элементы);
		
		// Заполнение Условного оформления группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.УсловноеОформление.Элементы, КоллекцияЭлементов.УсловноеОформление.Элементы);
		
	// Группировка диаграммы отчета
	ИначеЕсли ТипНастройки = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		// Заполнение полей группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.ПоляГруппировки.Элементы, КоллекцияЭлементов.ПоляГруппировки.Элементы);
		
		// Автоматическое поле
		ПолеРеквизитаСКД = ОбъектНастройки.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		ПолеРеквизитаСКД.Использование = Истина;
		
		// Заполнение Порядка группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.Порядок.Элементы, КоллекцияЭлементов.Порядок.Элементы);
		
		// Заполнение Параметров вывода группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.ПараметрыВывода.Элементы, КоллекцияЭлементов.ПараметрыВывода.Элементы);
		
		// Заполнение Отборов группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.Отбор.Элементы, КоллекцияЭлементов.Отбор.Элементы);
		
		// Заполнение Условного оформления группировки
		ЗаполнитьНастройкиСКД(ОбъектНастройки.УсловноеОформление.Элементы, КоллекцияЭлементов.УсловноеОформление.Элементы);
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
		
		ОбъектНастройки.Очистить();
		
		// Заполнение Условного оформления группировки
		Для Каждого ЭлементОформления Из КоллекцияЭлементов Цикл
			
			//Если Не ЭлементОформления.Использование Тогда
			//	Продолжить;
			//КонецЕсли;
			
			НовыйЭлементОформления = ОбъектНастройки.Добавить();
			
			ЗаполнитьЗначенияСвойств(НовыйЭлементОформления, ЭлементОформления);
			
			ЗаполнитьНастройкиСКД(НовыйЭлементОформления.Отбор.Элементы, ЭлементОформления.Отбор.Элементы);
			ЗаполнитьНастройкиСКД(НовыйЭлементОформления.Поля.Элементы, ЭлементОформления.Поля.Элементы);
			ЗаполнитьНастройкиСКД(НовыйЭлементОформления.Оформление.Элементы, ЭлементОформления.Оформление.Элементы);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Процедуры и функции работы с объектами регистров


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Возвращает структуру измерений регистра 
//
// Параметры: 
//  РегистрСписок - РегистрСведенийСписок
//  ТипРегистра - Строка - определяет тип регистра
//
// Возвращаемое значение:
//  СтруктураИзмерений - Структура - структуру измерений регистра (имя + описание типов значений)
//
Функция ргПолучитьИзмеренияРегистра(РегистрСписок, ТипРегистра = Неопределено)
	Если ТипРегистра = Неопределено Тогда ТипРегистра = "Сведений" КонецЕсли;
	СтруктураИзмерений = Новый Структура;
	
	СтрокаРегистр = Строка(РегистрСписок);
	СтрокаРегистрСписок = Сред(СтрокаРегистр, Найти(СтрокаРегистр, ".")+1);
	РегистрМетаданные = Вычислить("Метаданные.Регистры" + ТипРегистра + "[СтрокаРегистрСписок]");
	Попытка
		КоллекцияИзмерений = РегистрМетаданные.Измерения;
	Исключение
	КонецПопытки;
	Для Каждого ТекИзмерение Из КоллекцияИзмерений Цикл
		СтруктураИзмерений.Вставить(ТекИзмерение.Имя, ТекИзмерение.Тип);
	КонецЦикла;
	//регистратор фактически тоже измерение - добавим
	Попытка
		СтруктураИзмерений.Вставить("Регистратор", РегистрСписок.Отбор.Регистратор.ТипЗначения);
	Исключение
	КонецПопытки;
			
	Возврат СтруктураИзмерений;
КонецФункции // ргПолучитьИзмеренияРегистра()

//Возвращает строковое представление имени регистра
//если синоним пустой, то возвращает имя, как оно задано в конфигураторе
// Параметры: 
//  РегистрСписок - РегистрСведенийСписок
//  ТипРегистра - Строка - определяет тип регистра
//
// Возвращаемое значение:
//  ИмяРегистра - Строка - строковое представление имени регистра
//
Функция ргПолучитьИмяРегистра(РегистрСписок, ТипРегистра) Экспорт
	ИмяРегистра = "";
	Попытка
		ИмяРегистра = Сред(Строка(РегистрСписок), Найти(Строка(РегистрСписок), ".")+1);
		СинонимРегистра = Вычислить("Метаданные.Регистры" + ТипРегистра + "[ИмяРегистра].Синоним");
		ИмяРегистра = ?(ПустаяСтрока(СинонимРегистра), ИмяРегистра, СинонимРегистра);
	Исключение
	КонецПопытки;
		
	Возврат ИмяРегистра;
КонецФункции // ргПолучитьИмяРегистра()


#Если Клиент Тогда
// все дальнейшие процедуры относятся к интерфейсным
// и соответственно, на сервере и внешнем соединении исполняться не могут

//Устанавливает владельца формы по переданному источнику оповещения (тип - форма)
//
// Параметры:
//  ЭтаФорма - Форма - переданная форма
//  ФормаИсточник - Форма - источнику оповещения
//
Процедура ргУстановитьВладельцаФормы(ЭтаФорма, ФормаИсточник)
	Если ТипЗнч(ФормаИсточник) <> Тип("Форма") Тогда Возврат; КонецЕсли;
	Попытка 
		ЭтаФорма.ВладелецФормы = ФормаИсточник.ЭлементыФормы.Список;
	Исключение
	КонецПопытки;
КонецПроцедуры // ргУстановитьВладельцаФормы()

//Возвращает строковое представление типа регистра
//
// Параметры:
//  ЭтаФорма - Форма - переданная форма
//
Функция ргПолучитьТипРегистра(ЭтаФорма)
	ТипРегистра = "Сведений";
	Попытка
		Список = ЭтаФорма.РегистрСведенийСписок;
		ТипРегистра = "Сведений";
	Исключение
		Попытка
			Список = ЭтаФорма.РегистрНакопленияСписок;
			ТипРегистра = "Накопления";
		Исключение
			Попытка
				Список = ЭтаФорма.РегистрБухгалтерииСписок;
				ТипРегистра = "Бухгалтерии";
			Исключение
				Попытка
					Список = ЭтаФорма.РегистрРасчетаСписок;
					ТипРегистра = "Расчета";
				Исключение
				КонецПопытки;
			КонецПопытки;
		КонецПопытки;
	КонецПопытки;
	
	Возврат ТипРегистра;			
КонецФункции // ргПолучитьТипРегистра()

//заполнение дополнительных действий форм списков регистров
//
// Параметры:
//  ЭтаФорма - Форма - переданная форма
//
Процедура ргЗаполнитьДополнительныеДействияРегистров(ЭтаФорма)
	// зарезервировано на будущее
КонецПроцедуры // ргЗаполнитьДополнительныеДействияРегистров()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ПАНЕЛЕЙ ФОРМ СПИСКОВ, ЗАПИСЕЙ, НАБОРОВ ЗАПИСЕЙ РЕГИСТРОВ

// Событие нажатия кнопки "Связь" в панели "ОсновныеДействияФормы"
//
// Параметры:
//  ЭтаФорма           - Форма - переданная форма
//  Кнопка             - Кнпка - кнопка "Связь" в панели "ОсновныеДействияФормы"
//  Пометка            - Булево - Управляет пометкой кнопки командной панели.
//  ВызыватьОповещение - Булево - определяет необходимость оповещений
//
Процедура ргСписокДействияФормыСвязь(ЭтаФорма, Кнопка, Пометка=Неопределено, ВызыватьОповещение = ложь) Экспорт
	// Инвертируем или установим переданное состояние триггерной кнопки
    Кнопка.Пометка = ?(Пометка = Неопределено, НЕ Кнопка.Пометка, Пометка);
	
	// Отработаем изменение глобальной переменной глОповещениеАктивации, определяющей необходимость оповещений
	// на уровне сеанса. Если и пометка и переменная = истина, то оповещения и так возникают, если ложь и ложь,
	// то вероятно это ошибка, а вот если кнопка сброшена, а глобально оповещение включено... см.далее
	Если Кнопка.Пометка Тогда
		ВызыватьОповещение = Истина;
		Попытка	// владелец формы уже может быть и неопределен..
			// но если он определен, принудительно проведем оповещение (себя же) для установки отбор
			Оповестить("АктивизированЭлемент", ЭтаФорма.ВладелецФормы.ТекущиеДанные.Ссылка, ЭтаФорма.ВладелецФормы);
		Исключение
		КонецПопытки;
		ЭтаФорма.ВладелецФормы = Неопределено;

	ИначеЕсли ВызыватьОповещение Тогда
		// Разблокируем все отборы в настройке, поскольку автопривязки сняты
		ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
		НастройкиОтбора = Вычислить("ЭтаФорма.ЭлементыФормы.Регистр" + ТипРегистра + "Список").НастройкаОтбора;
		Для Каждого УпрОтбора Из НастройкиОтбора Цикл
			УпрОтбора.Доступность = Истина;
		КонецЦикла; 

		// Обновление вызвавшей формы отключено, а вот оповещение общее включено !
		// необходимо проверить, есть ли еще оповещаемые связанные формы с выставленной пометкой.
		// Проверка реализуется путем сброса общего признака оповещений и вызова служебного оповещения,
		// опрашивающего связанные формы на предмет привязки к оповещениям...
		ВызыватьОповещение = ложь;
		Оповестить("ПроверкаОповещения","НеРавноНеопределено",ЭтаФорма);
	КонецЕсли; 
	
КонецПроцедуры // ргСписокДействияФормыСвязь()
		
// Событие нажатия кнопки "Печать" в панели "ОсновныеДействияФормы"
//(пока прописан аналогично выводу реестра в обработке ОбработкаДокументов, используя общий макет Реестр)
//
// Параметры:
//  ЭтаФорма           - Форма - переданная форма
//  Кнопка             - Кнпка - кнопка "Печать" в панели "ОсновныеДействияФормы"
//
Процедура ргОсновныеДействияФормыПечать(ЭтаФорма, Кнопка) Экспорт
	
	// Получим табличное поле, отображающее список.
	Попытка
		ТабличноеПолеСписок = ЭтаФорма.ЭлементыФормы.РегистрСведенийСписок;
	Исключение
		Попытка
			ТабличноеПолеСписок = ЭтаФорма.ЭлементыФормы.РегистрНакопленияСписок;
		Исключение
			ТабличноеПолеСписок = ЭтаФорма.ЭлементыФормы.РегистрРасчетаСписок;
		КонецПопытки;
	КонецПопытки;
	
	ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
	РегистрСписок = ЭтаФорма["Регистр" + ТипРегистра + "Список"];
			
	ЭлементРегистрСписок = ЭтаФорма.ЭлементыФормы["Регистр" + ТипРегистра + "Список"];
	ИмяРегистра = ргПолучитьИмяРегистра(РегистрСписок, ТипРегистра);
	ТекстЗаголовка = "Регистр " + НРег(ТипРегистра) + " " + ИмяРегистра + ".";
	//получаем данные (придется запросом)
	СтрокаРегистр = Сред(Строка(РегистрСписок), Найти(Строка(РегистрСписок), ".")+1);
	МетаданныеРегистра = Метаданные["Регистры"+ТипРегистра][СтрокаРегистр];
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	*"+?(ТипРегистра="Накопления" И Строка(МетаданныеРегистра.ВидРегистра) = "Остатки",","+Символы.ПС+СтрокаРегистр+".ВидДвижения КАК Картинка", "")+"
	|ИЗ
	|	Регистр" + ТипРегистра + "." + СтрокаРегистр + " КАК " + СтрокаРегистр; 
	ПостроительЗапроса = Новый ПостроительЗапроса(ТекстЗапроса);
	ПостроительЗапроса.ЗаполнитьНастройки();
			
	//установим отборы
	ПоляОтбора = ПостроительЗапроса.Отбор;
	Для Каждого ТекОтбор Из РегистрСписок.Отбор Цикл
		Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		НовыйОтбор = ПоляОтбора.Добавить(ТекОтбор.Имя);
		НовыйОтбор.ВидСравнения = ТекОтбор.ВидСравнения;
		НовыйОтбор.Значение = ТекОтбор.Значение;
		НовыйОтбор.ЗначениеС = ТекОтбор.ЗначениеС;
		НовыйОтбор.ЗначениеПо = ТекОтбор.ЗначениеПо;
		НовыйОтбор.Представление = ТекОтбор.Представление;
		НовыйОтбор.Использование = Истина;
	КонецЦикла;
	ПостроительЗапроса.Выполнить();
	ТаблицаРегистра = ПостроительЗапроса.Результат.Выгрузить();
			
	ТекстОтборов = "";
	Если РегистрСписок.Отбор.Количество()<>0 Тогда
		// Текст отборов
		Для Каждого ТекОтбор Из РегистрСписок.Отбор Цикл
			Если ТекОтбор.Использование И Не ТекОтбор.Имя = "Периодичность" Тогда
				Если Найти(ТекОтбор.ВидСравнения,"Интервал")>0 Тогда
					ТекстОтборов = ТекстОтборов + ?(ТекстОтборов = "", "", ", ") + ТекОтбор.Представление + " " + ТекОтбор.ВидСравнения + " " + ТекОтбор.ЗначениеС+" - "+ТекОтбор.ЗначениеПо;
				Иначе
					ТекстОтборов = ТекстОтборов + ?(ТекстОтборов = "", "", ", ") + ТекОтбор.Представление + " " + ТекОтбор.ВидСравнения + " " + ТекОтбор.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ТекстОтборов = ?(ТекстОтборов = "", "Все", ТекстОтборов);
	Иначе
		ТекстОтборов = "Без отбора";
	КонецЕсли;
			
	ТаблицаКолонок = Новый ТаблицаЗначений;
	ТаблицаКолонок.Колонки.Добавить("Имя");
	ТаблицаКолонок.Колонки.Добавить("Представление");
	ТаблицаКолонок.Колонки.Добавить("Формат");
	ТаблицаКолонок.Колонки.Добавить("ЭтоПоказатель");
			
	СтруктураИтоговыхКолонок = Новый Структура();
	СтруктураКартинок = Неопределено;
	Для Каждого ТекКолонка Из ЭлементРегистрСписок.Колонки Цикл
		Если НЕ ТекКолонка.Видимость Тогда Продолжить; КонецЕсли;//печатаем только видимые колонки
				
		Если ТекКолонка.Имя = "Картинка" Тогда
			Если ТипРегистра="Накопления" И Строка(МетаданныеРегистра.ВидРегистра) = "Остатки" Тогда
				СтруктураКартинок = Новый Соответствие();
				СтруктураКартинок.Вставить(ВидДвиженияНакопления.Приход, БиблиотекаКартинок.Плюс);
				СтруктураКартинок.Вставить(ВидДвиженияНакопления.Расход, БиблиотекаКартинок.Минус);
						
				НоваяСтрока = ТаблицаКолонок.Добавить();
				НоваяСтрока.Имя = ТекКолонка.Имя;
				НоваяСтрока.Представление = "";
				НоваяСтрока.ЭтоПоказатель = Ложь;
				НоваяСтрока.Формат = "";
			КонецЕсли;
			Продолжить;
		КонецЕсли;
				
		Если ТекКолонка.ЭлементУправления = Неопределено Тогда Продолжить; КонецЕсли;
				
		НоваяСтрока = ТаблицаКолонок.Добавить();
		Если Не ПустаяСтрока(ТекКолонка.Данные) Тогда
			НоваяСтрока.Имя = ТекКолонка.Данные;	
		ИначеЕсли Не ПустаяСтрока(ТекКолонка.ДанныеФлажка) Тогда
			НоваяСтрока.Имя = ТекКолонка.ДанныеФлажка;	
		Иначе
			НоваяСтрока.Имя = ТекКолонка.Имя;
		КонецЕсли;
		
		НоваяСтрока.Представление = ТекКолонка.ТекстШапки;
		НоваяСтрока.ЭтоПоказатель = Ложь;
		НоваяСтрока.Формат = "";
				
		Если ТипЗнч(ТекКолонка.ЭлементУправления.Значение) = Тип("Дата") Тогда
			Квалификатор = ТекКолонка.ЭлементУправления.ТипЗначения.КвалификаторыДаты.ЧастиДаты;
			ФорматСтр = "";
			Если Квалификатор = ЧастиДаты.Дата Тогда
				ФорматСтр = "ДФ=dd.MM.yyyy; ДП=-"; 
			ИначеЕсли Квалификатор = ЧастиДаты.Время Тогда
				ФорматСтр = "ДЛФ=T";  
			КонецЕсли;
			НоваяСтрока.Формат = ФорматСтр;
			Продолжить;
		КонецЕсли;
				
		Если ТипРегистра = "Сведений" Тогда Продолжить; КонецЕсли;
		ТекРесурс = МетаданныеРегистра.Ресурсы.Найти(ТекКолонка.Данные);
		Если ТекРесурс<>Неопределено Тогда
			КвалификаторРесурса = ТекРесурс.Тип.КвалификаторыЧисла;
			НоваяСтрока.Формат = "ЧЦ="+КвалификаторРесурса.Разрядность+";ЧДЦ="+КвалификаторРесурса.РазрядностьДробнойЧасти;
			НоваяСтрока.ЭтоПоказатель = Истина;
			СтруктураИтоговыхКолонок.Вставить(ТекКолонка.Данные);
			Продолжить;
		КонецЕсли;
				
		ТекРеквизит = МетаданныеРегистра.Реквизиты.Найти(ТекКолонка.Данные);
		Если ТекРеквизит<>Неопределено Тогда
			Если ТекРеквизит.Тип.Типы()[0] = Тип("Число") Тогда
				КвалификаторРесурса = ТекРеквизит.Тип.КвалификаторыЧисла;
				НоваяСтрока.Формат = "ЧЦ="+КвалификаторРесурса.Разрядность+";ЧДЦ="+КвалификаторРесурса.РазрядностьДробнойЧасти;
				НоваяСтрока.ЭтоПоказатель = Истина;
				СтруктураИтоговыхКолонок.Вставить(ТекКолонка.Данные);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
	ПараметрыРеестра = Новый Структура();
	//Заголовок
	ПараметрыРеестра.Вставить("ЗаголовокРеестра", ТекстЗаголовка);
	//Период
	ПараметрыРеестра.Вставить("ДатаНачала",    ТабличноеПолеСписок.СтандартныйПериод.ДатаНачала);
	ПараметрыРеестра.Вставить("ДатаОкончания", ТабличноеПолеСписок.СтандартныйПериод.ДатаОкончания);
	//Отбор
	ПараметрыРеестра.Вставить("Отборы", ТекстОтборов);
	//Выбранная валюта реестра
	ПараметрыРеестра.Вставить("ВалютаРеестра", "");
			
	ТабДокумент = дкСформироватьРеестр(ПараметрыРеестра, ТаблицаРегистра, ТаблицаКолонок, СтруктураИтоговыхКолонок, СтруктураКартинок);
			
	// Зададим параметры макета
	ТабДокумент.ПолеСверху  = 0;
	ТабДокумент.ПолеСлева   = 0;
	ТабДокумент.ПолеСнизу   = 0;
	ТабДокумент.ПолеСправа  = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ОтображатьСетку = Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.Защита =  обПраво("ЗащитаПечатныхФорм");
				
	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
			
	// Перенесем все настройки из табличного документа в поле табличного документа формы печати
	СписокКопируемыхСвойств = "АвтоМасштаб,Защита,ОтображатьЗаголовки,ОтображатьСетку,ПолеСверху,ПолеСлева,ПолеСнизу,ПолеСправа,ТолькоПросмотр";
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ТабДокумент,СписокКопируемыхСвойств);
	ТабличныйДокумент.МасштабПечати			= ?(ТабДокумент.МасштабПечати			= Неопределено, 100,ТабДокумент.МасштабПечати);
	ТабличныйДокумент.РазборПоКопиям		= ?(ТабДокумент.РазборПоКопиям			= Неопределено,ЛОЖЬ,ТабДокумент.РазборПоКопиям);
	ТабличныйДокумент.ЧерноБелаяПечать		= ?(ТабДокумент.ЧерноБелаяПечать		= Неопределено,ЛОЖЬ,ТабДокумент.ЧерноБелаяПечать);
	ТабличныйДокумент.ЭкземпляровНаСтранице	= ?(ТабДокумент.ЭкземпляровНаСтранице	= Неопределено,   0,ТабДокумент.ЭкземпляровНаСтранице);
	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.ТолькоПросмотр = обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра");
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
			
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.ОбъектПредставление			= ТекстЗаголовка;
	ФормаПечати.ДополнительноКПредставлению	= "";
	ФормаПечати.Открыть();
			
КонецПроцедуры // ргОсновныеДействияФормыПечать()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ФОРМ ЗАПИСЕЙ РЕГИСТРОВ

// Общий обработчик события ПередОткрытием формы записей регистра 
//
// Параметры:
//  ЭтаФорма             - Форма - переданная форма
//  Отказ                - Булево - Признак отказа от открытия формы
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной
//									(системной) обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	// Может быть отраслевое переопределение функции модуля...
	Результат = орФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	// Стандартная обработка
	удФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	Возврат Результат;
	
КонецФункции // ргЗаписьПередОткрытием()

// Общий обработчик события ПриОткрытии формы записей регистра 
//
// Параметры:
//  ЭтаФорма          - Форма - переданная форма
//  ПовторноеОткрытие - Булево - Признак повторного открытия формы
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПриОткрытии(ЭтаФорма, ПовторноеОткрытие = Ложь) Экспорт
	
	Результат = Истина;
	
	// для стандартных командных панелей разберемся с отображением иконок
	ПанельОсновныеДействияФормы = ЭтаФорма.ЭлементыФормы.Найти("ОсновныеДействияФормы");
	Если (ПанельОсновныеДействияФормы<>Неопределено) Тогда
		Если обПраво("ПоказыватьИконкиНаКнопкахПанелей") Тогда
			РежимОтображения=ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
		Иначе
			РежимОтображения=ОтображениеКнопкиКоманднойПанели.Надпись;
		КонецЕсли;
		Для Каждого КнопкаПанели Из ПанельОсновныеДействияФормы.Кнопки Цикл
			Если КнопкаПанели.ТипКнопки<>ТипКнопкиКоманднойПанели.Разделитель Тогда
				КнопкаПанели.Отображение=РежимОтображения;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().ЗарегистрироватьФорму(ЭтаФорма, "", "РегистрСведений");
	
	Возврат Результат;
	
КонецФункции // ргЗаписьПриОткрытии()

// Общий обработчик события ПриПовторномОткрытии формы записей регистра 
//
// Параметры:
//  ЭтаФорма             - Форма - переданная форма
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной
//									(системной) обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПриПовторномОткрытии(ЭтаФорма, СтандартнаяОбработка) Экспорт
	
	Результат = Истина;
	
	ргЗаписьПриОткрытии(ЭтаФорма,Истина);
	
	Возврат Результат;
	
КонецФункции // ргЗаписьПриПовторномОткрытии()

// Общий обработчик события ОбновлениеОтображения формы записей регистра 
//
// Параметры:
//  ЭтаФорма - Форма - переданная форма
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьОбновлениеОтображения(ЭтаФорма) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // ргЗаписьОбновлениеОтображения()

// Общий обработчик события ПередЗакрытием формы записей регистра 
//
// Параметры:
//  ЭтаФорма             - Форма - переданная форма
//  Отказ                - Булево - Признак отказа от закрытия формы
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной
//									(системной) обработки события.
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат;
КонецФункции // ргЗаписьПередЗакрытием()

// Общий обработчик события ПриЗакрытии формы записей регистра 
//
// Параметры:
//  ЭтаФорма - Форма - переданная форма
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПриЗакрытии(ЭтаФорма) Экспорт
	
	Результат = Истина;
	
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().УдалитьИзДереваФорм(ЭтаФорма); 
	
	Возврат Результат;
	
КонецФункции // ргЗаписьПриЗакрытии()

// Общий обработчик события ОбработкаОповещения формы записей регистра 
//
// Параметры:
//  ЭтаФорма   - Форма - переданная форма
//  ИмяСобытия - Строка - Имя события. Может быть использовано для идентификации сообщений. 
//  Параметр   - Произвольный - Параметр, переданный в сообщении. 
//  Источник   - Источник события - переданный в сообщении. 
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Результат = Истина;
	
    // Отбросим собственные оповещения
	Если ЭтаФорма=Источник Тогда Возврат Результат; КонецЕсли;
	
	Если ИмяСобытия = "ОбновитьПрава" Тогда
		Если Параметр=ПараметрыСеанса.Пользователь ИЛИ Параметр=ПараметрыСеанса.Пользователь.ШаблонПрав ИЛИ
			 Параметр=ПараметрыСеанса.ПодразделениеКомпании ИЛИ Параметр=ПараметрыСеанса.Организация Тогда
			// Были обновлены права пользователя (глПрава) - необходимо пересчитывание кэша прав объекта
			Попытка Состояние("Обновляю права для элемента "+ЭтаФорма.Заголовок);
				ЭтаФорма.Права = обПолучитьПраваИНастройкиПользователя(Параметр);
				ЭтаФорма.Обновить();
			Исключение
			КонецПопытки; 
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции // ргЗаписьОбработкаОповещения()

// Общий обработчик события ВнешнееСобытие формы записей регистра 
//
// Параметры:
//  ЭтаФорма - Форма - переданная форма
//  Источник - Строка - Источник внешнего события. 
//  Событие  - Строка - Наименование события. 
//  Данные   - Строка - Данные для события.  
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьВнешнееСобытие(ЭтаФорма, Источник, Событие, Данные) Экспорт
	Результат = ЭтаФорма.ВводДоступен();
	Возврат Результат;
КонецФункции // ргЗаписьВнешнееСобытие()

// Общий обработчик события ПередЗаписью формы записей регистра 
//
// Параметры:
//  ЭтаФорма             - Форма - переданная форма
//  Отказ                - Булево - Признак отказа от записи формы
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПередЗаписью(ЭтаФорма, Отказ) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат ;
КонецФункции // ргЗаписьПередЗаписью()

// Общий обработчик события ПриЗаписи формы записей регистра 
//
// Параметры:
//  ЭтаФорма             - Форма - переданная форма
//  Отказ                - Булево - Признак отказа от записи формы
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПриЗаписи(ЭтаФорма, Отказ) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат ;
КонецФункции // ргЗаписьПриЗаписи()

// Общий обработчик события ПослеЗаписи формы записей регистра 
//
// Параметры:
//  ЭтаФорма             - Форма - переданная форма
//
// Возвращаемое значение:
//  Результат - Булево - Признак необходимости дальнейшей обработки (статус - отработано или нет)
//                       для определения возможности дальнейшей работы приватного обработчика
//
Функция ргЗаписьПослеЗаписи(ЭтаФорма) Экспорт
	// зарезервировано на будущее
	Результат = Истина;
	Возврат Результат ;
КонецФункции // ргЗаписьПослеЗаписи()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ТАБЛИЦЫ ФОРМ СПИСКОВ, ЗАПИСЕЙ, НАБОРОВ ЗАПИСЕЙ РЕГИСТРОВ

// Стандартный обработчик события выбора (двойное нажатие левой кнопки мыши или Enter) строки списка регистра
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Элемент              - Элемент управления, список
//  ВыбраннаяСтрока      - Выбранная строка списка
//  Колонка              - Выбранная колонка списка. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной
//									(системной) обработки события
//
Процедура ргСписокВыбор(ЭтаФорма, Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка) Экспорт
	// зарезервировано на будущее
КонецПроцедуры // ргСписокВыбор()

// Стандартный обработчик события активизации строки списка регистра
//
// Параметры:
//  ЭтаФорма            - форма - переданная форма
//  Элемент             - Элемент управления, список
//  ОбновитьКомментарий - Булево - может обновлять комментарий в форме списка (разрешить / запретить)
//
Процедура ргСписокПриАктивизацииСтроки(ЭтаФорма, Элемент, ОбновитьКомментарий=Истина) Экспорт
	// зарезервировано на будущее
КонецПроцедуры // ргСписокПриАктивизацииСтроки()

// Стандартный обработчик события перед началом добавления строки списка регистра
//
// Параметры:
//  ЭтаФорма    - форма - переданная форма
//  Элемент     - Элемент управления, список
//  Отказ       - Булево - Признак отказа от добавления строки. 
//  Копирование - Булево - Определяет режим копирования. Если установлено Истина, то происходит копирование строки. 
//
Процедура ргСписокПередНачаломДобавления(ЭтаФорма, Элемент, Отказ, Копирование) Экспорт
	ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	Попытка
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Вычислить("ЭтаФорма.Регистр" + ТипРегистра + "Список")));
	Исключение
		Возврат;
	КонецПопытки;
	//не нашли, выходим
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	//если редактируем в списке, выходим
	Если ОбъектМетаданных.СпособРедактирования = Метаданные.СвойстваОбъектов.СпособРедактирования.ВСписке Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ргСписокПередНачаломДобавления()

// Обработчик изменения отбора списка регистра посредством ручного выбора или пришедшего оповещения
//
// Параметры:
//  ЭтаФорма    - форма - переданная форма
//  Элемент     - Элемент управления, список
//
Процедура ргСписокПриИзмененииОтбора(ЭтаФорма, Элемент=Неопределено) Экспорт
	ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
	//вывод комментария (фильтра списка или гиперссылки на владельца)
	ПредставлениеОтбора = обПолучитьПредставлениеОтбора(Вычислить("ЭтаФорма.Регистр" + ТипРегистра + "Список.Отбор"));
	Если ПустаяСтрока(ПредставлениеОтбора) Тогда
		//нет ни одного отбора (событие очистить отбор)
		ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок = "";
		ЭтаФорма.ЭлементыФормы.тКомментарий.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
		ЭтаФорма.ЭлементыФормы.тКомментарий.Подсказка="";
	Иначе
		ЭтаФорма.ЭлементыФормы.тКомментарий.Заголовок = Лев(ПредставлениеОтбора, СтрДлина(ПредставлениеОтбора)-1);
		ЭтаФорма.ЭлементыФормы.тКомментарий.ЦветТекста = Новый Цвет(155,0,0);
		ЭтаФорма.ЭлементыФормы.тКомментарий.Подсказка="Текущий фильтр";
	КонецЕсли;
	ЭтаФорма.Обновить();
КонецПроцедуры // ргСписокПриИзмененииОтбора()

// Обработчик изменения набора записей
//
// Параметры:
//  ЭтаФорма    - форма - переданная форма
//  Элемент     - Элемент управления, список
//  Отказ       - Булево - Признак отказа от изменения.
//
Процедура ргСписокПередНачаломИзменения(ЭтаФорма, Элемент, Отказ) Экспорт
	// зарезервировано на будущее	
КонецПроцедуры // ргСписокПередНачаломИзменения()

// Обработчик удаления записи регистра
//
// Параметры:
//  ЭтаФорма    - форма - переданная форма
//  Элемент     - Элемент управления, список
//  Отказ       - Булево - Признак отказа от удаления.
//
Процедура ргСписокПередУдалением(ЭтаФорма, Элемент, Отказ) Экспорт
	// зарезервировано на будущее	
КонецПроцедуры // ргСписокПередУдалением()

// Обработчик вывода строки списка регистра
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  Элемент           - Элемент управления, список
//  ОформлениеСтроки  - ОформлениеСтроки - Содержит оформление строки (шрифт, цвет) и коллекцию оформлений ячеек.
//  ДанныеСтроки      - Данные выводимой строки - Параметр соответствует свойству ТекущиеДанные для выводимой строки. 
//
Процедура ргСписокПриВыводеСтроки(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	// зарезервировано на будущее	
КонецПроцедуры // ргСписокПриВыводеСтроки()

// Обработчик окончания редактирования записи
//
// Параметры:
//  ЭтаФорма     - форма - переданная форма
//  Элемент      - Элемент управления, список
//  НоваяСтрока  - Булево - Признак редактирования новой строки. Имеет значение Истина,
//  						если строка была добавлена или скопирована. 
//  Отказ        - Булево - Истина, если произошла отмена редактирования. 
//
Процедура ргСписокПриОкончанииРедактирования(ЭтаФорма, Элемент, НоваяСтрока, ОтменаРедактирования) Экспорт
	// зарезервировано на будущее	
КонецПроцедуры // ргСписокПриОкончанииРедактирования()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ФОРМ СПИСКОВ, ЗАПИСЕЙ, НАБОРОВ ЗАПИСЕЙ РЕГИСТРОВ

// Обработчик перед открытием форм
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  Отказ                - Булево - Признак отказа от открытия формы. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной
//  								(системной) обработки события.
//
Процедура ргСписокПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	// Может быть отраслевое переопределение функции модуля...
	Если орФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) <> Неопределено Тогда Возврат; КонецЕсли; 
	// Стандартная обработка
	удФормаПередОткрытием(ЭтаФорма, Отказ, СтандартнаяОбработка);
	ргЗаполнитьДополнительныеДействияРегистров(ЭтаФорма);
КонецПроцедуры // ргСписокПередОткрытием()

// Обработчик открытия форм
//
// Параметры:
//  ЭтаФорма          - форма - переданная форма
//  ПовторноеОткрытие - Булево - Признак повторного открытия формы. 
//
Процедура ргСписокПриОткрытии(ЭтаФорма, ПовторноеОткрытие=Ложь) Экспорт
	ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
	ЭлементРегистрСписок = Вычислить("ЭтаФорма.ЭлементыФормы.Регистр" + ТипРегистра + "Список");
	
	// общие действия форм списков при открытии
	удФормаПриОткрытии(ЭтаФорма);

	// Восстановим связь формы по владельцу
	Попытка
		Пометка = ВосстановитьЗначение("СвязьПоВладельцу" + Вычислить("ЭтаФорма.Регистр" + ТипРегистра + "Список"));
		Если (Пометка = Истина) И НЕ ПовторноеОткрытие Тогда
			// Надо восстановить привязку формы к оповещению о смене регистратора
			// Внимание: логическое выражение "Пометка = Истина" не упрощать - Пометка может не иметь булева типа !
			Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь;
			ЭтаФорма.ДействияФормыСвязь(Кнопка);
		КонецЕсли; 
	Исключение
		Кнопка = Неопределено;
	КонецПопытки;
		
	//подключим обработчик изменения отбора
	ЭтаФорма.ПодключитьОбработчикИзмененияДанных("Регистр" + ТипРегистра + "Список.Отбор", "Регистр" + ТипРегистра + "СписокПриИзмененииОтбора", Истина);
	ргСписокПриИзмененииОтбора(ЭтаФорма, ЭлементРегистрСписок);
	
	Попытка //Установим фокус ввода на список
		ЭтаФорма.ТекущийЭлемент = ЭлементРегистрСписок;
	Исключение
	КонецПопытки;
КонецПроцедуры // ргСписокПриОткрытии()

// Обработчик повторного открытия форм
//
// Параметры:
//  ЭтаФорма             - форма - переданная форма
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной
//  								(системной) обработки события.
//
Процедура ргСписокПриПовторномОткрытии(ЭтаФорма,СтандартнаяОбработка) Экспорт
	ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
	РегистрСписок = Вычислить("ЭтаФорма.Регистр" + ТипРегистра + "Список");
	ргСписокПриОткрытии(ЭтаФорма, Истина);
КонецПроцедуры // ргСписокПриПовторномОткрытии()

// Обработчик стандартных оповещений форм
//
// Параметры:
//  ЭтаФорма         - форма - переданная форма
//  ИмяСобытия       - Строка - Имя события. Может быть использовано для идентификации сообщений. 
//  Параметр         - Произвольный - Параметр, переданный в сообщении. 
//  Источник         - Источник события, переданный в сообщении. 
//  СвязьПоВладельцу - Булево.
//
Процедура ргСписокОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник) Экспорт
	// Отбросим собственные оповещения и оповещения без передачи параметра
	Если ЭтаФорма=Источник ИЛИ Параметр=Неопределено Тогда Возврат; КонецЕсли;
	
	// Проверим оповещения смены владельцев формы
	Если ИмяСобытия = "АктивизированЭлемент" Тогда
		// Получим положение кнопки СвязьПоВладельцу - отрабатываем только если она выставлена
		Попытка // кнопки может не быть на форме
			Если НЕ ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь.Пометка Тогда Возврат; КонецЕсли;
		Исключение
		КонецПопытки;
		
		ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
		НастройкиОтбора = Вычислить("ЭтаФорма.ЭлементыФормы.Регистр" + ТипРегистра + "Список").НастройкаОтбора;
		ТипПараметра = ТипЗнч(Параметр);
		
		ЭлОтбораНайден = Ложь;
		Для Каждого ЭлОтбора Из ЭтаФорма.Отбор Цикл
			УпрОтбора = НастройкиОтбора[ЭлОтбора.Имя];
			Если ЭлОтбора.ТипЗначения.СодержитТип(ТипПараметра) И НЕ ЭлОтбораНайден Тогда
				ЭлОтбора.ВидСравнения  = ВидСравнения.Равно;
				ЭлОтбора.Значение      = Параметр;
				//УпрОтбора.Доступность  = Ложь;
				ЭлОтбора.Использование = Истина;
				ЭлОтбораНайден = ИСтина;
			Иначе
				Если НЕ УпрОтбора.Доступность Тогда
					ЭлОтбора.Использование = Ложь;
				КонецЕсли; 
				УпрОтбора.Доступность = Истина;
			КонецЕсли; 
		КонецЦикла; 
		
	ИначеЕсли ИмяСобытия = "ПроверкаОповещения" Тогда
		// Если мы получили это оповещение и у данной формы выставлена привязка (пометка),
		// мы должны установить глОповещениеАктивации = Истина, сделаем это через форму, аналогично нажатию кнопки..
		// такой рекурсивный вызов обработчиков формы применен для исключения передачи переменной модуля приложения
		// глОповещениеАктивации в параметрах обработчика.
		Попытка
			Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь;
			Если Кнопка.Пометка Тогда
				Кнопка.Пометка = Ложь;
				ЭтаФорма.ДействияФормыСвязь(Кнопка);
			КонецЕсли; 
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	ЭтаФорма.Обновить();
КонецПроцедуры // ргСписокОбработкаОповещения()

// Обработчик обновления отображения форм
Процедура ргСписокОбновлениеОтображения(ЭтаФорма) Экспорт
	// зарезервировано на будущее
КонецПроцедуры // ргСписокОбновлениеОтображения()

// Обработчик перед закрытием форм
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//
Процедура ргСписокПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	// зарезервировано на будущее
КонецПроцедуры // ргСписокПередЗакрытием()

// Обработчик закрытия форм
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//
Процедура ргСписокПриЗакрытии(ЭтаФорма) Экспорт
	Попытка	// а вдруг...кнопки нет
		Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Связь;
		
		// сохраним текущее значение триггерной кнопки на будущее
		ТипРегистра = ргПолучитьТипРегистра(ЭтаФорма);
		СохранитьЗначение("СвязьПоВладельцу" + Вычислить("ЭтаФорма.Регистр" + ТипРегистра + "Список"), Кнопка.Пометка);
		
		// Для случая выставленной пометки (оповещения работают) иммитируем отжатие кнопки, чтобы
		// глобальный обработчик переустановил переменную глОповещениеАктивации посредством опроса оставшихся форм
		Если Кнопка.Пометка Тогда
			ЭтаФорма.ДействияФормыСвязь(Кнопка);
		КонецЕсли; 
	Исключение
	КонецПопытки;
	Обработки.ПанельАРМФормыИПоследниеОбъекты.Создать().УдалитьИзДереваФорм(ЭтаФорма);
КонецПроцедуры // ргСписокПриЗакрытии()

// Обработчик внешних событий
//
// Параметры:
//  ЭтаФорма - форма - переданная форма
//  Источник - Строка - Источник внешнего события. 
//  Событие  - Строка - Наименование события. 
//  Данные   - Строка - Данные для события. 
//
Функция ргСписокВнешнееСобытие(ЭтаФорма, Источник, Событие, Данные) Экспорт
	Возврат ЭтаФорма.ВводДоступен();
КонецФункции // ргСписокВнешнееСобытие()

// Обработчик события ПриЗаписи набора записей регистра
Функция ргПриЗаписи(НаборЗаписей, Отказ) Экспорт
	Результат = Истина;
	Возврат Результат;
КонецФункции

Процедура ргЗаполнитьАртикулДляПоиска() Экспорт
	РегистрыСведений.ГруппыАналогов.		ЗаполнитьАртикулДляПоиска();
	РегистрыСведений.Замены.				ЗаполнитьАртикулДляПоиска();
	РегистрыСведений.ПрайсЛистыКонтрагентов.ЗаполнитьАртикулДляПоиска();
	РегистрыСведений.УпущенныйСпрос.		ЗаполнитьАртикулДляПоиска();
КонецПроцедуры

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть
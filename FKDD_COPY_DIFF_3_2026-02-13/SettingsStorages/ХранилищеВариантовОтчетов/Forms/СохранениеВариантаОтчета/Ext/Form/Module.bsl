&НаКлиенте
Процедура УстановитьОграничениеТипа()
	
	Если ПользовательскаяНастройка = 1 Тогда
		Элементы.НазначениеДоступа.Доступность       = Ложь;
		Элементы.ОбъектДоступа.Доступность           = Ложь;
		Элементы.ОбъектДоступа.ОтметкаНезаполненного = Ложь;
	Иначе
		Элементы.НазначениеДоступа.Доступность       = Истина;
		Элементы.ОбъектДоступа.Доступность           = Истина;
		Элементы.ОбъектДоступа.ОтметкаНезаполненного = Истина;
		Если НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Организация") Тогда
			ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Организации");
			Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Подразделение") Тогда
			ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании");
			Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Пользователь") Тогда
			ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
			Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		Иначе
			Элементы.ОбъектДоступа.Доступность           = Ложь;
			Элементы.ОбъектДоступа.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам") Тогда
		СправочникИмя = Метаданные.Справочники.ВариантыОтчетов.Имя;
		ПравоДоступа = обПраво("Право доступа "+СправочникИмя);
		Если ПравоДоступа=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
			Отказ=Истина;
			Возврат;
		ИначеЕсли ПравоДоступа=Перечисления.ВидыПравДляСправочников.Чтение Тогда
			ТолькоПросмотр = Истина;
			Элементы.Наименование.ТолькоПросмотр              = Истина;
			Элементы.Описание.ТолькоПросмотр                  = Истина;
			Элементы.ВариантЗагрузкиПериода.ТолькоПросмотр    = Истина;
			Элементы.ПользовательскаяНастройка.ТолькоПросмотр = Истина;
			Элементы.ГруппаДоступности.ТолькоПросмотр         = Истина;
			Элементы.Сохранить.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	КлючОтчета   = Параметры.КлючОбъекта;
	КлючВарианта = Параметры.КлючТекущихНастроек;
	
	МетаданныеОтчета = Метаданные.НайтиПоПолномуИмени(Параметры.КлючОбъекта);
	Если МетаданныеОтчета = Неопределено Тогда
		ПозицияТочки = Найти(Параметры.КлючОбъекта, ".");
		Если ПозицияТочки>0 Тогда
			ИмяОтчета = Сред(Параметры.КлючОбъекта, ПозицияТочки+1);
		Иначе
			ИмяОтчета = Параметры.КлючОбъекта;
		КонецЕсли;
	Иначе
		ИмяОтчета = МетаданныеОтчета.Имя;
	КонецЕсли;
	
	ТекущийПользователь  = ПараметрыСеанса.Пользователь;
	ТекущаяОрганизация   = ПараметрыСеанса.Организация;
	ТекущееПодразделение = ПараметрыСеанса.ПодразделениеКомпании;
	
	Если обПраво("РазрешитьРедактироватьОбщиеВариантыОтчетов") Тогда
		ЭтаФорма.Элементы.ГруппаДоступности.Доступность         = Истина;
		ЭтаФорма.Элементы.ПользовательскаяНастройка.Доступность = Истина;
		ПолныеПраваНаВарианты = Истина;
	Иначе
		ЭтаФорма.Элементы.ГруппаДоступности.Доступность         = Ложь;
		ЭтаФорма.Элементы.ПользовательскаяНастройка.Доступность = Ложь;
		ПолныеПраваНаВарианты = Ложь;
	КонецЕсли;
	
	СписокВыбора = Элементы.ВариантЗагрузкиПериода.СписокВыбора;
	Для Каждого ТекЭлемент Из ВариантСтандартногоПериода Цикл
		
		ИмяЭлемента = ПолучитьПолноеИмяПредопределенногоЗначения(ТекЭлемент);
		Если ИмяЭлемента = "ВариантСтандартногоПериода.ПроизвольныйПериод" Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВыбора.Добавить(ПолучитьПолноеИмяПредопределенногоЗначения(ТекЭлемент), Строка(ТекЭлемент));
		
	КонецЦикла;
	
	ЗаполнитьСписокВариантов();
	
	Если ВариантОтчета.Пустая() Тогда
		СоздатьНовый = Истина;
	Иначе
		СоздатьНовый = Ложь;
	КонецЕсли;
	
	ОбновитьФормуВарианта(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Поле ""Наименование"" не заполнено'"),
			,
			"Наименование"
		);
		Отказ = Истина;
	ИначеЕсли ХранилищаНастроек.ХранилищеВариантовОтчетов.НаименованиеЗанято(КлючОтчета, ВариантОтчета, Наименование) Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = '"""+СокрЛП(Наименование)+""" занято, необходимо указать другое Наименование.'"),, "Наименование");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ИмяФормы Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.Наименование;
	УстановитьОграничениеТипа();
	
	Если СоздатьНовый Тогда
		Элементы.ГруппаНаименование.ТекущаяСтраница = Элементы.СтраницаНаименование;
	Иначе
		Элементы.ГруппаНаименование.ТекущаяСтраница = Элементы.СтраницаВариант;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Функция ПерейтиНаСтраницу2Клиент()
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		//	НСтр("ru = 'Поле ""Наименование"" не заполнено'"),
		//	,
		//	"ВариантНаименование"
		//);
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВариантОтчета) Тогда
		Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Ссылка", ВариантОтчета));
		Вариант = Найденные[0];
		Если НЕ ПравоЗаписиВарианта(Вариант, ПравоНастройкиВарианта(Вариант, ПолныеПраваНаВарианты)) Тогда
			//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			//	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			//		НСтр("ru = 'Недостаточно прав на изменение варианта ""%1"", необходимо выбрать другой вариант или изменить Наименование.'"),
			//		ВариантНаименование
			//	),
			//	,
			//	"ВариантНаименование"
			//);
			Возврат Ложь;
		КонецЕсли;
		
		Если Вариант.ПометкаУдаления = Истина Тогда
			ТекстВопроса = НСтр("ru = 'Вариант отчета """+Наименование+""" помечен на удаление.
			|Заменить помеченный на удаление вариант отчета?'");
			КнопкаПоУмолчанию = КодВозвратаДиалога.Нет;
		Иначе
			ТекстВопроса = НСтр("ru = 'Заменить ранее сохраненный вариант отчета """+Наименование+"""?'");
			КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
		КонецЕсли;
		
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КнопкаПоУмолчанию); 
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страница2;
	Элементы.Назад.Доступность        = Истина;
	Элементы.Далее.Доступность        = Ложь;
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура ОткрытьВариантДляИзменения()
	
	Если ЗначениеЗаполнено(ВариантОтчета) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоНастройкиВарианта(ВариантОтчета, ПолныеПраваНаВарианты) Тогда
		Предупреждение(НСтр("ru = 'Недостаточно прав доступа для изменения варианта """+Наименование+""".'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьЗначение(ВариантОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Если ПроверитьИЗаписатьНаСервере() И ЗначениеЗаполнено(КлючВарианта) Тогда
		
		//ВариантыОтчетовКлиент.ОбновитьОткрытыеПанелиОтчетов(, ИмяФормы);
		ПараметрОповещения = Новый Структура("КлючОтчета, ИмяОтчета, КлючВарианта, НаименованиеВарианта, ВариантЭтоНовый", КлючОтчета, ИмяОтчета, КлючВарианта, Наименование, СоздатьНовый);
		
		Оповестить("ВариантыОтчетов.ИзменениеВарианта", ПараметрОповещения, ИмяФормы);
		
		Закрыть(Новый ВыборНастроек(КлючВарианта));
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Клиент и сервер

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьФормуВарианта(ЭтаФорма)
	
	Если ЭтаФорма.СоздатьНовый Тогда
		//ЭтаФорма.ВариантОтчета             = ПредопределенноеЗначение("Справочник.ВариантыОтчетов.ПустаяСсылка");
		ЭтаФорма.ПользовательскаяНастройка = 1;
		ЭтаФорма.Элементы.Сохранить.Доступность = Истина;
	Иначе
		
		Варианты = ЭтаФорма.ВариантыОтчета.НайтиСтроки(Новый Структура("Ссылка", ЭтаФорма.ВариантОтчета));
		Если Варианты.Количество() = 0 Тогда
			#Если Сервер Тогда
				ЭтаФорма.ВариантЗагрузкиПериода = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьВариантПериода(ЭтаФорма.КлючОтчета, ЭтаФорма.КлючВарианта);
			#КонецЕсли
			Возврат;
		КонецЕсли;
		
		Вариант = Варианты[0];
		
		ЭтаФорма.НазначениеДоступа         = Вариант.НазначениеДоступа;
		ЭтаФорма.Наименование              = Вариант.Наименование;
		ЭтаФорма.ОбъектДоступа             = Вариант.ОбъектДоступа;
		ЭтаФорма.ВариантОтчета             = Вариант.Ссылка;
		ЭтаФорма.ПользовательскаяНастройка = ?(Вариант.ПользовательскаяНастройка, 1, 2);
		ЭтаФорма.Описание                  = Вариант.Описание;
		ЭтаФорма.ОбъектДоступа             = Вариант.ОбъектДоступа;
		ЭтаФорма.ПредставлениеОтчета       = Вариант.ПредставлениеОтчета;
		ЭтаФорма.ВариантЗагрузкиПериода    = Вариант.ВариантЗагрузкиПериода;
		
		Если Вариант.Предопределенный Тогда
			ЭтаФорма.Элементы.ГруппаДоступности.Доступность         = Ложь;
			ЭтаФорма.Элементы.ПользовательскаяНастройка.Доступность = Ложь;
		Иначе
			ЭтаФорма.Элементы.ГруппаДоступности.Доступность         = Истина;
			ЭтаФорма.Элементы.ПользовательскаяНастройка.Доступность = Истина;
		КонецЕсли;
		
		Если ЭтаФорма.ВариантЗагрузкиПериода = "" Тогда
			ЭтаФорма.ВариантЗагрузкиПериода = "ВариантСтандартногоПериода.ЭтотМесяц";
		КонецЕсли;
		
		Если НЕ ЭтаФорма.ПолныеПраваНаВарианты Тогда
			
			ЭтаФорма.Элементы.ГруппаДоступности.Доступность         = Ложь;
			ЭтаФорма.Элементы.ПользовательскаяНастройка.Доступность = Ложь;
			
			Если Вариант.ПользовательскаяНастройка Тогда
				ЭтаФорма.Элементы.Сохранить.Доступность = Истина;
			Иначе
				ЭтаФорма.Элементы.Сохранить.Доступность = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПравоНастройкиВарианта(Вариант, ПолныеПраваНаВарианты)
	
	Возврат (ПолныеПраваНаВарианты ИЛИ Вариант.АвторТекущийПользователь) И ЗначениеЗаполнено(Вариант.Ссылка);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПравоЗаписиВарианта(Вариант, ПравоНастройкиВарианта)
	
	Возврат (Вариант.ПользовательскаяНастройка = 1) И ПравоНастройкиВарианта;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьСвободноеНаименование(Вариант, ВариантыОтчета)
	//ШаблонИмениВарианта = СокрЛП(Вариант.Наименование) +" - "+ НСтр("ru = 'копия'");
	//
	//СвободноеНаименование = ШаблонИмениВарианта;
	//Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Наименование", СвободноеНаименование));
	//Если Найденные.Количество() = 0 Тогда
	//	Возврат СвободноеНаименование;
	//КонецЕсли;
	//
	//НомерВарианта = 1;
	//Пока Истина Цикл
	//	НомерВарианта = НомерВарианта + 1;
	//	СвободноеНаименование = ШаблонИмениВарианта +" (" + Формат(НомерВарианта, "") + ")";
	//	Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Наименование", СвободноеНаименование));
	//	Если Найденные.Количество() = 0 Тогда
	//		Возврат СвободноеНаименование;
	//	КонецЕсли;
	//КонецЦикла;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Вызов сервера

&НаСервереБезКонтекста
Процедура УдалитьВариантНаСервере(Ссылка, ИндексКартинки, ПометкаУдаления)
	
	ВариантОбъект = Ссылка.ПолучитьОбъект();
	ВариантОбъект.УстановитьПометкуУдаления(НЕ ВариантОбъект.ПометкаУдаления);
	ПометкаУдаления = ВариантОбъект.ПометкаУдаления;
	ИндексКартинки = ?(ПометкаУдаления, 4, 3);
	
КонецПроцедуры

&НаСервере
Функция ПерезаполнитьДерево()
	
	//Если ЗначениеЗаполнено(ВариантСсылка) Тогда
	//	ВариантОснование = ВариантСсылка;
	//Иначе
	//	ВариантОснование = ПрототипСсылка;
	//КонецЕсли;
	
	//ДеревоПриемник = ВариантыОтчетов.ДеревоПодсистемСформировать(ЭтаФорма, ВариантОснование);
	
	//Найденные = ДеревоПриемник.Строки.НайтиСтроки(Новый Структура("Использование", 1), Истина);
	//Для Каждого Приемник Из Найденные Цикл
	//	Приемник.ИспользованиеПоУмолчанию = 0;
	//	Приемник.ВажностьПоУмолчанию      = "";
	//	Приемник.ИзмененПользователем     = Истина;
	//КонецЦикла;
	//
	//ЗначениеВРеквизитФормы(ДеревоПриемник, "ДеревоПодсистем");
	//
	Возврат Истина;
КонецФункции

&НаСервере
Функция ПроверитьИЗаписатьНаСервере()
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам") Тогда
		СправочникИмя = Метаданные.Справочники.ВариантыОтчетов.Имя;
		ПравоДоступа = обПраво("Право доступа "+СправочникИмя);
		Если ПравоДоступа=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Доступ запрещен.'"));
			Возврат Ложь;
		ИначеЕсли ПравоДоступа=Перечисления.ВидыПравДляСправочников.Чтение Тогда
			ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрещено редактирование.'"));
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено Наименование.'"));
		Возврат Ложь;
	КонецЕсли;
	
	//ВариантЭтоНовый = НЕ ЗначениеЗаполнено(ВариантОтчета);
	
	Если СоздатьНовый И ХранилищаНастроек.ХранилищеВариантовОтчетов.НаименованиеЗанято(КлючОтчета, ВариантОтчета, Наименование) Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = '"""+СокрЛП(Наименование)+""" занято, необходимо указать другое Наименование.'"),, "Наименование");
		Возврат Ложь;
	ИначеЕсли ВариантОтчета.ЭтоПредопределенный Тогда
		ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = '"""+СокрЛП(Наименование)+""" занято, необходимо указать другое Наименование.'"),, "Наименование");
		Возврат Ложь;
	КонецЕсли;
	
	Если СоздатьНовый Тогда
		ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		ВариантОбъект.КлючОтчета            = КлючОтчета;
		ВариантОбъект.ИмяОтчета             = ИмяОтчета;
		ВариантОбъект.КлючВарианта          = Строка(Новый УникальныйИдентификатор());
	Иначе
		ВариантОбъект = ВариантОтчета.ПолучитьОбъект();
	КонецЕсли;
	
	ВариантОбъект.Автор                     = ТекущийПользователь;
	ВариантОбъект.Наименование              = Наименование;
	ВариантОбъект.Описание                  = Описание;
	ВариантОбъект.НазначениеДоступа         = НазначениеДоступа;
	ВариантОбъект.ПользовательскаяНастройка = ?(ПользовательскаяНастройка = 1, Истина, Ложь);
	ВариантОбъект.ПредставлениеОтчета       = ПредставлениеОтчета;
	ВариантОбъект.ВариантЗагрузкиПериода    = ВариантЗагрузкиПериода;
	
	Если ПользовательскаяНастройка = 1 Тогда
		ВариантОбъект.ОбъектДоступа = Неопределено;
	ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Организация") Тогда
		ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Организации");
		ВариантОбъект.ОбъектДоступа = ДопустимыеТипы.ПривестиЗначение(ОбъектДоступа);
	ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Подразделение") Тогда
		ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании");
		ВариантОбъект.ОбъектДоступа = ДопустимыеТипы.ПривестиЗначение(ОбъектДоступа);
	ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Пользователь") Тогда
		ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		ВариантОбъект.ОбъектДоступа = ДопустимыеТипы.ПривестиЗначение(ОбъектДоступа);
	Иначе
		ВариантОбъект.ОбъектДоступа = Неопределено;
	КонецЕсли;
	
	ВариантОбъект.Записать();
	
	ВариантОтчета = ВариантОбъект.Ссылка;
	КлючВарианта  = ВариантОбъект.КлючВарианта;
	
	Возврат Истина;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МногострочныйТекст   = Описание;
	
	ТекстВведен = ВвестиСтроку(МногострочныйТекст, НСтр("ru = 'Описание'"),, Истина);
	
	Если ТекстВведен Тогда
		Описание = МногострочныйТекст;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВариантов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.ВерсияДанных,
	|	ВариантыОтчетов.ПометкаУдаления,
	|	ВариантыОтчетов.ЭтоПредопределенный КАК Предопределенный,
	|	ВариантыОтчетов.Наименование,
	|	ВариантыОтчетов.ПредставлениеОтчета КАК ПредставлениеОтчета,
	|	ВариантыОтчетов.КлючОтчета КАК КлючОтчета,
	|	ВариантыОтчетов.ИмяОтчета КАК ИмяОтчета,
	|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	|	ВариантыОтчетов.Автор КАК Автор,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.Автор = &Пользователь
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АвторТекущийПользователь,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ПометкаУдаления
	|			ТОГДА 4
	|		КОГДА ВариантыОтчетов.ЭтоПредопределенный
	|			ТОГДА 5
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ПометкаУдаления
	|			ТОГДА 3
	|		КОГДА ВариантыОтчетов.ЭтоПредопределенный
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ВариантыОтчетов.Настройки КАК Настройки,
	|	ВариантыОтчетов.Описание КАК Описание,
	|	ВариантыОтчетов.НазначениеДоступа КАК НазначениеДоступа,
	|	ВариантыОтчетов.ОбъектДоступа КАК ОбъектДоступа,
	|	ВариантыОтчетов.ВариантЗагрузкиПериода КАК ВариантЗагрузкиПериода,
	|	ВариантыОтчетов.ПользовательскаяНастройка КАК ПользовательскаяНастройка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.ЭтоПредопределенный = ЛОЖЬ И 
	|	ВариантыОтчетов.КлючОтчета = &КлючОтчета И 
	|	(ВариантыОтчетов.Автор = &Пользователь ИЛИ 
	|	(ВариантыОтчетов.ПользовательскаяНастройка = ЛОЖЬ И 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Пользователь) И ВариантыОтчетов.ОбъектДоступа = &Пользователь) ИЛИ 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Подразделение) И ВариантыОтчетов.ОбъектДоступа = &Подразделение) ИЛИ 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Организация) И ВариантыОтчетов.ОбъектДоступа = &Организация) ИЛИ 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Компания) И ВариантыОтчетов.ОбъектДоступа = НЕОПРЕДЕЛЕНО)))
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ПометкаУдаления
	|			ТОГДА 3
	|		КОГДА ВариантыОтчетов.ЭтоПредопределенный
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ,
	|	ВариантыОтчетов.Наименование";
	
	ВариантыОтчета.Очистить();
	
	Запрос.УстановитьПараметр("КлючОтчета",    КлючОтчета);
	Запрос.УстановитьПараметр("Пользователь",  ТекущийПользователь);
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("Организация",   ТекущаяОрганизация);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	ВариантыОтчета.Загрузить(ТаблицаЗначений);
	
	СписокВариантов = Элементы.ВариантОтчета.СписокВыбора;
	Для Каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
		СписокВариантов.Добавить(ТекущаяСтрока.Ссылка, ТекущаяСтрока.Наименование);
	КонецЦикла;
	
	//ВариантыОтчета.Сортировать("Порядок, Наименование Возр");
	
	ИдентификаторТекущейСтроки = -1;
	Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("КлючВарианта", КлючВарианта));
	Если Найденные.Количество() > 0 Тогда
		Вариант = Найденные[0];
		ВариантОтчета             = Вариант.Ссылка;
		Описание                  = Вариант.Описание;
		НазначениеДоступа         = Вариант.НазначениеДоступа;
		Наименование              = Вариант.Наименование;
		ОбъектДоступа             = Вариант.ОбъектДоступа;
		ПользовательскаяНастройка = ?(Вариант.ПользовательскаяНастройка=1, Истина, Ложь);
		ПредставлениеОтчета       = Вариант.ПредставлениеОтчета;
		
		ИдентификаторТекущейСтроки            = Вариант.ПолучитьИдентификатор();
		
		ВариантЗагрузкиПериода = ?(Вариант.ВариантЗагрузкиПериода = "", "ВариантСтандартногоПериода.ЭтотМесяц", Вариант.ВариантЗагрузкиПериода);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеДоступаПриИзменении(Элемент)
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскаяНастройкаПриИзменении(Элемент)
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьВариантДляИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйПриИзменении(Элемент)
	
	Если СоздатьНовый Тогда
		Элементы.ГруппаНаименование.ТекущаяСтраница = Элементы.СтраницаНаименование;
	Иначе
		Элементы.ГруппаНаименование.ТекущаяСтраница = Элементы.СтраницаВариант;
	КонецЕсли;
	
	ОбновитьФормуВарианта(ЭтаФорма);
	УстановитьОграничениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтчетаПриИзменении(Элемент)
	
	ОбновитьФормуВарианта(ЭтаФорма);
	УстановитьОграничениеТипа();
	
КонецПроцедуры


Перем Права Экспорт;


Функция ХешированиеДанных(Знач Данные)
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Для Каждого Значение Из Данные Цикл
		Хеш.Добавить(Значение);
	КонецЦикла;
	Возврат СтрЗаменить(Хеш.ХешСумма, " ", "");
КонецФункции

// процедура разбора строки символов на массив символов
//
// Параметры:
// СтрокаДляРазбора - Строка - Строка для разбора
//
// Возвращаемое значение:
// Массив - Массив символов строки
//
Функция РазобратьСтрокуTXT(Знач СтрокаДляРазбора, Разделитель=" ")
	
	МассивСтрокиФайлаПрайсЛиста = Новый Массив();
	
	Пока Найти(СтрокаДляРазбора, "  ") > 0 Цикл
		СтрокаДляРазбора = СтрЗаменить(СтрокаДляРазбора, "  ", " ");
	КонецЦикла;
	
	Пока СтрДлина(СтрокаДляРазбора) > 0 Цикл
		ПозицияРазделителя = Найти(СтрокаДляРазбора ," ");
		Если ПозицияРазделителя = 0 Тогда
			ПозицияРазделителя = СтрДлина(СтрокаДляРазбора) + 1;
		КонецЕсли;
		СтрокаПараметр = Лев(СтрокаДляРазбора, ПозицияРазделителя-1);
		СтрокаДляРазбора = Сред(СтрокаДляРазбора, ПозицияРазделителя+1);
		МассивСтрокиФайлаПрайсЛиста.Добавить(СтрокаПараметр);
	КонецЦикла; 
	
	Возврат МассивСтрокиФайлаПрайсЛиста;
	
КонецФункции // РазобратьСтрокуTXT()

Функция НайтиНоменклатуруПоНаименованию(Знач Наименование, ЕстьЕще)
	
	Если обЗначениеНеЗаполнено(Наименование) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	МассивНаименований = РазобратьСтрокуTXT(Наименование);
	
	Если МассивНаименований.Количество() = 0 Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 101
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа";
	
	Сч = 0;
	Для Каждого Наименование Из МассивНаименований Цикл
		Сч = Сч + 1;
		ИмяПараметра = "Наименование"+Формат(Сч, "ЧГ=0");
		Запрос.Текст = Запрос.Текст + " И Номенклатура.Наименование ПОДОБНО &" + ИмяПараметра;
		Запрос.УстановитьПараметр(ИмяПараметра, "%"+Наименование+"%");
	КонецЦикла;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Наименование,
	|	Номенклатура.Производитель.Наименование,
	|	Номенклатура.АртикулДляПоиска";
	
	Результат = Запрос.Выполнить().Выгрузить();
	ЕстьЕще = Результат.Количество() = 101;
	Если ЕстьЕще Тогда
		Результат.Удалить(100);
	КонецЕсли;
	
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция НайтиНоменклатуруПоЧастиАртикула(Знач СтрокаПоиска, ЕстьЕще)
	
	Если обЗначениеНеЗаполнено(СтрокаПоиска) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	//МассивЧастей = РазобратьСтрокуTXT(СтрокаПоиска);
	//
	//Если МассивЧастей.Количество() = 0 Тогда
	//	Возврат Новый Массив;
	//КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 101
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа";
	
	//Сч = 0;
	//Для Каждого ЧастьАртикула Из МассивЧастей Цикл
	//	Сч = Сч + 1;
	//	ИмяПараметра = "Артикул"+Формат(Сч, "ЧГ=0");
	//	Запрос.Текст = Запрос.Текст + " И Номенклатура.АртикулДляПоиска ПОДОБНО &" + ИмяПараметра;
	//	Запрос.УстановитьПараметр(ИмяПараметра, "%"+Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(ЧастьАртикула)+"%");
	//КонецЦикла;
	
	Запрос.Текст = Запрос.Текст + " И (Номенклатура.АртикулДляПоиска ПОДОБНО &АртикулДляПоиска ИЛИ Номенклатура.АртикулДляПоиска ПОДОБНО &Артикул)";
	Запрос.УстановитьПараметр("АртикулДляПоиска", "%"+Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(СтрокаПоиска)+"%");
	Запрос.УстановитьПараметр("Артикул", "%"+СокрЛП(СтрокаПоиска)+"%");
	
	Запрос.Текст = Запрос.Текст + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Наименование,
	|	Номенклатура.Производитель.Наименование,
	|	Номенклатура.АртикулДляПоиска";
	
	Результат = Запрос.Выполнить().Выгрузить();
	ЕстьЕще = Результат.Количество() = 101;
	Если ЕстьЕще Тогда
		Результат.Удалить(100);
	КонецЕсли;
	
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Процедура ЗаполнитьАртикулыПоНоменклатуре() Экспорт
	
	Если Номенклатура.Пустая() И ПустаяСтрока(Артикул) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		АртикулДляПоиска = Номенклатура.АртикулДляПоиска;
		
		СтрокаАртикул = Артикулы.Добавить();
		СтрокаАртикул.Пометка = Истина;
		СтрокаАртикул.Артикул = Номенклатура.Артикул;
		СтрокаАртикул.АртикулДляПоиска = АртикулДляПоиска;
		СтрокаАртикул.Производитель = Номенклатура.Производитель;
		СтрокаАртикул.Номенклатура = Номенклатура;
		
		ПроизводительСервиса = атСервер.ПолучитьСловарьПоСсылке(Номенклатура.Производитель, Ложь);
		Если ЗначениеЗаполнено(ПроизводительСервиса) Тогда
			СтрокаАртикул.ПроизводительКод = ПроизводительСервиса.Код;
		Иначе
			СтрокаАртикул.ПроизводительКод = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АртикулДляПоиска) Тогда
			Данные = Новый Массив;
			Данные.Добавить(?(Номенклатура.Производитель.Пустая(), "000000000", Номенклатура.Производитель.Код));
			Данные.Добавить("_");
			Данные.Добавить(АртикулДляПоиска);
			СтрокаАртикул.ХешАртикулПроизводитель = ХешированиеДанных(Данные);
		Иначе
			СтрокаАртикул.ХешАртикулПроизводитель = "" + Номенклатура.УникальныйИдентификатор();
		КонецЕсли;
		
		СтрокаАртикул.ИсточникСтроки = "Номенклатура";
		СтрокаАртикул.Онлайн = Ложь;
		СтрокаАртикул.Пометка = Истина;
		
	Иначе
		
		АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, Производитель);
		Если ПустаяСтрока(АртикулДляПоиска) Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаАртикул = Артикулы.Добавить();
		СтрокаАртикул.Пометка = Истина;
		СтрокаАртикул.Артикул = Артикул;
		СтрокаАртикул.АртикулДляПоиска = АртикулДляПоиска;
		СтрокаАртикул.Производитель = Производитель;
		Номенклатура = атСервер.НайтиНоменклатуру(АртикулДляПоиска, Производитель);
		СтрокаАртикул.Номенклатура = Номенклатура;
		
		Если Не Производитель.Пустая() Тогда
			ПроизводительСервиса = атСервер.ПолучитьСловарьПоСсылке(Производитель, Ложь);
			Если ЗначениеЗаполнено(ПроизводительСервиса) Тогда
				СтрокаАртикул.ПроизводительКод = ПроизводительСервиса.Код;
			КонецЕсли;
		КонецЕсли;
		
		Данные = Новый Массив;
		Данные.Добавить(?(Производитель.Пустая(), "000000000", Производитель.Код));
		Данные.Добавить("_");
		Данные.Добавить(АртикулДляПоиска);
		
		СтрокаАртикул.ХешАртикулПроизводитель = ХешированиеДанных(Данные);
		СтрокаАртикул.ИсточникСтроки = "Артикул";
		СтрокаАртикул.Онлайн = Ложь;
		СтрокаАртикул.Пометка = Истина;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АртикулДляПоиска) Тогда
		// Сами себя в аналоги добавим в обязательном порядке!
		НоваяСтрока = Аналоги.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАртикул);
		НоваяСтрока.Раздел = 0;
		НоваяСтрока.ВидДополнения = "Группа аналогов";
		НоваяСтрока.ХешАналога = СтрокаАртикул.ХешАртикулПроизводитель;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАртикулыНоменклатурой(Ошибка) Экспорт
	
	Если обЗначениеНеЗаполнено(Артикул) Тогда // Строка поиска пустая
		Возврат;
	КонецЕсли;
	
	ЕстьЕще = Ложь;
	
	РезультатПоиска = НайтиНоменклатуруПоНаименованию(Артикул, ЕстьЕще);
	Если ЕстьЕще = Истина Тогда
		Ошибка = "По наименованию найдено более 100 позиций. Будут показаны только первые 100.";
	КонецЕсли;
	
	Для Каждого НайденнаяНоменклатура Из РезультатПоиска Цикл
		
		ПроизводительКод = "";
		Если ЗначениеЗаполнено(НайденнаяНоменклатура.Производитель) Тогда
			ПроизводительСловарь = атСервер.ПолучитьСловарьПоСсылке(НайденнаяНоменклатура.Производитель);
			Если ПроизводительСловарь <> Неопределено Тогда
				ПроизводительКод = ПроизводительСловарь.Код;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденнаяНоменклатура.АртикулДляПоиска) Тогда
			Данные = Новый Массив;
			Данные.Добавить(?(НайденнаяНоменклатура.Производитель.Пустая(), "000000000", НайденнаяНоменклатура.Производитель.Код));
			Данные.Добавить("_");
			Данные.Добавить(НайденнаяНоменклатура.АртикулДляПоиска);
			ХешАртикулПроизводитель = атСервер.ХешированиеДанных(Данные);
		Иначе
			ХешАртикулПроизводитель = ""+НайденнаяНоменклатура.УникальныйИдентификатор();
		КонецЕсли;
		
		Если Артикулы.Найти(ХешАртикулПроизводитель, "ХешАртикулПроизводитель") = Неопределено Тогда
			СтрокаАртикула = Артикулы.Добавить();
			СтрокаАртикула.Артикул = НайденнаяНоменклатура.Артикул;
			СтрокаАртикула.АртикулДляПоиска = НайденнаяНоменклатура.АртикулДляПоиска;
			СтрокаАртикула.ПроизводительКод = ПроизводительКод;
			СтрокаАртикула.Производитель = НайденнаяНоменклатура.Производитель;
			СтрокаАртикула.Номенклатура = НайденнаяНоменклатура;
			СтрокаАртикула.Наименование = НайденнаяНоменклатура.Наименование;
			СтрокаАртикула.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
			СтрокаАртикула.ИсточникСтроки = "Наименование";
			СтрокаАртикула.Онлайн = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	#Если Клиент Тогда
		
	РезультатПоиска = НайтиНоменклатуруПоЧастиАртикула(Артикул, ЕстьЕще);
	Если ЕстьЕще = Истина Тогда
		Ошибка = "По части артикула найдено более 100 позиций. Будут показаны только первые 100.";
	КонецЕсли;
	
	//РезультатПоиска = зфПоискСПреобразованиемАртикула(Артикул, "Номенклатура");
	//
	//Если ТипЗнч(РезультатПоиска) = Тип("СписокЗначений") Тогда
	//	РезультатПоиска = РезультатПоиска.ВыгрузитьЗначения();
	//КонецЕсли;
	
	Для Каждого НайденнаяНоменклатура Из РезультатПоиска Цикл
		
		ПроизводительКод = "";
		Если ЗначениеЗаполнено(НайденнаяНоменклатура.Производитель) Тогда
			ПроизводительСловарь = атСервер.ПолучитьСловарьПоСсылке(НайденнаяНоменклатура.Производитель);
			Если ПроизводительСловарь <> Неопределено Тогда
				ПроизводительКод = ПроизводительСловарь.Код;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденнаяНоменклатура.АртикулДляПоиска) Тогда
			Данные = Новый Массив;
			Данные.Добавить(?(НайденнаяНоменклатура.Производитель.Пустая(), "000000000", НайденнаяНоменклатура.Производитель.Код));
			Данные.Добавить("_");
			Данные.Добавить(НайденнаяНоменклатура.АртикулДляПоиска);
			ХешАртикулПроизводитель = атСервер.ХешированиеДанных(Данные);
		Иначе
			ХешАртикулПроизводитель = ""+НайденнаяНоменклатура.УникальныйИдентификатор();
		КонецЕсли;
		
		Если Артикулы.Найти(ХешАртикулПроизводитель, "ХешАртикулПроизводитель") = Неопределено Тогда
			СтрокаАртикула = Артикулы.Добавить();
			СтрокаАртикула.Артикул = НайденнаяНоменклатура.Артикул;
			СтрокаАртикула.АртикулДляПоиска = НайденнаяНоменклатура.АртикулДляПоиска;
			СтрокаАртикула.ПроизводительКод = ПроизводительКод;
			СтрокаАртикула.Производитель = НайденнаяНоменклатура.Производитель;
			СтрокаАртикула.Номенклатура = НайденнаяНоменклатура;
			СтрокаАртикула.Наименование = НайденнаяНоменклатура.Наименование;
			СтрокаАртикула.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
			СтрокаАртикула.ИсточникСтроки = "Часть артикула";
			СтрокаАртикула.Онлайн = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

Процедура ЗаполнитьАртикулыПроизводителями(Знач НеИспользоватьПомощникПродаж, Знач ОбновитьКеш, Ошибка) Экспорт
	
	Если обЗначениеНеЗаполнено(Артикул) Тогда // Строка поиска пустая
		Возврат;
	КонецЕсли;
	
	МассивАртикулов = атСервер.НайтиПроизводителейАртикула(Артикул, Истина, Истина, НеИспользоватьПомощникПродаж, Ошибка, ОбновитьКеш);
	
	Для Каждого СтрокаПроизводитель Из МассивАртикулов Цикл
		
		НайденнаяСтрока = Артикулы.Найти(СтрокаПроизводитель.ХешАртикулПроизводитель, "ХешАртикулПроизводитель");
		
		Если НайденнаяСтрока = Неопределено Тогда
			СтрокаАртикула = Артикулы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаАртикула, СтрокаПроизводитель);
			СтрокаАртикула.ИсточникСтроки = "Артикул";
		Иначе
			НайденнаяСтрока.ИсточникСтроки = "Артикул";
		КонецЕсли;
		
		Если Аналоги.Найти(СтрокаПроизводитель.ХешАртикулПроизводитель, "ХешАртикулПроизводитель") = Неопределено Тогда
			Для Каждого АналогАртикула Из СтрокаПроизводитель.Аналоги Цикл
				СтрокаАналога = Аналоги.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаАналога, АналогАртикула);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Артикулы.Количество() = 0 Тогда
		СтрокаАртикула = Артикулы.Добавить();
		СтрокаАртикула.Артикул = Артикул;
		СтрокаАртикула.АртикулДляПоиска = Артикул;
		СтрокаАртикула.ПроизводительКод = "";
		СтрокаАртикула.Производитель = "< Неизвестен >";
		СтрокаАртикула.Наименование = "< Не найден >";
		СтрокаАртикула.НеНайден = Истина;
		СтрокаАртикула.ХешАртикулПроизводитель = "";
		СтрокаАртикула.ИсточникСтроки = "";
		СтрокаАртикула.Онлайн = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьВПрайсЛистыКонтрагентов(ТаблицаЦен, ВидПредложения, МассивЗаменОтПоставщика=Неопределено)
	
	Если ТаблицаЦен = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаЦен Цикл
		
		НоваяСтрока = ПрайсЛистыКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ВидПредложения = ВидПредложения;
		НоваяСтрока.Раздел = ?(НоваяСтрока.ВидПредложения > 2, 2, 1); // Сначала со склада и из заказов, затем по прайс-листам
		НоваяСтрока.Аналог = СтрокаТаблицы.Раздел > 0;                // Аналоги - это все, кроме самого артикула!
		НоваяСтрока.ВоВнешнемИсточнике = Не СтрокаТаблицы.ЕстьВПодразделении;
		
		Если ПустаяСтрока(НоваяСтрока.ХешАналога) Тогда
			Если ЗначениеЗаполнено(НоваяСтрока.АртикулДляПоиска) Тогда
				Данные = Новый Массив;
				Данные.Добавить(?(обЗначениеНеЗаполнено(НоваяСтрока.Производитель), "000000000", ?(ТипЗнч(НоваяСтрока.Производитель)=Тип("СправочникСсылка.Производители"), НоваяСтрока.Производитель.Код, НоваяСтрока.Производитель)));
				Данные.Добавить("_");
				Данные.Добавить(НоваяСтрока.АртикулДляПоиска);
				ХешАналога = атСервер.ХешированиеДанных(Данные);
			Иначе
				ХешАналога = "" + НоваяСтрока.Номенклатура.УникальныйИдентификатор();
			КонецЕсли;
			НоваяСтрока.ХешАналога = ХешАналога;
			
			Если Аналоги.НайтиСтроки(Новый Структура("ХешАртикулПроизводитель, ХешАналога", НоваяСтрока.ХешАртикулПроизводитель, НоваяСтрока.ХешАналога)).Количество() = 0 Тогда
				НоваяСтрокаАналог = Аналоги.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаАналог, НоваяСтрока);
				НоваяСтрокаАналог.Раздел = 9;
				Если МассивЗаменОтПоставщика <> Неопределено Тогда
					СтрокаАналог = атСервер.СтруктураСтрокиАналога();
					ЗаполнитьЗначенияСвойств(СтрокаАналог, НоваяСтрокаАналог);
					МассивЗаменОтПоставщика.Добавить(СтрокаАналог);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьАналогиОтображение(АналогиОтображение, флАналоги, флТолькоГруппыАналогов) Экспорт
	
	АналогиОтображение.Очистить();
	
	Для Каждого СтрокаАртикула Из Артикулы Цикл
	
		Если СтрокаАртикула.Пометка Тогда
			
			СтруктураОтбораАналогов = Новый Структура("ХешАртикулПроизводитель", СтрокаАртикула.ХешАртикулПроизводитель);
			
			НайденныеАналоги = Аналоги.НайтиСтроки(СтруктураОтбораАналогов);
			
			Для Каждого СтрокаАналога ИЗ НайденныеАналоги Цикл
				
				Если АналогиОтображение.Найти(СтрокаАналога.ХешАналога, "ХешАналога") = Неопределено Тогда
					
					СтрокаТаблицы = АналогиОтображение.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаАналога);
					СтрокаТаблицы.ЕстьПредложения = Ложь;
					СтрокаТаблицы.Цена = 0;
					СтрокаТаблицы.ЦенаМин_СрокПоставки = "";
					СтрокаТаблицы.СрокПоставкиМин_Цена = "";
					
					Если Не флАналоги И СтрокаАналога.Раздел <> 0 Тогда
						Продолжить;
					КонецЕсли;
					Если флТолькоГруппыАналогов И СтрокаАналога.ВидДополнения <> "Группа аналогов" Тогда
						Продолжить;
					КонецЕсли;
					
					ТаблицаЦен = ПрайсЛистыКонтрагентов.Выгрузить(Новый Структура("ХешАналога", СтрокаАналога.ХешАналога), "Цена, СрокПоставки, СрокПоставкиМинимальный, ВидПредложения");
					
					Если ТаблицаЦен.Количество() > 0 Тогда
						
						СтрокаТаблицы.ЕстьПредложения = Истина;
						
						ТаблицаЦен.Сортировать("Цена, СрокПоставкиМинимальный, ВидПредложения");
						Для Каждого СтрокаЦены ИЗ ТаблицаЦен Цикл
							Если СтрокаЦены.Цена > 0 Тогда
								ЦенаМин_СрокПоставкиМинимальный = СтрокаЦены.СрокПоставкиМинимальный;
								СтрокаТаблицы.ЦенаМин_ВидПредложения = СтрокаЦены.ВидПредложения;
								СтрокаТаблицы.Цена = СтрокаЦены.Цена;
								СтрокаТаблицы.ЦенаМин_СрокПоставки = Формат(СтрокаЦены.Цена, "ЧЦ=15; ЧДЦ=2") + ?(СтрокаЦены.ВидПредложения = 1, "", ?(ЗначениеЗаполнено(СтрокаЦены.СрокПоставки), " ("+СокрЛП(СтрокаЦены.СрокПоставки)+")", " (не указан)"));
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						ТаблицаЦен.Сортировать("СрокПоставкиМинимальный, Цена, ВидПредложения");
						Для Каждого СтрокаЦены ИЗ ТаблицаЦен Цикл
							Если СтрокаЦены.Цена > 0 Тогда
								Если ЦенаМин_СрокПоставкиМинимальный > СтрокаЦены.СрокПоставкиМинимальный Тогда
									СтрокаТаблицы.СрокПоставкиМин_ВидПредложения = СтрокаЦены.ВидПредложения;
									СтрокаТаблицы.СрокПоставкиМин_Цена = Формат(СтрокаЦены.Цена, "ЧЦ=15; ЧДЦ=2") + ?(СтрокаЦены.ВидПредложения = 1, "", ?(ЗначениеЗаполнено(СтрокаЦены.СрокПоставки), " ("+СокрЛП(СтрокаЦены.СрокПоставки)+")", " (не указан)"));
								КонецЕсли;
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла; 
	
	АналогиОтображение.Сортировать("ЕстьПредложения Убыв, Цена, Раздел, Артикул");
	
КонецПроцедуры

Процедура ЗаполнитьИнформацию(Знач СтрокаАртикул, Знач СтруктураПараметров) Экспорт
	
	флНаСкладах = Неопределено;
	СтруктураПараметров.Свойство("флНаСкладах", флНаСкладах);
	флВЗаказах = Неопределено;
	СтруктураПараметров.Свойство("флВЗаказах", флВЗаказах);
	флВПрайсЛистах = Неопределено;
	СтруктураПараметров.Свойство("флВПрайсЛистах", флВПрайсЛистах);
	
	ОбновитьНаСкладах = Неопределено;
	СтруктураПараметров.Свойство("ОбновитьНаСкладах", ОбновитьНаСкладах);
	ОбновитьВЗаказах = Неопределено;
	СтруктураПараметров.Свойство("ОбновитьВЗаказах", ОбновитьВЗаказах);
	ОбновитьВПрайсЛистах = Неопределено;
	СтруктураПараметров.Свойство("ОбновитьВПрайсЛистах", ОбновитьВПрайсЛистах);
	
	ВебПрайсЛисты = Новый Массив;
	Если СтруктураПараметров.Свойство("ВебПрайсЛисты") Тогда
		ВебПрайсЛисты = СтруктураПараметров.ВебПрайсЛисты;
	КонецЕсли;
	
	ДатаЦен = ТекущаяДата();
	Если СтруктураПараметров.Свойство("ДатаЦен") Тогда
		ДатаЦен = ?(ЗначениеЗаполнено(СтруктураПараметров.ДатаЦен), СтруктураПараметров.ДатаЦен, ТекущаяДата());
	КонецЕсли;
	
	СУчетомАналогов = Истина;
	Если СтруктураПараметров.Свойство("СУчетомАналогов") Тогда
		СУчетомАналогов = СтруктураПараметров.СУчетомАналогов;
	КонецЕсли;
	
	ИспользоватьПомощникПродаж = Ложь;
	Если СтруктураПараметров.Свойство("ИспользоватьПомощникПродаж") Тогда
		ИспользоватьПомощникПродаж = СтруктураПараметров.ИспользоватьПомощникПродаж;
	КонецЕсли;
	
	МассивАртикулов = Новый Массив;
	МассивАналогов = Новый Массив; // Для поиска по наличию, заказам и прайс-листам поставщиков
	МассивЗаменОтПоставщика = Новый Массив;
	МассивХеш = Новый Массив;
	
	Если СтрокаАртикул = Неопределено Тогда
		// Нужны все помеченные строки
		МассивАртикулов = Артикулы.НайтиСтроки(Новый Структура("Пометка", Истина));
		// Сначала добавим помеченные артикулы
		Для Каждого СтрокаАртикул Из МассивАртикулов Цикл
			СтрокаАналог = атСервер.СтруктураСтрокиАналога();
			ЗаполнитьЗначенияСвойств(СтрокаАналог, СтрокаАртикул);
			СтрокаАналог.Раздел = 0;
			СтрокаАналог.ВидДополнения = "Группа аналогов";
			СтрокаАналог.ХешАналога = СтрокаАналог.ХешАртикулПроизводитель;
			МассивАналогов.Добавить(СтрокаАналог);
			МассивХеш.Добавить(СтрокаАналог.ХешАналога);
		КонецЦикла;
		
		Если СУчетомАналогов Тогда
			// Затем их аналоги
			Для Каждого СтрокаАртикул Из МассивАртикулов Цикл
				
				СтрокиАналогов = Аналоги.НайтиСтроки(Новый Структура("ХешАртикулПроизводитель", СтрокаАртикул.ХешАртикулПроизводитель));
				
				Если СтрокиАналогов.Количество() = 0 И ЗначениеЗаполнено(СтрокаАртикул.АртикулДляПоиска) Тогда
					// Ранее не получали аналоги (это бывает если позиция найдена по наименованию или по части артикула)
					СтрокиАналогов = атСервер.ПолучитьАналоги(СтрокаАртикул.АртикулДляПоиска, СтрокаАртикул.Производитель, Не ИспользоватьПомощникПродаж);
					Для Каждого СтрокаАналогов Из СтрокиАналогов Цикл
						НоваяСтрока = Аналоги.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАналогов);
						НоваяСтрока.ХешАртикулПроизводитель = СтрокаАртикул.ХешАртикулПроизводитель;
					КонецЦикла;
				КонецЕсли;
				
				Для Каждого СтрокаАналогов Из СтрокиАналогов Цикл
					Если МассивХеш.Найти(СтрокаАналогов.ХешАналога) = Неопределено Тогда
						СтрокаАналог = атСервер.СтруктураСтрокиАналога();
						ЗаполнитьЗначенияСвойств(СтрокаАналог, СтрокаАналогов);
						МассивАналогов.Добавить(СтрокаАналог);
						МассивХеш.Добавить(СтрокаАналог.ХешАналога);
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		// Указана одна строка, для которой нужно заполнить цены
		МассивАртикулов.Добавить(СтрокаАртикул);
		
		СтрокаАналог = атСервер.СтруктураСтрокиАналога();
		ЗаполнитьЗначенияСвойств(СтрокаАналог, СтрокаАртикул);
		СтрокаАналог.Раздел = 0;
		СтрокаАналог.ВидДополнения = "Группа аналогов";
		СтрокаАналог.ХешАналога = СтрокаАналог.ХешАртикулПроизводитель;
		МассивАналогов.Добавить(СтрокаАналог);
		МассивХеш.Добавить(СтрокаАналог.ХешАналога);
		
		Если СУчетомАналогов Тогда
			
			СтрокиАналогов = Аналоги.НайтиСтроки(Новый Структура("ХешАртикулПроизводитель", СтрокаАртикул.ХешАртикулПроизводитель));
			
			Если СтрокиАналогов.Количество() = 0 И ЗначениеЗаполнено(СтрокаАртикул.АртикулДляПоиска) Тогда
				// Ранее не получали аналоги (это бывает если позиция найдена по наименованию или по части артикула)
				СтрокиАналогов = атСервер.ПолучитьАналоги(СтрокаАртикул.АртикулДляПоиска, СтрокаАртикул.Производитель, Не ИспользоватьПомощникПродаж);
				Для Каждого СтрокаАналогов Из СтрокиАналогов Цикл
					НоваяСтрока = Аналоги.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАналогов);
					НоваяСтрока.ХешАртикулПроизводитель = СтрокаАртикул.ХешАртикулПроизводитель;
				КонецЦикла;
			КонецЕсли;
			
			Для Каждого СтрокаАналогов Из СтрокиАналогов Цикл
				Если МассивХеш.Найти(СтрокаАналогов.ХешАналога) = Неопределено Тогда
					СтрокаАналог = атСервер.СтруктураСтрокиАналога();
					ЗаполнитьЗначенияСвойств(СтрокаАналог, СтрокаАналогов);
					МассивАналогов.Добавить(СтрокаАналог);
					МассивХеш.Добавить(СтрокаАналог.ХешАналога);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ПодразделениеКомпании", СтруктураПараметров.ПодразделениеКомпании);
	СтруктураПоиска.Вставить("ТипЦен", СтруктураПараметров.ТипЦен);
	СтруктураПоиска.Вставить("Валюта", СтруктураПараметров.Валюта);
	СтруктураПоиска.Вставить("ДатаЦен", ДатаЦен);
	
	Если ВебПрайсЛисты.Количество() > 0 Тогда
		
		СтруктураПоиска.Вставить("СУчетомАналогов", СУчетомАналогов);
		
		Для Каждого СтрокаАртикул Из МассивАртикулов Цикл
			
			Для Каждого ВебПрайсЛист Из ВебПрайсЛисты Цикл
				
				Результат = атСервер.ПроверитьКэшПрайсЛиста(ВебПрайсЛист, СтрокаАртикул.ХешАртикулПроизводитель);
				
				Если Результат = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если Результат = Ложь Тогда // В кеш нет данных
					атСервер.ЗаполнитьКэшПрайсЛиста(СтрокаАртикул.Артикул, СтрокаАртикул.ПроизводительКод, СтрокаАртикул.Производитель, ВебПрайсЛист, СтрокаАртикул.ХешАртикулПроизводитель);
				КонецЕсли;
				
				// Массив аналогов может содержать лишние аналоги (для других строк артикулов)
				ДобавитьВПрайсЛистыКонтрагентов(атСервер.ПолучитьЦеныАртикула(ВебПрайсЛист, СтрокаАртикул.ХешАртикулПроизводитель, МассивАналогов, СтруктураПоиска), 4, ?(СУчетомАналогов, МассивЗаменОтПоставщика, Неопределено));
				
			КонецЦикла;
			
		КонецЦикла;
		
		СтруктураПоиска.Удалить("СУчетомАналогов"); // Дальше он нам не нужен
		
	КонецЕсли;
	
	Если МассивЗаменОтПоставщика.Количество() > 0 Тогда
		Для Каждого СтрокаАналогов Из МассивЗаменОтПоставщика Цикл
			Если МассивХеш.Найти(СтрокаАналогов.ХешАналога) = Неопределено Тогда
				МассивАналогов.Добавить(СтрокаАналог);
				МассивХеш.Добавить(СтрокаАналог.ХешАналога);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если флНаСкладах = Истина ИЛИ (ОбновитьНаСкладах = Истина И МассивЗаменОтПоставщика.Количество() > 0) Тогда
		Если ?(флНаСкладах = Истина, МассивАналогов.Количество(), МассивЗаменОтПоставщика.Количество()) > 0 Тогда
			ДобавитьВПрайсЛистыКонтрагентов(атСервер.ПолучитьЦеныАртикуловСклады(?(флНаСкладах = Истина, МассивАналогов, МассивЗаменОтПоставщика), СтруктураПоиска), 1);
		КонецЕсли;
	КонецЕсли;
	
	Если флВЗаказах = Истина ИЛИ (ОбновитьВЗаказах = Истина И МассивЗаменОтПоставщика.Количество() > 0) Тогда
		Если ?(флВЗаказах = Истина, МассивАналогов.Количество(), МассивЗаменОтПоставщика.Количество()) > 0 Тогда
			ДобавитьВПрайсЛистыКонтрагентов(атСервер.ПолучитьЦеныАртикуловЗаказы(?(флВЗаказах = Истина, МассивАналогов, МассивЗаменОтПоставщика), СтруктураПоиска), 2);
		КонецЕсли;
	КонецЕсли;
	
	Если флВПрайсЛистах = Истина ИЛИ (ОбновитьВПрайсЛистах = Истина И МассивЗаменОтПоставщика.Количество() > 0) Тогда
		
		Если ?(флВПрайсЛистах = Истина, МассивАналогов.Количество(), МассивЗаменОтПоставщика.Количество()) > 0 Тогда
			Если СтруктураПараметров.Свойство("КонтрагентДляЦены") Тогда
				СтруктураПоиска.Вставить("Поставщик", СтруктураПараметров.КонтрагентДляЦены);
			Иначе
				Если СтруктураПараметров.Свойство("Поставщик") Тогда
					СтруктураПоиска.Вставить("Поставщик", СтруктураПараметров.Поставщик);
				Иначе
					СтруктураПоиска.Вставить("Поставщик", Неопределено);
				КонецЕсли;
			КонецЕсли;
			
			Если СтруктураПараметров.Свойство("ПрайсЛистКонтрагента") Тогда
				СтруктураПоиска.Вставить("ПрайсЛист", СтруктураПараметров.ПрайсЛистКонтрагента);
			Иначе
				Если СтруктураПараметров.Свойство("ПрайсЛист") Тогда
					СтруктураПоиска.Вставить("ПрайсЛист", СтруктураПараметров.ПрайсЛист);
				Иначе
					СтруктураПоиска.Вставить("ПрайсЛист", Неопределено);
				КонецЕсли;
			КонецЕсли;
			
			ДобавитьВПрайсЛистыКонтрагентов(атСервер.ПолучитьЦеныАртикуловПрайсЛисты(?(флВПрайсЛистах = Истина, МассивАналогов, МассивЗаменОтПоставщика), СтруктураПоиска), 3);
		КонецЕсли;
		
	КонецЕсли;
	
	ПрайсЛистыКонтрагентов.Сортировать("Раздел, Аналог, Цена");
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Права = Обработки.Защита.Создать().Права; // Кеш прав пользователя для передачи в модули общего назначения


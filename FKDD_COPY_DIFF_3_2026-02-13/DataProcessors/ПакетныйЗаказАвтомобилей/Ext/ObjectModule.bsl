Перем ДополнительныеСвойства Экспорт;
Перем Права Экспорт;
Перем ТекущаяВалютаДокумента Экспорт ;
Перем ТекущийКурсДокумента Экспорт;
Перем ВалютаУпр Экспорт;
Перем ВалютаРегл Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Возвращает структуру обязательных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",           Организация);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбязательныеРеквизиты.Вставить("Автор",                 Автор);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",       ВалютаДокумента);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",         КурсДокумента);
	ОбязательныеРеквизиты.Вставить("ТипЦен",                ТипЦен);
	ОбязательныеРеквизиты.Вставить("Контрагент",            Контрагент);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
	ОбязательныеРеквизиты.Вставить("СрокПоставки",          СрокПоставки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

Функция ПроверитьДублиСтрокОпций()
	КолонкаОпции = Опции.Выгрузить(,"Опция, ИдентификаторСтроки");
	КолонкаОпции.Свернуть("Опция, ИдентификаторСтроки");
	НайденыДубли = Ложь;
	Для Каждого ТекСтрока Из КолонкаОпции Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Опция", ТекСтрока["Опция"]);
		ПараметрыОтбора.Вставить("ИдентификаторСтроки", ТекСтрока["ИдентификаторСтроки"]);
		НайденныеСтроки = Опции.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 1 Тогда
			НомераСтрок = "";
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НомераСтрок = НомераСтрок + ?(НомераСтрок = "", "", ", " ) + НайденнаяСтрока.НомерСтроки;
			КонецЦикла;
			Сообщить("Имеются дубли опций <" + ТекСтрока["Опция"] + "> в строках: " + НомераСтрок);
			НайденыДубли = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат НайденыДубли;
КонецФункции

Функция ПроверитьДублиСтрокАвтомобилей()
	КолонкаАвтомобили = Автомобили.Выгрузить(,"Автомобиль");
	КолонкаАвтомобили.Свернуть("Автомобиль");
	Для Каждого ТекСтрока Из КолонкаАвтомобили Цикл
		Если ТекСтрока.Автомобиль = Справочники.Автомобили.ПустаяСсылка() Тогда
			КолонкаАвтомобили.Удалить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	НайденыДубли = Ложь;
	Для Каждого ТекСтрока Из КолонкаАвтомобили Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Автомобиль", ТекСтрока["Автомобиль"]);
		НайденныеСтроки = Автомобили.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 1 Тогда
			НомераСтрок = "";
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НомераСтрок = НомераСтрок + ?(НомераСтрок = "", "", ", " ) + НайденнаяСтрока.НомерСтроки;
			КонецЦикла;
			Сообщить("Имеются дубли автомобилей <" + ТекСтрока["Автомобиль"] + "> в строках: " + НомераСтрок);
			НайденыДубли = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат НайденыДубли;
КонецФункции


//Проверят обязательные реквизиты для заполнения
//Параметры:
//ТабЧастьАвтомобили - табличная часть автомобили
//ТабЧастьОпции      - табличная часть опции
//Возвращаемое значение:
//Булево - результат выполения проверки
Функция ПроверкаОбязательныхРеквизитов(ТабЧастьАвтомобили, ТабЧастьОпции) Экспорт
	Результат = Истина;
	ОбязательныеРеквизиты = ПолучитьОбязательныеРеквизиты();
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.Организация) Тогда
		Сообщить("Организация не заполнена");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.ПодразделениеКомпании) Тогда
		Сообщить("Подразделение компании не заполнено");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.Автор) Тогда
		Сообщить("Автор не заполнен");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.ВалютаДокумента) Тогда
		Сообщить("Валюта не заполнена");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.КурсДокумента) Тогда
		Сообщить("Курс не заполнен");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.ТипЦен) Тогда
		Сообщить("Тип цен не заполнен");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.Контрагент) Тогда
		Сообщить("Контрагент не заполнен");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.ДоговорВзаиморасчетов) Тогда
		Сообщить("Договор взаиморасчетов не заполнен");
		Результат = Ложь;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ОбязательныеРеквизиты.СрокПоставки) Тогда
		Сообщить("Срок поставки документа не заполнен");
		Результат = Ложь;
	КонецЕсли;
	
	Если ТабЧастьАвтомобили.Количество() > 0 Тогда
		Для Каждого ТекСтрока Из ТабЧастьАвтомобили Цикл
			Если обЗначениеНеЗаполнено(ТекСтрока.Модель) Тогда
				Сообщить("Модель автомобиля не заполнена в строке " + ТекСтрока.НомерСтроки );
				Результат = Ложь;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекСтрока.Количество) Тогда
				Сообщить("Количество автомобилей не заполнено в строке " + ТекСтрока.НомерСтроки );
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Сообщить("Табличная часть ""Автомобили""  не содержит строк!");
		Результат = Ложь;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из ТабЧастьопции Цикл
		Если обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда
			Сообщить("Опция не заполнена в строке " + ТекСтрока.НомерСтроки );
			Результат = Ложь;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ТекСтрока.Количество) Тогда
			Сообщить("Количество опций не заполнено в строке " + ТекСтрока.НомерСтроки );
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если ПроверитьДублиСтрокОпций() Тогда
		Результат = Ложь;
	КонецЕсли;
	Если ПроверитьДублиСтрокАвтомобилей() Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя, ТекСтрока = Неопределено, ЭтаФорма = Неопределено, ДопПараметры = Неопределено) Экспорт
	Результат = Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет = ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет = Ложь;
		КонецПопытки; 
		Рез = Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена = обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена = НоваяЦена;
					Рез = ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
			Для каждого СтрокаТЧ Из Опции Цикл
				НоваяЦена = обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена = НоваяЦена;
					Рез = ОбработкаРеквизита("Опции.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента = ВалютаДокумента;
		ТекущийКурсДокумента = КурсДокумента;
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		
		Возврат Рез;
	ИначеЕсли Имя = "ТипЦен" Тогда
		
		ТребуетсяПересчет = Истина;
		Если ДопПараметры <> Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет",ТребуетсяПересчет) Тогда
				ТребуетсяПересчет = Истина;
			КонецЕсли; 
		КонецЕсли; 
		
		// пересчитываем
		Если ТребуетсяПересчет Тогда
			Для Каждого СтрокаАвтомобиля Из Автомобили Цикл
				
				ЗаказНаАвтомобиль   = СтрокаАвтомобиля.ЗаказПокупателя;
				
				Попытка
					СтрокаАвтомобиля.Цена = ЗаказНаАвтомобиль.Цена;
				Исключение
				КонецПопытки;
				
				Если СтрокаАвтомобиля.Цена = 0 Тогда
					СтрокаАвтомобиля.Цена = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль,ВариантКомплектации", СтрокаАвтомобиля.Модель, Справочники.ВариантыКомплектации.ПустаяСсылка()), Дата, , ТекущаяВалютаДокумента, ТекущийКурсДокумента);
				КонецЕсли;
				ОбработкаРеквизита("Автомобили.Цена", СтрокаАвтомобиля, ЭтаФорма, ДопПараметры);
				
				НайденныеОпции      = Опции.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаАвтомобиля.ИдентификаторСтроки));
				Модель              = СтрокаАвтомобиля.Модель;
				ВариантКомплектации = СтрокаАвтомобиля.ВариантКомплектации;
				
				Для Каждого СтрокаОпции Из НайденныеОпции Цикл
					
					ОпцияИзЗаказа = Неопределено;
					ОпцияИзЗаказа = ЗаказНаАвтомобиль.Опции.Найти(СтрокаОпции.Опция, "Опция");
					Если ОпцияИзЗаказа<>Неопределено Тогда
						СтрокаОпции.Цена = ОпцияИзЗаказа.Цена;
					Иначе
						СтрокаОпции.Цена  =  0;
					КонецЕсли;
					
					Если СтрокаОпции.Цена = 0 Тогда
						СтрокаОпции.Цена = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации, Опция", Модель, ВариантКомплектации, СтрокаОпции.Опция), Дата,, ТекущаяВалютаДокумента, ТекущийКурсДокумента);
					КонецЕсли;
					ОбработкаРеквизита("Опции.Цена", СтрокаОпции, ЭтаФорма, ДопПараметры);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		
		
	ИначеЕсли Имя = "Организация" Тогда
		
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Организация может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС = ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;
		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат = ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		
		Для каждого СтрокаТЧ Из Автомобили Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Автомобили.СтавкаНДС", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
		КонецЦикла;
		
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Опции.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла;
		
	ИначеЕсли Имя = "ПодразделениеКомпании" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		// Подразделение может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС	= ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;
		
		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат = ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		
		Для каждого СтрокаТЧ Из Автомобили Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Автомобили.СтавкаНДС", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
		КонецЦикла;
		
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Опции.СтавкаНДС", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
		КонецЦикла;
		
		//ОБРАБОТКА РЕКВИЗИТИТОВ ТАБЛИЧНОЙ ЧАСТИ АВТОМОБИЛИ
	ИначеЕсли Имя = "Автомобили.Модель" Тогда
		
		Если ТекСтрока.Количество = 0 Тогда
			ТекСтрока.Количество = 1
		КонецЕсли;
		// Заполним ставку НДС
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС = ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;
		
		Если ОсвобожденОтНДС Тогда
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		
		ТекСтрока.Цена = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации",ТекСтрока.Модель,ТекСтрока.ВариантКомплектации),ТекущаяДата(),,ВалютаДокумента,КурсДокумента);
		ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.ВариантКомплектации" Тогда
		
		ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		ОбработкаРеквизита("Автомобили.Модель",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Автомобили.ТипСалона" Тогда
		
	ИначеЕсли Имя = "Автомобили.Цвет" Тогда
		
	ИначеЕсли Имя = "Автомобили.Автомобиль" Тогда
		Если Не (ТекСтрока.Автомобиль = Справочники.Автомобили.ПустаяСсылка()) Тогда
			ТекСтрока.Количество = 1;
			ОбработкаРеквизита("Автомобили.Количество", ТекСтрока, ЭтаФорма, ДопПараметры);
			Автомобиль                    = ТекСтрока.Автомобиль;
			ТекСтрока.Модель              = Автомобиль.Модель;
			Текстрока.ВариантКомплектации = Автомобиль.ВариантКомплектации;
			ТекСтрока.Цвет                = Автомобиль.Цвет;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
		               |	ЗаказыАвтомобилейОстатки.КоличествоОстаток,
		               |	ЗаказыАвтомобилейОстатки.Заказ КАК Ссылка
		               |ИЗ
		               |	РегистрНакопления.ЗаказыАвтомобилей.Остатки(
		               |			,
		               |			Заказ ССЫЛКА Документ.ЗаказНаАвтомобиль
		               |				И Автомобиль <> ЗНАЧЕНИЕ(Справочник.Автомобили.ПустаяСсылка)) КАК ЗаказыАвтомобилейОстатки";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Результат = Ложь;
		
		Пока Выборка.Следующий() Цикл
			Если ТекСтрока.Автомобиль = Выборка.Автомобиль Тогда
				ТекСтрока.ЗаказПокупателя = Выборка.Ссылка;
				Результат = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Не Результат Тогда
			ТекСтрока.ЗаказПокупателя = Документы.ЗаказНаАвтомобиль.ПустаяСсылка();
		КонецЕсли;
		
		ОбработкаРеквизита("Автомобили.Модель",ТекСтрока,ЭтаФорма,ДопПараметры);
		ОбработкаРеквизита("Автомобили.ЗаказПокупателя",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Автомобили.Количество" Тогда
		Если ТекСтрока.Автомобиль <> Справочники.Автомобили.ПустаяСсылка() Тогда
			ТекСтрока.Количество = 1;
		КонецЕсли;
		
		Результат = ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Результат = ОбработкаРеквизита("Автомобили.Сумма", ТекСтрока, ЭтаФорма, ДопПараметры);
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		
	ИначеЕсли Имя = "Автомобили.Сумма" Тогда
		ТекСтрока.Цена = ?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма / ТекСтрока.Количество);
		Результат = ОбработкаРеквизита("Автомобили.СтавкаНДС", ТекСтрока, ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		
	ИначеЕсли Имя = "Автомобили.СтавкаНДС" Тогда
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Расчет НДС
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка) ,2);
		Иначе
			ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли; 
		// В любом случаее идем в секцию "СуммаНДС", т.к. в ней будем произведен расчет суммы всего.
		Результат = ЭтотОбъект.ОбработкаРеквизита("Автомобили.СуммаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Автомобили.СуммаНДС" Тогда
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
		Иначе
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма + ТекСтрока.СуммаНДС;
		КонецЕсли;
		
	ИначеЕсли Имя = "Автомобили.СуммаВсего" Тогда
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если НЕ ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма = Окр(ТекСтрока.СуммаВсего*100/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		КонецЕсли;
		// Пересчитываем цену.
		ТекСтрока.Цена = ?(ТекСтрока.Количество = 0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		
		// Рассчитываем новую сумму НДС.
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма=ТекСтрока.СуммаВсего;
			ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка) / (100 + ТекСтрока.СтавкаНДС.Ставка), 2);
		Иначе
			ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка / 100,2);
			ТекСтрока.Сумма = (100 * ТекСтрока.СуммаВсего) / (100 + ТекСтрока.СтавкаНДС.Ставка);
		КонецЕсли;
		
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли

		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Автомобили.ЗаказПокупателя" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыАвтомобилейОстатки.Заказ КАК Ссылка,
		|	ЗаказыАвтомобилейОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЗаказыАвтомобилей.Остатки КАК ЗаказыАвтомобилейОстатки
		|ГДЕ
		|	ЗаказыАвтомобилейОстатки.КоличествоОстаток - ЗаказыАвтомобилейОстатки.РезервОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ЗаказПокупателяПодходит = Ложь;
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.Ссылка = ТекСтрока.ЗаказПокупателя Тогда
				ЗаказПокупателяПодходит = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаказПокупателяПодходит Тогда
			УдалениеСтрокОпций(ТекСтрока.ИдентификаторСтроки);
			ЗаказПокупателя = ТекСтрока.ЗаказПокупателя;
			
			ТекСтрока.Модель              = ЗаказПокупателя.Модель;
			ТекСтрока.ВариантКомплектации = ЗаказПокупателя.ВариантКомплектации;
			ТекСтрока.ТипСалона           = ЗаказПокупателя.ТипСалона;
			ТекСтрока.Цвет                = ЗаказПокупателя.Цвет;
			ТекСтрока.Автомобиль          = ЗаказПокупателя.Автомобиль;
			ТекСтрока.СрокПоставки        = ЗаказПокупателя.СрокПоставки;
			ТекСтрока.Количество          = 1;
			
			Если ЗаказПокупателя.ВалютаДокумента = ВалютаДокумента Тогда
				ТекСтрока.Цена       = ЗаказПокупателя.СуммаВсегоНаАвтомобиль;
				ТекСтрока.СуммаВсего = ЗаказПокупателя.СуммаВсегоНаАвтомобиль;
				ТекСтрока.Сумма      = ЗаказПокупателя.СуммаВсегоНаАвтомобиль;
				ТекСтрока.СуммаНДС   = ЗаказПокупателя.СуммаНДСНаАвтомобиль;
			Иначе
				ТекСтрока.Цена       = обПересчет(ЗаказПокупателя.СуммаВсегоНаАвтомобиль, ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
				ТекСтрока.СуммаВсего = обПересчет(ЗаказПокупателя.СуммаВсегоНаАвтомобиль, ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
				ТекСтрока.Сумма      = обПересчет(ЗаказПокупателя.СуммаВсегоНаАвтомобиль, ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
				ТекСтрока.СуммаНДС   = обПересчет(ЗаказПокупателя.СуммаНДСНаАвтомобиль,   ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
			КонецЕсли;
			ТекСтрока.СтавкаНДС      = ЗаказПокупателя.СтавкаНДСНаАвтомобиль;
			
			ОпцииЗаказПокупателя     = ЗаказПокупателя.Опции.Выгрузить();
			
			Для Каждого ТекЭлемент Из ОпцииЗаказПокупателя Цикл
				
				НоваяСтрокаОпции = Опции.Добавить();
				
				НоваяСтрокаОпции.ИдентификаторСтроки = ТекСтрока.ИдентификаторСтроки;
				НоваяСтрокаОпции.Количество          = ТекЭлемент.Количество;
				НоваяСтрокаОпции.Опция               = ТекЭлемент.Опция;
				НоваяСтрокаОпции.СтавкаНДС           = ТекЭлемент.СтавкаНДС;
				Если ЗаказПокупателя.ВалютаДокумента = ВалютаДокумента Тогда
					НоваяСтрокаОпции.Сумма               = ТекЭлемент.Сумма;
					НоваяСтрокаОпции.СуммаВсего          = ТекЭлемент.СуммаВсего;
					НоваяСтрокаОпции.СуммаНДС            = ТекЭлемент.СуммаНДС;
					НоваяСтрокаОпции.Цена                = ТекЭлемент.Цена;
				Иначе
					НоваяСтрокаОпции.Сумма               = обПересчет(ТекЭлемент.Сумма,      ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
					НоваяСтрокаОпции.СуммаВсего          = обПересчет(ТекЭлемент.СуммаВсего, ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
					НоваяСтрокаОпции.СуммаНДС            = обПересчет(ТекЭлемент.СуммаНДС,   ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
					НоваяСтрокаОпции.Цена                = обПересчет(ТекЭлемент.Цена,       ЗаказПокупателя.ВалютаДокумента, ТекущаяДата(), ВалютаДокумента, ТекущаяДата());
				КонецЕсли;
			КонецЦикла;
		Иначе 
			ТекСтрока.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
		КонецЕсли;
		
		//ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ ОПЦИИ
		
	ИначеЕсли Имя = "Опции.Опция" Тогда
		Если ТекСтрока.Количество = 0 Тогда
			ТекСтрока.Количество = 1
		КонецЕсли;
		// Заполним ставку НДС
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС = ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
			ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;
		
		Если ОсвобожденОтНДС Тогда
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		СтруктураДляобПолучитьЦену = Новый Структура("Автомобиль,ВариантКомплектации,Опция",ДопПараметры.ТекСтрокаАвтомобиль.Модель,ДопПараметры.ТекСтрокаАвтомобиль.ВариантКомплектации,ТекСтрока.Опция);
		ТекСтрока.Цена = обПолучитьЦену(ТипЦен,СтруктураДляобПолучитьЦену,ТекущаяДата(),,ВалютаДокумента,КурсДокумента);
		ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Опции.Количество" Тогда
		Результат = ОбработкаРеквизита("Опции.Цена", ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Опции.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		
		Результат = ОбработкаРеквизита("Опции.Сумма", ТекСтрока, ЭтаФорма, ДопПараметры);
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		
	ИначеЕсли Имя = "Опции.Сумма" Тогда
		ТекСтрока.Цена = ?(ТекСтрока.Количество = 0, 0, ТекСтрока.Сумма / ТекСтрока.Количество);
		Результат = ОбработкаРеквизита("Опции.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		
	ИначеЕсли Имя = "Опции.СтавкаНДС" Тогда
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Расчет НДС
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли; 
		// В любом случае идем в секцию "СуммаНДС", т.к. в ней будем произведен расчет суммы всего.
		Результат = ЭтотОбъект.ОбработкаРеквизита("Опции.СуммаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Опции.СуммаНДС" Тогда
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаВсего = ТекСтрока.Сумма;
		Иначе
			ТекСтрока.СуммаВсего = ТекСтрока.Сумма + ТекСтрока.СуммаНДС;
		КонецЕсли;
		
	ИначеЕсли Имя = "Опции.СуммаВсего" Тогда
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если НЕ ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма = Окр(ТекСтрока.СуммаВсего * 100/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		КонецЕсли;
		// Пересчитываем цену.
		ТекСтрока.Цена = ?(ТекСтрока.Количество = 0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		// Рассчитываем новую сумму НДС.
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма = ТекСтрока.СуммаВсего;
			ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка/100,2);
			ТекСтрока.Сумма = (100*ТекСтрока.СуммаВсего)/(100 + ТекСтрока.СтавкаНДС.Ставка);
		КонецЕсли;
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		Возврат ОбработкаРеквизита("Опции.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

//Рассчитать сумму всего
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Автомобили.Итог("СуммаВсего") + Опции.Итог("СуммаВсего");
КонецФункции

//Проведение или запись документа "Заказ поставщику на автомобиль"
//Параметры:
//РежимПроведения - Режим проведения документа, Записать или Провести.
Процедура ОбработкаЗаписи(РежимПроведения) Экспорт
	ВсеОк = Истина;
	СписокДокументов = Новый Массив; //для вывода списка новых документов на макет
	
	ТаблицаНеЗаписанныхАвтомобилей = Автомобили.Выгрузить();
	ТаблицаНеЗаписанныхАвтомобилей.Очистить();
	ТаблицаАвтомобили = Автомобили.Выгрузить();
	
	ДокументСобытие = Документы.Событие.СоздатьДокумент();
	ДокументСобытие.Заполнить(Неопределено);
	дкОбработкаЗаполненияПоУмолчанию(ДокументСобытие);
	ДокументСобытие.УстановитьНовыйНомер();
	ДокументСобытие.Дата   = ТекущаяДата();
	ДокументСобытие.ФорматТекста  = Перечисления.ФорматТекста.ПростойТекст;
	ДокументСобытие.Тема          = Комментарий;
	ДокументСобытие.Контрагент    = Контрагент;
	ДокументСобытие.ВидСобытия    = Перечисления.ВидыСобытий.Прочее;
	ДокументСобытие.Состояние     = Перечисления.СостоянияСобытий.Запланировано;
	ДокументСобытие.ДатаНачала    = ТекущаяДата();
	ДокументСобытие.ДатаОкончания = ТекущаяДата();
	ДокументСобытие.Приоритет     = Перечисления.Важность.Средняя;
	ДокументСобытие.ТипСообщения  = Перечисления.ТипыСообщений.Исходящее;
	ДокументСобытие.Менеджер      = Автор;
	ДокументСобытие.Содержание    = Комментарий;
	ДокументСобытие.Комментарий   = Комментарий;
	ДокументСобытие.Менеджер      = Автор.Сотрудник;
	
	НоваяСтрокаСторонниеЛица = ДокументСобытие.СторонниеЛица.Добавить();
	НоваяСтрокаСторонниеЛица.Контрагент = Контрагент;
	
	ДокументСобытие.Записать(РежимЗаписиДокумента.Проведение);
	
	ТабЗначАвтомобили = Новый ТаблицаЗначений;
	ТабЗначАвтомобили.Колонки.Добавить("Автомобиль");
	ТабЗначАвтомобили.Колонки.Добавить("Количество");
	
	// Определим каким образом у нас вычисляется НДС
	ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
	
	Для Каждого ТекСтрока Из ТаблицаАвтомобили Цикл
		
		Колво = ТекСтрока.Количество;
		
		Пока Колво > 0 Цикл
			
			ЗаказНаАвтомобиль = Документы.ЗаказПоставщикуНаАвтомобиль.СоздатьДокумент();
			//ЗаказНаАвтомобиль.Заполнить(Неопределено);
			дкОбработкаЗаполненияПоУмолчанию(ЗаказНаАвтомобиль);
			
			ЗаказНаАвтомобиль.УстановитьНовыйНомер();
			ЗаказНаАвтомобиль.Дата            = ТекущаяДата();
			ЗаказНаАвтомобиль.ТипЦен          = ТипЦен;
			ЗаказНаАвтомобиль.ВалютаДокумента = ВалютаДокумента;
			ЗаказНаАвтомобиль.КурсДокумента   = КурсДокумента;

			Если ТекСтрока.Автомобиль = Справочники.Автомобили.ПустаяСсылка() Тогда
				НовыйАвтомобиль = Справочники.Автомобили.СоздатьЭлемент();
				НаименованиеАвтомобиля = Справочники.Автомобили.СформироватьНаименованиеАвтомобиляПоПолям(ТекСтрока.Модель.Наименование,ТекСтрока.Цвет.Наименование,"","" );
				НовыйАвтомобиль.Заполнить(Неопределено);
				
				НовыйАвтомобиль.Модель       = ТекСтрока.Модель;
				НовыйАвтомобиль.Наименование = НаименованиеАвтомобиля;
				НовыйАвтомобиль.ТипСалона    = ТекСтрока.ТипСалона;
				НовыйАвтомобиль.Цвет         = ТекСтрока.Цвет;
				
				НовыйАвтомобиль.Записать();
				
				ЗаказНаАвтомобиль.Автомобиль          = НовыйАвтомобиль.Ссылка;
				ЗаказНаАвтомобиль.Цвет                = ТекСтрока.Цвет;
				ЗаказНаАвтомобиль.Модель              = ТекСтрока.Модель;
				ЗаказНаАвтомобиль.ТипСалона           = ТекСтрока.ТипСалона;
				ЗаказНаАвтомобиль.ВариантКомплектации = ТекСтрока.ВариантКомплектации;
			Иначе
				ЗаказНаАвтомобиль.Автомобиль          = ТекСтрока.Автомобиль;
				ЗаказНаАвтомобиль.Цвет                = ТекСтрока.Автомобиль.Цвет;
				ЗаказНаАвтомобиль.Модель              = ТекСтрока.Автомобиль.Модель;
				ЗаказНаАвтомобиль.ТипСалона           = ТекСтрока.Автомобиль.ТипСалона;
				ЗаказНаАвтомобиль.ВариантКомплектации = ТекСтрока.Автомобиль.ВариантКомплектации;
			КонецЕсли;
			
			ЗаказНаАвтомобиль.Контрагент             = Контрагент;
			ЗаказНаАвтомобиль.ДоговорВзаиморасчетов  = ДоговорВзаиморасчетов;
			ЗаказНаАвтомобиль.ЦенаАвтомобиля         = ТекСтрока.Цена;
			ЗаказНаАвтомобиль.СтавкаНДСНаАвтомобиль  = ТекСтрока.СтавкаНДС;
			
			Если ЦенаВключаетНДС Тогда
				ЗаказНаАвтомобиль.СуммаНДСНаАвтомобиль   = (ЗаказНаАвтомобиль.СтавкаНДСНаАвтомобиль.Ставка * ТекСтрока.Цена) / (100 + ЗаказНаАвтомобиль.СтавкаНДСНаАвтомобиль.Ставка);
				ЗаказНаАвтомобиль.СуммаВсегоНаАвтомобиль = ТекСтрока.Цена;
			Иначе
				ЗаказНаАвтомобиль.СуммаНДСНаАвтомобиль   = (ЗаказНаАвтомобиль.СтавкаНДСНаАвтомобиль.Ставка * ТекСтрока.Цена) / 100;
				ЗаказНаАвтомобиль.СуммаВсегоНаАвтомобиль = ТекСтрока.Цена + ЗаказНаАвтомобиль.СуммаНДСНаАвтомобиль;
			КонецЕсли;
			
			Если Дата("00010101000000") = ТекСтрока.СрокПоставки Тогда
				ЗаказНаАвтомобиль.СрокПоставки       = СрокПоставки;
			Иначе
				ЗаказНаАвтомобиль.СрокПоставки       = ТекСтрока.СрокПоставки;
			КонецЕсли;
			
			ЗаказНаАвтомобиль.СтатусАвтомобиля       = СтатусАвтомобиля;
			ЗаказНаАвтомобиль.Комментарий            = Комментарий;
			ЗаказНаАвтомобиль.ДокументОснование      = ДокументСобытие.Ссылка;
			
			Для Каждого ТекСтрокаОпции Из Опции Цикл
				Если ТекСтрока.ИдентификаторСтроки = ТекСтрокаОпции.ИдентификаторСтроки Тогда
					
					ТЧДокументаОпции = ЗаказНаАвтомобиль.Опции.Добавить();
					
					ТЧДокументаОпции.Опция      = ТекСтрокаОпции.Опция;
					ТЧДокументаОпции.Количество = ТекСтрокаОпции.Количество;
					ТЧДокументаОпции.Цена       = ТекСтрокаОпции.Цена;
					ТЧДокументаОпции.Сумма      = ТекСтрокаОпции.Сумма;
					ТЧДокументаОпции.СтавкаНДС  = ТекСтрокаОпции.СтавкаНДС;
					ТЧДокументаОпции.СуммаНДС   = ТекСтрокаОпции.СуммаНДС;
					ТЧДокументаОпции.СуммаВсего = ТекСтрокаОпции.СуммаВсего;
					
				КонецЕсли;
			КонецЦикла;
			
			Попытка
				ЗаказНаАвтомобиль.Записать(РежимПроведения);
				СписокДокументов.Добавить(ЗаказНаАвтомобиль.Ссылка);
				НоваяСтрокаТабЗнач = ТабЗначАвтомобили.Добавить();
				НоваяСтрокаТабЗнач.Количество = ТекСтрока.Количество;
				НоваяСтрокаТабЗнач.Автомобиль = ЗаказНаАвтомобиль.Автомобиль;
			Исключение
				НоваяСтрокаТАб = ТаблицаНеЗаписанныхАвтомобилей.Добавить();
				
				НоваяСтрокаТАб.Модель              = ТекСтрока.Модель;
				НоваяСтрокаТАб.ВариантКомплектации = ТекСтрока.ВариантКомплектации;
				НоваяСтрокаТАб.ТипСалона           = ТекСтрока.ТипСалона;
				НоваяСтрокаТАб.Автомобиль          = ТекСтрока.Автомобиль;
				НоваяСтрокаТАб.Цвет                = ТекСтрока.Цвет;
				НоваяСтрокаТАб.Количество          = 1;
				НоваяСтрокаТАб.Цена                = ТекСтрока.Цена;
				НоваяСтрокаТАб.Сумма               = ТекСтрока.Сумма;
				НоваяСтрокаТАб.СуммаВсего          = ТекСтрока.СуммаВсего;
				НоваяСтрокаТАб.СтавкаНДС           = ТекСтрока.СтавкаНДС;
				НоваяСтрокаТАб.СуммаНДС            = ТекСтрока.СуммаНДС;
				НоваяСтрокаТАб.СтавкаНДС           = ТекСтрока.СтавкаНДС;
				НоваяСтрокаТАб.СуммаНДС            = ТекСтрока.СуммаНДС;
				НоваяСтрокаТАб.СрокПоставки        = ТекСтрока.СрокПоставки;
				НоваяСтрокаТАб.ЗаказПокупателя     = ТекСтрока.ЗаказПокупателя;
				НоваяСтрокаТАб.ИдентификаторСтроки = ТекСтрока.ИдентификаторСтроки;
				
				ТаблицаНеЗаписанныхАвтомобилейОпции = Опции.Выгрузить();
				
				ВсеОк = Ложь;
				
			КонецПопытки;
			Колво = Колво - 1;
		КонецЦикла;
	КонецЦикла;
	
	ДокументСобытие.Автомобили.Загрузить(ТабЗначАвтомобили);
	ДокументСобытие.Записать(РежимЗаписиДокумента.Проведение);
	
	ОчиститьТабЧасти();
		
	Если СписокДокументов.Количество() > 0 Тогда
		//ВывестиНаМакетСписокДокументов (СписокДокументов);
		ФормаДокумента = Документы.ЗаказПоставщикуНаАвтомобиль.ПолучитьФормуСписка();
		ФормаДокумента.Отбор.Ссылка.ВидСравнения  = ВидСравнения.ВСписке;
		ФормаДокумента.Отбор.Ссылка.Значение.ЗагрузитьЗначения(СписокДокументов);
		ФормаДокумента.Отбор.Ссылка.Использование = Истина;
		ФормаДокумента.ЭлементыФормы.Список.НастройкаОтбора.Ссылка.Доступность = Истина;
		ФормаДокумента.Открыть();
	КонецЕсли;
	
	Если Не ВсеОк Тогда
		ТаблицаНеЗаписанныхАвтомобилей.Свернуть("Модель, ВариантКомплектации, ТипСалона, Цвет, Автомобиль, Цена, Сумма,
		|СуммаВсего, СтавкаНДС, СуммаНДС, СрокПоставки, ЗаказПокупателя, ИдентификаторСтроки", "Количество");
		Опции.Загрузить(ТаблицаНеЗаписанныхАвтомобилейОпции);
		Автомобили.Загрузить(ТаблицаНеЗаписанныхАвтомобилей);
		МассивГуид = Автомобили.ВыгрузитьКолонку("ИдентификаторСтроки");
		ДляУдаления = Новый Массив;
		Для Каждого ТекСтрока Из Опции Цикл
			Если МассивГуид.Найти(ТекСтрока.ИдентификаторСтроки) = Неопределено Тогда
				ДляУдаления.Добавить(ТекСтрока);
			КонецЕсли;
		КонецЦикла;
		Для Каждого Строчка Из ДляУдаления Цикл
			Опции.Удалить(Строчка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

//Очистить все реквизиты и табличный части обработки
Процедура ОчиститьТабЧасти() Экспорт
	Автомобили.Очистить();
	Опции.Очистить();
	//Контрагент            = Справочники.Контрагенты.ПустаяСсылка();
	//ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	//СтатусАвтомобиля      = Справочники.СтатусыАвтомобилей.ПустаяСсылка();
КонецПроцедуры

//Выводит на макет новые документы
//Параметры
//Документы - массив ссылок на документы "ЗаказПоставщикуНаАвтомобиль"
Процедура ВывестиНаМакетСписокДокументов(Документы) Экспорт
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("Макет");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(ОбластьШапка);
	ОбластьДокументы = Макет.ПолучитьОбласть("Документы");
	НомерСтроки = 1;
	
	Для Каждого ТекЭл Из Документы Цикл
		ОбластьДокументы.Параметры.Номер               = НомерСтроки;
		ОбластьДокументы.Параметры.Документы           = ТекЭл.Ссылка;
		ОбластьДокументы.Параметры.Модель              = ТекЭл.Ссылка.Модель;
		ОбластьДокументы.Параметры.ВариантКомплектации = ТекЭл.Ссылка.ВариантКомплектации;
		
		ОбластьДокументы.Параметры.СсылкаРасшифровка              = ТекЭл.Ссылка;
		ОбластьДокументы.Параметры.МодельРасшифровка              = ТекЭл.Ссылка.Модель;
		ОбластьДокументы.Параметры.ВариантКомплектацииРасшифровка = ТекЭл.Ссылка.ВариантКомплектации;
		
		НомерСтроки = НомерСтроки + 1;
		ТабДок.Вывести(ОбластьДокументы);
	КонецЦикла;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.Показать();
КонецПроцедуры

//Процедура заполняет табличные части "Автомобили" и "Опции" по заказам покупателей
Процедура ЗаполнитьТЧПоЗаказамПокупателей() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВложенныйЗапрос.Автомобиль
	|ПОМЕСТИТЬ АвтомобилиНеВСписке
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыПоставщикамНаАвтомобилиОстатки.Автомобиль КАК Автомобиль
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки КАК ЗаказыПоставщикамНаАвтомобилиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиАвтомобилейОстатки.Автомобиль
	|	ИЗ
	|		РегистрНакопления.ОстаткиАвтомобилей.Остатки КАК ОстаткиАвтомобилейОстатки) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Заказ КАК ЗаказПокупателя,
	|	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	|	ЗаказыАвтомобилейОстатки.Автомобиль.Модель КАК Модель,
	|	ЗаказыАвтомобилейОстатки.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|	ЗаказыАвтомобилейОстатки.Автомобиль.Цвет КАК Цвет,
	|	ЗаказыАвтомобилейОстатки.СуммаОстаток КАК СуммаРег,
	|	ЗаказыАвтомобилейОстатки.СуммаУпрОстаток КАК СуммаУпр,
	|	ЗаказыАвтомобилейОстатки.Автомобиль.ТипСалона КАК ТипСалона,
	|	ЗаказНаАвтомобильОпции.Опция КАК Опция,
	|	ЗаказНаАвтомобильОпции.Количество КАК КоличествоОпция,
	|	ЗаказНаАвтомобильОпции.Цена КАК ЦенаОпция,
	|	ЗаказНаАвтомобильОпции.Сумма КАК СуммаОпция,
	|	ЗаказНаАвтомобильОпции.СтавкаНДС КАК СтавкаНДСОпция,
	|	ЗаказНаАвтомобильОпции.СуммаНДС КАК СуммаНДСОпция,
	|	ЗаказНаАвтомобильОпции.СуммаВсего КАК СуммаВсегоОпция,
	|	ЗаказНаАвтомобильОпции.Ссылка.ВалютаДокумента КАК ВалютаДокумента
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(
	|			,
	|			НЕ Автомобиль В
	|					(ВЫБРАТЬ
	|						АвтомобилиНеВСписке.Автомобиль
	|					ИЗ
	|						АвтомобилиНеВСписке КАК АвтомобилиНеВСписке)) КАК ЗаказыАвтомобилейОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаАвтомобиль.Опции КАК ЗаказНаАвтомобильОпции
	|		ПО ЗаказыАвтомобилейОстатки.Заказ.Ссылка = ЗаказНаАвтомобильОпции.Ссылка
	|ИТОГИ
	|	МАКСИМУМ(Автомобиль),
	|	МАКСИМУМ(Модель),
	|	МАКСИМУМ(ВариантКомплектации),
	|	МАКСИМУМ(Цвет),
	|	МАКСИМУМ(СуммаРег),
	|	МАКСИМУМ(СуммаУпр),
	|	МАКСИМУМ(ТипСалона),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Опция),
	|	МАКСИМУМ(КоличествоОпция)
	|ПО
	|	ЗаказПокупателя";
	
	Результат = Запрос.Выполнить();
	
	ВыборкаАвтомобили = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаАвтомобили.Следующий() Цикл
		
		НоваяСтрока = Автомобили.Добавить();
		
		НоваяСтрока.Автомобиль          = ВыборкаАвтомобили.Автомобиль;
		НоваяСтрока.Модель              = ВыборкаАвтомобили.Модель;
		НоваяСтрока.ВариантКомплектации = ВыборкаАвтомобили.ВариантКомплектации;
		НоваяСтрока.ТипСалона           = ВыборкаАвтомобили.ТипСалона;
		НоваяСтрока.Цвет                = ВыборкаАвтомобили.Цвет;
		НоваяСтрока.Количество          = 1;
		
		ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		
		Если ОсвобожденОтНДС Тогда
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		
		НоваяСтрока.СуммаНДС = (НоваяСтрока.СтавкаНДС.Ставка * НоваяСтрока.Сумма) / 100;
		
		Если ВалютаДокумента = ВалютаУпр Тогда	
			НоваяСтрока.СуммаВсего = ВыборкаАвтомобили.СуммаУпр;
		ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
			НоваяСтрока.СуммаВсего = ВыборкаАвтомобили.СуммаРег;
		Иначе
			НоваяСтрока.СуммаВсего = обПересчет(ВыборкаАвтомобили.СуммаУпр,ВалютаУпр,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
		
		НоваяСтрока.СрокПоставки        = СрокПоставки;
		НоваяСтрока.ЗаказПокупателя     = ВыборкаАвтомобили.ЗаказПокупателя;
		НоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		
		Если ВыборкаАвтомобили.Опция > 0 Тогда
			
			ВыборкаОпции = ВыборкаАвтомобили.Выбрать();
			
			Пока ВыборкаОпции.Следующий() Цикл
				
				НоваяСтрокаОпции = Опции.Добавить();
				
				НоваяСтрокаОпции.Опция               = ВыборкаОпции.Опция;
				НоваяСтрокаОпции.Количество          = ВыборкаОпции.КоличествоОпция;
				НоваяСтрокаОпции.ИдентификаторСтроки = НоваяСтрока.ИдентификаторСтроки;
				НоваяСтрокаОпции.СтавкаНДС           = ВыборкаОпции.СтавкаНДСОпция;
				
				Если ВалютаДокумента = ВыборкаОпции.ВалютаДокумента Тогда
					НоваяСтрокаОпции.СуммаНДС        = ВыборкаОпции.СуммаНДСОпция;
					НоваяСтрокаОпции.СуммаВсего      = ВыборкаОпции.СуммаВсегоОпция;
				Иначе
					НоваяСтрокаОпции.СуммаНДС        = обПересчет(ВыборкаОпции.СуммаНДСОпция,ВыборкаОпции.ВалютаДокумента,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
					НоваяСтрокаОпции.СуммаВсего      = обПересчет(ВыборкаОпции.СуммаВсегоОпция,ВыборкаОпции.ВалютаДокумента,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
				КонецЕсли;
				
				Если ОсвобожденОтНДС Тогда
					НоваяСтрокаОпции.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				Иначе
					НоваяСтрокаОпции.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				КонецЕсли;
				
				ОбработкаРеквизита("Опции.СуммаВсего",НоваяСтрокаОпции);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//Процедура заполняет табличные части "Автомобили" и "Опции" по продажам
//Параметры:
//ДатаНачала - дата начала
//ДатаОкончания - дата окончания
Процедура ЗаполнитьТЧПоПродажам(ДатаНачала, ДатаОкончания) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажиАвтомобилейОбороты.Автомобиль,
	|	ПродажиАвтомобилейОбороты.КоличествоОборот,
	|	ПродажиАвтомобилейОбороты.Регистратор,
	|	ПродажиАвтомобилейОбороты.Автомобиль.Модель,
	|	ПродажиАвтомобилейОбороты.Автомобиль.ВариантКомплектации,
	|	ПродажиАвтомобилейОбороты.Автомобиль.Цвет,
	|	ПродажиАвтомобилейОбороты.Автомобиль.ТипСалона,
	|	ПродажиАвтомобилейОбороты.СуммаОборот,
	|	ПродажиАвтомобилейОбороты.СуммаУпрОборот
	|ПОМЕСТИТЬ ПродажиАвтомобилей
	|ИЗ
	|	РегистрНакопления.ПродажиАвтомобилей.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиАвтомобилейОбороты
	|ГДЕ
	|	ПродажиАвтомобилейОбороты.Регистратор ССЫЛКА Документ.РеализацияАвтомобилей
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиАвтомобилейОбороты.Автомобиль,
	|	ПродажиАвтомобилейОбороты.КоличествоОборот,
	|	ПродажиАвтомобилейОбороты.Регистратор,
	|	ПродажиАвтомобилейОбороты.СуммаОборот,
	|	ПродажиАвтомобилейОбороты.Автомобиль.Модель,
	|	ПродажиАвтомобилейОбороты.Автомобиль.ВариантКомплектации,
	|	ПродажиАвтомобилейОбороты.Автомобиль.Цвет,
	|	ПродажиАвтомобилейОбороты.Автомобиль.ТипСалона,
	|	ПродажиАвтомобилейОбороты.СуммаУпрОборот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомплектацияАвтомобилейОбороты.Автомобиль КАК Автомобиль,
	|	КомплектацияАвтомобилейОбороты.Номенклатура,
	|	КомплектацияАвтомобилейОбороты.Регистратор,
	|	КомплектацияАвтомобилейОбороты.КоличествоРасход,
	|	КомплектацияАвтомобилейОбороты.СуммаРасход,
	|	КомплектацияАвтомобилейОбороты.СуммаУпрРасход,
	|	КомплектацияАвтомобилейОбороты.СуммаНДСРасход
	|ПОМЕСТИТЬ КомплектацияАвтомобилей
	|ИЗ
	|	РегистрНакопления.КомплектацияАвтомобилей.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК КомплектацияАвтомобилейОбороты
	|ГДЕ
	|	КомплектацияАвтомобилейОбороты.Номенклатура ССЫЛКА Справочник.Опции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПродажиАвтомобилей.АвтомобильМодель КАК Модель,
	|	ПродажиАвтомобилей.АвтомобильВариантКомплектации КАК ВариантКомплектации,
	|	ПродажиАвтомобилей.АвтомобильЦвет КАК Цвет,
	|	ПродажиАвтомобилей.АвтомобильТипСалона КАК ТипСалона,
	|	КомплектацияАвтомобилей.Номенклатура КАК Опция,
	|	ПродажиАвтомобилей.КоличествоОборот КАК Количество,
	|	ПродажиАвтомобилей.СуммаОборот КАК ЦенаРег,
	|	ПродажиАвтомобилей.СуммаОборот * ПродажиАвтомобилей.КоличествоОборот КАК СуммаРег,
	|	КомплектацияАвтомобилей.КоличествоРасход КАК КоличествоОпции,
	|	КомплектацияАвтомобилей.СуммаРасход КАК ЦенаРегОпции,
	|	ПродажиАвтомобилей.Регистратор КАК Регистратор,
	|	ПродажиАвтомобилей.СуммаУпрОборот * ПродажиАвтомобилей.КоличествоОборот КАК СуммаУпр,
	|	КомплектацияАвтомобилей.СуммаУпрРасход КАК ЦенаУпрОпции,
	|	КомплектацияАвтомобилей.СуммаНДСРасход КАК СуммаНДСОпции,
	|	КомплектацияАвтомобилей.СуммаУпрРасход * КомплектацияАвтомобилей.КоличествоРасход КАК СуммаУпрОпции,
	|	КомплектацияАвтомобилей.КоличествоРасход * КомплектацияАвтомобилей.СуммаРасход КАК СуммаРегОпции,
	|	ПродажиАвтомобилей.СуммаУпрОборот КАК ЦенаУпр
	|ИЗ
	|	ПродажиАвтомобилей КАК ПродажиАвтомобилей
	|		ЛЕВОЕ СОЕДИНЕНИЕ КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	|		ПО ПродажиАвтомобилей.Автомобиль = КомплектацияАвтомобилей.Автомобиль
	|			И ПродажиАвтомобилей.Регистратор = КомплектацияАвтомобилей.Регистратор
	|ИТОГИ
	|	МАКСИМУМ(Модель),
	|	МАКСИМУМ(ВариантКомплектации),
	|	МАКСИМУМ(Цвет),
	|	МАКСИМУМ(ТипСалона),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Опция),
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(ЦенаРег),
	|	МАКСИМУМ(СуммаРег),
	|	МАКСИМУМ(СуммаУпр),
	|	МАКСИМУМ(ЦенаУпр)
	|ПО
	|	Регистратор";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаАвтомобили = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаАвтомобили.Следующий() Цикл
		
		НоваяСтрока = Автомобили.Добавить();
		
		НоваяСтрока.Модель              = ВыборкаАвтомобили.Модель;
		НоваяСтрока.ВариантКомплектации = ВыборкаАвтомобили.ВариантКомплектации;
		НоваяСтрока.ТипСалона           = ВыборкаАвтомобили.ТипСалона;
		НоваяСтрока.Цвет                = ВыборкаАвтомобили.Цвет;
		НоваяСтрока.Количество          = ВыборкаАвтомобили.Количество;
		
		//проверка валюты документа
		Если ВалютаДокумента = ВалютаУпр Тогда	
			НоваяСтрока.СуммаВсего = ВыборкаАвтомобили.СуммаУпр;
		ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
			НоваяСтрока.СуммаВсего = ВыборкаАвтомобили.СуммаРег;
		Иначе
			НоваяСтрока.СуммаВсего = обПересчет(ВыборкаАвтомобили.СуммаУпр,ВалютаУпр,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		//проверка ставки НДС
		ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Если ОсвобожденОтНДС Тогда
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		
		НоваяСтрока.СрокПоставки        = СрокПоставки;
		НоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
		
		Если ВыборкаАвтомобили.Опция > 0 Тогда
			
			ВыборкаОпции = ВыборкаАвтомобили.Выбрать();
			
			Пока ВыборкаОпции.Следующий() Цикл
				
				НоваяСтрокаОпции = Опции.Добавить();
				НоваяСтрокаОпции.Опция               = ВыборкаОпции.Опция;
				НоваяСтрокаОпции.Количество          = ВыборкаОпции.КоличествоОпции;
				НоваяСтрокаОпции.ИдентификаторСтроки = НоваяСтрока.ИдентификаторСтроки;
				
				Если ОсвобожденОтНДС Тогда
					НоваяСтрокаОпции.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				Иначе
					НоваяСтрокаОпции.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				КонецЕсли;
				
				Если ВалютаДокумента = ВалютаУпр Тогда
					НоваяСтрокаОпции.СуммаВсего = ВыборкаОпции.СуммаУпрОпции;
				ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
					НоваяСтрокаОпции.СуммаВсего = ВыборкаОпции.СуммаРегОпции;
				Иначе
					НоваяСтрокаОпции.СуммаВсего = обПересчет(ВыборкаОпции.СуммаУпрОпции,ВалютаУпр,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
				КонецЕсли;
				
				ОбработкаРеквизита("Опции.СуммаВсего",НоваяСтрокаОпции);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//Процедура заполняет табличные части "Автомобили" и "Опции" по Заказам поставщику
//Параметры:
//ДатаНачала - дата начала
//ДатаОкончания - дата окончания
Процедура ЗаполнитьТЧПоЗаказамПоставщику(ДатаНачала, ДатаОкончания) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.Модель,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.ВариантКомплектации,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.Цвет,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.ТипСалона,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.КоличествоПриход КАК КоличествоПриход,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль,
	|	МАКСИМУМ(ЗаказыПоставщикамНаАвтомобилиОбороты.СуммаПриход) КАК СуммаПриход,
	|	МАКСИМУМ(ЗаказыПоставщикамНаАвтомобилиОбороты.СуммаУпрПриход) КАК СуммаУпрПриход,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Регистратор
	|ПОМЕСТИТЬ ЗаказПоставщикам
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ЗаказыПоставщикамНаАвтомобилиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.ВариантКомплектации,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.Модель,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.Цвет,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль.ТипСалона,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Автомобиль,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.КоличествоПриход,
	|	ЗаказыПоставщикамНаАвтомобилиОбороты.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПоставщикуНаАвтомобильОпции.Опция,
	|	ЗаказПоставщикуНаАвтомобильОпции.Цена,
	|	ЗаказПоставщикуНаАвтомобильОпции.Ссылка.Модель,
	|	ЗаказПоставщикуНаАвтомобильОпции.Ссылка.Автомобиль,
	|	ЗаказПоставщикуНаАвтомобильОпции.Количество,
	|	ЗаказПоставщикуНаАвтомобильОпции.Сумма,
	|	ЗаказПоставщикуНаАвтомобильОпции.СтавкаНДС,
	|	ЗаказПоставщикуНаАвтомобильОпции.СуммаНДС,
	|	ЗаказПоставщикуНаАвтомобильОпции.СуммаВсего,
	|	ЗаказПоставщикуНаАвтомобильОпции.Ссылка.ВалютаДокумента,
	|	ЗаказПоставщикуНаАвтомобильОпции.Ссылка
	|ПОМЕСТИТЬ Док
	|ИЗ
	|	Документ.ЗаказПоставщикуНаАвтомобиль.Опции КАК ЗаказПоставщикуНаАвтомобильОпции
	|ГДЕ
	|	ЗаказПоставщикуНаАвтомобильОпции.Ссылка В
	|			(ВЫБРАТЬ
	|				ЗаказПоставщикам.Регистратор
	|			ИЗ
	|				ЗаказПоставщикам КАК ЗаказПоставщикам)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказПоставщикам.Автомобиль КАК Автомобиль,
	|	ЗаказПоставщикам.АвтомобильМодель КАК Модель,
	|	ЗаказПоставщикам.АвтомобильВариантКомплектации КАК ВариантКомплектации,
	|	ЗаказПоставщикам.АвтомобильЦвет КАК Цвет,
	|	ЗаказПоставщикам.АвтомобильТипСалона КАК ТипСалона,
	|	СУММА(ЗаказПоставщикам.КоличествоПриход) КАК Количество,
	|	МАКСИМУМ(ЗаказПоставщикам.СуммаПриход) КАК ЦенаРег,
	|	Док.Опция КАК Опция,
	|	МАКСИМУМ(Док.Цена) КАК ЦенаОпция,
	|	СУММА(Док.Количество) КАК КоличествоОпция,
	|	ЗаказПоставщикам.СуммаУпрПриход КАК ЦенаУпр,
	|	Док.СуммаНДС КАК СуммаНДСОпция,
	|	Док.СтавкаНДС КАК СтавкаНДСОпция,
	|	Док.СуммаВсего КАК СуммаВсегоОпция,
	|	ЗаказПоставщикам.КоличествоПриход * ЗаказПоставщикам.СуммаПриход КАК СуммаРег,
	|	ЗаказПоставщикам.КоличествоПриход * ЗаказПоставщикам.СуммаУпрПриход КАК СуммаУпр,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.Сумма КАК СуммаОпция
	|ИЗ
	|	ЗаказПоставщикам КАК ЗаказПоставщикам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Док КАК Док
	|		ПО ЗаказПоставщикам.Регистратор = Док.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщикам.АвтомобильЦвет,
	|	ЗаказПоставщикам.АвтомобильМодель,
	|	Док.Опция,
	|	ЗаказПоставщикам.АвтомобильТипСалона,
	|	ЗаказПоставщикам.АвтомобильВариантКомплектации,
	|	ЗаказПоставщикам.Автомобиль,
	|	ЗаказПоставщикам.СуммаУпрПриход,
	|	Док.СуммаНДС,
	|	Док.СтавкаНДС,
	|	Док.СуммаВсего,
	|	Док.ВалютаДокумента,
	|	Док.Сумма,
	|	ЗаказПоставщикам.КоличествоПриход * ЗаказПоставщикам.СуммаПриход,
	|	ЗаказПоставщикам.КоличествоПриход * ЗаказПоставщикам.СуммаУпрПриход
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказПоставщикам.Автомобиль
	|ИТОГИ
	|	МАКСИМУМ(Модель),
	|	МАКСИМУМ(ВариантКомплектации),
	|	МАКСИМУМ(Цвет),
	|	МАКСИМУМ(ТипСалона),
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(ЦенаРег),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Опция),
	|	МАКСИМУМ(ЦенаУпр),
	|	МАКСИМУМ(СуммаРег),
	|	МАКСИМУМ(СуммаУпр)
	|ПО
	|	Автомобиль";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаАвтомобили = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаАвтомобили.Следующий() Цикл
		
		НоваяСтрока = Автомобили.Добавить();
		
		НоваяСтрока.Модель              = ВыборкаАвтомобили.Модель;
		НоваяСтрока.ВариантКомплектации = ВыборкаАвтомобили.ВариантКомплектации;
		НоваяСтрока.ТипСалона           = ВыборкаАвтомобили.ТипСалона;
		НоваяСтрока.Цвет                = ВыборкаАвтомобили.Цвет;
		НоваяСтрока.Количество          = ВыборкаАвтомобили.Количество;
		
		ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		
		Если ОсвобожденОтНДС Тогда
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		
		НоваяСтрока.СуммаНДС = (НоваяСтрока.СтавкаНДС.Ставка * НоваяСтрока.Сумма) / 100;
		
		Если ВалютаДокумента = ВалютаУпр Тогда
			НоваяСтрока.СуммаВсего = ВыборкаАвтомобили.СуммаУпр;
		ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
			НоваяСтрока.СуммаВсего = ВыборкаАвтомобили.СуммаРег;
		Иначе
			НоваяСтрока.СуммаВсего = обПересчет(ВыборкаАвтомобили.СуммаУпр,ВалютаУпр,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		НоваяСтрока.СрокПоставки        = СрокПоставки;
		НоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
		
		Если ВыборкаАвтомобили.Опция > 0 Тогда
			
			ВыборкаОпции = ВыборкаАвтомобили.Выбрать();
			
			Пока ВыборкаОпции.Следующий() Цикл
				
				НоваяСтрокаОпции = Опции.Добавить();
				
				НоваяСтрокаОпции.Опция               = ВыборкаОпции.Опция;
				НоваяСтрокаОпции.Количество          = ВыборкаОпции.КоличествоОпция;
				НоваяСтрокаОпции.Цена                = ВыборкаОпции.ЦенаОпция;
				НоваяСтрокаОпции.Сумма               = ВыборкаОпции.СуммаВсегоОпция;
				НоваяСтрокаОпции.СтавкаНДС           = ВыборкаОпции.СтавкаНДСОпция;
				НоваяСтрокаОпции.ИдентификаторСтроки = НоваяСтрока.ИдентификаторСтроки;
				
				Если ВалютаДокумента = ВыборкаОпции.ВалютаДокумента Тогда
					НоваяСтрокаОпции.Цена            = ВыборкаОпции.ЦенаОпция;
					НоваяСтрокаОпции.Сумма           = ВыборкаОпции.СуммаОпция;
					НоваяСтрокаОпции.СуммаНДС        = ВыборкаОпции.СуммаНДСОпция;
					НоваяСтрокаОпции.СуммаВсего      = ВыборкаОпции.СуммаВсегоОпция;
				Иначе
					НоваяСтрокаОпции.Цена            = обПересчет(ВыборкаОпции.ЦенаОпция,ВыборкаОпции.ВалютаДокумента,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
					НоваяСтрокаОпции.Сумма           = обПересчет(ВыборкаОпции.СуммаОпция,ВыборкаОпции.ВалютаДокумента,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
					НоваяСтрокаОпции.СуммаНДС        = обПересчет(ВыборкаОпции.СуммаНДСОпция,ВыборкаОпции.ВалютаДокумента,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
					НоваяСтрокаОпции.СуммаВсего      = обПересчет(ВыборкаОпции.СуммаВсегоОпция,ВыборкаОпции.ВалютаДокумента,ТекущаяДата(),ВалютаДокумента,КурсДокумента);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//Удаление лишних строк из табличной части опции
//Параметры
//ИДДляУдаления - Уникальный идетификатор для удаления
Процедура УдалениеСтрокОпций(ИДДляУдаления) Экспорт
	СтрокиДляУдаления = Опции.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ИДДляУдаления));
	Для Каждого Строка Из СтрокиДляУдаления Цикл
		Опции.Удалить(Строка);
	КонецЦикла;
КонецПроцедуры

#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли




#Если Клиент Тогда
Перем ТаблицаПоследниеОткрытыеФормы; // Таблица последних открытых форм

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Регистрация формы в таблице последних открытых форм
Процедура ЗарегистрироватьПоследниеОткрытыеФормы(Представление, ФормаКартинка, МенеджерОбъекта)	
	ТаблицаПоследниеОткрытыеФормы= ВосстановитьЗначение("МенеджерФорм_ТаблицаПоследниеОткрытыеФормы");
	Если ( ТипЗнч(ТаблицаПоследниеОткрытыеФормы) <> Тип("ТаблицаЗначений") ) ИЛИ (ТаблицаПоследниеОткрытыеФормы.Колонки.Количество()<>3) Тогда
		ТаблицаПоследниеОткрытыеФормы = Новый ТаблицаЗначений;
		ТаблицаПоследниеОткрытыеФормы.Колонки.Добавить("Представление");
		ТаблицаПоследниеОткрытыеФормы.Колонки.Добавить("ФормаКартинка");
		ТаблицаПоследниеОткрытыеФормы.Колонки.Добавить("МенеджерОбъекта");	
	КонецЕсли;
	
	КоличествоСохраняемыхФорм = 10;
	ВременнаяТаблица = ТаблицаПоследниеОткрытыеФормы.Скопировать();
	ВременнаяТаблица.Очистить();
	
	НоваяСтрока = ВременнаяТаблица.Добавить();
	НоваяСтрока.Представление = Представление;
	НоваяСтрока.ФормаКартинка = ФормаКартинка;
	НоваяСтрока.МенеджерОбъекта = МенеджерОбъекта;	
	Счетчик = 1;
	Для Индекс = 0 По ТаблицаПоследниеОткрытыеФормы.Количество() - 1 Цикл
		Если ( Счетчик >= КоличествоСохраняемыхФорм ) Тогда Прервать; КонецЕсли;		
		ТекущаяСтрока = ТаблицаПоследниеОткрытыеФормы[Индекс];
		Если ТекущаяСтрока.МенеджерОбъекта <> МенеджерОбъекта Тогда
			НоваяСтрока = ВременнаяТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			Счетчик = Счетчик + 1;
		КонецЕсли;
	КонецЦикла;
	ТаблицаПоследниеОткрытыеФормы = ВременнаяТаблица.Скопировать();
	СохранитьЗначение("МенеджерФорм_ТаблицаПоследниеОткрытыеФормы", ТаблицаПоследниеОткрытыеФормы);
КонецПроцедуры


// Получить строку дерева с заданной формой
Процедура ПолучитьСтрокуФормы(Форма, СтрокаДерева, СтрокаФормы = Неопределено)
	Для Каждого Строка Из СтрокаДерева.Строки Цикл
		Если ( СтрокаФормы <> Неопределено ) Тогда
			Возврат;
		КонецЕсли;
		ПолучитьСтрокуФормы(Форма, Строка, СтрокаФормы);
		Если ( Строка.ФормаСсылка = Форма ) Тогда
			СтрокаФормы = Строка;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Процедура Инициализации дерева форм
Процедура ИнициализироватьДеревоФорм()
	Если ТипЗнч(глДеревоОткрытыхФормАРМ) <> Тип("ДеревоЗначений") Тогда
		глДеревоОткрытыхФормАРМ = Новый ДеревоЗначений;
		глДеревоОткрытыхФормАРМ.Колонки.Добавить("Представление");
		глДеревоОткрытыхФормАРМ.Колонки.Добавить("ФормаСсылка");
		глДеревоОткрытыхФормАРМ.Колонки.Добавить("ФормаКартинка");		
	КонецЕсли;
КонецПроцедуры

// Процедура формирует массив ошибочных форм
Procedure ПолучитьМассивОшибочныхФорм(СтрокаДерева, МассивОшибок)
	Для Каждого Строка Из СтрокаДерева.Строки Цикл
		ПолучитьМассивОшибочныхФорм(Строка, МассивОшибок);
		ЭтоСтрокаСОшибкой = Ложь;
		Попытка
			Если ( НЕ Строка.ФормаСсылка.Открыта() ) Тогда
				ЭтоСтрокаСОшибкой = Истина;
			КонецЕсли;
		Исключение
			ЭтоСтрокаСОшибкой = Истина;
		КонецПопытки;
		Если ЭтоСтрокаСОшибкой Тогда
			МассивОшибок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
EndProcedure

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Поиск и удаление ошибочных форм
Процедура КонтрольИИсправлениеДереваФорм()	Экспорт
	МассивОшибок = Новый Массив;
	СтрокаДерева = глДеревоОткрытыхФормАРМ;
	ПолучитьМассивОшибочныхФорм(СтрокаДерева, МассивОшибок);
	Для Индекс = 0 По МассивОшибок.ВГраница() Цикл
		ОшибкаСтрока = МассивОшибок[Индекс];
		Если ( ОшибкаСтрока.Родитель = Неопределено ) Тогда
			РодительСтрока = СтрокаДерева;
		Иначе
			РодительСтрока = ОшибкаСтрока.Родитель;
		КонецЕсли;
		Попытка
			РодительСтрока.Строки.Удалить(ОшибкаСтрока);
		Исключение КонецПопытки;
	КонецЦикла;
КонецПроцедуры

// Регистрация открытия формы.
//Параметры:
//  Форма			- Форма		- форма помещаемая в дерево форм;
//	МенеджерОбъекта - тип: ЛюбаяСсылка или Строка - в случае добавления объекта является ссылкой на объект,
//													в остальных случаях - это строка запуска формы посредством Выполнить(Строка);
//	ФормаКартинка	- Строка	- тип картинки;
//	Представление	- Строка	- представление объекта на панели АРМ.
Процедура ЗарегистрироватьФорму(Форма, МенеджерОбъекта, ФормаКартинка, Представление=Неопределено)	Экспорт

	КонтрольИИсправлениеДереваФорм();
        
	СтрокаФормы = Неопределено;
	ПолучитьСтрокуФормы(Форма, глДеревоОткрытыхФормАРМ, СтрокаФормы);
	Если ( СтрокаФормы <> Неопределено ) Тогда // если форма уже в дереве
		РодительСтроки = СтрокаФормы.Родитель;
		Если ( РодительСтроки = Неопределено ) Тогда
			РодительСтроки = глДеревоОткрытыхФормАРМ;
		КонецЕсли;
		РодительСтроки.Строки.Удалить(СтрокаФормы);
	КонецЕсли;
	
	Если ( Форма.ВладелецФормы <> Неопределено ) Тогда
		ВладелецФормыНоваяСтрока = Неопределено;
		ПолучитьСтрокуФормы(Форма.ВладелецФормы, глДеревоОткрытыхФормАРМ, ВладелецФормыНоваяСтрока);
		Если ( ВладелецФормыНоваяСтрока <> Неопределено ) Тогда
			НоваяСтрока = ВладелецФормыНоваяСтрока.Строки.Добавить();
		Иначе
			НоваяСтрока = глДеревоОткрытыхФормАРМ.Строки.Добавить();
		КонецЕсли;
	Иначе
		НоваяСтрока = глДеревоОткрытыхФормАРМ.Строки.Добавить();
	КонецЕсли;
	
	Если ( Представление = Неопределено ) Тогда
		Представление = Форма.Заголовок;
	КонецЕсли;
	НоваяСтрока.Представление = Представление;
	НоваяСтрока.ФормаСсылка = Форма;
	НоваяСтрока.ФормаКартинка = ФормаКартинка;
	Если НЕ обЗначениеНеЗаполнено(МенеджерОбъекта) Тогда
		ЗарегистрироватьПоследниеОткрытыеФормы(Представление, ФормаКартинка, МенеджерОбъекта);
	КонецЕсли;
	Оповестить("МенеджерФорм_ДеревоФормОбновить");
КонецПроцедуры

// Удалить форму из дерева
Процедура УдалитьИзДереваФорм(Форма) Экспорт
	СтрокаФормы = Неопределено;
	ПолучитьСтрокуФормы(Форма, глДеревоОткрытыхФормАРМ, СтрокаФормы);
	Если ( СтрокаФормы <> Неопределено ) Тогда // если форма уже в дереве
		РодительСтроки = СтрокаФормы.Родитель;
		Если ( РодительСтроки = Неопределено ) Тогда
			РодительСтроки = глДеревоОткрытыхФормАРМ;
		КонецЕсли;
		РодительСтроки.Строки.Удалить(СтрокаФормы);
	КонецЕсли;
	Оповестить("МенеджерФорм_ДеревоФормОбновить");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ИнициализироватьДеревоФорм();

#КонецЕсли
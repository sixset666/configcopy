&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ns_Авито.НачальноеЗаполнение();
	ЗагрузитьНастройки();
	
	ОписаниеТипа = ns_Переопределяемый.ПолучитьТипВидыЦен();
	Элементы.ВидЦены.ОграничениеТипа = ОписаниеТипа;
	Элементы.ВидЦены.ВыбиратьТип = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НомерТекущейСтраницы = 1;
	УстановитьВидимостьГруппИЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
КонецПроцедуры

&НаКлиенте
Процедура ns_ИспользоватьЗапчастиИАксессуарыПриИзменении(Элемент)
	УстановитьВидимостьГруппИЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьБытоваяЭлектроникаПриИзменении(Элемент)
	УстановитьВидимостьГруппИЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВперед(Команда)
	Если МожноПерейтиВперед() Тогда
		НомерТекущейСтраницы = НомерТекущейСтраницы + 1;
	КонецЕсли; 
	УстановитьВидимостьГруппИЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНазад(Команда)
	Если МожноПерейтиНазад() Тогда
		НомерТекущейСтраницы = НомерТекущейСтраницы - 1;
	КонецЕсли; 
	УстановитьВидимостьГруппИЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьГруппИЭлементов()
	Элементы.ГруппаВыборКатегории.Видимость = Ложь;
	Элементы.ГруппаНСИ.Видимость = Ложь;
	Элементы.ЗагрузитьСправочникиЗапчастиИАксуссуары.Видимость = Ложь;	
	Элементы.ГруппаВыгрузка.Видимость = Ложь;
	Элементы.ГруппаШаблон.Видимость = Ложь;
	Элементы.ГруппаПоУмолчанию.Видимость = Ложь;
	Элементы.ГруппаНастройки.Видимость = Ложь;
	Элементы.ГруппаНоменклатура.Видимость = Ложь;
	Элементы.ГруппаИтог.Видимость = Ложь;
	Элементы.ConditionБытоваяЭлектроника.Видимость = Объект.ИспользоватьБытоваяЭлектроника;
	Элементы.ConditionЗапчасти.Видимость = Объект.ИспользоватьЗапчастиИАксессуары;
	Элементы.ЗонаПоказаОбъявления.Видимость = Объект.ИспользоватьЗапчастиИАксессуары;
	Элементы.ConditionДляДомаИДачи.Видимость = Объект.ИспользоватьДляДомаИДачи;
	Элементы.ConditionДляБизнеса.Видимость = Объект.ИспользоватьДляБизнеса;
	Элементы.ConditionХоббиИОтдых.Видимость = Объект.ИспользоватьХоббиИОтдых;
	Элементы.ConditionМотоциклыИМототехника.Видимость = Объект.ИспользоватьМотоциклыИМототехника;
	Элементы.ConditionЛичныеВещи.Видимость = Объект.ИспользоватьЛичныеВещи;
	
	УстановитьВидимостьЭлементовВыгрузки();
	
	Если НомерТекущейСтраницы = 1 Тогда
		Элементы.ГруппаВыборКатегории.Видимость = Истина;
		ЭтаФорма.Заголовок = "Выбор категорий";
	ИначеЕсли НомерТекущейСтраницы = 2 Тогда 
		Элементы.ГруппаНСИ.Видимость = Истина;
		ЭтаФорма.Заголовок = "Настройка НСИ";
		Если Объект.ИспользоватьЗапчастиИАксессуары Тогда
			Элементы.ЗагрузитьСправочникиЗапчастиИАксуссуары.Видимость = Истина;	
		КонецЕсли; 
	ИначеЕсли НомерТекущейСтраницы = 3 Тогда 		
		ЭтаФорма.Заголовок = "Настройка выгрузки данных";
		Элементы.ГруппаВыгрузка.Видимость = Истина;
	ИначеЕсли НомерТекущейСтраницы = 4 Тогда 		
		ЭтаФорма.Заголовок = "Настройка шаблонов";
		Элементы.ГруппаШаблон.Видимость = Истина;
	ИначеЕсли НомерТекущейСтраницы = 5 Тогда 		
		ЭтаФорма.Заголовок = "Настройка значений по умолчанию";
		Элементы.ГруппаПоУмолчанию.Видимость = Истина;
	ИначеЕсли НомерТекущейСтраницы = 6 Тогда 		
		ЭтаФорма.Заголовок = "Настройки формирования и выгрузки";
		Элементы.ГруппаНастройки.Видимость = Истина;
	ИначеЕсли НомерТекущейСтраницы = 7 Тогда 		
		ЭтаФорма.Заголовок = "Заполнение справочников Авито";
		Элементы.ГруппаНоменклатура.Видимость = Истина;
	ИначеЕсли НомерТекущейСтраницы = 8 Тогда 		
		ЭтаФорма.Заголовок = "Данные договора";
		Элементы.ГруппаИтог.Видимость = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементовВыгрузки()
	Элементы.FTPIPАдрес.Видимость = Ложь;
	Элементы.FTPLogin.Видимость = Ложь;
	Элементы.FTPPassword.Видимость = Ложь;
	Элементы.FTPПассивныйРежим.Видимость = Ложь;
	Элементы.FTPПорт.Видимость = Ложь;
	Элементы.ИмяДомена.Видимость = Ложь;
	Элементы.ПутьДляВыгрузки.Видимость = Ложь;
	Элементы.ПроверитьFTP.Видимость = Ложь;
	
	Если Объект.ВидВыгрузки = ПредопределенноеЗначение("Перечисление.ns_ВидыВыгрузки.File") Тогда
		Элементы.ПутьДляВыгрузки.Видимость = Истина;
	ИначеЕсли Объект.ВидВыгрузки = ПредопределенноеЗначение("Перечисление.ns_ВидыВыгрузки.FileHosting") Тогда 	
		Элементы.ИмяДомена.Видимость = Истина;
		Элементы.ПутьДляВыгрузки.Видимость = Истина;
	ИначеЕсли Объект.ВидВыгрузки = ПредопределенноеЗначение("Перечисление.ns_ВидыВыгрузки.FTP") Тогда 	
		Элементы.FTPIPАдрес.Видимость = Истина;
		Элементы.FTPLogin.Видимость = Истина;
		Элементы.FTPPassword.Видимость = Истина;
		Элементы.FTPПассивныйРежим.Видимость = Истина;
		Элементы.FTPПорт.Видимость = Истина;
		Элементы.ИмяДомена.Видимость = Истина;
		Элементы.ПроверитьFTP.Видимость = Истина;
	КонецЕсли; 	
КонецПроцедуры

&НаКлиенте
Функция МожноПерейтиВперед()
	Если НомерТекущейСтраницы = 1 Тогда
		Если НЕ Объект.ИспользоватьЗапчастиИАксессуары И 
			НЕ Объект.ИспользоватьБытоваяЭлектроника И
			НЕ Объект.ИспользоватьДляДомаИДачи И
			НЕ Объект.ИспользоватьДляБизнеса И
			НЕ Объект.ИспользоватьХоббиИОтдых И
			НЕ Объект.ИспользоватьМотоциклыИМототехника И
			НЕ Объект.ИспользоватьЛичныеВещи Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Выберите хотя бы одну категорию выгрузки");
			Возврат Ложь;
		КонецЕсли; 
	ИначеЕсли НомерТекущейСтраницы = 2 Тогда
		Если Объект.ИспользоватьЗапчастиИАксессуары Тогда
			Если НЕ СправочникПроизводителейЗагружен() Тогда
				ns_ОбщегоНазначения.СообщениеПользователю("Заполните служебные справочники для категории Запчасти и аксессуары");
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли НомерТекущейСтраницы = 3 Тогда
		Если НЕ ЗаполненыПараметрыВыгрузки() Тогда
			Возврат Ложь;
		КонецЕсли; 
	ИначеЕсли НомерТекущейСтраницы = 4 Тогда 	
		Если НЕ ЗначениеЗаполнено(Объект.ШаблонПоУмолчанию) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните шаблон по умолчанию");
			Возврат Ложь;
		КонецЕсли; 
	ИначеЕсли НомерТекущейСтраницы = 5 Тогда 	
	ИначеЕсли НомерТекущейСтраницы = 6 Тогда
	ИначеЕсли НомерТекущейСтраницы = 7 Тогда 	
		Если НЕ СправочникСопоставлениеЗагружен() Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Нажмите Сопоставить номенклатуру");
			Возврат Ложь;
		КонецЕсли; 
	ИначеЕсли НомерТекущейСтраницы > 7 Тогда 
		НомерТекущейСтраницы = 7;
	КонецЕсли; 	
	
	ЗаписатьЗначениеВБазу();
	Возврат Истина;
КонецФункции

&НаКлиенте
Функция МожноПерейтиНазад()
	Если НомерТекущейСтраницы = 1 Тогда
		Возврат Ложь;
	КонецЕсли; 
	Возврат Истина;
КонецФункции

// Область Запчасти и аксессуары 

&НаКлиенте
Процедура ЗагрузитьСправочникиЗапчастиИАксуссуары(Команда)
	ЗагрузитьСправочникиЗапчастиИАксуссуарыНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьСправочникиЗапчастиИАксуссуарыНаСервере()
	ns_ЗапчастиИАксессуары.ЗагрузитьПроизводителейЗапчастей(Истина);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СправочникПроизводителейЗагружен()
	Возврат ns_ЗапчастиИАксессуары.СправочникПроизводителейЗагружен();
КонецФункции

&НаКлиенте
Процедура ns_ВидВыгрузкиПриИзменении(Элемент)
	УстановитьВидимостьЭлементовВыгрузки();
КонецПроцедуры

&НаКлиенте
Процедура ВидВыгрузкиОчистка(Элемент, СтандартнаяОбработка)
	УстановитьВидимостьЭлементовВыгрузки();
КонецПроцедуры

&НаКлиенте
Процедура ns_ПутьДляВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Папка выгрузки данных:";
	Если Диалог.Выбрать() Тогда
		Объект.ПутьДляВыгрузки = Диалог.Каталог;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьFTP(Команда)
	ЗаписатьЗначениеВБазу();
	
	Если НЕ ЗначениеЗаполнено(Объект.FTPIPАдрес) ИЛИ
		НЕ ЗначениеЗаполнено(Объект.FTPLogin) ИЛИ
		НЕ ЗначениеЗаполнено(Объект.FTPPassword) ИЛИ
		НЕ ЗначениеЗаполнено(Объект.ИмяДомена) Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Заполните обязательные параметры для доступа к хостингу по FTP");
		Возврат;
	КонецЕсли; 
	
	Результат = ВыгрузитьТестовыйФайлНаХостинг();
	Если Результат Тогда
		ИмяФайлаНаХостинге = Объект.ИмяДомена + "/avito.xml";
		ЗапуститьПриложение(ИмяФайлаНаХостинге);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьТестовыйФайлНаХостинг()
	Попытка
		ИмяФайлаОбмена = КаталогВременныхФайлов() + "avito.xml";
		
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьФайл(ИмяФайлаОбмена);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Ads");
		
		ЗаписьXML.ЗаписатьАтрибут("formatVersion", "3");
		ЗаписьXML.ЗаписатьАтрибут("target", "Avito.ru");
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); // Ads
		ЗаписьXML.Закрыть();
		
		Порт = ?(ЗначениеЗаполнено(Объект.FTPПорт), Объект.FTPПорт, 21);
		ПассивныйРежим = ?(ЗначениеЗаполнено(Объект.FTPПассивныйРежим), Объект.FTPПассивныйРежим, Ложь);
		
		FTP = Новый FTPСоединение(Объект.FTPIPАдрес, Порт, Объект.FTPLogin, Объект.FTPPassword,, ПассивныйРежим);
		FTP.Записать(ИмяФайлаОбмена, "avito.xml");
	Исключение
		ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки; 
	
	Возврат Истина;
КонецФункции

// КонецОбласти  

&НаКлиенте
Процедура СопоставитьНоменклатуру(Команда)
	СопоставитьНоменклатуруНаСервере();
	ns_ОбщегоНазначения.СообщениеПользователю("Номенклатура сопоставлена");
КонецПроцедуры

&НаСервере
Процедура СопоставитьНоменклатуруНаСервере()
	ns_Сопоставление.СопоставитьВсюНоменклатуру();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначениеВБазу()
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		НачатьТранзакцию();
		
		НаборЗаписей = РегистрыСведений.ns_НастройкиАвито.СоздатьНаборЗаписей();
		Для каждого ИмяРеквизиты Из РеквизитыНастройки() Цикл
			ДобавитьНастройку(Перечисления.ns_НастройкиАвито[ИмяРеквизиты], Объект[ИмяРеквизиты], НаборЗаписей);
		КонецЦикла; 
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
		Сообщить(ОписаниеОшибки());
	КонецПопытки; 
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНастройку(Настройка, Значение, НаборЗаписей)
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Настройка = Настройка;
	Если Настройка = Перечисления.ns_НастройкиАвито.ВидЦены Тогда
		НоваяЗапись.Значение = XMLСтрока(Значение);
	Иначе	
		НоваяЗапись.Значение = Значение;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	НаборЗаписей = РегистрыСведений.ns_НастройкиАвито.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Для каждого Запись Из НаборЗаписей Цикл
		ИмяРеквизита = XMLСтрока(Запись.Настройка); 
		Попытка
			Если Запись.Настройка = Перечисления.ns_НастройкиАвито.ВидЦены Тогда
				Объект[ИмяРеквизита] = ns_Переопределяемый.ПолучитьСсылкуВидыЦен(Запись.Значение);
			Иначе	
				Объект[ИмяРеквизита] = Запись.Значение;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЗаписатьЗначениеВБазу();
	Закрыть();
КонецПроцедуры
 
Функция РеквизитыНастройки()
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("AdType");
	Реквизиты.Добавить("ConditionЗапчасти");
	Реквизиты.Добавить("ЗонаПоказаОбъявления");
	Реквизиты.Добавить("ConditionБытоваяЭлектроника");
	Реквизиты.Добавить("ConditionДляДомаИДачи");
	Реквизиты.Добавить("ConditionДляБизнеса");
	Реквизиты.Добавить("ConditionХоббиИОтдых");
	Реквизиты.Добавить("ConditionМотоциклыИМототехника");
	Реквизиты.Добавить("ConditionЛичныеВещи");
	Реквизиты.Добавить("FTPIPАдрес");
	Реквизиты.Добавить("FTPLogin");
	Реквизиты.Добавить("FTPPassword");
	Реквизиты.Добавить("FTPПассивныйРежим");
	Реквизиты.Добавить("FTPПорт");
	Реквизиты.Добавить("ВидВыгрузки");
	Реквизиты.Добавить("ВидЦены");
	Реквизиты.Добавить("ВыбиратьМинимальнуюЦену");
	Реквизиты.Добавить("ДобавлятьВВыгрузку");
	Реквизиты.Добавить("ВыгружатьБезОстатков");
	Реквизиты.Добавить("ИмяДомена");
	Реквизиты.Добавить("НеВыгружатьВРезерве");
	Реквизиты.Добавить("ПутьДляВыгрузки");
	Реквизиты.Добавить("ШаблонПоУмолчанию");
	Реквизиты.Добавить("ШаблонЗаголовка");
	Реквизиты.Добавить("ИспользоватьЗапчастиИАксессуары");
	Реквизиты.Добавить("ИспользоватьБытоваяЭлектроника");
	Реквизиты.Добавить("ИспользоватьДляДомаИДачи");
	Реквизиты.Добавить("ИспользоватьДляБизнеса");
	Реквизиты.Добавить("ИспользоватьХоббиИОтдых");
	Реквизиты.Добавить("ИспользоватьМотоциклыИМототехника");
	Реквизиты.Добавить("ИспользоватьЛичныеВещи");
	Реквизиты.Добавить("ДобавлятьIDКатегории");
	Реквизиты.Добавить("НомерДоговора");
	Реквизиты.Добавить("УстанавливатьДоступностьПоОстаткам");
	Реквизиты.Добавить("БратьЗаголовокИзПолногоНаименования");
	Реквизиты.Добавить("ПодставлятьХарактеристикуВЗаголовок");
	Реквизиты.Добавить("НеПерезаполнятьОписание");
	Реквизиты.Добавить("НеПерезаполнятьЗаголовок");
	Реквизиты.Добавить("СоздаватьОбъявлениеНаКаждуюХарактеристику");
	Реквизиты.Добавить("БратьФотоАвито");
	Реквизиты.Добавить("ОтображатьИерархиюПоВидамНоменклатуры");
	Реквизиты.Добавить("ВыгружатьКартинкиБезОптимизации");
	
	Возврат Реквизиты;
КонецФункции
 
&НаКлиенте
Функция ЗаполненыПараметрыВыгрузки()
	Если НЕ ЗначениеЗаполнено(Объект.ВидВыгрузки) Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Заполните Вид выгрузки");
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ВидВыгрузки = ПредопределенноеЗначение("Перечисление.ns_ВидыВыгрузки.File") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ПутьДляВыгрузки) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните Путь для выгрузки");
			Возврат Ложь;
		КонецЕсли; 
	ИначеЕсли Объект.ВидВыгрузки = ПредопределенноеЗначение("Перечисление.ns_ВидыВыгрузки.FileHosting") Тогда 	
		Если НЕ ЗначениеЗаполнено(Объект.ПутьДляВыгрузки) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните Путь для выгрузки");
			Возврат Ложь;
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(Объект.ИмяДомена) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните Имя домена");
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли Объект.ВидВыгрузки = ПредопределенноеЗначение("Перечисление.ns_ВидыВыгрузки.FTP") Тогда 	
		Если НЕ ЗначениеЗаполнено(Объект.ИмяДомена) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните Имя домена");
			Возврат Ложь;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.FTPIPАдрес) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните FTP IP Адрес");
			Возврат Ложь;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.FTPLogin) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните FTP Login");
			Возврат Ложь;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.FTPPassword) Тогда
			ns_ОбщегоНазначения.СообщениеПользователю("Заполните FTP Passwordа");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция СправочникСопоставлениеЗагружен()
	
	Если Объект.ИспользоватьЗапчастиИАксессуары = Истина И НЕ ns_ЗапчастиИАксессуары.СправочникЗапчастиСопоставлен() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Запчасти и аксессуары не сопоставлены. Нажмите Сопоставить номенклатуру");
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ИспользоватьБытоваяЭлектроника = Истина И НЕ ns_БытоваяЭлектроника.СправочникБытоваяЭлектроникаСопоставлен() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Бытовая техника не сопоставлена. Нажмите Сопоставить номенклатуру");
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ИспользоватьДляДомаИДачи = Истина И НЕ ns_ДляДомаИДачи.СправочникДляДомаИДачиСопоставлен() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Для дома и дачи не сопоставлен. Нажмите Сопоставить номенклатуру");
		Возврат Ложь;
	КонецЕсли; 

	Если Объект.ИспользоватьДляБизнеса = Истина И НЕ ns_ДляБизнеса.СправочникДляБизнесаСопоставлен() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Для бизнеса не сопоставлен. Нажмите Сопоставить номенклатуру");
		Возврат Ложь;
	КонецЕсли; 

	Если Объект.ИспользоватьХоббиИОтдых = Истина И НЕ ns_ХоббиИОтдых.СправочникХоббиИОтдыхСопоставлен() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Хобби и отдых не сопоставлен. Нажмите Сопоставить номенклатуру");
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ИспользоватьМотоциклыИМототехника = Истина И НЕ ns_МотоциклыИМототехника.СправочникМотоциклыИМототехникаСопоставлен() Тогда
		ns_ОбщегоНазначения.СообщениеПользователю("Мотоциклы и мототехника не сопоставлен. Нажмите Сопоставить номенклатуру");
		Возврат Ложь;
	КонецЕсли; 
	
	Возврат Истина;
КонецФункции

&НаСервере
Процедура ОчиститьСопоставленияНаСервере()
	Если Объект.ИспользоватьЗапчастиИАксессуары Тогда
		ОчиститьСправочникСопоставления("ns_ЗапчастиИАксессуары");
	КонецЕсли; 
	Если Объект.ИспользоватьБытоваяЭлектроника Тогда
		ОчиститьСправочникСопоставления("ns_БытоваяЭлектроника");
	КонецЕсли; 
	Если Объект.ИспользоватьДляДомаИДачи Тогда
		ОчиститьСправочникСопоставления("ns_ДляДомаИДачи");
	КонецЕсли; 
	Если Объект.ИспользоватьДляБизнеса Тогда
		ОчиститьСправочникСопоставления("ns_ДляБизнеса");
	КонецЕсли; 
	Если Объект.ИспользоватьХоббиИОтдых Тогда
		ОчиститьСправочникСопоставления("ns_ХоббиИОтдых");
	КонецЕсли; 
	Если Объект.ИспользоватьМотоциклыИМототехника Тогда
		ОчиститьСправочникСопоставления("ns_МотоциклыИМототехника");
	КонецЕсли; 
	Если Объект.ИспользоватьЛичныеВещи Тогда
		ОчиститьСправочникСопоставления("ns_ЛичныеВещи");
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьСправочникСопоставления(ИмяСправочника)
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникАвито.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ns_ЗапчастиИАксессуары КАК СправочникАвито";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ns_ЗапчастиИАксессуары", ИмяСправочника); 
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	Попытка
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.УстановитьПометкуУдаления(Истина);
		КонецЦикла; 
	Исключение
		ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСопоставления(Команда)
	ОчиститьСопоставленияНаСервере();
	ns_ОбщегоНазначения.СообщениеПользователю("Справочники очищены");
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНСИ(Команда)
	ЗагрузитьНСИНаСервере();
КонецПроцедуры        

&НаСервереБезКонтекста
Процедура ЗагрузитьНСИНаСервере()
	ns_НСИ.ЗагрузитьНСИ();
КонецПроцедуры

 












Перем КэшОстатковНоменклатурыНаСкладе;

Процедура ЗаполнитьРекомендации(ЗаполнениеСуммРекомендаций=Истина) Экспорт
	Запрос=Новый Запрос;
	ТекстЗапроса= 
		"ВЫБРАТЬ
		|	РекомендацииПоАвтомобилю.Период КАК Период,
		|	РекомендацииПоАвтомобилю.Период КАК ПериодЗаписи,
		|	РекомендацииПоАвтомобилю.Автомобиль КАК Автомобиль,
		|	РекомендацииПоАвтомобилю.Рекомендация.Артикул КАК РекомендацияАртикул,
		|	РекомендацииПоАвтомобилю.Рекомендация КАК Рекомендация,
		|	РекомендацииПоАвтомобилю.Рекомендация КАК РекомендацияЗаписи,
		|	РекомендацииПоАвтомобилю.Количество КАК Количество,
		|	РекомендацииПоАвтомобилю.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт,
		|	РекомендацииПоАвтомобилю.ЗаказНаряд КАК ЗаказНаряд,
		|	РекомендацииПоАвтомобилю.ПричинаОтказа КАК ПричинаОтказа,
		|	РекомендацииПоАвтомобилю.ДатаВыполнения КАК ДатаВыполнения,
		|	РекомендацииПоАвтомобилю.Автор КАК Автор,
		|	РекомендацииПоАвтомобилю.Исполнитель КАК Исполнитель,
		|	РекомендацииПоАвтомобилю.Выполнена КАК Выполнена,
		|	ВЫБОР
		|		КОГДА РекомендацииПоАвтомобилю.Выполнена = ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияСобытий.Завершено)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА (РекомендацииПоАвтомобилю.ЗаявкаНаРемонт <> ЗНАЧЕНИЕ(Документ.ЗаявкаНаРемонт.ПустаяСсылка)
		|						ИЛИ РекомендацииПоАвтомобилю.ЗаказНаряд <> ЗНАЧЕНИЕ(Документ.ЗаказНаряд.ПустаяСсылка))
		|						И РекомендацииПоАвтомобилю.Выполнена = ЛОЖЬ
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияСобытий.Выполняется)
		|				ИНАЧЕ ВЫБОР
		|						КОГДА РекомендацииПоАвтомобилю.ПричинаОтказа <> ЗНАЧЕНИЕ(Справочник.ПричиныОтказаОтОбслуживания.ПустаяСсылка)
		|							ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияСобытий.Отменено)
		|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияСобытий.Запланировано)
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Состояние,
		|	РекомендацииПоАвтомобилю.ДатаАктуальности,
		|	РекомендацииПоАвтомобилю.Комментарий КАК Комментарий,
		|	РекомендацииПоАвтомобилю.Комментарий КАК КомментарийЗаписи
		|ИЗ
		|	РегистрСведений.РекомендацииПоАвтомобилю КАК РекомендацииПоАвтомобилю
		|ГДЕ
		|	РекомендацииПоАвтомобилю.Автомобиль = &Автомобиль
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	Рекомендация";
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Автомобиль",Автомобиль);
	Рекомендации.Загрузить(Запрос.Выполнить().Выгрузить());
	Если ЗаполнениеСуммРекомендаций Тогда
		ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Для каждого СтрокаРекомендации Из Рекомендации Цикл
			РасчитатьЦенуРекомендации(СтрокаРекомендации);
		КонецЦикла; 
	КонецЕсли;
	Модифицированность=Ложь;
КонецПроцедуры

Процедура РасчитатьЦенуРекомендации(СтрокаРекомендации) Экспорт
	Если ЗначениеЗаполнено(Дата) Тогда
		ДатаЦен=КонецДня(Дата);
	Иначе
		ДатаЦен=Неопределено;
	КонецЕсли;
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	Коэффициент=1;
	Если ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
		Если ТипЗнч(СтрокаРекомендации.Рекомендация)=Тип("СправочникСсылка.Номенклатура") Тогда
			Цена=обПолучитьЦену(ТипЦен,СтрокаРекомендации.Рекомендация,ДатаЦен,,ВалютаРегл,,,,ПараметрыСеанса.ПодразделениеКомпании);
		ИначеЕсли ТипЗнч(СтрокаРекомендации.Рекомендация)=Тип("СправочникСсылка.Автоработы") Тогда
			ЦенаРаботы=орПолучитьЦенуАвтоработы(ТипЦенРабот,СтрокаРекомендации.Рекомендация,Автомобиль.Модель,ДоговорВзаиморасчетов,Цех,ВидРемонта,ДатаЦен,ВалютаРегл,1);
			Нормочас=ЦенаРаботы.Нормочас;
			Коэффициент=?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени),1,ЦенаРаботы.НормаВремени);
			Цена=ЦенаРаботы.Цена;
		Иначе
			Цена=0;
		КонецЕсли; 
	Иначе
		Цена=0;
	КонецЕсли; 
	СтрокаРекомендации.Цена=Цена;
	СтрокаРекомендации.Коэффициент=Коэффициент;
	СтрокаРекомендации.Сумма=СтрокаРекомендации.Цена*СтрокаРекомендации.Количество*СтрокаРекомендации.Коэффициент;
КонецПроцедуры

Процедура КоличествоПриИзменении(СтрокаРекомендации) Экспорт
	СтрокаРекомендации.Сумма=СтрокаРекомендации.Цена*СтрокаРекомендации.Количество*СтрокаРекомендации.Коэффициент;
КонецПроцедуры

Процедура ЗаявкаНаРемонтПриИзменении(СтрокаРекомендации) Экспорт
	Если ЗначениеЗаполнено(СтрокаРекомендации.ЗаявкаНаРемонт) Тогда
		СтрокаРекомендации.ПричинаОтказа=Неопределено;
	КонецЕсли;
	ЗаказНарядПриИзменении(СтрокаРекомендации);
КонецПроцедуры

Процедура ЗаказНарядПриИзменении(СтрокаРекомендации) Экспорт
	Если ЗначениеЗаполнено(СтрокаРекомендации.ЗаказНаряд) Тогда
		СтрокаРекомендации.ПричинаОтказа=Неопределено;
		Если СтрокаРекомендации.ЗаказНаряд.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен
			 ИЛИ СтрокаРекомендации.ЗаказНаряд.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			СтрокаРекомендации.ДатаВыполнения=СтрокаРекомендации.ЗаказНаряд.ДатаОкончания;
			СтрокаРекомендации.Выполнена=Истина;
			СтрокаРекомендации.Состояние=Перечисления.СостоянияСобытий.Завершено;
		Иначе
			СтрокаРекомендации.ДатаВыполнения=Неопределено;
			СтрокаРекомендации.Выполнена=Ложь;
			СтрокаРекомендации.Состояние=Перечисления.СостоянияСобытий.Выполняется;
		КонецЕсли; 
	Иначе
		СтрокаРекомендации.ДатаВыполнения=Неопределено;
		Если НЕ ЗначениеЗаполнено(СтрокаРекомендации.ЗаявкаНаРемонт) Тогда
			СтрокаРекомендации.Исполнитель=Неопределено;
		КонецЕсли; 
		СтрокаРекомендации.Выполнена=Ложь;
		Если ЗначениеЗаполнено(СтрокаРекомендации.ПричинаОтказа) Тогда
			СтрокаРекомендации.Состояние=Перечисления.СостоянияСобытий.Отменено;
		ИначеЕсли ЗначениеЗаполнено(СтрокаРекомендации.ЗаявкаНаРемонт) Тогда
			СтрокаРекомендации.Состояние=Перечисления.СостоянияСобытий.Выполняется;
		Иначе
			СтрокаРекомендации.Состояние=Перечисления.СостоянияСобытий.Запланировано;
		КонецЕсли; 
	КонецЕсли;
	СтрокаРекомендации.ЗаказНарядТекущий=Ложь;
КонецПроцедуры

Процедура ПричинаОтказаПриИзменении(СтрокаРекомендации) Экспорт
	Если ЗначениеЗаполнено(СтрокаРекомендации.ПричинаОтказа) Тогда
		СтрокаРекомендации.ЗаявкаНаРемонт=Неопределено;
		СтрокаРекомендации.ЗаказНаряд=Неопределено;
		СтрокаРекомендации.ДатаВыполнения=ТекущаяДата();
		СтрокаРекомендации.Исполнитель=ПараметрыСеанса.Пользователь;
		СтрокаРекомендации.Выполнена=Ложь;
		СтрокаРекомендации.Состояние=Перечисления.СостоянияСобытий.Отменено;
	Иначе
		ЗаказНарядПриИзменении(СтрокаРекомендации);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаписатьРекомендации() Экспорт
	Результат=Истина;
	Для каждого СтрокаРекомендации Из Рекомендации Цикл
		Результат=ЗаписатьСтрокуРекомендации(СтрокаРекомендации,Ложь);
	КонецЦикла; 
	#Если Клиент Тогда
	Оповестить("РекомендацияЗаписана",Автомобиль,ЭтотОбъект);
	#КонецЕсли
	Если Результат Тогда
		Модифицированность=Ложь;
	КонецЕсли; 
КонецПроцедуры
 
Функция ЗаписатьСтрокуРекомендации(СтрокаРекомендации,Оповещение=Истина) Экспорт
	Результат=Истина;
	Если НЕ ЗначениеЗаполнено(СтрокаРекомендации.Период) Тогда
		#Если Клиент Тогда
		СтрокаРекомендации.Период=РабочаяДата;
		#Иначе
	    СтрокаРекомендации.Период=ТекущаяДата();
		#КонецЕсли
	КонецЕсли; 
	Если СтрокаРекомендации.Количество=0 Тогда
		СтрокаРекомендации.Количество=1;
		КоличествоПриИзменении(СтрокаРекомендации);
	КонецЕсли;
	Если ЗначениеЗаполнено(СтрокаРекомендации.ЗаявкаНаРемонт) Тогда
		ЗаявкаНаРемонтПриИзменении(СтрокаРекомендации);
	КонецЕсли; 
	Если ЗначениеЗаполнено(СтрокаРекомендации.ЗаказНаряд) Тогда
		ЗаказНарядПриИзменении(СтрокаРекомендации);
	КонецЕсли; 
	Если ЗначениеЗаполнено(СтрокаРекомендации.ПричинаОтказа) Тогда
		ПричинаОтказаПриИзменении(СтрокаРекомендации);
	КонецЕсли; 

	МенеджерЗаписи=РегистрыСведений.РекомендацииПоАвтомобилю.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период=СтрокаРекомендации.ПериодЗаписи;
	МенеджерЗаписи.Автомобиль=Автомобиль;
	МенеджерЗаписи.Рекомендация=СтрокаРекомендации.РекомендацияЗаписи;
	МенеджерЗаписи.Комментарий = СтрокаРекомендации.КомментарийЗаписи;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Период=СтрокаРекомендации.Период;
	МенеджерЗаписи.Автомобиль=Автомобиль;
	МенеджерЗаписи.Рекомендация=СтрокаРекомендации.Рекомендация;
	МенеджерЗаписи.Комментарий = СтрокаРекомендации.Комментарий;
	МенеджерЗаписи.Количество=СтрокаРекомендации.Количество;
	МенеджерЗаписи.ЗаявкаНаРемонт=СтрокаРекомендации.ЗаявкаНаРемонт;
	МенеджерЗаписи.ЗаказНаряд=СтрокаРекомендации.ЗаказНаряд;
	МенеджерЗаписи.ПричинаОтказа=СтрокаРекомендации.ПричинаОтказа;
	МенеджерЗаписи.ДатаВыполнения=СтрокаРекомендации.ДатаВыполнения;
	МенеджерЗаписи.Автор=СтрокаРекомендации.Автор;
	МенеджерЗаписи.Исполнитель=СтрокаРекомендации.Исполнитель;
	МенеджерЗаписи.Выполнена=СтрокаРекомендации.Выполнена;
	МенеджерЗаписи.ДатаАктуальности=СтрокаРекомендации.ДатаАктуальности;
	Попытка
		МенеджерЗаписи.Записать();
		СтрокаРекомендации.ПериодЗаписи=СтрокаРекомендации.Период;
		СтрокаРекомендации.РекомендацияЗаписи=СтрокаРекомендации.Рекомендация;
		СтрокаРекомендации.КомментарийЗаписи=СтрокаРекомендации.Комментарий;
	Исключение
		Сообщить("Ошибка при записи рекомендации по автомобилю: "+ОписаниеОшибки());
		Оповещение=Ложь;
		Результат=Ложь;
	КонецПопытки; 
	#Если Клиент Тогда
	Если Оповещение Тогда
		Оповестить("РекомендацияЗаписана",Автомобиль,ЭтотОбъект);
	КонецЕсли; 	
	#КонецЕсли
	Возврат Результат;
КонецФункции

Процедура ПроверитьВыполнениеРекомендаций(ДокументСсылка) Экспорт
	Если ТипЗнч(ДокументСсылка)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		СтрокиРекомендаций=Рекомендации.НайтиСтроки(Новый Структура("ЗаявкаНаРемонт",ДокументСсылка));
		ИмяДокумента="ЗаявкаНаРемонт";
	ИначеЕсли ТипЗнч(ДокументСсылка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		СтрокиРекомендаций=Рекомендации.НайтиСтроки(Новый Структура("ЗаказНаряд",ДокументСсылка));
		ИмяДокумента="ЗаказНаряд";
	Иначе
		Возврат;
	КонецЕсли;
	Если СтрокиРекомендаций.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос=Новый Запрос;
	ТекстЗапроса= 
		"ВЫБРАТЬ
		|	ДокументРаботы.Работа КАК Рекомендация,
		|	СУММА(ДокументРаботы.Количество) КАК Количество
		|ИЗ
		|	Документ."+ИмяДокумента+".Работы КАК ДокументРаботы
		|ГДЕ
		|	ДокументРаботы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументРаботы.Работа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура,
		|	СУММА(ДокументТовары.Количество * ДокументТовары.Коэффициент)
		|ИЗ
		|	Документ."+ИмяДокумента+".Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура";
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	РекомендацииДокумента=Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаРекомендации Из СтрокиРекомендаций Цикл
		СтрокаРекомендацииДокумента=РекомендацииДокумента.Найти(СтрокаРекомендации.Рекомендация,"Рекомендация");
		Если СтрокаРекомендацииДокумента=Неопределено ИЛИ СтрокаРекомендацииДокумента.Количество<СтрокаРекомендации.Количество Тогда
			Если ТипЗнч(ДокументСсылка)=Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
				СтрокаРекомендации.ЗаявкаНаРемонт=Неопределено;
				ЗаявкаНаРемонтПриИзменении(СтрокаРекомендации);
				ЗаписатьСтрокуРекомендации(СтрокаРекомендации,Ложь);
			ИначеЕсли ТипЗнч(ДокументСсылка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
				СтрокаРекомендации.ЗаказНаряд=Неопределено;
				ЗаказНарядПриИзменении(СтрокаРекомендации);
				ЗаписатьСтрокуРекомендации(СтрокаРекомендации,Ложь);
			КонецЕсли;
		Иначе
			Если ТипЗнч(ДокументСсылка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
				ЗаявкаНаРемонтПриИзменении(СтрокаРекомендации);
				ЗаписатьСтрокуРекомендации(СтрокаРекомендации,Ложь);
			КонецЕсли;
			СтрокаРекомендацииДокумента.Количество=СтрокаРекомендацииДокумента.Количество-СтрокаРекомендации.Количество;
			Если СтрокаРекомендацииДокумента.Количество=0 Тогда
				РекомендацииДокумента.Удалить(СтрокаРекомендацииДокумента);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры
 
// Получить остатки номенклатуры
// Номенклатура - СправочникСсылка на номенклатуру
// ХарактеристикаНоменклатуры - СправочникСсылка на ХарактеристикаНоменклатуры
// Возвращаемое значение Число - остаток номенклатуры
Функция ПолучитьОстаткиНоменклатурыНаСкладах(Знач СкладКомпании,Номенклатура,ХарактеристикаНоменклатуры) Экспорт
	//Будем считать что остаток нулевой
	ОстатокНоменклатуры=0;
	СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",Номенклатура,ХарактеристикаНоменклатуры);
	Если Не ЗначениеЗаполнено(СкладКомпании) Тогда
		СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	СтруктураОтбора.Вставить("СкладКомпании", СкладКомпании);
	//Попробуем найти номенклатуру в кэше
	МассивСтрок=КэшОстатковНоменклатурыНаСкладе.НайтиСтроки(СтруктураОтбора);
	Если МассивСтрок.Количество()=0 Тогда
		//В кэше номенклатура не найдена - добавим остатки номенклатуры из регистра
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК Остаток
		//|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,"+?(ЗначениеЗаполнено(СкладКомпании),"СкладКомпании=&СкладКомпании И ","")+" Номенклатура = &Номенклатура И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ОстаткиТоваровКомпанииОстатки
		|";
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
		ВыборкаОстатковНоменклатурыНаСкладах=Запрос.Выполнить().Выбрать();
		Если ВыборкаОстатковНоменклатурыНаСкладах.Следующий() Тогда
			ОстатокНоменклатуры=ВыборкаОстатковНоменклатурыНаСкладах.Остаток;
		КонецЕсли; 
		НоваяСтрокаКеша=КэшОстатковНоменклатурыНаСкладе.Добавить();
		НоваяСтрокаКеша.СкладКомпании=СкладКомпании;
		НоваяСтрокаКеша.Номенклатура=Номенклатура;
		НоваяСтрокаКеша.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры;
		НоваяСтрокаКеша.Остаток = ОстатокНоменклатуры;
	Иначе
		ОстатокНоменклатуры = МассивСтрок[0].Остаток;
	КонецЕсли;

	Возврат ОстатокНоменклатуры;
КонецФункции

Процедура ПечатьРекомендаций(ВыводитьРаботы = Истина, ВыводитьЗапчасти = Истина, ВыводитьТолькоСКомментарием = Истина, СписокСостояний = Неопределено) Экспорт 
	
	Если ЭтотОбъект.Автомобиль.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПечатная = ТекущаяДата();
	
	ВалютаПечатногоДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	Макет = ПолучитьМакет("Рекомендации");
	ВыводитьКод = Ложь;//обПраво("ВыводитьКодВПечатныхФормах");
	КолонкаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах();
	ИмяРеквизитаКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах().Имя;
	
	НомерСтраницы = 1;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы");
	ФорматПредставленияГодаВыпускаАвтомобиля = орПолучитьФорматПредставленияГодаВыпускаАвтомобиля();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб = Истина;
	
	//зададим параметры макета
	ТабДокумент.ПолеСверху = 10; //поле равно высоте колонтитулов
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ОбластьУслугиРекомендацииШапка = Макет.ПолучитьОбласть("УслугиРекомендацииШапка");
	ОбластьУслугиРекомендацииСтрока= Макет.ПолучитьОбласть("УслугиРекомендацииСтрока");
	ОбластьУслугиРекомендацииПодвал= Макет.ПолучитьОбласть("УслугиРекомендацииПодвал");
	
	ОбластьДеталиРекомендацииШапка = Макет.ПолучитьОбласть("ДеталиРекомендацииШапка");
	ОбластьДеталиРекомендацииСтрока= Макет.ПолучитьОбласть("ДеталиРекомендацииСтрока");
	ОбластьДеталиРекомендацииПодвал= Макет.ПолучитьОбласть("ДеталиРекомендацииПодвал");
	
	ОбластьКомментарийРекомендацииШапка  = Макет.ПолучитьОбласть("КомментарийРекомендацииШапка");
	ОбластьКомментарийРекомендацииСтрока = Макет.ПолучитьОбласть("КомментарийРекомендацииСтрока");
	ОбластьКомментарийРекомендацииПодвал = Макет.ПолучитьОбласть("КомментарийРекомендацииПодвал");
	
	Если НЕ ВыводитьКод Тогда
			
		//Рекомендации услуг
		ВремОбластьУслугиРекомендацииШапка=ОбластьУслугиРекомендацииШапка.Область(2,4,2,4).Текст;
		ОбластьУслугиРекомендацииШапка.Область(2,3,2,7).Объединить();
		ОбластьУслугиРекомендацииШапка.Область(3,3,3,7).Объединить();
		ОбластьУслугиРекомендацииШапка.Область(2,3,2,4).Заполнение=ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьУслугиРекомендацииШапка.Область(2,3,2,3).Текст=ВремОбластьУслугиРекомендацииШапка;

		ОбластьУслугиРекомендацииСтрока.Область(1,3,1,8).Объединить();
		ОбластьУслугиРекомендацииСтрока.Область(1,3,1,7).Параметр=ВремОбластьУслугиРекомендацииШапка;
		ОбластьУслугиРекомендацииСтрока.Область(1,3,1,7).ПараметрРасшифровки="Номенклатура";

		
		Ном=3;
		Для Сч=3 По 14 Цикл
			Если обЗначениеНеЗаполнено(ОбластьУслугиРекомендацииШапка.Область(3,Сч+1,3,Сч+1).Текст) Тогда
				Продолжить;
			КонецЕсли;
			ОбластьУслугиРекомендацииШапка.Область(3,Сч+1,3,Сч+1).Текст=Ном;
			Ном=Ном+1;
		КонецЦикла;

		//Рекомендации деталей
		ВремОбластьДеталиРекомендацииШапка=ОбластьДеталиРекомендацииШапка.Область(2,4,2,4).Текст;
		ОбластьДеталиРекомендацииШапка.Область(2,3,2,7).Объединить();
		ОбластьДеталиРекомендацииШапка.Область(3,3,3,7).Объединить();
		ОбластьДеталиРекомендацииШапка.Область(2,3,2,4).Заполнение=ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьДеталиРекомендацииШапка.Область(2,3,2,3).Текст=ВремОбластьДеталиРекомендацииШапка;

		ОбластьДеталиРекомендацииСтрока.Область(1,3,1,8).Объединить();
		ОбластьДеталиРекомендацииСтрока.Область(1,3,1,7).Параметр=ВремОбластьДеталиРекомендацииШапка;
		ОбластьДеталиРекомендацииСтрока.Область(1,3,1,7).ПараметрРасшифровки="Номенклатура";

		
		Ном=3;
		Для Сч=3 По 14 Цикл
			Если обЗначениеНеЗаполнено(ОбластьДеталиРекомендацииШапка.Область(3,Сч+1,3,Сч+1).Текст) Тогда
				Продолжить;
			КонецЕсли;
			ОбластьДеталиРекомендацииШапка.Область(3,Сч+1,3,Сч+1).Текст=Ном;
			Ном=Ном+1;
		КонецЦикла;
 		
	КонецЕсли;
	//Вывод шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Организация = ПараметрыСеанса.Организация;
	ОбластьМакета.Параметры.ФирмаПолноеНаименование = спПолучитьНаименование(ПараметрыСеанса.Организация);
	ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(ПараметрыСеанса.Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(ПараметрыСеанса.Организация, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
	
	Заголовок = "Рекомендации по автомобилю от " + Формат(ДатаПечатная,"ДФ=дд.ММ.гггг"); 
	
	Если СписокСостояний <> Неопределено И СписокСостояний.Количество() = 1 Тогда
		
		Если СписокСостояний[0].Значение = Перечисления.СостоянияСобытий.Запланировано Тогда
			Заголовок = "Рекомендации по автомобилю, актуальные на " + Формат(ДатаПечатная,"ДФ=дд.ММ.гггг");
		ИначеЕсли СписокСостояний[0].Значение = Перечисления.СостоянияСобытий.Выполняется Тогда
			Заголовок = "Выполняющиеся на " + Формат(ДатаПечатная,"ДФ=дд.ММ.гггг") + " рекомендации по автомобилю";
		ИначеЕсли СписокСостояний[0].Значение = Перечисления.СостоянияСобытий.Завершено Тогда
			Заголовок = "Выполненные рекомендации по автомобилю";
		ИначеЕсли СписокСостояний[0].Значение = Перечисления.СостоянияСобытий.Отменено Тогда
			Заголовок = "Отклоненные рекомендации по автомобилю";
		КонецЕсли;
		
	КонецЕсли; 
		
	ОбластьМакета.Параметры.Заголовок = Заголовок;
	Владелец= Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ЭтотОбъект.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин,ДатаПечатная);
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		ОбластьМакета.Параметры.Владелец = Владелец;
		ОбластьМакета.Параметры.ВладелецПолноеНаименование = спПолучитьНаименование(Владелец);
		ОбластьМакета.Параметры.ВладелецАдресПочтовый = киПолучитьПредставлениеКИ(Владелец, Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
		ОбластьМакета.Параметры.ВладелецТелефоны = киПолучитьПредставлениеКИ(Владелец, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
	Иначе
		ОбластьМакета.Параметры.ВладелецПолноеНаименование = "Нет данных";
	КонецЕсли;
	
	ОбластьМакета.Параметры.Автомобиль = ЭтотОбъект.Автомобиль;
	ОбластьМакета.Параметры.АвтомобильКод = ЭтотОбъект.Автомобиль.VIN;
	ОбластьМакета.Параметры.АвтомобильМодель = ЭтотОбъект.Автомобиль.Модель;
	ОбластьМакета.Параметры.АвтомобильГодВыпуска = Формат(ЭтотОбъект.Автомобиль.ГодВыпуска,ФорматПредставленияГодаВыпускаАвтомобиля);
	
	ОбластьМакета.Параметры.АвтомобильГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ЭтотОбъект.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,ДатаПечатная);
	ОбластьМакета.Параметры.АвтомобильПробег = Формат(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ЭтотОбъект.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,ДатаПечатная),"ЧЦ=10");
		
	ТабДокумент.Вывести(ОбластьМакета);
	
	СуммаЗапчастей = 0;
	СуммаРабот = 0;
	
	ЕстьРаботы                    = Ложь;
	ЕстьДетали                    = Ложь;
	ЕстьТолькоСТекстовымОписанием = Ложь;
	
	Для Каждого СтрокаРекомендации Из Рекомендации Цикл
		
		Если СписокСостояний <> Неопределено 
			И СписокСостояний.НайтиПоЗначению(СтрокаРекомендации.Состояние) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(СтрокаРекомендации.Рекомендация) = Тип("СправочникСсылка.Автоработы") И ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) И НЕ ЕстьРаботы Тогда
			ЕстьРаботы = Истина;
		ИначеЕсли ТипЗнч(СтрокаРекомендации.Рекомендация) = Тип("СправочникСсылка.Номенклатура") И ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
			ЕстьДетали = Истина;
		ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
			ЕстьТолькоСТекстовымОписанием = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВыводитьРаботы И ЕстьРаботы Тогда
		
		КоличествоРабот = 0;
		Сч = 0;
		
		ТекстЗаголовка = "Рекомендованные работы";
		
		ТекОбластьШапка = ОбластьУслугиРекомендацииШапка;
		ТекОбластьШапка.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		Если ВыводитьКод Тогда
			ТекОбластьШапка.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
		КонецЕсли;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбластьШапка, , , НомерСтраницы);
		
		МассивОбластей = Новый Массив();
		МассивОбластей.Добавить(ТекОбластьШапка);
		
		Для Каждого СтрокаРекомендации ИЗ Рекомендации  Цикл
			
			Если ТипЗнч(СтрокаРекомендации.Рекомендация) = Тип("СправочникСсылка.Автоработы") И ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
				
				Если СписокСостояний <> Неопределено 
					И СписокСостояний.НайтиПоЗначению(СтрокаРекомендации.Состояние) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Сч = Сч + 1;
				
				ТекОбласть = ОбластьУслугиРекомендацииСтрока;
				ЗаполнитьЗначенияСвойств(ТекОбласть.Параметры,СтрокаРекомендации);
				Если ВыводитьКод Тогда
					ТекОбласть.Параметры.Код = дкПолучитьЗначениеКолонкиКода(СтрокаРекомендации.Рекомендация,ИмяРеквизитаКода); 
				КонецЕсли;
				
				ТекОбласть.Параметры.Наименование = спПолучитьНаименование(СтрокаРекомендации.Рекомендация) + ?(СтрокаРекомендации.Состояние = Перечисления.СостоянияСобытий.Отменено, " ("+спПолучитьНаименование(СтрокаРекомендации.ПричинаОтказа)+?(ЗначениеЗаполнено(СтрокаРекомендации.ДатаВыполнения), " "+Формат(СтрокаРекомендации.ДатаВыполнения, "ДФ=дд.ММ.гггг"), "")+")", "");
				ЦенаРаботы=орПолучитьЦенуАвтоработы(ТипЦенРабот,СтрокаРекомендации.Рекомендация,Автомобиль.Модель,ДоговорВзаиморасчетов,Цех,ВидРемонта,ДатаПечатная,ВалютаПечатногоДокумента,1);
				ТекОбласть.Параметры.Нормочас=ЦенаРаботы.Нормочас;
				
				ТекОбласть.Параметры.НомСтр = Сч;
				
				НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбласть,ТекОбластьШапка, , НомерСтраницы,,,МассивОбластей); 
				
				СуммаРабот = СуммаРабот + СтрокаРекомендации.Сумма;
				КоличествоРабот = КоличествоРабот + СтрокаРекомендации.Количество;
				
			КонецЕсли;
			
		КонецЦикла;
		                                        
		ТекОбластьПодвал = ОбластьУслугиРекомендацииПодвал;
		ТекОбластьПодвал.Параметры.Количество = КоличествоРабот;
		ТекОбластьПодвал.Параметры.Сумма = Формат(СуммаРабот,ФорматВыводаСуммы);
		ТекОбластьПодвал.Параметры.ЧислоПрописью = обЧислоПрописью(СуммаРабот,ВалютаПечатногоДокумента);
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбластьПодвал, , , НомерСтраницы);
		
	КонецЕсли;
	
	Если ВыводитьЗапчасти И ЕстьДетали Тогда
		
		КоличествоЗапчастей = 0;
		Сч = 0;
		
		ТекОбластьШапка = ОбластьДеталиРекомендацииШапка;
		ТекОбластьШапка.Параметры.ТекстЗаголовка = "Рекомендованные детали";
		Если ВыводитьКод Тогда
			ТекОбластьШапка.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
		КонецЕсли;
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбластьШапка, , , НомерСтраницы);
		
		МассивОбластей = Новый Массив();
		МассивОбластей.Добавить(ТекОбластьШапка);
		
		Для Каждого СтрокаРекомендации ИЗ Рекомендации  Цикл
			
			Если ТипЗнч(СтрокаРекомендации.Рекомендация) = Тип("СправочникСсылка.Номенклатура") И ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
				
				Если СписокСостояний <> Неопределено 
					И СписокСостояний.НайтиПоЗначению(СтрокаРекомендации.Состояние) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Сч = Сч + 1;
				
				ТекОбласть = ОбластьДеталиРекомендацииСтрока;
				ЗаполнитьЗначенияСвойств(ТекОбласть.Параметры,СтрокаРекомендации);
				Если ВыводитьКод Тогда
					ТекОбласть.Параметры.Код = дкПолучитьЗначениеКолонкиКода(СтрокаРекомендации.Рекомендация,ИмяРеквизитаКода); 
				КонецЕсли;
				ТекОбласть.Параметры.Наименование = спПолучитьНаименование(СтрокаРекомендации.Рекомендация) + ?(СтрокаРекомендации.Состояние = Перечисления.СостоянияСобытий.Отменено,  " ("+спПолучитьНаименование(СтрокаРекомендации.ПричинаОтказа)+?(ЗначениеЗаполнено(СтрокаРекомендации.ДатаВыполнения), " "+Формат(СтрокаРекомендации.ДатаВыполнения, "ДФ=дд.ММ.гггг"), "")+")", "");
				
				ТекОбласть.Параметры.НомСтр = Сч;
				
				НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбласть,ТекОбластьШапка, , НомерСтраницы,,,МассивОбластей); 

				СуммаЗапчастей = СуммаЗапчастей + СтрокаРекомендации.Сумма;
				КоличествоЗапчастей = КоличествоЗапчастей + СтрокаРекомендации.Количество;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ТекОбластьПодвал = ОбластьДеталиРекомендацииПодвал;
		ТекОбластьПодвал.Параметры.Количество = КоличествоЗапчастей;
		ТекОбластьПодвал.Параметры.Сумма = Формат(СуммаЗапчастей,ФорматВыводаСуммы);
 		ТекОбластьПодвал.Параметры.ЧислоПрописью = обЧислоПрописью(СуммаЗапчастей,ВалютаПечатногоДокумента);
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбластьПодвал, , , НомерСтраницы);
		
	КонецЕсли;
	
	Если ВыводитьТолькоСКомментарием И ЕстьТолькоСТекстовымОписанием Тогда
		
		КоличествоРекомендаций = 0;
		Сч = 0;
		
		ТекОбластьШапка = ОбластьКомментарийРекомендацииШапка;
		ТекОбластьШапка.Параметры.ТекстЗаголовка = "Рекомендации с текстовым описанием";
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбластьШапка, , , НомерСтраницы);
		
		МассивОбластей = Новый Массив();
		МассивОбластей.Добавить(ТекОбластьШапка);
		
		Для Каждого СтрокаРекомендации ИЗ Рекомендации  Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
				
				Если СписокСостояний <> Неопределено 
					И СписокСостояний.НайтиПоЗначению(СтрокаРекомендации.Состояние) = Неопределено И ЗначениеЗаполнено(СтрокаРекомендации.Рекомендация) Тогда
					Продолжить;
				КонецЕсли;
				
				Сч = Сч + 1;
				
				ТекОбласть = ОбластьКомментарийРекомендацииСтрока;
				ЗаполнитьЗначенияСвойств(ТекОбласть.Параметры,СтрокаРекомендации);
				ТекОбласть.Параметры.Наименование = СтрокаРекомендации.Комментарий  + ?(СтрокаРекомендации.Состояние = Перечисления.СостоянияСобытий.Отменено,  " ("+спПолучитьНаименование(СтрокаРекомендации.ПричинаОтказа)+?(ЗначениеЗаполнено(СтрокаРекомендации.ДатаВыполнения), " "+Формат(СтрокаРекомендации.ДатаВыполнения, "ДФ=дд.ММ.гггг"), "")+")", "");;
				
				ТекОбласть.Параметры.НомСтр = Сч;
				
				НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбласть,ТекОбластьШапка, , НомерСтраницы,,,МассивОбластей); 

				КоличествоРекомендаций = КоличествоРекомендаций + СтрокаРекомендации.Количество;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ТекОбластьПодвал = ОбластьКомментарийРекомендацииПодвал;
		ТекОбластьПодвал.Параметры.Количество = КоличествоРекомендаций;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТекОбластьПодвал, , , НомерСтраницы);
		
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалНачало");
		
	ОбластьМакета.Параметры.Сумма = Формат(СуммаРабот + СуммаЗапчастей, ФорматВыводаСуммы);
	ОбластьМакета.Параметры.ЧислоПрописью = обЧислоПрописью(СуммаРабот + СуммаЗапчастей, ВалютаПечатногоДокумента);
		
	Если ЗначениеЗаполнено(ПараметрыСеанса.Пользователь.Сотрудник) Тогда 
		ОбластьМакета.Параметры.МастерПолноеНаименование = "/" + ПараметрыСеанса.Пользователь.Сотрудник.Наименование + "/";
	Иначе
		ОбластьМакета.Параметры.МастерПолноеНаименование = "/________________________/";
	КонецЕсли;
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы);	
		
	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.Защита			= обПраво("ЗащитаПечатныхФорм");
	ТабличныйДокумент.ТолькоПросмотр	= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра");
	// Выведем печатную форму	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ОбъектПредставление			= "Рекомендации по автомобилю " + ЭтотОбъект.Автомобиль;
	ФормаПечати.Открыть();
	
КонецПроцедуры

МассивТиповСкладыКомпании=Новый Массив(1); МассивТиповСкладыКомпании[0]=Тип("СправочникСсылка.СкладыКомпании");
ОписаниеТиповСкладыКомпании=Новый ОписаниеТипов(МассивТиповСкладыКомпании);

МассивТиповНоменклатура=Новый Массив(1); МассивТиповНоменклатура[0]=Тип("СправочникСсылка.Номенклатура");
ОписаниеТиповНоменклатура=Новый ОписаниеТипов(МассивТиповНоменклатура);

МассивТиповХарактеристикиНоменклатуры=Новый Массив(1); МассивТиповХарактеристикиНоменклатуры[0]=Тип("СправочникСсылка.ХарактеристикиНоменклатуры");
ОписаниеТиповХарактеристикиНоменклатуры=Новый ОписаниеТипов(МассивТиповХарактеристикиНоменклатуры);

МассивТиповЧисло=Новый Массив(1); МассивТиповЧисло[0]=Тип("Число");
ОписаниеТиповЧисло=Новый ОписаниеТипов(МассивТиповЧисло);

КэшОстатковНоменклатурыНаСкладе=Новый ТаблицаЗначений;
КэшОстатковНоменклатурыНаСкладе.Колонки.Добавить("СкладКомпании",ОписаниеТиповСкладыКомпании);
КэшОстатковНоменклатурыНаСкладе.Колонки.Добавить("Номенклатура",ОписаниеТиповНоменклатура);
КэшОстатковНоменклатурыНаСкладе.Колонки.Добавить("ХарактеристикаНоменклатуры",ОписаниеТиповХарактеристикиНоменклатуры);
КэшОстатковНоменклатурыНаСкладе.Колонки.Добавить("Остаток",ОписаниеТиповЧисло);

////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ ОБРАБОТКИ УПРАВЛЕНИЯ ТОРГОВЫМ ОБОРУДОВАНИЕМ
// 
// Данный объект создается всегда на клиентском рабочем месте и живет в памяти системы.
// При необходимости всегда можно обратиться к объекту через переменную "глТорговоеОборудование" в модуле приложения
// Каждая форма обработки представляет собой экземпляр драйвера управления устройством
// Общий запуск торгового оборудования осуществляется из обработки "СтартСистемы"

////////////////////////////////////////////////////////////////////////////////
// БЛОК ИНИЦИАЛИЗАЦИИ ПЕРЕМЕННЫХ МОДУЛЯ

#Если Клиент Тогда                              // Блок существует исключительно на стороне клиента
Перем ТаблицаЭкземпляровОборудования   Экспорт; // Таблица значений со списком зарегистрированных в системе устройств 
                                                // и их моделей
Перем ИменаЗагруженныхВнешнихБиблиотек Экспорт; // Строка с именами загруженных и подключенных внешних библиотек в 
                                                // виде в виде ИмяФайла.dll
                                                // разделены запятыми, наличие в данной строке имени библиотеки 
                                                // означает что для нее уже вызывался метод 
                                                // "ЗагрузитьВнешнююКомпоненту" (или "ПодключитьВнешнююКомпоненту 
                                                // соответственно")
Перем ЗагруженныеОбработчикиУстройств  Экспорт; // Структура с загруженными формами-обработчиками устройств, в ключе
                                                // содержится GUID оборудования приведенный к корректному 
                                                // идентификатору, в значении - сама форма
Перем ЗагруженныеДрайверыУстройств     Экспорт; // Структура с загруженными COM-объектами драйверов устройств, в ключе
                                                // содержится имя COM-объекта, приведенное к корректному 
                                                // идентификатору, в значении - сам COM-объект
Перем GUIDНашейТочкиДоступа            Экспорт; // Строка с идентификатором текущей точки доступа (этого самого 
                                                // запущенного типового решения)
Перем ИмяНашейКомпоненты               Экспорт; // Ускоряющая переменная
Перем ИмяНашегоКомпьютера              Экспорт; // Ускоряющая переменная
Перем ТипСОМОбъект;                             // Ускоряющая переменная 
Перем ОбработкаСофтФон                 Экспорт; // РАРУС обработка управления звонками, проф
Перем КешНомеровОтправителей           Экспорт; // Структура содержащая соответсвие Оборудование - Номер по умолчанию 
                                                // на нем (первый в списке доступных номеров)
Перем Рарус_Компонента                 Экспорт; // Локальная ссылка на внешнюю компоненту типового решения
Перем БылиОшибкиПодключениSMS          Экспорт; // Булево, флаг того что были ошибки при подключении оборудования, 
                                                // если истина о при работе с оборудованием не выводим сообщения
//////////////////////////////////////////////////////////////////////////////
// СЕРВИСНЫЕ И СЛУЖЕБНЫЕ ФУНКЦИИ 



// Преобразует переданный GUID в правильный текстовый идентификатор
// Параметры:
//  СтрокаGUID - строку с глобальным уникальным идентификатором
// Возвращает:
//  Строку - правильный идентификатор
Функция ИдGUID(СтрокаGUID)
	Результат = Сред(СтрокаGUID, 2, СтрДлина(СтрокаGUID) - 2); 	// Вырезали из скобок
	Результат = СтрЗаменить(Результат, "-", ""); 				// избавились от разделителей
	Возврат "GUID" + Результат;		// добавили текстовый префикс, чтобы начиналось не с цифры
КонецФункции

// Проверят оборудование на корректность и его наличие (регистрацию) в системе оборудования
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
//				ДопустимыеСтатусы - массив с допустимыми статусами устройства, если устройство
//				будет иметь статус который присутствует в переданном массиве то функция вернет
//				положительный результат вместо ошибки
// Возвратные параметры:
//				Оборудование - СправочникСсылка.Оборудование (даже если на входе была строка-GUID)
//				GUID_Оборудования - строка, уникальный идентификатор экземпляра оборудования
//				ТаймАут - число, период ожидания ответа от оборудования (в сек.)
//				НомОшибки - число, код возникшей ошибки при проверке оборудования
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает: 	
//				Истина если с оборудованием все ОК и заполняет возвратные параметры
//			  	Ложь и текстовое описание ошибки в противном случае через "ОписаниеОшибки"
//
Функция ОборудованиеКорректно(	Оборудование = "",
								GUID_Оборудования = "",
								ТаймАут = 90,
								НомОшибки = 0,
								ОписаниеОшибки = "",
								ТекущийСтатус = НЕОПРЕДЕЛЕНО,
								ДопустимыеСтатусы = НЕОПРЕДЕЛЕНО,
								СтрокаТаблицыУстройств = НЕОПРЕДЕЛЕНО)
								
	// Инициализация рабочих переменных
	НомОшибки = 22010; 
	ОписаниеОшибки = "Ошибка при проверке корректности оборудования";
	СтрокаТаблицыУстройств = НЕОПРЕДЕЛЕНО; // Рабочая переменная
	Попытка // Все проверки на всякий случай будем проводить в защищенном блоке
		// Первая проверка на режим работы с оборудованием
		Если РаботаСОборудованиемЗапрещена() Тогда
			НомОшибки = 22012;
			ОписаниеОшибки = "Использование оборудования ЗАПРЕЩЕНО";
			Возврат Ложь;
		КонецЕсли;
		// Проверим корректность оборудования (или его идентификатора), определим GUID устройства
		Если НЕ ЗначениеЗаполнено(Оборудование) И ПустаяСтрока(GUID_Оборудования) Тогда
			НомОшибки = 22011;  // Нечего и проверять
			ОписаниеОшибки = "Некорректное задание оборудования во входных параметрах";
			Возврат Ложь;
		ИначеЕсли ТипЗнч(Оборудование)=Тип("СправочникСсылка.Оборудование") Тогда
			// Элемент справочника оборудования, сначала поищем его в системе оборудования (там быстрее)
			СтрокаТаблицыУстройств=ТаблицаЭкземпляровОборудования.Найти(Оборудование,"ОборудованиеСсылка");
		ИначеЕсли (ТипЗнч(Оборудование)=Тип("Строка")) И (НЕ ПустаяСтрока(Оборудование)) Тогда
			// Передали GUID оборудования - ищем по идентификатору
			СтрокаТаблицыУстройств=ТаблицаЭкземпляровОборудования.Найти(Оборудование,"GUID");
		ИначеЕсли (ТипЗнч(GUID_Оборудования)=Тип("Строка")) И (НЕ ПустаяСтрока(GUID_Оборудования)) Тогда
			// Использован второй параметр (обычно возвратный)
			СтрокаТаблицыУстройств=ТаблицаЭкземпляровОборудования.Найти(GUID_Оборудования,"GUID");
		КонецЕсли;
			
		Если СтрокаТаблицыУстройств=Неопределено Тогда
			// В таблице всех устройств нет нашего? Уже успело измениться что-то? Попробуем через справочник узнать
			Если ТипЗнч(Оборудование)=Тип("СправочникСсылка.Оборудование") Тогда
				GUID_Оборудования	= СокрЛП(Оборудование.ИдентификаторОборудования);
				Оборудование		= Оборудование.Ссылка;
				ТаймАут				= Оборудование.Таймаут;
			ИначеЕсли (ТипЗнч(Оборудование)=Тип("Строка")) И (НЕ ПустаяСтрока(Оборудование)) Тогда
				GUID_Оборудования=СокрЛП(Оборудование);
				Оборудование=Справочники.Оборудование.НайтиПоРеквизиту("ИдентификаторОборудования",GUID_Оборудования);
				Если НЕ Оборудование.Пустая() Тогда
					ТаймАут			= Оборудование.Таймаут;
				КонецЕсли;
			ИначеЕсли (ТипЗнч(GUID_Оборудования)=Тип("Строка")) И (НЕ ПустаяСтрока(GUID_Оборудования)) Тогда
				// Использован второй параметр (обычно возвратный)
				Оборудование=Справочники.Оборудование.НайтиПоРеквизиту("ИдентификаторОборудования",GUID_Оборудования);
				Если НЕ Оборудование.Пустая() Тогда
					ТаймАут			= Оборудование.Таймаут;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если НЕ ЗначениеЗаполнено(Оборудование) ИЛИ (ТипЗнч(Оборудование)<>Тип("СправочникСсылка.Оборудование")) Тогда
				Оборудование		= СтрокаТаблицыУстройств.ОборудованиеСсылка;
			КонецЕсли;
			Если ПустаяСтрока(GUID_Оборудования) Тогда
				GUID_Оборудования	= СтрокаТаблицыУстройств.GUID;
			КонецЕсли;
		КонецЕсли;
		
		// GUID должен быть в формате "{xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx}"
		Если ПустаяСтрока(GUID_Оборудования) ИЛИ (СтрДлина(GUID_Оборудования)<>38) ИЛИ (СтрДлина(GUID_Оборудования)<>38)
			ИЛИ	(Лев(GUID_Оборудования,1)<>"{") ИЛИ (Прав(GUID_Оборудования,1)<>"}") Тогда
			// В строке нет признаков GUIDa
			НомОшибки = 22020;
			ОписаниеОшибки = "Оборудование не настроено или указан некорректный GUID-идентификатор";
			Возврат Ложь;
		КонецЕсли;
		
		// Запросим текущее состояние устройства в схеме СЗиУО
		ТекущийСтатус=СтатусОборудования(GUID_Оборудования);
		Если (ДопустимыеСтатусы<>НЕОПРЕДЕЛЕНО) И (ДопустимыеСтатусы.Найти(ТекущийСтатус)<>НЕОПРЕДЕЛЕНО) Тогда
			НомОшибки = 0;
			ОписаниеОшибки = "ОК";
		ИначеЕсли 	(ТекущийСтатус = НЕОПРЕДЕЛЕНО)
				ИЛИ	(ТекущийСтатус = Перечисления.СтатусыОборудования.НеИспользуется)
				ИЛИ	(ТекущийСтатус = Перечисления.СтатусыОборудования.НетДрайвера)
				ИЛИ (ТекущийСтатус = Перечисления.СтатусыОборудования.Отсутствует)
				ИЛИ (ТекущийСтатус = Перечисления.СтатусыОборудования.Удалено) Тогда
			НомОшибки = 22022;
			ОписаниеОшибки = "Оборудование имеет статус <" + ТекущийСтатус +
			">. Возможно необходимо обновить схему устройств, для чего рекомендуется полностью перезапустить программу";
		ИначеЕсли (ТекущийСтатус = Перечисления.СтатусыОборудования.Недоступно) Тогда
			НомОшибки = 22023;
			ОписаниеОшибки = "Оборудование имеет статус <"+ТекущийСтатус+
			">. Возможно не включен компьютер или на нем не запущено типовое решение, под управлением которого было настроено" +
			 " данное оборудование";
		ИначеЕсли (ТекущийСтатус = Перечисления.СтатусыОборудования.Запрещено) Тогда
			НомОшибки = 22012;
			ОписаниеОшибки = "Текущему пользователю системы запрещено любое использование оборудования." + 
			" Проверьте установленный ""Режим использования оборудования"" в карточке пользователя";
		ИначеЕсли (ТекущийСтатус = Перечисления.СтатусыОборудования.Занято) Тогда
			НомОшибки = 22013;
			ОписаниеОшибки = "Оборудование имеет статус <"+ТекущийСтатус+">. В данном состоянии все обращения к оборудованию " +
			"возможны только из сессии пользователя, который его заблокировал для монопольного использования";
		Иначе
			НомОшибки = 0;
			ОписаниеОшибки = "ОК";
		КонецЕсли;
		
		Если (ТипЗнч(ТаймАут) <> Тип("Число")) ИЛИ (ТаймАут < 5) Тогда
			ТаймАут = 90; // Таймаут ожидания ответа оборудования по умолчанию
		КонецЕсли;
		
	Исключение
		// Возникла неизвестная ошибка в блоке проверки
		ОписаниеОшибки = "Ошибка при проверке оборудования <"+СокрЛП(Оборудование)+"> : "+ОписаниеОшибки();
		НомОшибки = 22010;
	КонецПопытки;
	
	// Вернем результат проверки	
	Возврат (НомОшибки=0);
КонецФункции // ОборудованиеКорректно

// Определяет текущее состояние конкретного экземпляра оборудования
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
// Возвратные параметры:
//				СтрОписание - строка, возвратный параметр с расширенным описанием статуса
// Возвращает: 	Соответствующее значение из перечисления "СтатусыОборудования"
//				либо <Неопределено> и текстовое описание ошибки через "ОписаниеОшибки"
//
Функция СтатусОборудования(Знач Оборудование, СтрОписание = "") Экспорт
	// Инициализация рабочих переменных	
	ОписаниеОшибки = "";
	СтрОписание = "";
	Статус = Неопределено;
	GUID_Оборудования = "";
	// Проверим входные параметры, определим GUID оборудования
	Если РаботаСОборудованиемЗапрещена() Тогда
		Статус=Перечисления.СтатусыОборудования.Запрещено; // Полный запрет на любое использование оборудования
		СтрОписание="В текущей сессии пользователя запрещено любое использование оборудования";
	ИначеЕсли НЕ ЗначениеЗаполнено(Оборудование) Тогда
		// Некого проверять - ничего не делаем, при стандартном выходе вернем Неопределено (то что надо!)
	ИначеЕсли ТипЗнч(Оборудование)=Тип("Строка") Тогда
		// Будем считать нам передали GUID-идентификатор оборудования
		GUID_Оборудования=СокрЛП(Оборудование); // Проверка по GUID'у будет ниже по коду
	ИначеЕсли ТипЗнч(Оборудование)=Тип("СправочникСсылка.Оборудование") И (НЕ Оборудование.Пустая()) Тогда
		GUID_Оборудования=СокрЛП(Оборудование.ИдентификаторОборудования);  // возьмем GUID из справочника
	КонецЕсли;
	// Если у нас есть GUID оборудования, то запросим систему оборудования
	Если ПустаяСтрока(GUID_Оборудования) Тогда
		СтрОписание="Ошибка: не удалось определить идентификатор оборудования";
	Иначе
		// Отправим запрос в систему управления оборудованием
		КодСостояния=10; // Заранее установим код ошибочного состояния, а если все получится будем иметь правильный
		Попытка
			КодСостояния=Число(Рарус_Компонента.ПолучитьСостояниеУстройства(GUID_Оборудования,СтрОписание));
		Исключение
			ОписаниеОшибки=ОписаниеОшибки();
			Попытка 
				ОписаниеОшибки=Рарус_Компонента.ОписаниеОшибки;
			Исключение
			КонецПопытки;
			ОписаниеОшибки="Ошибка получения статуса оборудования <"+GUID_Оборудования+"> : "+ОписаниеОшибки;
			// Сообщим в журнал регистрации
			ЗаписьЖурналаРегистрации("Оборудование.Статус",УровеньЖурналаРегистрации.Ошибка,ЭтотОбъект.Метаданные(),
			                         Оборудование,ОписаниеОшибки);
		КонецПопытки;
		
		Попытка
			Статус=Перечисления.СтатусыОборудования.Получить(КодСостояния);
		Исключение
			ОписаниеОшибки="Ошибка: получен неизвестный код состояния - "+КодСостояния;
		КонецПопытки;
		
	КонецЕсли;
	
	Если ПустаяСтрока(СтрОписание) Тогда
		СтрОписание = "Не удалось определить текущий статус оборудования: <"+Строка(Оборудование)+"> " + ОписаниеОшибки;
	КонецЕсли;
	
	// Обновим кэш значения статуса в сводной таблице оборудования
	СтрокаТаблицы=ТаблицаЭкземпляровОборудования.Найти(GUID_Оборудования,"GUID");
	Если СтрокаТаблицы<>Неопределено Тогда
		СтрокаТаблицы.СтатусОборудования=Статус;
		СтрокаТаблицы.ВремяПроверкиСтатуса=ТекущаяДата();
		СтрокаТаблицы.ОписаниеСтатуса=СтрОписание;
	КонецЕсли;
	
	// Вернем полученный статус
	Возврат Статус;
КонецФункции // СтатусОборудования

// Получает таблицу со свойствами устройства из системы управления оборудованием
// Параметры:
//  Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
// Возвратные параметры:
//  ТаблицаСвойств - таблица значений с колонками "Параметр" и "Значение" полученное из СЗиУО значение заданного
//  свойства оборудования
//  ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//  Истина	- как признак успешного выполнения операции
//  Ложь	- если операция не удалась (и заполняет текст ошибки)
Функция ПолучитьТаблицуСвойств(Знач Оборудование, ТаблицаСвойств, ОписаниеОшибки="") Экспорт
	
	// Инициализация рабочих переменных	
	Результат 		= Ложь;
	НомОшибки 		= 22010;
	ОписаниеОшибки 	= "Некорректные входные параметры при вызове функции ""ПолучитьТаблицуПараметровУстройства""";
	ТекущийСтатус 	= НЕОПРЕДЕЛЕНО;
	ЗначениеСвойства = НЕОПРЕДЕЛЕНО;
	СтрокаТаблицыУстройств  = НЕОПРЕДЕЛЕНО;
	ТаймАут 		= 90;
	GUID_Оборудования = "";
	// Проверим переданное оборудование (мало ли что там нам подсунули во входных параметрах)
	Если НЕ ОборудованиеКорректно(	Оборудование,
									GUID_Оборудования,
									ТаймАут,
									НомОшибки,
									ОписаниеОшибки,
									ТекущийСтатус,
									,
									СтрокаТаблицыУстройств) Тогда
		// Что-то с устройством не так. Описание ошибки см. "ОписаниеОшибки"
		ЗаписьЖурналаРегистрации(	"Оборудование.ПолучитьТаблицуСвойств",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									Оборудование,
									ОписаниеОшибки + " Состояние: " + ТекущийСтатус);
		ОписаниеОшибки = "Невозможно получить список параметров оборудования из-за возникшей ошибки" +
						Символы.ПС + ОписаниеОшибки;
		Возврат Ложь;
	КонецЕсли;
	
	Если СтрокаТаблицыУстройств <> НЕОПРЕДЕЛЕНО Тогда
		Если  (СтрокаТаблицыУстройств.ТаблицаСвойств <> НЕОПРЕДЕЛЕНО) 
			И (ТипЗнч(СтрокаТаблицыУстройств.ТаблицаСвойств) = Тип("ТаблицаЗначений")) 
			И (СтрокаТаблицыУстройств.ТаблицаСвойств.Количество() > 0)
			Тогда
			// Нам повезло, готовый ответ уже закеширован, возьмем его оттуда и отдадим наверх
			ТаблицаСвойств = СтрокаТаблицыУстройств.ТаблицаСвойств.Скопировать();
			ОписаниеОшибки = "";
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если (ТекущийСтатус = НЕОПРЕДЕЛЕНО) 
	 ИЛИ НЕ(	(ТекущийСтатус = Перечисления.СтатусыОборудования.Включено) 
	 		ИЛИ (ТекущийСтатус = Перечисления.СтатусыОборудования.Готово)
			) Тогда
		// Нужно сначала попытаться оборудование включить (иначе свойства запросить все равно не получится)
		Если ВключитьОборудование(GUID_Оборудования, ОписаниеОшибки) <> 0 Тогда
			// Не включилось описание ошибки установлено возвращаем его "наверх" тому кто вызвал
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
		
	
	Параметры="";
	Наименование="";
	// Отправим запрос в систему оборудования
	Попытка 
		НомОшибки = Рарус_Компонента.ПолучитьСвойстваОборудования(GUID_Оборудования, Параметры, Наименование);
	Исключение
		ОписаниеОшибки=ОписаниеОшибки();
		ОписаниеОшибки=ПолучитьОписаниеОшибкиКомпоненты(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации("Оборудование.ПолучитьТаблицуСвойств",УровеньЖурналаРегистрации.Ошибка,Неопределено,
		                         Оборудование,ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
		Возврат Ложь;
	КонецПопытки;
	// Проверим результат выполнения запроса к СЗиУО
	Если ТипЗнч(НомОшибки) <> Тип("Число") Тогда 
		КодОшибки = 22051;
		ОписаниеОшибки = "Компонента вернула не числовой код результата операции. " + ПолучитьОписаниеОшибкиКомпоненты();
		ЗаписьЖурналаРегистрации(	"Оборудование.ПолучитьТаблицуСвойств",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									Оборудование,
									ОписаниеОшибки + " Состояние: " + ТекущийСтатус);
		Возврат Ложь;
	ИначеЕсли НомОшибки <> 0 Тогда
		ОписаниеОшибки=ПолучитьОписаниеОшибкиКомпоненты();
		ЗаписьЖурналаРегистрации(	"Оборудование.ПолучитьТаблицуСвойств",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									Оборудование,
									ОписаниеОшибки + " Состояние: " + ТекущийСтатус);
		Возврат Ложь;
	ИначеЕсли ТипЗнч(Параметры) <> ТипСОМОбъект Тогда
		ОписаниеОшибки="Неверный тип возвратных параметров из системы управления оборудованием";
		ЗаписьЖурналаРегистрации(	"Оборудование.ПолучитьТаблицуСвойств",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									Оборудование,
									ОписаниеОшибки + " Состояние: " + ТекущийСтатус);
		Возврат Ложь;
	КонецЕсли;
	// Создадим таблицу куда перенесем результаты запроса СЗиУО
	МассивСтрока = Новый Массив(1);
	МассивСтрока[0] = Тип("Строка");
	ОписаниеТиповСтрока = Новый ОписаниеТипов(МассивСтрока);
	КвалификаторФлаг01 = Новый КвалификаторыЧисла(1,0,ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаФлаг01 = Новый ОписаниеТипов("Число",КвалификаторФлаг01);
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("Свойство",ОписаниеТиповСтрока);
	ТаблицаСвойств.Колонки.Добавить("Представление",ОписаниеТиповСтрока);
	ТаблицаСвойств.Колонки.Добавить("ТипЗначения",ОписаниеТиповСтрока);
	ТаблицаСвойств.Колонки.Добавить("Значение");
	ТаблицаСвойств.Колонки.Добавить("Описание",ОписаниеТиповСтрока);
	ТаблицаСвойств.Колонки.Добавить("ФлагТолькоЧтение",ОписаниеТипаФлаг01);
	ТаблицаСвойств.Колонки.Добавить("ЗначениеПоУмолчанию");
	// Попытаемся разобрать ответ СЗиУО
	ЧислоОшибок = 0;
	Попытка
		ОписаниеОшибки = "Систему управления оборудованием вернула пустой набор данных";
		КолвоСвойств = 0;
		КолвоПолей = 0; 
		Параметры.GetBounds(КолвоСвойств, КолвоПолей);
		// Заполним таблицу свойствами устройства
		Для Сч=0 По КолвоСвойств-1 Цикл
			Попытка
				Свойство		= ВРЕГ(СокрЛП(Параметры.GetValue(Сч,0)));
				ТипСтрокой		= СокрЛП(Параметры.GetValue(Сч,3));
				ОписаниеТипа	= Новый ОписаниеТипов(ТипСтрокой);
				ВременноеЗнч	= Параметры.GetValue(Сч,4);
				ЗначСвойства	= ОписаниеТипа.ПривестиЗначение(ВременноеЗнч);
				// Перенесем в таблицу значений
				НовСтр=ТаблицаСвойств.Добавить();
				НовСтр.ТипЗначения	= ТипСтрокой;
				НовСтр.Свойство		= Свойство;
				НовСтр.Значение		= ЗначСвойства;
				// Получим дополнительные свойства 
				Наименование	= СокрЛП(Параметры.GetValue(Сч,1));
				Описание		= СокрЛП(Параметры.GetValue(Сч,2));
				// Перенесем в таблицу значений
				НовСтр.Представление= Наименование;
                НовСтр.Описание		= Описание;
				// Необязательные параметры			
				ФлагТолькоЧтение= Число(Параметры.GetValue(Сч,5));
				НовСтр.ФлагТолькоЧтение	= ФлагТолькоЧтение;
				ВременноеЗнч	= Параметры.GetValue(Сч,6);
				ЗначПоУмолчанию	= ОписаниеТипа.ПривестиЗначение(ВременноеЗнч);
				НовСтр.ЗначениеПоУмолчанию = ЗначПоУмолчанию;
			Исключение
				ЧислоОшибок = ЧислоОшибок + 1;
				ТекстОшибки = ОписаниеОшибки();
			КонецПопытки;
		КонецЦикла;
		// Проверим есть ли что в результате
		Если ТаблицаСвойств.Количество() > 0 Тогда
			Если ТаблицаСвойств.Найти("Таймаут", "Свойство") = НЕОПРЕДЕЛЕНО Тогда
				НовСтр=ТаблицаСвойств.Добавить();
				НовСтр.ТипЗначения	= "Число";
				НовСтр.Свойство		= "Таймаут";
				НовСтр.Значение		= СтрокаТаблицыУстройств.Таймаут;
			КонецЕсли;
			// Сохраним полученный результат в кэше
			Если СтрокаТаблицыУстройств <> Неопределено Тогда
				СтрокаТаблицыУстройств.ТаблицаСвойств = ТаблицаСвойств.Скопировать();
			КонецЕсли;
			Результат = Истина;
		КонецЕсли;
		
		Если ЧислоОшибок > 0 Тогда
			ОписаниеОшибки = "Всего ошибок при получении свойств: " + ЧислоОшибок + " Описание последней: " + ОписаниеОшибки;
		Иначе
			ОписаниеОшибки = "";
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = "Ошибка при разборе таблицы свойств оборудования: " + ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(	"Оборудование.ПолучитьТаблицуСвойств",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									Оборудование,
		                         	ОписаниеОшибки + " Состояние: " + ТекущийСтатус);
		Возврат Ложь;
	КонецПопытки;
	// Вернем результат
	Возврат Результат;
	
КонецФункции // ПолучитьТаблицуСвойств

// Получает значение конкретного свойства оборудования
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
//				ИмяСвойства	 - строка, имя свойства, значение которого необходимо получить
// Возвратные параметры:
//				ЗначениеСвойства - полученное из СЗиУО значение заданного свойства оборудования
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//				Истина	- как признак успешного выполнения операции
//				Ложь	- если операция не удалась (и заполняет текст ошибки)
Функция ПолучитьЗначениеСвойстваОборудования(	Знач Оборудование, 
												Знач ИмяСвойства, 
												ЗначениеСвойства, 
												ОписаниеОшибки="") Экспорт
	// Инициализация рабочих переменных	
	Результат = Ложь; 
	НомОшибки = 22010; 
	ОписаниеОшибки = "Некорректные входные параметры при вызове функции ""ПолучитьЗначениеСвойстваОборудования""";
	ЗначениеСвойства = НЕОПРЕДЕЛЕНО;
	GUID_Оборудования = "";
	// Проверим входные параметры
	Если (ТипЗнч(ИмяСвойства) <> Тип("Строка")) ИЛИ ПустаяСтрока(ИмяСвойства) Тогда
		ОписаниеОшибки = "Некорректно задано имя свойства в параметрах вызова";
		Возврат Ложь;
	Иначе
		ИмяСвойства = ВРЕГ(СокрЛП(ИмяСвойства));
	КонецЕсли;
	// Получим таблицу параметров устройства
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ОписаниеОшибки = "Ошибка при получении таблицы параметров оборудования";
	Если ПолучитьТаблицуСвойств(Оборудование, ТаблицаСвойств, ОписаниеОшибки) Тогда
		// Поищем нужное свойство в таблице
		СтрокаТаблицыСвойств = ТаблицаСвойств.Найти(ИмяСвойства,"Свойство");
		Если СтрокаТаблицыСвойств = НЕОПРЕДЕЛЕНО Тогда
			ОписаниеОшибки = "Отсутствует свойство <" + ИмяСвойства + "> у оборудования <" + СокрЛП(Оборудование) + ">";
		Иначе
			ЗначениеСвойства = СтрокаТаблицыСвойств.Значение;
			Возврат Истина;
		КонецЕсли;			
	КонецЕсли;
	// Сюда попали при неудаче
	Возврат Ложь;
КонецФункции


//////////////////////////////////////////////////////////////////////////////
// БАЗОВЫЕ ФУНКЦИИ РАБОТЫ С УСТРОЙСТВАМИ (Вызываются на стороне клиентской сессии)

// Включает конкретный экземпляр оборудования
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
//				ДопДиагностика - булево, выводить сообщения расширенной диагностики устройств
// Возвратные параметры:
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//				Код ошибки: 0 - если действие выполнено успешно, номер ошибки
//				в противном случае и текстовое описание ошибки через "ОписаниеОшибки"
//
Функция ВключитьОборудование(Знач Оборудование, ОписаниеОшибки = "", ДопДиагностика = Истина) Экспорт
	
	// Инициализация рабочих переменных
	НомОшибки=0;
	ОписаниеОшибки="";
	GUID_Оборудования="";
	ТаймАут=90;
	ТекущийСтатус=НЕОПРЕДЕЛЕНО;
	СтрокаТаблицыУстройств=НЕОПРЕДЕЛЕНО;
	// Проверим входные параметры
	Если НЕ ОборудованиеКорректно(	Оборудование,GUID_Оборудования,ТаймАут,НомОшибки,
									ОписаниеОшибки,ТекущийСтатус,,СтрокаТаблицыУстройств) Тогда
		// Что-то с устройством не так. Описание ошибки см. "ОписаниеОшибки"
		ЗаписьЖурналаРегистрации("Оборудование.Включить",УровеньЖурналаРегистрации.Ошибка,Неопределено,Оборудование,
		                         ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
		Возврат НомОшибки;
	КонецЕсли;
	// Проверим статус, а то вдруг уже включено, так зачем зря систему напрягать?
	Если ТекущийСтатус=НЕОПРЕДЕЛЕНО Тогда
		НомОшибки=22053;
		ОписаниеОшибки="Не удалось определить статус оборудования: <"+Оборудование+">";
	ИначеЕсли Оборудование.ПометкаУдаления Тогда
		НомОшибки=22054;
		ОписаниеОшибки="Оборудование: <"+Оборудование+"> помечено на удаление, команда включения игнорируется";
	Иначе
		Попытка
			ТипУстройства=Оборудование.КлассОборудования;
		Исключение
			ТипУстройства=НЕОПРЕДЕЛЕНО;
		КонецПопытки;
		// Не то что включено, а даже еще и зарезервировано под текущую сессию - просто отлично!
		Если (ТекущийСтатус=Перечисления.СтатусыОборудования.Готово)
		// Просто включено (для любого устройства кроме сканера этого вполне достаточно)
		ИЛИ ((ТекущийСтатус=Перечисления.СтатусыОборудования.Включено) И ((ТипУстройства=НЕОПРЕДЕЛЕНО) ИЛИ 
		    (ТипУстройства<>Перечисления.КлассыОборудования.Сканер))) Тогда
			Возврат НомОшибки; // Оборудование уже и так включено, возвращаем ОК (НомОшибки=0) вот и все.
		КонецЕсли;
		//sms4b+
		Если СтрокаТаблицыУстройств<>НЕОПРЕДЕЛЕНО Тогда
			Если Найти(ВРЕГ(СтрокаТаблицыУстройств.МодельОборудования),"SMS4B") > 0 Тогда
				Сообщить(СтрокаТаблицыУстройств.НаименованиеУстройства + НСтр("ru = ': данное оборудование больше не поддерживается'"));
				Возврат НомОшибки; // Данное оборудование больше не поддерживается
			КонецЕсли;
		КонецЕсли;
		//sms4b-
		// Выполняем команду включения для устройства
		Попытка
			Параметры="";
			Состояние("Включение устройства: <"+СокрЛП(Оборудование)+"> ...");
			Если СтрокаТаблицыУстройств<>НЕОПРЕДЕЛЕНО Тогда
				СтрокаТаблицыУстройств.ВремяПроверкиСтатуса = НЕОПРЕДЕЛЕНО; // Сбросим время актуальности статуса
			КонецЕсли;
			НомОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_Оборудования,"Включить",Параметры,ТаймАут);
			Если ЗначениеЗаполнено(НомОшибки) Тогда
				ОписаниеОшибки=ПолучитьОписаниеОшибкиКомпоненты();
			Иначе
				// Выполним другие действия с устройством после включения CASE по классам/моделям оборудования
				Если (ТипУстройства<>НЕОПРЕДЕЛЕНО) И (ТипУстройства=Перечисления.КлассыОборудования.Сканер) Тогда
					// Сканер должен присылать нам события, для чего нужно еще дополнительно заблокировать его на себя
					НомОшибки=ЗаблокироватьОборудование(Оборудование,ОписаниеОшибки);
				КонецЕсли; 
			КонецЕсли;
		Исключение
			ОписаниеОшибки = "Ошибка при вызове метода компоненты: "+ОписаниеОшибки();
			ОписаниеОшибки = ПолучитьОписаниеОшибкиКомпоненты(ОписаниеОшибки);
			НомОшибки = 22050;
		КонецПопытки;
		
		// Проверим чем закончились наши попытки
		ТекущийСтатус = СтатусОборудования(Оборудование);
		
	КонецЕсли;	
	
	// Сообщим в журнал регистрации
	ЗаписьЖурналаРегистрации("Оборудование.Включить",
	                         ?(НомОшибки=0,УровеньЖурналаРегистрации.Информация,УровеньЖурналаРегистрации.Ошибка),
	                         ?(ТипЗнч(Оборудование)=Тип("СправочникСсылка.Оборудование"),Оборудование.Метаданные(),Неопределено),
							 Оборудование,
							 ОписаниеОшибки + " Состояние: "+ТекущийСтатус);
					
	// Возврат результата
	Возврат НомОшибки;
	
КонецФункции // ВключитьОборудование

// Выключает конкретный экземпляр оборудования
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
// Возвратные параметры:
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//				Код ошибки: 0 - если действие выполнено успешно, номер ошибки
//				в противном случае и текстовое описание ошибки через "ОписаниеОшибки"
//
Функция ВыключитьОборудование(Знач Оборудование, ОписаниеОшибки = "ОК") Экспорт
	// Инициализация рабочих переменных	
	НомОшибки=0;
	ОписаниеОшибки="ОК";
	GUID_Оборудования="";
	ТаймАут=90;
	ТекущийСтатус=НЕОПРЕДЕЛЕНО;
	СтрокаТаблицыУстройств=НЕОПРЕДЕЛЕНО;
	// Проверим входные параметры
	Если НЕ ОборудованиеКорректно(	Оборудование,GUID_Оборудования,ТаймАут,НомОшибки,
									ОписаниеОшибки,ТекущийСтатус,,СтрокаТаблицыУстройств) Тогда
		// Что-то с устройством не так. Описание ошибки см. "ОписаниеОшибки"
		ЗаписьЖурналаРегистрации("Оборудование.Выключить",УровеньЖурналаРегистрации.Ошибка,
		                         ?(ТипЗнч(Оборудование)=Тип("СправочникСсылка.Оборудование"),
		                         Оборудование.Метаданные(),Неопределено),Оборудование,
		                         ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
		Возврат НомОшибки;
	КонецЕсли;
	// Выполняем команду выключения для устройства
	Попытка
		Состояние("Выключение устройства: <"+СокрЛП(Оборудование)+"> ...");
		Если СтрокаТаблицыУстройств<>НЕОПРЕДЕЛЕНО Тогда
			СтрокаТаблицыУстройств.ВремяПроверкиСтатуса = НЕОПРЕДЕЛЕНО; // Сбросим время актуальности статуса
		КонецЕсли;
		Параметры="";
		НомОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_Оборудования,"Выключить",Параметры,ТаймАут);
		Попытка ОписаниеОшибки=Строка(Рарус_Компонента.ОписаниеОшибки);
		Исключение ОписаниеОшибки="Не удалось получить описание ошибки из системы оборудования"; КонецПопытки;
	Исключение
		НомОшибки=22050;
		ОписаниеОшибки="Ошибка при вызове метода компоненты: "+ОписаниеОшибки();
	КонецПопытки;
	
	// Проверим чем закончились наши попытки
	ТекущийСтатус = СтатусОборудования(Оборудование);

	// Сообщим в журнал регистрации
	ЗаписьЖурналаРегистрации("Оборудование.Выключить",
							?(НомОшибки=0,УровеньЖурналаРегистрации.Информация,УровеньЖурналаРегистрации.Ошибка),
	                        ?(ТипЗнч(Оборудование)=Тип("СправочникСсылка.Оборудование"),Оборудование.Метаданные(),Неопределено),
							Оборудование,
							ОписаниеОшибки + " Состояние: "+ТекущийСтатус);
	// Возврат результата
	Возврат НомОшибки;
КонецФункции // ВыключитьОборудование

// Блокирует для монопольного использования конкретный экземпляр оборудования
// Блокировка требуется для: 1) предотвращения использования устройства другим пользователем
//                           2) получения внешних событий от устройства на данном компьютере
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
//				ФлагПерекрытия - число 0 или 1. При установленном флаге можно принудительно заблокировать
//				                 устройство, заблокированное ранее другим пользователем (override)
// Возвратные параметры:
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//				Код ошибки: 0 - если действие выполнено успешно, номер ошибки
//				в противном случае и текстовое описание ошибки через "ОписаниеОшибки"
//
Функция ЗаблокироватьОборудование(Знач Оборудование, ОписаниеОшибки = "ОК", ФлагПерекрытия = 0) Экспорт
	// Инициализация рабочих переменных	
	НомОшибки=0;
	ОписаниеОшибки="ОК";
	GUID_Оборудования="";
	ТаймАут=90;
	ТекущийСтатус=НЕОПРЕДЕЛЕНО;
	СтрокаТаблицыУстройств=НЕОПРЕДЕЛЕНО;
	// Проверим входные параметры
	Если НЕ ОборудованиеКорректно(	Оборудование,GUID_Оборудования,ТаймАут,НомОшибки,
									ОписаниеОшибки,ТекущийСтатус,,СтрокаТаблицыУстройств) Тогда
		// Что-то с устройством не так. Описание ошибки см. "ОписаниеОшибки"
		ЗаписьЖурналаРегистрации("Оборудование.Заблокировать",УровеньЖурналаРегистрации.Ошибка,Неопределено,Оборудование,
								 ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
		Возврат НомОшибки;
	КонецЕсли;
	// Выполняем команду блокировки для устройства
	Попытка
		Состояние("Блокировка устройства: <"+СокрЛП(Оборудование)+"> ...");
		Если СтрокаТаблицыУстройств<>НЕОПРЕДЕЛЕНО Тогда
			СтрокаТаблицыУстройств.ВремяПроверкиСтатуса = НЕОПРЕДЕЛЕНО; // Сбросим время актуальности статуса
		КонецЕсли;
		// Создаем массив параметров
		Параметры=Рарус_Компонента.СоздатьПараметры(1,1);
		Параметры.SetValue(0,0,Число(ФлагПерекрытия));
		// Выполняем команду устройства
		НомОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_Оборудования,"Заблокировать",Параметры,ТаймАут);
		Попытка ОписаниеОшибки=Строка(Рарус_Компонента.ОписаниеОшибки);
		Исключение ОписаниеОшибки="Не удалось получить описание ошибки из системы оборудования"; КонецПопытки;
		// Проверка корректности работы метода
		Если ТипЗнч(НомОшибки)<>Тип("Число") Тогда 
			ОписаниеОшибки="Метод компоненты вернул некорректное значение"+?(ОписаниеОшибки="ОК","",". "+ОписаниеОшибки);;
			НомОшибки=22051;
		КонецЕсли;
	Исключение
		ОписаниеОшибки="Ошибка при вызове метода компоненты: "+ОписаниеОшибки();
		НомОшибки=22050;
	КонецПопытки;	
	// Проверим чем закончились наши попытки
	ТекущийСтатус = СтатусОборудования(Оборудование);
	// Сообщим в журнал регистрации
	ЗаписьЖурналаРегистрации("Оборудование.Заблокировать",
							?(НомОшибки=0,УровеньЖурналаРегистрации.Информация,УровеньЖурналаРегистрации.Ошибка),
							Оборудование.Метаданные(),Оборудование,ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
	// Возврат результата
	Возврат НомОшибки;
КонецФункции // ЗаблокироватьОборудование

// Освобождает для общего использования ранее монопольно заблокированный экземпляр оборудования
// Параметры:
//				Оборудование - СправочникСсылка.Оборудование или GUID-идентификатор оборудования
//				ФлагПерекрытия - число 0 или 1. При установленном флаге можно принудительно разблокировать
//				                 устройство, заблокированное ранее другим пользователем (override)
// Возвратные параметры:
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//				Код ошибки: 0 - если действие выполнено успешно, номер ошибки
//				в противном случае и текстовое описание ошибки через "ОписаниеОшибки"
//
Функция ОсвободитьОборудование(Знач Оборудование, ОписаниеОшибки = "ОК", ФлагПерекрытия = 0) Экспорт
	// Инициализация рабочих переменных	
	НомОшибки=0;
	ОписаниеОшибки="ОК";
	GUID_Оборудования="";
	ТаймАут=90;
	ТекущийСтатус=НЕОПРЕДЕЛЕНО;
	СтрокаТаблицыУстройств=НЕОПРЕДЕЛЕНО;
	// Проверим входные параметры
	Если НЕ ОборудованиеКорректно(	Оборудование,GUID_Оборудования,ТаймАут,НомОшибки,
									ОписаниеОшибки,ТекущийСтатус,,СтрокаТаблицыУстройств) Тогда
		// Что-то с устройством не так. Описание ошибки см. "ОписаниеОшибки"
		ЗаписьЖурналаРегистрации("Оборудование.Освободить",УровеньЖурналаРегистрации.Ошибка,Неопределено,Оборудование,
								ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
		Возврат НомОшибки;
	КонецЕсли;
	// Выполняем команду разблокировки для устройства
	Попытка
		Состояние("Снятие блокировки устройства: <"+СокрЛП(Оборудование)+"> ...");
		Если СтрокаТаблицыУстройств<>НЕОПРЕДЕЛЕНО Тогда
			СтрокаТаблицыУстройств.ВремяПроверкиСтатуса = НЕОПРЕДЕЛЕНО; // Сбросим время актуальности статуса
		КонецЕсли;
		// Создаем массив параметров
		Параметры=Рарус_Компонента.СоздатьПараметры(1,1);
		Параметры.SetValue(0,0,Число(ФлагПерекрытия));
		Состояние("Освобождение устройства: <"+СокрЛП(Оборудование)+"> ...");
		// Выполняем команду устройства
		НомОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_Оборудования,"Освободить",Параметры,ТаймАут);
		Попытка ОписаниеОшибки=Строка(Рарус_Компонента.ОписаниеОшибки); 
		Исключение ОписаниеОшибки="Не удалось получить описание ошибки из системы оборудования"; КонецПопытки;
	Исключение
		НомОшибки=22050;
		ОписаниеОшибки="Ошибка при вызове метода компоненты: "+ОписаниеОшибки();
	КонецПопытки;	
	// Проверим чем закончились наши попытки
	ТекущийСтатус = СтатусОборудования(Оборудование);
	// Сообщим в журнал регистрации
	ЗаписьЖурналаРегистрации("Оборудование.Освободить",
							?(НомОшибки=0,УровеньЖурналаРегистрации.Информация,УровеньЖурналаРегистрации.Ошибка),
							Оборудование.Метаданные(),Оборудование,ОписаниеОшибки+" Состояние: "+ТекущийСтатус);
	// Возврат результата
	Возврат НомОшибки;
КонецФункции // ОсвободитьОборудование

// Получает описание ошибки из системы управления оборудованием
// Параметры:
//				ОписаниеИз1С - необязательный, назначенное сообщение на случай ошибки получения описания из компоненты
// Возвратные параметры:
//				ОписаниеОшибки - строка, возвратный параметр с описанием возникшей ошибки
// Возвращает:
//				строку с описанием ошибки, возникшей во внешней компоненте типового решения
Функция ПолучитьОписаниеОшибкиКомпоненты(Знач ОписаниеИз1С = "") Экспорт
	// Сделаем попытку получить описание ошибки из компоненты
	Попытка 
		ОписаниеОшибки = Строка(глРарус_Компонента.ОписаниеОшибки);
	Исключение
		ОписаниеОшибки = "";
	КонецПопытки;
	// Проанализируем что мы получив результате
	СтрВремен = ВРЕГ(ОписаниеОшибки);
	Если ПустаяСтрока(СтрВремен) 
		ИЛИ (ОписаниеОшибки="ОК") // По-русски
		ИЛИ (СтрВремен="OK") // On english
		ИЛИ (Найти(СтрВремен," УСПЕШНО")>0) Тогда
		// Но ошибка все-таки была - значит просто имеем неправильное описание
		Если ПустаяСтрока(ОписаниеИз1С) Тогда
			ОписаниеОшибки = "Не удалось получить описание ошибки из внешней компоненты";
		Иначе
			ОписаниеОшибки = ОписаниеИз1С; // установим описание ошибки из 1С
		КонецЕсли;
	КонецЕсли;
	// Вернем полученное описание
	Возврат ОписаниеОшибки;
КонецФункции

// Добавляет новое устройство в схему системы управления оборудованием
// Параметры:
//  ОписаниеОшибки - GUID-идентификатор модели создаваемого оборудования
//  ПоказыватьФормуНастройки - число (0 либо 1), 1  означает показывать модальную форму настройки устройства
//  (если есть у него таковая)
// Возвратные параметры:
//  GUID_Оборудования - GUID-идентификатор оборудования
//  НаименованиеОборудования - строка, присвоенное системой именование устройства 
//  ОписаниеОшибки - строка, необязательный возвратный параметр с описанием возникшей ошибки
//
// Возвращает:	Булево, результат выполнения операции, Ложь если возникли ошибки
Функция СоздатьОборудование(Знач GUID_модели,
							Знач ПоказыватьФормуНастройки = 1,
							GUID_Оборудования,
							НаименованиеОборудования = "",
							ОписаниеОшибки = "") Экспорт
		// Начальная инициализация переменных
		ТекстОшибки = "";
		Если НЕ ЗначениеЗаполнено(ПоказыватьФормуНастройки) Тогда
			ПоказыватьФормуНастройки = 0;
		КонецЕсли;
		GUID_модели = СокрЛП(GUID_модели);
		GUID_Оборудования = "";			// Идентификатор создается в системе управления оборудованием
		НаименованиеОборудования = "";	// Для нового оборудования пусть всегда наименование берется из системы оборудования
		Попытка 
			КодОшибки = глРарус_Компонента.ДобавитьОборудование(ПоказыватьФормуНастройки, GUIDНашейТочкиДоступа, 
																GUID_модели, GUID_Оборудования, НаименованиеОборудования);
			Если ЗначениеЗаполнено(КодОшибки) Тогда
				Если (ТипЗнч(КодОшибки) = Тип("Число")) И (КодОшибки = -1) Тогда
					ОписаниеОшибки = "Пользователь отказался от сохранения устройства в схему";
					Возврат Ложь; // Экземпляр не создан
				КонецЕсли;
				ОписаниеОшибки = ПолучитьОписаниеОшибкиКомпоненты(" (Код ошибки = " + КодОшибки + ")");
				// Сообщим в журнал регистрации
				ЗаписьЖурналаРегистрации("Оборудование.СоздатьУстройство", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
				Возврат Ложь; // Экземпляр не создан
			КонецЕсли;
			// Обновим нашу копию схемы устройств
			глТорговоеОборудование.ПолучитьТаблицуУстройств();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ОписаниеОшибки = ПолучитьОписаниеОшибкиКомпоненты(ОписаниеОшибки);
			// Сообщим в журнал регистрации
			ЗаписьЖурналаРегистрации("Оборудование.СоздатьУстройство",УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
			Возврат Ложь; // Экземпляр не создан
		КонецПопытки;
КонецФункции

// Удаляет устройство из схемы системы управления оборудованием
// Параметры:
//				GUID_Оборудования - GUID-идентификатор оборудования
// Возвратные параметры:
//				ОписаниеОшибки - строка, необязательный возвратный параметр с описанием возникшей ошибки
//
// Возвращает:	Булево, результат выполнения операции, Ложь если возникли ошибки
Функция УдалитьОборудование(Знач GUID_Оборудования, ОписаниеОшибки = "ОК") Экспорт
	
	Если РаботаСОборудованиемЗапрещена() Тогда // Нет возможности удалить оборудование из схемы СЗиУО
		ОписаниеОшибки = "Текущий режим: ""Работа с оборудованием запрещена"" не позволяет вносить изменения " + 
						"в схему управления оборудованием.";
		ЗаписьЖурналаРегистрации("Оборудование.УдалитьУстройство",УровеньЖурналаРегистрации.Ошибка,Неопределено,,
								"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
		
	Если НЕ ЭтоГлавныйСеанс1С() Тогда // Нет возможности удалить оборудование из схемы СЗиУО
		ОписаниеОшибки = "Текущий сеанс 1С не является главным (первичным) на данной рабочей станции - нет возможности " +
						"вносить изменения в схему управления оборудованием.";
		ЗаписьЖурналаРегистрации("Оборудование.УдалитьУстройство",УровеньЖурналаРегистрации.Ошибка,Неопределено,,
								"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	СтатусОборудования = НЕОПРЕДЕЛЕНО;
	СтрокаТаблицыУстройств = НЕОПРЕДЕЛЕНО; // Рабочая переменная
	ДопустимыеСтатусы = Новый Массив;
	ДопустимыеСтатусы.Добавить(Перечисления.СтатусыОборудования.НеИспользуется);
	ДопустимыеСтатусы.Добавить(Перечисления.СтатусыОборудования.Недоступно);
	ДопустимыеСтатусы.Добавить(Перечисления.СтатусыОборудования.Занято);
	Если НЕ ОборудованиеКорректно( ,GUID_Оборудования,,,ОписаниеОшибки,СтатусОборудования,ДопустимыеСтатусы,
								СтрокаТаблицыУстройств)
		ИЛИ (СтрокаТаблицыУстройств = НЕОПРЕДЕЛЕНО) Тогда
		// Что-то с устройством не так - см. в "ОписаниеОшибки"
		ЗаписьЖурналаРегистрации("Оборудование.УдалитьУстройство",УровеньЖурналаРегистрации.Ошибка,Неопределено,,
								"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Если (СтрокаТаблицыУстройств.GUIDточкиДоступа <> GUIDНашейТочкиДоступа) Тогда
		// нельзя удалять "чужие" устройства
		ОписаниеОшибки = "Устройство принадлежит другой точке доступа - запрещено удалять ""чужие"" устройства";
		ЗаписьЖурналаРегистрации("Оборудование.УдалитьУстройство",УровеньЖурналаРегистрации.Ошибка,Неопределено,,
								"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	// Проверим статус оборудования	
	Если 	(СтатусОборудования = НЕОПРЕДЕЛЕНО) 
		ИЛИ (СтатусОборудования = Перечисления.СтатусыОборудования.НетДрайвера)
		ИЛИ (СтатусОборудования = Перечисления.СтатусыОборудования.Запрещено)
		ИЛИ (СтатусОборудования = Перечисления.СтатусыОборудования.Ошибка) Тогда
		// В этом состоянии мы не можем удалить устройство из схемы - не будем и пытаться
		ОписаниеОшибки = "Невозможно удалить из схемы оборудования устройство со статусом """ + 
						Строка(СтатусОборудования) + """";
		ЗаписьЖурналаРегистрации(	"Оборудование.Удалить",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									,
									"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Ложь;
	ИначеЕсли	(СтатусОборудования = Перечисления.СтатусыОборудования.Отсутствует)
			ИЛИ	(СтатусОборудования = Перечисления.СтатусыОборудования.Удалено) Тогда
		// Данного устройства уже и так нет в схеме СЗиУО - значит ничего делать и не будем
		ОписаниеОшибки = "Устройство со статусом """ + Строка(СтатусОборудования) + """ нет смысла удалять" ;
		ЗаписьЖурналаРегистрации(	"Оборудование.УдалитьУстройство",
									УровеньЖурналаРегистрации.Предупреждение,
									Неопределено,
									,
									"Устройство {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Истина;	// Ошибки нет
	ИначеЕсли	(СтатусОборудования = Перечисления.СтатусыОборудования.Включено) ИЛИ 
				(СтатусОборудования = Перечисления.СтатусыОборудования.Готово) Тогда
		// Нельзя удалять включенное оборудование
		ОписаниеОшибки = "Перед удалением оборудования необходимо перевести его в состояние ""Выключено""!";
		ЗаписьЖурналаРегистрации(	"Оборудование.УдалитьУстройство",
									УровеньЖурналаРегистрации.Ошибка,
									Неопределено,
									,
									"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
		Возврат Ложь;
	Иначе
	    // сделаем попытку удаления оборудования из общей схемы
		КодОшибки = 0; 
		Попытка
			КодОшибки = глРарус_Компонента.УдалитьОборудование(GUID_Оборудования);
		Исключение
			КодОшибки = 10000;
			ОписаниеОшибки = ОписаниеОшибки();
			ОписаниеОшибки = ПолучитьОписаниеОшибкиКомпоненты(ОписаниеОшибки);
		КонецПопытки;
		Если ЗначениеЗаполнено(КодОшибки) Тогда
			Если ПустаяСтрока(ОписаниеОшибки) Тогда
				ОписаниеОшибки = глТорговоеОборудование.ПолучитьОписаниеОшибкиКомпоненты(ОписаниеОшибки);
			КонецЕсли;
			Если ПустаяСтрока(ОписаниеОшибки) Тогда
				ОписаниеОшибки = "Ошибка (код = "+КодОшибки+") при попытке удаления устройства " + GUID_Оборудования +
								" из схемы оборудования";
			КонецЕсли;
			ЗаписьЖурналаРегистрации(	"Оборудование.УдалитьУстройство",
										УровеньЖурналаРегистрации.Ошибка,
										Неопределено,
										,
										"Ошибка при удалении {" + GUID_Оборудования + "} : " + ОписаниеОшибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	// По ошибке вышли бы раньше, а раз сюда дошли значит все хорошо 
	Возврат Истина;
КонецФункции	
//////////////////////////////////////////////////////////////////////////////
// БЛОК ОБСЛУЖИВАНИЯ КОМАНД КЛИЕНТОВ (Выполняются на стороне серверной сессии)

// Единая серверная точка входа для вызова из компоненты оборудования, требующего 1С-обработчика для выполнения задания
// Используется системной компонентой, не изменять без должных на то оснований!
// Параметры:
//  GUID_Оборудования - GUID-идентификатор устройства
//  Действие - строка, команда устройству
//  Параметры - ComSafeArray, массив входных параметров
//  ТаймАут - число, количество миллисекунд за которое должна выполниться команда (заказчик больше ждать не будет)
//  ОбъектЗадача - Ссылка на объект компоненты, предназначенный для взаимодействия с драйвером-плагином и возврата 
//  кода/описания ошибки в транспортную часть
// Возвратные параметры:
//  Параметры - ComSafeArray, массив выходных параметров
// Возвращает:
//  Код ошибки: 0 - если действие выполнено успешно, номер ошибки в противном случае
//  Текстовое описание само по себе не возвращается, только через "ОбъектЗадача"
//
Функция ВыполнитьДействие(	GUID_Оборудования = "",
							Действие = "",
							Параметры = Неопределено,
							ТаймАут = 90,
							ОбъектЗадача = Неопределено) Экспорт
	// Инициализация рабочих переменных	
	НомОшибки = 0;
	ОписаниеОшибки = "";
	// Проверки входных параметров
	Если (ТипЗнч(GUID_Оборудования) <> Тип("Строка")) ИЛИ (СтрДлина(GUID_Оборудования) <> 38)
	 ИЛИ (Лев(GUID_Оборудования,1) <> "{") ИЛИ (Прав(GUID_Оборудования,1) <> "}")
	 ИЛИ (ТипЗнч(Действие) <> Тип("Строка")) ИЛИ ПустаяСтрока(Действие)
	 ИЛИ НЕ((ТипЗнч(Параметры) = ТипСОМОбъект) ИЛИ (Параметры = НЕОПРЕДЕЛЕНО) ИЛИ ПустаяСтрока(Параметры))
	 ИЛИ (ТипЗнч(ТаймАут) <> Тип("Число"))
	 ИЛИ (ТипЗнч(ОбъектЗадача) <> ТипСОМОбъект)
	Тогда
	 	// Некорректные входные параметры
		НомОшибки = 22000; 	
		Если ТипЗнч(ОбъектЗадача) = ТипСОМОбъект Тогда // Задача все-таки существует
			Попытка
				// Попытаемся вернуть вызвавшему код и описание ошибки
				ОбъектЗадача.ОписаниеПоследнейОшибки = "Обнаружены некорректные параметры в точке вызова 1С-обработчика";
				ОбъектЗадача.КодПоследнейОшибки = НомОшибки;
			Исключение
			КонецПопытки;
			// Вернем код ошибки
			Возврат НомОшибки;
		КонецЕсли;
		
		// Попробуем освободить ссылки на объекты компоненты
		ОбъектЗадача = Неопределено;
		Параметры = Неопределено;
		
		// Вернем код ошибки
		Возврат НомОшибки;
	КонецЕсли;
	
	Если (ТаймАут < 5) Тогда
		ОбъектЗадача.ОписаниеПоследнейОшибки = "Таймаут должен быть целым положительным числом (не меньше 5)";
		ОбъектЗадача.КодПоследнейОшибки = 22001;
	КонецЕсли;
	
	// Найдем обработчик среди форм оборудования и вызовем его методы для выполнения команды
	ФормаОбработчик = Неопределено;
	Попытка
		// Превратим GUID-строку в идентификатор, откусив фигурные скобки и вырезав "-"
		ИдGUID = ИдGUID(GUID_Оборудования);
		// Проверим, есть ли у нас драйвер обработчик для данного устройства (вдруг динамически создали новое устройство?)
		Если НЕ ЗагруженныеОбработчикиУстройств.Свойство(ИдGUID, ФормаОбработчик) Тогда
			// Заменяем таблицу устройств
			ПолучитьТаблицуУстройств();
			// Загружаем нехватающие обработчики
			ОбновитьТаблицуОбработчиков();
		КонецЕсли;
		
		// Найдем обработчик среди загруженных
		Если ЗагруженныеОбработчикиУстройств.Свойство(ИдGUID, ФормаОбработчик) Тогда
			Попытка
				// При выполнении команды обработчик должен переустановить код ошибки (0 если не было ошибок)
				ФормаОбработчик.КодОшибки = 0;
				// При исполнении комнады обработчик должен заполнить описание ошибки ("ОК" если ошибок не было)
				ФормаОбработчик.ТекстОшибки = "";
				// Очистим если вдруг что осталось висеть в памяти с прошлого вызова
				ФормаОбработчик.ПараметрыВозврата = "";
				// Передача входных параметров (параметров вызова) обработчику
				ФормаОбработчик.ВходныеПараметры = Параметры;
				// Передадим обработчику контекст текущего задания и драйвера устройства
				ФормаОбработчик.Задача = ОбъектЗадача;
				Попытка
					Выполнить("ФормаОбработчик." + Действие + "()");
					// Код и описание ошибки всегда берем из обработки
					НомОшибки = ФормаОбработчик.КодОшибки;
					ОписаниеОшибки = ФормаОбработчик.ТекстОшибки;
					Если НомОшибки = 0 Тогда	// ОК, устанавливаем возвращаемые параметры
						Попытка
							Параметры = ФормаОбработчик.ПараметрыВозврата;
						Исключение
							ОписаниеОшибки = "Ошибка получения возвратных параметров из 1С-обработчика устройства";
							НомОшибки = 22006;
						КонецПопытки;
					КонецЕсли;
					// Получено ли описание ошибки
					Если ПустаяСтрока(ОписаниеОшибки) ИЛИ ((НомОшибки <> 0) И (ОписаниеОшибки = "ОК")) Тогда
						ПолучитьОшибкуОборудования(ОбъектЗадача, НомОшибки, ОписаниеОшибки);
					КонецЕсли;
				Исключение
					ОписаниеОшибки = 	"Ошибка реализации команды <" + Действие + "> в 1С-обработчике устройства
							  			|Текст ошибки: " + ОписаниеОшибки();
					НомОшибки = 22005;
				КонецПопытки;
			Исключение
				ОписаниеОшибки =	"Некорректная реализация 1С-обработчика устройства
						  			|Текст ошибки: " + ОписаниеОшибки();
				НомОшибки = 22003;
			КонецПопытки;
		Иначе
			ОписаниеОшибки = 	"Обработчик для оборудования GUID " + GUID_Оборудования + " не загружен.
					  			|Требуется обновление схемы устройств и перезапуск 1С-Предприятия.";
			НомОшибки=22002;
		КонецЕсли;
	Исключение
		ОписаниеОшибки = "Ошибка в точке вызова 1С-обработчика управления устройством
				  		 |Текст ошибки: "+ОписаниеОшибки();
		НомОшибки = 22001;
	КонецПопытки;
	
	// Освободим ссылки на объекты компоненты
	Если (ФормаОбработчик<>Неопределено) Тогда
		Попытка
			ФормаОбработчик.ВходныеПараметры = Неопределено;
		Исключение
		КонецПопытки;
		Попытка
			ФормаОбработчик.Задача = Неопределено;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	// Попытаемся вернуть код ошибки и описание в задачу
	Попытка
		ОбъектЗадача.ОписаниеПоследнейОшибки = ОписаниеОшибки;
		ОбъектЗадача.КодПоследнейОшибки = НомОшибки;
	Исключение
		// В отладочных целях здесь можно посмотреть здесь описание ошибки, но оно никуда не вернется
		ОписаниеОшибки = ОписаниеОшибки();
		ОписаниеОшибки = "КодОшибки=" + НомОшибки + ": ошибка при возврате результата: " + ОписаниеОшибки;
	КонецПопытки;
	
	// Закончили
	Возврат НомОшибки;
КонецФункции // ВыполнитьДействие

// Создает COM-объект - драйвер устройства для управления устройством из 1С-Предприятия
// Параметры:
//  ИмяУстройства - строка, имя объекта - идентификатор COM-объекта в реестре Windows
//  ИмяВнешнейБиблиотеки - строка, имя загружаемой внешней библиотеки в виде "ИмяФайла.dll"
//  оставить пустым, если используется OLE-объект, а не компонента 1С
//  Устройство - COM-объект, драйвер устройства, локальный объект обработки
//  Локально - булево, определяет, требуется ли создавать индивидуальный COM-объект для каждого логического устройства
//  в этом случае объект сохраняется в экземпляре формы-обработчика, в противном случае объект сохраняется в общей 
//  структуре и используется всеми формами-обработчиками
// Возвратные параметры:
//  Устройство - COM-объект, собственно драйвер устройства
//  ОписаниеОшибки - строка, описание возникшей ошибки
// Возвращает:
//  Код ошибки: 0 - если действие выполнено успешно, номер ошибки
//  в противном случае и текстовое описание ошибки через "ОписаниеОшибки"
//
Функция ПолучитьУстройство(	ИмяУстройства = "",
							ИмяВнешнейБиблиотеки = "",
							Устройство = Неопределено,
							Локально = Истина,
							ОписаниеОшибки = "ОК",
							ЭтоCOMОбъект = Ложь,
							ИдентификаторОбъекта = "") Экспорт
	
	// Инициализация рабочих переменных	
	НомОшибки=0; ОписаниеОшибки="ОК";
	
	// Проверим наличие локального драйвера (вдруг он уже загружен, тогда и проверять нечего)
	Если ТипЗнч(Устройство) = ТипСОМОбъект И Локально Тогда
		Возврат НомОшибки;
	КонецЕсли; 
	
	// Проверим корректность названия COM-объекта устройства
	Если ПустаяСтрока(ИмяУстройства) Тогда
		НомОшибки = 22102;
		ОписаниеОшибки = СокрЛП("Не указано имя COM-объекта");
		Возврат НомОшибки;
	КонецЕсли; 
	
	// Загрузим внешнюю библиотеку, если требуется
	Если ПустаяСтрока(ИмяВнешнейБиблиотеки) Тогда
		// ОК, создается OLE-объект, библиотека сама загрузится при создании по данным реестра Windows
	ИначеЕсли Найти(ИменаЗагруженныхВнешнихБиблиотек, "," + ИмяВнешнейБиблиотеки + ",") > 0 Тогда
		// ОК, библиотека уже была загружена, повторная загрузка не требуется
	Иначе
		КомпонентаПодключена = Ложь;
		Попытка
			ПодключитьВнешнююКомпоненту(?(ПустаяСтрока(ИдентификаторОбъекта), ИмяУстройства, ИдентификаторОбъекта));
			КомпонентаПодключена = Истина;
		Исключение
		КонецПопытки;
		// Попытаемся найти файл внешней библиотеки
		Если НЕ КомпонентаПодключена Тогда
			Каталог = СокрЛП(ПараметрыСеанса.Компьютер.ЛокальныйКаталог);
			Каталог = Каталог + ?(Прав(Каталог,1) <> "\", "\", "");
			Попытка
				ЗагрузитьВнешнююКомпоненту(Каталог + ИмяВнешнейБиблиотеки);
				КомпонентаПодключена = Истина;
			Исключение
			КонецПопытки;
		КонецЕсли;
		Если НЕ КомпонентаПодключена Тогда
			НомОшибки = 22101;
			ОписаниеОшибки = "Не удалось загрузить внешнюю библиотеку " + Каталог + ИмяВнешнейБиблиотеки;
			Возврат НомОшибки;
		Иначе	
			ИменаЗагруженныхВнешнихБиблиотек = ИменаЗагруженныхВнешнихБиблиотек + ИмяВнешнейБиблиотеки + ",";
		КонецЕсли;
	КонецЕсли; 
		
	// Загрузим собственно COM-объект и установим на него ссылку
	Если НЕ Локально И ЗагруженныеДрайверыУстройств.Свойство(обСтрокуКID(ИмяУстройства), Устройство) Тогда
		// Все ОК, драйвер устройства есть в общей структуре, на него установлена ссылка
		Возврат НомОшибки;
	Иначе
		// Локальный драйвер не загружен (проверено ранее), а для общей структуры не найден обработчик, 
		// что-ж, создаем драйвер
		Ошибка = Ложь;
		Если ЭтоCOMОбъект Тогда
			Попытка
				Устройство = Новый COMОбъект(ИмяУстройства);
			Исключение
				Ошибка = Истина;
			КонецПопытки;
		Иначе
			Попытка
				Устройство = Новый (ИмяУстройства);
			Исключение
				Ошибка = Истина;
			КонецПопытки;
		КонецЕсли;
		Если Ошибка Тогда
			НомОшибки = 22102;
			ОписаниеОшибки = "Не удалось создать COM объект " + ИмяУстройства;
			Возврат НомОшибки;
		КонецЕсли;
		
		// Для общей структуры вставим созданный обработчик в структуру
		Если НЕ Локально Тогда
			ЗагруженныеДрайверыУстройств.Вставить(обСтрокуКID(ИмяУстройства), Устройство);
		КонецЕсли;
	КонецЕсли;
	
	// Все ОК, устройство успешно создано
	Возврат НомОшибки;
	
КонецФункции // ПолучитьУстройство

// Получает код и описание возникшей ошибки из системы управления оборудованием
// возвращает Истина если удалось получить и Ложь в противном случае
Функция ПолучитьОшибкуОборудования(Задача, КодОшибки, ОписаниеОшибки = "") Экспорт
	Если КодОшибки = 0 Тогда
		ОписаниеОшибки = "ОК";
	КонецЕсли;
	Результат = Ложь;
	Если ТипЗнч(Задача) = ТипСОМОбъект Тогда // Задача существует, поэтому правильнее всего получить описание из СЗиУО
		Попытка
			КодОшибки = Задача.КодПоследнейОшибки;
			ОписаниеОшибки = Задача.ОписаниеПоследнейОшибки;
			Результат = Истина;
		Исключение
			// Если возникло исключение, то это ошибка всегда
			Если КодОшибки <> 0 Тогда
				КодОшибки = 22019;
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	// Проверим получилось ли взять текстовое описание ошибки
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		Если КодОшибки = 0 Тогда
			ОписаниеОшибки = "ОК";
		Иначе
			ОписаниеОшибки = "Не удалось получить описание ошибки из СЗиУО";
		КонецЕсли;
	ИначеЕсли (ОписаниеОшибки = "ОК") И (КодОшибки <> 0) Тогда
		ОписаниеОшибки = "Не удалось получить описание ошибки из СЗиУО";
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Сообщает о выполнении действий с устройством. Используется в режиме отладки.
// 				Отладка включается установкой флага "РежимТрассировки" для устройства в его параметрах.
//				Функция используется для единообразия трассировки различных устройств
// Параметры:
//				ИмяОбработки - строка, синоним вызвавшей обработки
//				ФлагКоманды - булево, определяет является ли текущий вызов устройства командой или простым сообщением
//				Текст - строка, название команды или текст сообщения
//				КодОшибки - число, код ошибки
//				ОписаниеОшибки - текст ошибки
// Возвращает:
//				Код ошибки: 0 - если действие выполнено успешно, номер ошибки в противном случае
//
Функция СообщениеОтладки(	ИмяОбработки = "",
							ФлагКоманды = Ложь,
							Текст = "",
							КодОшибки = 0,
							ОписаниеОшибки = "ОК") Экспорт
	// Сообщим о выполненной команде
	ЗаписьЖурналаРегистрации(	"Оборудование.Отладка",
								УровеньЖурналаРегистрации.Информация,
								,
								,
								СокрЛП(ИмяОбработки) + " - " + СокрЛП(Текст) + ?(ФлагКоманды,": "+Формат(КодОшибки,"ЧН=0; ЧГ=0") +
								", " + СокрЛП(ОписаниеОшибки),""));
КонецФункции // СообщениеОтладки

//////////////////////////////////////////////////////////////////////////////
// БЛОК ИНИЦИАЛИЗАЦИИ ТОРГОВОГО ОБОРУДОВАНИЯ

// Выполняет запрос к системе управления оборудованием для получения
// списка существующих устройств для данного компьютера
// Заполняет таблицу "ТаблицаЭкземпляровОборудования" данными из СЗиУО
// а также для устройств, которые были найдены в справочнике оборудования
// базы текущего типового решения и заполняет поля
// Возвращает:
//  Признак успешного выполнения операции
//
Функция ПолучитьТаблицуУстройств(ТекстОшибки = "") Экспорт
	// Инициализация рабочих переменных	
	SafeArrayУстройств = Неопределено;
	Результат = Ложь;	// Мы пессимисты, начнем с утверждения что все плохо
	// Запросим список оборудования и сохраним его в таблицу "ТаблицаЭкземпляровОборудования"
	Попытка
		Состояние("Получение полного списка оборудования из СЗиУО...");
		SafeArrayУстройств = Рарус_Компонента.ПолучитьСписокОборудования(0); // 0 - нужна полная схема СЗиУО
		КоличествоСтрок = 0; КоличествоКолонок = 0; 
		SafeArrayУстройств.GetBounds(КоличествоСтрок,КоличествоКолонок);
		Если КоличествоКолонок < 12 Тогда
			ЗаписьЖурналаРегистрации("Оборудование.Статус",УровеньЖурналаРегистрации.Ошибка,,
				,"Ошибка при инициализации оборудования: не удалось получить таблицу устройств из-за ошибки:
					|Неверное число колонок в возвратной структуре метода ""ПолучитьСписокОборудования""");
		ИначеЕсли НЕ(КоличествоСтрок>0) Тогда
			ЗаписьЖурналаРегистрации("Оборудование.Статус",УровеньЖурналаРегистрации.Ошибка,,
				,"Ошибка при инициализации оборудования: не удалось получить таблицу устройств из-за ошибки:
					|Нет строк в возвратной структуре метода ""ПолучитьСписокОборудования""");
		Иначе
			ТаблицаЭкземпляровОборудования.Очистить();
			// Заполним таблицу
			Для Сч=0 По КоличествоСтрок-1 Цикл
				Состояние("Заполнено строк таблицы устройств: "+Сч);
				НоваяСтрока=ТаблицаЭкземпляровОборудования.Добавить();
				НоваяСтрока.КлассОборудования		= ВРЕГ(СокрЛП(SafeArrayУстройств.GetValue(Сч,0)));
				НоваяСтрока.НаименованиеКласса		= СокрЛП(SafeArrayУстройств.GetValue(Сч,1));
				НоваяСтрока.МодельОборудования		= ВРЕГ(СокрЛП(SafeArrayУстройств.GetValue(Сч,2)));
				НоваяСтрока.НаименованиеМодели		= СокрЛП(SafeArrayУстройств.GetValue(Сч,3));
				НоваяСтрока.Компьютер				= ВРЕГ(СокрЛП(SafeArrayУстройств.GetValue(Сч,4)));
				НоваяСтрока.НаименованиеУстройства	= СокрЛП(SafeArrayУстройств.GetValue(Сч,5));
				стрGUIDоборудования					= СокрЛП(SafeArrayУстройств.GetValue(Сч,6));
				Если ПустаяСтрока(стрGUIDоборудования)	Тогда
					НоваяСтрока.GUID				= "";
					НоваяСтрока.ИдGUID				= "";
				Иначе
					НоваяСтрока.GUID				= стрGUIDоборудования;
					НоваяСтрока.ИдGUID				= идGUID(стрGUIDоборудования);
					ОборудованиеСсылка			= Справочники.Оборудование.НайтиПоРеквизиту("ИдентификаторОборудования",стрGUIDоборудования);
					НоваяСтрока.ОборудованиеСсылка	= ОборудованиеСсылка;
					Если ОборудованиеСсылка.Пустая() Тогда
						НоваяСтрока.ТаймАут			= 90; // Таймаут оборудования по умолчанию
					Иначе
						ТаймаутОборудования			= ОборудованиеСсылка.Таймаут;
						НоваяСтрока.ТаймАут			= ?(ТаймаутОборудования > 4, ТаймаутОборудования, 90);
						НоваяСтрока.НаименованиеИзСправочника = ОборудованиеСсылка.Наименование;
					КонецЕсли;
				КонецЕсли;
				НоваяСтрока.GUIDМодели				= СокрЛП(SafeArrayУстройств.GetValue(Сч,9));
				НоваяСтрока.GUIDточкиДоступа		= СокрЛП(SafeArrayУстройств.GetValue(Сч,10));
				НоваяСтрока.ИмяВнешнейКомпоненты	= ВРЕГ(СокрЛП(SafeArrayУстройств.GetValue(Сч,11)));
				Попытка 
					НоваяСтрока.Требует1С				= Число(SafeArrayУстройств.GetValue(Сч,7));
					НоваяСтрока.Использовать			= Число(SafeArrayУстройств.GetValue(Сч,8));
					Если НоваяСтрока.Использовать = 1 Тогда
						НоваяСтрока.СтатусОборудования	= НЕОПРЕДЕЛЕНО;
					Иначе
						НоваяСтрока.СтатусОборудования	= Перечисления.СтатусыОборудования.НеИспользуется;
					КонецЕсли;
				Исключение
					ТекстОшибки = "ОШИБКА: некорректные данные в таблице устройств из СЗиУО, строка № "+(Сч+1)+
								" нечисловые значения для флагов ""1С"" и/или ""Использование""";
					НоваяСтрока.Требует1С				= 0;
					НоваяСтрока.Использовать			= 0;
					НоваяСтрока.СтатусОборудования		= Перечисления.СтатусыОборудования.НеИспользуется;
					// Сообщим в журнал регистрации
					ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокОборудования",
											УровеньЖурналаРегистрации.Ошибка,ЭтотОбъект.Метаданные(),,ТекстОшибки);
				КонецПопытки;
				НоваяСтрока.ТаблицаСвойств			= Новый ТаблицаЗначений;
			КонецЦикла;
			// Все закончилось успешно
			Результат=Истина; 
		КонецЕсли;
	Исключение
		ТекстОшибки=ОписаниеОшибки();
		ТекстОшибки=ПолучитьОписаниеОшибкиКомпоненты(ТекстОшибки);
		ТекстОшибки="Ошибка получения списка оборудования из СЗиУО - " + ТекстОшибки; 
		// Сообщим в журнал регистрации
		ЗаписьЖурналаРегистрации("Оборудование.ПолучитьТаблицуУстройств",УровеньЖурналаРегистрации.Ошибка,
								ЭтотОбъект.Метаданные(),,ТекстОшибки);
	КонецПопытки;
	Состояние("");
	// Очистим рабочую структуру
	SafeArrayУстройств = Неопределено;
	// Вернем результат работы
	Возврат Результат;
КонецФункции // ПолучитьТаблицуУстройств

// Сопоставляет GUID'у устройства его 1С-й обработчик
// Обновляет структуру "ЗагруженныеОбработчикиУстройств":
//   ключ - GUID экземпляра оборудования, значение - форма обработчик
// Возвращает:
//				Признак успешного выполнения операции
//
Функция ОбновитьТаблицуОбработчиков() Экспорт
	// Инициализация рабочих переменных	
	Результат = Ложь;
	ТекстОшибки = "";
	Состояние("Подключение обработчиков устройств...");
	// Переберем список оборудования по текущему компьютеру и обновим таблицу обработчиков
	Попытка
		Для Каждого СтрокаТаблицы Из ТаблицаЭкземпляровОборудования Цикл
			// Выберем из общей таблицы устройств те, управлять которыми придется нам самим
			GUID=СтрокаТаблицы.GUID;
			Если НЕ ПустаяСтрока(GUID)
				И (СтрокаТаблицы.Требует1С = 1)
				И (СтрокаТаблицы.Использовать = 1)
				И (СтрокаТаблицы.Компьютер = ИмяНашегоКомпьютера)
				И (СтрокаТаблицы.ИмяВнешнейКомпоненты = ИмяНашейКомпоненты)
				Тогда
				ИдGUID=СтрокаТаблицы.ИдGUID;
				ОчереднаяФормаОбработчик = Неопределено;
				Если НЕ ЗагруженныеОбработчикиУстройств.Свойство(ИдGUID, ОчереднаяФормаОбработчик) Тогда
					ИмяФормыОбработчика=СтрокаТаблицы.КлассОборудования+"_"+СтрокаТаблицы.МодельОборудования;
					Попытка 
						ОчереднаяФормаОбработчик=ПолучитьФорму(ИмяФормыОбработчика);
						// Проверяем получена ли форма обработчика устройства и вносим ее в список обработчиков
						Если ОчереднаяФормаОбработчик <> Неопределено Тогда
							ОчереднаяФормаОбработчик.GUID_Устройства=GUID; // GUID устройства - обязательный параметр формы
							ОчереднаяФормаОбработчик.НаименованиеУстройства=СтрокаТаблицы.НаименованиеУстройства; // НаименованиеУстройства - обязательный параметр формы
							ЗагруженныеОбработчикиУстройств.Вставить(ИдGUID,ОчереднаяФормаОбработчик);
						Иначе
							ТекстОшибки="Ошибка инициализации оборудования: Не найден обработчик для оборудования <"+
							ИмяФормыОбработчика+"> GUID="+GUID;
							// Сообщим в журнал регистрации
							ЗаписьЖурналаРегистрации(	"Оборудование.ОбновитьТаблицуОбработчиков",
							УровеньЖурналаРегистрации.Ошибка,
							ЭтотОбъект.Метаданные(),,ТекстОшибки);
						КонецЕсли;
					Исключение 
						// Некорректная реализация обработчика
						ТекстОшибки="Ошибка реализации формы-обработчика оборудования <"+ИмяФормыОбработчика+">";
						// Сообщим в журнал регистрации
						ЗаписьЖурналаРегистрации(	"Оборудование.ОбновитьТаблицуОбработчиков",
						УровеньЖурналаРегистрации.Ошибка,
						ЭтотОбъект.Метаданные(),,ТекстОшибки);
						ОчереднаяФормаОбработчик=Неопределено;
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Результат=Истина;
	Исключение
		ТекстОшибки=ОписаниеОшибки();
		// Сообщим в журнал регистрации
		ЗаписьЖурналаРегистрации("Оборудование.ОбновитьТаблицуОбработчиков",УровеньЖурналаРегистрации.Ошибка,
		ЭтотОбъект.Метаданные(),,ТекстОшибки);
	КонецПопытки;
	
	// Вернем признак успешности
	Возврат Результат; 
КонецФункции // ОбновитьТаблицуОбработчиков

// Инициализация обработчиков управления устройствами
// Возвращает:
//				Признак успешного выполнения операции
//
Функция Инициализация(стрGUIDустройства = "") Экспорт
	// Инициализация рабочих и ускоряющих переменных	
	Результат			= Ложь;
	ИмяНашейКомпоненты 	= ВРЕГ(Обработки.Защита.Создать().ИмяКомпоненты + ".dll");
	ИмяНашегоКомпьютера = ПараметрыСеанса.Компьютер.ИмяКомпьютера;
	Если ПустаяСтрока(ИмяНашегоКомпьютера) Тогда
		ИмяНашегоКомпьютера = ИмяКомпьютера();
	КонецЕсли;
	ИмяНашегоКомпьютера	= ВРЕГ(СокрЛП(ИмяНашегоКомпьютера));
	Попытка
		// Получим наш собственный GUID
		GUIDНашейТочкиДоступа = Рарус_Компонента.ПолучитьСвойGUID();
		// Полная переинициализация всего - очистим служебные структуры
		ЗагруженныеОбработчикиУстройств.Очистить();
		// Перечитаем табличку устройств
		Результат = ПолучитьТаблицуУстройств();
		// Обновим структуру обработчиков
		Если Результат Тогда 
			Результат = ОбновитьТаблицуОбработчиков();
		КонецЕсли;
		Оповестить("ОбновлениеСхемыСЗиУО", стрGUIDустройства, ЭтотОбъект);
	Исключение
		ТекстОшибки = "Ошибка при инициализации СЗиУО: " + ОписаниеОшибки(); 
		// Сообщим в журнал регистрации
		ЗаписьЖурналаРегистрации(	"Оборудование.Инициализация",
									УровеньЖурналаРегистрации.Ошибка,
									,
									,
									,
									ТекстОшибки);
	КонецПопытки;
	// Вернем признак успеха инициализации оборудования
	Возврат Результат;
КонецФункции // Инициализация

// Включение оборудования, зарегистрированного за данным компьютером
Процедура ВключитьТорговоеОборудованиеПредоставляемое() Экспорт
	
	// Проверим текущий режим работы с оборудованием
	СтрРежимРаботы = Лев(ПараметрыСеанса.РежимРаботы,3);
	Если (Лев(СтрРежимРаботы,1) <> "0") Тогда
		Возврат; // На данном рабочем месте не нужно включать оборудование автоматически при запуске
	КонецЕсли;
	Если (Сред(СтрРежимРаботы,2,1) = "2") Тогда
		Возврат; // Использование оборудования в текущей сессии 1С Предприятия полностью запрещено 
	КонецЕсли;
	Если (Сред(СтрРежимРаботы,3,1) <> "1") Тогда
		Возврат; // Оборудование обслуживается не в данной (текущей) сессии 1С Предприятия
	КонецЕсли;
	
	Состояние("Подключение предоставляемого оборудования...");
	// Выберем оборудование под управлением 1С, которое должно предоставляться именно нашей точкой доступа
	МассивУстройств =ТаблицаЭкземпляровОборудования.НайтиСтроки(Новый Структура("GUIDточкиДоступа,Использовать,Требует1С",
																GUIDНашейТочкиДоступа,1,1));
	Для Каждого Устройство ИЗ МассивУстройств Цикл
		GUID = Устройство.GUID;
		Если ПустаяСтрока(GUID) Тогда
			Продолжить; // Невозможная ошибка, но пропустим это т.к. ничего с ним сделать не сможем
		КонецЕсли;
		ОборудованиеСсылка = Справочники.Оборудование.НайтиПоРеквизиту("ИдентификаторОборудования",GUID);
		Если ОборудованиеСсылка.Пустая() ИЛИ ОборудованиеСсылка.ПометкаУдаления Тогда
			Продолжить; // Нет в справочнике - значит не наше устройство. И помеченное на удаление включать не будем тоже
		КонецЕсли;
		ИмяОборудования = СокрЛП(ОборудованиеСсылка.Наименование);
		// Проверим текущий статус
		Статус=СтатусОборудования(GUID);
		Если(Статус=НЕОПРЕДЕЛЕНО) Тогда
			Продолжить;
		ИначеЕсли (Статус=Перечисления.СтатусыОборудования.Готово) Тогда 
			Продолжить; // Уже включено да еще и монопольно на нас, ничего не трогаем
		ИначеЕсли (Статус=Перечисления.СтатусыОборудования.Включено) Тогда
			Продолжить;	// Уже включено, ничего не нужно делать
		ИначеЕсли (Статус=Перечисления.СтатусыОборудования.Выключено) Тогда
			// Включаем устройство (скорее всего оно удаленное и поэтому не запустилось нами ранее)
			ОписаниеОшибки="";
			Если ВключитьОборудование(GUID,ОписаниеОшибки) <> 0 Тогда
				// 
			КонецЕсли;
		Иначе 
			// При всех других статусах пытаться включить оборудование вообще бессмысленно
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ВключитьТорговоеОборудованиеПредоставляемое

// Включение оборудования, используемого нашим компьютером (в том числе и на других компьютерах)
Процедура ВключитьТорговоеОборудованиеИспользуемое() Экспорт
	
	Состояние("Подключение используемого оборудования...");
	
	// Пройдемся по списку реквизитов нашего компьютера и выберем из них используемое оборудование
	МетаданныеКомпьютера=ПараметрыСеанса.Компьютер.Метаданные();
	Для каждого РеквизитКомпьютера Из МетаданныеКомпьютера.Реквизиты Цикл
		// Проверим, оборудование ли это?
		Попытка
			Оборудование=ПараметрыСеанса.Компьютер[РеквизитКомпьютера.Имя];
		Исключение 
			Оборудование=Неопределено;
		КонецПопытки; 
		Если НЕ ЗначениеЗаполнено(Оборудование) ИЛИ (ТипЗнч(Оборудование)<>Тип("СправочникСсылка.Оборудование")) Тогда
			Продолжить; // Это не оборудование
		КонецЕсли;
		// Проверим текущий статус
		Статус=СтатусОборудования(Оборудование);
		Если(Статус=НЕОПРЕДЕЛЕНО) Тогда
			Продолжить;
		ИначеЕсли (Статус=Перечисления.СтатусыОборудования.Готово) Тогда 
			Продолжить; // Уже включено да еще и монопольно на нас, ничего не трогаем
		ИначеЕсли (Статус=Перечисления.СтатусыОборудования.Включено) Тогда
			Продолжить;	// Уже включено, ничего не нужно делать
		ИначеЕсли (Статус=Перечисления.СтатусыОборудования.Выключено) Тогда
			// Включаем устройство (скорее всего оно удаленное и поэтому не запустилось нами ранее)
			ОписаниеОшибки="";
			Если ВключитьОборудование(Оборудование,ОписаниеОшибки) <> 0 Тогда
				//
			КонецЕсли;
		Иначе 
			// При всех других статусах включать скорее всего бессмысленно, сообщим про это
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры // ВключитьТорговоеОборудованиеИспользуемое

// Выключает и деинициализирует все предоставляемое торговое оборудование, обслуживаемое 1С-обработчиками
Процедура ОтключитьТорговоеОборудование() Экспорт
	
	// Вторичные сессии не должны выключать оборудование, т.к. его могут использовать еще другие сеансы
	ФлагГлавногоСеанса = ЭтоГлавныйСеанс1С();
	// Переберем все формы-обработчики и отключим все виды оборудования
	Для Каждого Элемент Из ЗагруженныеОбработчикиУстройств Цикл
		Попытка // чтобы ошибка выключения кого-то одного не прервала цикл выключения всего остального
			GUID=Элемент.Значение.GUID_Устройства;
			Если ЗначениеЗаполнено(GUID) И ФлагГлавногоСеанса Тогда
				ВыключитьОборудование(GUID); // Ошибки выключения тут игнорируем
			КонецЕсли;
			// Очистим все возможные ссылки на объекты (включая локальные объекты-драйверы)
			Элемент.Значение.Задача            = Неопределено;
			Элемент.Значение.ВходныеПараметры  = Неопределено;
			Элемент.Значение.ПараметрыВозврата = Неопределено;
			Элемент.Значение.Устройство        = Неопределено;
		Исключение
		КонецПопытки;
	КонецЦикла;
	// Очистим формы-обработчики
	ЗагруженныеОбработчикиУстройств.Очистить();
	// Очистим ссылки на драйверы
	ЗагруженныеДрайверыУстройств.Очистить();
	// Просигналим системе оборудования что мы уже пошли на посадку и пусть нас не дергают ...
	Попытка
		Рарус_Компонента.ОтключениеСистемыТорговогоОборудования();
	Исключение
		// Если и произошло что плохое, тут нас это все равно уже не интересует
	КонецПопытки;
	
КонецПроцедуры // ОтключитьТорговоеОборудование

// Функция возвращает признак режима запрета работы с оборудованием
Функция РаботаСОборудованиемЗапрещена() Экспорт
	Результат = Ложь;
	СтрРежимРаботы = СокрЛП(ПараметрыСеанса.РежимРаботы);
	Если (Сред(ПараметрыСеанса.РежимРаботы,2,1) = "2") Тогда
		Результат = Истина;
	КонецЕсли;
	Возврат Результат;
КонецФункции // РаботаСОборудованиемЗапрещена	

// Функция возвращает признак основной сессии, Истина или Ложь
//Среди всех запущенных копий 1С предприятия только одна является
//основной сессией. Именно она обслуживает работу всего оборудования
//Только из основной сессии можно создавать и настраивать оборудование
Функция ЭтоГлавныйСеанс1С() Экспорт
	Результат = Ложь;
	СтрРежимРаботы = СокрЛП(ПараметрыСеанса.РежимРаботы);
	Если (Сред(СтрРежимРаботы,3,1) = "1") Тогда
		Результат = Истина; // Оборудование обслуживается в текущей (в этой вот самой) сессии 1С Предприятия
	КонецЕсли;
	Возврат Результат;
КонецФункции // ЭтоГлавныйСеанс1С

// Запрашивался режим работы без обслуживания оборудования
//В таком режиме возможно обращение к удаленному оборудованию
//но нет возможностей управления (создания и настройки) устройств
Функция РежимТолькоКлиент() Экспорт
	Результат = Ложь;
	СтрРежимРаботы = СокрЛП(ПараметрыСеанса.РежимРаботы);
	Если (Сред(СтрРежимРаботы,2,1) <> "0") ИЛИ (Сред(СтрРежимРаботы,3,1) <> "1") Тогда
		Результат = Истина; // Отказ от функций серверной сессии
	КонецЕсли;
	Возврат Результат;
КонецФункции // РежимТолькоКлиент

// Функция возвращает признак необходимости подключения всего
//оборудования, подключенного (настроенного) на данной рабочей станции
Функция РежимАвтоподключенияОборудования() Экспорт
	Результат = Ложь;
	СтрРежимРаботы = СокрЛП(ПараметрыСеанса.РежимРаботы);
	Если (Лев(ПараметрыСеанса.РежимРаботы,1)="0") Тогда
		Результат = Истина; // Все оборудование рабочей станции нужно сразу включать при входе
	КонецЕсли;
	Возврат Результат;
КонецФункции // РежимАвтоподключенияОборудования

////////////////////////////////////////////////////////////////////////////////
//// БЛОК РАБОТЫ С SMS ОБОРУДОВАНИЕМ

//Функция создает фильтр необходимый для работы с SMS
Функция СоздатьФильтр(	ТипСообщения,
						Статус,
						Направление,
						GUIDТочкиОтправления = "",
						ИдентификаторПользователя = "",
						Измененные = 0,
						Сессия = "",
						ВремяНачала = 0,
						ВремяКонца = 0,
						Устройство) Экспорт
	// Создадим структуру массива фильтра
	Фильтр = Рарус_Компонента.СоздатьПараметры(9,1);
	// GUID
	Фильтр.SetValue(0,0,"");
	// ТипСообщения 1 - факс, 2 - SMS, 3 - реплика, 4 - голосовое сообщение, 5 - e-mail, 0 -  неизвестно
	Фильтр.SetValue(1,0,ТипСообщения);
	// Статус сообщения 1 - очередь (ожидание отправки), 2 - отправка, 3 - отправлено, 4 - получено, 5 - ошибка,
	//0 - неизвестно
	Фильтр.SetValue(2,0,Статус);
	// Направление сообщения. 1 - исходящее, 2 - входящее, 0 - неизвестно
	Фильтр.SetValue(3,0,Направление);
	// GUID точки отправления
	Фильтр.SetValue(4,0,GUIDТочкиОтправления);
	// Идентификатор пользователя
	Фильтр.SetValue(5,0,ИдентификаторПользователя);
	// Сессия
	Фильтр.SetValue(6,0,Сессия);
	// Время начала
	Фильтр.SetValue(7,0,ВремяНачала);
	// Время конца
	Фильтр.SetValue(8,0,ВремяКонца);
	Возврат Фильтр
КонецФункции

// Функция получает смещение текущего времени относительно текущего времени оборудования в часах
Функция ПолучитьСмещениеВремениОборудования(Устройство)
	РарусКомпонента = Рарус_Компонента;
	СмещениеВремени = 0;
	Попытка 
		СтрGUID = Устройство.ИдентификаторОборудования;
		Таймаут = Устройство.Таймаут;
		СтрКоманда = "ТекущееВремяОборудования";
		Параметры = РарусКомпонента.СоздатьПараметры(1,1);
		мКодОшибки = РарусКомпонента.ЗаказатьВыполнениеДействияСинхронно(СтрGUID, СтрКоманда, Параметры, Таймаут);
		Если НЕ (мКодОшибки = Неопределено) И (мКодОшибки = 0) Тогда
			i = Неопределено;
			j = Неопределено;
			Параметры.GetBounds(i, j);
			ВремяОборудования = Параметры.GetValue(0,0);
			ТекущееВремя = ТекущаяДата();
			СмещениеВремени = Окр((ВремяОборудования - ТекущееВремя) / 3600, 0, 0) * 3600;
		КонецЕсли;	
	Исключение
	КонецПопытки;
	Возврат СмещениеВремени;
КонецФункции // ПолучитьСмещениеВремениОборудования()	

// Функция возвращает символьное представление разрешенного периода отправки
//
// Параметры:
//	НачалоПериода	- Дата	- Дата начала периода запрета отправки
//	КонецПериода	- Дата	- Дата конца периода запрета отправки
//
// Возвращаемое значение:
//	Строка	- Символьное представление разрешенного периода отправки
//
Функция ПолучитьРазрешенныйПериодОтправки(НачалоПериода, КонецПериода) Экспорт
	Если НачалоПериода = Дата('00010101') Тогда
		НачалоПериода = Дата('00010101') + 82800;
	Иначе
		НачалоПериода = НачалоПериода - 3600;
	КонецЕсли;	
	СимволНачала	= Символ(65 + Час(КонецПериода));
	СимволКонца		= Символ(65 + Час(НачалоПериода));
	СтрокаВозврата	= СимволНачала + СимволКонца;
	Если Найти("XWVUTSRQPONMLKJIHGFEDCBAX", СтрокаВозврата) > 0 Тогда
		СтрокаВозврата	= "";
	КонецЕсли;	
	Возврат СтрокаВозврата;
КонецФункции // кмПолучитьРазрешенныйПериодОтправки()	

/////////////////////////////////////////////////////////////////////////////////////////
//// Процедуры и функции работы с оборудование

//Функция возвращает все номера отправителей для указанного устройства
Функция СписокОтправителейОборудования(Устройство) Экспорт
	СписокОтправителей = Новый СписокЗначений;
	
	Если Устройство.Пустая() Тогда
		Возврат СписокОтправителей;
	КонецЕсли;
	
	ЕстьОшибки = ложь;
	мСтрокаСообщения = "";
	Попытка
		СтрКоманда = "ПолучитьСписокАдресовОтправителя";
		Параметры = Рарус_Компонента.СоздатьПараметры(0,0);
		СтрGUID = Устройство.ИдентификаторОборудования;
		Таймаут = Устройство.Таймаут;
		мКодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(СтрGUID, СтрКоманда, Параметры, Таймаут);
		
		// Различные проверки что у нас получилось 
		Если мКодОшибки<>0 И НЕ мКодОшибки = Неопределено  Тогда
			
			ЕстьОшибки = Истина; 
			
			Попытка
				ТекстОшибки=Рарус_Компонента.ОписаниеОшибки;	
			Исключение
				ТекстОшибки=" Возникло исключение при получении описания ошибки. Текст ошибки исключения :
				|"+ОписаниеОшибки();
			КонецПопытки;
			// СтатусСообщения.Внимание
			ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокАдресовОтправителя",
											УровеньЖурналаРегистрации.Ошибка,,,
											"Действие <"+СтрКоманда+"> вернуло результат: "+СокрЛП(мКодОшибки)+" Описание: "+ТекстОшибки);

		КонецЕсли;
		
		Если мКодОшибки = Неопределено Тогда
			ЕстьОшибки = Истина;
			ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокАдресовОтправителя",
											УровеньЖурналаРегистрации.Ошибка,,,
											"Действие <"+СтрКоманда+"> вернуло неизвестную ошибку.");
		КонецЕсли;
		
		//Если все хорошо то разбираем что получили
		Если мКодОшибки = 0 тогда
			КолЗап=0;
			Кол=0;
			Параметры.GetBounds(КолЗап, Кол);	
			Если (КолЗап>0) и (Кол>0) Тогда //если не пусто
				Парам = Параметры.GetValue(1,0);
				Парам.GetBounds(КолЗап, Кол);
				Если КолЗап > 0 Тогда
					Для к=0 по КолЗап-1 Цикл	
						СписокОтправителей.Добавить(Парам.GetValue(к,0));
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Исключение
		мСтрокаСообщения=мСтрокаСообщения+" Возникло исключение. Текст ошибки исключения : 
		|"+ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокАдресовОтправителя",
											УровеньЖурналаРегистрации.Ошибка,,,
											мСтрокаСообщения);
		ЕстьОшибки = Истина;
	КонецПопытки;
	Возврат СписокОтправителей;
КонецФункции

//Функция выполняет отправку SMS сообщения
Функция ОтправитьSMS(	Получатели,
						Сообщение,
						Отправитель,
						Оборудование,
						Отчет,
						Транслит,
						ОписаниеОшибки = "",
						СтруктураОтправки = Неопределено) Экспорт
						
	// Инициализация переменных
	СписокВозврата = Новый СписокЗначений;
	GUID_Оборудования = "";
	ТаймАут = "";
	НомОшибки = "";
	СтрОшибкаПроверки = "";
	ТекущийСтатус = "";
	
	// Выполняем проверку оборудования
	Если НЕ ОборудованиеКорректно(Оборудование,GUID_Оборудования,ТаймАут,НомОшибки,СтрОшибкаПроверки,ТекущийСтатус) Тогда
		// Что-то с устройством не так. Описание ошибки см. "ОписаниеОшибки"
		ОписаниеОшибки = СтрОшибкаПроверки;
		Возврат СписокВозврата;
	КонецЕсли;
	
	// Проверяем номер отправителя
	Если НЕ ЗначениеЗаполнено(Отправитель) ИЛИ НЕ (ТипЗнч(Отправитель) = Тип("Строка")) Тогда
		ОписаниеОшибки = "Неправильно указан номер отправителя!" ;
		Возврат СписокВозврата;
	КонецЕсли;
	
	// Проверяем получателей
	Если НЕ (ТипЗнч(Получатели) = Тип("СписокЗначений")) Тогда
		ОписаниеОшибки = "Неправильно указан параметр ПОЛУЧАТЕЛЬ!" ;
		Возврат СписокВозврата;
	КонецЕсли;
	
	// Проверяем отчет
	Если НЕ (ТипЗнч(Отчет) = Тип("Булево")) Тогда
		ОписаниеОшибки = "Неправильно указан параметр ОТЧЕТ!" ;
		Возврат СписокВозврата;
	КонецЕсли;
	
	// Проверяем транслит
	Если НЕ (ТипЗнч(Транслит) = Тип("Булево")) Тогда
		ОписаниеОшибки = "Неправильно указан параметр ТРАНСЛИТ!" ;
		Возврат СписокВозврата;
	КонецЕсли;
	
	// Начинаем обрабатывать текст
	Если Транслит Тогда
		Сообщение = Транслитерация(Сообщение);
	КонецЕсли;
	
	РарусКомпонента = Рарус_Компонента;
	
	Сообщение = СокрЛП(СтрЗаменить(Сообщение, Символы.ПС, " "));
	Сообщение = СтрЗаменить(Сообщение, Символы.Таб, " ");	
	Сообщение = СтрЗаменить(Сообщение, "№", "N");	
	
	Если ТипЗнч(Получатели) = Тип("СписокЗначений") Тогда
		СписокПолучателей = Получатели;
	ИначеЕсли ТипЗнч(Получатели) = Тип("Строка") Тогда
		СписокПолучателей = Новый СписокЗначений;
		СписокПолучателей.Добавить(Получатели);
	КонецЕсли;
	
	КоличествоПолучателей = СписокПолучателей.Количество();
	
	Попытка
		Получатели = РарусКомпонента.СоздатьПараметры(КоличествоПолучателей,2);
	Исключение
		
	КонецПопытки;
	I = 0;
	Для Каждого ТекТелефон из СписокПолучателей Цикл
		Телефон = ТекТелефон.Значение;
		Телефон = ОчиститьТелефонОтРазделителей(Телефон);
		ПервыйСимвол = Лев(Телефон,1);
		Если НЕ (ПервыйСимвол = "+") Тогда
			ЭтоФедеральныйНомер =  СтрДлина(Телефон) > 7;
			Если  ЭтоФедеральныйНомер И НЕ (ПервыйСимвол = "8") И НЕ (ПервыйСимвол = "0") Тогда
				Телефон = "+" + Телефон;
			КонецЕсли;	
			Если СокрЛП(Телефон) = "" Тогда
				ОписаниеОшибки = "В сообщении не выбран телефон получателя!";
				Продолжить;
			КонецЕсли;	
		КонецЕсли;
		Получатели.SetValue(I, 0, Телефон);
		
		СтрокаПодписок = "3;" + ?(Отчет, "5;", "") + "6";
		Получатели.SetValue(I, 1, СтрокаПодписок);
		I = I + 1;
	КонецЦикла;
	
	СмещениеВремени = ПолучитьСмещениеВремениОборудования(Оборудование);
	
	ЕстьОшибки = Ложь;
	мСтрокаСообщения = "";
	Попытка 
		Устройство = Оборудование;
		СтрGUID = Устройство.ИдентификаторОборудования; // уникальный номер устройства
		Таймаут = Устройство.Таймаут;
		СтрКоманда = "ОтправитьСообщение";
		Если СтруктураОтправки = Неопределено Тогда
			Данные = РарусКомпонента.СоздатьПараметры(1,1);
			Данные.SetValue(0,0,Сообщение);
		Иначе
			Данные = РарусКомпонента.СоздатьПараметры(4,1);
			Данные.SetValue(0,0,Сообщение);
			Данные.SetValue(1,0,СтруктураОтправки.ВремяНачалаОтправки + СмещениеВремени);
			Данные.SetValue(2,0,СтруктураОтправки.ВремяКонцаОтправки + СмещениеВремени);
			Если СтруктураОтправки.НеОтправлятьSMS Тогда
				Данные.SetValue(3,0,ПолучитьРазрешенныйПериодОтправки(СтруктураОтправки.НачалоПериодаЗапрета + 
								СмещениеВремени, СтруктураОтправки.КонецПериодаЗапрета + СмещениеВремени));
			Иначе	
				Данные.SetValue(3,0,"");
			КонецЕсли;
		КонецЕсли;
		
		Параметры = РарусКомпонента.СоздатьПараметры(8,1);
		Параметры.SetValue(0,0,2); // тип сообщения
		Параметры.SetValue(1,0,Строка(Отправитель)); //!!!номер отправителя
		Параметры.SetValue(2,0,Получатели);
		Параметры.SetValue(3,0,""); //отправитель
		Параметры.SetValue(4,0,Данные);
		Параметры.SetValue(5,0,2);
		
		Если Отчет Тогда
			Параметры.SetValue(6,0,1);	//Стоит подтверждение о доставке
		Иначе
			Параметры.SetValue(6,0,0);	//НЕ стоит подтверждение о доставке
		КонецЕсли;
		Параметры.SetValue(7,0,"");
		мКодОшибки = РарусКомпонента.ЗаказатьВыполнениеДействияСинхронно(СтрGUID, СтрКоманда, Параметры, Таймаут);
		
		// Различные проверки что у нас получилось 
		Если мКодОшибки = Неопределено Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = "Действие <" + СтрКоманда + "> вернуло неизвестную ошибку.";
		ИначеЕсли НЕ (мКодОшибки = 0) Тогда
			ЕстьОшибки = Истина; 
			Попытка
				ТекстОшибки = РарусКомпонента.ОписаниеОшибки;	
			Исключение
				ТекстОшибки = " Возникло исключение при получении описания ошибки. Текст ошибки исключения :
				|" + ОписаниеОшибки();
			КонецПопытки;
			ОписаниеОшибки = "Действие <" + СтрКоманда + "> вернуло результат: " + СокрЛП(мКодОшибки) + " Описание: " +
							ТекстОшибки;
		КонецЕсли;	
		i = Неопределено;
		j = Неопределено;
		Параметры.GetBounds(i, j);
		//нужно перебрать всех получателей
		Парам = Параметры.GetValue(2,0);//получаю массив получатель/гуид
		Для М = 0 По КоличествоПолучателей - 1 Цикл
			НомерПолучателя = Парам.GetValue(М, 0);
			ГУИДполучатель = Парам.GetValue(М, 2);
			СписокВозврата.Добавить(НомерПолучателя,ГУИДполучатель);
		КонецЦикла;
		
	Исключение
		мСтрокаСообщения = мСтрокаСообщения + " Возникло исключение. Текст ошибки исключения : 
		|" + ОписаниеОшибки();
		ОписаниеОшибки = мСтрокаСообщения; 
		ЕстьОшибки = Истина;
	КонецПопытки;
	
	Возврат СписокВозврата;	
	
КонецФункции

//Функция выполняет подписку на событие
//направление = 1 - исх, 2 - вход
Функция ЗаказатьПодписку(ГУИД, Устройство, Направление1 = 0, СтатусСообщения1 = 0, Измененные1 = 0) Экспорт
	
	// Объявляем переменные
	Перем Строка; // Строка
    Перем КолЗап; // Количество записей
    Перем Кол; // Количество колонок

	ОсновнаяСессия = ЭтоГлавныйСеанс1С();
	
	Если ОсновнаяСессия Тогда
		
		
		СтрКоманда = "ЗаказатьПодпискуНаСобытие";
		
		СтрGUID = Устройство.ИдентификаторОборудования;//	уникальный номер устройства
		Таймаут = Устройство.Таймаут;
		
		ЕстьОшибки = Ложь;
		
		мКодОшибки=0;
		мСтрокаСообщения="";
		Параметры = Неопределено; 
		
		// *** Начинаем работать с оборудованием ****
		Попытка
			Параметры = Рарус_Компонента.СоздатьПараметры(2,1);

			Фильтр = СоздатьФильтр(2,СтатусСообщения1,Направление1,"","",0,"",0,0,Устройство);
			
			Параметры.SetValue(0,0,GUIDНашейТочкиДоступа);
			Параметры.SetValue(1,0,Фильтр);
			
			мКодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(СтрGUID, СтрКоманда, Параметры, Таймаут);
			
			// Различные проверки что у нас получилось 
			Если мКодОшибки<>0 И НЕ мКодОшибки = Неопределено  Тогда
				
				ЕстьОшибки = Истина; 
				
				Попытка
					ТекстОшибки=Рарус_Компонента.ОписаниеОшибки;	
				Исключение
					ТекстОшибки=" Возникло исключение при получении описания ошибки. Текст ошибки исключения :
					|"+ОписаниеОшибки();
				КонецПопытки;
				// СтатусСообщения.Внимание
				ЗаписьЖурналаРегистрации("Оборудование.ЗаказатьПодпискуНаСобытие",УровеньЖурналаРегистрации.Ошибка,,
			                         ,"Действие <"+СтрКоманда+"> вернуло результат: "+СокрЛП(мКодОшибки)+" Описание: "+ТекстОшибки);
			КонецЕсли;
			
			Если мКодОшибки = Неопределено Тогда
				ЕстьОшибки = Истина;
				ЗаписьЖурналаРегистрации("Оборудование.ЗаказатьПодпискуНаСобытие",УровеньЖурналаРегистрации.Ошибка,,
			                         ,"Действие <"+СтрКоманда+"> вернуло неизвестную ошибку.");
			КонецЕсли;
			
			Если мКодОшибки = 0 Тогда
				Параметры.GetBounds(КолЗап, Кол);
				Если (КолЗап > 0)и(Кол>2) тогда
					Подписка = Параметры.GetValue(2,0);
				КонецЕсли
			КонецЕсли;
			
			
		Исключение
			мСтрокаСообщения=мСтрокаСообщения+" Возникло исключение. Текст ошибки исключения : 
			|"+ОписаниеОшибки();
			ЗаписьЖурналаРегистрации("Оборудование.ЗаказатьПодпискуНаСобытие",УровеньЖурналаРегистрации.Ошибка,,
			                         ,мСтрокаСообщения);
			ЕстьОшибки = Истина;
		КонецПопытки;
		Если ЕстьОшибки Тогда
			Возврат "";
		Иначе
			Возврат Подписка
		КонецЕсли;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

//Функция получает с оборудования список всех сообщений подходящих под фильтр
//Фильтр - фильтр по которому ищутся SMS
//Устройство - Устройство на котором ищутся SMS по заданному фильтру
Функция ПолучитьСписокСообщений(Фильтр, Устройство)Экспорт
	
	// Объявляем переменные
    Перем КолЗап;   // Количество записей
    Перем Кол;      // Количество колонок
    Перем Строка;   // Строка

	СтрКоманда = "ПолучитьСписокСообщений";
		
	СтрGUID = Устройство.ИдентификаторОборудования;//	уникальный номер устройства
	Таймаут = Устройство.Таймаут;
	
	ЕстьОшибки = Ложь;
	
	мКодОшибки=0;
	мСтрокаСообщения="";
	Параметры = Неопределено; 
	
	//Создаем таблицу значений для возврата
	СписокСМС = Новый ТаблицаЗначений;
	СписокСМС.Колонки.Добавить("Статус");
	СписокСМС.Колонки.Добавить("Отправитель_Получатель");
	СписокСМС.Колонки.Добавить("ТекстСообщения");
	СписокСМС.Колонки.Добавить("ПорядковыйНомерВСоставе"); //ID
	СписокСМС.Колонки.Добавить("Очередь"); //Сессия
	СписокСМС.Колонки.Добавить("Количество");//КоличествоСтраниц
	СписокСМС.Колонки.Добавить("Ошибка");
	СписокСМС.Колонки.Добавить("GUID");
	СписокСМС.Колонки.Добавить("Приоритет");
	СписокСМС.Колонки.Добавить("ДатаВремяЛогическогоЗавершения");
	СписокСМС.Колонки.Добавить("Обрабатывать");
	СписокСМС.Колонки.Добавить("Оборудование");

	Список = Новый ТаблицаЗначений;
	Список.Колонки.Добавить("Отправитель_Получатель");
	Список.Колонки.Добавить("ДатаВремяЛогическогоЗавершения");
	Список.Колонки.Добавить("ТекстСообщения");
	Список.Колонки.Добавить("СписокGUID");
	Список.Колонки.Добавить("КоличествоЧастей");

	// *** Начинаем работать с оборудованием ****
	Попытка
		
		Параметры=Рарус_Компонента.СоздатьПараметры(1,1);
		Параметры.SetValue(0,0,Фильтр);
	
		мКодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(СтрGUID, СтрКоманда, Параметры, Таймаут);
		
		// Различные проверки что у нас получилось 
		Если мКодОшибки<>0 И НЕ мКодОшибки = Неопределено  Тогда
			
			ЕстьОшибки = Истина; 
			
			Попытка
				ТекстОшибки=Рарус_Компонента.ОписаниеОшибки;	
			Исключение
				ТекстОшибки=" Возникло исключение при получении описания ошибки. Текст ошибки исключения :
				|"+ОписаниеОшибки();
			КонецПопытки;
			
			Если НЕ БылиОшибкиПодключениSMS Тогда
				// СтатусСообщения.Внимание
				ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокСообщений",УровеньЖурналаРегистрации.Ошибка,,
			         ,"Действие <"+СтрКоманда+"> вернуло результат: "+СокрЛП(мКодОшибки)+" Описание: "+ТекстОшибки);
				БылиОшибкиПодключениSMS = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если мКодОшибки = Неопределено Тогда
			ЕстьОшибки = Истина;
			Если НЕ БылиОшибкиПодключениSMS Тогда
				ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокСообщений",УровеньЖурналаРегистрации.Ошибка,,
			         ,"Действие <"+СтрКоманда+"> вернуло неизвестную ошибку.");
				БылиОшибкиПодключениSMS = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если мКодОшибки = 0 тогда
			БылиОшибкиПодключениSMS = Ложь;
			Параметры.GetBounds(КолЗап, Кол);
			Если (КолЗап > 0)и(Кол > 0) тогда
				//Если массив пустой, то все равно его размерность всегда [1,1], поэтому 
				//необходимо проверять первый элемент,
				//если он пустой, значит и массив пустой
				Парам = Параметры.GetValue(1,0);
				Парам.GetBounds(КолЗап, Кол);
				Если КолЗап > 0 Тогда
					GUID = 	Парам.GetValue(0,0);
					Если НЕ (GUID = Неопределено) Тогда	
						Для ш = 0 по КолЗап - 1 цикл
							НоваяСтрока = СписокСМС.Добавить();
							НоваяСтрока.Статус=Парам.GetValue(ш, 4);
							Если Парам.GetValue(ш, 5)=1 Тогда //если Исходящая то записываем получателя
								НоваяСтрока.Отправитель_Получатель = Парам.GetValue(ш, 15);
							ИначеЕсли Парам.GetValue(ш, 5)=2 Тогда //если Входящая
								НоваяСтрока.Отправитель_Получатель = Парам.GetValue(ш, 14)
							КонецЕсли;
							НоваяСтрока.ТекстСообщения = Парам.GetValue(ш, 13);
							НоваяСтрока.ПорядковыйНомерВСоставе = Парам.GetValue(ш, 21);//21
							НоваяСтрока.Очередь = Парам.GetValue(ш, 3);
							НоваяСТрока.Количество = Парам.GetValue(ш, 20);
							НоваяСТрока.Ошибка = Парам.GetValue(ш, 8);
							НоваяСтрока.GUID = Парам.GetValue(ш, 0);
							НоваяСтрока.Приоритет = Парам.GetValue(ш, 6);
							НоваяСТрока.ДатаВремяЛогическогоЗавершения = Парам.GetValue(ш, 10);
							НоваяСТрока.Обрабатывать = 1;
							НоваяСтрока.Оборудование = Устройство;
						КонецЦикла
					КонецЕсли;
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
		
	Исключение
		мСтрокаСообщения=мСтрокаСообщения+"Возникло исключение. Текст ошибки исключения : 
		|"+ОписаниеОшибки();
		Если НЕ БылиОшибкиПодключениSMS Тогда
			ЗаписьЖурналаРегистрации("Оборудование.ПолучитьСписокСообщений",УровеньЖурналаРегистрации.Ошибка,,
				, мСтрокаСообщения);
		КонецЕсли;
		ЕстьОшибки = Истина;
	КонецПопытки;
	
	Возврат СписокСМС;
КонецФункции	

//Функция удаляет сообщение с заданным GUID с указанного устройства
//ГУИД - GUID сообщения которое нужно удалить
//Устройство - оборудование с которого нужно удалять
Функция УдалитьСообщение(ГУИД, Устройство) Экспорт
	
	// Объявляем переменные
	Перем Строка; // Строка
    Перем Инд; // Индекс
    
	СтрКоманда = "УдалитьСообщение";
	
	СтрGUID = Устройство.ИдентификаторОборудования; // уникальный номер устройства
	Таймаут = Устройство.Таймаут;
	
	ЕстьОшибки = Ложь;
	
	мКодОшибки=0;
	мСтрокаСообщения="";
	Параметры = Неопределено; 
	
	// *** Начинаем работать с оборудованием ****
	Попытка
		
		Параметры = Рарус_Компонента.СоздатьПараметры(1,1);
	
		Параметры.SetValue(0,0,ГУИД);
		
		мКодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(СтрGUID, СтрКоманда, Параметры, Таймаут);
		
		// Различные проверки что у нас получилось 
		Если мКодОшибки<>0 И НЕ мКодОшибки = Неопределено  Тогда
			
			ЕстьОшибки = Истина; 
			
			Попытка
				ТекстОшибки=Рарус_Компонента.ОписаниеОшибки;	
			Исключение
				ТекстОшибки=" Возникло исключение при получении описания ошибки. Текст ошибки исключения :
				|"+ОписаниеОшибки();
			КонецПопытки;
			// СтатусСообщения.Внимание
			ЗаписьЖурналаРегистрации("Оборудование.УдалитьСообщение",УровеньЖурналаРегистрации.Ошибка, ,
				,"Действие <"+СтрКоманда+"> вернуло результат: "+СокрЛП(мКодОшибки)+" Описание: "+ТекстОшибки);
		КонецЕсли;
		
		Если мКодОшибки = Неопределено Тогда
			ЕстьОшибки = Истина;
			ЗаписьЖурналаРегистрации("Оборудование.УдалитьСообщение",УровеньЖурналаРегистрации.Ошибка, ,
				,"Действие <"+СтрКоманда+"> вернуло неизвестную ошибку.");
		КонецЕсли;
		
	Исключение
		мСтрокаСообщения=мСтрокаСообщения+" Возникло исключение. Текст ошибки исключения : 
		|"+ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("Оборудование.УдалитьСообщение",УровеньЖурналаРегистрации.Ошибка, ,
				,мСтрокаСообщения);
		ЕстьОшибки = Истина;
	КонецПопытки;
	Возврат мКодОшибки;
 
КонецФункции

 //Функция удаляет из переданной строки все символы кроме цифр 
Функция ОчиститьТелефонОтРазделителей(Телефон) Экспорт
		
	НовыйТелефон = "";
	а = 1;
	Пока а <= СтрДлина(Телефон) Цикл
		Симв =  Сред(Телефон, а, 1);
		Если (КодСимвола(Симв) >= 48 И КодСимвола(Симв) <= 57) Тогда
			НовыйТелефон = НовыйТелефон + Симв;
		КонецЕсли;
		а = а + 1;
	КонецЦикла;
	
	Возврат НовыйТелефон;
	
КонецФункции	

//Функция возвращает транслитерированный текст сообщения
Функция Транслитерация(Текст) Экспорт
	// Заполняем список соответствий
	СписокСоответствийТранслитерации = Новый СписокЗначений;
	СписокСоответствийТранслитерации.Добавить("й","y");
	СписокСоответствийТранслитерации.Добавить("ц","tz");
	СписокСоответствийТранслитерации.Добавить("у","u");
	СписокСоответствийТранслитерации.Добавить("к","k");
	СписокСоответствийТранслитерации.Добавить("е","e");
	СписокСоответствийТранслитерации.Добавить("н","n");
	СписокСоответствийТранслитерации.Добавить("г","g");
	СписокСоответствийТранслитерации.Добавить("ш","sh");
	СписокСоответствийТранслитерации.Добавить("щ","sch");
	СписокСоответствийТранслитерации.Добавить("з","z");
	СписокСоответствийТранслитерации.Добавить("х","h");
	СписокСоответствийТранслитерации.Добавить("ъ","'");
	СписокСоответствийТранслитерации.Добавить("ф","f");
	СписокСоответствийТранслитерации.Добавить("ы","yi");
	СписокСоответствийТранслитерации.Добавить("в","v");
	СписокСоответствийТранслитерации.Добавить("а","a");
	СписокСоответствийТранслитерации.Добавить("п","p");
	СписокСоответствийТранслитерации.Добавить("р","r");
	СписокСоответствийТранслитерации.Добавить("о","o");
	СписокСоответствийТранслитерации.Добавить("л","l");
	СписокСоответствийТранслитерации.Добавить("д","d");
	СписокСоответствийТранслитерации.Добавить("ж","zh");
	СписокСоответствийТранслитерации.Добавить("э","ye");
	СписокСоответствийТранслитерации.Добавить("я","ya");
	СписокСоответствийТранслитерации.Добавить("ч","ch");
	СписокСоответствийТранслитерации.Добавить("с","s");
	СписокСоответствийТранслитерации.Добавить("м","m");
	СписокСоответствийТранслитерации.Добавить("и","i");
	СписокСоответствийТранслитерации.Добавить("т","t");
	СписокСоответствийТранслитерации.Добавить("ь","'");
	СписокСоответствийТранслитерации.Добавить("б","b");
	СписокСоответствийТранслитерации.Добавить("ю","yu");
	СписокСоответствийТранслитерации.Добавить("ё","e");
	РезультирующийТекст = "";
	ДлинаТекста =  СтрДлина(Текст);
	Для Сч = 1 По  ДлинаТекста Цикл
		КодСимвола = КодСимвола(Текст,Сч);
		РусскаяЗаглавная = Ложь;
		Если  (КодСимвола >= 1040 И КодСимвола <= 1071) ИЛИ КодСимвола = 1025 Тогда
			РусскаяЗаглавная = Истина;
		КонецЕсли;	
		Символ =  НРег(Сред(Текст,Сч,1));
		Соответствие = СписокСоответствийТранслитерации.НайтиПоЗначению(Символ);
		Если НЕ (Соответствие = Неопределено) Тогда
			Символ = Соответствие.Представление;
			Если РусскаяЗаглавная Тогда
				ПервыйСимвол = Сред(Символ,1,1);
				Символ = СтрЗаменить(Символ,ПервыйСимвол,ВРег(ПервыйСимвол));
			КонецЕсли;
		КонецЕсли;	
		РезультирующийТекст = РезультирующийТекст + Символ;
	КонецЦикла;
	Возврат  РезультирующийТекст;
КонецФункции // Транслитерация()	

// Функция возвращает номер отправителя по умолчанию на указанном оборудовании. если это оборудование включено 
// то берем из кэша
Функция НомерОтправителяИзКеша(Оборудование) Экспорт
	Для каждого ТекСтрока из КешНомеровОтправителей Цикл
		Если ТекСтрока.Ключ = Оборудование.ИдентификаторОборудования Тогда
			Возврат ТекСтрока.Значение;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

//Вывод на экран ошибки работы оборудования с Федеральными законами
//
Процедура ПроинформироватьОбОтсутствииПоддержкиФЗ(Закон, Оборудование) Экспорт
	Фронт = Обработки.ФронтКассира.Создать();
	ТекстОшибок = "Устройство <"+ Оборудование + "> не поддерживает " +Закон +"-ФЗ";
	Фронт.ОткрытьДиалог(ТекстОшибок,СтатусСообщения.Важное,15);
КонецПроцедуры // РезультатыРаботыСОборудованием()

//Вывод на экран ошибки работы оборудования с Федеральными законами
//
Процедура ПроинформироватьОбОтсутствииПоддержкиДаннойКоманды(Оборудование) Экспорт
	Фронт = Обработки.ФронтКассира.Создать();
	ТекстОшибок = "Устройство <"+ Оборудование + "> не поддерживает данную команду";
	Фронт.ОткрытьДиалог(ТекстОшибок,СтатусСообщения.Важное,15);
КонецПроцедуры // РезультатыРаботыСОборудованием()

//////////////////////////////////////////////////////////////////////////////
// БЛОК ИНИЦИАЛИЗАЦИИ МОДУЛЯ ТОРГОВОГО ОБОРУДОВАНИЯ

// Служебные типы и квалификаторы
МассивСтрока = Новый Массив(1);
МассивСтрока[0] = Тип("Строка");
МассивЧисло = Новый Массив(1);
МассивЧисло[0] = Тип("Число");
МассивДата = Новый Массив(1);
МассивДата[0] = Тип("Дата");
ТипСОМОбъект = Тип("COMОбъект");
ОписаниеТиповСтрока = Новый ОписаниеТипов(МассивСтрока);
КвалификаторФлаг01 = Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный);
КвалификаторСчетчика = Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный);
ОписаниеТипаФлаг01 = Новый ОписаниеТипов("Число", КвалификаторФлаг01);
ОписаниеТипаСчетчик = Новый ОписаниеТипов("Число",КвалификаторСчетчика);
ОписаниеТипаОборудованиеСсылка = Новый ОписаниеТипов("СправочникСсылка.Оборудование");
ОписаниеТипаОборудованиеСтатус = Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОборудования");
ОписаниеТипаТаблицаЗначений = Новый ОписаниеТипов("ТаблицаЗначений");
ОписаниеТипаДата = Новый ОписаниеТипов(МассивДата);
// Таблица значений со списком зарегистрированных в системе устройств и их моделей
ТаблицаЭкземпляровОборудования = Новый ТаблицаЗначений();
ТаблицаЭкземпляровОборудования.Колонки.Добавить("КлассОборудования", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("НаименованиеКласса", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("МодельОборудования", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("НаименованиеМодели", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("Компьютер", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("НаименованиеУстройства", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("GUID", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ИдGUID", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("Требует1С", ОписаниеТипаФлаг01);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("Использовать", ОписаниеТипаФлаг01);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ОборудованиеСсылка", ОписаниеТипаОборудованиеСсылка);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("НаименованиеИзСправочника", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ТаймАут", ОписаниеТипаСчетчик);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("GUIDМодели", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("GUIDточкиДоступа", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ИмяВнешнейКомпоненты", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("СтатусОборудования", ОписаниеТипаОборудованиеСтатус);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ВремяПроверкиСтатуса", ОписаниеТипаДата);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ОписаниеСтатуса", ОписаниеТиповСтрока);
ТаблицаЭкземпляровОборудования.Колонки.Добавить("ТаблицаСвойств", ОписаниеТипаТаблицаЗначений);

// Структура с загруженными формами-обработчиками устройств, в ключе содержится GUID оборудования приведенный к
// корректному идентификатору, в значении - сама форма
ЗагруженныеОбработчикиУстройств = Новый Структура;

// Структура с загруженными COM-объектами драйверов устройств, в ключе содержится имя COM-объекта, приведенное к
// корректному идентификатору, в значении - сам COM-объект
ЗагруженныеДрайверыУстройств = Новый Структура;

// Строка с именами загруженных и подключенных внешних библиотек в виде ИмяФайла.dll
ИменаЗагруженныхВнешнихБиблиотек = ",";

// Идентификаторы нашей сессии
GUIDНашейТочкиДоступа = "";	// Заполняется при инициализации
ИмяНашейКомпоненты = "";		// Заполняется при инициализации
ИмяНашегоКомпьютера = "";		// Заполняется при инициализации
ОбработкаСофтФон = РарусСофтФонПроф;

// структура с полученными номерами отправителей из оборудования
КешНомеровОтправителей = Новый Соответствие;
// Локальная ссылка на внешнюю компоненту типового решения
Рарус_Компонента = глРарус_Компонента;

БылиОшибкиПодключениSMS = Ложь;
#КонецЕсли // Блок существует исключительно на стороне клиента
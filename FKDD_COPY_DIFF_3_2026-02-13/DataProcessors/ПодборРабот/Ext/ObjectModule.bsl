////////////////////////////////////////////////////////////////////////////////
// Модуль обработки подбора работ

Перем СтруктураПараметровПодбора; // Структура параметров подбора
Перем ТаблицаСвязанныхРабот Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// функция добавляет "Работу" в документ приёмник.
//
// Параметры:
//  ВыбСтрока    - Структура - содержит параметры добавляемого элемента,
//  ИмяКолонкиРабота - Строка - содержит имя колонки в которую добавляется элемент,
//  ТабличноеПоле - Строка - содержит имя табличного поля в которое добавляется элемент,
//  ФормаДокумента - Форма - содержит форму документа в который добавляется элемент.
//
// Возвращаемое значение:
//  СтрокаДокумента - СтрокаДокумента - Добавленная строка в документ.
//
Функция ДобавитьРаботуВДокумент(ВыбСтрока,ИмяКолонкиРабота,ТабличноеПоле,ФормаДокумента, ЗапрашиватьЦену = Ложь) Экспорт

    Работа = ВыбСтрока.Работа;
    
    КоличествоРабот = 0;
    Если ВыбСтрока.Свойство("КоличествоРабот") Тогда
        КоличествоРабот = ВыбСтрока.КоличествоРабот;
    КонецЕсли;
    
    ЦенаРаботы = 0;
    Если ВыбСтрока.Свойство("ЦенаРаботы") Тогда
        ЦенаРаботы = ВыбСтрока.ЦенаРаботы;    
	КонецЕсли;
	
	ВремяРаботы = 0;
    Если ВыбСтрока.Свойство("ВремяРаботы") Тогда
        ВремяРаботы = ВыбСтрока.ВремяРаботы;
	КонецЕсли;
	
	НормочасРаботы = обПраво("НормочасПоУмолчанию", глПрава);
    Если ВыбСтрока.Свойство("НормочасРаботы") Тогда
        НормочасРаботы = ВыбСтрока.НормочасРаботы;
	КонецЕсли;
    
    //Поищем такую строку в документе
    Если ИмяКолонкиРабота = "Авторабота" Тогда
        СтрокаДокумента=ФормаДокумента[ТабличноеПоле].Найти(Работа,"Авторабота");
    ИначеЕсли ИмяКолонкиРабота = "Номенклатура" Тогда
        СтрокаДокумента=ФормаДокумента[ТабличноеПоле].Найти(Работа,"Номенклатура");
    ИначеЕсли ИмяКолонкиРабота = "Работа" Тогда
        СтрокаДокумента=ФормаДокумента[ТабличноеПоле].Найти(Работа,"Работа");
    КонецЕсли;
    
	Если СтрокаДокумента=Неопределено Тогда
		СтрокаДокумента=ФормаДокумента[ТабличноеПоле].Добавить();
		СтрокаДокумента[ИмяКолонкиРабота]=Работа;
		
		ФормаДокумента.ОбработкаРеквизита(ТабличноеПоле + "." + ИмяКолонкиРабота, СтрокаДокумента, ФормаДокумента);
		Попытка
			СтрокаДокумента.ПоДокументуРеализации=Ложь;
			СтрокаДокумента.Подтверждение=Истина;
		Исключение
		КонецПопытки;
		Если НужныИсполнители(ФормаДокумента) Тогда
			СтрокаДокумента.ИдентификаторРаботы=Новый УникальныйИдентификатор;
		КонецЕсли;
		Если НуженРеквизитКоличество(ФормаДокумента) Тогда
			СтрокаДокумента.Количество=КоличествоРабот;
			СтрокаДокумента.Цена=ЦенаРаботы;
			СтрокаДокумента.Коэффициент = ВремяРаботы;
			Попытка
				СтрокаДокумента.Нормочас         = НормочасРаботы;
			Исключение
				СтрокаДокумента.ЕдиницаИзмерения = НормочасРаботы;
			КонецПопытки;
		КонецЕсли; 
		Если НужныИсполнители(ФормаДокумента) Тогда
			ФормаДокумента.ЭлементыФормы[ТабличноеПоле].ТекущаяСтрока=СтрокаДокумента;
			зфЗНРаботыПриАктивизацииСтроки(ФормаДокумента);
			зфЗНЗаполнениеИсполнителей(ФормаДокумента);	
		КонецЕсли;
		
	Иначе
		Если НуженРеквизитКоличество(ФормаДокумента) Тогда 
			СтрокаДокумента.Количество=СтрокаДокумента.Количество+КоличествоРабот;
			СтрокаДокумента.Цена = ЦенаРаботы;
			СтрокаДокумента.Коэффициент = ВремяРаботы;
			Попытка
				СтрокаДокумента.Нормочас         = НормочасРаботы;
			Исключение
				СтрокаДокумента.ЕдиницаИзмерения = НормочасРаботы;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ФормаДокумента.ДокументОбъект) = Тип("ДокументОбъект.ИзменениеЦенАвторабот") Тогда
		СтоимостьАвтоработы = ФормаДокумента.ПолучитьЦенуАвтоработы(СтрокаДокумента.Авторабота, СтрокаДокумента.Модель);
		СтрокаДокумента.Нормочас = СтоимостьАвтоработы.Нормочас;
		СтрокаДокумента.Цена    = СтоимостьАвтоработы.Цена;
	КонецЕсли;
	
	Если ЗапрашиватьЦену Тогда
		// Если нужно устанавливать "свою" цену.
		// Обработаем реквизит "Цена".
		РеквизитЦенаФормыДокумента = ТабличноеПоле+".Цена";
		ФормаДокумента.ОбработкаРеквизита(РеквизитЦенаФормыДокумента,СтрокаДокумента,ФормаДокумента);
	Иначе
		// Если не нужно устанавливать "свою" цену.
		// Обработаем реквизит "Работа".
		РеквизитФормыДокумента = ТабличноеПоле+"."+ИмяКолонкиРабота;
		ФормаДокумента.ОбработкаРеквизита(РеквизитФормыДокумента,СтрокаДокумента,ФормаДокумента);
	КонецЕсли;
	
	Попытка
		ФормаДокумента.ОбработкаРеквизита(ТабличноеПоле + ".РасчетРазницы",СтрокаДокумента,ФормаДокумента);
	Исключение
	КонецПопытки;
	
	Возврат СтрокаДокумента;
КонецФункции

// функция добавляет "Работу" в документ приёмник.
//
// Параметры:
//  ВыбСтрока    - Структура - содержит параметры добавляемого элемента,
//  ИмяКолонкиРабота - Строка - содержит имя колонки в которую добавляется элемент,
//  ТабличноеПоле - Строка - содержит имя табличного поля в которое добавляется элемент,
//  ФормаДокумента - Форма - содержит форму документа в который добавляется элемент.
//
// Возвращаемое значение:
//  СтрокаДокумента - СтрокаДокумента - Добавленная строка в документ.
//
Функция ДобавитьРаботуВПланирование(ВыбСтрока,ИмяКолонкиРабота,ТабличноеПоле,ФормаДокумента, ЗапрашиватьЦену = Ложь) Экспорт
	СтрокаДокумента=ФормаДокумента[ТабличноеПоле].Добавить();
    СтрокаДокумента[ИмяКолонкиРабота]=ВыбСтрока.Работа;
	Если ТабличноеПоле = "ПланированиеТЗ" Тогда
		СтрокаДокумента["ИдентификаторРаботы"] = Новый УникальныйИдентификатор;
	КонецЕсли;
	ФормаДокумента.ОбработкаРеквизита(ТабличноеПоле + "." + ИмяКолонкиРабота, СтрокаДокумента, ФормаДокумента);
	Возврат СтрокаДокумента;
КонецФункции

#КонецЕсли

// Процедура добавляет "Номенклатуру" в документ приёмник.
//
// Параметры:
//  СтрокаНоменклатуры - Структура - содержит параметры добавляемого элемента,
//  ФормаДокумента - Форма - содержит форму документа в который добавляется элемент.
//
Процедура ДобавитьНоменклатуруВДокумент(СтрокаНоменклатуры,ФормаДокумента) Экспорт
	
	Номенклатура = СтрокаНоменклатуры.Номенклатура;
	Количество = СтрокаНоменклатуры.Количество;
	
	//Используя подбор добавим номенклатуру
	ОбработкиПодборНоменклатуры=Обработки.ПодборНоменклатуры.Создать();
	
	Если СтруктураПараметровПодбора.Количество()=0 Тогда
		ЗаполнитьСтруктуруПараметровПодбора(ФормаДокумента);
	КонецЕсли;
	
	// Добавим номенклатуру в документ
	Если СтрокаНоменклатуры.Свойство("ХарактеристикаНоменклатуры") Тогда
		ХарактеристикаНоменклатуры = СтрокаНоменклатуры.ХарактеристикаНоменклатуры;
		ЕдиницаИзмерения           = Номенклатура.ОсновнаяЕдиницаИзмерения;
		Цена = обПолучитьЦену(ФормаДокумента.ТипЦен, Номенклатура,,,ФормаДокумента.ВалютаДокумента,,ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ФормаДокумента.ПодразделениеКомпании);
		СтруктураВыбСтрока = Новый Структура("Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры",
		Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры);
		
		Попытка
			ТабличнаяЧасть = ФормаДокумента.Товары;
			СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти","Товары");
			ИмяТабличнойЧасти = "Товары";
        Исключение
            Попытка // Док. Ввод в эксплуатацию (Заполнить по МОЛ), подбор идет не в документ, а в таблицу значений 
                    // на форму ФормаЗаполненияПоМОЛ, поэтому пытаемся получить таблицу значений (BazD)
                ТабличнаяЧасть = ФормаДокумента.СписокТоваров;
                СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти","СписокТоваров");
                ИмяТабличнойЧасти = "СписокТоваров";
            Исключение
                Возврат;
            КонецПопытки;
        КонецПопытки;
		
		СтруктураПараметровПодбора.Вставить("ИмяРеквизитаКоличества","Количество");
		СтруктураПараметровПодбора.Вставить("ИмяТабличногоПоля","Товары");
		СтруктураПараметровПодбора.Вставить("СпроситьКоличество", Ложь);
		СтруктураПараметровПодбора.Вставить("СпроситьЦену",       Ложь);
		СтруктураПараметровПодбора.Вставить("УстановитьТекущей",  Ложь);
		ОбработкиПодборНоменклатуры.ДобавитьСтрокуВДокумент(СтруктураВыбСтрока, СтруктураПараметровПодбора);
	Иначе
		Если НЕ Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор Тогда
			ОбработкиПодборНоменклатуры.ДобавитьНоменклатуру(Неопределено,СтруктураПараметровПодбора,Номенклатура,Ложь,Ложь,Количество,Ложь,Ложь);
		Иначе
			Для Каждого СтрокаСостава Из Номенклатура.СоставНабора Цикл
				Если НЕ СтрокаСостава.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор Тогда
					ОбработкиПодборНоменклатуры.ДобавитьНоменклатуру(Неопределено,СтруктураПараметровПодбора,СтрокаСостава.Номенклатура,Ложь,Ложь,Количество*СтрокаСостава.Количество,Ложь,Ложь);
				Иначе
					СтруктураНоменклатуры = Новый Структура("Номенклатура,Количество",СтрокаСостава.Номенклатура,Количество*СтрокаСостава.Количество);
					Если НЕ обЗначениеНеЗаполнено(СтрокаСостава.ХарактеристикаНоменклатуры) Тогда
						СтруктураНоменклатуры.Вставить("СтруктураНоменклатуры",СтрокаСостава.ХарактеристикаНоменклатуры);
					КонецЕсли;
					ДобавитьНоменклатуруВДокумент(СтруктураНоменклатуры,ФормаДокумента);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает значения "ТабличноеПоле" и "ИмяКолонкиРабота" в зависимости от типа документа.
//
// Параметры:
//  ФормаДокумента - Форма - содержит форму документа реквизиты которого нужно получить.
//
// Возвращаемое значение:
//  СтруктураРеквизитовФормы - Структура - структура реквизитов формы.
//
Функция ПолучитьРеквизитыФормыВладельца(ФормаДокумента,ИмяТабличногоПоля=Неопределено,ФлагДобавленияВПланирование=Неопределено) Экспорт
    СтруктураРеквизитовФормы = Новый Структура();
	Если ФлагДобавленияВпланирование <> Неопределено И ФлагДобавленияВпланирование И ИмяТабличногоПоля <> Неопределено Тогда
        ТабличноеПоле = ИмяТабличногоПоля;
        ИмяКолонкиРабота = "Авторабота";
	ИначеЕсли ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.ЗаявкаНаРемонт")
        ИЛИ ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.ЗаказНаряд")
        ИЛИ ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.АктРазногласий") 
		ИЛИ ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ОбработкаОбъект.АРМКалькуляция")
		ИЛИ ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		ТабличноеПоле = "Работы";
        ИмяКолонкиРабота = "Работа";
    ИначеЕсли ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.ИзменениеЦенАвторабот") Тогда
        ТабличноеПоле = "Автоработы";
        ИмяКолонкиРабота = "Авторабота";
    ИначеЕсли ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.СчетНаОплату") Тогда
        ТабличноеПоле = "Товары";
        ИмяКолонкиРабота = "Номенклатура";
    Иначе
        // не будем обрабатывать
        ТабличноеПоле = Неопределено;
        ИмяКолонкиРабота = Неопределено;
    КонецЕсли;
               
    СтруктураРеквизитовФормы.Вставить("ТабличноеПоле",ТабличноеПоле);
    СтруктураРеквизитовФормы.Вставить("ИмяКолонкиРабота",ИмяКолонкиРабота);
    Возврат СтруктураРеквизитовФормы;
    
КонецФункции

// Функция возвращает Истина, если в документе присутствуют проверяемые реквизиты, иначе Ложь.
//
// Параметры:
//  ВладелецФормы - Форма - содержит форму документа реквизиты которого нужно проверить на существование.
//
// Возвращаемое значение:
//  Булево - Истина если в документе имеются такие реквизиты, иначе Ложь.
//
Функция ЕстьПараметрыПриемника(ВладелецФормы) Экспорт 
	РеквизитыФормыВладельца = ПолучитьРеквизитыФормыВладельца(ВладелецФормы);
	ТабличноеПоле = Неопределено;
	ИмяКолонкиРабота = Неопределено;
	РеквизитыФормыВладельца.Свойство("ТабличноеПоле", ТабличноеПоле);
	РеквизитыФормыВладельца.Свойство("ИмяКолонкиРабота", ИмяКолонкиРабота);
	Если ТабличноеПоле=Неопределено И ИмяКолонкиРабота=Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

// Функция возвращает Истина, если для данного документа актуален реквизит "количество" количество, иначе Ложь.
//
// Параметры:
//  ВладелецФормы - Форма - содержит форму документа для которого
//	может потребоваться использование реквизита "Количество".
//
// Возвращаемое значение:
//  Булево - Истина если актуален реквизит "Количество", иначе Ложь.
//
Функция НуженРеквизитКоличество(ВладелецФормы) Экспорт 
	Если ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.ЗаявкаНаРемонт") 
		ИЛИ ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.ЗаказНаряд")
		ИЛИ ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.АктРазногласий")
		ИЛИ ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.СчетНаОплату")
		ИЛИ ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции

// Функция возвращает таблицу значений заполненную связанными работами.
//
// Параметры:
//  НачальноеЗначениеВыбора - СправочникСсылка.Автоработы - элемент справочника "Автоработы",
//  КоличествоРабот - Число - количество работ.
//
// Возвращаемое значение:
//  ТаблицаЗначений - содержит связанные работы.
//
Функция ЗаполнитьСвязанныеРаботы(НачальноеЗначениеВыбора,КоличествоРабот, ИсключатьНоменклатуру = Ложь, Модель, ВариантКомплектации, ИмяРегистра = "СвязанныеРаботы") Экспорт 
	ВыводитьКод = Истина; ВыводитьАрт = Ложь; ВыводитьРаз = Ложь;
	#Если Клиент Тогда
	РежимВыводаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,глПрава);
	ВыводитьКод = (РежимВыводаКода.Имя="КодИАртикул" ИЛИ РежимВыводаКода.Имя="Код");
	ВыводитьАрт = (РежимВыводаКода.Имя="КодИАртикул" ИЛИ РежимВыводаКода.Имя="Артикул");
	ВыводитьРаз = (ВыводитьАрт И ВыводитьКод);
	#КонецЕсли

	Запрос= Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+" КАК СвязаннаяРабота,
	|	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы,
	|	МАКСИМУМ(СвязанныеРаботы.Количество) * &КоличествоРабот КАК Количество,
	|	ВЫБОР
	|		КОГДА СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+" ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+" КАК Справочник.Номенклатура).ТипНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ТипНоменклатуры
	|ПОМЕСТИТЬ ТаблицаСвязанныхРабот
	|ИЗ
	|	РегистрСведений."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязанныеРаботы")+" КАК СвязанныеРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|		ПО (СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+" ССЫЛКА Справочник.Номенклатура)
	|			И СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+".Артикул = ГруппыАналогов.Артикул
	|ГДЕ
	|	СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "Номенклатура", "Авторабота")+" = &Авторабота
	|
	|СГРУППИРОВАТЬ ПО
	|	СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+",
	|	ГруппыАналогов.ИдентификаторГруппы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторГруппы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСвязанныхРабот.СвязаннаяРабота КАК СвязаннаяРабота,
	|	ТаблицаСвязанныхРабот.Количество КАК Количество,
	|	ТаблицаСвязанныхРабот.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ГруппыАналогов.Артикул КАК Артикул
	|ПОМЕСТИТЬ СвязаннаяРаботаАналоги
	|ИЗ
	|	ТаблицаСвязанныхРабот КАК ТаблицаСвязанныхРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|		ПО ТаблицаСвязанныхРабот.ИдентификаторГруппы = ГруппыАналогов.ИдентификаторГруппы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаСвязанныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязаннаяРаботаАналоги.СвязаннаяРабота КАК СвязаннаяРабота,
	|	ВЫБОР
	|		КОГДА НЕ Номенклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА Номенклатура.Ссылка
	|		КОГДА СвязаннаяРаботаАналоги.СвязаннаяРабота ССЫЛКА Справочник.Номенклатура
	|			ТОГДА СвязаннаяРаботаАналоги.СвязаннаяРабота
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Номенклатура,
	|	СвязаннаяРаботаАналоги.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА Номенклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА Номенклатура.Ссылка = СвязаннаяРаботаАналоги.СвязаннаяРабота
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоАналог,
	|	ВЫБОР
	|		КОГДА Номенклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА СвязаннаяРаботаАналоги.ТипНоменклатуры
	|		ИНАЧЕ Номенклатура.ТипНоменклатуры
	|	КОНЕЦ КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА Номенклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА СвязаннаяРаботаАналоги.ТипНоменклатуры.ИспользованиеХарактеристик
	|		ИНАЧЕ Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик
	|	КОНЕЦ КАК ИспользованиеХарактеристик
	|ПОМЕСТИТЬ СвязанныеРаботыНоменклатура
	|ИЗ
	|	СвязаннаяРаботаАналоги КАК СвязаннаяРаботаАналоги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО СвязаннаяРаботаАналоги.Артикул = Номенклатура.Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СвязаннаяРаботаАналоги
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота КАК СвязаннаяРабота,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.Наименование КАК СвязаннаяРаботаНаименование,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.НаименованиеПолное КАК СвязаннаяРаботаПредставление,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.Код КАК СвязаннаяРаботаКод,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.Артикул КАК СвязаннаяРаботаАртикул,
	|	СвязанныеРаботыНоменклатура.Номенклатура КАК Аналог,
	|	СвязанныеРаботыНоменклатура.Номенклатура.Наименование КАК АналогНаименование,
	|	СвязанныеРаботыНоменклатура.Номенклатура.НаименованиеПолное КАК АналогПредставление,
	|	СвязанныеРаботыНоменклатура.Номенклатура.Артикул КАК АналогАртикул,
	|	СвязанныеРаботыНоменклатура.Номенклатура.Код КАК АналогКод,
	|	СвязанныеРаботыНоменклатура.ЭтоАналог КАК ЭтоАналог,
	|	ХарактеристикиНоменклатуры.Ссылка КАК ХарактеристикаНоменклатуры,
	|	ХарактеристикиНоменклатуры.Наименование КАК ХарактеристикаНоменклатурыНаименование,
	|	СвязанныеРаботыНоменклатура.Количество КАК Количество
	|ИЗ
	|	СвязанныеРаботыНоменклатура КАК СвязанныеРаботыНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО (СвязанныеРаботыНоменклатура.ИспользованиеХарактеристик = 1)
	|			И СвязанныеРаботыНоменклатура.ТипНоменклатуры = ХарактеристикиНоменклатуры.Владелец
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.Наименование,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.НаименованиеПолное,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.Код,
	|	СвязанныеРаботыНоменклатура.СвязаннаяРабота.Артикул,
	|	СвязанныеРаботыНоменклатура.Номенклатура,
	|	СвязанныеРаботыНоменклатура.Номенклатура.Наименование,
	|	СвязанныеРаботыНоменклатура.Номенклатура.НаименованиеПолное,
	|	СвязанныеРаботыНоменклатура.Номенклатура.Артикул,
	|	СвязанныеРаботыНоменклатура.Номенклатура.Код,
	|	СвязанныеРаботыНоменклатура.ЭтоАналог,
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	ХарактеристикиНоменклатуры.Наименование,
	|	СвязанныеРаботыНоменклатура.Количество
	|ИЗ
	|	СвязанныеРаботыНоменклатура КАК СвязанныеРаботыНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО (СвязанныеРаботыНоменклатура.ИспользованиеХарактеристик = 2)
	|			И СвязанныеРаботыНоменклатура.Номенклатура = ХарактеристикиНоменклатуры.Владелец
	|УПОРЯДОЧИТЬ ПО
	|	СвязаннаяРаботаНаименование УБЫВ,
	|	ЭтоАналог,
	|	АналогНаименование УБЫВ,
	|	ХарактеристикаНоменклатурыНаименование УБЫВ
	|ИТОГИ
	|	""<""+" + ?(ВыводитьКод, "СвязаннаяРаботаКод", "") + ?(ВыводитьРаз, "+ "" / "" +", "") + ?(ВыводитьАрт, "СвязаннаяРаботаАртикул", "" ) + "+""> "" + СвязаннаяРаботаПредставление КАК СвязаннаяРаботаПредставление,
	|	""<""+" + ?(ВыводитьКод, "АналогКод", "") + ?(ВыводитьРаз, "+ "" / "" +", "") + ?(ВыводитьАрт, "АналогАртикул", "" ) + "+""> "" + АналогПредставление КАК АналогПредставление,
	|	МАКСИМУМ(ЭтоАналог),
	|	МАКСИМУМ(Количество)
	|ПО
	|	СвязаннаяРабота,
	|	Аналог";
				    
	Запрос.УстановитьПараметр("Модель", Модель);
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("Авторабота", НачальноеЗначениеВыбора);
	Запрос.УстановитьПараметр("КоличествоРабот", ?(КоличествоРабот=0 ИЛИ КоличествоРабот = Неопределено, 1, КоличествоРабот));
	// посмотрим нужноли выбирать номенклатуру
	Если ИсключатьНоменклатуру Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"//ОтборСвязаннойНоменклатуры", " И НЕ (СвязанныеРаботы.СвязаннаяРабота ССЫЛКА Справочник.Номенклатура)");
	КонецЕсли;
	
	Результат = Новый ДеревоЗначений;
	//Результат.Добавить("Модель");
	//Результат.Добавить("ВариантКомплектации");
	Результат.Колонки.Добавить("Объект");
	Результат.Колонки.Добавить("Представление");
	
	Результат.Колонки.Добавить("ЭтоАналог", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ЭтоХарактеристика", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВидОбъекта");
	Результат.Колонки.Добавить("Количество");
	
	 ВыборкаРабот = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	 Пока ВыборкаРабот.Следующий() Цикл
		
		СтрокаРабот = Результат.Строки.Добавить();
		СтрокаРабот.Объект        = ВыборкаРабот.СвязаннаяРабота;
		СтрокаРабот.Представление = ВыборкаРабот.СвязаннаяРаботаПредставление;
		СтрокаРабот.Количество    = ВыборкаРабот.Количество;
		СтрокаРабот.ВидОбъекта    = 1;
		
		ВыборкаАналогов = ВыборкаРабот.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаАналогов.Следующий() Цикл
			
			Выборка = ВыборкаАналогов.Выбрать(ОбходРезультатаЗапроса.Прямой);
			РодительХарактеристик = СтрокаРабот;
			Если Не ВыборкаАналогов.Аналог = Null И ВыборкаАналогов.ЭтоАналог Тогда
				СтрокаРабот.ЭтоАналог        = Истина;
				СтрокаАналогов = СтрокаРабот.Строки.Добавить();
				СтрокаАналогов.Объект        = ВыборкаАналогов.Аналог;
				СтрокаАналогов.Представление = ВыборкаАналогов.АналогПредставление;
				СтрокаАналогов.ЭтоАналог     = Истина;
				СтрокаАналогов.Количество    = ВыборкаАналогов.Количество;
				СтрокаАналогов.ВидОбъекта    = 2;
				РодительХарактеристик = СтрокаАналогов;
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				Если Выборка.ХарактеристикаНоменклатуры = Null Тогда
					Продолжить;
				КонецЕсли;
				Если НЕ РодительХарактеристик.ЭтоАналог Тогда
					СтрокаРабот.ЭтоХарактеристика        = Истина;
				КонецЕсли;
				СтрокаХарактеристик = РодительХарактеристик.Строки.Добавить();
				СтрокаХарактеристик.Объект            = Выборка.ХарактеристикаНоменклатуры;
				СтрокаХарактеристик.Представление     = Выборка.ХарактеристикаНоменклатурыНаименование;
				СтрокаХарактеристик.ЭтоАналог         = Выборка.ЭтоАналог;
				СтрокаХарактеристик.Количество        = Выборка.Количество;
				СтрокаХарактеристик.ВидОбъекта        = 3;
				СтрокаХарактеристик.ЭтоХарактеристика = Истина;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СписокСвязанныхРабот = Новый СписокЗначений;
	Для Каждого ТекСтрока Из Результат.Строки Цикл
		СписокСвязанныхРабот.Добавить(ТекСтрока);
	КонецЦикла;
	
	//получаем таблицу связанных работ в разрезе моделей и вариантов комплектации
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СвязанныеРаботы.Модель КАК Модель,
	               |	СвязанныеРаботы.ВариантКомплектации,
	               |	СвязанныеРаботы.Количество * &КоличествоРабот КАК Количество,
	               |	СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+" КАК СвязаннаяРабота
	               |ИЗ
	               |	РегистрСведений."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязанныеРаботы")+" КАК СвязанныеРаботы
	               |ГДЕ
	               |	СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "Номенклатура", "Авторабота")+" = &Авторабота
	               |	И СвязанныеРаботы."+?(ИмяРегистра = "СвязаннаяНоменклатура", "СвязаннаяНоменклатура", "СвязаннаяРабота")+" В (&СвязаннаяРабота)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Модель";
	Запрос.УстановитьПараметр("Авторабота", НачальноеЗначениеВыбора);
	Запрос.УстановитьПараметр("СвязаннаяРабота",СписокСвязанныхРабот); 
	Запрос.УстановитьПараметр("КоличествоРабот", ?(КоличествоРабот=0 ИЛИ КоличествоРабот = Неопределено, 1, КоличествоРабот));
	
	ТаблицаСвязанныхРабот = Запрос.Выполнить().Выгрузить();
	
	ЗаполнитьКоличествоСвязанныеРаботы(Результат, Модель, ВариантКомплектации, 1);
	
	Возврат Результат;
	
КонецФункции

// Заполнения таблицы значений модели
Функция ПолучитьТаблицуМоделей(ТаблицаМоделейИВариантовКомплектаций,СвязаннаяРабота, ВидОбъекта,ОтборМодель,ОтборВариантКомплектации) Экспорт
	
	ТаблицаМоделейИВариантовКомплектаций.Очистить();
	Если  ВидОбъекта = 4 ИЛИ ВидОбъекта = 5 Тогда
		Возврат ТаблицаМоделейИВариантовКомплектаций;
	КонецЕсли;
	ТаблицаСвязаннойРаботыМоделей = ПолучитСтрокиТаблицыСвязанныхРабот(СвязаннаяРабота, ОтборМодель, ОтборВариантКомплектации, ложь);
	Для Н = 0 По ТаблицаСвязаннойРаботыМоделей.Количество() - 1 Цикл
		Если ТаблицаСвязаннойРаботыМоделей[Н].Модель <> "" Тогда
			ЗаписьМоделиИВариантаКомплектации = ТаблицаМоделейИВариантовКомплектаций.Добавить();
			ЗаписьМоделиИВариантаКомплектации.СвязаннаяРабота = ТаблицаСвязаннойРаботыМоделей[Н].СвязаннаяРабота;
			ЗаписьМоделиИВариантаКомплектации.Модель = ТаблицаСвязаннойРаботыМоделей[Н].Модель;
			ЗаписьМоделиИВариантаКомплектации.ВариантКомплектации = ТаблицаСвязаннойРаботыМоделей[Н].ВариантКомплектации;
			ЗаписьМоделиИВариантаКомплектации.Количество = ТаблицаСвязаннойРаботыМоделей[Н].Количество;
		КонецЕсли;
	КонецЦикла;
КонецФункции

// Функция возвращает таблицу значений заполненную работами по виду ремонта (работы по умолчанию).
//
// Параметры:
//  НачальноеЗначениеВыбора - СправочникСсылка.Автоработы - элемент справочника "Автоработы",
//  КоличествоРабот - Число - количество работ.
//
// Возвращаемое значение:
//  ТаблицаЗначений - содержит связанные работы.
//
Функция ЗаполнитьРаботыПоУмолчанию(ВидРемонта) Экспорт 
	ВыводитьКод = Истина; ВыводитьАрт = Ложь; ВыводитьРаз = Ложь;
	#Если Клиент Тогда
	РежимВыводаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,глПрава);
	ВыводитьКод = (РежимВыводаКода.Имя="КодИАртикул" ИЛИ РежимВыводаКода.Имя="Код");
	ВыводитьАрт = (РежимВыводаКода.Имя="КодИАртикул" ИЛИ РежимВыводаКода.Имя="Артикул");
	ВыводитьРаз = (ВыводитьАрт И ВыводитьКод);
	#КонецЕсли

	Запрос= Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыРемонтаРаботы.Авторабота КАК Авторабота,
	               |	ВидыРемонтаРаботы.Авторабота.Наименование КАК АвтоработаНаименование,
	               |	ВидыРемонтаРаботы.Авторабота.НаименованиеПолное КАК АвтоработаПредставление,
	               |	ВидыРемонтаРаботы.Авторабота.Код КАК АвтоработаКод,
	               |	ВидыРемонтаРаботы.Авторабота.Артикул КАК АвтоработаАртикул,
	               |	ВидыРемонтаРаботы.Количество КАК Количество
	               |ИЗ
	               |	Справочник.ВидыРемонта.Работы КАК ВидыРемонтаРаботы
	               |ГДЕ
	               |	ВидыРемонтаРаботы.Ссылка = &ВидРемонта";

	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	
	Результат = Новый ДеревоЗначений;
	Результат.Колонки.Добавить("Объект");
	Результат.Колонки.Добавить("Представление");
	Результат.Колонки.Добавить("ЭтоАналог", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ЭтоХарактеристика", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВидОбъекта");
	Результат.Колонки.Добавить("Количество");
	
	ВыборкаРабот = Запрос.Выполнить().Выбрать();
	Пока ВыборкаРабот.Следующий() Цикл
		
		СтрокаРабот = Результат.Строки.Добавить();
		СтрокаРабот.Объект        	  = ВыборкаРабот.Авторабота;
		СтрокаРабот.Представление 	  = ВыборкаРабот.АвтоработаПредставление;
		СтрокаРабот.ЭтоАналог         = Ложь;
		СтрокаРабот.ЭтоХарактеристика = Ложь;
		СтрокаРабот.ВидОбъекта   	  = 1;
		СтрокаРабот.Количество   	  = ВыборкаРабот.Количество;
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьКоличествоСвязанныеРаботы(ДеревоСвязанныхРабот, Модель, ВариантКомплектации, Индетификатор) Экспорт
	СтрокиСНулевымКоличеством = Новый Массив;
	Для Каждого ТекСтрокаРаботы Из ДеревоСвязанныхРабот.Строки Цикл
		Если Индетификатор = 1 Тогда
			ТекСтрокаРаботы.Количество = ПолучитСтрокиТаблицыСвязанныхРабот(ТекСтрокаРаботы.Объект, Модель, ВариантКомплектации, истина)[0].Количество;
		Иначе
			Если ТипЗнч(ТаблицаСвязанныхРабот) <> Тип("Неопределено") Тогда
				ТекСтрокаРаботы.Количество = ПолучитСтрокиТаблицыСвязанныхРабот(ТекСтрокаРаботы.СвязаннаяРабота, Модель, ВариантКомплектации, истина)[0].Количество;
				Если ТекСтрокаРаботы.Количество = 0 Тогда
					СтрокиСНулевымКоличеством.Добавить(ТекСтрокаРаботы);
					Продолжить;
				КонецЕсли;
				Для Каждого СтрокаАналогов Из ТекСтрокаРаботы.Строки Цикл
					СтрокаАналогов.Количество = ТекСтрокаРаботы.Количество;
					Для Каждого СтрокаХарактеристик Из СтрокаАналогов.Строки Цикл
						СтрокаХарактеристик.Количество = ТекСтрокаРаботы.Количество;
						Для Каждого СтрокаРазлиныхХарактеристик  Из СтрокаХарактеристик.Строки Цикл
							СтрокаРазлиныхХарактеристик.Количество = ТекСтрокаРаботы.Количество;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	// Удалим из дерева строки с пустым количеством
	Для Каждого ТекСтрокаРаботы Из СтрокиСНулевымКоличеством Цикл
		ДеревоСвязанныхРабот.Строки.Удалить(ТекСтрокаРаботы);
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция возвращает Истину, если для данного документа требуются исполнители работ, иначе Ложь.
//
// Параметры:
//  ВладелецФормы - Форма - содержит форму документа для которого могут потребоваться исполнители работ.
//
// Возвращаемое значение:
//  ТаблицаЗначений - содержит связанные работы.
//
Функция НужныИсполнители(ВладелецФормы)
    Возврат ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.ЗаявкаНаРемонт") 
		ИЛИ ТипЗнч(ВладелецФормы.ЭтотОбъект) = Тип("ДокументОбъект.ЗаказНаряд");
КонецФункции

// Заполняет структуру параметров подбора
//
// Параметры:
//
// ФормаДокумента - Форма - Форма документа владельца подбора.
//
Процедура ЗаполнитьСтруктуруПараметровПодбора(ФормаДокумента)
	СтруктураПараметровПодбора = Новый Структура;
	СписокВидовНоменклатуры=Новый СписокЗначений;
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Товар);
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Комплект);
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Тара);
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Набор);
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Шины);
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Диски);
	СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.НомерныеАгрегаты);
	СтруктураПараметровПодбора.Вставить("ВидыНоменклатуры",СписокВидовНоменклатуры);
	СтруктураПараметровПодбора.Вставить("ОбработкаВДокументе",Ложь);
	Если ФормаДокумента.ЭтотОбъект.Метаданные() = Метаданные.Обработки.АРМКалькуляция Тогда
		СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти","ЗапчастиКорзина");
		СтруктураПараметровПодбора.Вставить("ИмяТабличногоПоля","ЗапчастиКорзина");
	ИначеЕсли ФормаДокумента.ЭтотОбъект.Метаданные() = Метаданные.Документы.ЗаказНаряд Тогда
		СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти","Товары");
		СтруктураПараметровПодбора.Вставить("ИмяТабличногоПоля","Товары");
	КонецЕсли;
	СтруктураПараметровПодбора.Вставить("ФормаДокумента",ФормаДокумента);
	СтруктураПараметровПодбора.Вставить("ТипЦен",ФормаДокумента.ТипЦен);
	СтруктураПараметровПодбора.Свойство("ИмяРеквизитаКоличества","Количество");
	СтруктураПараметровПодбора.Вставить("ОткрытаФормаКоличестваГруппы",Ложь);
	СтруктураПараметровПодбора.Вставить("ОткрытаФормаКоличестваСписка",Ложь);
КонецПроцедуры

//Функция получить записи отбора по таблице ТаблицаСвязанныхРабот
Функция ПолучитСтрокиТаблицыСвязанныхРабот(СвязаннаяРабота, ОтборМодель, ОтборВариантКомплектации, ПризнакПолученияКоличестваСвязаннойРаботы)
	ЗаписьМоделей = Новый ТаблицаЗначений;
		
	ЗаписьМоделей.Колонки.Добавить("СвязаннаяРабота");
	ЗаписьМоделей.Колонки.Добавить("Модель");
	ЗаписьМоделей.Колонки.Добавить("ВариантКомплектации");
	ЗаписьМоделей.Колонки.Добавить("Количество");
	
	Если ТипЗнч(ТаблицаСвязанныхРабот) <> Тип("Неопределено") Тогда
		
		Отбор = Новый Структура();
		Отбор.Вставить("СвязаннаяРабота", СвязаннаяРабота);
		НайденныеСтрокиПоМодели = ТаблицаСвязанныхРабот.НайтиСтроки(Отбор);
		
		Если ОтборМодель <> Справочники.Модели.ПустаяСсылка() Тогда
			
			ЕстьПоМоделиИВарианту = Ложь;
			ЕстьПоМодели = Ложь;
			ЕстьОбщие = Ложь;
			КоличествоРабот = 0;
			Для Каждого ТекущаяСтрока Из НайденныеСтрокиПоМодели Цикл
				
				Если ТекущаяСтрока.Модель = ОтборМодель
					И ТекущаяСтрока.ВариантКомплектации = ОтборВариантКомплектации Тогда
					ЕстьПоМоделиИВарианту = Истина;
					КоличествоРабот = ТекущаяСтрока.Количество;
					Прервать;
				ИначеЕсли ТекущаяСтрока.Модель = ОтборМодель Тогда
					ЕстьПоМодели = Истина;
					КоличествоРабот = ТекущаяСтрока.Количество;
				ИначеЕсли НЕ ЕстьПоМодели И НЕ ЗначениеЗаполнено(ТекущаяСтрока.Модель) Тогда
					ЕстьОбщие = Истина;
					КоличествоРабот = ТекущаяСтрока.Количество;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ЕстьПоМоделиИВарианту ИЛИ ЕстьПоМодели ИЛИ ЕстьОбщие Тогда
				
				ЗаписьМодели = ЗаписьМоделей.Добавить();
				ЗаписьМодели.СвязаннаяРабота = СвязаннаяРабота;
				ЗаписьМодели.Модель = ?(ЕстьПоМоделиИВарианту ИЛИ ЕстьПоМодели, ОтборМодель, "<Для любой модели>");
				Если ОтборВариантКомплектации = Справочники.ВариантыКомплектации.ПустаяСсылка()
					ИЛИ НЕ ЕстьПоМоделиИВарианту Тогда
					ЗаписьМодели.ВариантКомплектации = "<Для любой комплектации>";
				Иначе
					ЗаписьМодели.ВариантКомплектации = ОтборВариантКомплектации;
				КонецЕсли;
				ЗаписьМодели.Количество = КоличествоРабот;
				Возврат ЗаписьМоделей;
				
			КонецЕсли;
			
		Иначе
			
			Для Каждого ТекСтрока Из НайденныеСтрокиПоМодели Цикл
				ЗаписьМодели = ЗаписьМоделей.Добавить();
				ЗаписьМодели.СвязаннаяРабота = СвязаннаяРабота;
				Если ТекСтрока.Модель = Справочники.Модели.ПустаяСсылка() Тогда
					ЗаписьМодели.Модель= "<Для любой модели>";
				Иначе
					ЗаписьМодели.Модель = ТекСтрока.Модель;
				КонецЕсли;
				Если ТекСтрока.ВариантКомплектации = Справочники.ВариантыКомплектации.ПустаяСсылка() Тогда
					ЗаписьМодели.ВариантКомплектации = "<Для любой комплектации>";
				Иначе
					ЗаписьМодели.ВариантКомплектации = ТекСтрока.ВариантКомплектации;
				КонецЕсли;
				ЗаписьМодели.Количество = ТекСтрока.Количество;
				Если ПризнакПолученияКоличестваСвязаннойРаботы = истина Тогда
					Возврат ЗаписьМоделей;
				КонецЕсли;
			КонецЦикла;
			Возврат ЗаписьМоделей;
		КонецЕсли;
	КонецЕсли;
	ЗаписьМодели = ЗаписьМоделей.Добавить();
	ЗаписьМодели.СвязаннаяРабота = СвязаннаяРабота;
	ЗаписьМодели.Модель= "";
	ЗаписьМодели.ВариантКомплектации = "";
	ЗаписьМодели.Количество = 0;
	Возврат ЗаписьМоделей;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

СтруктураПараметровПодбора = Новый Структура;
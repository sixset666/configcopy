#Если Клиент Тогда
	
Перем СписокПоиска	Экспорт; // Список поиска
//Перем ПоискБезОграниченияВывода	Экспорт;
Перем ПоискРазмерПорции	Экспорт; // Поиск размера порции
Перем ИсходнаяСтрокаПоиска Экспорт; // Исходная строка поиска
Перем УбратьПовторы Экспорт; // Убрать повторы

Перем РезультатПоискаНачальнаяПозиция Экспорт; // начальная позиция результата поиска
Перем РезультатПоискаКоличество Экспорт; // количество строк в результате поиска
Перем РезультатПоискаПолноеКоличество Экспорт; // полное количество результата поиска
Перем РезультатПоискаСлишкомМногоРезультатов Экспорт; // признак слишком большого количества строк в результате поиска

Перем РезультатПоиска Экспорт; // хранится результат поиска

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


// Функция проверяет возможность использования полнотекстового поиска
//
// Параметры:
// 	ВыводитьСообщение - булево - флаг того что нужно вывести сообщение о невозможности поиска
//  ВключатьПоиск     - булево - флаг того что нужно запросить включение поиска
//
// Возвращаемое значение:
// 	Истина	- режим разрешен
//	Ложь	- запрещен
//
Функция ПолнотекстовыйПоискРазрешен(ВключатьПоиск = Истина, ВыводитьСообщение = Истина) Экспорт
	ПоискРазрешен = (ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить);
	Если НЕ ПоискРазрешен Тогда
		Если ВключатьПоиск Тогда
			ПоискРазрешен = РазрешитьПолнотекстовыйПоиск(ВыводитьСообщение);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПоискРазрешен;
КонецФункции

// Функция включает полнотекстовый поиск
Функция РазрешитьПолнотекстовыйПоиск(ВыводитьСообщение = Истина, ОбновитьИндекс = Истина) Экспорт
	ПоискРазрешен = (ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить);
	Если ПоискРазрешен Тогда
		Возврат Истина;
	КонецЕсли;
	
	// проверка необходимых прав
	Если НЕ ПравоДоступа("Администрирование", Метаданные) Тогда
		Текст = "Полнотекстовый поиск в настоящий момент запрещен.
				|Для разрешения операций с полнотекстовым поиском необходимы административные права.
				|Обратитесь к администратору баз данных.";
		Предупреждение(Текст,, "Полнотекстовый поиск");
		Возврат Ложь;
	КонецЕсли;
	
	Если ВыводитьСообщение Тогда
		Текст = "Полнотекстовый поиск в настоящий момент запрещен.
				|Разрешить полнотекстовый поиск?";
		Ответ = Вопрос(Текст, РежимДиалогаВопрос.ОКОтмена,,,"Полнотекстовый поиск");
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ПовторВключенияПоиска = Истина;
	
	Пока ПовторВключенияПоиска цикл
		ПовторВключенияПоиска = Ложь;
		Попытка
			ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Разрешить);
			Если ОбновитьИндекс Тогда
				Сообщить("Обновляется индекс полнотекстового поиска. Это может занять продолжительное время, пожалуйста подождите...", СтатусСообщения.Информация);
				ПолнотекстовыйПоиск.ОбновитьИндекс();
				Сообщить("Обновление индекса завершено.", СтатусСообщения.Информация);
			КонецЕсли;
		Исключение
			Инфо = ИнформацияОбОшибке();
			Описание = Инфо.Описание;
			Если Инфо.Причина <> Неопределено Тогда
				Описание = Инфо.Причина.Описание;
			КонецЕсли;
			Если Найти(Описание, "Ошибка исключительной блокировки информационной базы.") > 0 Тогда
				Текст = "Ошибка исключительной блокировки информационной базы.
						|Закройте все остальные подключения к базе и повторите попытку.";
				Ответ = Вопрос(Текст,РежимДиалогаВопрос.ПовторитьОтмена,,, "Полнотекстовый поиск");
				Если Ответ = КодВозвратаДиалога.Повторить Тогда
					ПовторВключенияПоиска = Истина;
				КонецЕсли;
			Иначе
				Предупреждение(Описание,, "Полнотекстовый поиск");
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	ПоискРазрешен = (ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить);
	Возврат ПоискРазрешен;
КонецФункции
	
// Функция обновляет индекс полнотекстового поиска
Функция ОбновитьИндексПоиска() Экспорт
	Результат = Ложь;
	ПоискРазрешен = (ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить);
	Если НЕ ПоискРазрешен Тогда
		ПоискРазрешен = РазрешитьПолнотекстовыйПоиск(Истина, Ложь);
	КонецЕсли;
	Если ПоискРазрешен Тогда
		Если НЕ ПолнотекстовыйПоиск.ИндексАктуален() Тогда
			Попытка
				Сообщить("Обновляется индекс полнотекстового поиска. Это может занять продолжительное время, пожалуйста подождите...", СтатусСообщения.Информация);
				ПолнотекстовыйПоиск.ОбновитьИндекс();
				Сообщить("Обновление индекса завершено.", СтатусСообщения.Информация);
				Результат = Истина;
			Исключение
				Сообщить("Ошибка при обновление индекса!"+Символы.ПС+ОписаниеОшибки(),СтатусСообщения.Внимание);
			КонецПопытки;
		Иначе
			Сообщить("Индекс полнотекстового поиска находится в актуальном состоянии, обновление не требуется.",СтатусСообщения.Обычное);
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Производит инициализацию переменных
//
// Параметры:
// 	Нет
//
Процедура ИнициализацияПеременныхПоиска() Экспорт
	СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок("");
	СписокПоиска.ПолучатьОписание = Истина;
	РезультатПоиска = Новый ТаблицаЗначений;
	РезультатПоиска.Колонки.Добавить("НомерСтроки");
	РезультатПоиска.Колонки.Добавить("Значение");
	РезультатПоиска.Колонки.Добавить("Описание");
	РезультатПоиска.Колонки.Добавить("Представление");
	РезультатПоиска.Колонки.Добавить("Синоним");
	РезультатПоиска.Колонки.Добавить("ЗначениеКартинки");
	РезультатПоиска.Колонки.Добавить("HTMLОтображение");
КонецПроцедуры

// Процедура обрабатывает данные поиска и сохраняет их в таблицу накопления
// из которой впоследствии данные выводятся на экран
Процедура ВыгрузитьРезультатВТаблицу() Экспорт
	ИспользоватьОграниченияНаРезультат = (ИспользоватьОграничения И (ТаблицаМетаданныхВывода.Количество()<>0) );
	ЭлементРезультата = Новый Структура;
	ЭлементРезультата.Вставить("Значение");
	ЭлементРезультата.Вставить("Синоним");
	ЭлементРезультата.Вставить("Метаданные");
	ЭлементРезультата.Вставить("Описание");
	ЭлементРезультата.Вставить("Представление");
	
	Для каждого ЭлементСпискаПолнотекстовогоПоиска из СписокПоиска цикл
		
		ЗаполнитьЗначенияСвойств(ЭлементРезультата, ЭлементСпискаПолнотекстовогоПоиска);
		ЭлементРезультата.Синоним = ЭлементСпискаПолнотекстовогоПоиска.Метаданные.Синоним;
		
		СтрокаТабличнойЧасти = Неопределено;
		Если ИспользоватьОграниченияНаРезультат Тогда
			// В ЭлементРезультата уже заполнены значения ЭлементСпискаПолнотекстовогоПоиска.
			// Посмотрим, надо ли его менять.
			СтрокаТабличнойЧасти = ТаблицаМетаданныхВывода.Найти(ЭлементСпискаПолнотекстовогоПоиска.Метаданные.ПолноеИмя(),"ПолноеИмя");
			Если СтрокаТабличнойЧасти=Неопределено Тогда
				Если Найти(ЭлементРезультата.Метаданные.ПолноеИмя(), "Регистр")>0 Тогда
					// Значит найденным значением является запись регистра. Поскольку,
					// пользователь не возжелал выводить данный объект, 
					// то проходимся по измерениям текущей записи регистра, возможно что-то подойдет.
					Для Каждого ИзмерениеМетаданные Из ЭлементРезультата.Метаданные.Измерения Цикл
						ЗначениеИзмерения = ЭлементРезультата.Значение[ИзмерениеМетаданные.Имя];
						Если НЕ обЗначениеНеЗаполнено(ЗначениеИзмерения) И (Найти(Строка(ТипЗнч(ЗначениеИзмерения)), "ссылка")>0)Тогда
							СтрокаТабличнойЧасти = ТаблицаМетаданныхВывода.Найти(ЗначениеИзмерения.Метаданные().ПолноеИмя(),"ПолноеИмя");
							Если СтрокаТабличнойЧасти<>Неопределено Тогда
								ЭлементРезультата.Описание      = "Регистр """ + ЭлементРезультата.Метаданные.Синоним + """ (" + ЭлементРезультата.Представление + "): " + ЭлементРезультата.Описание; 
								ЭлементРезультата.Значение      = ЗначениеИзмерения;
								ЭлементРезультата.Представление = Строка(ЗначениеИзмерения);																
								ЭлементРезультата.Синоним       = ЭлементРезультата.Значение.Метаданные().Синоним;                                          								
								Прервать; 									
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;						
				Иначе         
					// Значит найденным значением является какой-либо объект. 
					// Если это справочник, то сперва проверим его владельца.
					Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЭлементРезультата.Значение)) Тогда
						Если НЕ обЗначениеНеЗаполнено(ЭлементРезультата.Значение.Владелец) Тогда
							СтрокаТабличнойЧасти = ТаблицаМетаданныхВывода.Найти(ЭлементРезультата.Значение.Владелец.Метаданные().ПолноеИмя(),"ПолноеИмя");
							Если СтрокаТабличнойЧасти<>Неопределено Тогда
								ЭлементРезультата.Описание      = "Справочник """ + ЭлементРезультата.Метаданные.Синоним + """ (" + ЭлементРезультата.Значение + """): " + ЭлементРезультата.Описание;
								ЭлементРезультата.Значение      = ЭлементРезультата.Значение.Владелец;	
								ЭлементРезультата.Представление = Строка(ЭлементРезультата.Значение);																
								ЭлементРезультата.Синоним       = ЭлементРезультата.Значение.Метаданные().Синоним;								
							КонецЕсли; 						
						КонецЕсли;
					КонецЕсли;   				
					
					// Значит проходимся по реквизитам объекта.
					Если СтрокаТабличнойЧасти=Неопределено Тогда				
						Для Каждого РеквизитМетаданные Из ЭлементРезультата.Метаданные.Реквизиты Цикл
							ЗначениеРеквизита = ЭлементРезультата.Значение[РеквизитМетаданные.Имя];
							Если НЕ обЗначениеНеЗаполнено(ЗначениеРеквизита) И (Найти(Строка(ТипЗнч(ЗначениеРеквизита)), "ссылка")>0)Тогда
								СтрокаТабличнойЧасти = ТаблицаМетаданныхВывода.Найти(ЗначениеРеквизита.Метаданные().ПолноеИмя(),"ПолноеИмя");
								Если СтрокаТабличнойЧасти<>Неопределено Тогда
									ЭлементРезультата.Описание      = ЭлементРезультата.Представление + ": " + ЭлементРезультата.Описание;
									ЭлементРезультата.Значение      = ЗначениеРеквизита;	
									ЭлементРезультата.Представление = Строка(ЭлементРезультата.Значение);																
									ЭлементРезультата.Синоним       = ЭлементРезультата.Значение.Метаданные().Синоним;	
									Прервать;
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;   						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 						
			
			// Если не нашли подходящий объект для вывода, то пропускаем
			Если СтрокаТабличнойЧасти=Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли; // ИспользоватьОграниченияНаРезультат
		
		НоваяСтрока = Неопределено;
		Если УбратьПовторы Тогда
			НоваяСтрока = РезультатПоиска.Найти(ЭлементРезультата.Значение, "Значение");
			Если НоваяСтрока<>Неопределено Тогда
				НоваяСтрока.Описание = НоваяСтрока.Описание + ", " + ЭлементРезультата.Описание;
			КонецЕсли;
		КонецЕсли;
		Если НоваяСтрока = Неопределено Тогда
			НоваяСтрока = РезультатПоиска.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементРезультата);
			НоваяСтрока.НомерСтроки = РезультатПоиска.Количество();
			
			ЗначениеКартинки = Неопределено;
			Если ТипЗнч(НоваяСтрока.Значение) = Тип("РегистрСведенийКлючЗаписи.КонтактнаяИнформация") Тогда
				Для каждого ТипКИ из Перечисления.ТипыКонтактнойИнформации цикл
					Если Найти(НоваяСтрока.Представление, "; " + Строка(ТипКИ) + ";") > 0 Тогда
						ЗначениеКартинки = киПолучитьКартинкуТипаКИ(ТипКИ);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ТипЗнч(НоваяСтрока.Значение)<>Тип("Строка") Тогда
				ЗначениеКартинки = НоваяСтрока.Значение.ПолучитьФорму().КартинкаЗаголовка;
				Если ЗначениеКартинки.Вид = ВидКартинки.Пустая Тогда
					ТипОбъекта = Лев(НоваяСтрока.Значение.Метаданные().ПолноеИмя(),Найти(НоваяСтрока.Значение.Метаданные().ПолноеИмя(),".")-1);
					ЗначениеКартинки = обВернутьСтандартнуюКартинку(ТипОбъекта);
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеКартинки = Неопределено Тогда
				ТипОбъекта = Лев(НоваяСтрока.Значение,Найти(НоваяСтрока.Значение,"КлючЗаписи")-1);
				ЗначениеКартинки = обВернутьСтандартнуюКартинку(ТипОбъекта);
			КонецЕсли;
			НоваяСтрока.ЗначениеКартинки = ЗначениеКартинки;
			
			СтрHTML = "<tr>
					|  <td><font face=""MS Sans Serif"" size=""1"">
					|	<a id=""FullTextSearchListItem"" href=./"+(НоваяСтрока.НомерСтроки-1)+">";
			СтрHTML = СтрHTML + НоваяСтрока.Представление +"</a>&nbsp;("+ НоваяСтрока.Синоним +")<br>"+Символы.ПС;
			СтрHTML = СтрHTML + ?(СписокПоиска.ПолучатьОписание,НоваяСтрока.Описание,"");
			СтрHTML = СтрHTML + "
					|  </font></td>
					|</tr>";
			НоваяСтрока.HTMLОтображение = СтрHTML;
			
		КонецЕсли;
		
	КонецЦикла; // Для каждого ЭлементСпискаПолнотекстовогоПоиска из СписокПоиска цикл
	
	Если РезультатПоискаПолноеКоличество = Неопределено Тогда
		Если ((СписокПоиска.НачальнаяПозиция()+СписокПоиска.Количество()) = СписокПоиска.ПолноеКоличество()) Тогда
			РезультатПоискаПолноеКоличество = РезультатПоиска.Количество();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыгрузитьРезультатВТаблицу()

// Процедура производит полнотекстовый поиск
//
// Параметры:
//  ПрименитьНечеткийПоиск	- Булево - условие применения Нечеткого поиска
//	Нечеткость              - Число - размер порога нечеткости
//	РазмерПорции            - Число - размер порции
//  ПоискПоВхождениям		- Булево - условие применения поиска по вхождениям
//
Процедура ВыполнитьПоиск(ПрименитьНечеткийПоиск = Ложь, Нечеткость = 25, РазмерПорции = 20, ПоискПоВхождениям = Ложь, ПолучатьОписание = Истина, УбиратьПовторы = Ложь) Экспорт

	Если обЗначениеНеЗаполнено(ПолеВводаПоиска) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьОграничения = Истина Тогда
		МассивМД = Новый Массив();
		Для Каждого МД Из ТаблицаМетаданныхПоиска Цикл
			МассивМД.Добавить(Метаданные.НайтиПоПолномуИмени(МД.ПолноеИмя));
		КонецЦикла;	
		СписокПоиска.ОбластьПоиска = МассивМД;
	Иначе	
		МассивМД = Новый Массив();
		СписокПоиска.ОбластьПоиска = МассивМД;
	КонецЕсли;
	
	Если ПрименитьНечеткийПоиск = Истина Тогда
		СписокПоиска.ПорогНечеткости = Нечеткость;
	Иначе	
		СписокПоиска.ПорогНечеткости = 0;
	КонецЕсли;
	
	Если ПоискПоВхождениям Тогда
		ИсходнаяСтрокаПоиска = ПолеВводаПоиска;
		СписокПоиска.СтрокаПоиска = ПоискПоВхождениямПреобразоватьСтроку(ИсходнаяСтрокаПоиска);
	Иначе
		СписокПоиска.СтрокаПоиска = ПолеВводаПоиска;
	КонецЕсли;
	
	СписокПоиска.ПолучатьОписание = ПолучатьОписание;
	УбратьПовторы = УбиратьПовторы;
	ПоискРазмерПорции = РазмерПорции;
	СписокПоиска.РазмерПорции = ПоискРазмерПорции;
	Попытка
		СписокПоиска.ПерваяЧасть();
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Предупреждение(ИнформацияОбОшибке.Описание);
	КонецПопытки;
	РезультатПоиска.Очистить();
	
	РезультатПоискаНачальнаяПозиция = 0;
	РезультатПоискаПолноеКоличество = Неопределено;
	РезультатПоискаСлишкомМногоРезультатов = СписокПоиска.СлишкомМногоРезультатов();
	ВыгрузитьРезультатВТаблицу();
	Пока ((СписокПоиска.НачальнаяПозиция()+СписокПоиска.Количество()) < СписокПоиска.ПолноеКоличество()) // Обработали ещё не все данные в СпискеПоиска...
		И (ПоискРазмерПорции > РезультатПоиска.Количество()) // ... и для первой порции нужно больше данных
	Цикл
		СписокПоиска.СледующаяЧасть();
		ВыгрузитьРезультатВТаблицу();
	КонецЦикла;
	РезультатПоискаКоличество = Мин(ПоискРазмерПорции, РезультатПоиска.Количество()-РезультатПоискаНачальнаяПозиция);
	
КонецПроцедуры // ВыполнитьПоиск()

// Функция преобразует строку поиска - если в строке нет служебных символов, то все неключевые слова заключает в "*"
//
// Параметры:
//  СтрокаПоиска	- строка - строка, которую следует преобразовать
//	
// Возвращаемое значение - строка - преобразованная строка
//
Функция ПоискПоВхождениямПреобразоватьСтроку(СтрокаПоиска);
	СтрокаСлужебныхСимволов = "&|,~()*#!/+-";
	СписокСлужебныхСлов = Новый Массив(8);
	СписокСлужебныхСлов.Вставить(1,"И");
	СписокСлужебныхСлов.Вставить(2,"ИЛИ");
	СписокСлужебныхСлов.Вставить(3,"НЕ");
	СписокСлужебныхСлов.Вставить(4,"РЯДОМ");
	СписокСлужебныхСлов.Вставить(5,"AND");
	СписокСлужебныхСлов.Вставить(6,"OR");
	СписокСлужебныхСлов.Вставить(7,"NOT");
	СписокСлужебныхСлов.Вставить(8,"NEAR");
	
	Для Счетчик=1 По СтрДлина(СтрокаСлужебныхСимволов) Цикл
		Если Найти(СтрокаПоиска, Сред(СтрокаСлужебныхСимволов,Счетчик,1))<>0 Тогда 
			Возврат СтрокаПоиска;		
		КонецЕсли;
	КонецЦикла;
	НоваяСтрокаПоиска = "";
	Пока СтрДлина(СтрокаПоиска) <> 0 Цикл
		Лексема = "";
		Символ = Лев(СтрокаПоиска,1);
		СтрокаПоиска = Сред(СтрокаПоиска,2);
		Пока Символ<>" " И Символ<>"" Цикл
			Лексема = Лексема + Символ;
			Символ = Лев(СтрокаПоиска,1);
			СтрокаПоиска = Сред(СтрокаПоиска,2);
		КонецЦикла;
		Если СписокСлужебныхСлов.Найти(Лексема)=Неопределено И Лексема<>"" Тогда
			НоваяСтрокаПоиска = НоваяСтрокаПоиска + "*"+Лексема + "* ";
		Иначе
			НоваяСтрокаПоиска = НоваяСтрокаПоиска + Лексема + " ";
		КонецЕсли;
	КонецЦикла;
	
	Возврат СокрП(НоваяСтрокаПоиска);
КонецФункции //ПоискПоВхождениямПреобразоватьСтроку

// Процедура вычисляет следующую порцию результатов поиска 
//
// Параметры:
//  Нет
//
Процедура СписокПоискаСледующаяЧасть()	Экспорт
	Пока ((СписокПоиска.НачальнаяПозиция()+СписокПоиска.Количество()) < СписокПоиска.ПолноеКоличество()) // Обработали ещё не все данные в СпискеПоиска...
		И ((РезультатПоискаНачальнаяПозиция+РезультатПоискаКоличество+ПоискРазмерПорции) > РезультатПоиска.Количество()) // ... и для следующей порции нужно больше данных
	Цикл
		СписокПоиска.СледующаяЧасть();
		ВыгрузитьРезультатВТаблицу();
	КонецЦикла;
	Если (РезультатПоискаНачальнаяПозиция+РезультатПоискаКоличество) < РезультатПоиска.Количество() Тогда
		РезультатПоискаНачальнаяПозиция = РезультатПоискаНачальнаяПозиция + РезультатПоискаКоличество;
		РезультатПоискаКоличество = Мин(ПоискРазмерПорции, РезультатПоиска.Количество()-РезультатПоискаНачальнаяПозиция);
	КонецЕсли;
КонецПроцедуры

// Процедура вычисляет предыдущую порцию результатов поиска 
//
// Параметры:
//  Нет
//
Процедура СписокПоискаПредыдущаяЧасть()	Экспорт
	Если РезультатПоискаНачальнаяПозиция > 0 Тогда
		РезультатПоискаНачальнаяПозиция = Макс(РезультатПоискаНачальнаяПозиция-ПоискРазмерПорции, 0);
		РезультатПоискаКоличество = Мин(ПоискРазмерПорции, РезультатПоиска.Количество()-РезультатПоискаНачальнаяПозиция);
	КонецЕсли;
КонецПроцедуры

// Производит очистку таблиц метаданных поиска и вывода
//
// Параметры:
// 	Нет
//
Процедура ОчиститьОграничения() Экспорт
	ТаблицаМетаданныхПоиска.Очистить();
	ТаблицаМетаданныхВывода.Очистить();
КонецПроцедуры

// Процедура добавляет ограничение для поиска и/или вывода
//
// Параметры:
// 	ОбъектМетаданных - тип:ОбъектМетаданных - объект полное имя и синоним которого добавляется в таблицу поиска/вывода
// 	ДобавитьВПоиск   - тип:булево           - флаг добавления в таблицу поиска
// 	ДобавитьВВывод   - тип:булево           - флаг добавления в таблицу вывода
//
Процедура ДобавитьОграничение(ОбъектМетаданных, ДобавитьВПоиск = Истина, ДобавитьВВывод = Истина) Экспорт
	Если ДобавитьВПоиск Тогда
		НоваяСтрока = ТаблицаМетаданныхПоиска.Добавить();
		НоваяСтрока.НазваниеОМД = ОбъектМетаданных.Синоним;
		НоваяСтрока.ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	КонецЕсли;
	Если ДобавитьВВывод Тогда
		НоваяСтрока = ТаблицаМетаданныхВывода.Добавить();
		НоваяСтрока.НазваниеОМД = ОбъектМетаданных.Синоним;
		НоваяСтрока.ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	КонецЕсли;
КонецПроцедуры

// Процедура удаляет ограничение из поиска и/или вывода
//
// Параметры:
// 	ОбъектМетаданных - тип:ОбъектМетаданных - объект полное имя и синоним которого добавляется в таблицу поиска/вывода
// 	УдалитьВПоиске   - тип:булево           - флаг удаления из таблицы поиска
// 	УдалитьВВыводе   - тип:булево           - флаг удаления из таблицы вывода
//
Процедура УдалитьОграничение(ПолноеИмя, УдалитьВПоиске = Истина, УдалитьВВыводе = Истина) Экспорт
	Если УдалитьВПоиске Тогда
		Строка = ТаблицаМетаданныхПоиска.Найти(ПолноеИмя, "ПолноеИмя");
		Если Строка <> Неопределено Тогда
			ТаблицаМетаданныхПоиска.Удалить(Строка);
		КонецЕсли;
	КонецЕсли;
	Если УдалитьВВыводе Тогда
		Строка = ТаблицаМетаданныхВывода.Найти(ПолноеИмя, "ПолноеИмя");
		Если Строка <> Неопределено Тогда
			ТаблицаМетаданныхВывода.Удалить(Строка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецЕсли
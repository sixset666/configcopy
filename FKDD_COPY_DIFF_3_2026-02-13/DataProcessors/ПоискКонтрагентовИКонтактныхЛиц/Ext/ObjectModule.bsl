// Модуль обработки

Перем ПараметрыПоиска Экспорт;   // параметры поиска клиентов
Перем СписокПолейПоискаПоУмолчанию Экспорт; // список полей поиска клиентов

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Определение, если ли хоть одна пометка в списке
//
// Параметры
//  Список  – СписокЗначений - список, который проверяем
// Возвращаемое значение:
//   Булево – флаг, есть ли пометка
//
Функция ЕстьПометка(Список) Экспорт
	 ЕстьПометка = Ложь;
	 Для Каждого Значение Из Список Цикл
	 	Если Значение.Пометка Тогда
			ЕстьПометка = Истина;				
			Прервать;
		КонецЕсли;  
	 КонецЦикла; 
	 Возврат  ЕстьПометка;
КонецФункции // ЕстьПометка()

// Выполнение поиска и формирование таблицы найденных ссылок
//
//
// Параметры:
//  ТаблицаРезультата - ТаблицаЗначение - таблица результата поиска,
//  ВыполнятьПроверки - Булево - определяет выполнять проверками на указание параметров поиска. 
//
Процедура ВыполнитьПоиск(ТаблицаРезультата, ВыполнятьПроверки = Истина) Экспорт 
	
	стСтрокаПоиска = ПараметрыПоиска.СтрокаПоиска;
	
	Если ВыполнятьПроверки Тогда
		Если ПустаяСтрока(стСтрокаПоиска) Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ЕстьПометка(ПараметрыПоиска.СписокПолейПоиска) Тогда
			#Если Клиент Тогда
			Предупреждение("Не выбран ни один вариант поиска! Поиск невозможен.",,"Внимание!");
			#ИначеЕсли Сервер Тогда
			Сообщить("Не выбран ни один вариант поиска! Поиск невозможен.",СтатусСообщения.Внимание);
			#КонецЕсли
	    Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	стСтрокаПоиска = СтрЗаменить(стСтрокаПоиска, """", """""");
	
	ИскатьВКомментариях = ПараметрыПоиска.СписокПолейПоиска.НайтиПоЗначению("ИскатьВКомментариях").Пометка;
	
	СписокПолейПоискаПоНаименованию = Новый СписокЗначений;
	Для ин = 0 По 1 Цикл
		Элемент = ПараметрыПоиска.СписокПолейПоиска.Получить(ин);
		Если Элемент.Пометка Тогда
		СписокПолейПоискаПоНаименованию.Добавить(Элемент.Значение, ,Элемент.Пометка);
		КонецЕсли;
	КонецЦикла; 
	
	СписокПолейПоискаВКИ = Новый СписокЗначений;
	Для ин = 2 По 7 Цикл
		Элемент = ПараметрыПоиска.СписокПолейПоиска.Получить(ин);
		Если Элемент.Пометка Тогда
		СписокПолейПоискаВКИ.Добавить(Элемент.Значение, ,Элемент.Пометка);
		КонецЕсли;
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	Если СписокПолейПоискаПоНаименованию.Количество() > 0 Тогда
		// Поиск по наименованию
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ";
		ТекстЗапроса = ТекстЗапроса + "	
		|	Контрагенты.Ссылка КАК Ссылка,
		|	Выразить (Контрагенты.НаименованиеПолное КАК Строка (255)) КАК ПолноеНаименование,
		|	Контрагенты.ФормаСобственности КАК ТипКлиента,
		|	Контрагенты.ВидКонтрагента КАК ВидКонтрагента,
		|	Контрагенты.Пол КАК Пол,
		|	Контрагенты.Должность КАК Должность,
		|	Контрагенты.ОсновнойТелефон КАК ОсновнойТелефон,
		|	ВЫРАЗИТЬ(Контрагенты.ДатаРождения КАК Дата) КАК ДатаРождения
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ(Контрагенты.ЭтоГруппа) 
		|И(";
		ДобавленоУсловие = Ложь;
		Для Каждого ТекЗначение Из СписокПолейПоискаПоНаименованию Цикл
			Если ТекЗначение.Пометка Тогда
				Если ДобавленоУсловие Тогда
					ТекстЗапроса = ТекстЗапроса + "
					|ИЛИ";	
				Иначе
					ДобавленоУсловие = Истина;
				КонецЕсли;
				ТекстЗапроса = ТекстЗапроса + "
				|  Контрагенты." + Строка(ТекЗначение.Значение) + " ПОДОБНО ""%" + стСтрокаПоиска + "%""";
			КонецЕсли; 
		КонецЦикла; 
		Если ИскатьВКомментариях Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ИЛИ
			|  Контрагенты.Комментарий ПОДОБНО ""%" + стСтрокаПоиска + "%"" 
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ")";
	КонецЕсли;
	
	Если СписокПолейПоискаВКИ.Количество() > 0 Тогда 
   		// Поиск в контактной информации
		Если ТекстЗапроса <> "" Тогда
			ТекстЗапроса = ТекстЗапроса + "	
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + "	
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КонтактнаяИнформация.Объект КАК Ссылка,
		|   Выбор
		|      Когда КонтактнаяИнформация.Объект Ссылка Справочник.Контрагенты Тогда
		|			Выразить (КонтактнаяИнформация.Объект.НаименованиеПолное КАК Строка (255))
		|	   Иначе
		|			КонтактнаяИнформация.Объект.Наименование
		|	КОНЕЦ КАК ПолноеНаименование,
		|   Выбор
		|      Когда КонтактнаяИнформация.Объект Ссылка Справочник.Контрагенты Тогда
		|			КонтактнаяИнформация.Объект.ВидКонтрагента
		|	   Иначе
		|			КонтактнаяИнформация.Объект.Наименование
		|	КОНЕЦ КАК ВидКонтрагента,
		|   Выбор
		|      Когда КонтактнаяИнформация.Объект Ссылка Справочник.Контрагенты Тогда
		|			КонтактнаяИнформация.Объект.ФормаСобственности
		|	   Иначе
		|			NULL
		|	КОНЕЦ КАК ТипКлиента,
		|	КонтактнаяИнформация.Объект.Пол КАК Пол,
		|   Выбор
		|      Когда КонтактнаяИнформация.Объект Ссылка Справочник.Контрагенты Тогда
		|			КонтактнаяИнформация.Объект.Должность
		|	   Иначе
		|			NULL
		|	КОНЕЦ КАК Должность,
		|   Выбор
		|      Когда КонтактнаяИнформация.Объект Ссылка Справочник.Контрагенты Тогда
		|			КонтактнаяИнформация.Объект.ОсновнойТелефон
		|	   Иначе
		|			NULL
		|	КОНЕЦ КАК ОсновнойТелефон,
		|	ВЫРАЗИТЬ(КонтактнаяИнформация.Объект.ДатаРождения КАК Дата) КАК ДатаРождения
		|
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип.Ссылка В (&СписокТипов)
		|  И
		|";                          
		// ограничим типы объекта контактной информации
		стрОбъектКонтрагент =" КонтактнаяИнформация.Объект ССЫЛКА Справочник.Контрагенты ";
		ТекстЗапроса = ТекстЗапроса + "
		|	(" + стрОбъектКонтрагент +")
		|  И(
		|	КонтактнаяИнформация.Представление ПОДОБНО""%" + стСтрокаПоиска + "%""
		|";
		Если ИскатьВКомментариях Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ИЛИ
			|  КонтактнаяИнформация.Комментарий ПОДОБНО ""%" + стСтрокаПоиска + "%"" 
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ")";
		
		СписокТипов = Новый СписокЗначений;
		Для Каждого Значение Из СписокПолейПоискаВКИ Цикл
			Если Значение.Пометка Тогда
				СписокТипов.Добавить(Значение.Значение);
			КонецЕсли;
		КонецЦикла;
		Запрос.УстановитьПараметр("СписокТипов",СписокТипов);
	КонецЕсли;		
	
	Запрос.Текст = ТекстЗапроса;
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();	
		
КонецПроцедуры // ВыполнитьПоиск()
 
////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

СписокПолейПоискаПоУмолчанию = Новый СписокЗначений;
СписокПолейПоискаПоУмолчанию.Добавить("Наименование", "Наименование (ФИО)",Истина);
СписокПолейПоискаПоУмолчанию.Добавить("НаименованиеПолное", "Полное наименование",Истина);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон, "Телефон", Истина);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.Адрес, "Адрес",Ложь);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, "Адрес электронной почты",Ложь);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.ВебСтраница, "Web сайт",Ложь);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.НомерICQ, "ICQ номер",Ложь);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.Другое, "Другое (произвольная информация)",Ложь);
СписокПолейПоискаПоУмолчанию.Добавить("ИскатьВКомментариях", "Комментарии",Ложь);

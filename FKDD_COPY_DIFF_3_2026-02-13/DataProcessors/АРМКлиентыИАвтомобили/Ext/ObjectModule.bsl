// Модуль обработки АРМКлиентыИАвтомобили

Перем Права Экспорт; // права пользователя

// Поиск клиента и автомобиля в источниках, заданных параметрами поиска
// Параметры:
//  СтрокаПоиска - Строка -  содержимое поискового запроса пользователя
// 	РасширенныйПоиск - Булево - Если истина - значит поиск осуществляется в соответствии со всеми настройками
//								с учетом флагов, ложь - поиск везде, флаги ограничений игнорируются
//  СтруктураНастройкиПоиска - Структура - Структура источников поиска
//	СтруктураФлаговНаФорме - Структура флагов отборов на форме
Функция ПоискКлиентаИАвтомобиля(СтрокаПоиска,РасширенныйПоиск,СтруктураНастройкиПоиска=Неопределено,СтруктураФлаговНаФорме=Неопределено) Экспорт
	
	//источники поиска
	НаименованиеКлиента = Истина;
	КонтактнаяИнформация = Истина; 
	ГосНомер = Истина; 
	VIN = Истина; 
	НомерКарты = Истина; 
	
	Если ТипЗнч(СтруктураНастройкиПоиска) = Тип("ТаблицаЗначений") И РасширенныйПоиск Тогда
		НаименованиеКлиента = СтруктураНастройкиПоиска.Найти("НаименованиеКлиента","Параметр").Значение;
		КонтактнаяИнформация = СтруктураНастройкиПоиска.Найти("КонтактнаяИнформация","Параметр").Значение; 
		ГосНомер = СтруктураНастройкиПоиска.Найти("ГосНомер","Параметр").Значение; 
		VIN = СтруктураНастройкиПоиска.Найти("VIN","Параметр").Значение; 
		НомерКарты = СтруктураНастройкиПоиска.Найти("НомерКарты","Параметр").Значение; 
	КонецЕсли;
	
	//флаги отборов
	ФлажокТолькоМои = Ложь;
	ФлажокЗаписанныеНаСегодня = Ложь;
	ФлажокОжидающиеВыдачи = Ложь;
	
	Если ТипЗнч(СтруктураФлаговНаФорме) = Тип("Структура") И РасширенныйПоиск  Тогда
		ФлажокТолькоМои = СтруктураФлаговНаФорме.ФлажокТолькоМои;
		ФлажокЗаписанныеНаСегодня = СтруктураФлаговНаФорме.ФлажокЗаписанныеНаСегодня;
		ФлажокОжидающиеВыдачи = СтруктураФлаговНаФорме.ФлажокОжидающиеВыдачи;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	ЗапросНаименованиеКлиента =	"ВЫБРАТЬ
	                           	|	Контрагенты.Ссылка КАК Клиент
	                           	|ИЗ
	                           	|	Справочник.Контрагенты КАК Контрагенты
	                           	|ГДЕ
	                           	|	Контрагенты.Наименование ПОДОБНО ""%"" + &СтрокаПоиска + ""%""";
				   
	ЗапросКонтактнаяИнформация = "
	               |ВЫБРАТЬ
	               |	КонтактнаяИнформация.Объект КАК Клиент
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
				   |	КонтактнаяИнформация.Объект ССЫЛКА Справочник.Контрагенты И
	               |	КонтактнаяИнформация.Представление ПОДОБНО ""%"" + &СтрокаПоиска + ""%""";
				   
	ЗапросГосНомер = "
	               |ВЫБРАТЬ
	               |	Автомобили.Значение КАК Клиент
	               |ИЗ
	               |	РегистрСведений.Автомобили.СрезПоследних(
	               |			,
	               |			ВидЗначения = &Хозяин
	               |				И Автомобиль В
	               |					(ВЫБРАТЬ
	               |						АвтомобилиСрезПоследних.Автомобиль
	               |					ИЗ
	               |						РегистрСведений.Автомобили.СрезПоследних(, Значение ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
	               |							И ВидЗначения = &ГосНомер) КАК АвтомобилиСрезПоследних)) КАК Автомобили";
				   
	ЗапросНомерКарты = "
		           |ВЫБРАТЬ
	               |	ШтрихКоды.Объект.Объект КАК Клиент
	               |ИЗ
	               |	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	               |ГДЕ
				   |	ШтрихКоды.Объект ССЫЛКА Справочник.Карточки И
				   |	ШтрихКоды.Объект.Объект ССЫЛКА Справочник.Контрагенты И
	               |	ШтрихКоды.ШтрихКод ПОДОБНО ""%"" + &СтрокаПоиска + ""%""";
				   
				   
	ЗапросVIN = "
		           |ВЫБРАТЬ
	               |	Автомобили.Значение КАК Клиент
	               |ИЗ
	               |	РегистрСведений.Автомобили.СрезПоследних(
	               |			,
	               |			ВидЗначения = &Хозяин
	               |				И Автомобиль В
	               |					(ВЫБРАТЬ
	               |						Автомобили.Ссылка
	               |					ИЗ
	               |						Справочник.Автомобили КАК Автомобили
	               |					ГДЕ
	               |						Автомобили.VIN ПОДОБНО ""%"" + &СтрокаПоиска + ""%"")) КАК Автомобили";

	ЗапросОбъединить = "
	               |
				   |ОБЪЕДИНИТЬ
				   |";  				   
				   
	ТекстЗапроса = "
					|ВЫБРАТЬ
					|   НайденныеКлиенты.Клиент
					|ПОМЕСТИТЬ ВременнаяТаблицаКлиентов
					|ИЗ
					|	(	";
	
	ЭтоПервыйФрагмент = Истина;
	ЗапросПустой = Истина;
	
	Если НаименованиеКлиента Тогда
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросНаименованиеКлиента;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросНаименованиеКлиента;
		КонецЕсли;
	КонецЕсли;
	
	Если КонтактнаяИнформация Тогда 
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросКонтактнаяИнформация;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросКонтактнаяИнформация;
		КонецЕсли;
	КонецЕсли;
		
	Если ГосНомер Тогда 			  
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросГосНомер;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросГосНомер;
		КонецЕсли;
	КонецЕсли;
		
	Если VIN Тогда 			  
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросVIN;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросVIN;
		КонецЕсли;
	КонецЕсли;
		
	Если НомерКарты Тогда 			  
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросНомерКарты;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросНомерКарты;
		КонецЕсли;
	КонецЕсли;
	
	// пустой текст запроса означает, что сняты все флажки в параметрах поиска
	Если ЗапросПустой Тогда
		Сообщить("Не заданы параметры поиска. Проверьте настройки поиска!");
		Возврат Неопределено;		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса + ") КАК НайденныеКлиенты";
	Запрос.УстановитьПараметр("СтрокаПоиска",СтрокаПоиска);
	Запрос.УстановитьПараметр("ВидКарточки", Перечисления.ВидыКарточек.ДисконтнаяКарта);
	Запрос.УстановитьПараметр("ГосНомер", Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
	Запрос.УстановитьПараметр("Хозяин", Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин);
	
	КлиентыТЗ = Новый ТаблицаЗначений;
	КлиентыТЗ = Запрос.Выполнить().Выгрузить();
	
	// установка отборов на сформированную временную таблицу клиентов
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	ТекстЗапросаИтоговый = "
					|ВЫБРАТЬ
					|	СписокКлиентов.Клиент КАК Клиент
					|ИЗ
					|	(ВЫБРАТЬ Клиент ИЗ ВременнаяТаблицаКлиентов) КАК СписокКлиентов";
	
	ЗапросФлажокТолькоМои = "
		           |(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаявкаНаРемонт.Заказчик КАК Клиент
	               |ИЗ
	               |	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	               |ГДЕ
	               |	ЗаявкаНаРемонт.Автор = &Автор
	               |	И ЗаявкаНаРемонт.Заказчик В(ВЫБРАТЬ Клиент ИЗ ВременнаяТаблицаКлиентов)) КАК ЗапросФлажокТолькоМои
				   |ПО СписокКлиентов.Клиент = ЗапросФлажокТолькоМои.Клиент";
				   
	ЗапросЗаписанныеНаСегодня = "
		           |(ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ЗаявкаНаРемонт.Заказчик КАК Клиент 
				   |ИЗ
				   |	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
				   |ГДЕ
				   |	(ЗаявкаНаРемонт.ДатаНачала < &НачалоДня
				   |				И ЗаявкаНаРемонт.ДатаОкончания > &НачалоДня
				   |			ИЛИ ЗаявкаНаРемонт.ДатаНачала < &КонецДня
				   |				И ЗаявкаНаРемонт.ДатаОкончания > &КонецДня
				   |			ИЛИ ЗаявкаНаРемонт.ДатаНачала >= &НачалоДня
				   |				И ЗаявкаНаРемонт.ДатаОкончания <= &КонецДня)
				   |	И ЗаявкаНаРемонт.Заказчик В(ВЫБРАТЬ Клиент ИЗ ВременнаяТаблицаКлиентов)) КАК ЗапросЗаписанныеНаСегодня
				   |ПО СписокКлиентов.Клиент = ЗапросЗаписанныеНаСегодня.Клиент";
				   
	ЗапросОжидающиеВыдачи = "	
		           	   |(ВЫБРАТЬ РАЗЛИЧНЫЕ
					   |	ЗаказНаряд.Заказчик КАК Клиент
					   |ИЗ
					   |	Документ.ЗаказНаряд КАК ЗаказНаряд
					   |ГДЕ
					   |	ЗаказНаряд.Заказчик В(ВЫБРАТЬ Клиент ИЗ ВременнаяТаблицаКлиентов)
					   |	И ЗаказНаряд.Состояние = &Состояние) КАК ЗапросОжидающиеВыдачи
					   |ПО СписокКлиентов.Клиент = ЗапросОжидающиеВыдачи.Клиент";
					   
   	ЗапросСоединение = "
	               |
				   |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
				   |";  				   

	// Только мои
	Если ФлажокТолькоМои Тогда
		ТекстЗапросаИтоговый = ТекстЗапросаИтоговый + ЗапросСоединение + ЗапросФлажокТолькоМои;
		Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.Пользователь);
	КонецЕсли;

	// Записанные на сегодня
	Если ФлажокЗаписанныеНаСегодня Тогда
		ТекстЗапросаИтоговый = ТекстЗапросаИтоговый + ЗапросСоединение + ЗапросЗаписанныеНаСегодня;
		Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДата()));
		Запрос.УстановитьПараметр("КонецДня", Конецдня(ТекущаяДата()));
	КонецЕсли;

	// Клиенты на сервисе
	
	// Ожидающие выдачи
	Если ФлажокОжидающиеВыдачи Тогда
		ТекстЗапросаИтоговый = ТекстЗапросаИтоговый + ЗапросСоединение + ЗапросОжидающиеВыдачи;
		Запрос.УстановитьПараметр("Состояние", Справочники.ВидыСостоянийЗаказНарядов.Выполнен);
	КонецЕсли;

	ТекстЗапросаИтоговый = ТекстЗапросаИтоговый + " УПОРЯДОЧИТЬ ПО Клиент";
	
	Запрос.Текст = ТекстЗапросаИтоговый;
    ВыборкаТЗ = Запрос.Выполнить().Выгрузить();
	СписокКлиентов = Новый СписокЗначений;
	СписокКлиентов.ЗагрузитьЗначения(ВыборкаТЗ.ВыгрузитьКолонку("Клиент"));
	Возврат СписокКлиентов;
КонецФункции

Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


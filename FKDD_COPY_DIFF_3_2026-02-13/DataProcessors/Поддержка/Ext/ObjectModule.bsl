
Перем СоответствиеАнглийскихФразРусским; // Соответствие английских фраз русским


//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СПЕЦИАЛЬНОГО НАЗНАЧЕНИЯ
//////////////////////////////////////////////////////////////////////////////////

#Если Клиент Тогда
	
// формирование тела post запроса
Функция СформироватьТелоPostЗапроса(СтруктураПараметров) Экспорт
		
		Тело="";
		Для Каждого Параметр Из СтруктураПараметров Цикл
			Тело=Тело + "&" + Параметр.Ключ + "=";
			ЗначениеПараметра=Параметр.Значение;
			Для й=1 По СтрДлина(ЗначениеПараметра) Цикл
				СимволПараметра=Сред(ЗначениеПараметра, й, 1);
				Если СимволПараметра=" " Тогда
					Тело=Тело + "+";
				ИначеЕсли СимволПараметра="&" Тогда
					Тело=Тело + "%26";
				ИначеЕсли СимволПараметра="%" Тогда
					Тело=Тело + "%3D";
				ИначеЕсли СимволПараметра=Символы.ВК Тогда
					Тело=Тело + "%0D";
				ИначеЕсли СимволПараметра=Символы.ПС Тогда
					Тело=Тело + "%0A";				
				ИначеЕсли СимволПараметра="%" Тогда
					Тело=Тело + "%25";
				ИначеЕсли СимволПараметра="\" Тогда
					Тело=Тело + "%5C";
				ИначеЕсли СимволПараметра="'" Тогда
					Тело=Тело + "%27";
				ИначеЕсли СимволПараметра="""" Тогда
					Тело=Тело + "%22";
				ИначеЕсли СимволПараметра="+" Тогда
					Тело=Тело + "%2B";			
				Иначе
					Тело=Тело + СимволПараметра;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;  
		
		Возврат Тело;	
	КонецФункции // ПолучитьТелоPostЗапроса()

// Функция предназначена для получения информации о системе, для отправки в службу поддержки программы.
//
// Параметры:
//  СтруктураПараметров – "Структура" - Фильтр информации о системе.
//  ТекущийОбъект       - "Ссылка"    - Задает ссылку на произвольный объект, информацию о котором необходимо получить.
//
// Возвращаемое значение:
//   Текст - сформированный текст.
// 
Функция СборИнформацииОСистеме(СтруктураПараметров, ТекущийОбъект=Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Текст", ""); 
	Результат.Вставить("СписокФайлов", Новый Массив); // Полные пути к файлам.
	Результат.Вставить("СписокОшибок", Новый Массив); // Список возникших ошибок.
	
	СчетчикБлоков = 1;
	
	// 1. ОБЩАЯ ИНФОРМАЦИЯ.
	Если СтруктураПараметров.Свойство("РегистрационныеДанные") Или СтруктураПараметров.Свойство("ОбщиеСведения") Тогда
		
		ТекстБлока = "";
		
		Если СтруктураПараметров.Свойство("РегистрационныеДанные") Тогда
			ВремТекст = ПолучитьИнформациюИзРегистрационнойАнкеты();
			Если НЕ ПустаяСтрока(ВремТекст) Тогда
				ТекстБлока = ТекстБлока + " " + "Регистрационные данные:";
				ТекстБлока = ТекстБлока + Символы.ПС;
				ТекстБлока = ТекстБлока + ВремТекст;
				ТекстБлока = ТекстБлока + Символы.ПС;
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ОбщиеСведения") Тогда
			ВремТекст = ПолучитьОбщиеСведения();
			
			Если НЕ ПустаяСтрока(ВремТекст) Тогда
				Если НЕ ПустаяСтрока(ТекстБлока) Тогда
					ТекстБлока = ТекстБлока + Символы.ПС;		            
				КонецЕсли;
				ТекстБлока = ТекстБлока + " Общие сведения о программе и системе:";
				ТекстБлока = ТекстБлока + Символы.ПС;
				ТекстБлока = ТекстБлока + ВремТекст;
				ТекстБлока = ТекстБлока + Символы.ПС;
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ИнформацияОбУстановленныхСоединениях") Тогда
			ВремТекст = ПолучитьИнформациюОбУстановленныхСоединениях(Результат);
			
			Если НЕ ПустаяСтрока(ВремТекст) Тогда
				Если НЕ ПустаяСтрока(ТекстБлока) Тогда
					ТекстБлока = ТекстБлока + Символы.ПС;		            
				КонецЕсли;
				ТекстБлока = ТекстБлока + " Информация об установленных соединениях с текущей ИБ:";
				ТекстБлока = ТекстБлока + Символы.ПС;
				ТекстБлока = ТекстБлока + ВремТекст;
				ТекстБлока = ТекстБлока + Символы.ПС;
			КонецЕсли;
		КонецЕсли;

		
		// Если какая-то информация есть, то формируем текст информационного раздела.
		Если НЕ ПустаяСтрока(ТекстБлока) Тогда			
			Результат.Текст = Результат.Текст + СчетчикБлоков + ". ИНФОРМАЦИЯ О ПРОГРАММЕ";
			Результат.Текст = Результат.Текст + Символы.ПС;
			Результат.Текст = Результат.Текст + ТекстБлока;
			СчетчикБлоков = СчетчикБлоков + 1;
		КонецЕсли;                            		
	КонецЕсли;
	
	// 2. ТЕХНИЧЕСКИЕ СВЕДЕНИЯ
	Если СтруктураПараметров.Свойство("ИнформацияОФайлахЗащиты") Или СтруктураПараметров.Свойство("ИнформацияОКлючеЗащиты") 
		Или СтруктураПараметров.Свойство("ХарактеристикиКомпьютера") Или СтруктураПараметров.Свойство("ЗапущенныеПроцессы") Тогда
		
		ТекстБлока = "";

		// Получим информацию по всем подразделам, используя компоненту.
		МассивПараметров = Новый Массив;
		Если СтруктураПараметров.Свойство("ИнформацияОФайлахЗащиты") Тогда
			МассивПараметров.Добавить("InformationAboutFiles");			
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ИнформацияОКлючеЗащиты") Тогда
			МассивПараметров.Добавить("InformationAboutProtection");			
		КонецЕсли;

		Если СтруктураПараметров.Свойство("ХарактеристикиКомпьютера") Тогда
			МассивПараметров.Добавить("Win32_Processor");	
			МассивПараметров.Добавить("Win32_PhysicalMemory");	
			МассивПараметров.Добавить("Win32_DiskDrive");	
			МассивПараметров.Добавить("Win32_LogicalDisk");	
			//МассивПараметров.Добавить("Win32_OperatingSystem");	
		КонецЕсли;

		Если СтруктураПараметров.Свойство("ЗапущенныеПроцессы") Тогда
			МассивПараметров.Добавить("Win32_Process");			
		КонецЕсли;

		Попытка
			Если глРарус_Компонента=Неопределено Тогда
				СтруктураРезультат = Неопределено;
				ПолучитьПараметрыСистемы = Ложь;
            Иначе
				СтруктураРезультат = глРарус_Компонента.ПолучитьПараметрыСистемы(МассивПараметров);
				ПолучитьПараметрыСистемы = Истина;
			КонецЕсли; 
		Исключение
			Сообщить("При сборе информации о системе произошла ошибка: " + ОписаниеОшибки(), СтатусСообщения.Внимание);
			ПолучитьПараметрыСистемы = Ложь;
		КонецПопытки;
		
		Если ПолучитьПараметрыСистемы Тогда
			// Информация о файлах защиты и драйверов.
			Если СтруктураПараметров.Свойство("ИнформацияОФайлахЗащиты") Тогда
				ВремТекст = ПолучитьТекстСИнформациейФайлахЗащитыИДрайверов(СтруктураРезультат);
				Если НЕ ПустаяСтрока(ВремТекст) Тогда
					ТекстБлока = ТекстБлока + " Информация о файлах защиты и драйверов:";
					ТекстБлока = ТекстБлока + Символы.ПС;
					ТекстБлока = ТекстБлока + ВремТекст;
					ТекстБлока = ТекстБлока + Символы.ПС;
				КонецЕсли;
			КонецЕсли;
			
			// Информация о ключе защиты
			Если СтруктураПараметров.Свойство("ИнформацияОКлючеЗащиты") Тогда
				ВремТекст = ПолучитьТекстСИнформациейОКлючеЗащиты(СтруктураРезультат);			
				Если НЕ ПустаяСтрока(ВремТекст) Тогда
					Если НЕ ПустаяСтрока(ТекстБлока) Тогда
						ТекстБлока = ТекстБлока + Символы.ПС;		            
					КонецЕсли;
					ТекстБлока = ТекстБлока + " Информация о ключе защиты:";
					ТекстБлока = ТекстБлока + Символы.ПС;
					ТекстБлока = ТекстБлока + ВремТекст;
					ТекстБлока = ТекстБлока + Символы.ПС;
				КонецЕсли;
			КонецЕсли;
			
			// Характеристики компьютера
			Если СтруктураПараметров.Свойство("ХарактеристикиКомпьютера") Тогда
				ВремТекст = ПолучитьТекстСИнформациейОКомпьютере(СтруктураРезультат);
				
				Если НЕ ПустаяСтрока(ВремТекст) Тогда
					Если НЕ ПустаяСтрока(ТекстБлока) Тогда
						ТекстБлока = ТекстБлока + Символы.ПС;		            
					КонецЕсли;
					ТекстБлока = ТекстБлока + ВремТекст;
					ТекстБлока = ТекстБлока + Символы.ПС;
				КонецЕсли;
			КонецЕсли;
			
			// Запущенные процессы
			Если СтруктураПараметров.Свойство("ЗапущенныеПроцессы") Тогда
				ВремТекст = ПолучитьТекстСИнформациейОЗапущенныхПроцессах(СтруктураРезультат);
				
				Если НЕ ПустаяСтрока(ВремТекст) Тогда
					Если НЕ ПустаяСтрока(ТекстБлока) Тогда
						ТекстБлока = ТекстБлока + Символы.ПС;		            
					КонецЕсли;
					ТекстБлока = ТекстБлока + " Запущенные процессы:";
					ТекстБлока = ТекстБлока + Символы.ПС;
					ТекстБлока = ТекстБлока + ВремТекст;
					ТекстБлока = ТекстБлока + Символы.ПС;
				КонецЕсли;
			КонецЕсли;
			
			// Если какая-то информация есть, то формируем текст информационного раздела.
			Если НЕ ПустаяСтрока(ТекстБлока) Тогда
				Если НЕ ПустаяСтрока(Результат.Текст) Тогда
					Результат.Текст = Результат.Текст + Символы.ПС;
					Результат.Текст = Результат.Текст + Символы.ПС;
				КонецЕсли;
				Результат.Текст = Результат.Текст + СчетчикБлоков + ". ТЕХНИЧЕСКИЕ ХАРАКТЕРИСТИКИ СИСТЕМЫ";
				Результат.Текст = Результат.Текст + Символы.ПС;
				Результат.Текст = Результат.Текст + ТекстБлока;
				СчетчикБлоков = СчетчикБлоков + 1;
			КонецЕсли;                            		
		КонецЕсли; 
	КонецЕсли;
	
	
	// 3. ДОПОЛНИТЕЛЬНЫЕ СВЕДЕНИЯ 
	
	// Информация об текущем объекте.
	Если СтруктураПараметров.Свойство("ИнформацияОТекущемОбъекте") Тогда
		ТекущийОбъект = СтруктураПараметров.ИнформацияОТекущемОбъекте;
		Если ТекущийОбъект<>Неопределено Тогда
			ПолныйПутьКФайлу = ПолучитьИнформациюОТекущемОбъекте(ТекущийОбъект);
			Если НЕ ПустаяСтрока(ПолныйПутьКФайлу) Тогда
				Результат.СписокФайлов.Добавить(ПолныйПутьКФайлу);		
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Выгрузка из журнала регистрации.
	Если СтруктураПараметров.Свойство("ИнформацияЖурналаРегистрации") Тогда
		Фильтр = СтруктураПараметров.ИнформацияЖурналаРегистрации;
		
		ПолныйПутьКФайлу = ПолучитьИнформациюИзЖурналаРегистрации(Фильтр);
		Если НЕ ПустаяСтрока(ПолныйПутьКФайлу) Тогда
			Результат.СписокФайлов.Добавить(ПолныйПутьКФайлу);		
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;	
КонецФункции // СборИнформацииОСистеме() 

// Функция собирает информация из регистрационной анкеты.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Строка - информация из регистрационной анкеты.
//
Функция ПолучитьИнформациюИзРегистрационнойАнкеты()
	Текст = "";
	
	// Получать будем из регистра сведений
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КартинкиИФайлы.Данные
	|ИЗ
	|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	|ГДЕ
	|	КартинкиИФайлы.Объект = &Объект
	|	И КартинкиИФайлы.Идентификатор = &Идентификатор";
	Запрос.УстановитьПараметр("Объект",        Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка);
	Запрос.УстановитьПараметр("Идентификатор", "РегистрационныеДанные");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Структура = Выборка.Данные.Получить();
		Если ТипЗнч(Структура)=Тип("Структура") Тогда
			Текст = Текст + " - Организация: "                + Структура.НаименованиеОрганизации + Символы.ПС;
			Текст = Текст + " - Руководитель: "               + Структура.РуководительОрганизации + Символы.ПС;
			Текст = Текст + " - Ответственный сотрудник: "    + Структура.ОтветственныйЗаРаботу   + Символы.ПС;
			Текст = Текст + " - Почтовый адрес организации: " + Структура.ПочтовыйАдрес           + Символы.ПС;
			Текст = Текст + " - Почтовый индекс: "            + Структура.ПочтовыйИндекс          + Символы.ПС;
			Текст = Текст + " - Телефон для связи: "          + Структура.Телефон                 + Символы.ПС;
			Текст = Текст + " - Факс: "                       + Структура.Факс                    + Символы.ПС;
			Текст = Текст + " - E-mail: "                     + Структура.ЭлектроннаяПочта        + Символы.ПС;
			Текст = Текст + " - Серийный номер программы: "   + Структура.СерийныйНомер;
		КонецЕсли;      				 		
	КонецЕсли;
	
	Возврат Текст;
КонецФункции // элПолучитьИнформациюИзРегистрационнойАнкеты()

// Функция получает необходимую информацию о программе.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Строка - информация о программе.
//
Функция ПолучитьОбщиеСведения()
	Текст = "";
	Текст = Текст + " - Конфигурация: " + Метаданные.Имя  + " (" + Метаданные.Синоним + ")";	
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Поставщик: " + Метаданные.Поставщик;
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Версия: " + Метаданные.Версия;	
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Пользователь ИБ: " + ИмяПользователя();	 	
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Полное имя пользователя ИБ: " + ПолноеИмяПользователя();
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Каталог временных файлов: " + КаталогВременныхФайлов();    
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Код локализации: " + КодЛокализации();
	Текст = Текст + Символы.ПС;	
	Текст = Текст + " - Строка соединения ИБ: " + СтрокаСоединенияИнформационнойБазы(); 
	
	// Теперь добавим информацию, полученную компонентой.
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить("GeneralInformation");
	Попытка
		Если глРарус_Компонента=Неопределено Тогда
			СтруктураРезультат = Неопределено;
			ПолучитьПараметрыСистемы = Ложь;
        Иначе
			СтруктураРезультат = глРарус_Компонента.ПолучитьПараметрыСистемы(МассивПараметров);
			ПолучитьПараметрыСистемы = Истина;
		КонецЕсли; 
	Исключение
		Сообщить("При получении общих сведений о системе произошла ошибка: " + ОписаниеОшибки(), СтатусСообщения.Внимание);
		ПолучитьПараметрыСистемы = Ложь;
	КонецПопытки;
	Если ПолучитьПараметрыСистемы Тогда
		Если СтруктураРезультат.Свойство("GeneralInformation") Тогда  	
			ТаблицаСИнформацией = СтруктураРезультат.GeneralInformation;		
			Для каждого ТекСтрока Из ТаблицаСИнформацией Цикл  			
				Если ТекСтрока.value1="No" Тогда
					ТекСтрока.value1 = "Нет"; 				
				ИначеЕсли ТекСтрока.value1="Yes" Тогда
					ТекСтрока.value1 = "Да";  				
				КонецЕсли;
				
				ТекСтрока.name = ПолучитьПеревод(ТекСтрока.name);
				
				Если НЕ ПустаяСтрока(ТекСтрока.name) Тогда
					Текст = Текст + Символы.ПС;	
					Текст = Текст + " - " + ТекСтрока.name + ": " + ТекСтрока.value1;  
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Текст;
КонецФункции // ПолучитьОбщиеСведения()

// Получает информацию об установленных соединениях с текущей ИБ
//
// Параметры:
//  Результат - "Структура" - Структура содержащая результирующие данные.
//
// Возвращаемое значение:
//   Строка - Результирующая информация
// 
Функция ПолучитьИнформациюОбУстановленныхСоединениях(Результат)
	
	Текст = "";
	// Проверим право на получение информации.
	Если ПравоДоступа("АктивныеПользователи", Метаданные) Тогда
		МассивСоединений = ПолучитьСоединенияИнформационнойБазы();
	Иначе
		Результат.СписокОшибок = "Недостаточно прав для получения информации об установленных соединениях с ИБ";
		Возврат "";
	КонецЕсли;
	
	Сч = 1;
	Для каждого ТекСоединение Из МассивСоединений Цикл  	
		Если НЕ ПустаяСтрока(Текст) Тогда
			Текст = Текст + Символы.ПС;	
		КонецЕсли;
		
		Текст = Текст + " - Соединение " + Сч + " (Имя пользователя: " + ТекСоединение.Пользователь.ПолноеИмя + "; ";
		Текст = Текст + "Имя компьютера: " + ТекСоединение.ИмяКомпьютера + "; " ;
		Текст = Текст + "Режим: " + ?(ТекСоединение.ИмяПриложения="Designer", "Конфигуратор", "Предприятие") + ")";
		Сч = Сч + 1;
	КонецЦикла;
	
	Возврат Текст;
КонецФункции // ПолучитьИнформациюОбУстановленныхСоединениях() 

// Функция возвращает текст с информацией о файлах защиты и драйверов.
//
// Параметры:
//  СтруктураРезультат - "Структура" - Структура, в которой содержит необходимая информация.
//
// Возвращаемое значение:
//   Текст
// 
Функция ПолучитьТекстСИнформациейФайлахЗащитыИДрайверов(СтруктураРезультат)
	Текст = "";
	Если НЕ СтруктураРезультат.Свойство("InformationAboutFiles") Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаСИнформацией = СтруктураРезультат.InformationAboutFiles;
	
	Для каждого ТекСтрока Из ТаблицаСИнформацией Цикл  	
		ТекСтрока.name = СокрЛП(ТекСтрока.name);
		ТекСтрока.version = СокрЛП(ТекСтрока.version);
		Если ПустаяСтрока(ТекСтрока.version) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Текст<>"" Тогда
			Текст = Текст + Символы.ПС;
		КонецЕсли;
		
		Текст = Текст + " - " + ТекСтрока.name + ": " + ТекСтрока.version;		
	КонецЦикла;
	
	Возврат Текст;	
КонецФункции // ПолучитьТекстСИнформациейФайлахЗащитыИДрайверов() 
   
// Функция возвращает текст с информацией об установленных ключах защиты.
//
// Параметры:
//  СтруктураРезультат - "Структура" - Структура, в которой содержит необходимая информация.
//
// Возвращаемое значение:
//   Текст
// 
Функция ПолучитьТекстСИнформациейОКлючеЗащиты(СтруктураРезультат)
	Текст = "";
	
	Если НЕ СтруктураРезультат.Свойство("InformationAboutProtection") Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаСИнформацией = СтруктураРезультат.InformationAboutProtection;
	
	Для каждого ТекСтрока Из ТаблицаСИнформацией Цикл  	
		ТекСтрока.name    = ПолучитьПеревод(ТекСтрока.name);
		Если ПустаяСтрока(ТекСтрока.name) Тогда
			Продолжить;
		КонецЕсли;
		Если Текст<>"" Тогда
			Текст = Текст + Символы.ПС;
		КонецЕсли;
		Текст = Текст + " - " + ТекСтрока.name + ": " + ТекСтрока.value1;				
	КонецЦикла; 	
	
	Возврат Текст;	
КонецФункции // ПолучитьТекстСИнформациейОКлючеЗащиты() 

// Функция возвращает текст с информацией о запущенных процессах.
//
// Параметры:
//  СтруктураРезультат - "Структура" - Структура, в которой содержит необходимая информация.
//
// Возвращаемое значение:
//   Текст
// 
Функция ПолучитьТекстСИнформациейОЗапущенныхПроцессах(СтруктураРезультат)
	Текст = "";
	
	Если НЕ СтруктураРезультат.Свойство("Win32_Process") Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаСИнформацией = СтруктураРезультат.Win32_Process;
	
	// Найдем строку со значением колонки "name" равным "Name".
	СтрокаСИменами = ТаблицаСИнформацией.Найти("Name", "name");
	Если СтрокаСИменами=Неопределено Тогда
		Возврат "";
	КонецЕсли;
		
	Для Сч=1 По (ТаблицаСИнформацией.Колонки.Количество()-1) Цикл 	
		Если Текст<>"" Тогда
			Текст = Текст + Символы.ПС;
		КонецЕсли;
		
		Текст = Текст + " - " + СтрокаСИменами[Сч];
	КонецЦикла;
	
	Возврат Текст;	
КонецФункции // ПолучитьТекстСИнформациейОЗапущенныхПроцессах() 

// Функция возвращает текст с информацией о характеристиках компьютера.
//
// Параметры:
//  СтруктураРезультат - "Структура" - Структура, в которой содержит необходимая информация.
//
// Возвращаемое значение:
//   Текст
// 
Функция ПолучитьТекстСИнформациейОКомпьютере(СтруктураРезультат)
	ИтоговыйТекст = "";
	ВремТекст     = "";
		
	// ПРОЦЕССОРЫ
	Если СтруктураРезультат.Свойство("Win32_Processor") Тогда
		ТаблицаСИнформацией = СтруктураРезультат.Win32_Processor;
		// Найдем нужные нам строки.
		СтрокаСИменами      = ТаблицаСИнформацией.Найти("Name", "name");
		СтрокаСРазрядностью = ТаблицаСИнформацией.Найти("AddressWidth", "name");
		СтрокаСМаксимальнымиЧастотами = ТаблицаСИнформацией.Найти("MaxClockSpeed", "name");
		СтрокаСТекущейЧастотой        = ТаблицаСИнформацией.Найти("CurrentClockSpeed", "name");
		Для Сч=1 По (ТаблицаСИнформацией.Колонки.Количество()-1) Цикл
			Если ВремТекст<>"" Тогда
				ВремТекст = ВремТекст + Символы.ПС;
			КонецЕсли;
			ВремТекст = ВремТекст + " - Процессор " + Сч + " (Наименование: " + СокрЛП(СтрокаСИменами[Сч]) + "; Разрядность: " + СтрокаСРазрядностью[Сч];
			ВремТекст = ВремТекст + "; Максимальная частота: " + СтрокаСМаксимальнымиЧастотами[Сч] + "; Текущая частота: " + СтрокаСТекущейЧастотой[Сч] + ")"
		КонецЦикла; 
		
		Если НЕ ПустаяСтрока(ВремТекст) Тогда
			ИтоговыйТекст = ИтоговыйТекст + " Информация о процессорах:" + Символы.ПС + ВремТекст;	
		КонецЕсли;
	КонецЕсли; 	
	
	// ОПЕРАТИВНАЯ ПАМЯТЬ.
	Если СтруктураРезультат.Свойство("Win32_PhysicalMemory") Тогда
		ТаблицаСИнформацией = СтруктураРезультат.Win32_PhysicalMemory;
		
		ВремТекст = "";
		// Найдем нужные нам строки.
		СтрокаСОбъемомПамяти = ТаблицаСИнформацией.Найти("Capacity", "name");
		Для Сч=1 По (ТаблицаСИнформацией.Колонки.Количество()-1) Цикл
			Если ВремТекст<>"" Тогда
				ВремТекст = ВремТекст + Символы.ПС;
			КонецЕсли;
			ВремТекст = ВремТекст + " - Модуль памяти " + Сч + " (Размер физической памяти: " + (Число(СтрокаСОбъемомПамяти[Сч])/1048576) + " МБ)";
		КонецЦикла;                                                                                                      
		
		Если НЕ ПустаяСтрока(ВремТекст) Тогда
			Если НЕ ПустаяСтрока(ИтоговыйТекст) Тогда
				ИтоговыйТекст = ИтоговыйТекст + Символы.ПС;
				ИтоговыйТекст = ИтоговыйТекст + Символы.ПС;
			КонецЕсли;
			ИтоговыйТекст = ИтоговыйТекст + " Информация об оперативной памяти:" + Символы.ПС + ВремТекст;	
		КонецЕсли;
	КонецЕсли;

	// ЖЕСТКИЕ ДИСКИ.
	Если СтруктураРезультат.Свойство("Win32_DiskDrive") Тогда
		ТаблицаСИнформацией = СтруктураРезультат.Win32_DiskDrive;
		
		ВремТекст = "";		
		// Найдем нужные нам строки.
		СтрокаСНазваниемДиска  = ТаблицаСИнформацией.Найти("Caption", "name");
		СтрокаСТипомИнтерфейса = ТаблицаСИнформацией.Найти("InterfaceType", "name");
		СтрокаСОбъемомПамяти   = ТаблицаСИнформацией.Найти("Size", "name");
		
		Для Сч=1 По (ТаблицаСИнформацией.Колонки.Количество()-1) Цикл
			Если ВремТекст<>"" Тогда
				ВремТекст = ВремТекст + Символы.ПС;
			КонецЕсли;
			ВремТекст = ВремТекст + " - Жесткий диск " + Сч + " (Описание: " + СтрокаСНазваниемДиска[Сч] + "; ";
			ВремТекст = ВремТекст + "Тип интерфейса: " + СтрокаСТипомИнтерфейса[Сч] + "; ";
			ВремТекст = ВремТекст + "Размер диска: " + Окр(Число(СтрокаСОбъемомПамяти[Сч])/1073741824, 0) + " ГБ)";
		КонецЦикла;                                                                                                      
		
		Если НЕ ПустаяСтрока(ВремТекст) Тогда
			Если НЕ ПустаяСтрока(ИтоговыйТекст) Тогда
				ИтоговыйТекст = ИтоговыйТекст + Символы.ПС;
				ИтоговыйТекст = ИтоговыйТекст + Символы.ПС;
			КонецЕсли;
			ИтоговыйТекст = ИтоговыйТекст + " Информация о жестких дисках:" + Символы.ПС + ВремТекст;	
		КонецЕсли;
	КонецЕсли;
	
	// ЛОГИЧЕСКИЕ ДИСКИ.
	Если СтруктураРезультат.Свойство("Win32_LogicalDisk") Тогда
		ТаблицаСИнформацией = СтруктураРезультат.Win32_LogicalDisk;
		
		ВремТекст = "";		
		// Найдем нужные нам строки.
		СтрокаСНазваниемДиска  = ТаблицаСИнформацией.Найти("Name", "name");
		СтрокаСРазмерамиДисков = ТаблицаСИнформацией.Найти("Size", "name");
		СтрокаССвободнымМестом = ТаблицаСИнформацией.Найти("FreeSpace", "name");
		СтрокаСТипомПривода    = ТаблицаСИнформацией.Найти("DriveType", "name");
		
		Для Сч=1 По (ТаблицаСИнформацией.Колонки.Количество()-1) Цикл
			Если СтрокаСТипомПривода[Сч]<>"3" Тогда
				Продолжить;
			КонецЕсли;
			
			Если ВремТекст<>"" Тогда
				ВремТекст = ВремТекст + Символы.ПС;
			КонецЕсли;
			ВремТекст = ВремТекст + " - Логический диск """ + СтрокаСНазваниемДиска[Сч] + """ (";
			ВремТекст = ВремТекст + "Размер диска: " + Окр(Число(СтрокаСРазмерамиДисков[Сч])/1073741824, 0) + " ГБ; ";
			ВремТекст = ВремТекст + "Размер свободного места: " + Окр(Число(СтрокаССвободнымМестом[Сч])/1073741824, 0) + " ГБ)";
		КонецЦикла;                                                                                                      
		
		Если НЕ ПустаяСтрока(ВремТекст) Тогда
			Если НЕ ПустаяСтрока(ИтоговыйТекст) Тогда
				ИтоговыйТекст = ИтоговыйТекст + Символы.ПС;
				ИтоговыйТекст = ИтоговыйТекст + Символы.ПС;
			КонецЕсли;
			ИтоговыйТекст = ИтоговыйТекст + " Информация о логических дисках:" + Символы.ПС + ВремТекст;	
		КонецЕсли;
	КонецЕсли;  
	
	Возврат ИтоговыйТекст;	
КонецФункции // ПолучитьТекстСИнформациейОКомпьютере() 

//Получает информацию о текущем объекте в виде табличного документа (внешний файл).
//
//Параметры:
// ТекущийОбъект – "Произвольный объект" - Любой объект ИБ.
//
//Возвращаемое значение:
//  Строка - Полный путь к файлу с сформированным отчетом по объекту.
//
Функция ПолучитьИнформациюОТекущемОбъекте(ТекущийОбъект)
	
	Попытка
		ОбъектВыгрузкаОбъекта              = Обработки.ВыгрузкаВТабличныйДокумент.Создать();
		ОбъектВыгрузкаОбъекта.ОбъектСсылка = ТекущийОбъект.Ссылка;
		ТаблДокумент                       = Новый ТабличныйДокумент;
		
		ОбъектВыгрузкаОбъекта.ВыгрузитьВТабличныйДокумент(ТаблДокумент, Ложь);	
						
		ИмяФайла = КаталогВременныхФайлов() + "ИнформацияОбОбъекте.html"; 				
		ТаблДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.HTML);
		ТаблДокумент = "";
		Возврат ИмяФайла;		 
	Исключение
		Возврат "";	 
	КонецПопытки;
	
КонецФункции // ПолучитьИнформациюОТекущемОбъекте() 

// Получает выгрузку из журнала регистрации в виде xml файла.
//
// Параметры:
//  Фильтр – "Структура" - Фильтр, накладываемый на выгрузку.
//
// Возвращаемое значение:
//   Строка - Полный путь к файлу xml, содержащему выгрузку из журнала регистрации
// 
Функция ПолучитьИнформациюИзЖурналаРегистрации(Фильтр)
	
	ИмяВыходногоФайла = КаталогВременныхФайлов() + "ЖурналРегистрации.xml";
	
	// Подготовим фильтр.
	ИтоговыйФильтр = Новый Структура;
	Если Фильтр.Свойство("ДатаНачала") Тогда
		Если НЕ обЗначениеНеЗаполнено(Фильтр.ДатаНачала) Тогда
			ИтоговыйФильтр.Вставить("ДатаНачала", Фильтр.ДатаНачала);		
		КонецЕсли;
	КонецЕсли;
	
	Если Фильтр.Свойство("ДатаОкончания") Тогда
		Если НЕ обЗначениеНеЗаполнено(Фильтр.ДатаОкончания) Тогда
			ИтоговыйФильтр.Вставить("ДатаОкончания", Фильтр.ДатаОкончания);		
		КонецЕсли;
	КонецЕсли;
	
	Если Фильтр.Свойство("Пользователи") Тогда
		// Создадим массив на основе списка значений.
		МассивПользователей = Новый Массив;
		Для каждого ТекЭлементСписка Из Фильтр.Пользователи Цикл  	
			Если ТекЭлементСписка.Пометка Тогда
				МассивПользователей.Добавить(ТекЭлементСписка.Значение);	
			КонецЕсли;
		КонецЦикла;
		ИтоговыйФильтр.Вставить("Пользователь", МассивПользователей);	
	КонецЕсли;
	
	Если Фильтр.Свойство("Компьютеры") Тогда
		Если Фильтр.Компьютеры.Количество()<>0 Тогда
			ИтоговыйФильтр.Вставить("Компьютер", Фильтр.Компьютеры);	
		КонецЕсли;
	КонецЕсли;
	
	Если Фильтр.Свойство("Уровни") Тогда
		// Создадим массив на основе списка значений.
		МассивУровней = Новый Массив;
		Для каждого ТекЭлементСписка Из Фильтр.Уровни Цикл  	
			Если ТекЭлементСписка.Пометка Тогда
				МассивУровней.Добавить(ТекЭлементСписка.Значение);	
			КонецЕсли;
		КонецЦикла;
		ИтоговыйФильтр.Вставить("Уровень", МассивУровней);	
	КонецЕсли;
	
	Попытка
		ВыгрузитьЖурналРегистрации(ИмяВыходногоФайла, ИтоговыйФильтр);
		Возврат ИмяВыходногоФайла;
	Исключение
		Возврат "";
	КонецПопытки;
	
КонецФункции // ПолучитьИнформациюИзЖурналаРегистрации() 
   
// Получает перевод для англоязычной строки.
//
// Параметры:
//  СтрокаНаАнгл – "Строка" - фраза на английском.
//
// Возвращаемое значение:
//   Строка - перевод
// 
Функция ПолучитьПеревод(СтрокаНаАнгл)
	ЭлементСписка = СоответствиеАнглийскихФразРусским.НайтиПоЗначению(СтрокаНаАнгл);
	Если ЭлементСписка<>Неопределено Тогда
		Возврат ЭлементСписка.Представление;		
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции // ПолучитьПеревод() 
           
#КонецЕсли 


//////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
//////////////////////////////////////////////////////////////////////////////////

СоответствиеАнглийскихФразРусским = Новый СписокЗначений;
// Общие сведения
СоответствиеАнглийскихФразРусским.Добавить("Platform", "Текущая операционная система");
СоответствиеАнглийскихФразРусским.Добавить("Computer", "Сетевое имя компьютера");
СоответствиеАнглийскихФразРусским.Добавить("IP", "IP");
СоответствиеАнглийскихФразРусским.Добавить("User", "Текущий пользователь Windows");
СоответствиеАнглийскихФразРусским.Добавить("UserAdmin", "Текущий пользователь имеет права администратора");
СоответствиеАнглийскихФразРусским.Добавить("MDac ver", "Версия MDac");
СоответствиеАнглийскихФразРусским.Добавить("Win_Firewall_Service_Started", "Запущен системный брандмауэр");
СоответствиеАнглийскихФразРусским.Добавить("Win_Firewall_Enabled", "Системный брандмауэр активен");
СоответствиеАнглийскихФразРусским.Добавить("Platform80_Installed", "Платформа 8.0 установлена");
СоответствиеАнглийскихФразРусским.Добавить("Platform80_Folder", "Каталог программы 1С:Предприятие 8.0");
СоответствиеАнглийскихФразРусским.Добавить("Platform81_Installed", "Платформа 8.1 установлена");
СоответствиеАнглийскихФразРусским.Добавить("Platform81_Folder", "Каталог программы 1С:Предприятие 8.1");
СоответствиеАнглийскихФразРусским.Добавить("Platform82_Installed", "Платформа 8.2 установлена");
СоответствиеАнглийскихФразРусским.Добавить("Platform82_Folder", "Каталог программы 1С:Предприятие 8.2");
СоответствиеАнглийскихФразРусским.Добавить("ModuleName", "Путь к компоненте");
СоответствиеАнглийскихФразРусским.Добавить("ModuleNameVersion", "Версия компоненты");
СоответствиеАнглийскихФразРусским.Добавить("MainProgramVersion", "Версия платформы");

// Информация о ключе защиты.
СоответствиеАнглийскихФразРусским.Добавить("SerialNumber", "Серийный номер ключа");
СоответствиеАнглийскихФразРусским.Добавить("HardwareNumber", "Аппаратный номер ключа");
СоответствиеАнглийскихФразРусским.Добавить("UserCount", "Количество пользователей (255-без ограничения)");
СоответствиеАнглийскихФразРусским.Добавить("RunCount", "Количество разрешенных запусков (65535-без ограничения)");
СоответствиеАнглийскихФразРусским.Добавить("LicCount", "Количество лицензий на торговое оборудование (255-без ограничения)");
СоответствиеАнглийскихФразРусским.Добавить("CreateDate", "Дата программирования ключа");
СоответствиеАнглийскихФразРусским.Добавить("ExpireDate", "Дата окончания времени действия ключа (30.12.1899-ограничения нет)");
СоответствиеАнглийскихФразРусским.Добавить("InitialDate", "Дата первого запуска программы (30.12.1899-ограничения нет)");
СоответствиеАнглийскихФразРусским.Добавить("WorkDays", "Количество разрешенных дней с первого запуска программы");
СоответствиеАнглийскихФразРусским.Добавить("UpdateNumber", "Номер последнего установленного обновления (0-обновлений не было)");
СоответствиеАнглийскихФразРусским.Добавить("Param1", "Дополнительный параметр лицензирования");

// Модуль обработки
Перем ПрефиксУзлаCML;
Перем НачалоЭлементаCML;
Перем КонецЭлементаCML;
Перем ПрефиксАтрибутаCML;

Перем ПодкаталогКартинок;
Перем ПодкаталогБезопасностиКаталогаВыгрузки;

Перем ПараметрЗапросаHTTP_Инициализация;
Перем ПараметрЗапросаHTTP_ПередачаФайла;
Перем ПараметрЗапросаHTTP_ИмпортФайлаСервером;
Перем ПараметрЗапросаHTTP_ПолучитьДанные; 
Перем ПараметрЗапросаHTTP_ПолнаяВыгрузка;
Перем ПараметрЗапросаHTTP_УспешноеЗавершениеИмпорта;
     
Перем ОтветСервера_ZIPРазрешен;
Перем ОтветСервера_ОграничениеРазмераФрагментаФайлаОбмена;
Перем ОтветСервера_УспешноеЗавершениеТекущейОперации;
Перем ОтветСервера_АварийноеЗавершениеТекущейОперации;
Перем ОтветСервера_ВыполнениеТекущейОперации;

Перем ПустаяХарактеристикаСсылка;

Перем НаименованиеНалога;

Перем НаименованиеКаталогаТоваровCML;
Перем НаименованиеПакетаПредложенийCML;

Перем БулевоЗначениеCML_Истина;
Перем БулевоЗначениеCML_Ложь;
Перем БулевоЗначениеCML_Да;
Перем БулевоЗначениеCML_Нет;

Перем ВидНоменклатурыCML_Услуга;
Перем ВидНоменклатурыCML_Товар;
Перем ЗначениеCML_ТипНоменклатуры;

Перем мСтруктураИнформацииИсторииОбмена;

Перем мТипНоменклатурыУслуга;	
Перем мТипНоменклатурыТовар;
Перем мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом;

Перем мМассивЗагруженныхОбъектов;
Перем мПрефикс;                             // Префикс текущего сайта, используется для префиксации входящего номера документа.
Перем Права;								// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ОшибкиТекстом Экспорт;                // Ошибки в виде текста.
Перем ОшибкиHTMLем 	Экспорт;				// Ошибки в виде html, в случае исключительной ситуации web сервер возвращает причину ошибки и дамп переменных в виде html страницы
Перем ТаблицаФайловОбмена Экспорт;			// Для хранения промежуточных файлов обмена.
Перем ТаблицаУслуг;							// Таблица, в которой хранятся услуги документов

Перем ПодразделениеДляВыборкиТоваров;
Перем КонтрагентДляВыборкиТоваров;
Перем СпособОпределенияЦен;
Перем ПредставленияВалют;
Перем СообщениеОНевыгруженныхТоварах;
Перем ВыгружатьТоварыБезЦены;


///////////////////////////////////////////////////////////////////////////////
// СЕРВИСНЫЕ ПРОЦЕДУРЫ

Процедура ВыполнитьРегистрациюИзмененияОбъектовОбмен(Источник, Отказ, Замещение=Неопределено) Экспорт
	
	Если НЕ обПраво("ОбменССайтомАвтосервиса") Тогда
		Возврат;
	КонецЕсли;
	ТипИсточника = ТипЗнч(Источник);
	
	// объекты на удаление не выгружаем
	Если ТипИсточника=Тип("УдалениеОбъекта") Тогда
		Возврат;
	КонецЕсли;
	
	// В режиме загрузки объекта с сайта автосервиса регистрация изменений не производится
	Если ЗначениеЗаполнено(Источник.ОбменДанными.Отправитель) И ТипЗнч(Источник.ОбменДанными.Отправитель)=Тип("ПланОбменаСсылка.ОбменССайтомАвтосервиса") Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектМетаданных = Источник.Метаданные();
	
	// Регистрация изменений для обмена для групп не производится
	Если Метаданные.Справочники.Содержит(ОбъектМетаданных) И (Источник.ЭтоГруппа ИЛИ Источник.ПометкаУдаления) Тогда
		Возврат;
	КонецЕсли;
	
	// Регистрация изменений для не проведенных операций не осуществляется
	Если Метаданные.Документы.Содержит(ОбъектМетаданных) И (НЕ Источник.Проведен) Тогда
		Возврат;
	КонецЕсли;
	
	// Регистрацию изменений пустых регистров не производим
	Если Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) И Источник.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим возможность выгрузки текущего объекта
	Если ТипИсточника=Тип("СправочникОбъект.Контрагенты") Тогда
		ВыгружатьНаСайт = Источник.ВыгружатьАвтомобилиИЗаказНарядыНаСайт ИЛИ ЗначениеЗаполнено(СокрЛП(Источник.GUIDСайта));
		
	ИначеЕсли ТипИсточника=Тип("ДокументОбъект.ЗаявкаНаРемонт") И ТипЗнч(Источник.Заказчик)=Тип("СправочникСсылка.Контрагенты") ИЛИ ТипИсточника=Тип("ДокументОбъект.ЗаказНаряд") Тогда
		ВыгружатьНаСайт = Источник.Заказчик.ВыгружатьАвтомобилиИЗаказНарядыНаСайт;
		
	ИначеЕсли ТипИсточника=Тип("СправочникОбъект.Автомобили") ИЛИ ТипИсточника=Тип("РегистрСведенийНаборЗаписей.Автомобили") Тогда
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ
		|	АвтомобилиСрезПоследних.Значение,
		|	АвтомобилиСрезПоследних.Значение.ВыгружатьАвтомобилиИЗаказНарядыНаСайт КАК ВыгружатьАвтомобилиИЗаказНарядыНаСайт
		|ИЗ
		|	РегистрСведений.Автомобили.СрезПоследних(
		|			,
		|			Автомобиль = &Автомобиль
		|				И ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК АвтомобилиСрезПоследних";
		Запрос.УстановитьПараметр("Автомобиль", ?(ТипИсточника=Тип("СправочникОбъект.Автомобили"), Источник.Ссылка, Источник.Отбор.Автомобиль.Значение));
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ВыгружатьНаСайт = Выборка.ВыгружатьАвтомобилиИЗаказНарядыНаСайт;
		Иначе
			// У автомобиля нет владельца
			Возврат;
		КонецЕсли;
	Иначе
		ВыгружатьНаСайт = ИСТИНА;
	КонецЕсли;
	
	Если НЕ ВыгружатьНаСайт Тогда
		Возврат;
	КонецЕсли;
	
	// Зарегистрируем объект для обмена с сайтом
	МассивУзлов = Новый Массив;
	ВыборкаУзлов = ПланыОбмена.ОбменССайтомАвтосервиса.Выбрать();
	Пока ВыборкаУзлов.Следующий() Цикл
		// Производим обмен данными со всеми узлами, кроме текущего (ЭтотУзел) и узла отправителя
		Если ВыборкаУзлов.Ссылка<>ПланыОбмена.ОбменССайтомАвтосервиса.ЭтотУзел() И ВыборкаУзлов.Ссылка<>Источник.ОбменДанными.Отправитель Тогда
			МассивУзлов.Добавить(ВыборкаУзлов.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Источник);
	
КонецПроцедуры // ВыполнитьРегистрациюИзмененияОбъектовОбмен()

Процедура ВыполнитьОбновлениеКонфигурацииПереходОтСвойствОбъектовКРеквизитам() Экспорт

	СвойствоВходящийНомер=ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию("Входящий номер");
	
	Если НЕ ЗначениеЗаполнено(СвойствоВходящийНомер) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если НЕ СвойствоВходящийНомер.ПометкаУдаления Тогда
			ВходящийНомерОбъект = СвойствоВходящийНомер.ПолучитьОбъект();
			ВходящийНомерОбъект.УстановитьПометкуУдаления(ИСТИНА);
			Сообщить("Свойство """+СвойствоВходящийНомер+""" помечено на удаление.");
		КонецЕсли;
	Исключение
		ТекстСообщения = "Ошибка при удалении свойства объекта """+СвойствоВходящийНомер+""": " + ОписаниеОшибки();
		Сообщить(ТекстСообщения);
		ЗаписьЖурналаРегистрации("ОшибкаПриОбновленииСвойствОбъектов", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
	КонецПопытки;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Объект КАК Объект,
	|	ЗначенияСвойствОбъектов.Свойство,
	|	ЗначенияСвойствОбъектов.Значение,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ЗначенияСвойствОбъектов.Объект) = ТИП(Справочник.Контрагенты) ТОГДА
	|		0
	|	КОГДА ТИПЗНАЧЕНИЯ(ЗначенияСвойствОбъектов.Объект) = ТИП(Справочник.Автомобили) ТОГДА
	|		1
	|	КОГДА ТИПЗНАЧЕНИЯ(ЗначенияСвойствОбъектов.Объект) = ТИП(Документ.ЗаявкаНаРемонт) ТОГДА
	|		2
	|	КОГДА ТИПЗНАЧЕНИЯ(ЗначенияСвойствОбъектов.Объект) = ТИП(Документ.ЗаказПокупателя) ТОГДА
	|		3
	|	ИНАЧЕ
	|		4
	|	КОНЕЦ КАК ПорядокВыборки
	|ИЗ
	|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Свойство = &Свойство
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокВыборки,
	|	Объект";
	
	Запрос.УстановитьПараметр("Свойство", СвойствоВходящийНомер);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
			ВыборкаОбъект=Выборка.Объект.ПолучитьОбъект();
			ВыборкаОбъект.ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
		ИначеЕсли ТипЗнч(Выборка.Объект) = Тип("СправочникСсылка.Автомобили") Тогда
			ВыборкаОбъект=Выборка.Объект.ПолучитьОбъект();
		ИначеЕсли ТипЗнч(Выборка.Объект) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
			ВыборкаОбъект=Выборка.Объект.ПолучитьОбъект();
		ИначеЕсли ТипЗнч(Выборка.Объект) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ВыборкаОбъект=Выборка.Объект.ПолучитьОбъект();
		Иначе 
			Продолжить;
		КонецЕсли;
		
		ВыборкаОбъект.GUIDСайта = Выборка.Значение;
		ВыборкаОбъект.ОбменДанными.Загрузка=Истина;
		
		Попытка
			ВыборкаОбъект.Записать();
			Сообщить("Обновлен <"+ВыборкаОбъект+">.");
		Исключение
			ТекстСообщения = "Не удалось обновить <"+ВыборкаОбъект+">. Ошибка : "+ОписаниеОшибки();
			Сообщить(ТекстСообщения);
			ЗаписьЖурналаРегистрации("ОшибкаПриОбновленииСвойствОбъектов", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
		КонецПопытки; 
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоВходящийНомер);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	Попытка
		НаборЗаписей.Записать();
		Сообщить("Обновлены свойства объекта");
	Исключение
		ТекстСообщения = "Ошибка при обновлении свойств объекта """+СвойствоВходящийНомер+""": " + ОписаниеОшибки();
		Сообщить(ТекстСообщения);
		ЗаписьЖурналаРегистрации("ОшибкаПриОбновленииСвойствОбъектов", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
//  ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТОВ

// Проверяет. можно ли использовать переданный договор в соответствии с переданными параметрами.
//
// Параметры
//  Договор                                 – ссылка на договор, который нужно проверить,
//  Контрагент                              - ссылка на контрагента, которому должен принадлежать договор,
//  Организация                             - ссылка на организацию, от имени которой должен быть выписан договор,
//  СтруктураПараметровДляПолученияДоговора - структура, содержащая параметры для определения договора:
//                                            список допустимых видов договоров и
//                                            список допустимых способов ведения взаиморасчетов.
//
// Возвращаемое значение:
//   Логическое, Истина - можно использовать, ложь - нельзя.
//
Функция МожноИспользоватьДоговорДляДокумента(Договор, Контрагент, Организация, СтруктураПараметровДляПолученияДоговора) Экспорт

	Перем СписокДопустимыхВидовДоговоров, СписокДопустимыхВидовВзаиморасчетов, ВалютаВзаиморасчетовДоговора, ВидСравненияВалютыВзаиморасчетов;


	Если ( обЗначениеНеЗаполнено(Договор) ) Тогда
		Возврат Ложь; // Если не передали параметры, то считаем, что нельзя использовать.
	КонецЕсли;

	Если ( СтруктураПараметровДляПолученияДоговора <> Неопределено ) Тогда
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовДоговоров", СписокДопустимыхВидовДоговоров);
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовВзаиморасчетов", СписокДопустимыхВидовВзаиморасчетов);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВалютаВзаиморасчетовДоговора", ВалютаВзаиморасчетовДоговора);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВидСравненияВалютыВзаиморасчетов", ВидСравненияВалютыВзаиморасчетов);
	КонецЕсли;

	// Организация должна совпадать.
	Если ( НЕ обЗначениеНеЗаполнено(Организация) И Организация <> Договор.Организация ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Контрагент должен совпадать, если в документе не выбран контрагент, то любой договор не подходит.
	Если ( Контрагент <> Договор.Владелец ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Проверка по виду договора.
	Если ( СписокДопустимыхВидовДоговоров <> Неопределено 
	   И СписокДопустимыхВидовДоговоров.НайтиПоЗначению(Договор.ВидДоговора) = Неопределено ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Проверка по виду взаиморасчетов.
	Если ( СписокДопустимыхВидовВзаиморасчетов <> Неопределено 
	   И СписокДопустимыхВидовВзаиморасчетов.НайтиПоЗначению(Договор.ВедениеВзаиморасчетов) = Неопределено ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Проверка по валюте взаиморасчетов.
	Если ( ВалютаВзаиморасчетовДоговора <> Неопределено
	   И ВидСравненияВалютыВзаиморасчетов <> Неопределено ) Тогда
		Если ( ВидСравненияВалютыВзаиморасчетов = "=" ) Тогда
			Если ( Договор.ВалютаВзаиморасчетов <> ВалютаВзаиморасчетовДоговора ) Тогда
				Возврат Ложь;
			КонецЕсли;
		Иначе // ВидСравненияВалютыВзаиморасчетов может принимать только два значения: "=" и "<>".
			Если ( Договор.ВалютаВзаиморасчетов = ВалютаВзаиморасчетовДоговора ) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Истина;

КонецФункции // МожноИспользоватьДоговорДляДокумента()

// Функция возвращает таблицу договоров данного контрагента, доступных для выбора
//
// Параметры
//  Контрагент                              - контрагент, по которому определяется договор
//  СтруктураПараметровДляПолученияДоговора - структура, содержащая параметры для определения договора:
//                                            список допустимых видов договоров и
//                                            список допустимых способов ведения взаиморасчетов, не обязательный.
// 	ВозвращатьТолькоПервые                  - булево, Истина - нужно вернуть только первые два договора,
//                                          - ложь - нужно вернуть весь список.
//  Организация                             - необязательный, организация, по которой определяется договор,
//                                            если не передана, то возвращается список договоров по всем ораганизациям.
//  ДополнительныеРеквизиты                 - необязательный, массив дополнительных реквизитов договора,
//                                            которые необходимо вернуть в результате запроса.
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция ПолучитьДоступныеДоговорыКонтрагента(Контрагент, СтруктураПараметровДляПолученияДоговора, ВозвращатьТолькоПервые, Организация = Неопределено, ДополнительныеРеквизиты = Неопределено) Экспорт

	Перем СписокДопустимыхВидовДоговоров, СписокДопустимыхВидовВзаиморасчетов, ВалютаВзаиморасчетовДоговора, ВидСравненияВалютыВзаиморасчетов;

	Если ( СтруктураПараметровДляПолученияДоговора <> Неопределено ) Тогда
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовДоговоров", СписокДопустимыхВидовДоговоров);
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовВзаиморасчетов", СписокДопустимыхВидовВзаиморасчетов);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВалютаВзаиморасчетовДоговора", ВалютаВзаиморасчетовДоговора);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВидСравненияВалютыВзаиморасчетов", ВидСравненияВалютыВзаиморасчетов);
	КонецЕсли;

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ПарКонтрагент",  Контрагент);
	Запрос.УстановитьПараметр("ПарВидДоговора", СписокДопустимыхВидовДоговоров);
	Запрос.УстановитьПараметр("ПарВидВзаиморасчетов", СписокДопустимыхВидовВзаиморасчетов);
	Запрос.УстановитьПараметр("ПарВалютаВзаиморасчетов", ВалютаВзаиморасчетовДоговора);
	Запрос.УстановитьПараметр("ПарОрганизация", Организация);

	ПсевдонимТаблицы = "ДоговорыКонтрагентов"; 
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ"
	+?(ВозвращатьТолькоПервые," ПЕРВЫЕ 2","") + "
	|	ДоговорыКонтрагентов.Ссылка КАК Договор";
	
	Если ( Не обЗначениеНеЗаполнено(ДополнительныеРеквизиты) ) Тогда
		Для Каждого НаименованиеРеквизита Из ДополнительныеРеквизиты Цикл
			Запрос.Текст = Запрос.Текст  + ", " + ПсевдонимТаблицы + "." + НаименованиеРеквизита;
		КонецЦикла; 		
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст  + "
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК " + ПсевдонимТаблицы + "
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)"
	+?(Не обЗначениеНеЗаполнено(Организация), "	И ДоговорыКонтрагентов.Организация = &ПарОрганизация", "") + "
	|	И ДоговорыКонтрагентов.Владелец    = &ПарКонтрагент"
	+?(ВалютаВзаиморасчетовДоговора = Неопределено, "", "
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов " 
	+?(ВидСравненияВалютыВзаиморасчетов = Неопределено, "=", ВидСравненияВалютыВзаиморасчетов) + "(&ПарВалютаВзаиморасчетов)
	|")
	+?(СписокДопустимыхВидовДоговоров = Неопределено, "", "
	|	И ДоговорыКонтрагентов.ВидДоговора В (&ПарВидДоговора)
	|")
	+?(СписокДопустимыхВидовВзаиморасчетов = Неопределено, "", "
	|	И ДоговорыКонтрагентов.ВедениеВзаиморасчетов В (&ПарВидВзаиморасчетов)
	|");

	Если ( НЕ обЗначениеНеЗаполнено(Организация) ) Тогда 

		Запрос.Текст = Запрос.Текст + "
		|	И (ВЫБОР КОГДА ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.Организация = &ПарОрганизация"
		+?(ВалютаВзаиморасчетовДоговора = Неопределено, "", "
		|	И ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.ВалютаВзаиморасчетов " 
		+?(ВидСравненияВалютыВзаиморасчетов = Неопределено, "=", ВидСравненияВалютыВзаиморасчетов) + "&ПарВалютаВзаиморасчетов
		|")
		+?(СписокДопустимыхВидовДоговоров = Неопределено, "", "
		|	И ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.ВидДоговора В (&ПарВидДоговора)
		|")
		+?(СписокДопустимыхВидовВзаиморасчетов = Неопределено, "", "
		|	И ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.ВедениеВзаиморасчетов В (&ПарВидВзаиморасчетов)
		|")
		+ "
		|	         ТОГДА ДоговорыКонтрагентов.Ссылка  = ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента
		|	         ИНАЧЕ ДоговорыКонтрагентов.Ссылка <> ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента КОНЕЦ)";

	КонецЕсли;

	Возврат Запрос.Выполнить();

КонецФункции // ПолучитьДоступныеДоговорыКонтрагента()

// функция возвращает основной договор контрагента
Функция ПолучитьОсновнойДоговорКонтрагента(Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДоговорыКонтрагентов.Ссылка КАК ДоступныйДоговорКонтрагента,
	|	ДоговорыКонтрагентов.Наименование КАК ДоступныйДоговорКонтрагентаНаименование
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО (Контрагенты.Ссылка = &Контрагент)
	|			И Контрагенты.ОсновнойДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ДоговорыКонтрагентов.Владелец = &Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА Контрагенты.ОсновнойДоговорКонтрагента ЕСТЬ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		Возврат Выборка.ДоступныйДоговорКонтрагента;		
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();		
	КонецЕсли;	
		
КонецФункции

// Функция определяет договор, соответствующий указанным значениям
// организации и контрагента, а также переданным параметрам.
// Если всем параметрам удовлетворяет основной договор, то выбирается он.
//
// Параметры
//  Организация                             - организация, по которой определяется договор
//  Контрагент                              - контрагент, по которому определяется договор
//  СтруктураПараметровДляПолученияДоговора - структура, содержащая параметры для определения договора:
//                                            список допустимых видов договоров и
//                                            список допустимых способов ведения взаиморасчетов, не обязательный.
//
// Возвращаемое значение:
//  Договор - договор контрагентов
//
Функция ПолучитьДоговорПоОрганизацииИКонтрагенту(Организация, Контрагент, СтруктураПараметровДляПолученияДоговора = Неопределено) Экспорт

	Перем СписокДопустимыхВидовДоговоров, СписокДопустимыхВидовВзаиморасчетов, ВалютаВзаиморасчетовДоговора, ВидСравненияВалютыВзаиморасчетов;

	Если (обЗначениеНеЗаполнено(Контрагент)) Тогда
		Возврат Неопределено;
	КонецЕсли;

	// Если не передана организация, то проверим, можно ли использовать основной договор. 
	Если ( обЗначениеНеЗаполнено(Организация) ) Тогда
		Договор = ПолучитьОсновнойДоговорКонтрагента(Контрагент);
		Если ( МожноИспользоватьДоговорДляДокумента(Договор, Контрагент, Организация, СтруктураПараметровДляПолученияДоговора) ) Тогда
			Возврат Договор;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;

	РезультатЗапроса = ПолучитьДоступныеДоговорыКонтрагента(Контрагент, СтруктураПараметровДляПолученияДоговора, Истина, Организация );
	ТаблицаДоговоров = РезультатЗапроса.Выгрузить();

	Если ТаблицаДоговоров.Количество() = 1 Тогда
		Возврат ТаблицаДоговоров[0].Договор;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ПолучитьДоговорПоОрганизацииИКонтрагенту()

// функция возвращает основное контактное лицо контрагента
Функция ПолучитьОсновноеКонтактноеЛицоКонтрагента(Контрагент) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КонтактныеЛицаКонтрагентов.Ссылка КАК ДоступноеКонтактноеЛицоКонтрагента
	|ИЗ
	|	Справочник.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО (Контрагенты.Ссылка = &Контрагент)
	|			И Контрагенты.ОсновноеКонтактноеЛицо = КонтактныеЛицаКонтрагентов.Ссылка
	|ГДЕ
	|	КонтактныеЛицаКонтрагентов.Владелец = &Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА Контрагенты.ОсновноеКонтактноеЛицо ЕСТЬ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ";

	Запрос.УстановитьПараметр("Контрагент", Контрагент);

	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		Возврат Выборка.ДоступноеКонтактноеЛицоКонтрагента;		
	Иначе
		Возврат Справочники.КонтактныеЛицаКонтрагентов.ПустаяСсылка();		
	КонецЕсли;	

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УПРАВЛЕНИЯ ОБМЕНОМ

Функция ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоОбработанныхОбъектовНаВыгрузке, ТипСоединения, КаталогОбмена)
	
	КоличествоОбработанныхОбъектовНаВыгрузке	= 0;
	
	Если      ТипСоединения = "colour" Тогда
		Успешно = ВыгрузитьЦветаВФайл          (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "model" Тогда
		Успешно = ВыгрузитьМоделиВФайл         (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "manufacturer" Тогда
		Успешно = ВыгрузитьПроизводителейВФайл (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "repair" Тогда
		Успешно = ВыгрузитьВидыРемонтаВФайл    (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "carworks" Тогда
		Успешно = ВыгрузитьАвтоработыВФайл     (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "prices" Тогда
		Успешно = ВыгрузитьЦеныВФайл           (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "schedule" Тогда
		Успешно = ВыгрузитьПлановыеТОВФайл     (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "users" Тогда
		Успешно = ВыгрузитьКонтрагентовВФайл   (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "car" Тогда
		Успешно = ВыгрузитьАвтомобилиВФайл     (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "request" Тогда
		Успешно = ВыгрузитьЗаявкиВФайл         (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	ИначеЕсли ТипСоединения = "order" Тогда
		Успешно = ВыгрузитьЗаказНарядыВФайл    (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхОбъектовНаВыгрузке);
	КонецЕсли;
	
	Если (НЕ Успешно) Тогда
		Возврат Ложь;
	ИначеЕсли (КоличествоОбработанныхОбъектовНаВыгрузке = 0) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Успешно = HTTPВыгрузитьНаСервер(СтруктураПараметровСайта, КаталогОбмена,, ВыгружатьТолькоИзменения, ТипСоединения);
	
	Попытка
		УдалитьФайлы(КаталогОбмена);
	Исключение
	КонецПопытки;
		
	Возврат Успешно;
	 
КонецФункции

// //////////////////////////////////
// выгрузка документов в файл

Функция ВыгрузитьЗаказНарядыВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженныхЗаказов = 0)
	
	Успешно						= Истина;
	КоличествоВыгруженныхЗаказов= 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.ЗаказНаряды;
		// из этого массива нужно удалить все только что загруженные заказы
		Для Каждого Эл Из мМассивЗагруженныхОбъектов Цикл		
			ИндексЭлемента = МассивИзменений.Найти(Эл);
			Если ( ИндексЭлемента <> Неопределено ) Тогда
				МассивИзменений.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивИзменений.Количество() = 0 Тогда		
			СообщитьПользователю("Изменения заказов не зарегистрированы. Выгрузка заказов не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;			
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаДокументов = СформироватьCMLСРанееЗагруженнымиЗаказНарядами(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженныхЗаказов = ТаблицаДокументов.Количество();
	
	Если КоличествоВыгруженныхЗаказов = 0 Тогда	
		СообщитьПользователю("Не выгружен ни один заказ-наряд.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-order.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено заказов-нарядов: " + ТаблицаДокументов.Количество(), Истина,,1);	
	Возврат Истина;
	
КонецФункции


Функция ВыгрузитьМоделиВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженныхМоделей=0)
	
	Успешно						= Истина;
	КоличествоВыгруженныхМоделей = 0;
	
	МассивИзменений = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Модели;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке моделей не зарегистрированы. Выгрузка моделей не произведена.", Истина,,2); 
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаМоделей = СформироватьCMLСМоделями(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженныхМоделей = ТаблицаМоделей.Количество();
	
	Если КоличествоВыгруженныхМоделей = 0 Тогда	
		СообщитьПользователю("Не выгружена ни одна модель автомобиля.", Истина,,2);
		Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-models.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено моделей: " + ТаблицаМоделей.Количество(), Истина,,1);	
	Возврат Истина;
	
КонецФункции

Функция ВыгрузитьПроизводителейВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Производители;
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке производителей не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаПроизводителей = СформироватьCMLИзПростогоСправочника(ОбъектCMLСтрока, МассивИзменений, "Производители", "Производитель");
	
	КоличествоВыгруженных = ТаблицаПроизводителей.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Не выгружен ни один производитель.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-manufacturer.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено производителей: " + ТаблицаПроизводителей.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции

Функция ВыгрузитьЦветаВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Цвета;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке цветов не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLСЦветами(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда
		СообщитьПользователю("Не выгружен ни один цвет.", Истина,,2);
		Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-colour.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено цветов: " + КоличествоВыгруженных, Истина,,1);
	Возврат Истина;
	
КонецФункции

Функция ВыгрузитьАвтоработыВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Автоработы;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке авторабот не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLИзПростогоСправочника(ОбъектCMLСтрока, МассивИзменений, "Автоработы", "Авторабота", Истина, Истина);
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Не выгружена ни одина авторабота.", Истина,,2);
		Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-carworks.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено авторабот: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции

Функция ВыгрузитьВидыРемонтаВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.ВидыРемонта;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке видов ремонта не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLИзПростогоСправочника(ОбъектCMLСтрока, МассивИзменений, "ВидыРемонта", "ВидРемонта");
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Не выгружен ни один вид ремонта.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-repair.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено видов ремонта: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции

Функция ВыгрузитьАвтомобилиВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Автомобили;
		
		// из этого массива нужно удалить все только что загруженные автомобили
		Для Каждого Эл Из мМассивЗагруженныхОбъектов Цикл
			ИндексЭлемента = МассивИзменений.Найти(Эл);
			Если ( ИндексЭлемента <> Неопределено ) Тогда
				МассивИзменений.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке автомобилей не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLСАвтомобилями(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Не выгружен ни один автомобиль.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-car.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено автомобилей: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции

Функция ВыгрузитьЗаявкиВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженныхОбъектов = 0)
	
	Успешно						= Истина;
	КоличествоВыгруженныхОбъектов= 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Заявки;
		// из этого массива нужно удалить все только что загруженные заказы
		Для Каждого Эл Из мМассивЗагруженныхОбъектов Цикл
			ИндексЭлемента = МассивИзменений.Найти(Эл);
			Если ( ИндексЭлемента <> Неопределено ) Тогда
				МассивИзменений.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения заявок не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;			
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLСЗаявками(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженныхОбъектов = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженныхОбъектов = 0 Тогда	
		СообщитьПользователю("Не выгружена ни одина заявка.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-request.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено заявок: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
КонецФункции

Функция ВыгрузитьКонтрагентовВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Контрагенты;
		
		// из этого массива нужно удалить все только что загруженные объекты
		Для Каждого Эл Из мМассивЗагруженныхОбъектов Цикл
			ИндексЭлемента = МассивИзменений.Найти(Эл);
			Если ( ИндексЭлемента <> Неопределено ) Тогда
				МассивИзменений.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в списке контрагентов не зарегистрированы. Выгрузка не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLСКонтрагентами(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Не выгружен ни один контрагент.", Истина,,2);
		Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяФайлаОбмена 		 = "1cbitrix-users.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено контрагентов: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции

Функция ВыгрузитьЦеныВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLСЦенами(ОбъектCMLСтрока);
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Не выгружена ни одина цена.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-prices.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено цен: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции

Функция ВыгрузитьПлановыеТОВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженных=0)
	
	Успешно						= Истина;
	КоличествоВыгруженных = 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.ЗаказНаряды;
		
		Если МассивИзменений.Количество() = 0 Тогда
			СообщитьПользователю("Изменения в Заказ-Нарядах не зарегистрированы. Выгрузка плановых ТО не произведена.", Истина,,2); 
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаОбъектов = СформироватьCMLСПлановымиТО(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженных = ТаблицаОбъектов.Количество();
	
	Если КоличествоВыгруженных = 0 Тогда	
		СообщитьПользователю("Информация о плановых ТО не выгружена.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-schedule.xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено дат ТО: " + ТаблицаОбъектов.Количество(), Истина,,1);	
	Возврат Истина;
	
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ УПРАВЛЕНИЯ ПРОФИЛЯМИ (НАСТРОЙКАМИ)

Функция ОпределитьМожноВыгружатьЗаказы()
	
	ЕстьФайлЗагрузки 			= Не обЗначениеНеЗаполнено(ФайлЗагрузки);
	ЕстьСайт			  		= Не обЗначениеНеЗаполнено(HTTPОбменСервер);	
	ЕстьОрганизация  			= Не обЗначениеНеЗаполнено(Организация);
	ЕстьПараметрыИдентификации  = Не обЗначениеНеЗаполнено(СпособИдентификацииКонтрагентов);
	
	Если ( ВыгружатьНаСайт ) Тогда
		Возврат	ЕстьСайт И ЕстьОрганизация И ЕстьПараметрыИдентификации;
	Иначе
		Возврат ЕстьФайлЗагрузки;
	КонецЕсли;	
	
КонецФункции

////////////////////////////////////////////////////////////////
// СЕРВИСНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ ОБМЕНА

// функция разбирает строку адреса сайта на составные части
// Параметры:
//	АдресСайта - строка с адресом сайта
// Возвращает структуру с частями
//
Функция РазобратьАдресСайта(Знач АдресСайта) Экспорт
	
	АдресСайта = СокрЛП(АдресСайта); 
	
	HTTPСервер		 = "";
	HTTPПорт		 = 0;	
	HTTPАдресСкрипта = "";
	
	Если ( Не обЗначениеНеЗаполнено(АдресСайта) ) Тогда
		
		АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
		АдресСайта = СтрЗаменить(АдресСайта, " ", "");
		АдресСайта = СтрЗаменить(АдресСайта, "http://", "");
		ПозицияСлэша = Найти(АдресСайта, "/");
		Если ( ПозицияСлэша > 0 ) Тогда
			HTTPСервер 		 = Лев(АдресСайта, ПозицияСлэша - 1);	
			HTTPАдресСкрипта = Прав(АдресСайта, СтрДлина(АдресСайта) - ПозицияСлэша);
		Иначе	
			HTTPСервер 		 = АдресСайта;	
			HTTPАдресСкрипта = "";
		КонецЕсли;
		ПозицияДвоеточия = Найти(HTTPСервер, ":");
		Если ПозицияДвоеточия > 0 Тогда
			HTTPСерверСПортом = HTTPСервер;
			HTTPСервер		  = Лев(HTTPСерверСПортом, ПозицияДвоеточия - 1);
			HTTPПортСтрока 	  = Прав(HTTPСерверСПортом, СтрДлина(HTTPСерверСПортом) - ПозицияДвоеточия);
		Иначе
			HTTPПортСтрока = "80";
		КонецЕсли;
		Попытка
			HTTPПорт = Число(HTTPПортСтрока);
		Исключение
			HTTPПорт = 0;
		КонецПопытки;		
		
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("HTTPСервер"	  , HTTPСервер); 
	СтруктураРезультата.Вставить("HTTPАдресСкрипта", HTTPАдресСкрипта);
	СтруктураРезультата.Вставить("HTTPПорт"		  , HTTPПорт);
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Функция возвращает структуру параметров для соединения с сайтом
// Параметры:
//	Объект - соединяемый объект
//	Строка со значением начала адреса скрипта
//
Функция ПолучитьСтруктуруПараметровДляСоединения(Объект, Знач НачалоАдресаСкрипта = "") Экспорт
	
	СтруктураПараметровСайта = Новый Структура();
	Если ( ТипЗнч(Объект) = Тип("ОбработкаОбъект.ОбменССайтомАвтосервиса") ) Тогда                                                 
		СтруктураПараметровСайта.Вставить("ИмяПользователя", Объект.УзелОбмена.ПользовательHTTP);
		СтруктураПараметровСайта.Вставить("Пароль"		   , Объект.УзелОбмена.ПарольHTTP);	
	Иначе
		СтруктураПараметровСайта.Вставить("ИмяПользователя", ?(ЗначениеЗаполнено(Объект.HTTPОбменИмяПользователя), Объект.HTTPОбменИмяПользователя, Объект.УзелОбмена.ПользовательHTTP));
		СтруктураПараметровСайта.Вставить("Пароль"		   , ?(ЗначениеЗаполнено(Объект.HTTPОбменПароль), Объект.HTTPОбменПароль, Объект.УзелОбмена.ПарольHTTP));
	КонецЕсли;

	
	Если ( Не ПустаяСтрока(НачалоАдресаСкрипта) ) Тогда
		
		СтруктураАдреса = РазобратьАдресСайта(НачалоАдресаСкрипта);
		
		СтруктураПараметровСайта.Вставить("АдресСкрипта", СтруктураАдреса.HTTPАдресСкрипта);
		СтруктураПараметровСайта.Вставить("Сервер"		, СтруктураАдреса.HTTPСервер);
		СтруктураПараметровСайта.Вставить("Порт"		, Объект.УзелОбмена.ПортHTTP);
		
	Иначе
		
		СтруктураПараметровСайта.Вставить("АдресСкрипта", Объект.HTTPОбменАдресСкрипта);
		СтруктураПараметровСайта.Вставить("Сервер"		, Объект.HTTPОбменСервер);
		СтруктураПараметровСайта.Вставить("Порт"		, Объект.HTTPОбменПорт);

	КонецЕсли;
	
	СтруктураПараметровСайта.Вставить("ПроксиСервер"		 , Объект.HTTPОбменПроксиСервер);
	СтруктураПараметровСайта.Вставить("ПроксиПорт"		     , Объект.HTTPОбменПроксиПорт);
	СтруктураПараметровСайта.Вставить("ПроксиИмяПользователя", Объект.HTTPОбменПроксиИмяПользователя);
	СтруктураПараметровСайта.Вставить("ПроксиПароль"		 , Объект.HTTPОбменПроксиПароль);
	СтруктураПараметровСайта.Вставить("ПроксиИспользование"  , Объект.HTTPОбменПроксиИспользование);	
	
	Возврат СтруктураПараметровСайта;
	
КонецФункции

// Функция возвращает полученные данные с HTTP сервера
// Параметры:
//	Соединение - объект HTTP соединения
//	ПараметрыЗапроса - строка параметров запроса
//	Заголовки - строка заголовков
//	СтрокаСообщенияПользователю - строка сообщения пользователю
//
Функция HTTPПолучитьДанныеССервера(Соединение, ПараметрыЗапроса="", Заголовки="", СтрокаСообщенияПользователю = "") Экспорт
	
	ОтветСервера   = Неопределено; 
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Если Заголовки="" Тогда
		Заголовки=Новый Соответствие;
	КонецЕсли; 
	
	Попытка
		Соединение.Получить(СокрЛП(ПараметрыЗапроса), ИмяФайлаОтвета, Заголовки);
	Исключение
		СтрокаСообщенияПользователю = "Не удалось получить данные с сервера. Проверьте правильность адреса сервера, порт, имя пользователя и пароль,"+Символы.ПС+"а также настройки подключения к Интернет.";
	КонецПопытки;	
	
	ФайлОтвета = Новый Файл(ИмяФайлаОтвета);
	
	Если (ФайлОтвета.Существует()) Тогда		
		ТекстОтвета		= Новый ТекстовыйДокумент();
		ТекстОтвета.Прочитать(ИмяФайлаОтвета);
		Если (ТекстОтвета.КоличествоСтрок() > 0) Тогда
			ОтветСервера				= ТекстОтвета.ПолучитьТекст();	
		Иначе
			СтрокаСообщенияПользователю	= "Получение данных с сервера: Получен пустой ответ сервера."; 	
		КонецЕсли;		
	Иначе	
		СтрокаСообщенияПользователю = "Получение данных с сервера: Ответ сервера не получен."; 
	КонецЕсли;	
	
	Попытка
		УдалитьФайлы(КаталогВременныхФайлов(), ИмяФайлаОтвета);
	Исключение
	КонецПопытки;
	
	Возврат ОтветСервера;
	
КонецФункции

// Функция производит авторизацию на сервере
// Параметры:
//	Соединение - объект HTTP соединения
//	СтруктураПараметровСайта - сутруктура параметров сайта
//	ОтветСервера - хранит ответ сервера
//	СтрокаСообщенияПользователю - строка сообщения пользователю
//	ТипСоединения - тип соединения с сервером
//
Функция HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения = "catalog") Экспорт
	
	Успешно	= Истина;
	ОтобразитьСостояние("Установка соединения с сервером...");

	Соединение = HTTPУстановитьСоединение(СтруктураПараметровСайта);
	 
	Если ( Соединение = Неопределено ) Тогда
		СтрокаСообщенияПользователю = "Не удалось установить соединение с сервером.";
		Возврат Ложь;
	КонецЕсли;
	
	ОтобразитьСостояние("Проверка имени пользователя и пароля...");
	
	ОтветСервера = HTTPПолучитьДанныеССервера(Соединение, СтруктураПараметровСайта.АдресСкрипта + "?type=" + ТипСоединения + "&mode=checkauth");
		
	Если ОтветСервера = Неопределено Тогда 
		СтрокаСообщенияПользователю = "Не удалось установить соединение с сервером. Авторизация пользователя не выполнена." + Символы.ПС + ОписаниеОшибки();
		Возврат Ложь;
	КонецЕсли;
		
	Если НРег(СтрПолучитьСтроку(ОтветСервера,1)) <> "success" Тогда
		СтрокаСообщенияПользователю = "Не удалось установить соединение с сервером. Проверьте имя пользователя и пароль." + Символы.ПС + ОписаниеОшибки();
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// функция устанавливает соединение с сервером
// Параметры:
//	СтруктураПараметровСайта - структура параметров сайта
// Возвращает объект http соединения
//
Функция HTTPУстановитьСоединение(СтруктураПараметровСайта) Экспорт
	
	Соединение		= Неопределено;
	ИнтернетПрокси	= Неопределено;
	
	Если ( СтруктураПараметровСайта.ПроксиИспользование ) Тогда
		ИнтернетПрокси				= Новый ИнтернетПрокси();
		ИнтернетПрокси.Пользователь	= СтруктураПараметровСайта.ПроксиИмяПользователя;
		ИнтернетПрокси.Пароль		= СтруктураПараметровСайта.ПроксиПароль;	
		Если ( СтруктураПараметровСайта.ПроксиПорт = 0 ) Тогда
			ИнтернетПрокси.Установить("HTTP", СтруктураПараметровСайта.ПроксиСервер);
		Иначе	
			ИнтернетПрокси.Установить("HTTP", СтруктураПараметровСайта.ПроксиСервер, СтруктураПараметровСайта.ПроксиПорт);
		КонецЕсли;		
	КонецЕсли;	
	
	Порт = ?(НЕ обЗначениеНеЗаполнено(СтруктураПараметровСайта.Порт), СтруктураПараметровСайта.Порт, 80);
	Попытка	
		Соединение	= Новый HTTPСоединение(СтруктураПараметровСайта.Сервер, Порт, СтруктураПараметровСайта.ИмяПользователя, СтруктураПараметровСайта.Пароль, ИнтернетПрокси);
	Исключение
		СообщитьОбОшибке("Не удалось установить соединение с сервером " + СтруктураПараметровСайта.Сервер + ":" + Строка(СтруктураПараметровСайта.Порт) + ".
			|Проверьте правильность адреса сервера, порт, имя пользователя и пароль.");	
		Соединение = Неопределено;		
	Конецпопытки;	
		
	Возврат Соединение;
	
КонецФункции

// Выполняет тестовое подключение к серверу
Функция ВыполнитьТестовоеПодключениеКСерверуHTTP(Объект, СтрокаСообщенияПользователю, РезультатСоединения = Неопределено, ТипСоединения = "catalog") Экспорт
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект, Объект.СерверHTTP);
		
	Соединение = HTTPУстановитьСоединение(СтруктураПараметровСайта);
	
	Если ( Соединение = Неопределено ) Тогда
		Возврат "Ошибка при установке соединения с сайтом.";
	КонецЕсли;
	
	ОтветСервера		= "";
	РезультатСоединения	= HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	
	Если ( Не РезультатСоединения ) Тогда
		Возврат "Соединение с сайтом не установлено.";
	Иначе
		Возврат "Соединение выполнено успешно.";
	КонецЕсли;
	
КонецФункции


////////////////////////////////////
// Возвращает заказ-наряды

Функция ПолучитьЗаказНаряды(МассивИзменений, МассивЗагруженныхДокументов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка,
	|	ЗаказНаряд.Заказчик,
	|	ЗаказНаряд.Автомобиль,
	|	ЗаказНаряд.Цех,
	|	ЗаказНаряд.Мастер,
	|	ЗаказНаряд.Дата,
	|	ЗаказНаряд.Номер,
	|	ЗаказНаряд.СуммаДокумента,
	|	ЗаказНаряд.ДатаНачала,
	|	ЗаказНаряд.ДатаОкончания,
	|	ЗаказНаряд.ПричинаОбращения,
	|	ЗаказНаряд.ВидРемонта,
	|	ЗаказНаряд.Состояние,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказНаряд.ДокументОснование) = ТИП(Документ.ЗаявкаНаРемонт)
	|			ТОГДА ЗаказНаряд.ДокументОснование
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказНаряд.ДокументОснование) = ТИП(Документ.ЗаявкаНаРемонт)
	|			ТОГДА ЗаказНаряд.ДокументОснование.GUIDСайта
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИдССайта,
	|	ЗаказНаряд.ВалютаДокумента,
	|	ЗаказНаряд.СуммаРаботДокумента,
	|	ЗаказНаряд.Комментарий,
	|	ЗаказНаряд.Рекомендации,
	|	ЗаказНаряд.Гарантии КАК Гарантии
	|ПОМЕСТИТЬ ЗаказНаряды
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗаказНаряд.Заказчик) = ТИП(Справочник.Контрагенты)
	|	И ЗаказНаряд.Заказчик.ВыгружатьАвтомобилиИЗаказНарядыНаСайт
	|	И ЗаказНаряд.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыДокументов.Заказчик,
	|	МАКСИМУМ(ВЫРАЗИТЬ(ЕСТЬNULL(КонтактнаяИнформация.Представление, """""""") КАК СТРОКА(200))) КАК email
	|ПОМЕСТИТЬ emailЗаказчиков
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗаказНаряды.Заказчик КАК Заказчик
	|	ИЗ
	|		ЗаказНаряды КАК ЗаказНаряды) КАК КонтрагентыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО КонтрагентыДокументов.Заказчик = КонтактнаяИнформация.Объект
	|ГДЕ
	|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтрагентыДокументов.Заказчик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказНаряды.Ссылка КАК ДокументСсылка,
	|	ЗаказНаряды.Заказчик КАК Заказчик,
	|	ЗаказНаряды.Автомобиль КАК Автомобиль,
	|	ЗаказНаряды.Цех КАК Цех,
	|	ЗаказНаряды.Мастер КАК Мастер,
	|	ЗаказНаряды.Дата КАК Дата,
	|	ЗаказНаряды.Номер КАК Номер,
	|	ЗаказНаряды.СуммаДокумента КАК СуммаДокумента,
	|	ЗаказНаряды.ДатаНачала КАК ДатаНачала,
	|	ЗаказНаряды.ДатаОкончания КАК ДатаОкончания,
	|	ЗаказНаряды.ПричинаОбращения КАК ПричинаОбращения,
	|	ЗаказНаряды.ВидРемонта КАК ВидРемонта,
	|	ЗаказНаряды.Состояние КАК Состояние,
	|	ЗаказНаряды.ДокументОснование,
	|	ЗаказНаряды.ИдССайта,
	|	ЗаказНаряды.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗаказНаряды.СуммаРаботДокумента КАК СуммаРаботДокумента,
	|	ЗаказНаряды.Комментарий КАК Комментарий,
	|	ЗаказНаряды.Рекомендации КАК Рекомендации,
	|	ЗаказНаряды.Гарантии КАК Гарантии,
	|	0 КАК Пробег,
	|	ЕСТЬNULL(emailЗаказчиков.email, """") КАК emailКонтрагента
	|ИЗ
	|	ЗаказНаряды КАК ЗаказНаряды
	|		ЛЕВОЕ СОЕДИНЕНИЕ emailЗаказчиков КАК emailЗаказчиков
	|		ПО ЗаказНаряды.Заказчик = emailЗаказчиков.Заказчик";
	
	Если МассивИзменений.Количество() > 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ ЗаказНаряд.ПометкаУдаления", "НЕ ЗаказНаряд.ПометкаУдаления И ЗаказНаряд.Ссылка В(&ИзмененныеДокументы)");
		Запрос.УстановитьПараметр("ИзмененныеДокументы",  МассивИзменений);
	КонецЕсли;
	Если МассивЗагруженныхДокументов.Количество() > 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ ЗаказНаряд.ПометкаУдаления", "НЕ ЗаказНаряд.ПометкаУдаления И НЕ ЗаказНаряд.Ссылка В(&ЗагруженныеДокументы)");
		Запрос.УстановитьПараметр("ЗагруженныеДокументы", МассивЗагруженныхДокументов);
	КонецЕсли;
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДокументов;
	
КонецФункции

Функция ПолучитьМодели(МассивИзменений) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Модели.Ссылка,
	|	Модели.НаименованиеПолное,
	|	Модели.Производитель,
	|	Модели.Родитель
	|ИЗ
	|	Справочник.Модели КАК Модели
	|ГДЕ
	|	Модели.ЭтоГруппа = ЛОЖЬ
	|	И НЕ Модели.ПометкаУдаления";
	
	Если НЕ МассивИзменений.Количество() = 0 Тогда
		Запрос.Текст = Запрос.Текст + " И Модели.Ссылка В(&МассивСсылок)";
		Запрос.УстановитьПараметр("МассивСсылок", МассивИзменений);
	КонецЕсли;
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция ПолучитьЦвета(МассивИзменений) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Цвета.Ссылка,
	|	Цвета.ЦветПоГИБДД,
	|	Цвета.Наименование
	|ИЗ
	|	Справочник.Цвета КАК Цвета
	|ГДЕ
	|	НЕ Цвета.ПометкаУдаления";
	
	Если НЕ МассивИзменений.Количество() = 0 Тогда
		Запрос.Текст = Запрос.Текст + " И Цвета.Ссылка В(&МассивСсылок)";
		Запрос.УстановитьПараметр("МассивСсылок", МассивИзменений);
	КонецЕсли;
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция ПолучитьЦены() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦеныАвтоработСрезПоследних.Авторабота,
	|	ЦеныАвтоработСрезПоследних.Цена,
	|	ЦеныАвтоработСрезПоследних.ВидРемонта,
	|	ЦеныАвтоработСрезПоследних.Валюта.Наименование как валюта
	|ИЗ
	|	РегистрСведений.ЦеныАвторабот.СрезПоследних КАК ЦеныАвтоработСрезПоследних";
	//|ГДЕ
	//|	ЦеныАвтоработСрезПоследних.КлассАвтомобиля.Наименование = ""Для выгрузки цен на сайт""";
	//для ааа4
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция ПолучитьПлановыеТО(МассивИзменений) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ЗаказНаряд.ДатаСледующегоТО) КАК ДатаСледующегоТО,
	|	ЗаказНаряд.Автомобиль КАК Автомобиль,
	|	ЗаказНаряд.ВидРемонта КАК ВидРемонта
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	НЕ ЗаказНаряд.ДатаСледующегоТО = ДАТАВРЕМЯ(1, 1, 1)
	|	И ЗаказНаряд.Проведен
	|	И ТИПЗНАЧЕНИЯ(ЗаказНаряд.Заказчик) = ТИП(Справочник.Контрагенты)
	|	И ЗаказНаряд.Заказчик.ВыгружатьАвтомобилиИЗаказНарядыНаСайт
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаряд.Автомобиль,
	|	ЗаказНаряд.ВидРемонта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСледующегоТО УБЫВ";
	
	Если МассивИзменений.Количество() > 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ЗаказНаряд.Заказчик.ВыгружатьАвтомобилиИЗаказНарядыНаСайт", "И ЗаказНаряд.Заказчик.ВыгружатьАвтомобилиИЗаказНарядыНаСайт И ЗаказНаряд.Ссылка В(&ИзмененныеДокументы)");
		Запрос.УстановитьПараметр("ИзмененныеДокументы",  МассивИзменений);
	КонецЕсли;
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция ПолучитьИзПростогоСправочника(МассивИзменений, ИмяСправочника, Иерархический=Ложь, Коммент=Ложь) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТекСправочник.Ссылка,
	|	ТекСправочник.Наименование" + ?(Иерархический, " ,
	|	ТекСправочник.Родитель КАК Родитель, 
	|	ТекСправочник.ЭтоГруппа", "") + ?(Коммент, " ,
	|	ТекСправочник.Комментарий", "") + "
	|ИЗ
	|	Справочник." + ИмяСправочника + " КАК ТекСправочник
	|ГДЕ
	|	НЕ ТекСправочник.ПометкаУдаления";
	
	Если НЕ МассивИзменений.Количество()=0 Тогда
		Запрос.Текст = Запрос.Текст + " И ТекСправочник.Ссылка В(&МассивСсылок)";
		Запрос.УстановитьПараметр("МассивСсылок", МассивИзменений);
	КонецЕсли;
	Если Иерархический Тогда
		Запрос.Текст = Запрос.Текст + " И ТекСправочник.ЭтоГруппа = ЛОЖЬ";
	КонецЕсли;
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция ПолучитьАвтомобили(МассивИзменений) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Автомобили.Ссылка КАК Ссылка,
	|	Автомобили.Наименование КАК Наименование,
	|	Автомобили.Родитель КАК Родитель,
	|	Автомобили.Модель.Производитель КАК ПроизводительМодели,
	|	Автомобили.Комментарий КАК Комментарий,
	|	Автомобили.Модель КАК Модель,
	|	Автомобили.VIN КАК VIN,
	|	Автомобили.ГодВыпуска КАК ГодВыпуска,
	|	АвтомобилиСрезПоследнихХозяин.Значение КАК Владелец,
	|	Автомобили.Цвет КАК Цвет,
	|	Автомобили.GUIDСайта КАК ИдССайта,
	|	АвтомобилиСрезПоследнихГосНомер.Значение КАК ГосНомер,
	|	АвтомобилиСрезПоследнихПробег.Значение КАК Пробег
	|ИЗ
	|	Справочник.Автомобили КАК Автомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК АвтомобилиСрезПоследнихХозяин
	|		ПО Автомобили.Ссылка = АвтомобилиСрезПоследнихХозяин.Автомобиль
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследнихГосНомер
	|		ПО Автомобили.Ссылка = АвтомобилиСрезПоследнихГосНомер.Автомобиль
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Пробег)) КАК АвтомобилиСрезПоследнихПробег
	|		ПО Автомобили.Ссылка = АвтомобилиСрезПоследнихПробег.Автомобиль
	|ГДЕ
	|	Автомобили.ЭтоГруппа = ЛОЖЬ
	|	И НЕ Автомобили.ПометкаУдаления
	|	И ТИПЗНАЧЕНИЯ(АвтомобилиСрезПоследнихХозяин.Значение) = ТИП(Справочник.Контрагенты)
	|	И АвтомобилиСрезПоследнихХозяин.Значение <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И АвтомобилиСрезПоследнихХозяин.Значение.ВыгружатьАвтомобилиИЗаказНарядыНаСайт";
	
	Если НЕ МассивИзменений.Количество()=0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ Автомобили.ПометкаУдаления", "НЕ Автомобили.ПометкаУдаления И Автомобили.Ссылка В(&МассивСсылок)");
		Запрос.УстановитьПараметр("МассивСсылок", МассивИзменений);
	КонецЕсли;
	
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаОбъектов;
	
КонецФункции

Функция ПолучитьЗаявки(МассивИзменений, МассивЗагруженныхДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявкаНаРемонт.Заказчик
	|ПОМЕСТИТЬ КонтрагентыДокументов
	|ИЗ
	|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	|ГДЕ
	|	ЗаявкаНаРемонт.Заказчик ССЫЛКА Справочник.Контрагенты
	|	И ЗаявкаНаРемонт.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыДокументов.Заказчик,
	|	МАКСИМУМ(ВЫРАЗИТЬ(ЕСТЬNULL(КонтактнаяИнформация.Представление, """") КАК СТРОКА(200))) КАК email
	|ПОМЕСТИТЬ КонтактыЗаказчиков
	|ИЗ
	|	КонтрагентыДокументов КАК КонтрагентыДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО КонтрагентыДокументов.Заказчик = КонтактнаяИнформация.Объект
	|ГДЕ
	|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтрагентыДокументов.Заказчик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК ДокументСсылка,
	|	Документ.Заказчик КАК Заказчик,
	|	Документ.Автомобиль КАК Автомобиль,
	|	Документ.Цех КАК Цех,
	|	Документ.Мастер КАК Мастер,
	|	Документ.СуммаДокумента КАК СуммаДокумента,
	|	Документ.ДатаНачала КАК ДатаНачала,
	|	Документ.ДатаОкончания КАК ДатаОкончания,
	|	Документ.ПричинаОбращения КАК ПричинаОбращения,
	|	Документ.ВидРемонта КАК ВидРемонта,
	|	Документ.ВалютаДокумента КАК ВалютаДокумента,
	|	Документ.СуммаРаботДокумента КАК СуммаРаботДокумента,
	|	Документ.GUIDСайта КАК ИдССайта,
	|	Документ.Дата КАК Дата,
	|	Документ.Номер КАК Номер,
	|	0 КАК Пробег,
	|	Документ.Состояние,
	|	Документ.Комментарий КАК Комментарий,
	|	Документ.КонтактнаяИнформация КАК Телефон,
	|	ЕСТЬNULL(КонтактыЗаказчиков.email, """") КАК emailКонтрагента
	|ИЗ
	|	Документ.ЗаявкаНаРемонт КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтактыЗаказчиков КАК КонтактыЗаказчиков
	|		ПО Документ.Заказчик = КонтактыЗаказчиков.Заказчик
	|ГДЕ
	|	Документ.Заказчик ССЫЛКА Справочник.Контрагенты
	|	И Документ.Заказчик.ВыгружатьАвтомобилиИЗаказНарядыНаСайт
	|	И Документ.Проведен";

	Если (МассивИзменений.Количество() = 0) И (МассивЗагруженныхДокументов.Количество()=0) Тогда
	Иначе
		Если НЕ МассивИзменений.Количество()=0 Тогда
			Запрос.Текст = Запрос.Текст + ?(МассивИзменений.Количество()=0,""," И Документ.Ссылка В(&Ссылка)");
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + ?(МассивЗагруженныхДокументов.Количество()=0,""," И НЕ Документ.Ссылка В(&ЗагруженныеДокументы)");
		Запрос.УстановитьПараметр("Ссылка", МассивИзменений);
		Запрос.УстановитьПараметр("ЗагруженныеДокументы", МассивЗагруженныхДокументов);
	КонецЕсли;
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДокументов;
	
КонецФункции

Функция ПолучитьКонтрагентов(МассивИзменений) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	МАКСИМУМ(КИEmail.Представление) КАК email,
	|	МАКСИМУМ(КИТелефоны.Поле1 + "" "" + КИТелефоны.Поле2 + КИТелефоны.Поле3) КАК Телефон
	|ПОМЕСТИТЬ ТаблицаКонтрагентов
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КИТелефоны
	|		ПО Контрагенты.Ссылка = КИТелефоны.Объект
	|			И (КИТелефоны.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КИEmail
	|		ПО Контрагенты.Ссылка = КИEmail.Объект
	|			И (КИEmail.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
	|ГДЕ
	|	Контрагенты.ЭтоГруппа = ЛОЖЬ
	|	И НЕ Контрагенты.ПометкаУдаления
	|	И (Контрагенты.GUIDСайта <> """"
	|			ИЛИ Контрагенты.ВыгружатьАвтомобилиИЗаказНарядыНаСайт)
	|	И ВЫБОР
	|			КОГДА КИEmail.Представление ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			КОГДА (ВЫРАЗИТЬ(КИEmail.Представление КАК СТРОКА(1))) = "" ""
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	Контрагенты.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКонтрагентов.Ссылка,
	|	ТаблицаКонтрагентов.email,
	|	ТаблицаКонтрагентов.Телефон,
	|	ТаблицаКонтрагентов.Ссылка.Наименование КАК Наименование,
	|	ТаблицаКонтрагентов.Ссылка.НаименованиеПолное КАК НаименованиеПолное,
	|	ТаблицаКонтрагентов.Ссылка.Комментарий КАК Комментарий,
	|	ТаблицаКонтрагентов.Ссылка.GUIDСайта КАК ИдССайта,
	|	ТаблицаКонтрагентов.Ссылка.ВыгружатьАвтомобилиИЗаказНарядыНаСайт КАК ВыгружатьАвтомобилиИЗаказНарядыНаСайт,
	|	ТаблицаКонтрагентов.Ссылка.Фамилия Как Фамилия,
	|	ТаблицаКонтрагентов.Ссылка.Имя Как Имя,
	|	ТаблицаКонтрагентов.Ссылка.Отчество Как Отчество,
	|	ВЫБОР
	|		КОГДА ТаблицаКонтрагентов.Ссылка.ФормаСобственности = ЗНАЧЕНИЕ(Перечисление.ФормыСобственности.ЧастноеЛицо)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЯвляетсяЮридическимЛицом
	|ИЗ
	|	ТаблицаКонтрагентов КАК ТаблицаКонтрагентов";
	
	Если НЕ МассивИзменений.Количество()=0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ Контрагенты.ПометкаУдаления", "НЕ Контрагенты.ПометкаУдаления И Контрагенты.Ссылка В(&МассивСсылок)");
		Запрос.УстановитьПараметр("МассивСсылок", МассивИзменений);
	КонецЕсли;
	
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаОбъектов;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБМЕНА ПО HTTP

Функция HTTPЗагрузитьССервера(СтруктураПараметровСайта, ТипСоединения, КоличествоОбработанныхДокументов)
	
	Успешно								= Истина;
	ОтветСервера						= "";
	Соединение							= Неопределено;
	КоличествоОбработанныхДокументов	= 0;
	
	АдресДляРаботы	= СтруктураПараметровСайта.АдресСкрипта + "?type="+ТипСоединения;
	
	СтрокаСообщенияПользователю			= "";
	Успешно	= HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	Если (НЕ ПустаяСтрока(СтрокаСообщенияПользователю)) Тогда
		СообщитьОбОшибкеОбмена(СтрокаСообщенияПользователю, Ложь);
	КонецЕсли;
	
	Если (НЕ Успешно) Тогда Возврат Ложь; КонецЕсли;
			
	КукиИмя      	  = СтрПолучитьСтроку(ОтветСервера,2);
	КукиЗначение 	  = СтрПолучитьСтроку(ОтветСервера,3);
	ЗаголовкиЗапросов = Новый Соответствие;
	ЗаголовкиЗапросов.Вставить("Cookie", КукиИмя + "=" + КукиЗначение);

	ОтобразитьСостояние("Загрузка данных с сервера...");
	
	ИнформацияДляПользователя	= "";
	
	ПараметрЗапросаHTTP_ДатаВремяИзмененияНаСайте = "";
	Если ТипСоединения = "users" Тогда
		ПараметрЗапросаHTTP_ДатаВремяИзмененияНаСайте = ПреобразоватьДатаВремяВПараметрЗапросаНаСайт(УзелОбмена.ДатаИзмененияКонтрагенты);
	ИначеЕсли ТипСоединения = "car" Тогда
		ПараметрЗапросаHTTP_ДатаВремяИзмененияНаСайте = ПреобразоватьДатаВремяВПараметрЗапросаНаСайт(УзелОбмена.ДатаИзмененияАвтомобили);
	ИначеЕсли ТипСоединения = "request" Тогда
		ПараметрЗапросаHTTP_ДатаВремяИзмененияНаСайте = ПреобразоватьДатаВремяВПараметрЗапросаНаСайт(УзелОбмена.ДатаИзмененияЗаявки);
	КонецЕсли;
	
	ОтветСервера				= HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_ПолучитьДанные + ?(ВыгружатьТолькоИзменения, ПараметрЗапросаHTTP_ДатаВремяИзмененияНаСайте, ПараметрЗапросаHTTP_ПолнаяВыгрузка), ЗаголовкиЗапросов, ИнформацияДляПользователя);
	Если (НЕ ПустаяСтрока(ИнформацияДляПользователя)) Тогда
		СообщитьПользователю("Данные, полученные от сервера:"+Символы.ПС+ИнформацияДляПользователя, Ложь,,1);
	КонецЕсли;
	
	Если (ОтветСервера = Неопределено) Тогда
		СообщитьОбОшибкеОбмена("Не удалось загрузить данные с сервера.", Ложь);
		Возврат Ложь;
	КонецЕсли;
		
	СтрокаCML					= "";
	
	Если (Лев(ОтветСервера, 2) = "PK") Тогда
		СтрокаCML	= РаспаковатьZIPАрхив(ОтветСервера);
	Иначе
		Если (Лев(ОтветСервера, 5) = "<?xml") Тогда
			СтрокаCML = ОтветСервера;
		КонецЕсли;	
	КонецЕсли;						
	
	Если (обЗначениеНеЗаполнено(СтрокаCML)) Тогда
		СообщитьОбОшибкеОбмена("Не удалось прочитать данные, загруженные с сервера.", Ложь);
		Возврат Ложь;		
	КонецЕсли;
	
	///////////////////////////////////////
	//
	
	ТаблицаОбъектов = РазобратьXML(СтрокаCML, ТипСоединения);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Загрузка";
	НоваяСтрока.Содержание	= СтрокаCML;
	
	Успешно = ОбработатьЗагруженныеОбъекты(ТаблицаОбъектов, ТипСоединения, КоличествоОбработанныхДокументов);
	
	
	ИнформацияДляПользователя	= "";
	HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы+ПараметрЗапросаHTTP_УспешноеЗавершениеИмпорта, ЗаголовкиЗапросов, ИнформацияДляПользователя);
	Если (НЕ ПустаяСтрока(ИнформацияДляПользователя)) Тогда
		СообщитьПользователю("Данные, полученные от сервера:"+Символы.ПС+ИнформацияДляПользователя, Ложь,,1);
	КонецЕсли;
					
	Возврат Успешно;

	
КонецФункции

Функция HTTPВыгрузитьНаСервер(СтруктураПараметровСайта,	КаталогОбмена, МассивПодкаталогов = Неопределено, ОжидатьЗавершенияИмпортаФайловСервером = Ложь, ТипСоединения = "catalog")
	
	Успешно			= Ложь;
	ОтветСервера	= "";
	Соединение		= Неопределено;
	
	АдресДляРаботы = СтруктураПараметровСайта.АдресСкрипта + "?type=" + ТипСоединения; 
		
	СтрокаСообщенияПользователю = "";
	Успешно = HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	Если (НЕ ПустаяСтрока(СтрокаСообщенияПользователю)) Тогда
		СообщитьОбОшибкеОбмена(СтрокаСообщенияПользователю, Истина);
	КонецЕсли;
	
	Если (НЕ Успешно) Тогда Возврат Ложь; КонецЕсли;	
		
	КукиИмя      	 	= СтрПолучитьСтроку(ОтветСервера, 2);
	КукиЗначение 	 	= СтрПолучитьСтроку(ОтветСервера, 3);
	ЗаголовкиЗапросов = Новый Соответствие;
	ЗаголовкиЗапросов.Вставить("Cookie", КукиИмя + "=" + КукиЗначение);

	ОтобразитьСостояние("Запрос параметров обмена...");
	
	ОтветСервера		= HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_Инициализация, ЗаголовкиЗапросов);
	
	Если (ОтветСервера	= Неопределено) Тогда 
		СообщитьОбОшибкеОбмена("Не удалось получить параметры обмена с сервера.", Истина);
		Возврат Ложь;
	КонецЕсли;
		
	ZIPФайлыРазрешены						= Ложь;
	ОграничениеРазмераФрагментаФайлаОбмена	= 0;
					
	Если (СтрЧислоСтрок(ОтветСервера) <> 2) Тогда	
		СообщитьОбОшибкеОбмена("Не удалось прочитать ответ сервера. Параметры обмена не получены.", Истина);
		Возврат Ложь;
	КонецЕсли;
		
	ZIPФайлыРазрешены	= НРег(СтрПолучитьСтроку(ОтветСервера,1))	= ОтветСервера_ZIPРазрешен;

	Попытка 
		ОграничениеРазмераФрагментаФайлаОбмена = Число(СтрЗаменить(НРег(СтрПолучитьСтроку(ОтветСервера,2)), ОтветСервера_ОграничениеРазмераФрагментаФайлаОбмена, ""));
	Исключение
		СообщитьОбИсключительнойОшибке(Истина);
		ОграничениеРазмераФрагментаФайлаОбмена	= -1;
	КонецПопытки;	
			
	МассивИсходныхCMLФайлов	= НайтиФайлы(КаталогОбмена, "*.xml");
	СписокФайловДляОтправки	= ПолучитьСписокФайловДляОтправки(КаталогОбмена, МассивПодкаталогов);
	
	Для Каждого ТекФайл Из СписокФайловДляОтправки Цикл
		ВспомогательныйФайл		= Новый Файл(ТекФайл.Значение);
		Если (Не Булево(Найти(ТекФайл.Значение,".xml"))) Тогда Продолжить; КонецЕсли;
		Если (ВспомогательныйФайл.Существует()) Тогда
			СодержаниеФайла 	= Новый ТекстовыйДокумент();
			СодержаниеФайла.Прочитать(ТекФайл.Значение);
			Если (СодержаниеФайла.КоличествоСтрок()>0) Тогда
				НоваяСтрока	= ТаблицаФайловОбмена.Добавить();
				НоваяСтрока.Дата		= ТекущаяДата();
				НоваяСтрока.Режим		= "Выгрузка";
				НоваяСтрока.Содержание	= СодержаниеФайла.ПолучитьТекст();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если (ZIPФайлыРазрешены) Тогда
		ОтобразитьСостояние("Подготовка ZIP-архива...");
		СписокФайловДляОтправки	= ПодготовитьZIPАрхивы(СписокФайловДляОтправки, КаталогОбмена);
	КонецЕсли;	
	
	Если (ОграничениеРазмераФрагментаФайлаОбмена > 0) Тогда
		СписокФайловДляОтправки = РазделитьФайлыНаФрагменты(СписокФайловДляОтправки, ОграничениеРазмераФрагментаФайлаОбмена);
	КонецЕсли;	
	
	ВсегоФайлов					= СписокФайловДляОтправки.Количество();
	
	Для Каждого ТекФайл Из СписокФайловДляОтправки Цикл
		ОтобразитьСостояние("Идет отправка файла на сервер (" + Строка(СписокФайловДляОтправки.Индекс(ТекФайл) + 1) + " из " + Строка(ВсегоФайлов) + "): " + ТекФайл.Значение);
		ОтветСервера = HTTPОтправитьФайлНаСервер(ТекФайл.Значение, Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_ПередачаФайла + ТекФайл.Представление, ЗаголовкиЗапросов);
		
		Если (ZIPФайлыРазрешены) Тогда
			Попытка
				УдалитьФайлы(ТекФайл.Значение);
			Исключение
			КонецПопытки;	
		КонецЕсли;	
		
		Если (ОтветСервера = Неопределено) Тогда
			СообщитьОбОшибкеОбмена("Не удалось получить ответ сервера. Файл не отправлен (" + ТекФайл.Значение + ").", Истина);
			Возврат Ложь;
		КонецЕсли;
			
		СостояниеОбмена = СокрЛП(НРег(СтрПолучитьСтроку(ОтветСервера,1)));
		
		Если (СостояниеОбмена = ОтветСервера_АварийноеЗавершениеТекущейОперации) Тогда
			СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера. Файл не отправлен (" + ТекФайл.Значение + ").", Истина);
			СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
			Возврат Ложь;
			
		ИначеЕсли (СостояниеОбмена = ОтветСервера_УспешноеЗавершениеТекущейОперации) Тогда		
			Если (СтрЧислоСтрок(ОтветСервера)>1) Тогда
				СообщитьПользователю("Получен расширенный статус успешного завершения сеанса", Истина, СтатусСообщения.Информация,1);
				СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
			КонецЕсли;
			
		Иначе
			СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера. Не получен статус завершения операции. Файл не отправлен (" + ТекФайл.Значение + ").", Истина);
			//СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
			Возврат Ложь;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	ИмпортУспешноЗавершен					= Ложь;
	
	Если (ОжидатьЗавершенияИмпортаФайловСервером) Тогда
							
		Для Каждого ТекФайл Из МассивИсходныхCMLФайлов Цикл
			ИмпортПродолжается		= Истина;
			ТекущееСостояние		= "";
		
			Пока ( ИмпортПродолжается ) Цикл			
				ИмпортПродолжается = Ложь;
				ОтобразитьСостояние("Ожидание окончания загрузки данных сервером: " + ТекущееСостояние);
				ТекущееСостояние	= "";
				
				ОтветСервера		= HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_ИмпортФайлаСервером + ТекФайл.Имя, ЗаголовкиЗапросов);
				
				Если (ОтветСервера = Неопределено) Тогда 
					
					Успешно			= Ложь;
					СообщитьОбОшибкеОбмена("Не удалось получить текущее состояние процесса обмена. Данные обмена отправлены, но не загружены.", Истина);
					
				ИначеЕсли СтрЧислоСтрок(ОтветСервера) = 0 Тогда
					Успешно			= Ложь;
					СообщитьПользователю("Не удалось прочитать данные о текущем состоянии процесса обмена. Данные обмена отправлены, но не загружены.", Истина,,1);	
				Иначе
					
					СостояниеОбмена	= НРег(СтрПолучитьСтроку(ОтветСервера,1));
					Если (СостояниеОбмена = ОтветСервера_АварийноеЗавершениеТекущейОперации) Тогда
						Успешно = Ложь;
						СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера.", Истина);
						СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
					ИначеЕсли (СостояниеОбмена = ОтветСервера_УспешноеЗавершениеТекущейОперации) Тогда
						ИмпортУспешноЗавершен	= Истина;
					ИначеЕсли (СостояниеОбмена = ОтветСервера_ВыполнениеТекущейОперации) Тогда
						ТекущееСостояние		= ТекущееСостояние + СтрПолучитьСтроку(ОтветСервера, 2);
						ИмпортПродолжается		= Истина;
					Иначе
						Успешно					= Ложь;
						СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера. Получен неизвестный статус импорта.", Истина);
						СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
					КонецЕсли;
					
				КонецЕсли;					
				
			КонецЦикла;	//Импорт
			
			Если (НЕ ИмпортУспешноЗавершен) Тогда Прервать; КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;								
	
	Возврат Успешно;
	
КонецФункции

Функция HTTPОтправитьФайлНаСервер(ПолноеИмяФайла, Соединение, ПараметрыЗапроса="", Заголовки="")
	
	ОтветСервера		= Неопределено;
	ИмяФайлаОтвета		= ПолучитьИмяВременногоФайла();
	
	Если Заголовки="" Тогда
		Заголовки=Новый Соответствие;
	КонецЕсли; 
	
	Попытка
		Соединение.ОтправитьДляОбработки(ПолноеИмяФайла, СокрЛП(ПараметрыЗапроса), ИмяФайлаОтвета, Заголовки);
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, ОписаниеОшибки())
	КонецПопытки;	
	
	ФайлОтвета			= Новый Файл(ИмяФайлаОтвета);
	
	Если (ФайлОтвета.Существует()) Тогда
		ТекстОтвета 	= Новый ТекстовыйДокумент();
		ТекстОтвета.Прочитать(ИмяФайлаОтвета);
		Если (ТекстОтвета.КоличествоСтрок()>0) Тогда
			ОтветСервера = ТекстОтвета.ПолучитьТекст();	
		Иначе
			СообщитьПользователю("Отправка файла на сервер: Получен пустой ответ сервера.", Истина,,1); 	
		КонецЕсли;		
	Иначе	
		СообщитьПользователю("Отправка файла на сервер: Ответ сервера не получен.", Истина,,1); 
	КонецЕсли;	
	
	Попытка
		УдалитьФайлы(КаталогВременныхФайлов(), ИмяФайлаОтвета);
	Исключение
	КонецПопытки;
	
	Возврат ОтветСервера;	
	
КонецФункции

Функция ПолучитьСписокФайловДляОтправки(КаталогОбмена, МассивПодкаталогов);
	
	СписокФайлов		= Новый СписокЗначений();
	Маска				= "*.*";
	
	ВсеФайлыДляВыгрузки = НайтиФайлы(КаталогОбмена, Маска);
	
	Если ( МассивПодкаталогов <> Неопределено ) Тогда
		
		Для Каждого Подкаталог Из МассивПодкаталогов Цикл
			
			ФайлыВПодкаталоге	= НайтиФайлы(КаталогОбмена + "\" + Подкаталог, Маска);
			Для Каждого ТекФайл Из ФайлыВПодкаталоге Цикл		
				Если (ТекФайл.ЭтоКаталог()) Тогда	
					ФайлыВДобавочномПодкаталоге	= НайтиФайлы(ТекФайл.ПолноеИмя, Маска);
					Для Каждого ТекФайлВПодкаталоге Из ФайлыВДобавочномПодкаталоге Цикл
						Если (НЕ ТекФайлВПодкаталоге.ЭтоКаталог()) Тогда	
							ВсеФайлыДляВыгрузки.Добавить(ТекФайлВПодкаталоге);  								
						КонецЕсли;		
					КонецЦикла;				
				Иначе	
					ВсеФайлыДляВыгрузки.Добавить(ТекФайл);  
				КонецЕсли;			
			КонецЦикла;	
			
		КонецЦикла;			
				
	КонецЕсли;
	
	Для Каждого ТекФайл Из ВсеФайлыДляВыгрузки цикл
		
		Если (НЕ ТекФайл.ЭтоКаталог()) Тогда
			ПолноеИмяФайлаДляСервера = ПодготовитьИмяФайлаДляСервера(ТекФайл, КаталогОбмена);
			СписокФайлов.Добавить(ТекФайл.ПолноеИмя, ПолноеИмяФайлаДляСервера);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат СписокФайлов;
	
КонецФункции

Функция ПодготовитьИмяФайлаДляСервера(ФайлОбъект, КаталогОбмена)
	
	ПолноеИмяФайлаДляСервера = "";	
	Если (Булево(Найти(ФайлОбъект.Имя, ".xml"))) Тогда
		ПолноеИмяФайлаДляСервера	= ФайлОбъект.Имя;
	Иначе	
		//у картинки надо оставить 2 папки и развернуть слэши
		ПолноеИмяФайлаДляСервера	= ФайлОбъект.ПолноеИмя;
		ПутьДляУдаления				= КаталогОбмена + "\";
		ПолноеИмяФайлаДляСервера	= СтрЗаменить(ПолноеИмяФайлаДляСервера, ПутьДляУдаления, "");
		ПолноеИмяФайлаДляСервера	= СтрЗаменить(ПолноеИмяФайлаДляСервера, "\", "/");
	КонецЕсли;	
	ПолноеИмяФайлаДляСервера = УдалитьДополнительныеРасширенияФайла(ПолноеИмяФайлаДляСервера);
	
	Возврат ПолноеИмяФайлаДляСервера;
	
КонецФункции

Функция УдалитьДополнительныеРасширенияФайла(ИсходноеИмяФайла)

	ПозицияТочки 			  = Найти(ИсходноеИмяФайла, ".");
	ИмяФайла    			  = Лев(ИсходноеИмяФайла, ПозицияТочки - 1);
	ПраваяЧастьИсходногоИмени = Прав(ИсходноеИмяФайла, СтрДлина(ИсходноеИмяФайла) - ПозицияТочки);
	ПозицияТочки 			  = Найти(ПраваяЧастьИсходногоИмени, ".");
	Расширение 				  = ПраваяЧастьИсходногоИмени;
	Если (ПозицияТочки > 0) Тогда
		Расширение = Лев(ПраваяЧастьИсходногоИмени, ПозицияТочки - 1);	
	КонецЕсли;	
	Возврат ИмяФайла + "." + Расширение;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ ФАЙЛОВ

Функция РаспаковатьZIPАрхив(СтрокаZIP)
	
	СтрокаСодержимого	= "ZIP_ERROR";
	
	ИмяФайла			= ПолучитьИмяВременногоФайла("zip");
	ИмяКаталога			= КаталогВременныхФайлов()+Строка(Новый УникальныйИдентификатор);
	СоздатьКаталог(ИмяКаталога);
	
	СтрокаВФайл			= Новый ТекстовыйДокумент();
	СтрокаВФайл.УстановитьТекст(СтрокаZIP);
	Попытка
		СтрокаВФайл.Записать(ИмяФайла);
	Исключение
	КонецПопытки;
	
	ЧтениеZIP			= Новый ЧтениеZIPФайла(ИмяФайла);
	ЧтениеZIP.ИзвлечьВсе(ИмяКаталога);
	ЧтениеZIP.Закрыть();
	
	РаспакованныеФайлы	= НайтиФайлы(ИмяКаталога, "*.xml");
	
	Если (РаспакованныеФайлы.Количество() = 1) Тогда
		СтрокаИзФайла 	  = Новый ТекстовыйДокумент();
		СтрокаИзФайла.Прочитать(РаспакованныеФайлы[0].ПолноеИмя);
		СтрокаСодержимого = СтрокаИзФайла.ПолучитьТекст();
	КонецЕсли;
	
	Попытка
		УдалитьФайлы(ИмяФайла);
		УдалитьФайлы(ИмяКаталога);
	Исключение
	КонецПопытки;
	
	Возврат СтрокаСодержимого;
	
КонецФункции

Функция РазделитьФайлыНаФрагменты(СписокФайлов, ОграничениеРазмераФрагмента)
	
	НовыйСписокФайлов = Новый СписокЗначений();
	Для Каждого ТекФайл Из СписокФайлов цикл
		
		ФайлНаДиске	= Новый Файл(ТекФайл.Значение);
		Если (ФайлНаДиске.Размер() > ОграничениеРазмераФрагмента) Тогда
			МассивФрагментов = РазделитьФайл(ФайлНаДиске.ПолноеИмя, ОграничениеРазмераФрагмента);
			Для Каждого НовыйФайл Из МассивФрагментов Цикл
				НовыйСписокФайлов.Добавить(НовыйФайл, ТекФайл.Представление);
			КонецЦикла;	
			УдалитьФайлы(ФайлНаДиске.ПолноеИмя);
		Иначе
			НовыйСписокФайлов.Добавить(ТекФайл.Значение, ТекФайл.Представление);	
		КонецЕсли;	
		
	КонецЦикла;
	Возврат НовыйСписокФайлов;
	
КонецФункции

Функция ПодготовитьZIPАрхивы(СписокФайлов, КаталогОбмена)
	
	ПолноеИмяФайлаАрхива = ПолучитьИмяВременногоФайла("zip");
	ЗаписьАрхива   	     = Новый ЗаписьZipФайла(ПолноеИмяФайлаАрхива);
	
	СоздатьКаталог(КаталогОбмена);
	
	ЗаписьАрхива.Добавить(КаталогОбмена + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ЗаписьАрхива.Записать();
	
	НовыйСписокФайлов = Новый СписокЗначений();
	НовыйСписокФайлов.Добавить(ПолноеИмяФайлаАрхива, ПолучитьИмяФайла(ПолноеИмяФайлаАрхива));
	
	Возврат НовыйСписокФайлов;
	
КонецФункции

Функция ПолучитьИмяФайла(ПолноеИмяФайла)
	
	ИмяФайла = "";
	ПромТекст = СтрЗаменить(ПолноеИмяФайла, "/", "\");
	ПромТекст = СтрЗаменить(ПолноеИмяФайла, "\", Символы.ПС);	
	КоличествоЭлементов = СтрЧислоСтрок(ПромТекст);
	Для Счетчик = 1 По КоличествоЭлементов Цикл
		ИмяФайла = СтрПолучитьСтроку(ПромТекст, Счетчик);
	КонецЦикла;
	Возврат ИмяФайла;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ АНАЛИЗА ИЗМЕНЕНИЙ ДАННЫХ

Процедура ЗаполнитьСтруктуруИзмененийДляУзла(УзелПланаОбмена, СтруктураВозврата)
	
	Если ( обЗначениеНеЗаполнено(УзелПланаОбмена) ) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	
	ЭтотУзелСсылка	= ПланыОбмена.ОбменССайтомАвтосервиса.ЭтотУзел();	
	ЗаписьСообщения	= ПланыОбмена.СоздатьЗаписьСообщения();
	Если УзелПланаОбмена=ЭтотУзелСсылка Тогда
		СообщитьПользователю("Выбранная настройка обмена должна быть заполнена в другом узле плана обмена ""Обмен с сайтом"".
				|Настройка плана обмена, в адрес которой будет послано сообщение обмена данными, не должна быть пустой и должна быть ссылкой на ""этот узел""", Истина, СтатусСообщения.Информация, 1);
		Возврат;
	Иначе
		Попытка
			ЗаписьСообщения.НачатьЗапись(ЗаписьXML, УзелПланаОбмена);
		Исключение
			СообщитьПользователю("Некорректно заполнен узел плана обмена ""Обмен с сайтом"""+Символы.ПС+ИнформацияОбОшибке().Описание, Истина, СтатусСообщения.Информация, 1);
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	// Получение Изменений
	ОтобразитьСостояние("Выбор изменений ...");
	Счетчик = 0;
	
	Выборка = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения);
	
	Пока Выборка.Следующий() Цикл
		
		Счетчик = Счетчик + 1;
		Если Счетчик % 100 = 0 Тогда
			ОтобразитьСостояние("Обработано объектов: " + Счетчик);
		КонецЕсли;
		
		Данные    = Выборка.Получить();
		ТипДанных = ТипЗнч(Данные);              
		                                         
		Если      ( ТипДанных = Тип("СправочникОбъект.Модели") ) Тогда
			СтруктураВозврата.Модели.Добавить(Данные.Ссылка);
			
		ИначеЕсли ( ТипДанных = Тип("СправочникОбъект.Автомобили") ) Тогда
			СтруктураВозврата.Автомобили.Добавить(Данные.Ссылка);	
			
		ИначеЕсли ( ТипДанных = Тип("СправочникОбъект.Контрагенты") ) Тогда
			СтруктураВозврата.Контрагенты.Добавить(Данные.Ссылка);	
			
		ИначеЕсли ( ТипДанных = Тип("СправочникОбъект.Производители") ) Тогда
			СтруктураВозврата.Производители.Добавить(Данные.Ссылка);
			
		ИначеЕсли ( ТипДанных = Тип("СправочникОбъект.Цвета") ) Тогда
			СтруктураВозврата.Цвета.Добавить(Данные.Ссылка);
			
		ИначеЕсли ( ТипДанных = Тип("СправочникОбъект.ВидыРемонта") ) Тогда
			СтруктураВозврата.ВидыРемонта.Добавить(Данные.Ссылка);
			
		ИначеЕсли ( ТипДанных = Тип("СправочникОбъект.Автоработы") ) Тогда
			СтруктураВозврата.Автоработы.Добавить(Данные.Ссылка);
		
		ИначеЕсли ( ТипДанных = Тип("ДокументОбъект.ЗаявкаНаРемонт") ) Тогда
			СтруктураВозврата.Заявки.Добавить(Данные.Ссылка);
			
			Если ТипЗнч(Данные.Ссылка.Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
				ТекАвто = СтруктураВозврата.Автомобили.Найти(Данные.Ссылка.Автомобиль);
				Если ТекАвто = Неопределено Тогда
					СтруктураВозврата.Автомобили.Добавить(Данные.Ссылка.Автомобиль);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ( ТипДанных = Тип("ДокументОбъект.ЗаказНаряд") ) Тогда
			СтруктураВозврата.ЗаказНаряды.Добавить(Данные.Ссылка);
			
			ТекАвто = СтруктураВозврата.Автомобили.Найти(Данные.Ссылка.Автомобиль);
			Если ТекАвто = Неопределено Тогда
				СтруктураВозврата.Автомобили.Добавить(Данные.Ссылка.Автомобиль);
			КонецЕсли;
			
		ИначеЕсли (ТипЗнч(Данные) = Тип("РегистрСведенийНаборЗаписей.Автомобили")) Тогда
			
			Данные.Прочитать();
			Для Каждого Запись Из Данные Цикл
				ТекАвто = СтруктураВозврата.Автомобили.Найти(Запись.Автомобиль);
				Если ТекАвто = Неопределено Тогда
					СтруктураВозврата.Автомобили.Добавить(Запись.Автомобиль);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает массив узлов для регистрации, РегистрацияДляТоваров - флаг регистрации для товаров
Функция ПолучитьМассивУзловДляРегистрации(РегистрацияДляТоваров = Истина) Экспорт
	
	МассивУзлов = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбменССайтом.Ссылка КАК Ссылка
	               |ИЗ
	               |	ПланОбмена.ОбменССайтомАвтосервиса КАК ОбменССайтом
	               |ГДЕ
	               |	ОбменССайтом.Ссылка <> &ЭтотУзел";
				   
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменССайтомАвтосервиса.ЭтотУзел());
	МассивУзлов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
	Возврат МассивУзлов;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМАТИРОВАНИЯ ЗНАЧЕНИЙ CML

Функция ФорматКомментарияДляCML(Комментарий)
	Возврат Лев(Комментарий, 3000);
КонецФункции

Функция ФорматНаименованияДляCML(Наименование)
	Возврат Лев(Наименование, 250);
КонецФункции

Функция ФорматДатыДляCML(ЗначениеДата, ВернутьДату = Истина, ВернутьВремя = Ложь)
	
	ДатаСтрока  = Формат(Дата(ЗначениеДата), "ДФ=yyyy-MM-dd");
	ВремяСтрока = Формат(Дата(ЗначениеДата), "ДФ=ЧЧ:мм:сс"); //"ДЛФ=T");	
	Результат   = ""; 
	
	Если ( ВернутьДату И ВернутьВремя ) Тогда
		Результат = ДатаСтрока + "T" + ВремяСтрока;
	ИначеЕсли ( ВернутьДату И (НЕ ВернутьВремя) ) Тогда	
		Результат = ДатаСтрока;
	ИначеЕсли ((НЕ ВернутьДату) И ВернутьВремя ) Тогда		
		Результат = ВремяСтрока;
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции

Функция ФорматВалютыДляCML(ВалютаСсылка)
	
	ТекстВалюты = "???";
	
	Если ТипЗнч(ВалютаСсылка) = Тип("СправочникСсылка.Валюты") Тогда
		НайденнаяСтрока	= ПредставленияВалют.Найти(ВалютаСсылка,"Валюта");
		Если НайденнаяСтрока=Неопределено Тогда
			Если Строка(ВалютаСсылка.Код)="810" Тогда
				ТекстВалюты	= "RUB";
			Иначе
				ТекстВалюты	= ВалютаСсылка.Наименование;
			КонецЕсли;
		Иначе
			ТекстВалюты	= НайденнаяСтрока.Представление;
		КонецЕсли;
	КонецЕсли;	
		
	Возврат Лев(ТекстВалюты, 3);
	
КонецФункции

Функция ПолучитьПоСтавкеНДСЗначениеДляВыгрузки(СтавкаНДС)
	
	Если ( СтавкаНДС = Справочники.СтавкиНДС.БезНДС ) Тогда
		ЗначениеНалога = "Без налога";
	ИначеЕсли ( СтавкаНДС.Ставка = 0 ) Тогда		
		ЗначениеНалога = "0";
	ИначеЕсли ( СтавкаНДС.Ставка = 10 ) Тогда		
		ЗначениеНалога = "10";
	// ставка ндс 10%/110%		
	ИначеЕсли ( СтавкаНДС.Ставка = 18 ) Тогда		
		ЗначениеНалога = "18";
	// ставка НДС 18%/118%				
	ИначеЕсли ( СтавкаНДС.Ставка = 20 ) Тогда				
		ЗначениеНалога = "20";
	// ставка НДС 20%/120%
	Иначе
		ЗначениеНалога = "";	
	КонецЕсли;
	
	Возврат ЗначениеНалога;
	
КонецФункции

Функция ПолучитьПоЗначениюДляВыгрузкиСтавкуНДС(ЗначениеНалога)
	
	Если ЗначениеНалога = "0" Тогда		
		Ставка = 0;
	ИначеЕсли ЗначениеНалога = "10" Тогда		
		Ставка = 10;     
	ИначеЕсли ЗначениеНалога = "10/110" Тогда		
		Ставка = Окр(10/100, 2, 1);
	ИначеЕсли ЗначениеНалога = "18" Тогда		
		Ставка = 18;
	ИначеЕсли ЗначениеНалога = "18/118" Тогда				
		Ставка = Окр(10/100, 2, 1);
	ИначеЕсли ЗначениеНалога = "20" Тогда				
		Ставка = 20
	ИначеЕсли ЗначениеНалога = "20/120" Тогда						
		Ставка = Окр(20/120, 2, 1);
	Иначе
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	СтавкиНДС.Ссылка КАК СтавкаНДС
	      	               |ИЗ
	      	               |	Справочник.СтавкиНДС КАК СтавкиНДС
	      	               |ГДЕ
	      	               |	СтавкиНДС.Ставка = &Ставка");
	Запрос.УстановитьПараметр("Ставка", Ставка);
	РезультатЗапроса	= Запрос.Выполнить();
	
	Если ( Не РезультатЗапроса.Пустой() ) Тогда
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат	Выборка.СтавкаНДС;
	Иначе
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьДеньСледНедели(День)
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, &Приращение) КАК ДеньСледНедели");
	Запрос.УстановитьПараметр("Дата", День);
	Запрос.УстановитьПараметр("Приращение", 7);
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		Возврат Выборка.ДеньСледНедели;	
	Иначе
		Возврат ТекущаяДата();
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ CML ИЗ СПИСКА ЗНАЧЕНИЙ

Процедура ЗаписатьCMLПоСпискуЗначений(ОбъектCML, СписокЗначений);
	
	Для Каждого Элемент Из СписокЗначений Цикл
		Если ( Элемент.Представление = КонецЭлементаCML ) Тогда
			ОбъектCML.ЗаписатьКонецЭлемента();
		ИначеЕсли ( Элемент.Представление = НачалоЭлементаCML ) Тогда	
			ОбъектCML.ЗаписатьНачалоЭлемента(Элемент.Значение);
		ИначеЕсли ( Элемент.Представление = "" ) Тогда
		    ОбъектCML.ЗаписатьТекст(Элемент.Значение);
		ИначеЕсли ( Булево(Найти(Элемент.Представление, ПрефиксУзлаCML)) ) Тогда
			ИмяУзла = СтрЗаменить(Элемент.Представление, ПрефиксУзлаCML, "");
			ЗаписатьТекстовойУзел(ОбъектCML, ИмяУзла, Элемент.Значение);	
		ИначеЕсли ( Булево(Найти(Элемент.Представление, ПрефиксАтрибутаCML)) ) Тогда
			ИмяАтрибута = СтрЗаменить(Элемент.Представление, ПрефиксАтрибутаCML, "");
			ОбъектCML.ЗаписатьАтрибут(ИмяАтрибута, Элемент.Значение);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ДобавитьУзелCML(СписокЗначений, НаименованиеУзла, Значение);
	СписокЗначений.Добавить(Значение, ПрефиксУзлаCML + НаименованиеУзла);
КонецПроцедуры

Процедура ДобавитьНачалоЭлементаCML(СписокЗначений, НаименованиеЭлемента);
	СписокЗначений.Добавить(НаименованиеЭлемента, НачалоЭлементаCML); 
КонецПроцедуры

Процедура ДобавитьАтрибутCML(СписокЗначений, НаименованиеАтрибута, Значение)
	СписокЗначений.Добавить(Значение, ПрефиксАтрибутаCML + НаименованиеАтрибута);
КонецПроцедуры

Процедура ДобавитьТекстCML(СписокЗначений, Значение)
	СписокЗначений.Добавить(Значение, "");
КонецПроцедуры

Процедура ДобавитьКонецЭлементаCML(СписокЗначений);
	СписокЗначений.Добавить("", КонецЭлементаCML); 
КонецПроцедуры

Процедура ЗаписатьАтрибутВXML(ОбъектЗаписи, ИмяАтрибута, ЗначениеАтрибута)
	
	СтрокаЗаписи = ПолучитьСтрокуЗаписиДляXML(ЗначениеАтрибута);
	ОбъектЗаписи.ЗаписатьАтрибут(ИмяАтрибута, СтрокаЗаписи);	
	
КонецПроцедуры

Функция ПолучитьМеждународноеСокращение(ЕдиницаИзмерения)
	Если (ЕдиницаИзмерения.Наименование = "шт") Тогда
		Возврат "PCE";	
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "элем") Тогда
		Возврат "NCL";	
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "упак") Тогда
		Возврат "NMP";	
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "тыс.шт") Тогда
		Возврат "MIL";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "т") Тогда
		Возврат "TNE";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "рул") Тогда
		Возврат "NPL";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "пара") Тогда
		Возврат "NPR";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "набор") Тогда
		Возврат "SET";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "кг") Тогда
		Возврат "KGM";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "изд") Тогда
		Возврат "NAR";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "г") Тогда
		Возврат "GRM";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "100 ящ") Тогда
		Возврат "HBX";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "100 шт") Тогда
		Возврат "HBX";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "100 упак") Тогда
		Возврат "CNP";
	Иначе
		Возврат "";	
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПИСИ ДАННЫХ В CML

Процедура ЗаписатьТекстовойУзел(ОбъектXML, Имя, Значение, ОбязательнаяПроверкаНаПустуюСтроку = Истина)
    
	СтрокаЗаписи = ПолучитьСтрокуЗаписиДляXML(Значение);
	
	Если ( ОбязательнаяПроверкаНаПустуюСтроку И НЕ ЗначениеЗаполнено(СтрокаЗаписи) ) Тогда	
		Возврат;			
	КонецЕсли;	

	ОбъектXML.ЗаписатьНачалоЭлемента(Имя);
	ОбъектXML.ЗаписатьТекст(СтрокаЗаписи);
	ОбъектXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВыгрузитьОсновныеРеквизитыГруппыДляКлассификатора(ОбъектCML, Группа)
	
	ИдГруппы = Группа.Ссылка.УникальныйИдентификатор();
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид", ИдГруппы);
	ИмяГруппы = Группа.Наименование;
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", ФорматНаименованияДляCML(ИмяГруппы));
	
КонецПроцедуры

Процедура ВыгрузитьГруппыРекурсивно(ОбъектCML, ДеревоГрупп);
	
	НужноВыгружатьГруппы = Ложь;
	Для Каждого СтрокаДерева Из ДеревоГрупп Цикл	
		Если ( НЕ СтрокаДерева.ЭтоГруппа ) Тогда Продолжить; КонецЕсли;
		
		Если ( Не НужноВыгружатьГруппы ) Тогда
			ОбъектCML.ЗаписатьНачалоЭлемента("Группы");
			НужноВыгружатьГруппы = Истина;
		КонецЕсли;
		
		ОбъектCML.ЗаписатьНачалоЭлемента("Группа");
		ВыгрузитьОсновныеРеквизитыГруппыДляКлассификатора(ОбъектCML, СтрокаДерева);
		
		Если ( СтрокаДерева.Строки.Количество() > 0 ) Тогда
			ВыгрузитьГруппыРекурсивно(ОбъектCML, СтрокаДерева.Строки);
		КонецЕсли;	
		ОбъектCML.ЗаписатьКонецЭлемента();		
	КонецЦикла;	
	
	Если ( НужноВыгружатьГруппы ) Тогда
		ОбъектCML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСтрокуЗаписиДляXML(Значение)
	
	СтрокаЗаписи = Строка(Значение);
		
	Если ( ТипЗнч(Значение) = Тип("Число") ) Тогда
		
		СтрокаЗаписи = СтрЗаменить(СтрокаЗаписи, Символы.НПП, "");
		СтрокаЗаписи = СтрЗаменить(СтрокаЗаписи, ",", ".");
		
	ИначеЕсли ( ТипЗнч(Значение) = Тип("Булево") ) Тогда
		
		Если (Значение) Тогда
			СтрокаЗаписи = "true";
		Иначе
			СтрокаЗаписи = "false";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаЗаписи;
	
КонецФункции

Процедура ВыгрузитьЮрАдресОрганизации(ОбъектXML, Организация)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Поле5,
	               |	КонтактнаяИнформация.Поле6,
	               |	КонтактнаяИнформация.Поле7,
	               |	КонтактнаяИнформация.Поле8,
	               |	КонтактнаяИнформация.Поле9,
	               |	КонтактнаяИнформация.Поле10,
	               |	КонтактнаяИнформация.Комментарий
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |	И КонтактнаяИнформация.Тип = &Тип
	               |	И КонтактнаяИнформация.Вид = &Вид";
				   
	Запрос.УстановитьПараметр("Объект", Организация);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ОбъектXML.ЗаписатьНачалоЭлемента("ЮридическийАдрес");
		
		ЗаписатьТекстовойУзел(ОбъектXML, "Представление", Выборка.Представление);
		ЗаписатьТекстовойУзел(ОбъектXML, "Комментарий", Выборка.Комментарий);
		
		АдресРоссийский = Истина;
		
		Если ( ЗначениеЗаполнено(Выборка.Поле1) ) Тогда
			
			Попытка
				ЧислоИндекса = Число(Выборка.Поле1);
			Исключение
				АдресРоссийский = Ложь;
			КонецПопытки;
			
		КонецЕсли;
			
		// российский адрес
		Если АдресРоссийский Тогда
		
			//Возможные значения: Почтовый индекс, Страна, Регион, Район, Населенный пункт, Город, Улица, Дом, Корпус, Квартира
			ЗаписатьАдресноеПоле(ОбъектXML, "Почтовый индекс", Выборка.Поле1);
			ЗаписатьАдресноеПоле(ОбъектXML, "Регион", Выборка.Поле2);
			ЗаписатьАдресноеПоле(ОбъектXML, "Район", Выборка.Поле3);
			ЗаписатьАдресноеПоле(ОбъектXML, "Населенный пункт", Выборка.Поле4);
			ЗаписатьАдресноеПоле(ОбъектXML, "Город", Выборка.Поле5);
			ЗаписатьАдресноеПоле(ОбъектXML, "Улица", Выборка.Поле6);
			ЗаписатьАдресноеПоле(ОбъектXML, "Дом", Выборка.Поле7);
			ЗаписатьАдресноеПоле(ОбъектXML, "Корпус", Выборка.Поле8);
			ЗаписатьАдресноеПоле(ОбъектXML, "Квартира", Выборка.Поле9);
			
		Иначе
			
			ЗаписатьАдресноеПоле(ОбъектXML, "Страна", Выборка.Поле1);
			
		КонецЕсли;	
		
		ОбъектXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПоСтруктуреСтрокиСФИО(СтруктураДанныхКонтрагента)
	
	ТекущаяСтрока	= "";
	Фамилия			= "";
	Имя				= "";
	Отчество		= "";
	
	СтруктураДанныхКонтрагента.Свойство("Фамилия", Фамилия);
	СтруктураДанныхКонтрагента.Свойство("Имя", Имя);
	СтруктураДанныхКонтрагента.Свойство("Отчество", Отчество);
	
	Если ( ЗначениеЗаполнено(Фамилия) ) Тогда
		ТекущаяСтрока = Фамилия;
	КонецЕсли;
	
	Если ( ЗначениеЗаполнено(Имя) ) Тогда
		ТекущаяСтрока = СокрЛП(ТекущаяСтрока + " " + Имя);
	КонецЕсли;
	
	Если ( ЗначениеЗаполнено(Отчество) ) Тогда
		ТекущаяСтрока = СокрЛП(ТекущаяСтрока + " " + Отчество);
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
	
КонецФункции

Процедура ЗаписатьАдресноеПоле(ОбъектXML, ИмяПоля, Значение)
	
	Если ( обЗначениеНеЗаполнено(Значение) ) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента("АдресноеПоле");
	
	ЗаписатьТекстовойУзел(ОбъектXML, "Тип", ИмяПоля);
	ЗаписатьТекстовойУзел(ОбъектXML, "Значение", Значение);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьГруппыПростогоСправочника(ОбъектCML, ТаблицаОбъектов, ИмяСправочника)
	
	ЗапросПоГруппам = Новый Запрос(
	"ВЫБРАТЬ
	|	ТекСправочник.Ссылка КАК Ссылка,
	|	ТекСправочник.Наименование,
	|	ТекСправочник.ЭтоГруппа
	|ИЗ
	|	Справочник." + ИмяСправочника + " КАК ТекСправочник
	|ГДЕ
	|	ТекСправочник.Ссылка В(&Ссылка)
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ");
	ЗапросПоГруппам.УстановитьПараметр("Ссылка", ТаблицаОбъектов);
	
	ДеревоГрупп = ЗапросПоГруппам.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ВыгрузитьГруппыРекурсивно(ОбъектCML, ДеревоГрупп.Строки);
	
КонецПроцедуры

Процедура ВыгрузитьМодельВXML(ОбъектCML, ТекМодель)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Модель");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование",  ТекМодель.НаименованиеПолное);
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид",            Строка(ТекМодель.Ссылка.УникальныйИдентификатор()));
	ЗаписатьТекстовойУзел(ОбъектCML, "ИдГруппы",      Строка(ТекМодель.Родитель.УникальныйИдентификатор()));
	
	Если ЗначениеЗаполнено(ТекМодель.Производитель) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Производитель", Строка(ТекМодель.Производитель.УникальныйИдентификатор()));
	КонецЕсли;
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьЦветВXML(ОбъектCML, ТекЦвет)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Цвет");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", ТекЦвет.Наименование);
	ЗаписатьТекстовойУзел(ОбъектCML, "ЦветПоГИБДД",  ТекЦвет.ЦветПоГИБДД);
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид",           Строка(ТекЦвет.Ссылка.УникальныйИдентификатор()));
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьЦенуВXML(ОбъектCML, ТекЦена)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Цена");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Авторабота", Строка(ТекЦена.Авторабота.УникальныйИдентификатор()));
	Если ЗначениеЗаполнено(ТекЦена.ВидРемонта) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ВидРемонта", Строка(ТекЦена.ВидРемонта.УникальныйИдентификатор()));
	КонецЕсли;
	ЗаписатьТекстовойУзел(ОбъектCML, "Цена",       ТекЦена.Цена);
	ЗаписатьТекстовойУзел(ОбъектCML, "Валюта",       ТекЦена.Валюта);
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьСтрокуПростогоСправочникаВXML(ОбъектCML, ТекОбъект, ИмяЭлемента, Иерархический = Ложь, Коммент = Ложь)
	
	ОбъектCML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
	
	ЗаписатьТекстовойУзел(ОбъектCML,     "Наименование", ТекОбъект.Наименование);
	ЗаписатьТекстовойУзел(ОбъектCML,     "Ид",           Строка(ТекОбъект.Ссылка.УникальныйИдентификатор()));
	Если Иерархический Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ИдГруппы",     Строка(ТекОбъект.Родитель.УникальныйИдентификатор()));
	КонецЕсли;
	
	Если Коммент Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Комментарий",  ТекОбъект.Комментарий);
	КонецЕсли;
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьАвтомобильВXML(ОбъектCML, ТекОбъект)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Автомобиль");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "ДатаИзменения",       XMLСтрока(УзелОбмена.ДатаИзмененияАвтомобили - 1));
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование",        ТекОбъект.Наименование);
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид",                  Строка(ТекОбъект.Ссылка.УникальныйИдентификатор()));
	Если ЗначениеЗаполнено(ТекОбъект.ПроизводительМодели) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ПроизводительМодели", Строка(ТекОбъект.ПроизводительМодели.УникальныйИдентификатор()));
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекОбъект.Родитель) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ИдГруппы",        Строка(ТекОбъект.Родитель.УникальныйИдентификатор()));
	КонецЕсли;
	ЗаписатьТекстовойУзел(ОбъектCML, "Комментарий",         ТекОбъект.Комментарий);
	Если ЗначениеЗаполнено(ТекОбъект.Модель) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Модель",          Строка(ТекОбъект.Модель.УникальныйИдентификатор()));
	КонецЕсли;
	ЗаписатьТекстовойУзел(ОбъектCML, "VIN",                 ТекОбъект.VIN);
	Если ЗначениеЗаполнено(ТекОбъект.ГодВыпуска) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ГодВыпуска",      ТекОбъект.ГодВыпуска);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекОбъект.Цвет) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Цвет",            Строка(ТекОбъект.Цвет.УникальныйИдентификатор()));
	КонецЕсли;
	ЗаписатьТекстовойУзел(ОбъектCML, "ГосНомер",            ТекОбъект.ГосНомер);
	Если ЗначениеЗаполнено(ТекОбъект.Владелец) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Владелец",        Строка(ТекОбъект.Владелец.УникальныйИдентификатор()));
	КонецЕсли;
	ЗаписатьТекстовойУзел(ОбъектCML, "ИдССайта",            ТекОбъект.ИдССайта);
	Если ЗначениеЗаполнено(ТекОбъект.Пробег) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Пробег",              ТекОбъект.Пробег);
	КонецЕсли;
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьКонтрагентаВXML(ОбъектCML, ТекОбъект)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Контрагент");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "ДатаИзменения",      XMLСтрока(УзелОбмена.ДатаИзмененияКонтрагенты - 1));
	ЗаписатьТекстовойУзел(ОбъектCML, "ЯвляетсяЮридическимЛицом", ТекОбъект.ЯвляетсяЮридическимЛицом);
	
	Если ТекОбъект.ЯвляетсяЮридическимЛицом = 0 Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Фамилия",        ТекОбъект.Фамилия);
		ЗаписатьТекстовойУзел(ОбъектCML, "Имя",            ТекОбъект.Имя);
		ЗаписатьТекстовойУзел(ОбъектCML, "Отчество",       ТекОбъект.Отчество);
	Иначе
		ЗаписатьТекстовойУзел(ОбъектCML, "Наименование",        ТекОбъект.Наименование);
		ЗаписатьТекстовойУзел(ОбъектCML, "НаименованиеПолное",  ТекОбъект.НаименованиеПолное);
	КонецЕсли;
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид",                  Строка(ТекОбъект.Ссылка.УникальныйИдентификатор()));
	ЗаписатьТекстовойУзел(ОбъектCML, "ИдССайта",            ТекОбъект.ИдССайта);
	ЗаписатьТекстовойУзел(ОбъектCML, "Телефон",             ТекОбъект.Телефон);
	ЗаписатьТекстовойУзел(ОбъектCML, "email",               ТекОбъект.email);
	ЗаписатьТекстовойУзел(ОбъектCML, "Комментарий",         ТекОбъект.Комментарий);
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьТОВXML(ОбъектCML, ТекОбъект)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("ПлановоеТО");
	
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Автомобиль",        Строка(ТекОбъект.Автомобиль.УникальныйИдентификатор()));
	ЗаписатьТекстовойУзел(ОбъектCML, "ДатаСледующегоТО",  ТекОбъект.ДатаСледующегоТО);
	ЗаписатьТекстовойУзел(ОбъектCML, "ВидРемонта",        Строка(ТекОбъект.ВидРемонта.УникальныйИдентификатор()));
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьИнформациюОБанке(ОбъектXML, РольБанка, Банк)
	
	Если ( обЗначениеНеЗаполнено(Банк)) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента(РольБанка);
	
	ЗаписатьТекстовойУзел(ОбъектXML, "СчетКорреспондентский", Банк.КоррСчет);
	ЗаписатьТекстовойУзел(ОбъектXML, "Наименование", Банк.Наименование);
	
	Если (Не ПустаяСтрока(Банк.Адрес)) Тогда
		
		ОбъектXML.ЗаписатьНачалоЭлемента("Адрес");
		ЗаписатьТекстовойУзел(ОбъектXML, "Представление", Банк.Адрес);
		ОбъектXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
	ЗаписатьТекстовойУзел(ОбъектXML, "БИК", Банк.Код);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьБанковскийСчетОрганизации(ОбъектXML, БанковскийСчет)
	
	Если ( обЗначениеНеЗаполнено(БанковскийСчет)
		ИЛИ ТипЗнч(БанковскийСчет) <> Тип("СправочникСсылка.БанковскиеСчета")) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента("РасчетныеСчета");
	
	// информация о счете
	ОбъектXML.ЗаписатьНачалоЭлемента("РасчетныйСчет");
	
	ЗаписатьТекстовойУзел(ОбъектXML, "НомерСчета", БанковскийСчет.НомерСчета);
	
	ВыгрузитьИнформациюОБанке(ОбъектXML, "Банк", БанковскийСчет.Банк);
	ВыгрузитьИнформациюОБанке(ОбъектXML, "БанкКорреспондент", БанковскийСчет.БанкДляРасчетов);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
	ОбъектXML.ЗаписатьКонецЭлемента();
		
Конецпроцедуры

Функция ВыгрузитьКартинку(Номенклатура, КаталогНаДиске);
	
	СтруктураРезультата = Новый Структура();
	СтруктураРезультата.Вставить("Адрес",  "");
	СтруктураРезультата.Вставить("Формат", Строка(ФорматКартинки.НеизвестныйФормат));
	СтруктураРезультата.Вставить("Размер", "0");
	
	// Получим картинку
	НаборКартинок = РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей();
	НаборКартинок.Отбор.Объект.Установить(Номенклатура);
	НаборКартинок.Прочитать();
	Если ( НаборКартинок.Количество() = 0 ) Тогда
		Возврат СтруктураРезультата;
	Иначе
		Картинка = НаборКартинок[0].Данные.Получить();
	КонецЕсли;	
	
	Если ( ТипЗнч(Картинка) <> Тип("Картинка") ) Тогда
		Возврат СтруктураРезультата;
	КонецЕсли;
	
	Попытка
		
		ФорматКартинкиОбъекта = Картинка.Формат();
		Если ( ФорматКартинкиОбъекта = ФорматКартинки.НеизвестныйФормат ) Тогда
			СообщитьПользователю("У товара обнаружено основное изображение неизвестного формата: " + Номенклатура, Истина, СтатусСообщения.Информация, 1);
			Возврат СтруктураРезультата;	
		КонецЕсли;
			
		ФорматКартинкиРазрешен = Истина;
		Если ( НЕ (ФорматКартинкиОбъекта = ФорматКартинки.GIF
		 	ИЛИ ФорматКартинкиОбъекта = ФорматКартинки.JPEG
		 	ИЛИ ФорматКартинкиОбъекта = ФорматКартинки.PNG) ) Тогда
			 
			Попытка
				ФорматКартинкиОбъекта = Картинка.Преобразовать(ФорматКартинки.JPEG);		 
			Исключение
				СообщитьОбИсключительнойОшибке(Истина, "Не удалось преобразовать картинку для " + Номенклатура + " из " + Строка(ФорматКартинкиОбъекта) + " в JPEG");
				ФорматКартинкиРазрешен = Ложь;
			КонецПопытки;
			
		КонецЕсли;	 
		
		Если ( НЕ ФорматКартинкиРазрешен ) Тогда Возврат СтруктураРезультата; КонецЕсли;
		
	Исключение
		
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось выгрузить картинку для товара: " + Номенклатура + " " + ОписаниеОшибки());
		ФорматКартинкиРазрешен = Ложь;
		
	КонецПопытки;
		
	РасширениеФайлаКартинки    = "." + НРег(Строка(ФорматКартинкиОбъекта));
	ИмяФайлаКартинки           = Строка(Номенклатура.УникальныйИдентификатор()) + РасширениеФайлаКартинки;
	КаталогПоИмени			   = Лев(ИмяФайлаКартинки, 2);
	КаталогКартинки			   = КаталогНаДиске + "\" + ПодкаталогКартинок + "\" + КаталогПоИмени;
	СоздатьКаталог(КаталогКартинки);
	ПолноеИмяФайлаКартинки     = КаталогКартинки + "\" + ИмяФайлаКартинки;
	ФайлКартинкиНаДиске 	   = Новый Файл(ПолноеИмяФайлаКартинки);
	
	Картинка.Записать(ПолноеИмяФайлаКартинки);		
	
	СтруктураРезультата.Адрес  = ПодкаталогКартинок + "/" + КаталогПоИмени + "/" + ИмяФайлаКартинки;
	СтруктураРезультата.Формат = Строка(ФорматКартинкиОбъекта);
	СтруктураРезультата.Размер = Формат(ФайлКартинкиНаДиске.Размер(), "ЧГ=");
	
	Возврат СтруктураРезультата;
	
КонецФункции

Процедура ДобавитьНовоеЗначениеРеквизита(ОбъектXML, Наименование, Значение, ПустоеЗначение = "", ИмяГруппыЭлементов = "ЗначениеРеквизита")
	
	ЗначениеДляЗаписи = Значение;
	Если ( ЗначениеДляЗаписи = Null ) Тогда
		ЗначениеДляЗаписи = ПустоеЗначение;
	КонецЕсли;
	
	Если ( ЗначениеДляЗаписи = "" ) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента(ИмяГруппыЭлементов);
	
	ЗаписатьТекстовойУзел(ОбъектXML, "Наименование",  Наименование, Ложь);
	ЗаписатьТекстовойУзел(ОбъектXML, "Значение",  ЗначениеДляЗаписи, Ложь);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Функция СформироватьCMLСРанееЗагруженнымиЗаказНарядами(СтрокаCML, МассивИзменений)
	
	ТаблицаЗаказНарядов = ПолучитьЗаказНаряды(МассивИзменений, мМассивЗагруженныхОбъектов);
	
	Если ( ТаблицаЗаказНарядов.Количество() = 0 ) Тогда  
		Возврат ТаблицаЗаказНарядов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
	
	ОбъектCML.ЗаписатьНачалоЭлемента("ЗаказНаряды");
		
	Для Каждого СтрокаТД Из ТаблицаЗаказНарядов Цикл
		
		Если ПустаяСтрока(СтрокаТД.emailКонтрагента) Тогда
			Сообщить("Документ №" + СтрокаТД.Номер + " не выгружен: адрес электронной почты контрагента не указан", СтатусСообщения.Внимание);
			Продолжить;
		КонецЕсли;
		
		ОбъектCML.ЗаписатьНачалоЭлемента("ЗаказНаряд");
		
		ВыгрузитьДокументВCML(ОбъектCML, СтрокаТД);
					
		ОбъектCML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	ОбъектCML.ЗаписатьКонецЭлемента(); // корневой элемент
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаЗаказНарядов;
	 
КонецФункции

Функция СформироватьCMLСЗаявками(СтрокаCML, МассивИзменений)
	
	ТаблицаОбъектов = ПолучитьЗаявки(МассивИзменений, мМассивЗагруженныхОбъектов);
	
	Если ( ТаблицаОбъектов.Количество() = 0 ) Тогда  
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
	
	ОбъектCML.ЗаписатьНачалоЭлемента("ЗаявкиНаРемонт");
	
	Для Каждого СтрокаТД Из ТаблицаОбъектов Цикл
		
		Если ПустаяСтрока(СтрокаТД.emailКонтрагента) Тогда
			Сообщить("Документ №" + СтрокаТД.Номер + " не выгружен: адрес электронной почты контрагента не указан", СтатусСообщения.Внимание);
			Продолжить;
		КонецЕсли;
		
		ОбъектCML.ЗаписатьНачалоЭлемента("ЗаявкаНаРемонт");
		
		ВыгрузитьДокументВCML(ОбъектCML, СтрокаТД);
					
		ОбъектCML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента(); // ЗаявкиНаРемонт
	ОбъектCML.ЗаписатьКонецЭлемента(); // корневой элемент
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаОбъектов;
	 
КонецФункции

Функция СформироватьCMLСМоделями(СтрокаCML, МассивИзменений)
	
	ТаблицаМоделей = ПолучитьМодели(МассивИзменений);
	
	Если ( ТаблицаМоделей.Количество() = 0 ) Тогда
		Возврат ТаблицаМоделей;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
		
	ВыгрузитьГруппыПростогоСправочника(ОбъектCML, ТаблицаМоделей, "Модели");
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Модели");
	
	Для Каждого СтрокаТМ Из ТаблицаМоделей Цикл
		
		ВыгрузитьМодельВXML(ОбъектCML, СтрокаТМ)
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // "Модели"
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент
	
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаМоделей;
	 
КонецФункции

Функция СформироватьCMLИзПростогоСправочника(СтрокаCML, МассивИзменений, ИмяСправочника, ИмяЭлементаXML, Иерархический = Ложь, ЕстьКомментарий = Ложь) 

	ТаблицаОбъектов = ПолучитьИзПростогоСправочника(МассивИзменений, ИмяСправочника, Иерархический, ЕстьКомментарий);
	
	Если (ТаблицаОбъектов.Количество() = 0 ) Тогда  
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
	
	Если Иерархический Тогда
		ВыгрузитьГруппыПростогоСправочника(ОбъектCML, ТаблицаОбъектов, ИмяСправочника);
	КонецЕсли;
	
	ОбъектCML.ЗаписатьНачалоЭлемента(ИмяСправочника);
	

	
	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ВыгрузитьСтрокуПростогоСправочникаВXML(ОбъектCML, СтрокаТаблицы, ИмяЭлементаXML, Иерархический, ЕстьКомментарий);
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // ИмяСправочника
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент

	
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаОбъектов;
	 
КонецФункции

Функция СформироватьCMLСЦветами(СтрокаCML, МассивИзменений) 
	
	ТаблицаОбъектов = ПолучитьЦвета(МассивИзменений);
	
	Если (ТаблицаОбъектов.Количество() = 0 ) Тогда
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
		
	ОбъектCML.ЗаписатьНачалоЭлемента("Цвета");
	
	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ВыгрузитьЦветВXML(ОбъектCML, СтрокаТаблицы);
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // ИмяСправочника
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент

	
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаОбъектов;
	 
КонецФункции

Функция СформироватьCMLСЦенами(СтрокаCML) 
	
	ТаблицаОбъектов = ПолучитьЦены();
	
	Если (ТаблицаОбъектов.Количество() = 0 ) Тогда  
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
		
	ОбъектCML.ЗаписатьНачалоЭлемента("Цены");
	
	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ВыгрузитьЦенуВXML(ОбъектCML, СтрокаТаблицы);
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // ИмяСправочника
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент
	
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаОбъектов;
	 
КонецФункции

Функция СформироватьCMLСПлановымиТО(СтрокаCML, МассивИзменений) 
	
	ТаблицаОбъектов = ПолучитьПлановыеТО(МассивИзменений);
	
	Если (ТаблицаОбъектов.Количество() = 0 ) Тогда  
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
		
	ОбъектCML.ЗаписатьНачалоЭлемента("ПлановыеТО");
	
	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ВыгрузитьТОВXML(ОбъектCML, СтрокаТаблицы);
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // ИмяСправочника
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент

	
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаОбъектов;
	 
КонецФункции

Функция СформироватьCMLСАвтомобилями(СтрокаCML, МассивИзменений)
	
	ТаблицаОбъектов = ПолучитьАвтомобили(МассивИзменений);
	
	Если (ТаблицаОбъектов.Количество() = 0 ) Тогда  
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
		
	ВыгрузитьГруппыПростогоСправочника(ОбъектCML, ТаблицаОбъектов, "Автомобили");
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Автомобили");

	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ВыгрузитьАвтомобильВXML(ОбъектCML, СтрокаТаблицы);
		
	КонецЦикла;
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // ИмяСправочника
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент

	
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаОбъектов;
	
КонецФункции

Функция СформироватьCMLСКонтрагентами(СтрокаCML, МассивИзменений) 
	
	ТаблицаОбъектов = ПолучитьКонтрагентов(МассивИзменений);
	
	Если (ТаблицаОбъектов.Количество() = 0 ) Тогда
		Возврат ТаблицаОбъектов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Контрагенты");
	
	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ВыгрузитьКонтрагентаВXML(ОбъектCML, СтрокаТаблицы);
		
	КонецЦикла;
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // ИмяСправочника
	
	ОбъектCML.ЗаписатьКонецЭлемента();   // корневой элемент
	
	
	СтрокаCML = ОбъектCML.Закрыть();
	
	Возврат ТаблицаОбъектов;
	
КонецФункции

Функция ПроверкаДублейEMail()
	
	ОтсутствиеДублей = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Объект КАК Объект,
		|	КонтактнаяИнформация.Представление,
		|	1 КАК Количество
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКОнтактнойИнформации.АдресЭлектроннойПочты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Объект";
		
	Результат = Запрос.Выполнить().Выгрузить();
	ЭлАдреса = Результат.Скопировать();
	
	ЭлАдреса.Свернуть("Представление","Количество");
	Для Каждого ЭлАдрес Из ЭлАдреса Цикл
		Если ЭлАдрес.Количество > 1 Тогда
			сп = Результат.НайтиСтроки(новый структура("Представление", ЭлАдрес.Представление));
			Для Каждого СтрТаб Из сп Цикл
				Сообщить("Ошибка. Адрес электронной почты <"+СтрТаб.Представление+"> не уникален. Контрагент <"+ СтрТаб.Объект +">");
			КонецЦикла;
			ОтсутствиеДублей = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтсутствиеДублей;
	
КонецФункции

Процедура ВыгрузитьТоварыУслугиЗаказаВCML(ОбъектCML, Док)
	
	Если Док.Товары.Количество()=0 Тогда
		возврат;
	КонецЕсли;
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Товары");
	
	Для Каждого СтрокаТЧ Из Док.Товары Цикл
		ВыгрузитьТоварДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ);
	КонецЦикла; 	
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьАвтоработыЗаказаВCML(ОбъектCML, Док)
	
	Если Док.Работы.Количество()=0 Тогда 
		Возврат;
	КонецЕсли;
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Автоработы");
	
	Для Каждого СтрокаТЧ Из Док.Работы Цикл
		ВыгрузитьАвтоработуДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ);
	КонецЦикла; 	
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьТоварДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ, ТЧУслуги = Ложь)

	ОбъектCML.ЗаписатьНачалоЭлемента("Товар");
	
	ТЧУслуги	= НЕ ( СтрокаТЧ.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 
		ИЛИ СтрокаТЧ.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2);
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Артикул"		, СтрокаТЧ.Номенклатура.Артикул);
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование"	, ФорматНаименованияДляCML(СтрокаТЧ.Номенклатура.Наименование));
	ЗаписатьТекстовойУзел(ОбъектCML, "Производитель", СтрокаТЧ.Номенклатура.Производитель.Наименование);
	ЗаписатьТекстовойУзел(ОбъектCML, "Цена", СтрокаТЧ.Цена);
	ЗаписатьТекстовойУзел(ОбъектCML, "Количество", СтрокаТЧ.Количество);
	ЗаписатьТекстовойУзел(ОбъектCML, "Сумма", СтрокаТЧ.СуммаВсего);
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	Возврат;
	
	Если ( НЕ ТЧУслуги И СтрокаТЧ.ХарактеристикаНоменклатуры <> ПустаяХарактеристикаСсылка ) Тогда
			
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
					   |	ЗначенияСвойствОбъектов.Свойство,
					   |	ЗначенияСвойствОбъектов.Значение
					   |ИЗ
					   |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
					   |ГДЕ
					   |	ЗначенияСвойствОбъектов.Объект = &Объект";
					   
		Запрос.УстановитьПараметр("Объект", СтрокаТЧ.ХарактеристикаНоменклатуры); 			   
		РезультатЗапроса = Запрос.Выполнить();
		
		ЗаписанЗаголовокВыгрузкиХарактеристик = Ложь;
		
		Если ( НЕ РезультатЗапроса.Пустой() ) Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока ( Выборка.Следующий() ) Цикл
				
				Если ( обЗначениеНеЗаполнено(Выборка.Значение)
					ИЛИ обЗначениеНеЗаполнено(ФорматНаименованияДляCML(Выборка.Свойство))) Тогда
					
					Продолжить;
					
				КонецЕсли;					
				
				Если НЕ ЗаписанЗаголовокВыгрузкиХарактеристик Тогда
					
					ОбъектCML.ЗаписатьНачалоЭлемента("ХарактеристикиТовара");
					ЗаписанЗаголовокВыгрузкиХарактеристик = Истина;
					
				КонецЕсли;					
				
				ОбъектCML.ЗаписатьНачалоЭлемента("ХарактеристикаТовара");
				ЗаписатьТекстовойУзел(ОбъектCML, "Наименование"	, ФорматНаименованияДляCML(Выборка.Свойство));
				ЗаписатьТекстовойУзел(ОбъектCML, "Значение"	    , Выборка.Значение);
				ОбъектCML.ЗаписатьКонецЭлемента();	
			КонецЦикла;
			
			Если ( ЗаписанЗаголовокВыгрузкиХарактеристик ) Тогда
				ОбъектCML.ЗаписатьКонецЭлемента();
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	ОбъектCML.ЗаписатьНачалоЭлемента("ЗначенияРеквизитов");
	ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "ВидНоменклатуры", СтрокаТЧ.Номенклатура.ТипНоменклатуры.ВидНоменклатуры);
	ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "ТипНоменклатуры", СтрокаТЧ.Номенклатура.ТипНоменклатуры);
	ОбъектCML.ЗаписатьКонецЭлемента();	
	
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Единица", СтрокаТЧ.ЕдиницаИзмерения);
	ЗаписатьТекстовойУзел(ОбъектCML, "Коэффициент", СтрокаТЧ.Коэффициент);
	
	Если ( НЕ обЗначениеНеЗаполнено(СтрокаТЧ.СтавкаНДС) И (СтрокаТЧ.Номенклатура.СтавкаНДС <> Справочники.СтавкиНДС.БезНДС) ) Тогда
		ОбъектCML.ЗаписатьНачалоЭлемента("Налоги");
		ОбъектCML.ЗаписатьНачалоЭлемента("Налог");
		ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", НаименованиеНалога);
		ЗаписатьТекстовойУзел(ОбъектCML, "УчтеноВСумме", ?(СтрокаТЧ.СуммаНДС>0,Истина,Ложь));
		ЗаписатьТекстовойУзел(ОбъектCML, "Сумма"	   , СтрокаТЧ.СуммаНДС);
		ЗаписатьТекстовойУзел(ОбъектCML, "Ставка"      , ПолучитьПоСтавкеНДСЗначениеДляВыгрузки(СтрокаТЧ.СтавкаНДС));
		ОбъектCML.ЗаписатьКонецЭлемента();
		ОбъектCML.ЗаписатьКонецЭлемента();
	КонецЕсли;
		
	Если ( СтрокаТЧ.ПроцентСкидки <> 0 ) Тогда
		ОбъектCML.ЗаписатьНачалоЭлемента("Скидки");
		ОбъектCML.ЗаписатьНачалоЭлемента("Скидка");
		ЗаписатьТекстовойУзел(ОбъектCML, "Процент"     , СтрокаТЧ.ПроцентСкидки);
		ЗаписатьТекстовойУзел(ОбъектCML, "УчтеноВСумме", Истина);
		ОбъектCML.ЗаписатьКонецЭлемента();
		ОбъектCML.ЗаписатьКонецЭлемента();	
	КонецЕсли;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВыгрузитьАвтоработуДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ, ТЧУслуги = Ложь)

	ОбъектCML.ЗаписатьНачалоЭлемента("Работа");
	
	ИдТовара = Строка(СтрокаТЧ.Работа.УникальныйИдентификатор());
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид"			, ИдТовара);
	ЗаписатьТекстовойУзел(ОбъектCML, "Артикул"		, СтрокаТЧ.Работа.Артикул);
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование"	, ФорматНаименованияДляCML(СтрокаТЧ.Работа.НаименованиеПолное));
	ЗаписатьТекстовойУзел(ОбъектCML, "Количество"	, СтрокаТЧ.Количество);
	ЗаписатьТекстовойУзел(ОбъектCML, "Цена"	, СтрокаТЧ.Цена);
	ЗаписатьТекстовойУзел(ОбъектCML, "Сумма"	, СтрокаТЧ.СуммаВсего);
	
	
	ОбъектCML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВыгрузитьДокументВCML(ОбъектCML, Док);
	
	Если ТипЗнч(Док.ДокументСсылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		
		Если ЗначениеЗаполнено(Док.ДокументОснование) Тогда
			ЗаписатьТекстовойУзел(ОбъектCML, "ДокументОснование", Строка(Док.ДокументОснование.УникальныйИдентификатор()));
		КонецЕсли;
		ЗаписатьТекстовойУзел(ОбъектCML, "Рекомендации", Док.Рекомендации);
		ЗаписатьТекстовойУзел(ОбъектCML, "Гарантии", Док.Гарантии);

	ИначеЕсли ТипЗнч(Док.ДокументСсылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		
		ЗаписатьТекстовойУзел(ОбъектCML, "ДатаИзменения", XMLСтрока(УзелОбмена.ДатаИзмененияЗаявки - 1));
		
		Если ЗначениеЗаполнено(Док.Телефон) Тогда
			ЗаписатьТекстовойУзел(ОбъектCML, "Телефон", Док.Телефон);
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(Док.ИдССайта) Тогда
			ЗаписатьТекстовойУзел(ОбъектCML, "ИдССайта" , Док.ИдССайта);
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	// Общие реквизиты
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид"	, Строка(Док.ДокументСсылка.УникальныйИдентификатор()));
	ЗаписатьТекстовойУзел(ОбъектCML, "Дата" , Док.Дата);
	ЗаписатьТекстовойУзел(ОбъектCML, "НомерДокумента", Док.Номер);
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Валюта" , ФорматВалютыДляCML(Док.ВалютаДокумента));
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Сумма"      , Док.СуммаДокумента);
	ЗаписатьТекстовойУзел(ОбъектCML, "СуммаРабот" , Док.СуммаРаботДокумента);
	
	ЗаписатьТекстовойУзел(ОбъектCML, "ПричинаОбращения", ФорматКомментарияДляCML(Док.ПричинаОбращения));
	
	Если ЗначениеЗаполнено(Док.Заказчик) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Заказчик", Строка(Док.Заказчик.УникальныйИдентификатор()));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Док.Автомобиль) Тогда
		Если ТипЗнч(Док.Автомобиль) = Тип("Строка") ИЛИ ТипЗнч(Док.Автомобиль) = Тип("СправочникСсылка.Модели") Тогда
			ЗаписатьТекстовойУзел(ОбъектCML, "АвтомобильСтрокой", Док.Автомобиль);
		Иначе 
			ЗаписатьТекстовойУзел(ОбъектCML, "Автомобиль", Строка(Док.Автомобиль.УникальныйИдентификатор()));
			
			Пробег = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Док.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег, Док.ДатаНачала);
			
			Если ЗначениеЗаполнено(Пробег) Тогда
				ЗаписатьТекстовойУзел(ОбъектCML, "Пробег", Строка(Пробег));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Док.Мастер) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "Мастер", Строка(Док.Мастер.Наименование));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Док.ДатаНачала) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ДатаНачала", Строка(Док.ДатаНачала));
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(Док.ДатаОкончания) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ДатаОкончания", Строка(Док.ДатаОкончания));
	КонецЕсли;
	
	ЗаписатьТекстовойУзел(ОбъектCML, "ПричинаОбращения", Док.ПричинаОбращения);
	
	Если ЗначениеЗаполнено(Док.ВидРемонта) Тогда
		ЗаписатьТекстовойУзел(ОбъектCML, "ВидРемонта", Строка(Док.ВидРемонта.УникальныйИдентификатор()));
	КонецЕсли;
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Состояние", Строка(Док.Состояние));
	
	ВыгрузитьТоварыУслугиЗаказаВCML(ОбъектCML, Док.ДокументСсылка);
	ВыгрузитьАвтоРаботыЗаказаВCML(ОбъектCML, Док.ДокументСсылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ УПРАВЛЕНИЯ ВЫГРУЗКОЙ ДАННЫХ

// Процедура осуществляет выгрузку данных в соответствии с переданной в качестве параметра настройки.
Процедура ПроизвестиВыгрузкуДанных() Экспорт
	
	НастройкаОбъект	= УзелОбмена.ПолучитьОбъект();
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект, НастройкаОбъект.СерверHTTP);
	
	Соединение = HTTPУстановитьСоединение(СтруктураПараметровСайта);
	
	Если ( Соединение = Неопределено ) Тогда
		Сообщить("Ошибка при установке соединения с сайтом.");
		Возврат;
	КонецЕсли;
	
	ОтветСервера		= ""; ТипСоединения = "query"; СтрокаСообщенияПользователю = "";
	РезультатСоединения	= HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	Если НЕ РезультатСоединения Тогда
		Сообщить("Ошибка при установке соединения с сайтом.");
		Возврат;
	КонецЕсли;
	
	ТаблицаФайловОбмена.Очистить();
	
	мСтруктураИнформацииИсторииОбмена	= Новый Структура("ДатаПоследнейЗагрузки,ДатаПоследнейВыгрузки,ДатаНачалаПоследнейЗагрузки,ДатаНачалаПоследнейВыгрузки,РезультатПоследнейЗагрузки,РезультатПоследнейВыгрузки,КомментарийКЗагрузкеДанных,КомментарийКВыгрузкеДанных");
	мСтруктураИнформацииИсторииОбмена.КомментарийКЗагрузкеДанных = "";
	мСтруктураИнформацииИсторииОбмена.КомментарийКВыгрузкеДанных = "";
	
	мСтруктураИнформацииИсторииОбмена.ДатаНачалаПоследнейВыгрузки = ТекущаяДата();
		
	ЗаполнитьСвойстваПоНастройке(УзелОбмена);
			
	Если ( Не ПустаяСтрока(КаталогВыгрузки) ) Тогда
		ФайлЗагрузки = КаталогВыгрузки + "\order.xml";
		ФайлЗагрузки = СтрЗаменить(ФайлЗагрузки,"\\","\");
	КонецЕсли;
	
	СтруктураИзменений = Новый Структура;
	
	Если ( ВыгружатьТолькоИзменения И ВыгружатьДанныеНаСайт) Тогда
		
		СтруктураИзменений.Вставить("ВидыРемонта",   Новый Массив);
		СтруктураИзменений.Вставить("Производители", Новый Массив);
		СтруктураИзменений.Вставить("Цвета",         Новый Массив);
		СтруктураИзменений.Вставить("Автоработы",    Новый Массив);
		СтруктураИзменений.Вставить("Модели",        Новый Массив);
		СтруктураИзменений.Вставить("Автомобили",    Новый Массив);
		СтруктураИзменений.Вставить("ЗаказНаряды",   Новый Массив);
		СтруктураИзменений.Вставить("Заявки",        Новый Массив);
		СтруктураИзменений.Вставить("Контрагенты",   Новый Массив);
		
		ЗаполнитьСтруктуруИзмененийДляУзла(УзелОбмена, СтруктураИзменений);
		
	КонецЕсли;
	
	КаталогОбмена = КаталогВременныхФайлов()+Строка(Новый УникальныйИдентификатор);
	
	Если (ВыгружатьДанныеНаСайт И ОбменМоделями) Тогда // только на сайт
		МоделиУспешноВыгружены = ВыполнитьОбменМоделями(СтруктураИзменений, КаталогОбмена);
		// производитель, вид ремонта, автоработы, модели, цвета
		
		Если ( ВыгружатьТолькоИзменения И МоделиУспешноВыгружены) Тогда
			УдалитьВыгруженныеОбъекты(УзелОбмена, СтруктураИзменений.Модели);
		КонецЕсли;
	Иначе
		МоделиУспешноВыгружены = Истина;
	КонецЕсли;

	Если (ОбменЦенами и ВыгружатьДанныеНаСайт) Тогда //только на сайт
		ЦеныУспешноВыгружены = ВыполнитьОбменЦенами(КаталогОбмена);
	Иначе
		ЦеныУспешноВыгружены = Истина;
	КонецЕсли;
	
	Если (ОбменТО и ВыгружатьДанныеНаСайт) Тогда //только на сайт
		ОбменТОУспешноВыгружены = ВыполнитьОбменПлановымиТО(СтруктураИзменений, КаталогОбмена);
	Иначе
		ОбменТОУспешноВыгружены = Истина;
	КонецЕсли;
	
	Если (ОбменКонтрагентами) Тогда  // двусторонний обмен
		КонтрагентыУспешноВыгружены = ВыполнитьОбменКонтрагентами(СтруктураИзменений, КаталогОбмена);
		
		Если (ВыгружатьТолькоИзменения И КонтрагентыУспешноВыгружены И ВыгружатьДанныеНаСайт) Тогда
			УдалитьВыгруженныеОбъекты(УзелОбмена, СтруктураИзменений.Контрагенты);
		КонецЕсли;
	Иначе
		КонтрагентыУспешноВыгружены = Истина;
	КонецЕсли;
	
	Если (ОбменАвтомобилями) Тогда  // двусторонний обмен
		АвтомобилиУспешноВыгружены = ВыполнитьОбменАвтомобилями(СтруктураИзменений, КаталогОбмена);
		
		Если (ВыгружатьТолькоИзменения И АвтомобилиУспешноВыгружены И ВыгружатьДанныеНаСайт) Тогда
			УдалитьВыгруженныеОбъекты(УзелОбмена, СтруктураИзменений.Автомобили);
		КонецЕсли;
	Иначе
		АвтомобилиУспешноВыгружены = Истина;
	КонецЕсли;
	
	
	Если (ОбменЗаявками) Тогда  // двусторонний обмен
		ЗаявкиУспешноВыгружены = ВыполнитьОбменЗаявками(СтруктураИзменений, КаталогОбмена);
		
		Если ( ВыгружатьТолькоИзменения И ЗаявкиУспешноВыгружены И ВыгружатьДанныеНаСайт) Тогда
			УдалитьВыгруженныеОбъекты(УзелОбмена, СтруктураИзменений.Заявки);
		КонецЕсли;
	Иначе
		ЗаявкиУспешноВыгружены = Истина;
	КонецЕсли;
	
	Если (ОбменЗаказНарядами и ВыгружатьДанныеНаСайт) Тогда //только на сайт
		ЗаказНарядыУспешноВыгружены = ВыполнитьОбменЗаказНарядами(СтруктураИзменений, КаталогОбмена);
		Если ( ВыгружатьТолькоИзменения И ЗаказНарядыУспешноВыгружены) Тогда
			УдалитьВыгруженныеОбъекты(УзелОбмена, СтруктураИзменений.ЗаказНаряды);
		КонецЕсли;
	Иначе
		ЗаказНарядыУспешноВыгружены = Истина;
	КонецЕсли;
	
	мСтруктураИнформацииИсторииОбмена.ДатаПоследнейВыгрузки			= ТекущаяДата();
	мСтруктураИнформацииИсторииОбмена.РезультатПоследнейВыгрузки	=  МоделиУспешноВыгружены И КонтрагентыУспешноВыгружены И 
	                  АвтомобилиУспешноВыгружены  и ЗаявкиУспешноВыгружены И 
	                  ЗаказНарядыУспешноВыгружены и ЦеныУспешноВыгружены и ОбменТОУспешноВыгружены;

	ЗаписатьИнформациюВПротоколОбмена(мСтруктураИнформацииИсторииОбмена);
		
КонецПроцедуры


Процедура УдалитьВыгруженныеОбъекты(УзелОбмена, МассивСсылок)
	
	Если МассивСсылок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(МассивСсылок) = Тип("Массив") Тогда
		Для Каждого ТекСсылка Из МассивСсылок Цикл
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ТекСсылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


// Процедура осуществляет заполнение переменных обработки значениями
// из настройки связи с сайтом.
Процедура ЗаполнитьСвойстваПоНастройке(УзелОбмена) Экспорт
	
	НачалоАдресаСкрипта					= УзелОбмена.СерверHTTP;
	СтруктураАдреса						= РазобратьАдресСайта(НачалоАдресаСкрипта);	
	HTTPОбменПорт						= УзелОбмена.ПортHTTP;
	HTTPОбменСервер						= СтруктураАдреса.HTTPСервер;
	НачалоАдресаСкрипта					= СтруктураАдреса.HTTPАдресСкрипта;
	
	//ВыгружатьТолькоИзменения			= УзелОбмена.ВыгружатьТолькоИзменения;
	ВыгружатьНаСайт						= Истина; 
	
	Если ( ПустаяСтрока(УзелОбмена.КаталогОбменаHTTP) ) Тогда
		КаталогВыгрузки	= КаталогВременныхФайлов();
	Иначе
		ПоследнийСимвол = Сред(УзелОбмена.КаталогОбменаHTTP, СтрДлина(УзелОбмена.КаталогОбменаHTTP), 1);
		Если ( ПоследнийСимвол <> "\" ) Тогда
			КаталогВыгрузки = УзелОбмена.КаталогОбменаHTTP + "\";			
		Иначе
			КаталогВыгрузки	= УзелОбмена.КаталогОбменаHTTP;
		КонецЕсли;
		ФайлЗагрузки = КаталогВыгрузки + "order.xml";
	КонецЕсли;	
	
	мПрефикс							= СокрЛП(УзелОбмена.Код);				
	HTTPОбменИмяПользователя			= УзелОбмена.ПользовательHTTP;
	HTTPОбменПароль						= УзелОбмена.ПарольHTTP;
	HTTPОбменАдресСкрипта				= НачалоАдресаСкрипта;
	HTTPОбменПроксиИспользование		= ?(обЗначениеНеЗаполнено(УзелОбмена.ПользовательПроксиHTTP),Ложь,Истина);
	HTTPОбменПроксиИмяПользователя		= УзелОбмена.ПользовательПроксиHTTP;
	HTTPОбменПроксиСервер				= УзелОбмена.СерверПроксиHTTP;
	HTTPОбменПроксиПорт					= УзелОбмена.ПортПроксиHTTP;
	ГруппаДляНовыхКонтрагентов			= УзелОбмена.ГруппаДляНовыхКонтрагентов;
	Организация							= УзелОбмена.Организация;
	Менеджер							= УзелОбмена.Менеджер;
	Подразделение						= УзелОбмена.Подразделение;
	ТипЦены								= УзелОбмена.ТипЦены;
	ЗаписыватьДокументыТекущейДатой		= УзелОбмена.ЗаписыватьДокументыТекущейДатой;
	ПроводитьДокументы					= УзелОбмена.ПроводитьДокументы;
	ПроводитьДокументыОперативно		= УзелОбмена.ПроводитьДокументыОперативно;
	СпособИдентификацииКонтрагентов		= УзелОбмена.СпособИдентификацииКонтрагентов;
	ВыгружатьКартинки					= УзелОбмена.ВыгружатьКартинки;
	//ОбменТоварами						= УзелОбмена.ОбменТоварами;
	ОбменЗаказами						= УзелОбмена.ОбменЗаказами;
	ПодразделениеДляВыборкиТоваров		= ?(обЗначениеНеЗаполнено(УзелОбмена.ПодразделениеДляВыборкиТоваров),Справочники.ПодразделенияКомпании.ОсновноеПодразделение,УзелОбмена.ПодразделениеДляВыборкиТоваров);
	КонтрагентДляВыборкиТоваров			= УзелОбмена.КонтрагентДляВыборкиТоваров;
	СпособОпределенияЦен				= УзелОбмена.ВидЦенКонтрагентаДляВыборкиТоваров;
	Если СпособОпределенияЦен=0 Тогда
		СпособОпределенияЦен	= 1;
	КонецЕсли;
	ВыгружатьТоварыБезЦены				= УзелОбмена.ВыгружатьТоварыБезЦены;
	ПредставленияВалют					= УзелОбмена.ПредставленияВалют.Выгрузить().Скопировать();
	
	//chabav
	//ОбменМоделями						= УзелОбмена.ОбменМоделями;        
	//ОбменАвтомобилями					= УзелОбмена.ОбменАвтомобилями;        
	//ОбменЗаказНарядами					= УзелОбмена.ОбменЗаказНарядами;        
	//ОбменЗаявками						= УзелОбмена.ОбменЗаявками;
	

КонецПроцедуры

Функция ПолучитьОбъектДляЗаписиXML(ИмяФайла, ДатаФормирования)
	
	ЗаписьXML = Новый ЗаписьXML();
	
	Если ( ПустаяСтрока(ИмяФайла) ) Тогда
		ЗаписьXML.УстановитьСтроку("UTF-8");
	Иначе
		ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8"); 
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("КоммерческаяИнформация");
	ЗаписьXML.ЗаписатьАтрибут("ВерсияСхемы", "2.04");
	ЗаписьXML.ЗаписатьАтрибут("ДатаФормирования", ФорматДатыДляCML(ДатаФормирования, Истина, Истина));
	
	Возврат ЗаписьXML;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОПИСАНИЯ И ВЫВОДА ОШИБОК И СООБЩЕНИЙ

Процедура ЗаписатьИнформациюВПротоколОбмена(СтруктураИнформации)
	
	ДатаПоследнегоОбмена	= ТекущаяДата();					
	Если (НЕ обЗначениеНеЗаполнено(СтруктураИнформации.ДатаПоследнейЗагрузки)) Тогда	
		ДатаПоследнейЗагрузки		=  СтруктураИнформации.ДатаПоследнейЗагрузки;
		РезультатПоследнейЗагрузки	= СтруктураИнформации.РезультатПоследнейЗагрузки; 	
	КонецЕсли;
	
	Если ( НЕ обЗначениеНеЗаполнено(СтруктураИнформации.ДатаПоследнейВыгрузки)) Тогда	
		ДатаПоследнейВыгрузки		= СтруктураИнформации.ДатаПоследнейВыгрузки;
		РезультатПоследнейВыгрузки	= СтруктураИнформации.РезультатПоследнейВыгрузки;				
	КонецЕсли;	
							
КонецПроцедуры

Процедура ОтобразитьСостояние(ТекстСообщения)
	#Если Клиент Тогда
	Состояние(ТекстСообщения);
	#КонецЕсли	
КонецПроцедуры

Процедура СообщитьПользователю(ТекстСообщения, ИнформацияОВыгрузке, Статус = Неопределено, ВыводитьВРежимеОтладки=0)
	
	Если ( ВыводитьВРежимеОтладки > 0 ) Тогда
		// Определим является ли сообщение html документом по наличию ключевых тегов.
		Если ( Булево(Найти(ТекстСообщения,"<table")) Или Булево(Найти(ТекстСообщения,"<html")) Или Булево(Найти(ТекстСообщения,"<body"))  ) Тогда
			ОшибкиHTMLем	= ТекстСообщения;	
		Иначе
			ОшибкиТекстом	= ОшибкиТекстом + ?(ПустаяСтрока(ОшибкиТекстом),"",Символы.ПС) + ТекстСообщения;	
		КонецЕсли;
	КонецЕсли;	
	
	// Если сообщение слишком длинное, то не следует выводить его в окне служебных сообщений,
	// т.к. это приводит к падению производительности обработки.
	Если ( СтрДлина(ТекстСообщения)>255 ) Тогда Возврат; КонецЕсли;
	
	#Если Клиент Тогда
		Если (ВыводитьВРежимеОтладки = 0 Или ВыводитьВРежимеОтладки = 2) Тогда
			Сообщить(ТекстСообщения, ?(ТипЗнч(Статус)=Тип("СтатусСообщения"), Статус, СтатусСообщения.Обычное));	
		КонецЕсли;
	#КонецЕсли
	
	ЗаписьЖурналаРегистрации("ОшибкаПриВыполненииОбмена", УровеньЖурналаРегистрации.Ошибка, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
	
КонецПроцедуры

// Процедура выводит текст ответа сервера, если в нем имеется
// расширенное описание ошибки (строки, начиная со 2-й)
//
// Параметры:
//  ОтветСервера - строка, текст ответа сервера
//
Процедура СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера)
	
	ТекстСообщения = "Ответ сервера:" + Символы.ПС;
	
	Если (СтрЧислоСтрок(ОтветСервера) > 1) Тогда
		Для ТекСтрока = 2 по СтрЧислоСтрок(ОтветСервера) Цикл
			ТекстСообщения = ТекстСообщения + СтрПолучитьСтроку(ОтветСервера, ТекСтрока) + Символы.ПС;
		КонецЦикла;	
	КонецЕсли;
	
	СообщитьПользователю(ТекстСообщения, Истина, СтатусСообщения.Информация,1);	

КонецПроцедуры	

// Процедура выводит текст сообщения об ошибке обмена
// и текст "Обмен не выполнен."
//
// Параметры:
//  ТекстСообщения - строка, текст сообщения
//
Процедура СообщитьОбОшибкеОбмена(ТекстСообщения, ИнформацияОВыгрузке)
	
	ТекстСообщения = ТекстСообщения + Символы.ПС + "Обмен не выполнен";
	СообщитьПользователю(ТекстСообщения, ИнформацияОВыгрузке, СтатусСообщения.Важное,2);	
	
КонецПроцедуры	

// Процедура выводит текст сообщения об исключительной ошибке
//
// Параметры:
//
Процедура СообщитьОбИсключительнойОшибке(ИнформацияОВыгрузке, ТекстНачалаСообщения =  "", ТекстОкончанияСообщения = "")
	
	РасширеннаяИнформацияОбОшибке = ИнформацияОбОшибке();
	ТекстСообщения = "";
	Если (Не обЗначениеНеЗаполнено(ТекстНачалаСообщения)) Тогда
		ТекстСообщения = ТекстНачалаСообщения + Символы.ПС;
	КонецЕсли;	
	ТекстСообщения = ТекстСообщения
				   + "Произошла ошибка: "
				   + РасширеннаяИнформацияОбОшибке.Описание;
	
	Если (НЕ Строка(РасширеннаяИнформацияОбОшибке.Причина) = "ИнформацияОбОшибке") Тогда
		ТекстСообщения = ТекстСообщения
					   + ". По причине: "
					   + РасширеннаяИнформацияОбОшибке.Причина;
	КонецЕсли;	
	
	ТекстСообщения = ТекстСообщения + Символы.ПС + ТекстОкончанияСообщения;
	
	СообщитьПользователю(ТекстСообщения, ИнформацияОВыгрузке, СтатусСообщения.Важное, 1);	
	
КонецПроцедуры	

// Выводит сообщение об ошибке и выставляет параметр Отказ в "Истина". 
// В случае работы на клиенте или на сервере выводит в окно сообщений,
// в случае внешнего соединения вызывает исключение.
//
// Параметры:
//  ТекстСообщения - строка, текст сообщения.
//  Отказ          - булево, признак отказа (необязательный).
//
Процедура СообщитьОбОшибке(ТекстСообщения, Отказ = Ложь, Заголовок = "", Статус = Неопределено) Экспорт

	Если (Статус = Неопределено) Тогда
		Статус = СтатусСообщения.Важное;
	КонецЕсли;
	
	ТекстСообщения = СформироватьТекстСообщения(ТекстСообщения);
	Отказ = Истина;
	
	#Если ВнешнееСоединение Тогда	
		Если (НЕ обЗначениеНеЗаполнено(Заголовок)) Тогда
			ТекстСообщения = Заголовок + Символы.ПС + ТекстСообщения;
			Заголовок = "";
		КонецЕсли;
		
		ВызватьИсключение (ТекстСообщения);	
	#Иначе	
		Если ( НЕ обЗначениеНеЗаполнено(Заголовок)) Тогда
			Сообщить(Заголовок);
			Заголовок = "";
		КонецЕсли;	
		Сообщить(ТекстСообщения, Статус);	
	#КонецЕсли
	
КонецПроцедуры // СообщитьОбОшибке()

// Функция убирает из текста сообщения служебную информацию
//
// Параметры
//  ТекстСообщения, Строка, исходный текст сообщения//
// Возвращаемое значение:
//   Строка
//
Функция СформироватьТекстСообщения(Знач ТекстСообщения) Экспорт

	НачалоСлужебногоСообщения    = Найти(ТекстСообщения, "{");
	ОкончаниеСлужебногоСообщения = Найти(ТекстСообщения, "}:");
	
	Если (ОкончаниеСлужебногоСообщения > 0 
		И НачалоСлужебногоСообщения > 0 
		И НачалоСлужебногоСообщения < ОкончаниеСлужебногоСообщения) Тогда
		
		ТекстСообщения = Лев(ТекстСообщения, (НачалоСлужебногоСообщения - 1)) +
		                 Сред(ТекстСообщения, (ОкончаниеСлужебногоСообщения + 2));
						 
	КонецЕсли;
	
	Возврат СокрЛП(ТекстСообщения);

КонецФункции // ()

Процедура ДобавитьТехническуюИнформацияВСообщение(СокращенноеСообщение, СообщениеСТехИнформацией)
	
	Если (СокращенноеСообщение = СообщениеСТехИнформацией) Тогда
		Возврат;
	КонецЕсли;
	
	СокращенноеСообщение = СокращенноеСообщение + Символы.ПС + Символы.ПС + "Техническая информация:" + Символы.ПС + СообщениеСТехИнформацией;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ ЗАГРУЖЕННЫХ ДАННЫХ

Функция ОбработатьНачалоЭлемента(ОбъектCML, Знач ИмяЭлемента, ДеревоДокументов);
	
	Успешно = Истина;
	
	ТекущаяСтрокаДерева  	  = Неопределено;
	ТекущаяСтрокаТоваровУслуг = Неопределено;
	
	КоличествоДокументов		= ДеревоДокументов.Строки.Количество();
	Если ( КоличествоДокументов > 0 ) Тогда
		ТекущаяСтрокаДерева    = ДеревоДокументов.Строки[КоличествоДокументов - 1];
		КоличествоТоваровУслуг = ТекущаяСтрокаДерева.Строки.Количество();
		
		Если ( КоличествоТоваровУслуг > 0 ) Тогда
        	ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];
		КонецЕсли;		
	КонецЕсли;	
	
	Если (ИмяЭлемента = "Документ") Тогда
		
		НовыйДокумент 				   = Документы.ЗаказПокупателя.СоздатьДокумент();
		ОбработкаЗаполненияНовогоОбъекта(НовыйДокумент);
		НовыйДокумент.УстановитьНовыйНомер();
		
		НовСтрокаДерева 			   = ДеревоДокументов.Строки.Добавить();
		НовСтрокаДерева.ДокументОбъект = НовыйДокумент;
		НовСтрокаДерева.СтавкаНДС 	   = Справочники.СтавкиНДС.БезНДС;
	    НовСтрокаДерева.СтруктураДанныхКонтрагента = Новый Структура();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент" ) Тогда
		
		ДеревоАдресов = Новый ДеревоЗначений();
		ДеревоАдресов.Колонки.Добавить("ВидАдреса");
		ДеревоАдресов.Колонки.Добавить("Представление");
		ДеревоАдресов.Колонки.Добавить("Комментарий");
		ДеревоАдресов.Колонки.Добавить("ПолеТип");
		ДеревоАдресов.Колонки.Добавить("ПолеЗначение");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
		ТаблицаКонтактов = Новый ТаблицаЗначений();
		ТаблицаКонтактов.Колонки.Добавить("КонтактТип");
		ТаблицаКонтактов.Колонки.Добавить("КонтактЗначение");
		ТаблицаКонтактов.Колонки.Добавить("КонтактКомментарий");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
		
		ТаблицаКонтактныхЛиц = Новый ТаблицаЗначений();
		ТаблицаКонтактныхЛиц.Колонки.Добавить("Наименование");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);		
	
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева				= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса	= "АдресРегистрации";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 				= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
		НовСтрокаДерева				= СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);		
	
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева				= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса	= "ЮридическийАдрес";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 				= ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
		НовСтрокаДерева 			= СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);		
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес" ) Тогда	
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева	= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса = "Адрес";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле" ) Тогда		
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 	= ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
		НовСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактов", ТаблицаКонтактов);
		ТаблицаКонтактов.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Представители.Представитель.Контрагент" ) Тогда	
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
		ТаблицаКонтактныхЛиц.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
				
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар" ) Тогда
		
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.БазоваяЕдиница" ) Тогда		
		
		Пока ( ОбъектCML.ПрочитатьАтрибут() ) Цикл
			Если ( ОбъектCML.Имя = "Код" ) Тогда
				КодБазовойЕдиницы = ОбъектCML.Значение;
				ТекущаяСтрокаТоваровУслуг.ТоварУслугаБазоваяЕдиницаКод = КодБазовойЕдиницы;
				Прервать;
			КонецЕсли;	
			
		КонецЦикла
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита" ) Тогда
		
		ТекущаяСтрокаТоваровУслуг.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка" ) Тогда
		
		ТекущаяСтрокаТоваровУслуг.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита" ) Тогда
		
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
		
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
// chabav


	ИначеЕсли ( ИмяЭлемента = "Контрагенты" ) Тогда
		
		//НовСтрокаДерева 			   = ДеревоДокументов.Строки.Добавить();
		//НовСтрокаДерева.ДокументОбъект = НовыйДокумент;
		//НовСтрокаДерева.СтавкаНДС 	   = Справочники.СтавкиНДС.БезНДС;
		//НовСтрокаДерева.СтруктураДанныхКонтрагента = Новый Структура();
	ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент" ) Тогда
		НовСтрокаДерева = ДеревоДокументов.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль" ) Тогда
		НовСтрокаДерева = ДеревоДокументов.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт" ) Тогда
		НовСтрокаДерева = ДеревоДокументов.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Товары.Товар" ) Тогда
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Автоработы.Работа" ) Тогда
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
		
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Функция РазобратьCML(СтрокаCML)
	
	Успешно		= Истина;
	ОбъектCML	= Новый ЧтениеXML();
	
	Попытка
		ОбъектCML.УстановитьСтроку(СтрокаCML);
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь);
		Возврат Неопределено;
	КонецПопытки;
	
	ПоследовательностьЭлементов = "";
	
	ДеревоДокументов = Новый ДеревоЗначений();
	
	ДеревоДокументов.Колонки.Добавить("ДокументОбъект");
	ДеревоДокументов.Колонки.Добавить("НомерВходящий");
	ДеревоДокументов.Колонки.Добавить("ДатаВходящегоДокумента");
	ДеревоДокументов.Колонки.Добавить("РанееЗагруженныйДокументСсылка", Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя"));
	ДеревоДокументов.Колонки.Добавить("ЕстьСсылкиНаРанееЗагруженныйДокумент", Новый ОписаниеТипов("Булево"));
	ДеревоДокументов.Колонки.Добавить("СтруктураДанныхКонтрагента");
	ДеревоДокументов.Колонки.Добавить("СтавкаНДС");
	
	// Подчинение документу
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаТипНоменклатуры");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаНаименование");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаБазоваяЕдиницаКод");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаБазоваяЕдиница");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКоэффициент");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаСумма");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКомментарий");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаЦенаЗаЕдиницу");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКоличество");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаИд");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаХарактеристика");
	ДеревоДокументов.Колонки.Добавить("СвойствоНаименование");
	ДеревоДокументов.Колонки.Добавить("СвойствоЗначение");
	
	// Подчинение товарам/услугам
	ДеревоДокументов.Колонки.Добавить("СуммаСкидки");
	ДеревоДокументов.Колонки.Добавить("СкидкаВСумме");
	ДеревоДокументов.Колонки.Добавить("ЗначениеРеквизитаНаименование");
	ДеревоДокументов.Колонки.Добавить("ЗначениеРеквизитаЗначение");
	
	Пока ( Истина ) Цикл	
		ОчереднойУзелCMLПрочитан = Ложь;	
		Попытка
			ОчереднойУзелCMLПрочитан = ОбъектCML.Прочитать();
		Исключение
			Успешно = Ложь;
			СообщитьОбИсключительнойОшибке(Ложь);
			Прервать;
		КонецПопытки;	
		
		Если ( НЕ ОчереднойУзелCMLПрочитан ) Тогда Прервать; КонецЕсли;	
		
		ТипУзла = ОбъектCML.ТипУзла;
		ИмяУзла = ОбъектCML.Имя;
		
		Если (ТипУзла = ТипУзлаXML.НачалоЭлемента) Тогда		
			ПоследовательностьЭлементов = ДобавитьЭлементКПоследовательности(ПоследовательностьЭлементов, ИмяУзла);
			Успешно = ОбработатьНачалоЭлемента(ОбъектCML, ПоследовательностьЭлементов, ДеревоДокументов);
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось обработать начало элемента (" + ПоследовательностьЭлементов + ").", Ложь,,1);
				Прервать;
			КонецЕсли;			
		ИначеЕсли ( ТипУзла = ТипУзлаXML.КонецЭлемента ) Тогда
			ПоследовательностьЭлементов = УдалитьПоследнийЭлементИзПоследовательности(ПоследовательностьЭлементов);		
		ИначеЕсли ( ТипУзла = ТипУзлаXML.Текст ) Тогда			
			ЗначениеЭлемента = ОбъектCML.Значение;  
			Успешно = ОбработатьЗначениеЭлемента(ПоследовательностьЭлементов, ЗначениеЭлемента, ДеревоДокументов);
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось обработать значение элемента (" + ПоследовательностьЭлементов + ") = (" + ЗначениеЭлемента + ").", Ложь,,1);
				Прервать;
			КонецЕсли;			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбъектCML.Закрыть();
	
	Если ( НЕ Успешно ) Тогда ДеревоДокументов = Неопределено; КонецЕсли;	
		
	Возврат ДеревоДокументов;
	
КонецФункции

Процедура ДобавитьКолонкиПоТипуСоединения(ДеревоДокументов, ТипСоединения)
	ДеревоДокументов.Колонки.Добавить("ДатаИзменения", Новый ОписаниеТипов("Дата"));
	Если      ТипСоединения = "users" Тогда
		ДеревоДокументов.Колонки.Добавить("ОбъектСсылка");
		ДеревоДокументов.Колонки.Добавить("Наименование");
		ДеревоДокументов.Колонки.Добавить("НаименованиеПолное");
		ДеревоДокументов.Колонки.Добавить("ИД");
		ДеревоДокументов.Колонки.Добавить("ИдССайта");
		ДеревоДокументов.Колонки.Добавить("Телефон");
		ДеревоДокументов.Колонки.Добавить("email");
		ДеревоДокументов.Колонки.Добавить("Комментарий");
		ДеревоДокументов.Колонки.Добавить("Фамилия");
		ДеревоДокументов.Колонки.Добавить("Имя");
		ДеревоДокументов.Колонки.Добавить("Отчество");
		ДеревоДокументов.Колонки.Добавить("ЮрЛицо");
		
	ИначеЕсли ТипСоединения = "car" Тогда
		ДеревоДокументов.Колонки.Добавить("ОбъектСсылка");
		ДеревоДокументов.Колонки.Добавить("Наименование");
		ДеревоДокументов.Колонки.Добавить("ИД");
		ДеревоДокументов.Колонки.Добавить("ИдССайта");
		ДеревоДокументов.Колонки.Добавить("ИдГруппы");
		ДеревоДокументов.Колонки.Добавить("Производитель");
		ДеревоДокументов.Колонки.Добавить("Модель");
		ДеревоДокументов.Колонки.Добавить("МодельСтрокой");
		ДеревоДокументов.Колонки.Добавить("VIN");
		ДеревоДокументов.Колонки.Добавить("ГодВыпуска");
		ДеревоДокументов.Колонки.Добавить("Цвет");
		ДеревоДокументов.Колонки.Добавить("ГосНомер");
		ДеревоДокументов.Колонки.Добавить("Хозяин");
		ДеревоДокументов.Колонки.Добавить("ХозяинИд");
		ДеревоДокументов.Колонки.Добавить("ХозяинИдССайта");
		ДеревоДокументов.Колонки.Добавить("Комментарий");
		ДеревоДокументов.Колонки.Добавить("Пробег");
	ИначеЕсли ТипСоединения = "request" Тогда
		
		ДеревоДокументов.Колонки.Добавить("ОбъектСсылка");
		ДеревоДокументов.Колонки.Добавить("Ид");
		ДеревоДокументов.Колонки.Добавить("ИдССайта");
		ДеревоДокументов.Колонки.Добавить("НомерЗаявкиСайт");
		ДеревоДокументов.Колонки.Добавить("Заказчик");
		ДеревоДокументов.Колонки.Добавить("ЗаказчикИд");
		ДеревоДокументов.Колонки.Добавить("ЗаказчикИдССайта");
		ДеревоДокументов.Колонки.Добавить("ЗаказчикСтрокой");
		ДеревоДокументов.Колонки.Добавить("ВидРемонта");
		ДеревоДокументов.Колонки.Добавить("Автомобиль");
		ДеревоДокументов.Колонки.Добавить("АвтомобильИд");
		ДеревоДокументов.Колонки.Добавить("АвтомобильИдССайта");
		ДеревоДокументов.Колонки.Добавить("АвтомобильСтрокой");
		ДеревоДокументов.Колонки.Добавить("Дата");
		ДеревоДокументов.Колонки.Добавить("ДатаНачала");
		ДеревоДокументов.Колонки.Добавить("ПричинаОбращения");
		ДеревоДокументов.Колонки.Добавить("Телефон");
		ДеревоДокументов.Колонки.Добавить("Комментарий");
		
		ДеревоДокументов.Колонки.Добавить("ТоварИд");
		ДеревоДокументов.Колонки.Добавить("ТоварКоличество");
		ДеревоДокументов.Колонки.Добавить("РаботаИд");
		ДеревоДокументов.Колонки.Добавить("РаботаКоличество");
		
		ДеревоДокументов.Колонки.Добавить("Автоработы");
		ДеревоДокументов.Колонки.Добавить("Товары");

	КонецЕсли;
	
КонецПроцедуры

Функция РазобратьXML(СтрокаCML, ТипСоединения)
	
	Успешно = Истина;
	ОбъектCML	= Новый ЧтениеXML();
	
	Попытка
		ОбъектCML.УстановитьСтроку(СтрокаCML);
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь);
		Возврат Неопределено;
	КонецПопытки;
	
	ПоследовательностьЭлементов = "";
	
	ДеревоОбъектов = Новый ДеревоЗначений();
	
	
	ДобавитьКолонкиПоТипуСоединения(ДеревоОбъектов, ТипСоединения);
	
	Пока ( Истина ) Цикл	
		ОчереднойУзелCMLПрочитан = Ложь;	
		Попытка
			ОчереднойУзелCMLПрочитан = ОбъектCML.Прочитать();
		Исключение
			Успешно = Ложь;
			СообщитьОбИсключительнойОшибке(Ложь);
			Прервать;
		КонецПопытки;	
		
		Если ( НЕ ОчереднойУзелCMLПрочитан ) Тогда Прервать; КонецЕсли;	
		
		ТипУзла = ОбъектCML.ТипУзла;
		ИмяУзла = ОбъектCML.Имя;
		
		Если (ТипУзла = ТипУзлаXML.НачалоЭлемента) Тогда
			ПоследовательностьЭлементов = ДобавитьЭлементКПоследовательности(ПоследовательностьЭлементов, ИмяУзла);
			Успешно = ОбработатьНачалоЭлемента(ОбъектCML, ПоследовательностьЭлементов, ДеревоОбъектов);
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось обработать начало элемента (" + ПоследовательностьЭлементов + ").", Ложь,,1);
				Прервать;
			КонецЕсли;
		ИначеЕсли ( ТипУзла = ТипУзлаXML.КонецЭлемента ) Тогда
			ПоследовательностьЭлементов = УдалитьПоследнийЭлементИзПоследовательности(ПоследовательностьЭлементов);
		ИначеЕсли ( ТипУзла = ТипУзлаXML.Текст ) Тогда	
			ЗначениеЭлемента = ОбъектCML.Значение;  
			Успешно = ОбработатьЗначениеЭлементаXML(ПоследовательностьЭлементов, ЗначениеЭлемента, ДеревоОбъектов);
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось обработать значение элемента (" + ПоследовательностьЭлементов + ") = (" + ЗначениеЭлемента + ").", Ложь,,1);
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбъектCML.Закрыть();
	
	Если ( НЕ Успешно ) Тогда ДеревоОбъектов = Неопределено; КонецЕсли;
		
	Возврат ДеревоОбъектов;
	
	
КонецФункции

Функция ДобавитьЭлементКПоследовательности(Знач ПоследовательностьЭлементов, Знач ИмяУзла)
	
	ИсключатьИзПоследовательности	= Новый Массив();
	ИсключатьИзПоследовательности.Добавить("КоммерческаяИнформация");
	
	Если ( ИсключатьИзПоследовательности.Найти(ИмяУзла) = Неопределено ) Тогда		
		Если ( НЕ ПоследовательностьЭлементов = "" ) Тогда
			ПоследовательностьЭлементов = ПоследовательностьЭлементов + ".";	
		КонецЕсли;	
		ПоследовательностьЭлементов = ПоследовательностьЭлементов + ИмяУзла;	
	КонецЕсли;
	
	Возврат ПоследовательностьЭлементов;

КонецФункции

Функция ОбработатьЗначениеЭлементаXML(Знач ИмяЭлемента, Знач ЗначениеЭлемента, ТаблицаОбъектов);
	Успешно 			 = Истина;
	КоличествоОбъектов   = ТаблицаОбъектов.Строки.Количество();
	ИмяТекущегоЭлемента  = ПолучитьИмяЭлементаИзПоследовательности(ИмяЭлемента);
	
	Если ( КоличествоОбъектов > 0 ) Тогда
		
		ТекущаяСтрокаДерева       = ТаблицаОбъектов.Строки[КоличествоОбъектов - 1];
		
		ТекущаяСтрокаТоваровУслуг = Неопределено;
		КоличествоТоваровУслуг    = ТекущаяСтрокаДерева.Строки.Количество();
		Если ( КоличествоТоваровУслуг > 0 ) Тогда
			ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];
		КонецЕсли;	
		
		Если      ( ИмяЭлемента = "Контрагенты.Контрагент.Наименование") Тогда
			ТекущаяСтрокаДерева.Наименование = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.НаименованиеПолное") Тогда
			ТекущаяСтрокаДерева.НаименованиеПолное = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.Фамилия") Тогда
			ТекущаяСтрокаДерева.Фамилия = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.Имя") Тогда
			ТекущаяСтрокаДерева.Имя = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.Отчество") Тогда
			ТекущаяСтрокаДерева.Отчество = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.ЮрЛицо") Тогда
			ТекущаяСтрокаДерева.ЮрЛицо = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.Ид") Тогда
			ТекущаяСтрокаДерева.Ид = ЗначениеЭлемента;
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.ОбъектСсылка = Справочники.Контрагенты.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка: уникальный идентификатор не найден");
				ТекущаяСтрокаДерева.ОбъектСсылка = Справочники.Контрагенты.ПустаяСсылка();
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.ДатаИзменения") Тогда
			Попытка
				ТекущаяСтрокаДерева.ДатаИзменения = XMLЗначение(Тип("Дата"), ЗначениеЭлемента);
			Исключение
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.Телефон") Тогда
			ТекущаяСтрокаДерева.Телефон = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.email") Тогда
			ТекущаяСтрокаДерева.email = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.EMAIL") Тогда
			ТекущаяСтрокаДерева.email = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.Комментарий") Тогда
			ТекущаяСтрокаДерева.Комментарий = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Контрагенты.Контрагент.ИдССайта") Тогда
			ТекущаяСтрокаДерева.ИдССайта = ЗначениеЭлемента;

			/////////////////////////////   автомобили
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Наименование") Тогда
			ТекущаяСтрокаДерева.Наименование = ЗначениеЭлемента;
		ИначеЕсли ( ВРег(ИмяЭлемента) = ВРег("Автомобили.Автомобиль.Ид")) Тогда
			ТекущаяСтрокаДерева.ИД = ЗначениеЭлемента;
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.ОбъектСсылка = Справочники.Автомобили.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка: уникальный идентификатор не найден");
				ТекущаяСтрокаДерева.ОбъектСсылка = Справочники.Автомобили.ПустаяСсылка();
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ИдССайта") Тогда
			ТекущаяСтрокаДерева.ИдССайта = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ИдГруппы") Тогда
			Попытка
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.ИдГруппы = Справочники.Автомобили.ПолучитьСсылку(Гуид);
			Исключение
				ТекущаяСтрокаДерева.ИдГруппы = Неопределено;
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ДатаИзменения") Тогда
			Попытка
				ТекущаяСтрокаДерева.ДатаИзменения = XMLЗначение(Тип("Дата"), ЗначениеЭлемента);
			Исключение
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Производитель") Тогда
			Попытка
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Производитель = Справочники.Производители.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка при загрузке данных: уникальный идентификтор производителя не найден");
				СообщитьОбИсключительнойОшибке(Истина, "Ошибка при загрузке данных: уникальный идентификтор производителя не найден");
				ТекущаяСтрокаДерева.Производитель = Справочники.Производители.ПустаяСсылка();
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Модель") Тогда
			Попытка
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Модель = Справочники.Модели.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка при загрузке данных: уникальный идентификтор модели не найден");
				СообщитьОбИсключительнойОшибке(Истина, "Ошибка при загрузке данных: уникальный идентификтор модели не найден");
				ТекущаяСтрокаДерева.Модель = Справочники.Модели.ПустаяСсылка();
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.МодельСтрокой") Тогда
				ТекущаяСтрокаДерева.МодельСтрокой = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.VIN") Тогда
			ТекущаяСтрокаДерева.VIN = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ГодВыпуска") Тогда
			Попытка
				ТекущаяСтрокаДерева.ГодВыпуска = ОбработатьДатуВремяCML(ЗначениеЭлемента);
			Исключение
				
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ГосНомер") Тогда
			ТекущаяСтрокаДерева.ГосНомер = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ВладелецИд") Тогда
			ТекущаяСтрокаДерева.ХозяинИд = ЗначениеЭлемента;
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Хозяин = Справочники.Контрагенты.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка при загрузке данных: уникальный идентификтор контрагента не найден");
				СообщитьОбИсключительнойОшибке(Истина,"Ошибка при загрузке данных: уникальный идентификтор контрагента не найден");
				ТекущаяСтрокаДерева.Хозяин= Справочники.Контрагенты.ПустаяСсылка();
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.ВладелецИдCСайта") Тогда
			ТекущаяСтрокаДерева.ХозяинИдССайта = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Владелец") Тогда
			Если НЕ ЗначениеЭлемента = "<не выбран>" Тогда
				Попытка 
					Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
					ТекущаяСтрокаДерева.Хозяин = Справочники.Контрагенты.ПолучитьСсылку(Гуид);
				Исключение
					Сообщить("Ошибка при загрузке данных: уникальный идентификтор контрагента не найден");
					СообщитьОбИсключительнойОшибке(Истина,"Ошибка при загрузке данных: уникальный идентификтор контрагента не найден");
					ТекущаяСтрокаДерева.Хозяин= Справочники.Контрагенты.ПустаяСсылка();
				КонецПопытки;
			КонецЕсли;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Комментарий") Тогда
			ТекущаяСтрокаДерева.Комментарий = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Пробег") Тогда
			Попытка 
				Пробег = Число(ЗначениеЭлемента);
			Исключение 
				Пробег = 0;
			КонецПопытки;
			ТекущаяСтрокаДерева.Пробег = Пробег;
		ИначеЕсли ( ИмяЭлемента = "Автомобили.Автомобиль.Цвет") Тогда
			Попытка
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Цвет = Справочники.Цвета.ПолучитьСсылку(Гуид);
			Исключение
				ТекущаяСтрокаДерева.Цвет = Справочники.Цвета.ПустаяСсылка();
			КонецПопытки;
			
			////////////////////////////////////////////////////////////////
			/////////////////////////////   заявки на ремонт
			
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Заказчик") Тогда
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Заказчик = Справочники.Контрагенты.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка при создании документа. Контрагент не найден")
			КонецПопытки;;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ЗаказчикИд") Тогда
			ТекущаяСтрокаДерева.ЗаказчикИд = ЗначениеЭлемента;
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Заказчик = Справочники.Контрагенты.ПолучитьСсылку(Гуид);
			Исключение
				
			КонецПопытки;;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ЗаказчикИдССайта") Тогда
			ТекущаяСтрокаДерева.ЗаказчикИдССайта = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ЗаказчикСтрокой") Тогда
			ТекущаяСтрокаДерева.ЗаказчикСтрокой = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Ид") Тогда
			ТекущаяСтрокаДерева.Ид = ЗначениеЭлемента;
			 //есть ли смысл, если она на сайте не редактируются? нет
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.ОбъектСсылка = Документы.ЗаявкаНаРемонт.ПолучитьСсылку(Гуид);
			Исключение
				ТекущаяСтрокаДерева.ОбъектСсылка = Документы.ЗаявкаНаРемонт.ПустаяСсылка();
				Сообщить("Ошибка при создании документа. Уникальный идентификатор не найден")
			КонецПопытки;;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ИдССайта") Тогда
			ТекущаяСтрокаДерева.ИдССайта = ЗначениеЭлемента;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ДатаИзменения") Тогда
			Попытка
				ТекущаяСтрокаДерева.ДатаИзменения = XMLЗначение(Тип("Дата"), ЗначениеЭлемента);
			Исключение
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ЖелаемаяДата") Тогда
			Попытка
				ТекущаяСтрокаДерева.ДатаНачала = XMLЗначение(Тип("Дата"), ЗначениеЭлемента);
			Исключение
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ВидРемонта") Тогда
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.ВидРемонта = Справочники.ВидыРемонта.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка при создании документа. Уникальный идентификатор вида ремонта не найден");
				ТекущаяСтрокаДерева.ВидРемонта = Справочники.ВидыРемонта.ПустаяСсылка();
			КонецПопытки;;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.АвтомобильИд") Тогда
			ТекущаяСтрокаДерева.АвтомобильИд = ЗначениеЭлемента;
			Попытка
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаДерева.Автомобиль = Справочники.Автомобили.ПолучитьСсылку(Гуид);
			Исключение
				// потом сообщим, если по ИД с сайта не удастся найти автомобиль
				ТекущаяСтрокаДерева.Автомобиль = Справочники.Автомобили.ПустаяСсылка();
			КонецПопытки;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.АвтомобильСтрокой") Тогда
			ТекущаяСтрокаДерева.АвтомобильСтрокой = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.АвтомобильИдССайта") Тогда
			ТекущаяСтрокаДерева.АвтомобильИдССайта = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Дата") Тогда
			Попытка
				ТекущаяСтрокаДерева.Дата = ОбработатьДатуВремяCML(ЗначениеЭлемента);
			Исключение
				ТекущаяСтрокаДерева.Дата = ТекущаяДата();
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ДатаНачала") Тогда
			Попытка
				ТекущаяСтрокаДерева.ДатаНачала = Дата(ЗначениеЭлемента);
			Исключение
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.ПричинаОбращения") Тогда
			ТекущаяСтрокаДерева.ПричинаОбращения = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.НомерЗаявкиСайт") Тогда
			ТекущаяСтрокаДерева.НомерЗаявкиСайт = ЗначениеЭлемента;
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Телефон") Тогда
			ТекущаяСтрокаДерева.Телефон = ЗначениеЭлемента;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Комментарий") Тогда
			ТекущаяСтрокаДерева.Комментарий = ЗначениеЭлемента;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Товары.Товар.Ид") Тогда
			
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаТоваровУслуг["ТоварИд"] = Справочники.Номенклатура.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка. Товар не найден");
				ТекущаяСтрокаТоваровУслуг["ТоварИд"] = Справочники.Номенклатура.ПустаяСсылка();
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Товары.Товар.Количество") Тогда
			Попытка
				ТекущаяСтрокаТоваровУслуг["ТоварКоличество"] = Число(ЗначениеЭлемента);
			Исключение
				ТекущаяСтрокаТоваровУслуг["ТоварКоличество"] = 1;
			КонецПопытки;
			
		//ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Автоработы.Работа.Ид") Тогда
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Автоработы.Работа") Тогда
			Попытка 
				Гуид = Новый УникальныйИдентификатор(ЗначениеЭлемента);
				ТекущаяСтрокаТоваровУслуг["РаботаИд"] = Справочники.Автоработы.ПолучитьСсылку(Гуид);
			Исключение
				Сообщить("Ошибка. Товар не найден");
				ТекущаяСтрокаТоваровУслуг["РаботаИд"] = Справочники.Автоработы.ПустаяСсылка();
			КонецПопытки;
			
		ИначеЕсли ( ИмяЭлемента = "ЗаявкиНаРемонт.ЗаявкаНаРемонт.Автоработы.Работа.Количество") Тогда
			Попытка
				ТекущаяСтрокаТоваровУслуг["РаботаКоличество"] = Число(ЗначениеЭлемента);
			Исключение
				ТекущаяСтрокаТоваровУслуг["РаботаКоличество"] = 1;
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат Успешно;
КонецФункции

Функция ОбработатьЗначениеЭлемента(Знач ИмяЭлемента, Знач ЗначениеЭлемента, ДеревоДокументов);
	
	Успешно 			 = Истина;
	КоличествоДокументов = ДеревоДокументов.Строки.Количество();
	ИмяТекущегоЭлемента  = ПолучитьИмяЭлементаИзПоследовательности(ИмяЭлемента);
	ДеревоАдресов		 = Неопределено;
	ТаблицаКонтактов	 = Неопределено;
	ТаблицаКонтактныхЛиц = Неопределено;
	
	
	
	Если ( КоличествоДокументов > 0 ) Тогда
		
		ТекущаяСтрокаДерева       = ДеревоДокументов.Строки[КоличествоДокументов - 1];
		
		Попытка
			ТекущийДокумент 	      = ТекущаяСтрокаДерева.ДокументОбъект;
		Исключение
		КонецПопытки;
		
		
		ТекущаяСтрокаТоваровУслуг = Неопределено;
		ТекущаяСтрокаСкидок 	  = Неопределено;
		
		КоличествоТоваровУслуг    = ТекущаяСтрокаДерева.Строки.Количество();
		
		Если ( КоличествоТоваровУслуг > 0 ) Тогда
			ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];	
			КоличествоСкидок    	  = ТекущаяСтрокаТоваровУслуг.Строки.Количество();			
			Если ( КоличествоСкидок > 0 ) Тогда
				ТекущаяСтрокаСкидок = ТекущаяСтрокаТоваровУслуг.Строки[КоличествоСкидок - 1];	
			КонецЕсли;		
		КонецЕсли;	
		
		Если ( ИмяЭлемента = "Документ.Ид" ) Тогда
		
		ИначеЕсли ( ИмяЭлемента = "Документ.Номер" ) Тогда
			
			ТекущийДокумент.Организация				= Организация; 
			ТекущийДокумент.Автор					= Менеджер;
			ТекущийДокумент.ПодразделениеКомпании	= Подразделение;
			ТекущийДокумент.Комментарий				= "№ "+ЗначениеЭлемента+" "+HTTPОбменСервер;
			ТекущийДокумент.ВидОплаты				= Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			ТекущийДокумент.ТипЦен					= ТипЦены;
			
			ТекущаяСтрокаДерева.НомерВходящий = мПрефикс+ЗначениеЭлемента;
			
			ОтобразитьСостояние("Обработка документа CML: " + мПрефикс + ТекущаяСтрокаДерева.НомерВходящий);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Дата" ) Тогда
			
			ДатаВремя = ОбработатьДатуВремяCML(ЗначениеЭлемента);
			Если ( ЗаписыватьДокументыТекущейДатой ) Тогда
				ТекущийДокумент.Дата = ТекущаяДата();
			Иначе	
				ТекущийДокумент.Дата = ДатаВремя;
			КонецЕсли;	
			ТекущаяСтрокаДерева.ДатаВходящегоДокумента	= Строка(ДатаВремя);
					
		ИначеЕсли ( ИмяЭлемента = "Документ.Время" ) Тогда
			
			Если ( НЕ ЗаписыватьДокументыТекущейДатой ) Тогда
				ТекущийДокумент.Дата						= ОбработатьДатуВремяCML(ЗначениеЭлемента, ТекущийДокумент.Дата);
				ТекущаяСтрокаДерева.ДатаВходящегоДокумента	= Строка(ТекущийДокумент.Дата);		
			КонецЕсли;
												 
		ИначеЕсли ( ИмяЭлемента = "Документ.ХозОперация" ) Тогда
			
			Если ( НЕ ЗначениеЭлемента = "Заказ товара" ) Тогда	
				СообщитьОбОшибкеОбмена("Ошибка в значении узла <Документ>.<ХозОперация> документа CML (" + ЗначениеЭлемента + ").", Ложь);
				Успешно = Ложь;
			Иначе
				ТекущийДокумент.ХозОперация	= Справочники.ХозОперации.ЗаказПокупателя;
			КонецЕсли;	
							
		ИначеЕсли ( ИмяЭлемента = "Документ.Валюта" ) Тогда
			
			ТекущийДокумент.ВалютаДокумента = ОбработатьВалютуCML(ЗначениеЭлемента);
			Если ( обЗначениеНеЗаполнено(ТекущийДокумент.ВалютаДокумента) ) Тогда
				СообщитьОбОшибкеОбмена("Ошибка в значении узла <Документ>.<Валюта> документа CML (" + ЗначениеЭлемента + ").", Ложь);
				Успешно = Ложь;
			КонецЕсли;	
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Курс" ) Тогда
			
			КурсCML							= Число(ЗначениеЭлемента);
			ТекущийДокумент.КурсДокумента	= КурсCML; //КурсВзаиморасчетов
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Сумма" ) Тогда
							
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Наименование" ) Тогда
			
      		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Наименование", ЗначениеЭлемента); 
							
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Роль" ) Тогда	
			
			Если ( ЗначениеЭлемента = "Покупатель" ) Тогда
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ВидКонтрагента", Перечисления.ВидыКонтрагентов.Покупатель);				
			Иначе
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ВидКонтрагента", Перечисления.ВидыКонтрагентов.Прочее);	
			КонецЕсли;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Комментарий" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Комментарий", ЗначениеЭлемента); 
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ПолноеНаименование"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.ОфициальноеНаименование" ) Тогда

			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("НаименованиеПолное", ЗначениеЭлемента); 
			
			// ЮрФизЛицо
			Если ИмяЭлемента = "Документ.Контрагенты.Контрагент.ПолноеНаименование" Тогда
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ФормаСобственности", Перечисления.ФормыСобственности.ЧастноеЛицо);
			Иначе
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ФормаСобственности", Перечисления.ФормыСобственности.ЮридическоеЛицо);
			КонецЕсли;
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Фамилия" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Фамилия", ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Имя" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Имя", ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Отчество" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Отчество", ЗначениеЭлемента);			
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ДатаРождения" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДатаРождения", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.МестоРождения" Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("МестоРождения", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Пол" Тогда
			
			Если ( Булево(Найти(ЗначениеЭлемента,"уж")) ) Тогда
				ПолФизЛица	= Перечисления.ПолФизическихЛиц.Мужской;	
			ИначеЕсли ( Булево(Найти(ЗначениеЭлемента,"ен")) ) Тогда
				ПолФизЛица	= Перечисления.ПолФизическихЛиц.Женский;	
			Иначе
				ПолФизЛица	= Перечисления.ПолФизическихЛиц.НеУказан;	
			КонецЕсли;		
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Пол", ПолФизЛица); 
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ИНН" ) Тогда
			
			Если ( НЕ ПустаяСтрока(ЗначениеЭлемента) ) Тогда
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ИНН", ЗначениеЭлемента);
			КонецЕсли;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.КПП" ) Тогда		
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("КПП", ЗначениеЭлемента);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.ВидДокумента"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.Серия"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.Номер"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.ДатаВыдачи"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.КемВыдан" ) Тогда		
			
			СохраненноеЗначение = "";
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДокументУдостоверяющийЛичность", СохраненноеЗначение); 
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДокументУдостоверяющийЛичность", СохраненноеЗначение + ЗначениеЭлемента + " "); 
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.Представление"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.Комментарий" ) Тогда
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева						= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента]	= ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле.Тип"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле.Значение" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева			= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
			СтрокаАдресногоПоля		= СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
					
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.Представление"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.Комментарий" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле.Тип"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле.Значение" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.КПП" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("КПП", ЗначениеЭлемента); 

		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ОсновнойВидДеятельности" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ОсновнойВидДеятельности", ЗначениеЭлемента); 
							
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Представление"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Комментарий" ) Тогда	
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле.Тип"	
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле.Значение" ) Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Тип" 
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Значение"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Комментарий" ) Тогда		
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактов", ТаблицаКонтактов);
			СтрокаТаблицы = ТаблицаКонтактов[ТаблицаКонтактов.Количество() - 1];
			СтрокаТаблицы["Контакт" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Представители.Представитель.Контрагент.Наименование" ) Тогда		
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
			СтрокаТаблицы		= ТаблицаКонтактныхЛиц[ТаблицаКонтактныхЛиц.Количество() - 1];
			СтрокаТаблицы["Наименование"] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Комментарий" ) Тогда	
			
			ТекущийДокумент.Комментарий = ТекущийДокумент.Комментарий + ": " + ЗначениеЭлемента;
						
		ИначеЕсли ИмяЭлемента = "Документ.Налоги.Налог.УчтеноВСумме" Тогда
			
			//ТекущийДокумент.СуммаВключаетНДС = Ложь;
			//Если ( ЗначениеЭлемента = БулевоЗначениеCML_Истина ) Тогда
			//	ТекущийДокумент.СуммаВключаетНДС = Истина;
			//КонецЕсли;	
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Налоги.Налог.Ставка" ) Тогда
			
			//ТекущийДокумент.УчитыватьНДС = ЗначениеЗаполнено(ЗначениеЭлемента);
			//Если ( ТекущийДокумент.УчитыватьНДС ) Тогда
			//	ТекущаяСтрокаДерева.СтавкаНДС = ПолучитьПоЗначениюДляВыгрузкиСтавкуНДС(ЗначениеЭлемента);						
			//КонецЕсли;	
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.СтавкиНалогов.СтавкаНалога.Ставка" Тогда
			
			//ТекущийДокумент.УчитыватьНДС = ЗначениеЗаполнено(ЗначениеЭлемента);
			//Если ( ТекущийДокумент.УчитыватьНДС ) Тогда			
			//	ТекущаяСтрокаТоваровУслуг.СтавкаНДС = ПолучитьПоЗначениюДляВыгрузкиСтавкуНДС(ЗначениеЭлемента);						
			//КонецЕсли;			
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Скидки.Скидка.Наименование" ) Тогда
			
			ТекущийДокумент.СкидкаНаценка	= Справочники.ТипыСкидок.НайтиПоНаименованию(ЗначениеЭлемента);		
				
		ИначеЕсли ( ИмяЭлемента = "Документ.Скидки.Скидка.Сумма" ) Тогда
			 
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаДерева.СуммаСкидки = СуммаЧисло;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Скидки.Скидка.УчтеноВСумме" ) Тогда
			
			ТекущаяСтрокаДерева.СкидкаВСумме = Ложь;
			Если ( ЗначениеЭлемента = БулевоЗначениеCML_Истина ) Тогда
				ТекущаяСтрокаДерева.СкидкаВСумме = Истина;
			КонецЕсли;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Ид"
		      ИЛИ ИмяЭлемента = "Документ.Товары.Товар.Наименование" ) Тогда
			  
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		ИначеЕсли (ИмяЭлемента = "Документ.Товары.Товар.БазоваяЕдиница") Тогда 	
			
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента]	= ЗначениеЭлемента;
			ТекущаяСтрокаТоваровУслуг["ТоварУслугаКоэффициент"]				= 1;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.ЦенаЗаЕдиницу"
			  ИЛИ ИмяЭлемента = "Документ.Товары.Товар.Сумма"
		      ИЛИ ИмяЭлемента = "Документ.Товары.Товар.Количество" ) Тогда
			  
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента] = Число(ЗначениеЭлемента);			  
						
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита.Наименование"
			  ИЛИ ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита.Значение" ) Тогда
			  
			ТекущаяСтрокаСкидок["ЗначениеРеквизита" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
						
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка.Сумма" ) Тогда
			
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаСкидок.СуммаСкидки = СуммаЧисло;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка.УчтеноВСумме" ) Тогда
			
			ТекущаяСтрокаСкидок.СкидкаВСумме = Ложь;
			Если ЗначениеЭлемента = БулевоЗначениеCML_Истина Тогда
				ТекущаяСтрокаСкидок.СкидкаВСумме = Истина;
			КонецЕсли;	
			
		ИначеЕсли ( ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита.Наименование"
			  ИЛИ ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита.Значение" ) Тогда
			
			ТекущаяСтрокаТоваровУслуг["Свойство" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		КонецЕсли;
			
	КонецЕсли; 
	
	Возврат Успешно;

КонецФункции

Функция УдалитьПоследнийЭлементИзПоследовательности(Знач ПоследовательностьЭлементов);
	
	ПромСтрока 			= СтрЗаменить(ПоследовательностьЭлементов, ".", Символы.ПС);
	КоличествоЭлементов = СтрЧислоСтрок(ПромСтрока);
	
	ПоследовательностьЭлементов	= "";
	Если ( КоличествоЭлементов > 0 ) Тогда
		КоличествоЭлементов = КоличествоЭлементов - 1;
		Для Счетчик = 1 По КоличествоЭлементов Цикл
			ПоследовательностьЭлементов	= ПоследовательностьЭлементов + "." + СтрПолучитьСтроку(ПромСтрока, Счетчик);
		КонецЦикла;	
		ПоследовательностьЭлементов = Прав(ПоследовательностьЭлементов, СтрДлина(ПоследовательностьЭлементов) - 1);
	КонецЕсли;
	
	Возврат ПоследовательностьЭлементов;
КонецФункции

Функция ПолучитьИмяЭлементаИзПоследовательности(Знач ПоследовательностьЭлементов)
	
	ПромСтрока 			= СтрЗаменить(ПоследовательностьЭлементов, ".", Символы.ПС);
	КоличествоЭлементов = СтрЧислоСтрок(ПромСтрока);
	
	ИмяПоследнегоЭлемента = "";
	Если ( КоличествоЭлементов > 0 ) Тогда
		ИмяПоследнегоЭлемента = СтрПолучитьСтроку(ПромСтрока, КоличествоЭлементов);
	КонецЕсли;
	
	Возврат ИмяПоследнегоЭлемента;
	
КонецФункции

Функция ОбработатьЗагруженныеОбъекты(ДеревоОбъектов, ТипСоединения, КоличествоОбработанныхОбъектов)
	Успешно                        = Истина;
	КоличествоОбработанныхОбъектов = 0;
	ДатаИзменения = '00010101';
	
	Если ТипСоединения = "users" Тогда
		
		Для Каждого ТекСтрока из ДеревоОбъектов.Строки Цикл
			
			Если НЕ ЗначениеЗаполнено(ТекСтрока.ИдССайта) Тогда
				Сообщить("Ошибка! <" +ТекСтрока.Наименование + ">: ИД с сайта не заполнено");
				Успешно = Ложь;
				Продолжить;
			КонецЕсли;
			
			// Рассмотрим вариант когда не заполнен GUID контрагента и заполнен ИД с сайта контрагента
			Если ПустаяСтрока(ТекСтрока.ИД) И НЕ ПустаяСтрока(ТекСтрока.ИдССайта) Тогда
				
				// найдем контрагента по значению реквизита GUIDСайта
				Запрос = новый запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	Контрагенты.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Контрагенты КАК Контрагенты
				|ГДЕ
				|	Контрагенты.GUIDСайта = &Значение";
				Запрос.УстановитьПараметр("Значение", ТекСтрока.ИдССайта);
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					// нашли и отлично
					КонтрагентСсылка = Выборка.Ссылка;
					ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
					ОбновитьРеквизитыОбъекта(КонтрагентСсылка, ТекСтрока, ВыгружатьАвтомобилиИЗаказНарядыНаСайт);
				Иначе
					// айдишник с сайта не нашли, ищем дальше
					Успешно = НайтиДобавитьКонтрагента(ТекСтрока);
				КонецЕсли;
				
			// Рассмотрим вариант когда заполнен GUID контрагента и заполнен ИД с сайта контрагента
			ИначеЕсли НЕ ПустаяСтрока(ТекСтрока.ИД) И НЕ ПустаяСтрока(ТекСтрока.ИдССайта) Тогда
				Если ТекСтрока.ОбъектСсылка <> Справочники.Контрагенты.ПустаяСсылка() Тогда
					ОбновленыРеквизиты = ОбновитьРеквизитыОбъекта(ТекСтрока.ОбъектСсылка, ТекСтрока);
					Если НЕ ОбновленыРеквизиты Тогда
						ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
						Успешно = НайтиДобавитьКонтрагента(ТекСтрока, ВыгружатьАвтомобилиИЗаказНарядыНаСайт);
					КонецЕсли;
				Иначе
					ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
					Успешно = НайтиДобавитьКонтрагента(ТекСтрока, ВыгружатьАвтомобилиИЗаказНарядыНаСайт);
				КонецЕсли;
			КонецЕсли;
			
			Если ТекСтрока.ДатаИзменения > ДатаИзменения Тогда
				ДатаИзменения = ТекСтрока.ДатаИзменения;
			КонецЕсли;
			//Если Успешно Тогда
			КоличествоОбработанныхОбъектов = КоличествоОбработанныхОбъектов + 1;
			//КонецЕсли;
		КонецЦикла;
		
		УзелОбъект = УзелОбмена.ПолучитьОбъект();
		УзелОбъект.ДатаИзмененияКонтрагенты = ДатаИзменения;
		УзелОбъект.ОбменДанными.Загрузка = ИСТИНА;
		
		Попытка
			УзелОбъект.Записать();
		Исключение
		КонецПопытки;
		
	ИначеЕсли ТипСоединения = "car" Тогда
		
		Для Каждого ТекСтрока из ДеревоОбъектов.Строки Цикл
			
			Если НЕ ЗначениеЗаполнено(ТекСтрока.ИдССайта) Тогда
				Сообщить("Ошибка! <" +ТекСтрока.Наименование + ">: ИД с сайта не заполнено");
				Продолжить;
			КонецЕсли;
			
			// Рассмотрим вариант когда не заполнен GUID автомобиля и заполнен ИД с сайта автомобиля
			Если ПустаяСтрока(ТекСтрока.ИД) И НЕ ПустаяСтрока(ТекСтрока.ИДССайта) Тогда
				//  ищем авто по айди с сайта
				ХозяинАвтомобиляНайден = ЛОЖЬ;
				
				Запрос = новый запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Автомобили.Ссылка КАК Объект
				|ИЗ
				|	Справочник.Автомобили КАК Автомобили
				|ГДЕ
				|	Автомобили.GUIDСайта = &Значение";
				Запрос.УстановитьПараметр("Значение", ТекСтрока.ИдССайта);
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					// Нашли - проверим хозяина
					АвтоСсылка = Выборка.Объект;
					ХозяинАвтомобиляНайден = ПроверитьХозяинаАвтомобиля(АвтоСсылка, ТекСтрока);
					Если ХозяинАвтомобиляНайден Тогда
						ОбновитьРеквизитыОбъекта(АвтоСсылка, ТекСтрока);
					Иначе
						Продолжить;
					КонецЕсли;
				Иначе
					Успешно = НайтиДобавитьАвтомобиль(ТекСтрока) И Успешно;
				КонецЕсли;
				
			// Рассмотрим вариант когда заполнен GUID автомобиля и заполнен ИД с сайта автомобиля
			ИначеЕсли НЕ ПустаяСтрока(ТекСтрока.ИД) И НЕ ПустаяСтрока(ТекСтрока.ИДССайта) Тогда
				Если ТекСтрока.ОбъектСсылка <> Справочники.Автомобили.ПустаяСсылка() Тогда
					ОбновленыРеквизиты = ОбновитьРеквизитыОбъекта(ТекСтрока.ОбъектСсылка, ТекСтрока);
					Если НЕ ОбновленыРеквизиты Тогда
						Успешно = НайтиДобавитьАвтомобиль(ТекСтрока) И Успешно;
					КонецЕсли;
				Иначе
					Успешно = НайтиДобавитьАвтомобиль(ТекСтрока) И Успешно;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекСтрока.ДатаИзменения > ДатаИзменения Тогда
				ДатаИзменения = ТекСтрока.ДатаИзменения;
			КонецЕсли;
			//Если Успешно Тогда
			КоличествоОбработанныхОбъектов = КоличествоОбработанныхОбъектов + 1;
			//КонецЕсли;
			
		КонецЦикла;
		
		УзелОбъект = УзелОбмена.ПолучитьОбъект();
		УзелОбъект.ДатаИзмененияАвтомобили = ДатаИзменения;
		УзелОбъект.ОбменДанными.Загрузка = ИСТИНА;
		
		Попытка
			УзелОбъект.Записать();
		Исключение
		КонецПопытки;
		
	ИначеЕсли ТипСоединения = "request" Тогда
		
		Для Каждого ТекСтрока ИЗ ДеревоОбъектов.Строки Цикл
			
			Если НЕ ЗначениеЗаполнено(ТекСтрока.ИдССайта) Тогда
				Сообщить("Ошибка! < Заявка на ремонт №" +ТекСтрока.НомерЗаявкиСайт + ">: ИД с сайта не заполнено");
				Продолжить;
			КонецЕсли;
			
			Если ПустаяСтрока(ТекСтрока.ИД) И НЕ ПустаяСтрока(ТекСтрока.ИДССайта) Тогда
				// проверим не создана ли эта заявка в фоновом режиме
				Если ЗначениеЗаполнено(ТекСтрока.ЗаказчикСтрокой) И НЕ ЗначениеЗаполнено(ТекСтрока.Заказчик) Тогда
					ТекСтрока.Заказчик = ТекСтрока.ЗаказчикСтрокой;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ТекСтрока.Заказчик) Тогда
					// Скорее всего это новый Заказчик
					Если ЗначениеЗаполнено(ТекСтрока.ЗаказчикИДССайта) Тогда
						Запрос = Новый Запрос;
						Запрос.Текст = 
						"ВЫБРАТЬ
						|	Контрагенты.Ссылка
						|ИЗ
						|	Справочник.Контрагенты КАК Контрагенты
						|ГДЕ
						|	Контрагенты.GUIDСайта = &GUIDСайта";
						Запрос.УстановитьПараметр("GUIDСайта", ТекСтрока.ЗаказчикИДССайта);
						
						Выборка = Запрос.Выполнить().Выбрать();
						Если Выборка.Следующий() Тогда
							ТекСтрока.Заказчик = Выборка.Ссылка;
						Иначе
							Сообщить("Ошибка! <Заявка на ремонт №" +ТекСтрока.НомерЗаявкиСайт + ">: ИД с сайта Заказчика не соответствует имеющимся, рекомендуем произвести обмен Контрагентами");
							Продолжить;
						КонецЕсли;
					Иначе
						Сообщить("Ошибка! <Заявка на ремонт №" +ТекСтрока.НомерЗаявкиСайт + ">: ИД с сайта Заказчика не заполнено, рекомендуем произвести обмен Контрагентами");
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				// проверим не создана ли эта заявка в фоновом режиме
				Если ЗначениеЗаполнено(ТекСтрока.АвтомобильСтрокой) И НЕ ЗначениеЗаполнено(ТекСтрока.Автомобиль) Тогда
					ТекСтрока.Автомобиль = ТекСтрока.АвтомобильСтрокой;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ТекСтрока.Автомобиль) Тогда 
					// Скорее всего это новый Автомобиль
					Если ЗначениеЗаполнено(ТекСтрока.АвтомобильИДССайта) Тогда
						Запрос = Новый Запрос;
						Запрос.Текст = 
						"ВЫБРАТЬ
						|	Автомобили.Ссылка
						|ИЗ
						|	Справочник.Автомобили КАК Автомобили
						|ГДЕ
						|	Автомобили.GUIDСайта = &GUIDСайта";
						Запрос.УстановитьПараметр("GUIDСайта", ТекСтрока.АвтомобильИДССайта);
						
						Выборка = Запрос.Выполнить().Выбрать();
						Если Выборка.Следующий() Тогда
							ТекСтрока.Автомобиль = Выборка.Ссылка;
						Иначе
							Сообщить("Ошибка! <Заявка на ремонт №" +ТекСтрока.НомерЗаявкиСайт + ">: ИД с сайта Автомобиля не соответствует имеющимся, рекомендуем произвести обмен Автомобилями");
							Продолжить;
						КонецЕсли;
					Иначе
						Сообщить("Ошибка! <Заявка на ремонт №" +ТекСтрока.НомерЗаявкиСайт + ">: ИД с сайта Автомобиля не заполнено, рекомендуем произвести обмен Автомобилями");
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЗаявкаНаРемонт.Ссылка,
				|	ЗаявкаНаРемонт.Заказчик
				|ИЗ
				|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
				|ГДЕ
				|	ЗаявкаНаРемонт.GUIDСайта = &GUIDСайта";
				
				Запрос.УстановитьПараметр("GUIDСайта",  ТекСтрока.ИДССайта);
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					ЗаявкаНаРемонтСсылка = Выборка.Ссылка;
					Если Выборка.Заказчик = ТекСтрока.Заказчик ИЛИ ТипЗнч(Выборка.Заказчик) = Тип("Строка") Тогда
						ОбновитьРеквизитыОбъекта(ЗаявкаНаРемонтСсылка, ТекСтрока);
					Иначе
						Продолжить;
					КонецЕсли;
				Иначе
					Успешно = СоздатьНовыйДокументЗаявкаНаРемонт(ТекСтрока);
				КонецЕсли;
				
			ИначеЕсли НЕ ПустаяСтрока(ТекСтрока.ИД) И НЕ ПустаяСтрока(ТекСтрока.ИДССайта) Тогда
				Если ТекСтрока.ОбъектСсылка <> Документы.ЗаявкаНаРемонт.ПустаяСсылка() Тогда
					ОбновленыРеквизиты = ОбновитьРеквизитыОбъекта(ТекСтрока.ОбъектСсылка, ТекСтрока);
					Если НЕ ОбновленыРеквизиты Тогда
						Успешно = СоздатьНовыйДокументЗаявкаНаРемонт(ТекСтрока);
					КонецЕсли;
				Иначе
					Успешно = СоздатьНовыйДокументЗаявкаНаРемонт(ТекСтрока);
				КонецЕсли;
			КонецЕсли;
			
			Если ТекСтрока.ДатаИзменения > ДатаИзменения Тогда
				ДатаИзменения = ТекСтрока.ДатаИзменения;
			КонецЕсли;
			//Если Успешно Тогда
			КоличествоОбработанныхОбъектов = КоличествоОбработанныхОбъектов + 1;
			//КонецЕсли;
			
		КонецЦикла;
		
		УзелОбъект = УзелОбмена.ПолучитьОбъект();
		УзелОбъект.ДатаИзмененияЗаявки = ДатаИзменения;
		УзелОбъект.ОбменДанными.Загрузка = ИСТИНА;
		
		Попытка
			УзелОбъект.Записать();
		Исключение
		КонецПопытки;
		
			//Если Успешно Тогда
			//КонецЕсли;

	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

// Проверяем соответсвует ли хозяин автомобиля переданному с сайта
Функция ПроверитьХозяинаАвтомобиля(АвтоСсылка, АвтомобильСтрока)
	
	ХозяинАвтомобиляНайден = ЛОЖЬ;
	АвтомобильХозяин = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(АвтоСсылка, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин);
	
	Если ЗначениеЗаполнено(АвтомобильСтрока.Хозяин) И АвтомобильХозяин = АвтомобильСтрока.Хозяин Тогда
		ХозяинАвтомобиляНайден = ИСТИНА;
	КонецЕсли;
	
	Возврат ХозяинАвтомобиляНайден;
	
КонецФункции

Функция ИдентифицироватьКонтрагентов(ДеревоДокументов)
	
	Успешно = Истина;
	
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		Если ( СтрокаДД.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ТипЗнч(СтрокаДД.СтруктураДанныхКонтрагента)=Тип("Структура") И СтрокаДД.СтруктураДанныхКонтрагента.Свойство("Наименование") Тогда
			НаименованиеКонтрагента	= СтрокаДД.СтруктураДанныхКонтрагента.Наименование;
			ОтобразитьСостояние("Идентификация контрагента: " + СтрокаДД.СтруктураДанныхКонтрагента.Наименование);
		Иначе
			НаименованиеКонтрагента	= "";
		КонецЕсли;
		
		Запрос = Новый Запрос();
		
		ИНН = "";
		СтрокаДД.СтруктураДанныхКонтрагента.Свойство("ИНН", ИНН);
		Запрос.УстановитьПараметр("Наименование", НаименованиеКонтрагента);
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка Как Контрагент
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|";
			
		НуженПоискПоИнн = (СпособИдентификацииКонтрагентов = "ИНН") И ( НЕ обЗначениеНеЗаполнено(ИНН));
			
		Если ( НуженПоискПоИнн ) Тогда
			Запрос.Текст = Запрос.Текст + " Контрагенты.ИНН = &ИНН ";
		Иначе
			Запрос.Текст = Запрос.Текст + " Контрагенты.Наименование = &Наименование ";
		КонецЕсли;
				
		Выборка = Запрос.Выполнить().Выбрать();	
		Если (Выборка.Следующий()) Тогда
			КонтрагентСсылка	= Выборка.Контрагент;
			КонтрагентОбъект		= КонтрагентСсылка.ПолучитьОбъект();
		Иначе
			КонтрагентОбъект = СоздатьКонтрагента(СтрокаДД.СтруктураДанныхКонтрагента);
			КонтрагентСсылка = КонтрагентОбъект.Ссылка;
		КонецЕсли;
		
		Если (обЗначениеНеЗаполнено(КонтрагентСсылка)) Тогда
			Успешно = Ложь;
			Прервать;
		КонецЕсли;
		
		ЗаполнитьПодчиненныеДанныеКонтрагента(КонтрагентСсылка, СтрокаДД.СтруктураДанныхКонтрагента);
		
		Док				= СтрокаДД.ДокументОбъект;
		Док.Контрагент	= КонтрагентСсылка;
		
		ДопПараметры	= Новый Структура(); 		
		ЕстьТипЦен		= (Док.Метаданные().Реквизиты.Найти("ТипЦен")<>Неопределено);
		
		//ЗаказыНаОтгрузку	 
		ВидДоговора	= Перечисления.ВидыДоговоров.Продажа;
		ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
		ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());		 
		ДопПараметры.Вставить("ТипЦенПродажи",?(ЕстьТипЦен,Док.ТипЦен,Справочники.ТипыЦен.ПустаяСсылка()));			  
	
		Попытка
			ТекущийДоговор				= Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Док.Контрагент,ВидДоговора,Док,ДопПараметры);
			Док.ДоговорВзаиморасчетов	= ТекущийДоговор;
		Исключение
			Успешно	= Ложь;
			СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки(), "Не удалось получить договор взаиморасчетов для контрагента"+Строка(КонтрагентСсылка.Наименование));
			Прервать;
		КонецПопытки;
		
		Если (обЗначениеНеЗаполнено(Док.ДоговорВзаиморасчетов)) Тогда						
				Док.ДоговорВзаиморасчетов = СоздатьДоговорПоПараметрам(Док, Док.Контрагент, Док.Организация, Док.ВалютаДокумента);	
		КонецЕсли;
			
				
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция СоздатьДоговорПоПараметрам(Док, Контрагент, Организация, ВалютаВзаиморасчетов)
	
	НайденныйДоговорОбъект = Справочники.ДоговорыВзаиморасчетов.СоздатьЭлемент();
	ОбработкаЗаполненияНовогоОбъекта(НайденныйДоговорОбъект);
	НайденныйДоговорОбъект.УстановитьНовыйКод();
	
	Если ( обЗначениеНеЗаполнено(ВалютаВзаиморасчетов) ) Тогда
		ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
	НайденныйДоговорОбъект.Наименование	= "Договор в "+ВалютаВзаиморасчетов+" от "+Док.Дата;
	
	НайденныйДоговорОбъект.ВалютаВзаиморасчетов		= ВалютаВзаиморасчетов;
	НайденныйДоговорОбъект.Организация				= Организация;
	НайденныйДоговорОбъект.Подразделение			= Подразделение;
	НайденныйДоговорОбъект.ДатаНачала				= Док.Дата;
	НайденныйДоговорОбъект.Владелец           		= Контрагент;
	НайденныйДоговорОбъект.ВидДоговора 				= Перечисления.ВидыДоговоров.Продажа;
	НайденныйДоговорОбъект.ВидОплаты				= Док.ВидОплаты;
	НайденныйДоговорОбъект.АвтоЗакрытиеСделок		= обПраво("АвтоЗакрытиеСделок",,,Организация);
	НайденныйДоговорОбъект.ЕдиницаИзмеренияАвтоработВПечатныхФормах = Перечисления.ЕдиницаИзмеренияАвтоработВПечатныхФормах.ЕдиницаИзмеренияЧас;
	
	ПоискПоВидуДеятельности = (НЕ Док = Неопределено) И зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон");
	
	Если ПоискПоВидуДеятельности И Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.Документооборот.Подсистемы.Автосалон.Состав.Содержит(Док.Метаданные()) Тогда
		НайденныйДоговорОбъект.ДляАвтосалона  = Истина;
	Иначе
		НайденныйДоговорОбъект.ДляАвтосервиса = Истина;
	КонецЕсли;
	
	Попытка
		НайденныйДоговорОбъект.Записать();
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки(), "Не удалось записать договор контрагента.");
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НайденныйДоговорОбъект.Ссылка;
		
КонецФункции

Функция СоздатьКонтрагента(СтруктураДанныхКонтрагента, СсылкаКонтрагент=Неопределено)
	
	НовыйКонтрагент				= Справочники.Контрагенты.СоздатьЭлемент();
	ОбработкаЗаполненияНовогоОбъекта(НовыйКонтрагент);
	НовыйКонтрагент.УстановитьНовыйКод();
	НовыйКонтрагент.Родитель	= ГруппаДляНовыхКонтрагентов;
	ЗаполнитьЗначенияСвойств(НовыйКонтрагент, СтруктураДанныхКонтрагента);
	НовыйКонтрагент.Наименование = СтруктураДанныхКонтрагента.email;
	
	СтрокаФИО = ПолучитьПоСтруктуреСтрокиСФИО(СтруктураДанныхКонтрагента);
	Если ( Не ПустаяСтрока(СтрокаФИО) И СтрокаФИО <> НовыйКонтрагент.НаименованиеПолное ) Тогда	
		НовыйКонтрагент.НаименованиеПолное = СтрокаФИО;
	КонецЕсли;	
	
	Если СсылкаКонтрагент<>Неопределено Тогда
		Попытка
			НовыйКонтрагент.УстановитьСсылкуНового(СсылкаКонтрагент); 
		Исключение
			ОписаниеО=ОписаниеОшибки();
			ОписаниеО=ОписаниеО;
		КонецПопытки; 
	КонецЕсли; 
	
	Попытка
		НовыйКонтрагент.Записать();
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
	КонецПопытки;	
	
	Возврат НовыйКонтрагент.Ссылка;
	
КонецФункции

Функция ОпределитьПоТипуИмяПоляКонтактнойИнформации(ИмяТипа)
	
	ИмяПоля = "Поле10";
	
	Если ( ИмяТипа = "Почтовый индекс"
		ИЛИ ИмяТипа = "Страна") Тогда
		ИмяПоля = "Поле1";
	ИначеЕсли ( ИмяТипа = "Регион" ) Тогда
		ИмяПоля = "Поле2";
	ИначеЕсли ( ИмяТипа = "Район" ) Тогда
		ИмяПоля = "Поле3";
	ИначеЕсли ( ИмяТипа = "Населенный пункт" ) Тогда
		ИмяПоля = "Поле4";
	ИначеЕсли ( ИмяТипа = "Город" ) Тогда
		ИмяПоля = "Поле5";
	ИначеЕсли ( ИмяТипа = "Улица" ) Тогда
		ИмяПоля = "Поле6";
	ИначеЕсли ( ИмяТипа = "Дом" ) Тогда
		ИмяПоля = "Поле7";
	ИначеЕсли ( ИмяТипа = "Корпус" ) Тогда
		ИмяПоля = "Поле8";
	ИначеЕсли ( ИмяТипа = "Квартира" ) Тогда
		ИмяПоля = "Поле9";
	Конецесли;		
	
	Возврат ИмяПоля;
	
КонецФункции

Процедура ОпределитьПоданнымТипВидКонтактнойИнформации(КонтактТип, ТипКИ, ВидКИ)
	
	Если ( КонтактТип = "ТелефонРабочий" ) Тогда
		
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон;
		ВидКИ = Справочники.ВидыКонтактнойИнформации.ТелефонСотовый;
		
	ИначеЕсли ( КонтактТип = "Почта" ) Тогда
		
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		ВидКИ = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодчиненныеДанныеКонтрагента(КонтрагентСсылка, СтруктураДанныхКонтрагента)
	
	//Адреса
	ДеревоАдресов	= СтруктураДанныхКонтрагента.ДеревоАдресов;
	Для Каждого СтрокаДерева Из ДеревоАдресов.Строки Цикл
		
		НаборКИ	= РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборКИ.Отбор.Объект.Установить(КонтрагентСсылка);
		НаборКИ.Отбор.Тип.Установить(Перечисления.ТипыКонтактнойИнформации.Адрес);
		НаборКИ.Отбор.Вид.Установить(Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		
		СтрокаКи				= НаборКИ.Добавить();
		СтрокаКи.Объект			= КонтрагентСсылка;
		СтрокаКи.Тип			= Перечисления.ТипыКонтактнойИнформации.Адрес;
		СтрокаКи.Вид			= Справочники.ВидыКонтактнойИнформации.АдресЮридический;
		
		СтрокаКи.Комментарий	= СтрокаДерева.Комментарий;
		СтрокаКи.Представление	= СтрокаДерева.Представление;
		
		УточнениеСтрокой	= "";
		Для Каждого СтрокаУточнения Из СтрокаДерева.Строки Цикл
			ИмяПоля				= ОпределитьПоТипуИмяПоляКонтактнойИнформации(СтрокаУточнения.ПолеТип);
			СтрокаКи[ИмяПоля]	= СтрокаУточнения.ПолеЗначение;
			УточнениеСтрокой	= УточнениеСтрокой + СтрокаУточнения.ПолеЗначение + ";";
		КонецЦикла;
		
		Если ПустаяСтрока(СтрокаКи.Представление)
			И (НЕ ПустаяСтрока(УточнениеСтрокой)) Тогда
			СтрокаКи.Представление	= УточнениеСтрокой;
		КонецЕсли;
		
		Попытка
			НаборКИ.Записать();
		Исключение КонецПопытки;
		
	КонецЦикла;	
	
	//контакты
	ТаблицаКонтактов	= СтруктураДанныхКонтрагента.ТаблицаКонтактов;
	Для Каждого СтрокаДерева Из ТаблицаКонтактов Цикл
		
		//КонтактТип
		//КонтактЗначение
		//КонтактКомментарий
		
		ТипКИ = Неопределено;
		ВидКИ = Неопределено;
		
		ОпределитьПоданнымТипВидКонтактнойИнформации(СтрокаДерева.КонтактТип, ТипКИ, ВидКИ);
		
		Если ( обЗначениеНеЗаполнено(ТипКИ) ИЛИ обЗначениеНеЗаполнено(ВидКИ) ) Тогда
			Продолжить;		
		КонецЕсли;
		
		НаборКИ	= РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборКИ.Отбор.Объект.Установить(КонтрагентСсылка);
		НаборКИ.Отбор.Тип.Установить(ТипКИ);
		НаборКИ.Отбор.Вид.Установить(ВидКИ);
		
		СтрокаКи			= НаборКИ.Добавить();
		СтрокаКи.Объект		= КонтрагентСсылка;
		СтрокаКи.Тип		= ТипКИ;
		СтрокаКи.Вид		= ВидКИ;
		
		СтрокаКи.Комментарий	= СтрокаДерева.КонтактКомментарий;
		СтрокаКи.Представление	= СтрокаДерева.КонтактЗначение;
		
		Попытка
			НаборКИ.Записать();				
		Исключение	КонецПопытки;
	
	КонецЦикла;
	
	// контактные лица
	ТаблицаКонтактныхЛиц	= СтруктураДанныхКонтрагента.ТаблицаКонтактныхЛиц;
	ТаблицаКонтактныхЛиц.Свернуть("Наименование");
	Для Каждого СтрокаКЛ Из ТаблицаКонтактныхЛиц Цикл
		
		КонтактноеЛицоКонтрагента = Справочники.КонтактныеЛица.НайтиПоНаименованию(СтрокаКЛ.Наименование, Истина, , КонтрагентСсылка);
		Если ( НЕ обЗначениеНеЗаполнено(КонтактноеЛицоКонтрагента)) Тогда
			Продолжить;
		КонецЕсли;
		
		Элемент					= Справочники.КонтактныеЛица.СоздатьЭлемент();
		ОбработкаЗаполненияНовогоОбъекта(Элемент);
		Элемент.УстановитьНовыйКод();
		Элемент.Владелец 		= КонтрагентСсылка;
		Элемент.Наименование	= СтрокаКЛ.Наименование;
		Элемент.Записать();		
		
	КонецЦикла;
	
	//свойства
		
КонецПроцедуры

Функция ОпределитьТипНоменклатурыПоВиду(ВидНоменклатуры)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТипыНоменклатуры.Ссылка КАК ТипНоменклатуры
		|ИЗ
		|	Справочник.ТипыНоменклатуры КАК ТипыНоменклатуры
		|ГДЕ
		|	ТипыНоменклатуры.ВидНоменклатуры = &ВидНоменклатуры";
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ( РезультатЗапроса.Пустой() ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].ТипНоменклатуры;
		
КонецФункции

Функция ИнициализироватьВидыНоменкалатуры()
	
	Если ( Не обЗначениеНеЗаполнено(мТипНоменклатурыТовар)
		И Не обЗначениеНеЗаполнено(мТипНоменклатурыУслуга) ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	мТипНоменклатурыТовар	= ОпределитьТипНоменклатурыПоВиду(Перечисления.ВидыНоменклатуры.Товар);
	мТипНоменклатурыУслуга	= ОпределитьТипНоменклатурыПоВиду(Перечисления.ВидыНоменклатуры.Услуга);	
	
	Если ( Не обЗначениеНеЗаполнено(мТипНоменклатурыТовар)
		И Не обЗначениеНеЗаполнено(мТипНоменклатурыУслуга) ) Тогда
		Возврат Истина;	
	КонецЕсли;
	
	Если ( обЗначениеНеЗаполнено(мТипНоменклатурыТовар) ) Тогда
		СообщитьПользователю("Не удалось найти тип номенклатуры: Товар", Ложь,,1);		
	КонецЕсли;
	
	Если ( обЗначениеНеЗаполнено(мТипНоменклатурыУслуга) ) Тогда
		СообщитьПользователю("Не удалось найти тип номенклатуры: Услуга", Ложь,,1);	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьВидНоменклатуры(ИмяВидаНоменклатурыCML)
	
	ТекВидНоменклатуры	= Неопределено;
	Для Каждого Элемент Из Метаданные.Перечисления.ВидыНоменклатуры.ЗначенияПеречисления Цикл
		Если (Элемент.Имя = ИмяВидаНоменклатурыCML) Тогда
			Выполнить("ТекВидНоменклатуры = Перечисления.ВидыНоменклатуры."+Элемент.Имя);	
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ТекВидНоменклатуры
	
КонецФункции

Функция ПолучитьТипНоменклатуры(ИмяТипаНоменклатурыCML, ВидНоменклатурыCML)
	
	ВидНеЗаполнен	= обЗначениеНезаполнено(ВидНоменклатурыCML);
	Запрос = Новый Запрос();
	Запрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
	            	  |	ТипыНоменклатуры.Ссылка КАК Ссылка
	            	  |ИЗ
	            	  |	Справочник.ТипыНоменклатуры КАК ТипыНоменклатуры
	            	  |ГДЕ
	            	  |	ТипыНоменклатуры.Наименование = &ИмяТипаНоменклатурыCML	
					  |"+?(ВидНеЗаполнен,"","И ТипыНоменклатуры.ВидНоменклатуры = &ВидНоменклатурыCML");
	Запрос.УстановитьПараметр("ИмяТипаНоменклатурыCML",ИмяТипаНоменклатурыCML);
	Если ( НЕ ВидНеЗаполнен ) Тогда
		Запрос.УстановитьПараметр("ВидНоменклатурыCML", ВидНоменклатурыCML);
	КонецЕсли;
	РезультатЗапроса	= Запрос.Выполнить();
	Если ( РезультатЗапроса.Пустой() ) Тогда
		Возврат Неопределено;	
	Иначе
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;	
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИдНоменклатуры(Знач Ид)
	
	ПозицияРазделителя = Найти(Ид, "#");
	Если ( ПозицияРазделителя > 0 ) Тогда
		ИдНоменклатуры = Лев(Ид, ПозицияРазделителя - 1);
	Иначе
		ИдНоменклатуры = Ид;
	КонецЕсли;	
	
	Возврат ИдНоменклатуры;
	
КонецФункции

Функция ПолучитьИдХарактеристики(Знач Ид)
	
	ПозицияРазделителя = Найти(Ид, "#");
	Если ( ПозицияРазделителя > 0 ) Тогда
		ИдХарактеристики = Прав(Ид, СтрДлина(Ид) - ПозицияРазделителя);
	Иначе
		ИдХарактеристики = "";
	КонецЕсли;	
	
	Возврат ИдХарактеристики;
	
КонецФункции

Функция ВыполнитьПоискНоменклатурыХарактеристикиПоСсылкам(СтрокаТовара, Номенклатура, ХарактеристикаНоменклатуры)
	
	Если ( обЗначениеНеЗаполнено(СтрокаТовара.ТоварУслугаИд) ) Тогда
		Возврат Ложь;
	КонецЕсли;
			
	Попытка
		
		ИдНоменклатуры	= ПолучитьИдНоменклатуры(СтрокаТовара.ТоварУслугаИд);
		Номенклатура	= Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдНоменклатуры));
		Если ( Номенклатура = Справочники.Номенклатура.ПустаяСсылка() ) Тогда			
			Возврат Ложь;
		КонецЕсли;
			
		Если ( Номенклатура.ПолучитьОбъект() = Неопределено ) Тогда
			// Объект не найден
			СообщитьПользователю("Номенклатура не найдена по уникальному идентификатору: " + ИдНоменклатуры, Ложь,,1);
			Возврат Ложь;
		КонецЕсли;	
			
		Если ( Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик > 2 ) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ИдХарактеристики = ПолучитьИдХарактеристики(СтрокаТовара.ТоварУслугаИд);
		Если ( ПустаяСтрока(ИдХарактеристики) ) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдХарактеристики));
		
		Если ( ХарактеристикаНоменклатуры = ПустаяХарактеристикаСсылка ) Тогда
			Возврат Истина;
		КонецЕсли;
			
		Если ( ХарактеристикаНоменклатуры.ПолучитьОбъект() = Неопределено ) Тогда
			СообщитьПользователю("Объект <ХарактеристикаНоменклатуры> не найден: " + Строка(ИдХарактеристики)
				+ ". Будет создан новый объект.", Ложь,,1);		
			Возврат Ложь;	
		КонецЕсли;			
						
	Исключение	
		Возврат Ложь;	
	КонецПопытки;
	
	Возврат Истина;
		
КонецФункции

Функция НайтиСоздатьНоменклатуру(СтрокаТовара, Знач ТипНоменклатуры, СтавкаНДС, ХарактеристикаНоменклатуры = Неопределено)
	
	Номенклатура				= Справочники.Номенклатура.ПустаяСсылка();
	ХарактеристикаНоменклатуры	= Неопределено;
	
	УспешноНайдено = ВыполнитьПоискНоменклатурыХарактеристикиПоСсылкам(СтрокаТовара, Номенклатура, ХарактеристикаНоменклатуры);
	Если УспешноНайдено Тогда
		Возврат Номенклатура;
	КонецЕсли;		
	
	ХарактеристикаНоменклатуры = Неопределено;
	
	Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(Лев(СтрокаТовара.ТоварУслугаНаименование, Метаданные.Справочники.Номенклатура.ДлинаНаименования));
	Если ( Не обЗначениеНеЗаполнено(Номенклатура) ) Тогда
		Возврат Номенклатура;
	КонецЕсли;
		
	ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
		                                               
	Если ( Не обЗначениеНеЗаполнено(СтрокаТовара.ТоварУслугаБазоваяЕдиницаКод) ) Тогда
		ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(СтрокаТовара.ТоварУслугаБазоваяЕдиницаКод);
	КонецЕсли;	
	
	Если ( обЗначениеНеЗаполнено(ЕдиницаПоКлассификатору)
		И ЗначениеЗаполнено(СтрокаТовара.ТоварУслугаБазоваяЕдиница) ) Тогда	
		ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию(СтрокаТовара.ТоварУслугаБазоваяЕдиница, Истина);	
	КонецЕсли;	
	
	// Тип номенклатуры услуга предопределенный
	Если (ТипНоменклатуры = мТипНоменклатурыУслуга) Тогда
		ЕдиницаПоКлассификатору	= ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения;
	КонецЕсли;
	
	ЗапросПоЕдиницеХранения	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                       	               |	ЕдиницыИзмерения.Ссылка
	                       	               |ИЗ
	                       	               |	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	                       	               |ГДЕ
	                       	               |	ЕдиницыИзмерения.Наименование = &Наименование");
	ЗапросПоЕдиницеХранения.УстановитьПараметр("Наименование",ЕдиницаПоКлассификатору.Наименование);
	ВыборкаЕдиницыХранения	= ЗапросПоЕдиницеХранения.Выполнить().Выбрать();
	Если (ВыборкаЕдиницыХранения.Следующий()) Тогда
		ЕдиницаХраненияОстатков							= ВыборкаЕдиницыХранения.Ссылка;	
	Иначе
		ЕдиницаХраненияОстатков = Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
		ОбработкаЗаполненияНовогоОбъекта(ЕдиницаХраненияОстатков);
		ЕдиницаХраненияОстатков.УстановитьНовыйКод();
		ЕдиницаХраненияОстатков.Наименование            = ЕдиницаПоКлассификатору.Наименование;
		ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору = ЕдиницаПоКлассификатору;
		ЕдиницаХраненияОстатков.Коэффициент             = 1;
		ЕдиницаХраненияОстатков.Владелец                = ТипНоменклатуры;
		
		Попытка
			ЕдиницаХраненияОстатков.Записать();
		Исключение
			СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
			Возврат Справочники.Номенклатура.ПустаяСсылка();
		КонецПопытки;
	
	КонецЕсли;
	
	Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
	ОбработкаЗаполненияНовогоОбъекта(Номенклатура);
	
	Номенклатура.УстановитьНовыйКод();
	Номенклатура.Наименование 			 	= СтрокаТовара.ТоварУслугаНаименование;	
	Номенклатура.ВидНоменклатуры 		 	= ТипНоменклатуры.ВидНоменклатуры;
	Номенклатура.ТипНоменклатуры 		 	= ТипНоменклатуры;	
	Номенклатура.БазоваяЕдиницаИзмерения	= ЕдиницаПоКлассификатору;
	Номенклатура.ОсновнаяЕдиницаИзмерения	= ЕдиницаХраненияОстатков;
	Номенклатура.Комментарий 			 	= СтрокаТовара.ТоварУслугаКомментарий;
	Номенклатура.НаименованиеИностранное	= СтрокаТовара.ТоварУслугаНаименование;
	Номенклатура.НаименованиеПолное		 	= СтрокаТовара.ТоварУслугаНаименование;
	Номенклатура.ВалютаУчета				= СтрокаТовара.Родитель.ДокументОбъект.ВалютаДокумента;
	Номенклатура.СтавкаНДС 				 	= СтавкаНДС;
	Номенклатура.ОсновнаяЕдиницаИзмерения	= ЕдиницаХраненияОстатков.Ссылка;
	
	Попытка
		//Особенность альфа-авто: Автосервис - для услуги должен быть заполнен № по каталогу.
		НомерПоКаталогуУникальный	= обПраво("НомерПоКаталогуУникальный",Права);
		Если (ТипЗнч(НомерПоКаталогуУникальный)=Тип("Булево") И ТипНоменклатуры = мТипНоменклатурыУслуга И НомерПоКаталогуУникальный) Тогда		
			ГСЧ = Новый ГенераторСлучайныхЧисел(4709555);
			Номенклатура.Артикул	= мПрефикс+ГСЧ.СлучайноеЧисло(0, 4294967295);  
		КонецЕсли; 
	Исключение КонецПопытки;
	
	Если ( Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ) Тогда
		Номенклатура.СпособРаспределенияДопРасходов	= Перечисления.СпособыРаспределенияДопРасходов.ПоСумме;
	КонецЕсли;
	
	Попытка
		Номенклатура.Записать();
		Если ( ОбменТоварами И ВыгружатьТолькоИзменения ) Тогда
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, Номенклатура.Ссылка);
		КонецЕсли;
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
		Возврат Справочники.Номенклатура.ПустаяСсылка();	
	КонецПопытки;	
		
	Возврат Номенклатура.Ссылка;	
	
КонецФункции

Функция ИдентифицироватьНоменклатуру(ДеревоДокументов)
	
	Успешно = Истина;
	Успешно = ИнициализироватьВидыНоменкалатуры();
	Если ( НЕ Успешно ) Тогда Возврат Ложь; КонецЕсли;
	ТаблицаУслуг.Очистить();
	
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		Если ( СтрокаДД.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			Продолжить;
		КонецЕсли;	
		
		ОтобразитьСостояние("Идентификация товаров в документе: " + СтрокаДД.ДокументОбъект);
		
		Для Каждого ТоварУслугаСвойство Из СтрокаДД.Строки Цикл
			
			Если ( Не обЗначениеНеЗаполнено(ТоварУслугаСвойство.СвойствоНаименование) ) Тогда
				Продолжить;
			КонецЕсли;
			
			ВидНоменклатурыCML		= Неопределено;
			ТипНоменклатурыCML		= Неопределено;
			ИмяТипаНоменклатурыCML	= "";
			ИмяВидаНоменклатурыCML	= "";
			
			Для Каждого ПодчиненнаяСтрокаТовараУслуги Из ТоварУслугаСвойство.Строки Цикл
				
				Если ( НЕ обЗначениеНеЗаполнено(ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаНаименование)) Тогда
					Если (ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаНаименование = "ТипНоменклатуры") Тогда	
						ИмяТипаНоменклатурыCML	= ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаЗначение;
					ИначеЕсли (ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаНаименование = "ВидНоменклатуры") Тогда
						ИмяВидаНоменклатурыCML	= ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаЗначение; 	
					КонецЕсли;
				КонецЕсли;	
				
			КонецЦикла;
			
			ВидНоменклатурыCML	= ПолучитьВидНоменклатуры(ИмяВидаНоменклатурыCML);
			ТипНоменклатурыCML	= ПолучитьТипНоменклатуры(ИмяТипаНоменклатурыCML, ВидНоменклатурыCML);
			
			Если (обЗначениеНеЗаполнено(ТипНоменклатурыCML)) Тогда
				Если ( ИмяВидаНоменклатурыCML = ВидНоменклатурыCML_Услуга ) Тогда
					ТипНоменклатурыCML	= мТипНоменклатурыУслуга;
				Иначе
					ТипНоменклатурыCML	= мТипНоменклатурыТовар;
				КонецЕсли;
			КонецЕсли;
			
			Если ( обЗначениеНеЗаполнено(ВидНоменклатурыCML) ) Тогда
				ВидНоменклатурыCML	= ТипНоменклатурыCML.ВидНоменклатуры;	
			КонецЕсли;
			
			ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			СтавкаНДСНоменклатуры = ?(ЗначениеЗаполнено(ТоварУслугаСвойство.СтавкаНДС), ТоварУслугаСвойство.СтавкаНДС, СтрокаДД.СтавкаНДС);
			
			
			Если ( ИмяВидаНоменклатурыCML = ВидНоменклатурыCML_Услуга ) Тогда					
				Номенклатура = НайтиСоздатьНоменклатуру(ТоварУслугаСвойство, мТипНоменклатурыУслуга, СтавкаНДСНоменклатуры);		
			Иначе	
				Номенклатура = НайтиСоздатьНоменклатуру(ТоварУслугаСвойство, мТипНоменклатурыТовар, СтавкаНДСНоменклатуры, ХарактеристикаНоменклатуры);		
			КонецЕсли;
			
			Если (обЗначениеНеЗаполнено(Номенклатура)) Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Если ( Номенклатура.ТипНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ) Тогда	
				
				// Если в заказе, загружаемом с сайта попадает услуга, то она не записывается в табличную ЗаказаПокупателя,
				// а воспринимается как свойство документа (Вид, СтоимостьДоставки)
				
				НовСтрока					= ТаблицаУслуг.Добавить();
				НовСтрока.Документ			= СтрокаДД.ДокументОбъект;
				НовСтрока.ВидДоставки		= Номенклатура;
				НовСтрока.СтоимостьДоставки	= ТоварУслугаСвойство.ТоварУслугаСумма;
		
			Иначе	
				
				НовСтрока = СтрокаДД.ДокументОбъект.Товары.Добавить();
				
				НовСтрока.Номенклатура     			= Номенклатура;	
				НовСтрока.ЕдиницаИзмерения 		    = Номенклатура.ОсновнаяЕдиницаИзмерения;				
				НовСтрока.Количество   	   		 	= ТоварУслугаСвойство.ТоварУслугаКоличество;
				НовСтрока.Коэффициент      		 	= Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				НовСтрока.Цена						= ТоварУслугаСвойство.ТоварУслугаЦенаЗаЕдиницу;
				НовСтрока.Сумма						= ТоварУслугаСвойство.ТоварУслугаСумма;
				
				Если ( ЗначениеЗаполнено(ТоварУслугаСвойство.СтавкаНДС) ) Тогда
					НовСтрока.СтавкаНДС    = ТоварУслугаСвойство.СтавкаНДС;
				Иначе
					НовСтрока.СтавкаНДС    = Номенклатура.СтавкаНДС;
				КонецЕсли;
							
				Если (обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры)) Тогда
					Если (НЕ обЗначениеНеЗаполнено(Номенклатура))
						И (НЕ обЗначениеНеЗаполнено(Номенклатура.ТипНоменклатуры))
						И (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=3) Тогда
						НовСтрока.ХарактеристикаНоменклатуры = Неопределено;
					Иначе
						НовСтрока.ХарактеристикаНоменклатуры = СоздатьХарактеристикуНаОсновеНоменклатуры(Номенклатура);
					КонецЕсли;
				Иначе
					НовСтрока.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;	
				КонецЕсли;
				
				Для Каждого СвойствоТовара Из ТоварУслугаСвойство.Строки Цикл
					Если ( СвойствоТовара.СкидкаВСумме = Истина ) Тогда
						НовСтрока.СуммаСкидки	=	СвойствоТовара.СуммаСкидки;
						СтрокаДД.ДокументОбъект.ОбработкаРеквизита("Товары.СуммаСкидки",НовСтрока);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				СтрокаДД.ДокументОбъект.ОбработкаРеквизита("Товары.СтавкаНДС",НовСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
				
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция СоздатьХарактеристикуНаОсновеНоменклатуры(Номенклатура)
	ТекХарактеристика	= Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Номенклатура.НаименованиеПолное);
	Если (обЗначениеНеЗаполнено(ТекХарактеристика)) Тогда
		НоваяХарактеристика	= Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
		ОбработкаЗаполненияНовогоОбъекта(НоваяХарактеристика);
		НоваяХарактеристика.УстановитьНовыйКод();
		Если (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1) Тогда
			НоваяХарактеристика.Владелец	= Номенклатура.ТипНоменклатуры;	
		ИначеЕсли (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик > 1) Тогда
			НоваяХарактеристика.Владелец	= Номенклатура;
		КонецЕсли;
		НоваяХарактеристика.Наименование	= Номенклатура.НаименованиеПолное;
		Попытка
			НоваяХарактеристика.Записать();
		Исключение
			СообщитьПользователю(ОписаниеОшибки(),Ложь,,1);
			НоваяХарактеристика = Неопределено;
		КонецПопытки;
		Возврат НоваяХарактеристика.Ссылка;
	Иначе
		Возврат ТекХарактеристика;
	КонецЕсли;
КонецФункции

Функция ОбработатьДатуВремяCML(ДатаВремяСтрока, НачальнаяДата = Неопределено)
	
	ДатаВремя = Неопределено;	
	Если ( ЗначениеЗаполнено(НачальнаяДата) ) Тогда
		Время 	  = СтрЗаменить(ДатаВремяСтрока, ":", "");
		ДатаВремя = Дата(Формат(НачальнаяДата, "ДФ=yyyyMMdd") + Время);
	Иначе
		Если СтрДлина(ДатаВремяСтрока) = 4 Тогда
			ДатаВремяСтрока = ДатаВремяСтрока + "0101";
		КонецЕсли;
		ДатаВремя = Дата(СтрЗаменить(ДатаВремяСтрока, "-", "") + "000000");	
	КонецЕсли;
	
	Возврат ДатаВремя;
КонецФункции

// Функция - Преобразовать дату и время в параметр запроса на сайт
//
Функция ПреобразоватьДатаВремяВПараметрЗапросаНаСайт(ДатаВремя)
	Результат = "";
	Если ДатаВремя > '00010101' Тогда
		ДатаВремяСтрока = СокрЛП(Формат(ДатаВремя,""));
		Результат = "&last_date_exhange=" + СтрЗаменить(ДатаВремяСтрока, " ", "&last_time_exhange=");
	Иначе
		Результат = "&last_date_exhange=01.01.0001&last_time_exhange=00:00:01";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Функция - Убрать префикс из номера телефона
//
Функция УбратьПрефиксИзНомераТелефона(Телефон)
	
	Телефон = СокрЛП(Телефон);
	ТелефонТолькоЧисла = "";
	Для Элемент = 1 По СтрДлина(Телефон) Цикл
		Если Сред(Телефон, Элемент, 1) <> " " И Сред(Телефон, Элемент, 1) <> "+" И СтрНайти("0123456789", Сред(Телефон, Элемент, 1)) > 0 Тогда
			ТелефонТолькоЧисла = ТелефонТолькоЧисла + Сред(Телефон, Элемент, 1);
		КонецЕсли;
	КонецЦикла;
	
	// Если российский номер или казахский
	Если Лев(ТелефонТолькоЧисла, 1) = "7" Тогда
		ТелефонБезПрефикса = Прав(ТелефонТолькоЧисла,(СтрДлина(ТелефонТолькоЧисла) - 1));
		
	// Если украинский номер
	ИначеЕсли Лев(ТелефонТолькоЧисла, 3) = "380" Тогда
		ТелефонБезПрефикса = Прав(ТелефонТолькоЧисла,(СтрДлина(ТелефонТолькоЧисла) - 3));
		
	// Если белорусский номер
	ИначеЕсли Лев(ТелефонТолькоЧисла, 3) = "375" Тогда
		ТелефонБезПрефикса = Прав(ТелефонТолькоЧисла,(СтрДлина(ТелефонТолькоЧисла) - 3));
		
	Иначе
		ТелефонБезПрефикса = ТелефонТолькоЧисла;
	КонецЕсли;
	
	Возврат ТелефонБезПрефикса;
	
КонецФункции

Функция ОбработатьВалютуCML(КодВалютыСтрока)
	Валюта = Справочники.Валюты.НайтиПоНаименованию(КодВалютыСтрока);
	Возврат Валюта;
КонецФункции

Функция СоздатьОбновитьДокументы(ДеревоДокументов, СтруктураСтатистики, МассивДокументовДляПроведения, МассивЗагруженныхДокументов)
	
	Успешно = Истина;
	Для Каждого Док Из ДеревоДокументов.Строки Цикл
		
		// если на документ есть ссылки, то он вообще пропускается и не загружается
		Если ( Док.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			СтруктураСтатистики.Пропущено = СтруктураСтатистики.Пропущено + 1;
			Продолжить;
		КонецЕсли;
		
		Если ( НЕ обЗначениеНеЗаполнено(Док.РанееЗагруженныйДокументСсылка)) Тогда
			СтруктураСтатистики.Обновлено = СтруктураСтатистики.Обновлено + 1;
		Иначе
			СтруктураСтатистики.Создано   = СтруктураСтатистики.Создано + 1;
		КонецЕсли;	
		
		//Чтение значения для срока поставки
		Попытка
			СрокПоставкиПокупателюПоУмолчанию	= обПраво("СрокПоставкиПокупателюПоУмолчанию", Права);
		Исключение
			СрокПоставкиПокупателюПоУмолчанию	= 7;
		КонецПопытки;
		
		СтруктураОтбора					= Новый Структура("Организация,Объект", Док.ДокументОбъект.Контрагент, Перечисления.ВидыОбъектовСведений.СрокПоставки);
		СтруктураСведений				= РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(?(обЗначениеНеЗаполнено(Док.ДокументОбъект.Дата),ТекущаяДата(),Док.ДокументОбъект.Дата),СтруктураОтбора);
		СрокПоставкиДней				= ?(обЗначениеНеЗаполнено(СтруктураСведений.Значение),СрокПоставкиПокупателюПоУмолчанию,СтруктураСведений.Значение);
		Док.ДокументОбъект.СрокПоставки	= НачалоДня(Док.ДокументОбъект.Дата)+СрокПоставкиДней*60*60*24;
		
		Попытка
			
			// отдельная работа с комментарием		
			Если ( Не ПустаяСтрока(Док.ДокументОбъект.Комментарий)) Тогда
			
				Попытка
					СтрокаИмениСайта = Док.Строки.Найти("Сайт", "СвойствоНаименование");
					
					Если (СтрокаИмениСайта = Неопределено) Тогда
						ИмяСайта = HTTPОбменСервер;
					Иначе					
						ИмяСайта = СтрокаИмениСайта.СвойствоЗначение;
					КонецЕсли;
					
					Док.ДокументОбъект.Комментарий   = Док.ДокументОбъект.Комментарий + " " + ИмяСайта;
				
				Исключение
				КонецПопытки;
				
			КонецЕсли;			
			
			//Проверка на заполненность суммы документа
			Если ТипЗнч(Док.ДокументОбъект)=Тип("ДокументОбъект.ЗаказПокупателя")
				И ТипЗнч(ДеревоДокументов)=Тип("ДеревоЗначений")
				И ДеревоДокументов.Колонки.Найти("ТоварУслугаСумма")<>Неопределено
				И НЕ ЗначениеЗаполнено(Док.ДокументОбъект.СуммаДокумента) Тогда
				РасчетнаяСуммаДокумента	= Док.Строки.Итог("ТоварУслугаСумма");
				Если РасчетнаяСуммаДокумента>0 Тогда
					Док.ДокументОбъект.СуммаДокумента	= РасчетнаяСуммаДокумента;	
				КонецЕсли;
			КонецЕсли;
			
			Док.ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);

			СсылкаНаДокумент = Док.ДокументОбъект.Ссылка;
			МассивЗагруженныхДокументов.Добавить(СсылкаНаДокумент);
			
			Если ( ПроводитьДокументы ИЛИ Док.ДокументОбъект.Проведен ) Тогда
				МассивДокументовДляПроведения.Добавить(Док.ДокументОбъект);
			КонецЕсли;
			
		Исключение	
			СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
		
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция ЗаписатьСвойстваДокументов(ДеревоДокументов, СтруктураСтатистики, МассивОтклоненныхДокументов)
	
	Успешно = Истина;
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		//в качестве свойства устанавливается дата входящего электронного документа.	   
		Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Дата входящего документа", СтрокаДД.ДатаВходящегоДокумента);			
		Если ( НЕ Успешно ) Тогда
			СообщитьПользователю("Не удалось установить свойство документа - Дата входящего документа", Ложь,,1);
			Прервать;
		КонецЕсли;
		
		// ВидДоставки и СтоимостьДоставки записываются как свойства заказа.
		ОтборУслуг = Новый Структура("Документ", СтрокаДД.ДокументОбъект);		
		НайденныеСтроки	= ТаблицаУслуг.НайтиСтроки(ОтборУслуг);
		Если ( НайденныеСтроки.Количество() > 0 ) Тогда
			Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Вид доставки", НайденныеСтроки[0].ВидДоставки);	
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось установить свойство документа - Вид доставки", Ложь,,1);
				Прервать;
			КонецЕсли;
			
			Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Стоимость доставки", НайденныеСтроки[0].СтоимостьДоставки);	
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось установить свойство документа - Стоимость доставки", Ложь,,1);
				Прервать;
			КонецЕсли;			
		КонецЕсли;
		
		//Адрес контрагента также должен попасть в свойства заказа
		Если ТипЗнч(СтрокаДД.СтруктураДанныхКонтрагента)=Тип("Структура")
			И ТипЗнч(СтрокаДД.СтруктураДанныхКонтрагента.ДеревоАдресов)=Тип("ДеревоЗначений") Тогда
			
			Для Каждого СтрокаДереваАдресов Из СтрокаДД.СтруктураДанныхКонтрагента.ДеревоАдресов.Строки Цикл			
				Если СтрокаДереваАдресов.ВидАдреса="АдресРегистрации" И ЗначениеЗаполнено(СтрокаДереваАдресов.Представление) Тогда
					Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Адрес", СтрокаДереваАдресов.Представление);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Для Каждого ТоварУслугаСвойство Из СтрокаДД.Строки Цикл
		
			Если ( обЗначениеНеЗаполнено(ТоварУслугаСвойство.СвойствоНаименование)) Тогда
				Продолжить;
			КонецЕсли;
				
			ИмяСвойства      = ТоварУслугаСвойство.СвойствоНаименование;
			ЗначениеСвойства = ТоварУслугаСвойство.СвойствоЗначение;
			
			Если (НЕ обЗначениеНеЗаполнено(ИмяСвойства) И НЕ обЗначениеНеЗаполнено(ЗначениеСвойства)) Тогда
			   
				Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, ИмяСвойства, ЗначениеСвойства);
				
				Если ( НЕ Успешно ) Тогда
					СообщитьПользователю("Не удалось установить свойство документа - " + ИмяСвойства, Ложь,,1);
					Прервать;
				КонецЕсли;	
									
				Если (ТоварУслугаСвойство.СвойствоЗначение = БулевоЗначениеCML_Истина)
					ИЛИ (ТоварУслугаСвойство.СвойствоЗначение = БулевоЗначениеCML_Да)  Тогда					
				
					Если ( ТоварУслугаСвойство.СвойствоНаименование = "Заказ оплачен") Тогда
						СтруктураСтатистики.ОплаченСписок.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					ИначеЕсли (ТоварУслугаСвойство.СвойствоНаименование = "Доставка разрешена") Тогда
						СтруктураСтатистики.ДоставкаРазрешенаСписок.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					ИначеЕсли (ТоварУслугаСвойство.СвойствоНаименование = "Финальный статус") Тогда
						СтруктураСтатистики.ФинальныйСтатусСписок.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					ИначеЕсли (ТоварУслугаСвойство.СвойствоНаименование = "Отменен") Тогда
						МассивОтклоненныхДокументов.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					КонецЕСли;
				КонецЕсли;
			   
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция НайтиСоздатьУстановитьСвойствоДокумента(ОбъектСсылка, ИмяСвойства, ЗначениеСвойства);
	
	Успешно = Истина;
	
	СвойствоСсылка 	 	= ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка();
	НазначениеСсылка	= ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Документ_ЗаказПокупателя;
	ТипНазначенияСвойств= Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Назначение" , НазначениеСсылка);
	Запрос.УстановитьПараметр("ИмяСвойства", ИмяСвойства+"_йй");
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НазначенияСвойствОбъектовВидыСвойств.Свойство.Ссылка КАК СвойствоСсылка
		|ИЗ
		|	ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойствОбъектовВидыСвойств
		|ГДЕ
		|	НазначенияСвойствОбъектовВидыСвойств.Ссылка = &Назначение
		|	И НазначенияСвойствОбъектовВидыСвойств.Свойство.Наименование = &ИмяСвойства";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		СвойствоСсылка = Выборка.СвойствоСсылка;	
	КонецЕсли;	
	
	Если ( СвойствоСсылка = ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка() ) Тогда
		
		ТекСвойство	= ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию(ИмяСвойства+"_йй");
		Если (обЗначениеНеЗаполнено(ТекСвойство)) Тогда
			НовоеСвойство = ПланыВидовХарактеристик.СвойстваОбъектов.СоздатьЭлемент();
			ОбработкаЗаполненияНовогоОбъекта(НовоеСвойство);
			НовоеСвойство.УстановитьНовыйКод();
			НовоеСвойство.Наименование 		 = ИмяСвойства;
			
			Если (ИмяСвойства = "Вид доставки") Тогда	
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("СправочникСсылка.Номенклатура");				
			ИначеЕсли (ИмяСвойства = "Дата входящего документа") Или (ИмяСвойства = "Дата изменения статуса")
				 Или (ИмяСвойства = "Дата разрешения доставки") Тогда
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата));
			ИначеЕсли (ИмяСвойства = "Заказ оплачен") Или (ИмяСвойства = "Доставка разрешена")
				Или (ИмяСвойства = "Отменен") Или (ИмяСвойства = "Финальный статус")Тогда
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОтветов");	
			ИначеЕсли (ИмяСвойства = "Стоимость доставки") Тогда
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный));	
			//ИначеЕсли (ИмяСвойства = "Входящий номер") Тогда
			//	НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Число",
			//	Новый КвалификаторыЧисла(10,,ДопустимыйЗнак.Неотрицательный));
			ИначеЕсли (ИмяСвойства = "Сайт") Тогда	
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("ПланОбменаСсылка.ОбменССайтомАвтосервиса");
			Иначе
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Строка");
			КонецЕсли;
				
			Попытка 
				НовоеСвойство.Записать();
			Исключение
				СообщитьПользователю("Не удалось записать свойство " + ИмяСвойства, Ложь,,1);
				Возврат Ложь;
			КонецПопытки;
			СвойствоСсылка = НовоеСвойство.Ссылка;
		Иначе
			СвойствоСсылка = ТекСвойство;
		КонецЕсли;
		
		НазначениеДляЗаказа			= ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Документ_ЗаказПокупателя.ПолучитьОбъект();
		НовоеНазначение				= НазначениеДляЗаказа.ВидыСвойств.Добавить();
		НовоеНазначение.Свойство	= СвойствоСсылка;
		НазначениеДляЗаказа.Записать();
		
	КонецЕсли;	
	
	//Преобразование типа значения свойства
	ТекЗначениеСвойства				= ЗначениеСвойства;
	
	Если (Не СвойствоСсылка.ТипЗначения.СодержитТип(ТипЗнч(ТекЗначениеСвойства))) Тогда
		Если (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("Число"))) Тогда
			Попытка
				ТекЗначениеСвойства	= Число(Строка(ЗначениеСвойства));
			Исключение
				Возврат Ложь;
			КонецПопытки;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("Дата"))) Тогда
			Если Найти(ЗначениеСвойства, "-")=5 ИЛИ Найти(ЗначениеСвойства, ".")=5 Тогда
				РазбираемыйГод		= Сред(ЗначениеСвойства,0,4);
				РазбираемыйМесяц	= Сред(ЗначениеСвойства,6,2);
				РазбираемоеЧисло	= Сред(ЗначениеСвойства,9,2);
				ТекЗначение			= ""+РазбираемоеЧисло+"."+РазбираемыйМесяц+"."+РазбираемыйГод;
				Если СтрДлина(ЗначениеСвойства)>10 Тогда
					РазбираемыйЧас		= Сред(ЗначениеСвойства,12,2);
					РазбираемаяМинута	= Сред(ЗначениеСвойства,15,2);
					РазбираемаяСекунда	= Сред(ЗначениеСвойства,18,2);
					ТекЗначение			= ТекЗначение + " "+РазбираемыйЧас+":"+РазбираемаяМинута+":"+РазбираемаяСекунда;
				КонецЕсли;
			Иначе
				ЗначениеСвойства= СтрЗаменить(ЗначениеСвойства,"-",".");				
				ТекЗначение		= ЗначениеСвойства;
			КонецЕсли;
			Попытка
				ТекЗначениеСвойства	= Дата(ТекЗначение);
			Исключение
				Возврат Ложь;
			КонецПопытки;			
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("Строка"))) Тогда
			Попытка
				ТекЗначениеСвойства	= Строка(ЗначениеСвойства);
			Исключение
				Возврат Ложь;
			КонецПопытки;			
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура"))) Тогда
			ЗапросНоменклатуры	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			                  	               |	Номенклатура.Ссылка
			                  	               |ИЗ
			                  	               |	Справочник.Номенклатура КАК Номенклатура
			                  	               |ГДЕ
			                  	               |	Номенклатура.Наименование = &Наименование
			                  	               |	И (НЕ Номенклатура.ПометкаУдаления)");
			ЗапросНоменклатуры.УстановитьПараметр("Наименование", Строка(ЗначениеСвойства));
			РезультатЗапросаНоменклатуры	= ЗапросНоменклатуры.Выполнить();
			Если (РезультатЗапросаНоменклатуры.Пустой()) Тогда
				Возврат Ложь;	
			Иначе
				ВыборкаНоменклатуры	= РезультатЗапросаНоменклатуры.Выбрать();
				ВыборкаНоменклатуры.Выбрать();
				ТекЗначениеСвойства	= ВыборкаНоменклатуры.Ссылка;
			КонецЕсли;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("ПеречислениеСсылка.ВариантыОтветов"))) Тогда
			Если ( Строка(ЗначениеСвойства) = БулевоЗначениеCML_Истина Или Строка(ЗначениеСвойства) = БулевоЗначениеCML_Да ) Тогда
				ТекЗначениеСвойства	= перечисления.ВариантыОтветов.Да;	
			ИначеЕсли ( Строка(ЗначениеСвойства) = БулевоЗначениеCML_Ложь Или Строка(ЗначениеСвойства) = БулевоЗначениеCML_Нет) Тогда
				ТекЗначениеСвойства	= перечисления.ВариантыОтветов.Нет;	
			Иначе
				Возврат Ложь;	
			КонецЕсли;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("ПланОбменаСсылка.ОбменССайтомАвтосервиса"))) Тогда
			ТекЗначениеСвойства		= УзелОбмена;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойств"))) Тогда
			ЗапросЗначенияСвойства	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			                      	               |	ЗначенияСвойств.Ссылка
			                      	               |ИЗ
			                      	               |	Справочник.ЗначенияСвойств КАК ЗначенияСвойств
			                      	               |ГДЕ
			                      	               |	ЗначенияСвойств.Наименование = &Наименование
			                      	               |	И (НЕ ЗначенияСвойств.ПометкаУдаления)");
			ЗапросЗначенияСвойства.УстановитьПараметр("Наименование", Строка(ЗначениеСвойства));
			РезультатЗапросаЗначенияСвойства	= ЗапросЗначенияСвойства.Выполнить();
			Если (РезультатЗапросаЗначенияСвойства.Пустой()) Тогда
				НовоеЗначениеСвойства	= Справочники.ЗначенияСвойств.СоздатьЭлемент();
				ОбработкаЗаполненияНовогоОбъекта(НовоеЗначениеСвойства);
				НовоеЗначениеСвойства.УстановитьНовыйКод();
				НовоеЗначениеСвойства.Наименование	= ЗначениеСвойства;
				НовоеЗначениеСвойства.Владелец		= СвойствоСсылка;
				Попытка
					НовоеЗначениеСвойства.Записать();
				Исключение
					Возврат Ложь;
				КонецПопытки;
				ТекЗначениеСвойства	= НовоеЗначениеСвойства;
			Иначе
				ВыборкаЗначенияСвойства	= РезультатЗапросаЗначенияСвойства.Выбрать();
				ВыборкаЗначенияСвойства.Выбрать();
				ВыборкаЗначенияСвойства.Следующий();
				ТекЗначениеСвойства	= ВыборкаЗначенияСвойства.Ссылка;
			КонецЕсли;			
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
			
	Запись = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
	Запись.Объект	= ОбъектСсылка;
	Запись.Свойство = СвойствоСсылка;
	Запись.Значение = ТекЗначениеСвойства;
	
	Попытка
		Запись.Записать();
	Исключение
		Успешно = Ложь;
		СообщитьОбИсключительнойОшибке(Ложь, "Не удалось записать значение свойства документа " + ИмяСвойства);	
	КонецПопытки;	
	
	Возврат Успешно;
	
КонецФункции

Процедура ОбработкаЗаполненияНовогоОбъекта(ТекОбъект)
	
	//Попытка вызова метода заполнения объекта
	Попытка
		ТекОбъект.Заполнить(Неопределено);	
	Исключение КонецПопытки;
	
	//Установка флага загрузки при обмене данными
	Попытка
		ТекОбъект.ОбменДанными.Загрузка	= Истина;
	Исключение КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УПРАВЛЕНИЯ ОБМЕНОМ ЗАКАЗАМИ

Функция ВыполнитьОбменЗаказНарядами(СтруктураИзменений, КаталогОбмена)
	
	ТипСоединения = "order";
	
	мМассивЗагруженныхОбъектов = Новый Массив();
	КоличествоОбработанныхДокументовНаЗагрузке = 0;
	КоличествоОбработанныхДокументовНаВыгрузке = 0;
	
	СтруктураПараметровСайта	= ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);	
	
	Если ( ВыгружатьНаСайт ) Тогда		
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		
		УспешноЗагружено = Истина;
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= УспешноЗагружено;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
	
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгружено       = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоОбработанныхДокументовНаВыгрузке, ТипСоединения, КаталогОбмена);
		Иначе
			УспешноВыгружено = Истина;
		КонецЕсли;
		
	Иначе	
		
		УспешноВыгружено = ВыгрузитьЗаказНарядыВФайл(СтруктураИзменений, СтруктураПараметровСайта, КаталогВыгрузки, КоличествоОбработанныхДокументовНаВыгрузке);	
		
	КонецЕсли;	
	
	Успешно = УспешноВыгружено;// И УспешноВыгруженыНаряды;
	
	Если ( Успешно ) Тогда
		Если ( КоличествоОбработанныхДокументовНаЗагрузке + КоличествоОбработанныхДокументовНаВыгрузке > 0 ) Тогда
			СообщитьПользователю("Обмен заказ-нарядами успешно завершен", Истина,,2);
		КонецЕсли;	
	ИначеЕсли (КоличествоОбработанныхДокументовНаВыгрузке>0) Тогда
		СообщитьПользователю("Обмен заказ-нарядами завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Функция ВыполнитьОбменМоделями(СтруктураИзменений, КаталогОбмена)
	мМассивЗагруженныхОбъектов = Новый Массив();
	
	КоличествоВыгруженныхОбъектовВ = 0;
	КоличествоВыгруженныхОбъектовЦ = 0;
	КоличествоВыгруженныхОбъектовП = 0;
	КоличествоВыгруженныхОбъектовМ = 0;
	КоличествоВыгруженныхОбъектовА = 0;
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);
	
	Если ( ВыгружатьНаСайт ) Тогда		
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		
		УспешноЗагружено = Истина;
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= УспешноЗагружено;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
	
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгруженыЦ       = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектовВ, "colour", КаталогОбмена);
			УспешноВыгруженыМ       = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектовЦ, "model", КаталогОбмена);
			УспешноВыгруженыП       = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектовП, "manufacturer", КаталогОбмена);
			УспешноВыгруженыВ       = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектовМ, "repair", КаталогОбмена);
			УспешноВыгруженыА       = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектовА, "carworks", КаталогОбмена);
		Иначе 
			УспешноВыгруженыВ = Истина;
			УспешноВыгруженыЦ = Истина;
			УспешноВыгруженыП = Истина;
			УспешноВыгруженыМ = Истина;
			УспешноВыгруженыА = Истина;
		КонецЕсли;
	
	Иначе	

		УспешноВыгруженыЦ = ВыгрузитьЦветаВФайл          (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектовЦ);
		УспешноВыгруженыМ = ВыгрузитьМоделиВФайл         (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектовМ);
		УспешноВыгруженыП = ВыгрузитьПроизводителейВФайл (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектовП);
		УспешноВыгруженыВ = ВыгрузитьВидыРемонтаВФайл    (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектовВ);
		УспешноВыгруженыА = ВыгрузитьАвтоработыВФайл     (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектовА);
		
	КонецЕсли;	
	
	Успешно = УспешноВыгруженыП и УспешноВыгруженыЦ и УспешноВыгруженыВ и УспешноВыгруженыА и УспешноВыгруженыМ;
	КоличествоВыгруженныхМоделей = КоличествоВыгруженныхОбъектовВ + КоличествоВыгруженныхОбъектовЦ + КоличествоВыгруженныхОбъектовП + КоличествоВыгруженныхОбъектовМ + КоличествоВыгруженныхОбъектовА;
	
	Если ( Успешно ) Тогда
		Если (КоличествоВыгруженныхМоделей > 0 ) Тогда
			СообщитьПользователю("Обмен моделями успешно завершен", Истина,,2);
		КонецЕсли;	
	ИначеЕсли (КоличествоВыгруженныхМоделей =0 ) Тогда
		СообщитьПользователю("Обмен моделями завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
КонецФункции

Функция ВыполнитьОбменАвтомобилями(СтруктураИзменений, КаталогОбмена)
	// Двусторонний обмен
	
	ТипСоединения = "car";
	
	мМассивЗагруженныхОбъектов = Новый Массив();
	КоличествоВыгруженныхОбъектов = 0;
	КоличествоОбработанныхОбъектов = 0;
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);	
	
	Если ( ВыгружатьНаСайт ) Тогда
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		
		Если ЗагружатьДанныеССайта Тогда
			УспешноЗагружено	= HTTPЗагрузитьССервера(СтруктураПараметровСайта, ТипСоединения, КоличествоОбработанныхОбъектов);
		Иначе 
			УспешноЗагружено	= Истина;
		КонецЕсли;
		//
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= УспешноЗагружено;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
	
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгружено = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектов, ТипСоединения, КаталогОбмена);
		Иначе 
			УспешноВыгружено = Истина;
		КонецЕсли;
		
	Иначе	
		УспешноВыгружено = ВыгрузитьАвтомобилиВФайл(СтруктураИзменений, СтруктураПараметровСайта, КаталогВыгрузки, КоличествоВыгруженныхОбъектов);
		
	КонецЕсли;	
	
	Успешно = УспешноЗагружено И УспешноВыгружено;
	
	Если (Успешно) Тогда
		Если (КоличествоВыгруженныхОбъектов > 0) Тогда
			СообщитьПользователю("Обмен автомобилями успешно завершен", Истина,,2);
		КонецЕсли;
	ИначеЕсли (КоличествоВыгруженныхОбъектов + КоличествоОбработанныхОбъектов > 0 ) Тогда
		СообщитьПользователю("Обмен автомобилями завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
КонецФункции

Функция ВыполнитьОбменЗаявками(СтруктураИзменений, КаталогОбмена)
	// Двусторонний обмен
	
	ТипСоединения = "request";
	
	мМассивЗагруженныхОбъектов = Новый Массив();
	КоличествоВыгруженныхОбъектов = 0;
	КоличествоОбработанныхОбъектов = 0;
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);	
	
	Если ( ВыгружатьНаСайт ) Тогда
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		
		Если ЗагружатьДанныеССайта Тогда 
			УспешноЗагружено = HTTPЗагрузитьССервера(СтруктураПараметровСайта, ТипСоединения, КоличествоОбработанныхОбъектов);
		Иначе 
			УспешноЗагружено = Истина;
		КонецЕсли;
		//
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= УспешноЗагружено;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
		
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгружено = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектов, ТипСоединения, КаталогОбмена);
		Иначе
			УспешноВыгружено = Истина;
		КонецЕсли;
		
	Иначе	
		УспешноВыгружено = ВыгрузитьЗаявкиВФайл(СтруктураИзменений, СтруктураПараметровСайта, КаталогВыгрузки, КоличествоВыгруженныхОбъектов);
		
	КонецЕсли;	
	
	Успешно = УспешноЗагружено И УспешноВыгружено;
	
	Если (Успешно) Тогда
		Если (КоличествоВыгруженныхОбъектов > 0 ) Тогда
			СообщитьПользователю("Обмен заявками успешно завершен", Истина,,2);
		КонецЕсли;
	ИначеЕсли (КоличествоВыгруженныхОбъектов + КоличествоОбработанныхОбъектов >0 ) Тогда
		СообщитьПользователю("Обмен заявками завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
КонецФункции

Функция ВыполнитьОбменКонтрагентами(СтруктураИзменений, КаталогОбмена)
	// Двусторонний обмен
	
	ТипСоединения = "users";
	
	КоличествоВыгруженныхОбъектов = 0;
	КоличествоОбработанныхОбъектов = 0;
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);	
	
	Если ( ВыгружатьНаСайт ) Тогда
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		
		Если ЗагружатьДанныеССайта Тогда
			УспешноЗагружено = HTTPЗагрузитьССервера(СтруктураПараметровСайта, ТипСоединения, КоличествоОбработанныхОбъектов);
		Иначе
			УспешноЗагружено = Истина;
		КонецЕсли;
		//
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= УспешноЗагружено;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
	
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгружено = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектов, ТипСоединения, КаталогОбмена);
		Иначе 
			УспешноВыгружено = Истина;
		КонецЕсли;
		
	Иначе
		УспешноВыгружено = ВыгрузитьЗаявкиВФайл(СтруктураИзменений, СтруктураПараметровСайта, КаталогВыгрузки, КоличествоВыгруженныхОбъектов);
	КонецЕсли;
	
	Успешно = УспешноЗагружено И УспешноВыгружено;
	
	Если (Успешно) Тогда
		Если (КоличествоВыгруженныхОбъектов > 0 ) Тогда
			СообщитьПользователю("Обмен контрагентами успешно завершен", Истина,,2);
		КонецЕсли;
	ИначеЕсли (КоличествоВыгруженныхОбъектов =0 ) Тогда
		СообщитьПользователю("Обмен контрагентами завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
КонецФункции

Функция ВыполнитьОбменЦенами(КаталогОбмена)
	
	СтруктураПараметровСайта	= ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);
	КоличествоВыгруженныхОбъектов = 0;
	СтруктураИзменений = Новый Структура();
	
	Если ( ВыгружатьНаСайт ) Тогда		
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки = Истина;
		
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгружены = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектов, "prices", КаталогОбмена);
		Иначе
			УспешноВыгружены = Истина;
		КонецЕсли;
		
	Иначе	

		УспешноВыгружены = ВыгрузитьЦеныВФайл    (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектов);
		
		
	КонецЕсли;	
	
	Успешно = УспешноВыгружены;
	
	Если ( Успешно ) Тогда
		Если (КоличествоВыгруженныхОбъектов > 0 ) Тогда
			СообщитьПользователю("Обмен ценами успешно завершен", Истина,,2);
		КонецЕсли;	
	ИначеЕсли (КоличествоВыгруженныхОбъектов >0 ) Тогда
		СообщитьПользователю("Обмен ценами завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
КонецФункции

Функция ВыполнитьОбменПлановымиТО(СтруктураИзменений, КаталогОбмена)
	
	СтруктураПараметровСайта	= ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);
	КоличествоВыгруженныхОбъектов = 0;
	
	ТипСоединения = "schedule";
	
	Если ( ВыгружатьНаСайт ) Тогда		
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки = Истина;
		
		Если ВыгружатьДанныеНаСайт Тогда
			УспешноВыгружены = ВыгрузитьОбъектыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоВыгруженныхОбъектов, ТипСоединения, КаталогОбмена);
		Иначе
			УспешноВыгружены = Истина;
		КонецЕсли;
		
	Иначе	

		УспешноВыгружены = ВыгрузитьПлановыеТОВФайл    (СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоВыгруженныхОбъектов);
		
	КонецЕсли;	
	
	Успешно = УспешноВыгружены;
	
	Если ( Успешно ) Тогда
		Если (КоличествоВыгруженныхОбъектов > 0 ) Тогда
			СообщитьПользователю("Обмен датами ТО успешно завершен", Истина,,2);
		КонецЕсли;	
	ИначеЕсли (КоличествоВыгруженныхОбъектов =0 ) Тогда
		СообщитьПользователю("Обмен датами ТО завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
КонецФункции

Функция ПроверитьОсновнойДоговорКонтрагента(КонтрагентСсылка, ДокОбъект)
	
	Успешно = Истина;
	
	Если ( обЗначениеНеЗаполнено(ДокОбъект.Организация)
		Или обЗначениеНеЗаполнено(ДокОбъект.ПодразделениеКомпании) ) Тогда		
		СообщитьПользователю("Не удалось определить основной договор контрагента (не найдена организация(подразделение)).", Ложь,,1);
		Возврат Ложь;		
	КонецЕсли;
	
	// ищем договор по контрагенту, организации и валюте
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	               |	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	               |ИЗ
	               |	Справочник.ДоговорыВзаиморасчетов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	ДоговорыКонтрагентов.Организация = &Организация
				   |	И ДоговорыКонтрагентов.Подразделение = &Подразделение
				   |	И ДоговорыКонтрагентов.ВидДоговора = Значение(Перечисление.ВидыДоговоров.Продажа)";
				   	               //|	И ДоговорыКонтрагентов.ВидВзаиморасчетов =
								   //|Значение(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам)
				   
	Запрос.УстановитьПараметр("Организация", ДокОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение", ДокОбъект.ПодразделениеКомпании);
	Выборка = Запрос.Выполнить().Выбрать();
	                        
	ТекущийДоговор			= Неопределено;	
	ВалютаВзаиморасчетов	= ДокОбъект.ВалютаДокумента;
		
	Если ( обЗначениеНеЗаполнено(ВалютаВзаиморасчетов)) Тогда
		ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
	Пока ( Выборка.Следующий() ) Цикл		
		ТекущийДоговор = Выборка.Ссылка;
		Если ( Выборка.ВалютаВзаиморасчетов = ВалютаВзаиморасчетов ) Тогда
			Прервать;
		КонецЕсли;	
	КонецЦикла;
    	
	Если ( обЗначениеНеЗаполнено(ТекущийДоговор) ) Тогда
		
		ТекущийДоговор = СоздатьДоговорПоПараметрам(ДокОбъект, КонтрагентСсылка, ДокОбъект.Организация, ВалютаВзаиморасчетов);
		
		Если ( обЗначениеНеЗаполнено(ТекущийДоговор) ) Тогда
			Возврат Ложь;
		КонецЕсли;	
				
	КонецЕсли;
	
	//Попытка	
		ДокОбъект.ДоговорВзаиморасчетов		= ТекущийДоговор;
		//КонтрагентОбъект								= КонтрагентСсылка.ПолучитьОбъект();
		//КонтрагентОбъект.ОсновнойДоговорКонтрагента	= ТекущийДоговор;
		//КонтрагентОбъект.Записать();
	//Исключение
	//	СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки(), "Не удалось записать основной договор контрагенту.");
	//	Успешно = Ложь;
	//КонецПопытки;		
		
	Возврат Успешно;
	
КонецФункции

Функция ПроверитьДобавитьСвойствоОбъекта(ИмяСвойства, НазначениеСсылка, ОписаниеТипа)
	
	///////////////////////////
	// проверим  свойство объекта на наличие в системе
	// и создадим если отсутствует
	
	
	СвойствоСсылка 	 	= ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка();
	ТипНазначенияСвойств= Новый ОписаниеТипов(ОписаниеТипа);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Назначение" , НазначениеСсылка);
	Запрос.УстановитьПараметр("ИмяСвойства", ИмяСвойства);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НазначенияСвойствОбъектовВидыСвойств.Свойство.Ссылка КАК СвойствоСсылка
		|ИЗ
		|	ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойствОбъектовВидыСвойств
		|ГДЕ
		|	НазначенияСвойствОбъектовВидыСвойств.Ссылка = &Назначение
		|	И НазначенияСвойствОбъектовВидыСвойств.Свойство.Наименование = &ИмяСвойства";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		СвойствоСсылка = Выборка.СвойствоСсылка;	
	КонецЕсли;	
	
	Если ( СвойствоСсылка = ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка() ) Тогда
		
		ТекСвойство	= ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию(ИмяСвойства);

		Если (обЗначениеНеЗаполнено(ТекСвойство)) Тогда
			НовоеСвойство = ПланыВидовХарактеристик.СвойстваОбъектов.СоздатьЭлемент();
			ОбработкаЗаполненияНовогоОбъекта(НовоеСвойство);
			НовоеСвойство.УстановитьНовыйКод();
			НовоеСвойство.Наименование 		 = ИмяСвойства;
			НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Строка");
			
			Попытка 
				НовоеСвойство.Записать();
			Исключение
				СообщитьПользователю("Не удалось записать свойство <" + ИмяСвойства+">", Ложь,,1);
				Возврат СвойствоСсылка;
			КонецПопытки;
			
			СвойствоСсылка = НовоеСвойство.Ссылка;
		Иначе
			СвойствоСсылка = ТекСвойство;
		КонецЕсли;
		
		НазначениеДляОбъекта		= НазначениеСсылка.ПолучитьОбъект();
		НовоеНазначение				= НазначениеДляОбъекта.ВидыСвойств.Добавить();
		НовоеНазначение.Свойство	= СвойствоСсылка;
		НазначениеДляОбъекта.Записать();
		
	КонецЕсли;	
	
	
	Возврат СвойствоСсылка;
КонецФункции

Функция НайтиДобавитьКонтрагента(СтрокаДерева, ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ЛОЖЬ)
	
	Успешно = Ложь;
	
	// Ищем контрагента по гуиду
	Если ЗначениеЗаполнено(СтрокаДерева.Ид) Тогда
		
		Попытка
			НовГуид = Новый УникальныйИдентификатор(СтрокаДерева.Ид);
			КонтрагентСсылка = Справочники.Контрагенты.ПолучитьСсылку(НовГуид);
			Если ЗначениеЗаполнено(КонтрагентСсылка) Тогда
				Если КонтрагентСсылка.ПолучитьОбъект() = Неопределено Тогда
					СтруктураКонтрагента = Новый Структура();
					СтруктураКонтрагента = ПреобразоватьСтрокуДереваВСтруктуру(СтрокаДерева);
					КонтрагентСсылка = СоздатьНовогоКонтрагента(СтруктураКонтрагента, КонтрагентСсылка);
					Успешно = Истина; 
				Иначе
					ОбновитьРеквизитыОбъекта(КонтрагентСсылка, СтрокаДерева, ВыгружатьАвтомобилиИЗаказНарядыНаСайт);
					Успешно = Истина; 
				КонецЕсли; 
			Иначе
				ТекстСообщения = "Ошибка обмена: передан несуществующий GUID контрагента "+СтрокаДерева.Ид;
				Сообщить(ТекстСообщения);
				ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
			КонецЕсли;
		Исключение
			ТекстСообщения = "Ошибка обмена: передан несуществующий GUID контрагента "+СтрокаДерева.Ид;
			Сообщить(ТекстСообщения);
			ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
		КонецПопытки;
		
	КонецЕсли;
	
	Если НЕ Успешно Тогда
		Если ЗначениеЗаполнено(СтрокаДерева.email) Тогда
			// Если не нашелся по гуиду, то ишем по мылу
			СтруктураКонтрагента = Новый Структура();
			СтруктураКонтрагента = ПреобразоватьСтрокуДереваВСтруктуру(СтрокаДерева);
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Представление
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
			|	И КонтактнаяИнформация.Представление ПОДОБНО &email";
			Запрос.УстановитьПараметр("email", НРег(СокрЛП(СтрокаДерева.email)));
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				
				МассивКонтрагентов = Новый Массив;
				Выборка = Результат.Выбрать();
				Пока Выборка.Следующий() Цикл
					Если НРег(СокрЛП(Выборка.Представление)) = НРег(СокрЛП(СтрокаДерева.email)) Тогда
						МассивКонтрагентов.Добавить(Выборка.Объект);
					КонецЕсли;
				КонецЦикла;
				Если МассивКонтрагентов.Количество() > 0 Тогда
					КонтрагентСсылка = МассивКонтрагентов[0];
					Если МассивКонтрагентов.Количество() = 1 Тогда
						ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
						ОбновитьРеквизитыОбъекта(КонтрагентСсылка, СтруктураКонтрагента, ВыгружатьАвтомобилиИЗаказНарядыНаСайт);
					Иначе
						ОбновитьРеквизитыОбъекта(КонтрагентСсылка, СтруктураКонтрагента);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(СтрокаДерева.Телефон) Тогда
				//Если есть телефон, ищем по нему
				
				НомерТелефона = УбратьПрефиксИзНомераТелефона(СтрокаДерева.Телефон);
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	КонтактнаяИнформация.Объект
				|ИЗ
				|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
				|ГДЕ
				|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
				|	И КонтактнаяИнформация.Поле2 + КонтактнаяИнформация.Поле3 = &Телефон
				|	И КонтактнаяИнформация.Объект ССЫЛКА Справочник.Контрагенты";
				Запрос.УстановитьПараметр("Телефон", НомерТелефона);
				Выборка = Запрос.Выполнить().Выбрать();
				
				Если Выборка.Следующий() Тогда
					КонтрагентСсылка = Выборка.Объект;
					ПрисвоитьИдССайтаОбъекту(СтрокаДерева, КонтрагентСсылка);
					УстановитьФлагВерификацииКонтрагента(КонтрагентСсылка, СтрокаДерева.ИдССайта);
				Иначе
					// если по e-mail и телефону не нашли 
					КонтрагентСсылка = СоздатьНовогоКонтрагента(СтруктураКонтрагента);
				КонецЕсли;
			Иначе
				// если e-mail не нашли и телефон не заполнен
				КонтрагентСсылка = СоздатьНовогоКонтрагента(СтруктураКонтрагента);
			КонецЕсли;
			
			Успешно = Истина;
		Иначе
			//Сообщить("Ошибка! e-mail не заполнен");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Функция СоздатьНовогоКонтрагента(СтруктураКонтрагента, СсылкаКонтрагент=Неопределено)
	
	Успешно = Истина;
	КонтрагентСсылка = СоздатьКонтрагента(СтруктураКонтрагента,СсылкаКонтрагент);
	
	ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
	ВыгружатьСразуНаСайт = ИСТИНА;
	ОбновитьРеквизитыОбъекта(КонтрагентСсылка, СтруктураКонтрагента, ВыгружатьАвтомобилиИЗаказНарядыНаСайт, ВыгружатьСразуНаСайт);
	
	возврат КонтрагентСсылка;
	
КонецФункции

Функция НайтиДобавитьАвтомобиль(СтрокаДерева)
	
	Успешно = Ложь;
	
	// Ищем автомобиль по гуиду
	Если ЗначениеЗаполнено(СтрокаДерева.Ид) Тогда
		Попытка 
			НовГуид = Новый УникальныйИдентификатор(СтрокаДерева.Ид);
			АвтоСсылка = Справочники.Автомобили.ПолучитьСсылку(НовГуид);
			
			Если ЗначениеЗаполнено(АвтоСсылка) Тогда 
				Если АвтоСсылка.ПолучитьОбъект()=Неопределено Тогда
					СтруктураОбъекта = Новый Структура();
					СтруктураОбъекта = ПреобразоватьСтрокуДереваВСтруктуру(СтрокаДерева);
					Успешно = СоздатьНовыйАвтомобиль(СтруктураОбъекта, АвтоСсылка);
				Иначе
					Успешно = Истина; 
					ОбновитьРеквизитыОбъекта(АвтоСсылка, СтрокаДерева);
				КонецЕсли; 
			Иначе
				ТекстСообщения = "Ошибка обмена: передан несуществующий GUID автомобиля";
				Сообщить(ТекстСообщения);
				ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
			КонецЕсли;
		Исключение
			ТекстСообщения = "Ошибка обмена: передан несуществующий GUID автомобиля";
			Сообщить(ТекстСообщения);
			ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
		КонецПопытки;
	КонецЕсли;
	
	// по ИДССайта не нашли, ищем по VIN
	Если ЗначениеЗаполнено(СтрокаДерева.VIN) И НЕ Успешно Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Автомобили.Ссылка
		|ИЗ
		|	Справочник.Автомобили КАК Автомобили
		|ГДЕ
		|	Автомобили.VIN = &VIN";
		
		Запрос.УстановитьПараметр("VIN", СтрокаДерева.VIN);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			АвтоСсылка = Выборка.Ссылка;
			ХозяинАвтомобиляНайден = ПроверитьХозяинаАвтомобиля(АвтоСсылка, СтрокаДерева);
			Если ХозяинАвтомобиляНайден Тогда
				ОбновитьРеквизитыОбъекта(АвтоСсылка, СтрокаДерева);
				Успешно = ИСТИНА;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// по ИДССайта не нашли, VIN не нашли, ищем по гос.номеру
	Если ЗначениеЗаполнено(СтрокаДерева.ГосНомер) И НЕ Успешно Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвтомобилиСрезПоследних.Автомобиль КАК Ссылка
		|ИЗ
		|	РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследних
		|ГДЕ
		|	АвтомобилиСрезПоследних.Значение = &ГосНомер";
		
		Запрос.УстановитьПараметр("ГосНомер", СтрокаДерева.ГосНомер);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			АвтоСсылка = Выборка.Ссылка;
			ХозяинАвтомобиляНайден = ПроверитьХозяинаАвтомобиля(АвтоСсылка, СтрокаДерева);
			Если ХозяинАвтомобиляНайден Тогда
				ОбновитьРеквизитыОбъекта(АвтоСсылка, СтрокаДерева);
				Успешно = ИСТИНА;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Успешно Тогда
		// по ИДССайта не нашли, VIN не нашли, по гос.номеру не нашли - ищем владельца
		Если НЕ ЗначениеЗаполнено(СтрокаДерева.Хозяин) Тогда
			// если гуид не пришел, то это новый контрагент. попробуем по ID 
			Если ЗначениеЗаполнено(СтрокаДерева.ХозяинИдССайта) Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Контрагенты.Ссылка КАК Объект
				|ИЗ
				|	Справочник.Контрагенты КАК Контрагенты
				|ГДЕ
				|	Контрагенты.GUIDСайта = &Значение";
				Запрос.УстановитьПараметр("Значение", СтрокаДерева.ХозяинИдССайта);
				Результат = Запрос.Выполнить().Выбрать();
				Если Результат.Следующий() Тогда
					СтрокаДерева.Хозяин = Результат.Объект;
				Иначе
					// владелец автомобиля как контрагент в базе отсутствует
					ТекстСообщения = "Контрагент не найден. Необходимо произвести обмен контрагентами. Автомобиль <"+СтрокаДерева.Наименование+"> не создан";
					Сообщить(ТекстСообщения);
					ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
					Возврат ЛОЖЬ;
				КонецЕсли;
				
			Иначе // если нет ни Ид, ни ИдССайта, то это у авто  нет владельца
				ТекстСообщения = "Контрагент не найден. Необходимо произвести обмен контрагентами. Автомобиль <"+СтрокаДерева.Наименование+"> не создан";
				Сообщить(ТекстСообщения);
				ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , ТекстСообщения);
				Возврат ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураОбъекта = Новый Структура();
		СтруктураОбъекта = ПреобразоватьСтрокуДереваВСтруктуру(СтрокаДерева);
		Успешно = СоздатьНовыйАвтомобиль(СтруктураОбъекта);
	КонецЕсли;
	
	Возврат Успешно;

КонецФункции

Функция СоздатьНовыйАвтомобиль(СтруктураОбъекта, АвтоСсылка=Неопределено)
	
	Если ЗначениеЗаполнено(СтруктураОбъекта.МодельСтрокой) Тогда
		СтруктураОбъекта.Модель = НайтиДобавитьМодельНеВыбрана(СтруктураОбъекта.МодельСтрокой);
		СтруктураОбъекта.Комментарий = Строка(СтруктураОбъекта.Комментарий) + Символы.ПС +  "<T>" + "Модель: " + СтруктураОбъекта.МодельСтрокой + "</T>";
	КонецЕсли;
	
	Успешно = Истина;
	АвтоОбъект = Справочники.Автомобили.СоздатьЭлемент();
	АвтоОбъект.УстановитьНовыйКод();
	ЗаполнитьЗначенияСвойств(АвтоОбъект, СтруктураОбъекта);
	АвтоОбъект.Заполнить(Неопределено);
	
	Если АвтоСсылка<>Неопределено Тогда
		Попытка
			АвтоОбъект.УстановитьСсылкуНового(АвтоСсылка); 
		Исключение
			ОписаниеО=ОписаниеОшибки();
			ОписаниеО=ОписаниеО;
		КонецПопытки; 
	КонецЕсли; 
	
	Попытка
		АвтоОбъект.ОбменДанными.Загрузка=Истина;
		АвтоОбъект.Записать();
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Ошибка при создании Автомобиля");
		Возврат Ложь;
	КонецПопытки;
	
	АвтоСсылка = АвтоОбъект.Ссылка;
	
	ВыгружатьСразуНаСайт = ИСТИНА;
	ОбновитьРеквизитыОбъекта(АвтоСсылка, СтруктураОбъекта, , ВыгружатьСразуНаСайт);
	
	// добавить владельца
	Движение = РегистрыСведений.Автомобили.СоздатьМенеджерЗаписи();
	Движение.Автомобиль = АвтоСсылка;
	Движение.ВидЗначения = Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин;
	Движение.Значение = СтруктураОбъекта.Хозяин;
	Движение.Период = ТекущаяДата();
	Движение.Записать();
	
	возврат Успешно;
	
КонецФункции

//
// возвращает ссылку на модель "Модель не выбрана"
Функция НайтиДобавитьМодельНеВыбрана(НаименованиеМодели)
	
	НаименованиеПустое = "(Модель не выбрана)";
	
	МодельСсылка = Справочники.Модели.НайтиПоНаименованию(НаименованиеМодели, Истина);
	Если МодельСсылка = Справочники.Модели.ПустаяСсылка() ИЛИ МодельСсылка = Неопределено Тогда
		НовМодель = Справочники.Модели.СоздатьЭлемент();
		НовМодель.Код = "НеВыбрана";
		НовМодель.Наименование = НаименованиеПустое;
		НовМодель.НаименованиеПолное = НаименованиеПустое;
		НовМодель.Заполнить(Истина);
		Попытка
			НовМодель.ОбменДанными.Загрузка=Истина;
			НовМодель.Записать();
			МодельСсылка = НовМодель.Ссылка;
		Исключение
			Сообщение = ИнформацияОбОшибке();
			СообщитьОбИсключительнойОшибке(Ложь, "Ошибка при создании пустой модели автомобиля: " + Сообщение.Описание);
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат МодельСсылка;
	
КонецФункции

Функция СоздатьНовыйДокументЗаявкаНаРемонт(СтрокаДерева)
	
	Успешно = ЛОЖЬ;
	УстановитьСсылкуСуществующегоДокумента = ЛОЖЬ;
	
	Если НЕ ПустаяСтрока(СтрокаДерева.ИД) Тогда
		Попытка
			ГУИД = Новый УникальныйИдентификатор(СтрокаДерева.ИД);
			ЗаявкаНаРемонт = Документы.ЗаявкаНаРемонт.ПолучитьСсылку(ГУИД);
			
			ЗаявкаНаРемонтОбъект = ЗаявкаНаРемонт.ПолучитьОбъект();
		Исключение
			Ошибка = ОписаниеОшибки();
		КонецПопытки;
		
		Если ЗаявкаНаРемонтОбъект = Неопределено Тогда
			УстановитьСсылкуСуществующегоДокумента = ИСТИНА;
		Иначе
			ОбновитьРеквизитыОбъекта(ЗаявкаНаРемонт, СтрокаДерева);
			Успешно = ИСТИНА;
			Возврат Успешно;
		КонецЕсли;
	КонецЕсли;
	
	ДокументОбъект = Документы.ЗаявкаНаРемонт.СоздатьДокумент();
	ОбработкаЗаполненияНовогоОбъекта(ДокументОбъект);
	
	Если УстановитьСсылкуСуществующегоДокумента Тогда
		Попытка
			ДокументОбъект.УстановитьСсылкуНового(ЗаявкаНаРемонт); 
		Исключение
			ОписаниеО = ОписаниеОшибки();
		КонецПопытки; 
	КонецЕсли; 
	
	Успешно = ИСТИНА;
	
	ДокументОбъект.Номер = СтрокаДерева.НомерЗаявкиСайт;
	ДокументОбъект.ПредставлениеТелефона="";
	ДокументОбъект.ТипЦен = ТипЦены;
	ДокументОбъект.Заказчик   = СтрокаДерева.Заказчик; // Заказчик
	ДокументОбъект.ОбработкаРеквизита("Заказчик");
	ДокументОбъект.Контрагент = СтрокаДерева.Заказчик; // Плательщик
	ДокументОбъект.ОбработкаРеквизита("Контрагент");
	ДокументОбъект.Автомобиль = СтрокаДерева.Автомобиль;
	ДокументОбъект.ОбработкаРеквизита("Автомобиль");
	ДокументОбъект.ПричинаОбращения = СтрокаДерева.ПричинаОбращения;
	ДокументОбъект.КонтактнаяИнформация = СтрокаДерева.Телефон;
	ДокументОбъект.ОбработкаРеквизита("КонтактнаяИнформация");
	ДокументОбъект.ПредставлениеТелефона = СтрокаДерева.Телефон;
	ДокументОбъект.GUIDСайта = СтрокаДерева.ИДССайта;
	ДокументОбъект.ДатаНачала = СтрокаДерева.ДатаНачала;
	
	Для Каждого ТекСтрока из СтрокаДерева.Строки Цикл
		
		Если ЗначениеЗаполнено(ТекСтрока.ТоварИд) Тогда
			НовСтрока = ДокументОбъект.Товары.Добавить();
			НовСтрока.Номенклатура = ТекСтрока.ТоварИд;
			НовСтрока.Количество   = ТекСтрока.ТоварКоличество;
			НовСтрока.ЕдиницаИзмерения =  НовСтрока.Номенклатура.БазоваяЕдиницаИзмерения;
		Иначе
			НовСтрока = ДокументОбъект.Работы.Добавить();
			НовСтрока.Работа = ТекСтрока.РаботаИд;
			НовСтрока.Количество = ?(ЗначениеЗаполнено(ТекСтрока.РаботаКоличество), ТекСтрока.РаботаКоличество, 1);
			НовСтрока.ИдентификаторРаботы = Новый УникальныйИдентификатор;
			НовСтрока.Нормочас = Справочники.Нормочасы.Рубль;
			
		КонецЕсли;
	КонецЦикла;
	
	Попытка 
		ДокументОбъект.ОбменДанными.Загрузка=Истина;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		Сообщить("Ошибка при записи документа");
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	Возврат Успешно; // ДокументСсылка;
	
КонецФункции

Процедура УстановитьФлагВерификацииКонтрагента(КонтрагентСсылка, ИдССайта = "")
	Если УзелОбмена.Пользователи.Количество()>0 Тогда
		ЗаголовокСообщения="Подтверждение подлинности """+КонтрагентСсылка+"""";
		ТекстСообщения="Требуется подтверждение подинности контрагента """+КонтрагентСсылка+""" при регистрации на сайте автосервиса.
		|ID: "+ИдССайта;
		
		Для каждого СтрокаПолучателяНапоминания Из УзелОбмена.Пользователи Цикл
			РегистрСведенийМенеджерЗаписи = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();		
			РегистрСведенийМенеджерЗаписи.Пользователь=СтрокаПолучателяНапоминания.Пользователь;
			РегистрСведенийМенеджерЗаписи.Автор=ПараметрыСеанса.Пользователь;
			РегистрСведенийМенеджерЗаписи.Завершено=Ложь;
			РегистрСведенийМенеджерЗаписи.Объект=КонтрагентСсылка;
			РегистрСведенийМенеджерЗаписи.ДатаНачала=ТекущаяДата();
			РегистрСведенийМенеджерЗаписи.ДатаОповещения=РегистрСведенийМенеджерЗаписи.ДатаНачала;
			РегистрСведенийМенеджерЗаписи.Описание=ТекстСообщения;
			РегистрСведенийМенеджерЗаписи.Редактирование=Ложь;
			РегистрСведенийМенеджерЗаписи.СрокДоНачала=0;
			РегистрСведенийМенеджерЗаписи.Тема=ЗаголовокСообщения;
			РегистрСведенийМенеджерЗаписи.ТипПериода="";
			РегистрСведенийМенеджерЗаписи.УдалитьПоИстеченииСрока=Истина;
			РегистрСведенийМенеджерЗаписи.РеальнаяДатаОповещения = Макс(ТекущаяДата(), РегистрСведенийМенеджерЗаписи.ДатаОповещения - РегистрСведенийМенеджерЗаписи.СрокДоНачала);
			РегистрСведенийМенеджерЗаписи.Записать();
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры

Функция ПрисвоитьИдССайтаОбъекту(СтруктураОбъекта, ОбъектСсылка)
	
	Если ЗначениеЗаполнено(СтруктураОбъекта.ИдССайта) Тогда
		ОбъектОбъект = ОбъектСсылка.ПолучитьОбъект();
		ОбъектОбъект.GUIDСайта = СтруктураОбъекта.ИдССайта;
		Попытка
			ОбъектОбъект.ОбменДанными.Загрузка = Истина;
			ОбъектОбъект.Записать();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не удалось записать <" + ОбъектОбъект.Наименование+">'");
			Сообщение.Сообщить();
			ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , Сообщение.Текст);
		КонецПопытки;
	Иначе 
		//Сообщить("Внмиание! <" + + ">: Телефон не заполнен");
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

функция ПреобразоватьСтрокуДереваВСтруктуру(СтрокаДерева)
	
	Результат = Новый Структура;
	
	// Контрагенты
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Фамилия");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Имя");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Отчество");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "email");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Телефон");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Комментарий");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "НаименованиеПолное");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "ИдССайта");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "ЮрЛицо");
	
	// автомобили
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Наименование");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Производитель");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Модель");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "МодельСтрокой");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "VIN");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "ГодВыпуска");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Комментарий");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "ГосНомер");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Цвет");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Хозяин");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "ИдГруппы");
	ДобавитьКолонкуВСтруктуру(Результат, СтрокаДерева, "Пробег");
	
	
	
	Возврат Результат;
	
КонецФункции

//
// Колонку из дерева в структуру
Функция ДобавитьКолонкуВСтруктуру(Структура, СтрокаДерева, ИмяКолонки)
	
	Попытка 
		Структура.Вставить(ИмяКолонки, СтрокаДерева[ИмяКолонки]);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ОбновитьРеквизитыОбъекта(ОбъектСсылка, СтруктураПараметров, ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ЛОЖЬ, ВыгружатьСразуНаСайт = ЛОЖЬ)
	
	ОбъектОбъект = ОбъектСсылка.ПолучитьОбъект();
	Если ОбъектОбъект = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗНЧ(ОбъектСсылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		СтруктураКонтрагента = ПреобразоватьСтрокуДереваВСтруктуру(СтруктураПараметров);
		
		Если НЕ ЗначениеЗаполнено(ОбъектОбъект.ВидКонтрагента) Тогда
			ОбъектОбъект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураПараметров.ЮрЛицо) И СтруктураПараметров.ЮрЛицо = "1" Тогда
			ОбъектОбъект.Наименование = СтруктураПараметров.Наименование;
			ОбъектОбъект.НаименованиеПолное = СтруктураПараметров.НаименованиеПолное;
		Иначе
			ОбъектОбъект.Фамилия = СтруктураКонтрагента.Фамилия;
			ОбъектОбъект.Имя = СтруктураКонтрагента.Имя;
			ОбъектОбъект.Отчество = СтруктураКонтрагента.Отчество;
			СтрокаФИО = ПолучитьПоСтруктуреСтрокиСФИО(СтруктураКонтрагента);
			Если Не ПустаяСтрока(СтрокаФИО) И СтрокаФИО <> ОбъектОбъект.Наименование Тогда
				ОбъектОбъект.Наименование = СтрокаФИО;
			КонецЕсли;	
		КонецЕсли;
		
		ОбъектОбъект.GUIDСайта = СтруктураПараметров.ИдССайта;
		
		Если НЕ ЗначениеЗаполнено(ОбъектОбъект.НаименованиеПолное) Тогда
			ОбъектОбъект.НаименованиеПолное = ОбъектОбъект.Наименование;
		КонецЕсли;
		
		Если ВыгружатьАвтомобилиИЗаказНарядыНаСайт Тогда
			ОбъектОбъект.ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ИСТИНА;
		КонецЕсли;
		
		Если НЕ ВыгружатьСразуНаСайт Тогда
			ОбъектОбъект.ОбменДанными.Отправитель = УзелОбмена;
		КонецЕсли;
			
		Попытка
			ОбъектОбъект.ОбменДанными.Загрузка=Истина;
			ОбъектОбъект.Записать();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не удалось записать контрагента <" + ОбъектОбъект.Наименование+">'");
			Сообщение.Сообщить();
			ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , Сообщение.Текст);
			Возврат Ложь;
		КонецПопытки;
		
		// обновить e-mail, если он не задан или был изменен
		Если ЗначениеЗаполнено(СтруктураКонтрагента.email) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ 
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Представление
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
			|	И КонтактнаяИнформация.Объект = &Объект";
			Запрос.УстановитьПараметр("Объект", ОбъектОбъект.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			
			ОбновитьEmail = Ложь;
			Если Выборка.Следующий() Тогда
				Если ПустаяСтрока(СокрЛП(Выборка.Представление)) ИЛИ НРег(СокрЛП(Выборка.Представление)) <> НРег(СокрЛП(СтруктураКонтрагента.email)) Тогда
					ОбновитьEmail = Истина;
				КонецЕсли; 
			Иначе 
				ОбновитьEmail = Истина;
			КонецЕсли;
			
			Если ОбновитьEmail Тогда
				
				Движение = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
				Движение.Объект = ОбъектСсылка;
				Движение.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				Движение.Вид = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний;
				Движение.Прочитать();
				
				Движение.Объект = ОбъектСсылка;
				Движение.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				Движение.Вид = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний;
				Движение.Представление = НРег(СокрЛП(СтруктураКонтрагента.email));
				
				Движение.Записать();
				
			КонецЕсли;

		Иначе
			//Сообщить("Ошибка! e-mail не заполнен");
		КонецЕсли;
		
		// Обновить телефон, если он не задан
		Если ЗначениеЗаполнено(СтруктураКонтрагента.Телефон) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Представление,
			|	КонтактнаяИнформация.Вид
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
			|	И КонтактнаяИнформация.Объект = &Объект";
			Запрос.УстановитьПараметр("Объект", ОбъектОбъект.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			
			ОбновитьТелефон = Ложь;
			Если Выборка.Следующий() Тогда
				Если ПустаяСтрока(СокрЛП(Выборка.Представление)) Тогда
					ОбновитьТелефон = Истина;
				КонецЕсли; 
			Иначе 
				ОбновитьТелефон = Истина;
			КонецЕсли;
			
			Если ОбновитьТелефон Тогда
				ОбработкаРедактированиеКонтактнойИнформации=Обработки.РедактированиеКонтактнойИнформации.Создать();
				ОбработкаРедактированиеКонтактнойИнформации.Объект=ОбъектСсылка;
				ОбработкаРедактированиеКонтактнойИнформации.Тип=Перечисления.ТипыКонтактнойИнформации.Телефон;
				ОбработкаРедактированиеКонтактнойИнформации.Вид=Справочники.ВидыКонтактнойИнформации.ТелефонСотовый;
				НовыйТелефон=СтруктураКонтрагента.Телефон;
				Пока СтрДлина(НовыйТелефон)>0 И Лев(НовыйТелефон,1)<>" " Цикл
					ОбработкаРедактированиеКонтактнойИнформации.Поле1=ОбработкаРедактированиеКонтактнойИнформации.Поле1+Лев(НовыйТелефон,1);
					НовыйТелефон=Сред(НовыйТелефон,2);
				КонецЦикла; 
				НовыйТелефон=Сред(НовыйТелефон,2);
				ОбработкаРедактированиеКонтактнойИнформации.Поле2=Лев(НовыйТелефон,3);
				НовыйТелефон=Сред(НовыйТелефон,4);
				ОбработкаРедактированиеКонтактнойИнформации.Поле3=НовыйТелефон;
				
				ОбработкаРедактированиеКонтактнойИнформации.Поле1=ОбработкаРедактированиеКонтактнойИнформации.УбратьИзНомераТелефонаВсеПрефиксы(ОбработкаРедактированиеКонтактнойИнформации.УбратьИзНомераТелефонаВсеБуквы(ОбработкаРедактированиеКонтактнойИнформации.Поле1));
				Если Лев(СокрЛ(ОбработкаРедактированиеКонтактнойИнформации.Поле1),1)<>"+" И НЕ ПустаяСтрока(ОбработкаРедактированиеКонтактнойИнформации.Поле1) Тогда
					ОбработкаРедактированиеКонтактнойИнформации.Поле1=СокрЛП(ОбработкаРедактированиеКонтактнойИнформации.Поле1);
					Пока Лев(ОбработкаРедактированиеКонтактнойИнформации.Поле1, 1)="0" Цикл
						ОбработкаРедактированиеКонтактнойИнформации.Поле1=Сред(ОбработкаРедактированиеКонтактнойИнформации.Поле1,2);
					КонецЦикла;
					Если НЕ ПустаяСтрока(ОбработкаРедактированиеКонтактнойИнформации.Поле1) Тогда
						ОбработкаРедактированиеКонтактнойИнформации.Поле1="+"+ОбработкаРедактированиеКонтактнойИнформации.Поле1;
					КонецЕсли; 
				КонецЕсли;
				ОбработкаРедактированиеКонтактнойИнформации.Поле2=ОбработкаРедактированиеКонтактнойИнформации.УбратьИзНомераТелефонаВсеПрефиксы(ОбработкаРедактированиеКонтактнойИнформации.УбратьИзНомераТелефонаВсеБуквы(ОбработкаРедактированиеКонтактнойИнформации.Поле2));
				ОбработкаРедактированиеКонтактнойИнформации.Поле3=ОбработкаРедактированиеКонтактнойИнформации.УбратьИзНомераТелефонаВсеПрефиксы(ОбработкаРедактированиеКонтактнойИнформации.УбратьИзНомераТелефонаВсеБуквы(ОбработкаРедактированиеКонтактнойИнформации.Поле3));
				ОбработкаРедактированиеКонтактнойИнформации.СформироватьПредставлениеТелефона(ОбработкаРедактированиеКонтактнойИнформации);
				ОбработкаРедактированиеКонтактнойИнформации.ЗаписыватьВРегистр = ИСТИНА;
				ОбработкаРедактированиеКонтактнойИнформации.мРедактируемаяЗапись = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
				ОбработкаРедактированиеКонтактнойИнформации.Записать();
				
			КонецЕсли;

		Иначе
			//Сообщить("телефон не заполнен");
		КонецЕсли;

		Возврат Истина;
		
	ИначеЕсли ТипЗНЧ(ОбъектСсылка) = Тип("СправочникСсылка.Автомобили") Тогда
		
		ЗаполнитьЗначенияСвойств(ОбъектОбъект, СтруктураПараметров);
		ОбъектОбъект.Родитель = ?(СтруктураПараметров.ИдГруппы = Неопределено, Справочники.Автомобили.ПустаяСсылка(), СтруктураПараметров.ИдГруппы);
		ОбъектОбъект.Заполнить(Неопределено);
		ОбъектОбъект.GUIDСайта = СтруктураПараметров.ИдССайта;
		
		Если ЗначениеЗаполнено(СтруктураПараметров.МодельСтрокой) Тогда
			ОбъектОбъект.Модель = НайтиДобавитьМодельНеВыбрана(СтруктураПараметров.МодельСтрокой);
			ОбъектОбъект.Комментарий = Строка(СтруктураПараметров.Комментарий) + Символы.ПС +  "<T>" + "Модель: " + СтруктураПараметров.МодельСтрокой + "</T>";
		КонецЕсли;
		
		Если НЕ ВыгружатьСразуНаСайт Тогда
			ОбъектОбъект.ОбменДанными.Отправитель = УзелОбмена;
		КонецЕсли;
		
		Попытка
			ОбъектОбъект.ОбменДанными.Загрузка=Истина;
			ОбъектОбъект.Записать();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не удалось записать автомобиль <" + ОбъектОбъект.Наименование+">'");
			Сообщение.Сообщить();
			ЗаписьЖурналаРегистрации("ОшибкаПриЗагрузкеДанных", УровеньЖурналаРегистрации.Предупреждение, Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса, , Сообщение.Текст);
			Возврат Ложь;
		КонецПопытки;
		
		// Хозяин
		Если ЗначениеЗаполнено(СтруктураПараметров.Хозяин) Тогда
			ОбновитьХозяина = Ложь;
			АвтомобильХозяин = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ОбъектСсылка, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин);
			Если НЕ ЗначениеЗаполнено(АвтомобильХозяин) Тогда
				ОбновитьХозяина = Истина;
			Иначе
				Если АвтомобильХозяин <> СтруктураПараметров.Хозяин Тогда
					ОбновитьХозяина = Истина;
				КонецЕсли; 
			КонецЕсли;
			Если ОбновитьХозяина Тогда
				Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(ОбъектСсылка, СтруктураПараметров.Хозяин, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин);			
			КонецЕсли;
		КонецЕсли; 
		
		// Пробег
		Если ЗначениеЗаполнено(СтруктураПараметров.Пробег) Тогда
			ОбновитьПробег = Ложь;
			АвтомобильПробег = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ОбъектСсылка, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег);
			Если НЕ ЗначениеЗаполнено(АвтомобильПробег) Тогда
				ОбновитьПробег = Истина;
			Иначе
				Если АвтомобильПробег <> СтруктураПараметров.Пробег Тогда
					ОбновитьПробег = Истина;
				КонецЕсли; 
			КонецЕсли;
			Если ОбновитьПробег Тогда 
				Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(ОбъектСсылка, СтруктураПараметров.Пробег, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег);
			КонецЕсли;
		КонецЕсли;
		
		// Госномер
		Если ЗначениеЗаполнено(СтруктураПараметров.Госномер) Тогда
			АвтомобильГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ОбъектСсылка, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
			ОбновитьГосНомер = Ложь;
			Если НЕ ЗначениеЗаполнено(АвтомобильГосНомер) Тогда
				ОбновитьГосНомер = Истина;
			Иначе
				Если АвтомобильГосНомер <> СтруктураПараметров.Госномер Тогда
					ОбновитьГосНомер = Истина;
				КонецЕсли; 
			КонецЕсли;
			Если ОбновитьГосномер Тогда
				Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(ОбъектСсылка, СтруктураПараметров.Госномер, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
			КонецЕсли;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли ТипЗНЧ(ОбъектСсылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		// реквизиты не обновляем, потому, что считаем, что на сайте заявки только создаются и не редактируются
		Возврат ИСТИНА;
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ПрефиксУзлаCML	   = "CMLУзел.";
ПрефиксАтрибутаCML = "CMLАтрибут.";
НачалоЭлементаCML  = "CMLНачалоЭлемента";
КонецЭлементаCML   = "CMLКонецЭлемента";

ПодкаталогКартинок 					   = "import_files";
ПодкаталогБезопасностиКаталогаВыгрузки = "1cbitrix";

ПараметрЗапросаHTTP_Инициализация       	  = "&mode=init";
ПараметрЗапросаHTTP_ПередачаФайла       	  = "&mode=file&filename=";
ПараметрЗапросаHTTP_ИмпортФайлаСервером		  = "&mode=import&filename=";
ПараметрЗапросаHTTP_ПолучитьДанные	    	  = "&mode=query"; 
ПараметрЗапросаHTTP_ПолнаяВыгрузка       	  = "&export=all"; 
ПараметрЗапросаHTTP_УспешноеЗавершениеИмпорта = "&mode=success";

ОтветСервера_ZIPРазрешен							= "zip=yes";
ОтветСервера_ОграничениеРазмераФрагментаФайлаОбмена = "file_limit=";
ОтветСервера_УспешноеЗавершениеТекущейОперации 		= "success";
ОтветСервера_АварийноеЗавершениеТекущейОперации		= "failure";
ОтветСервера_ВыполнениеТекущейОперации 				= "progress";

ПустаяХарактеристикаСсылка	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
НаименованиеНалога 			= "НДС";

НаименованиеКаталогаТоваровCML   = "Каталог товаров";
НаименованиеПакетаПредложенийCML = "Пакет предложений";

БулевоЗначениеCML_Истина 	= "true";
БулевоЗначениеCML_Да		= "Да";
БулевоЗначениеCML_Ложь 		= "false";
БулевоЗначениеCML_Нет		= "Нет";

ВидНоменклатурыCML_Услуга 	= "Услуга";
ВидНоменклатурыCML_Товар  	= "Товар";
ЗначениеCML_ТипНоменклатуры = "ТипНоменклатуры";

Если ( обЗначениеНеЗаполнено(мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом)) Тогда
	мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом = 1;
КонецЕсли;

ПостроительЗапроса	= Новый ПостроительЗапроса();
мПрефикс			= "";
ОшибкиHTMLем		= "";							
ОшибкиТекстом		= "";

ТаблицаФайловОбмена	= Новый ТаблицаЗначений();
ТаблицаФайловОбмена.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата",,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Дата");
ТаблицаФайловОбмена.Колонки.Добавить("Режим",Новый ОписаниеТипов("Строка",Новый КвалификаторыСтроки(20)),"Режим");
ТаблицаФайловОбмена.Колонки.Добавить("Содержание",Новый ОписаниеТипов("Строка"),"Содержание");

ТаблицаУслуг		= Новый ТаблицаЗначений();
ТаблицаУслуг.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументОбъект.ЗаказПокупателя"),"Документ");
ТаблицаУслуг.Колонки.Добавить("ВидДоставки", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"),"ВидДоставки");
ТаблицаУслуг.Колонки.Добавить("СтоимостьДоставки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный)),"СтоимостьДоставки");

ПредставленияВалют	= Новый ТаблицаЗначений();

Права				= Неопределено;

СообщениеОНевыгруженныхТоварах	= "";
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

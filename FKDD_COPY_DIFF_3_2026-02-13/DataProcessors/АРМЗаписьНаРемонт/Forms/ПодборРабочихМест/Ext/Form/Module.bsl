#Область СЛУЖЕБНЫЕ_ПРОЦЕДУРЫ_И_ФУНКЦИИ

// Формирует таблицу исполнителей и помещает ее во временное хранилище
//
&НаСервере
Функция ВыполнитьПодборИсполнителей()
	
	компоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	макетКомпоновки = компоновщикМакетаКомпоновкиДанных.Выполнить(
		ПолучитьИзВременногоХранилища(АдресСхемыКомпановкиДанных),
		КомпоновщикНастроекКомпоновкиДанных.Настройки,,,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	процессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	процессорКомпоновкиДанных.Инициализировать(макетКомпоновки);
	процессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	таблицаРезультат = Новый ТаблицаЗначений();
	процессорВывода.УстановитьОбъект(таблицаРезультат);
	
	процессорВывода.НачатьВывод();
	процессорВывода.Вывести(процессорКомпоновкиДанных, Истина);
	процессорВывода.ЗакончитьВывод();
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаРезультат.ВыгрузитьКолонку("Цех"));
	
КонецФункции

#КонецОбласти

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_КОМАНД_ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	Закрыть(ВыполнитьПодборИсполнителей());
	
КонецПроцедуры

#КонецОбласти

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	схемаКомпановкиДанных = Обработки.АРМЗаписьНаРемонт.ПолучитьМакет("СКД_ПодборРабочихМест");
	АдресСхемыКомпановкиДанных = ПоместитьВоВременноеХранилище(схемаКомпановкиДанных, УникальныйИдентификатор);
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпановкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпановкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти
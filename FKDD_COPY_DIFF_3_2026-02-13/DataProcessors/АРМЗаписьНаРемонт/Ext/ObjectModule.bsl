// Модуль обработки АРМЗаписьНаРемонт

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем Права Экспорт; 								// кэш прав пользователя

Перем ЦветаРаскраски Экспорт;	 					// цвета раскраски ячеек
Перем ОтступСлеваГоризонтально Экспорт; 			// отступ слева от левого верхнего угла поля до первой ячейки диаграммы
Перем ОтступСверхуГоризонтально Экспорт;			// отступ сверху от левого верхнего угла поля до первой ячейки диаграммы
Перем ОтступСлеваВертикально Экспорт; 				// отступ слева от левого верхнего угла поля до первой ячейки диаграммы
Перем ОтступСверхуВертикально Экспорт; 				// отступ сверху от левого верхнего угла поля до первой ячейки диаграммы
Перем ЛинияРамкиВыделенияРаботы Экспорт; 			// линия рамки выделения работы (сильное выделение)
Перем ЛинияРамкиВнутренняя; 						// линия рамки внутренняя
Перем ЛинияРамкиСплошная; 							// линия рамки сплошная
Перем ЛинияРамкиТочечная; 							// линия рамки 
Перем ЛинияРамкиДвойная; 							// линия рамки 

Перем МВТРабочиеМеста; 								// менеджер временных таблиц для рабочих мест
Перем ДлительностьИнтервала Экспорт; 				// длительность интервала
Перем ЧислоРабочихМест Экспорт; 					// число рабочих мест
Перем ЧислоИнтерваловВремени Экспорт; 				// число колонок таблицы расшифровок (длина диаграммы)
Перем ЧислоЯчеекДиаграммы Экспорт; 					// число рабочих мест и колонок УРВ (ширина диаграммы)
Перем ИсполнителиОтбор Экспорт; 					// содержит список значений исполнителей в результате применения отборов
Перем ЦехаОтбор Экспорт; 							// содержит список значений цехов в результате применения отборов
Перем УстановленОтборИсполнители Экспорт;			// флаг показывает, что на таблицу исполнителей наложен отбор
Перем УстановленОтборЦеха Экспорт;					// флаг показывает, что на таблицу цехов наложен отбор
Перем ТаблицаЗагрузкиЦехов Экспорт;					// таблица загрузки цехов для расчета показателей в режиме на месяц
Перем ТаблицаЗагрузкиИсполнителей Экспорт;			// таблица загрузки исполнителей для расчета показателей в режиме на месяц
Перем ТаблицаРабочихМест; 							// таблица ресурсов для отображения на диаграмме
Перем ГрафикиРабочихМест;							// таблица содержит кэш с данными регистра График работы календарный
Перем ТаблицаГраницГрафика; 						// таблица содержит дату начала и окончания дней календаря по графикам
Перем ДетальнаяТаблицаЗагрузкиРабочихМест;			// таблица загрузки рабочих мест для формирования диаграммы в режиме на день
Перем ТаблицаОформления; 							// таблица оформления
Перем ТаблицаЛегенды; 								// таблица оформления легенды
Перем ТаблицаРасшифровок Экспорт; 					// таблица расшифровок
Перем ТаблицаСмещений Экспорт; 						// таблица смещений
Перем ТаблицаПозицийРабочихМест Экспорт;			// таблица содержит исходную позицию колонки рабочего места на диаграмме 
Перем ТаблицаДанныхУРВ; 							// таблица фактически выполненных работ по данным подсистемы УРВ
Перем ТаблицаСостоянияЗаявок Экспорт; 				// таблица состояний заявок

// БИЗТЕХ КОВАЛЬ 12.09.2025 ++
Перем ТаблицаСуммМК Экспорт; 						// таблица сумм МК
// БИЗТЕХ КОВАЛЬ 12.09.2025 ++

Перем ТаблицаПорядкаРабочихМест Экспорт;			// таблица содержит рабочие места в том порядке, в котором их необходимо выводить

Перем КартинкаПоСостоянию Экспорт;					// Соответствие картинок к типу состояния ЗнР
Перем ПредставлениеСостояний Экспорт;				// Соответствие Представления к типу состояния ЗнР

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Сохраняет настройки АРМ в хранилище настроек 
//
Процедура СохранитьНастройкиОбщие() Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("БазовыйГрафик",                     БазовыйГрафик);
	СтруктураНастроек.Вставить("ИнтервалКалендаря",                 ИнтервалКалендаря);
	СтруктураНастроек.Вставить("Продолжительность",                 Продолжительность);
	СтруктураНастроек.Вставить("ШиринаКолонкиВертикально",          ШиринаКолонкиВертикально);
	СтруктураНастроек.Вставить("ШиринаКолонкиГоризонтально",        ШиринаКолонкиГоризонтально);
	СтруктураНастроек.Вставить("КритерийРасчетаЗагруженности",      КритерийРасчетаЗагруженности);
	СтруктураНастроек.Вставить("ИспользованиеГрафиковРабот",        ИспользованиеГрафиковРабот);
	СтруктураНастроек.Вставить("ИнтервалАвтоматическогоОбновления", ИнтервалАвтоматическогоОбновления);
	СтруктураНастроек.Вставить("ОтображатьТолькоРаботающих",        ОтображатьТолькоРаботающих);
	СтруктураНастроек.Вставить("ОтображатьЛегенду",                 ОтображатьЛегенду);
	СтруктураНастроек.Вставить("ОтображатьДанныеУРВ",               ОтображатьДанныеУРВ);
	СтруктураНастроек.Вставить("ФорматПредставленияЗаявкиНаРемонт", ФорматПредставленияЗаявкиНаРемонт);
	СтруктураНастроек.Вставить("РежимКалендаряПоУмолчанию",         РежимКалендаряПоУмолчанию);
	СтруктураНастроек.Вставить("РежимПланирования",                 РежимПланирования);
	СтруктураНастроек.Вставить("РежимДобавленияРаботы",             РежимДобавленияРаботы);
	СтруктураНастроек.Вставить("ПоворотКалендаря",                  ПоворотКалендаря);
	СтруктураНастроек.Вставить("ТаблицаПорядкаРабочихМест",         ТаблицаПорядкаРабочихМест);
	
	//БИЗТЕХ КОВАЛЬ 16.10.2024 ++
	СтруктураНастроек.Вставить("АвтоработаПриемки",         		АвтоработаПриемки);
	СтруктураНастроек.Вставить("АвтоработаВыдачи",         			АвтоработаВыдачи);
	СтруктураНастроек.Вставить("ВремяДляПриемки",         			ВремяДляПриемки);
	СтруктураНастроек.Вставить("ВремяДляВыдачи",         			ВремяДляВыдачи);
	//БИЗТЕХ КОВАЛЬ 16.10.2024 --
	
	ХранилищеСистемныхНастроек.Сохранить("Обработка.АРМЗаписьНаРемонт/Общие",, СтруктураНастроек);
	
КонецПроцедуры

// Восстанавливает настройки АРМ из хранилища настроек или устанавливает значения по умолчанию
//
Процедура ВосстановитьНастройкиОбщие(КлючОбъектаНастройки) Экспорт
	
	СохраненныеНастройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъектаНастройки);
	Если СохраненныеНастройки = Неопределено ИЛИ ТипЗнч(СохраненныеНастройки) <> Тип("Структура") Тогда
		СохраненныеНастройки = Новый Структура;
	КонецЕсли;
	
	НастройкиПоУмолчанию = Обработки.АРМЗаписьНаРемонт.ПолучитьСписокНастроекАРМ();
	Для Каждого ТекНастройка Из НастройкиПоУмолчанию Цикл
		Если СохраненныеНастройки.Свойство(ТекНастройка.Ключ) Тогда
			СохраненнаяНастройка = СохраненныеНастройки[ТекНастройка.Ключ];
		КонецЕсли;
		ЭтотОбъект[ТекНастройка.Ключ] = ?(ЗначениеЗаполнено(СохраненнаяНастройка),СохраненнаяНастройка,ТекНастройка.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Сохраняет настройки отборов в хранилище настроек 
//
Процедура СохранитьНастройкиОтборов(ОтборСтрокЦеха, ОтборСтрокИсполнители) Экспорт
	
	ТаблицаОтбора = Новый ТаблицаЗначений;
	ТаблицаОтбора.Колонки.Добавить("Использование");
	ТаблицаОтбора.Колонки.Добавить("ПутьКДанным");
	ТаблицаОтбора.Колонки.Добавить("ВидСравнения");
	ТаблицаОтбора.Колонки.Добавить("Значение");
	ТаблицаОтбора.Колонки.Добавить("ЗначениеС");
	ТаблицаОтбора.Колонки.Добавить("ЗначениеПо");
	
	Для Каждого ТекОтбор Из ОтборСтрокЦеха Цикл
		НовыйОтбор = ТаблицаОтбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйОтбор, ТекОтбор);
	КонецЦикла;                                                     
	ХранилищеСистемныхНастроек.Сохранить("Обработка.АРМЗаписьНаРемонт/ТаблицаОтборовЦеха",, ТаблицаОтбора);
	
	ТаблицаОтбора.Очистить();
	Для Каждого ТекОтбор Из ОтборСтрокИсполнители Цикл
		НовыйОтбор = ТаблицаОтбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйОтбор, ТекОтбор);
	КонецЦикла;                                                     
	ХранилищеСистемныхНастроек.Сохранить("Обработка.АРМЗаписьНаРемонт/ТаблицаОтборовИсполнители",, ТаблицаОтбора);
	
КонецПроцедуры

// Восстанавливает настройки отборов из хранилища настроек
//
Процедура ВосстановитьНастройкиОтборов() Экспорт
	
	ТаблицаОтбора = ХранилищеСистемныхНастроек.Загрузить("Обработка.АРМЗаписьНаРемонт/ТаблицаОтборовИсполнители");
	Если ТипЗнч(ТаблицаОтбора) = Тип("ТаблицаЗначений") И ТаблицаОтбора.Количество()>0 Тогда
		УстановленОтборИсполнители = Истина;	
		ИсполнителиОтбор = УстановитьОтборНаСписокИсполнителей(ТаблицаОтбора);
	Иначе
		УстановленОтборИсполнители = Ложь;	
		ИсполнителиОтбор = Новый СписокЗначений;
	КонецЕсли;
	
	ТаблицаОтбора = ХранилищеСистемныхНастроек.Загрузить("Обработка.АРМЗаписьНаРемонт/ТаблицаОтборовЦеха");
	Если ТипЗнч(ТаблицаОтбора) = Тип("ТаблицаЗначений") И ТаблицаОтбора.Количество()>0 Тогда
		УстановленОтборЦеха = Истина;	
		ЦехаОтбор = УстановитьОтборНаСписокЦехов(ТаблицаОтбора);
	Иначе
		УстановленОтборЦеха = Ложь;	
		ЦехаОтбор = Новый СписокЗначений;
	КонецЕсли;
	
КонецПроцедуры

// Накладывает отбор и формирует список исполнителей
// Возвращает Истина или Ложь в зависимости от результата установки отбора
// Параметры: 
//	ТаблицаОтбора - Таблица значений - исходные данные для отбора
Функция УстановитьОтборНаСписокИсполнителей(ТаблицаОтбора) Экспорт
	ПостроительОтчета = Новый ПостроительОтчета; 
	ПостроительОтчета.Текст = "ВЫБРАТЬ
	                          |	Сотрудники.Ссылка КАК Сотрудник,
	                          |	Сотрудники.Подразделение,
	                          |	Сотрудники.Должность,
	                          |	0 КАК НомерСтроки
	                          |ИЗ
	                          |	Справочник.Сотрудники КАК Сотрудники
	                          |ГДЕ
	                          |	Сотрудники.ЭтоГруппа = ЛОЖЬ
	                          |	И Сотрудники.ФлагУволен = ЛОЖЬ
	                          |	И Сотрудники.ПометкаУдаления = ЛОЖЬ
	                          |	И Сотрудники.Исполнитель = ИСТИНА";
	ПостроительОтчета.ЗаполнитьНастройки();

	Для Каждого ЭлементОтбора Из ТаблицаОтбора Цикл 
		Если ЭлементОтбора.Использование Тогда 
			НовыйОтбор = ПостроительОтчета.Отбор.Добавить(ЭлементОтбора.ПутьКДанным); 
			НовыйОтбор.Использование = ЭлементОтбора.Использование; 
			НовыйОтбор.ВидСравнения = ЭлементОтбора.ВидСравнения;
			НовыйОтбор.ЗначениеС = ЭлементОтбора.ЗначениеС; 
			НовыйОтбор.ЗначениеПо = ЭлементОтбора.ЗначениеПо; 
			НовыйОтбор.Значение = ЭлементОтбора.Значение; 
		КонецЕсли; 
	КонецЦикла; 

	ПостроительОтчета.Выполнить();
	Результат = ПостроительОтчета.Результат.Выгрузить();
	Возврат Результат;
	
КонецФункции //УстановитьОтборНаСписокИсполнителей()

// Накладывает отбор и формирует список цехов
// Возвращает Истина или Ложь в зависимости от результата установки отбора
// Параметры: 
//	ТаблицаОтбора - Таблица значений - исходные данные для отбора 
Функция УстановитьОтборНаСписокЦехов(ТаблицаОтбора) Экспорт
	ПостроительОтчета = Новый ПостроительОтчета; 
	ПостроительОтчета.Текст = "ВЫБРАТЬ
	                          |	Цеха.Ссылка КАК Цех,
	                          |	Цеха.Подразделение,
	                          |	0 КАК НомерСтроки,
	                          |	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, ЗНАЧЕНИЕ(Перечисление.ВидыИспользованияРабочихМест.ПустаяСсылка)) КАК ВидИспользованияРабочегоМеста
	                          |ИЗ
	                          |	Справочник.Цеха КАК Цеха
	                          |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	                          |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                          |ПО
	                          |	ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.ВидИспользованияРабочегоМеста) И 
	                          |	Цеха.Ссылка = ЗначенияСвойствОбъектов.Объект
	                          |ГДЕ
	                          |	Цеха.ПометкаУдаления = ЛОЖЬ";
	ПостроительОтчета.ЗаполнитьНастройки();

	Для Каждого ЭлементОтбора Из ТаблицаОтбора Цикл 
		Если ЭлементОтбора.Использование Тогда 
			НовыйОтбор = ПостроительОтчета.Отбор.Добавить(ЭлементОтбора.ПутьКДанным); 
			НовыйОтбор.Использование = ЭлементОтбора.Использование; 
			НовыйОтбор.ВидСравнения = ЭлементОтбора.ВидСравнения;
			НовыйОтбор.ЗначениеС = ЭлементОтбора.ЗначениеС; 
			НовыйОтбор.ЗначениеПо = ЭлементОтбора.ЗначениеПо; 
			НовыйОтбор.Значение = ЭлементОтбора.Значение; 
		КонецЕсли; 
	КонецЦикла; 

	ПостроительОтчета.Выполнить();
	Результат = ПостроительОтчета.Результат.Выгрузить();
	Возврат Результат;
	
КонецФункции //УстановитьОтборНаСписокЦехов()

// Возвращает цвет текущей ячейки УРВ 
//
Функция ПолучитьЦветОформленияЯчейкиУРВ(ДанныеОсновнойЯчейки,ДанныеЯчейкиУРВ)

	ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");;
	
	Если ТипЗнч(ДанныеЯчейкиУРВ) = Тип("СписокЗначений") Тогда
		
		Если ДанныеЯчейкиУРВ.Количество() > 1 Тогда
			ДанныеТекущейЯчейкиУРВ = ДанныеЯчейкиУРВ[ДанныеЯчейкиУРВ.Количество() - 1];
		Иначе
			ДанныеТекущейЯчейкиУРВ = ДанныеЯчейкиУРВ[0];
		КонецЕсли;
		ДанныеТекущейЯчейкиУРВ = ДанныеТекущейЯчейкиУРВ.Значение;
		
		Если ТипЗнч(ДанныеОсновнойЯчейки) = Тип("СписокЗначений") Тогда
			
			ЗаявкаПлан = ДанныеОсновнойЯчейки[0].Значение;
			
			ЗаявкаФакт = Неопределено;
			Если ТипЗнч(ДанныеТекущейЯчейкиУРВ) = Тип("Массив") Тогда
				ЗаявкаФакт  = ДанныеТекущейЯчейкиУРВ[4];
			КонецЕсли;
			                       
			Если ЗаявкаПлан = ЗаявкаФакт Тогда
				ОтметкаФакт = ДанныеТекущейЯчейкиУРВ[0];
				Если ТипЗнч(ОтметкаФакт) = Тип("СправочникСсылка.ВидыОтметокВремени") Тогда
					Цвет = ОтметкаФакт.Цвет.Получить();
					ЦветФона = ?(Цвет <> Неопределено,Цвет,WebЦвета.Белый);
				КонецЕсли;
			Иначе
				Цвет = Справочники.ВидыОтметокВремени.ОтклонениеОтПлана.Цвет.Получить();
				ЦветФона = ?(Цвет <> Неопределено,Цвет,WebЦвета.Белый);
			КонецЕсли;
			
		ИначеЕсли ДанныеОсновнойЯчейки  = "СвободнаяЯчейка" Тогда
			
			ОтметкаФакт = ДанныеТекущейЯчейкиУРВ[0];
			Если ТипЗнч(ОтметкаФакт) = Тип("СправочникСсылка.ВидыОтметокВремени") Тогда
				Цвет = ОтметкаФакт.Цвет.Получить();
				ЦветФона = ?(Цвет <> Неопределено,Цвет,WebЦвета.Белый);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЦветФона;
	
КонецФункции

// Возвращает текст текущей ячейки УРВ 
//
Функция ПолучитьТекстОформленияЯчейкиУРВ(ДанныеЯчейкиУРВ)

	Если ТипЗнч(ДанныеЯчейкиУРВ) = Тип("СписокЗначений") Тогда
		
		Если ДанныеЯчейкиУРВ.Количество() > 1 Тогда
			ДанныеТекущейЯчейкиУРВ = ДанныеЯчейкиУРВ[ДанныеЯчейкиУРВ.Количество() - 1];
		Иначе
			ДанныеТекущейЯчейкиУРВ = ДанныеЯчейкиУРВ[0];
		КонецЕсли;
		ДанныеТекущейЯчейкиУРВ = ДанныеТекущейЯчейкиУРВ.Значение;
		
		Если ТипЗнч(ДанныеТекущейЯчейкиУРВ) = Тип("Массив") Тогда
			Возврат ДанныеТекущейЯчейкиУРВ[5];
		КонецЕсли;
			                       
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Выполняет проверку пересечения по времени работ из заявки в пределах указанной ячейки
// Возвращает Истина или Ложь в зависимости от того, есть ли хоть одно пересечение или нет
// Параметры: 
//	СписокЗаявок - Список значений - Ссылки на документы 
//	СмещениеСтроки - Число - Смещение строки в таблице смещений 
//	СмещениеКолонки - Число - Смещение колонки в таблице смещений 
Функция ПроверитьПересечениеЗаявок(СписокЗаявок,СмещениеСтроки,СмещениеКолонки) 
	Отбор = Новый Структура();
	Отбор.Вставить("СмещениеКолонки",СмещениеКолонки);
	Отбор.Вставить("СмещениеСтроки",СмещениеСтроки);
	НайденныеСтроки = ТаблицаСмещений.НайтиСтроки(Отбор);
	ЕстьПересечение = Ложь;
	Для Индекс1 = 0 По НайденныеСтроки.Количество()-2 Цикл
		Для Индекс2 = Индекс1+1 По НайденныеСтроки.Количество()-1 Цикл
			Работа1 = ДетальнаяТаблицаЗагрузкиРабочихМест.Найти(НайденныеСтроки[Индекс1].ИдентификаторРаботы,"ИдентификаторРаботы");
			Работа2 = ДетальнаяТаблицаЗагрузкиРабочихМест.Найти(НайденныеСтроки[Индекс2].ИдентификаторРаботы,"ИдентификаторРаботы");
			Если (Работа1.НачалоВыполнения < Работа2.ОкончаниеВыполнения) И (Работа1.ОкончаниеВыполнения > Работа2.НачалоВыполнения) Тогда
		   		ЕстьПересечение = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Возврат ЕстьПересечение;	
КонецФункции //ПроверитьПересечениеЗаявок()

// Формирует массив псевдонимов рабочих мест  
// Возвращает массив
Функция ПолучитьМассивПредставленийРабочихМест()
	МассивПредставлений = Новый Массив;
	Для СчетчикРабочихМест = 1 По ЧислоРабочихМест Цикл
		
		Исполнитель = ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто;
		Должность = "";
		
		Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
			Если ЗначениеЗаполнено(Исполнитель.Должность) Тогда
				Должность = Исполнитель.Должность;
				Если ЗначениеЗаполнено(Исполнитель.Должность.Псевдоним) Тогда
					Должность = Исполнитель.Должность.Псевдоним;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(Исполнитель.Псевдоним) Тогда
				Исполнитель = Исполнитель.Псевдоним;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Должность) Тогда
			МассивПредставлений.Добавить(Строка(Исполнитель) + Символы.ПС + "(" + Строка(Должность) + ")");
		Иначе
			МассивПредставлений.Добавить(Исполнитель);
		КонецЕсли;
	КонецЦикла;	
	Возврат МассивПредставлений;
КонецФункции //ПолучитьМассивПредставленийРабочихМест()			

// Возвращает длину самой длинной строки в переданном массиве строк
Функция ПолучитьМаксимальнуюДлинуСтрокиВМассиве(МассивСтрок)
	МаксДлина = 0;
	Для Каждого ТекСтрока из МассивСтрок Цикл
		
		Буфер = СокрЛ(ТекСтрока);
		ДлинаБуфера = СтрДлина(ТекСтрока);
	    ПозицияПереноса = Найти(Буфер, Символы.ПС);

	    Если ПозицияПереноса = 0 Тогда
			МаксДлина = Макс(МаксДлина,ДлинаБуфера);
		Иначе
		    Стр1 = СокрЛП(Лев(Буфер, ПозицияПереноса));
			МаксДлина = Макс(МаксДлина,СтрДлина(Стр1));
			Стр2 = СокрЛП(Прав(Буфер, ДлинаБуфера-ПозицияПереноса));
			МаксДлина = Макс(МаксДлина,СтрДлина(Стр2));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МаксДлина;
КонецФункции			

// Формирует представление документа "Заявка на ремонт"
// Возвращает строку - представление документа
// Параметры:
//  ДокументСсылка 		- ссылка на документ,
//  ФорматСтроки: 
//		   Г - гос. номер (представление автомобиля), 
//		   Н - номер документа, 
//         Д - дата документа, 
//         З - заказчик (наименование), 
//         П - первые 10 символов причины обращения, 
//         М - мастер (наименование)
// 		   Любой другой символ считается разделителем
Функция ПолучитьПредставлениеЗаявкиНаРемонт(ДокументСсылка, ФорматСтроки = "") Экспорт
	
	ВидДокумента = "";
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ВидДокумента = "ЗН";	
	Иначе
		ВидДокумента = "ЗР";	
	КонецЕсли;
	
	Если ДокументСсылка=Неопределено ИЛИ НЕ ЗначениеЗаполнено(ДокументСсылка.Ссылка) Тогда
		Возврат "Новая заявка на ремонт";
	КонецЕсли;	
	
	Если ПустаяСтрока(ФорматСтроки) Тогда
		Возврат Строка(ДокументСсылка);
	КонецЕсли;
	
	ФорматДляРазбора = ФорматСтроки;
	
	СтрПредставление = "";
	
	Пока СтрДлина(ФорматДляРазбора) > 0 Цикл
		
		ТекущийСимвол = Лев(ФорматДляРазбора,1);
		Если СтрДлина(ФорматДляРазбора) > 1 Тогда
			ФорматДляРазбора = Сред(ФорматДляРазбора,2,СтрДлина(ФорматДляРазбора)-1);
		Иначе
			ФорматДляРазбора = "";
		КонецЕсли;
		     
		Если ТекущийСимвол = "&" Тогда 
			ТекущийСимвол = Лев(ФорматДляРазбора,1);
			Если СтрДлина(ФорматДляРазбора) > 1 Тогда
				ФорматДляРазбора = Сред(ФорматДляРазбора,2,СтрДлина(ФорматДляРазбора)-1);
			Иначе
				ФорматДляРазбора = "";
			КонецЕсли;
		Иначе
			Если НЕ (ТекущийСимвол = Символы.ПС И Прав(СтрПредставление,1) = Символы.ПС) Тогда
				стрПредставление = стрПредставление + ТекущийСимвол;
			КонецЕсли;
			Продолжить;
		КонецЕсли;

		Если ТекущийСимвол = "Г" Тогда
			Фрагмент = "";
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
				ГосНомер =  РегистрыСведений.Автомобили.ПолучитьПоследнее(ДокументСсылка.Дата,Новый Структура("Автомобиль,ВидЗначения",ДокументСсылка.Автомобиль,Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер)).Значение;
				Если ТипЗнч(ГосНомер) = Тип("Строка") И Не ПустаяСтрока(ГосНомер) Тогда
					Фрагмент = СокрЛП(ГосНомер);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
				Фрагмент = СокрЛП(ДокументСсылка.ГосНомер);	
			КонецЕсли;
			стрПредставление = стрПредставление + Фрагмент;
		ИначеЕсли ТекущийСимвол = "Т" Тогда
			Фрагмент = "";
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
				Телефоны = киПолучитьСписокТелефоновОбъекта(ДокументСсылка.Заказчик);
				Если Телефоны<>Неопределено И Телефоны.Количество()>0 Тогда
					Фрагмент = Телефоны[0].Значение.ПредставлениеТелефона;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
				Фрагмент = СокрЛП(ДокументСсылка.ПредставлениеТелефона);	
			КонецЕсли;
			стрПредставление = стрПредставление + Фрагмент;
		ИначеЕсли ТекущийСимвол = "А" Тогда	
			Фрагмент = "";
			Если ТипЗнч(ДокументСсылка.Автомобиль) = Тип("СправочникСсылка.Модели") Тогда
				Фрагмент = СокрЛП(ДокументСсылка.Автомобиль.Наименование);	
			ИначеЕсли ТипЗнч(ДокументСсылка.Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда	
				Фрагмент = СокрЛП(ДокументСсылка.Автомобиль.Модель.Наименование);	
			КонецЕсли;
			стрПредставление = стрПредставление + Фрагмент;
		ИначеЕсли ТекущийСимвол = "З" Тогда	
			Фрагмент = "";
			Если ТипЗнч(ДокументСсылка.Заказчик) = Тип("Строка") Тогда
				Фрагмент = СокрЛП(ДокументСсылка.Заказчик);
			ИначеЕсли ТипЗнч(ДокументСсылка.Заказчик) = Тип("СправочникСсылка.Контрагенты") Тогда
				Фрагмент = СокрЛП(ДокументСсылка.Заказчик.Наименование);	
			КонецЕсли;
			стрПредставление = стрПредставление + Фрагмент;
		ИначеЕсли ТекущийСимвол = "Н" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.Номер), СокрЛП(ДокументСсылка.Номер), "");
		ИначеЕсли ТекущийСимвол = "Д" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.Дата), Формат(ДокументСсылка.Дата,"ДЛФ=Д"), "");
		ИначеЕсли ТекущийСимвол = "П" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.ПричинаОбращения), СокрЛП(ДокументСсылка.ПричинаОбращения), "");
		ИначеЕсли ТекущийСимвол = "М"  Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.Мастер),СокрЛП(ДокументСсылка.Мастер.Наименование),"");
		ИначеЕсли ТекущийСимвол = "И" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.Диспетчер),СокрЛП(ДокументСсылка.Диспетчер.Наименование),"");
		ИначеЕсли ТекущийСимвол = "Р" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.ВидРемонта),СокрЛП(ДокументСсылка.ВидРемонта.Наименование),"");
		ИначеЕсли ТекущийСимвол = "Ч" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.ДатаНачала), Формат(ДокументСсылка.ДатаНачала,"ДЛФ=DT"), "");
		ИначеЕсли ТекущийСимвол = "О" Тогда	
			стрПредставление = стрПредставление + ?(ЗначениеЗаполнено(ДокументСсылка.ДатаОкончания), Формат(ДокументСсылка.ДатаОкончания,"ДЛФ=DT"), "");
		Иначе
			Если НЕ (ТекущийСимвол = Символы.ПС И Прав(СтрПредставление,1) = Символы.ПС) Тогда
				стрПредставление = стрПредставление + ТекущийСимвол;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Возврат ВидДокумента + " " + стрПредставление;
	
КонецФункции //ПолучитьПредставлениеЗаявкиНаРемонт()

// Заполнение структуры цветов
//
Процедура ПолучитьЦветаРаскраски() Экспорт
	ЦветаРаскраски = Новый Соответствие;
	МакетОформления = Справочники.ГрафикиРаботы.ПолучитьМакет("МакетОформления");
	ОбластьОформления = МакетОформления.ПолучитьОбласть("Оформление");
	Для ПеременнаяЦикла = 1 По ОбластьОформления.ВысотаТаблицы Цикл
		ТекущаяОбласть = ОбластьОформления.Область(ПеременнаяЦикла, 1);
		Если обЗначениеНеЗаполнено(ТекущаяОбласть.Текст) Тогда
			Продолжить;
		КонецЕсли;
		ЦветаРаскраски.Вставить("Цвет" + ТекущаяОбласть.Текст, ТекущаяОбласть.ЦветФона);
	КонецЦикла;
	ЦветаРаскраски.Вставить("ЦветВыделенияДокумента",  Новый Цвет(211,217,239)); 
	ЦветаРаскраски.Вставить("ЦветВыделенияРаботы",  Новый Цвет(83,106,194));  
	ЦветаРаскраски.Вставить("ЦветРамкиСтандарт", ЦветаСтиля.ЦветРамки);
КонецПроцедуры // ЗаполнитьСтруктуруЦветов()

// Произвольное упорядочивание исполнителей и цехов из настроек
//
Процедура УстановитьПорядокРабочихМестИзНастроек()
	
	// Упорядочим так, как запомнили в настройках
	Если НЕ ТаблицаПорядкаРабочихМест = Неопределено И ТаблицаПорядкаРабочихМест.Количество() <> 0 Тогда
		КопияДетальнаяТаблицаЗагрузкиРабочихМест = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
		КопияДетальнаяТаблицаЗагрузкиРабочихМест.Очистить();
		Для Каждого РабочееМесто Из ТаблицаПорядкаРабочихМест Цикл
			СтараяСтрока = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Новый Структура("РабочееМесто", РабочееМесто));
			Если НЕ СтараяСтрока = Неопределено И СтараяСтрока.Количество() > 0 Тогда
				Для Каждого НайденнаяСтрока Из СтараяСтрока Цикл
					НоваяСтрока = КопияДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		// lismak - Отключено, но возможно еще понадобится
		//Для Каждого Строка Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		//	НайденнаяСтрока = КопияДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Новый Структура("РабочееМесто", Строка.РабочееМесто));
		//	Если НайденнаяСтрока = Неопределено ИЛИ НайденнаяСтрока.Количество() = 0 Тогда
		//		НоваяСтрока = КопияДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
		//		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		//	КонецЕсли;
		//КонецЦикла;
		ДетальнаяТаблицаЗагрузкиРабочихМест = КопияДетальнаяТаблицаЗагрузкиРабочихМест;
	КонецЕсли;
	
КонецПроцедуры

// БИЗТЕХ КОВАЛЬ 13.09.2024
// Произвольное упорядочивание исполнителей и цехов по группам 
//
Процедура УстановитьПорядокРабочихМестПоГруппам()
	
	//Получим список групп
	МассивГрупп = новый Массив;
	Для инд = 0 по Перечисления.ГруппыДляОтображенияВАРМ.Количество()-1 Цикл
		МассивГрупп.Добавить(Перечисления.ГруппыДляОтображенияВАРМ.Получить(инд));
	КонецЦикла; 
	
	//Добавим людей без групп
	МассивГрупп.Добавить(Перечисления.ГруппыДляОтображенияВАРМ.ПустаяСсылка());
	
	
	// Упорядочим так, как запомнили в настройках
	Если НЕ ТаблицаПорядкаРабочихМест = Неопределено И ТаблицаПорядкаРабочихМест.Количество() <> 0 Тогда
		КопияДетальнаяТаблицаЗагрузкиРабочихМест = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
		//Извлечем исполнителей
		КопияДетальнаяТаблицаЗагрузкиРабочихМест.Свернуть("РабочееМесто");
		Исполнители = КопияДетальнаяТаблицаЗагрузкиРабочихМест.ВыгрузитьКолонку("РабочееМесто");
		КопияДетальнаяТаблицаЗагрузкиРабочихМест = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
		КопияДетальнаяТаблицаЗагрузкиРабочихМест.Очистить();
		
		//Получим группы
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Ссылка,
			|	Сотрудники.ГруппаДляОтображенияВАРМ КАК ГруппаДляОтображенияВАРМ
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка В(&Исполнители)";

		Запрос.УстановитьПараметр("Исполнители", Исполнители);
		Исполнители = Запрос.Выполнить().Выгрузить();

	
		Для Каждого Группа Из МассивГрупп Цикл
			ИсполнителиГруппы = Исполнители.НайтиСтроки(Новый Структура("ГруппаДляОтображенияВАРМ", Группа));
			Для Каждого Исполнитель из ИсполнителиГруппы Цикл
				СтараяСтрока = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Новый Структура("РабочееМесто", Исполнитель.Ссылка));
				Если НЕ СтараяСтрока = Неопределено И СтараяСтрока.Количество() > 0 Тогда
					Для Каждого НайденнаяСтрока Из СтараяСтрока Цикл
						НоваяСтрока = КопияДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
					КонецЦикла;
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;
		ДетальнаяТаблицаЗагрузкиРабочихМест = КопияДетальнаяТаблицаЗагрузкиРабочихМест;
	КонецЕсли;
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДИАГРАММЫ В РЕЖИМЕ ДЕНЬ

// Формирует табличный документ календаря в режиме на день
//
Процедура СформироватьТабличныйДокументКалендарьНаДень(ДокументРезультат,ТекущаяЗаявкаНаРемонт=Неопределено,Планирование=Неопределено) Экспорт
	
	ДлительностьИнтервала = ИнтервалКалендаря*60;
	
	МВТРабочиеМеста = Новый МенеджерВременныхТаблиц;
	Если РежимКалендаря = 1 Тогда
		ТаблицаРабочихМест = ЗаполнитьНастройкиГрафиковРаботДляЦехов(); 
	ИначеЕсли РежимКалендаря = 2 Тогда
		ТаблицаРабочихМест = ЗаполнитьНастройкиГрафиковРаботДляИсполнителей(); 
	КонецЕсли;
	
	СформироватьГрафикиРабочихМест();
	
	Если РежимКалендаря = 1 Тогда
		СформироватьДетальнуюТаблицуЗагрузкиДляЦехов(ТекущаяЗаявкаНаРемонт,Планирование); 
	ИначеЕсли РежимКалендаря = 2 Тогда
		СформироватьДетальнуюТаблицуЗагрузкиДляИсполнителей(ТекущаяЗаявкаНаРемонт,Планирование); 
	КонецЕсли;
	
	КопияДетальнойТаблицыЗагрузки = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
	КопияДетальнойТаблицыЗагрузки.Свернуть("РабочееМесто");
	ЧислоРабочихМест = КопияДетальнойТаблицыЗагрузки.Количество();

	Если ЗапретРедактированияУРВ Тогда
		СформироватьТаблицуСостояниеЗаявок();	
	Иначе
		СформироватьТаблицуСостояниеЗаявокВключаяУРВ();	
	КонецЕсли;
	
	СформироватьТаблицуГраницГрафика();
	
	СформироватьТаблицуРасшифровок();	
	
	Если ОтображатьДанныеУРВ > 0 Тогда
		СформироватьТаблицаДанныхУРВ();
	КонецЕсли;
	
	СформироватьТаблицуПозицийРабочихМест();
	
	СформироватьТаблицуСмещений();
	
	СформироватьТаблицуОформления();
	
	//БИЗТЕХ КОВАЛЬ 12.09.2025 ++
	СформироватьТаблицуСуммМК();
	//БИЗТЕХ КОВАЛЬ 12.09.2025 --
	
	Если ПоворотКалендаря = 1 Тогда
		СформироватьМакетВертикально(ДокументРезультат);
	Иначе
		СформироватьМакетГоризонтально(ДокументРезультат);
	КонецЕсли;
	
	Если ОтображатьЛегенду = 1 Тогда
		СформироватьМакетЛегенды(ДокументРезультат);
	КонецЕсли;
	
	ДокументРезультат.ФиксацияСверху = ?(ПоворотКалендаря = 1,ОтступСверхуВертикально,ОтступСверхуГоризонтально);
	ДокументРезультат.ФиксацияСлева = ?(ПоворотКалендаря = 1,ОтступСлеваВертикально,ОтступСлеваГоризонтально);
	
	Если МВТРабочиеМеста <> Неопределено Тогда
		МВТРабочиеМеста.Закрыть();
	КонецЕсли;
	
КонецПроцедуры             

// Формирует настройки графиков работ для цехов
//
Функция ЗаполнитьНастройкиГрафиковРаботДляЦехов()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	
	Если УстановленОтборЦеха И ТипЗнч(ЦехаОтбор) = Тип("ТаблицаЗначений") Тогда
		ЕстьОтбор = Истина;
		Запрос.УстановитьПараметр("Цеха", ЦехаОтбор.ВыгрузитьКолонку("Цех"));
	Иначе
		ЕстьОтбор = Ложь;
	КонецЕсли;
	
	// Получим список рабочих мест
	Запрос.Текст = "ВЫБРАТЬ
	               |	Цеха.Ссылка КАК Ссылка,
	               |	Цеха.ГрафикРаботы КАК ГрафикРаботы,
	               |	ЗначенияСвойствОбъектов.Значение КАК ВидИспользованияРабочегоМеста,
	               |	ВЫБОР
	               |		КОГДА ЗначенияСвойствОбъектов.Значение = ЗНАЧЕНИЕ(Перечисление.ВидыИспользованияРабочихМест.ПланированиеПриемкиВыдачи)
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК КатегорияУпорядочивания
	               |ПОМЕСТИТЬ Цеха
	               |ИЗ
	               |	Справочник.Цеха КАК Цеха
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	               |		ПО (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.ВидИспользованияРабочегоМеста))
	               |			И Цеха.Ссылка = ЗначенияСвойствОбъектов.Объект
	               |ГДЕ
	               |	Цеха.ПометкаУдаления = ЛОЖЬ" + ?(ЕстьОтбор, " И 
	               |	Цеха.Ссылка В (&Цеха)", "")+"
	               |;
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Цеха.Ссылка КАК Ссылка,
				   |	Цеха.ВидИспользованияРабочегоМеста,
	               |	Цеха.ГрафикРаботы КАК ГрафикРаботы
	               |ИЗ
	               |	Цеха КАК Цеха
	               |ГДЕ
	               |	Цеха.ВидИспользованияРабочегоМеста <> ЗНАЧЕНИЕ(Перечисление.ВидыИспользованияРабочихМест.НеУчаствуетВПланировании)
	               |";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции //ЗаполнитьНастройкиГрафиковРаботДляЦехов()

// Формирует настройки графиков работ для исполнителей
//
Функция ЗаполнитьНастройкиГрафиковРаботДляИсполнителей()
	
	// Получим список рабочих мест
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	
	Если УстановленОтборИсполнители И ТипЗнч(ИсполнителиОтбор) = Тип("ТаблицаЗначений") Тогда
		ЕстьОтбор = Истина;
		Запрос.УстановитьПараметр("Сотрудники", ИсполнителиОтбор.ВыгрузитьКолонку("Сотрудник"));
	Иначе
		ЕстьОтбор = Ложь;
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Сотрудники.Ссылка,
	               |	Сотрудники.ГрафикРаботы,
	               |	ВЫБОР
	               |		КОГДА Сотрудники.Псевдоним = """"
	               |			ТОГДА Сотрудники.Наименование
	               |		ИНАЧЕ Сотрудники.Псевдоним
	               |	КОНЕЦ КАК СотрудникПсевдоним,
	               |	ВЫБОР
	               |		КОГДА Сотрудники.Должность.Псевдоним = """"
	               |			ТОГДА Сотрудники.Должность.Наименование
	               |		ИНАЧЕ Сотрудники.Должность.Псевдоним
	               |	КОНЕЦ КАК ДолжностьПсевдоним,
	               |	ВЫБОР
	               |		КОГДА Сотрудники.Ссылка.Должность = ЗНАЧЕНИЕ(Справочник.Должности.Диспетчер)
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК КатегорияУпорядочивания
	               |ПОМЕСТИТЬ Мастера
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |ГДЕ
	               |	Сотрудники.Исполнитель = ИСТИНА
	               |	И Сотрудники.ФлагУволен = ЛОЖЬ
	               |	И Сотрудники.ПометкаУдаления = ЛОЖЬ" + ?(ЕстьОтбор, " И 
	               |	Сотрудники.Ссылка В (&Сотрудники)", "")+"
	               |;
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Мастера.Ссылка КАК Ссылка,
	               |	Мастера.ГрафикРаботы КАК ГрафикРаботы
	               |ИЗ
	               |	Мастера КАК Мастера
	               |";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции //ЗаполнитьНастройкиГрафиковРаботДляИсполнителей()

// Формирует таблицу графиков рабочих мест по данным календарного графика работы
//
Процедура СформироватьГрафикиРабочихМест(НаМесяц=Ложь)
	
	ИспользованиеБазовогоГрафика = Истина;
	
	КопияТаблицыРабочихМест = ТаблицаРабочихМест.Скопировать();
	КопияТаблицыРабочихМест.Свернуть("ГрафикРаботы");
	МассивГрафиков = КопияТаблицыРабочихМест.ВыгрузитьКолонку("ГрафикРаботы");
	Если МассивГрафиков.Найти(БазовыйГрафик) = Неопределено Тогда
		МассивГрафиков.Добавить(БазовыйГрафик);
		ИспользованиеБазовогоГрафика = Ложь;
	КонецЕсли;
	
	ГрафикиРабочихМест = Неопределено;
	ГрафикиРабочихМест = Новый ТаблицаЗначений;
	Для Каждого ТекГрафик Из МассивГрафиков Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекГрафик) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ГрафикиРабочихМест.Количество() > 0  Тогда
			Отбор = Новый Структура("ГрафикРаботы",ТекГрафик);
			Строки = ГрафикиРабочихМест.НайтиСтроки(Отбор);
			Если Строки.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
		Если НаМесяц Тогда
			ДанныеТаблицыГрафика = кпПолучитьГрафик(ТекГрафик, НачалоМесяца(ДатаОтчета), КонецМесяца(ДатаОтчета));
		Иначе
			ДанныеТаблицыГрафика = кпПолучитьГрафик(ТекГрафик, НачалоНедели(ДатаОтчета), КонецНедели(ДатаОтчета));
		КонецЕсли;
		Если ДанныеТаблицыГрафика.Количество() <> 0 Тогда
			Если ГрафикиРабочихМест.Количество() = 0 Тогда
				ГрафикиРабочихМест = ДанныеТаблицыГрафика.СкопироватьКолонки();
				ГрафикиРабочихМест.Колонки.Добавить("ГрафикРаботы",Новый ОписаниеТипов("СправочникСсылка.ГрафикиРаботы"));
				ГрафикиРабочихМест.Колонки.Добавить("ИспользованиеГрафика",Новый ОписаниеТипов("Булево"));
			КонецЕсли;
			Для Каждого ТекДанныеТаблицыГрафика Из ДанныеТаблицыГрафика Цикл
			 	НоваяСтрока = ГрафикиРабочихМест.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекДанныеТаблицыГрафика);
				НоваяСтрока.ГрафикРаботы = ТекГрафик;
				НоваяСтрока.ИспользованиеГрафика = Истина;
				Если ТекГрафик = БазовыйГрафик Тогда
					НоваяСтрока.ИспользованиеГрафика = ИспользованиеБазовогоГрафика;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры //СформироватьГрафикиРабочихМест()

// Формирует таблицу границ рабочих дней
//
Процедура СформироватьТаблицуГраницГрафика() 
	
	ТаблицаГраницГрафика = Новый ТаблицаЗначений;
	ТаблицаГраницГрафика.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаГраницГрафика.Колонки.Добавить("НачалоДня", Новый ОписаниеТипов("Дата"));
	ТаблицаГраницГрафика.Колонки.Добавить("КонецДня", Новый ОписаниеТипов("Дата"));
	
	Для СчетчикДней = 0 По Продолжительность-1 Цикл	
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Добавить();
		СтрокаТаблицыГраниц.Дата = ДатаОтчета+24*60*60*(СчетчикДней);
		СтрокаТаблицыГраниц.НачалоДня = '00010101';
		СтрокаТаблицыГраниц.КонецДня = '00010101';
		
		Если ИспользованиеГрафиковРабот > 0 И ГрафикиРабочихМест.Количество()>0 Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("Дата",НачалоДня(ДатаОтчета+60*60*24*СчетчикДней));
			//Отбор.Вставить("ИспользованиеГрафика",Истина);
			Если ИспользованиеГрафиковРабот = 1 Тогда
				Отбор.Вставить("ГрафикРаботы",БазовыйГрафик);
			КонецЕсли;
			
			НайденныеСтрокиГрафика = ГрафикиРабочихМест.НайтиСтроки(Отбор);
			КопияТаблицыГрафиковРабот = ГрафикиРабочихМест.Скопировать(НайденныеСтрокиГрафика);
			
			Если КопияТаблицыГрафиковРабот.Количество() > 0 Тогда
				//исключим из рассмотрения строки, где время не заполнено,
				//для этого время начала установим равным концу дня
				Для Каждого СтрокаГрафика Из КопияТаблицыГрафиковРабот Цикл
					Если СтрокаГрафика.НачалоРабочегоВремени = '00010101' И  СтрокаГрафика.КонецРабочегоВремени = '00010101' Тогда
						СтрокаГрафика.НачалоРабочегоВремени =  КонецДня(СтрокаГрафика.НачалоРабочегоВремени);
					КонецЕсли;
				КонецЦикла;
				
				//нижняя граница (начало дня)
				КопияТаблицыГрафиковРабот.Сортировать("Дата ВОЗР, НачалоРабочегоВремени ВОЗР");
				ГрафикДатаНачалоПоГрафику = КопияТаблицыГрафиковРабот[0].Дата + Час(КопияТаблицыГрафиковРабот[0].НачалоРабочегоВремени)*60*60 + Минута(КопияТаблицыГрафиковРабот[0].НачалоРабочегоВремени)*60 + Секунда(КопияТаблицыГрафиковРабот[0].НачалоРабочегоВремени);
				Если СтрокаТаблицыГраниц.НачалоДня = '00010101' ИЛИ ГрафикДатаНачалоПоГрафику < СтрокаТаблицыГраниц.НачалоДня Тогда
					СтрокаТаблицыГраниц.НачалоДня = ГрафикДатаНачалоПоГрафику;	
				КонецЕсли;
				
				//верхняя граница (конец дня)
				КопияТаблицыГрафиковРабот.Сортировать("Дата УБЫВ,КонецРабочегоВремени УБЫВ");
				ГрафикДатаКонецПоГрафику = КопияТаблицыГрафиковРабот[0].Дата + Час(КопияТаблицыГрафиковРабот[0].КонецРабочегоВремени)*60*60 + Минута(КопияТаблицыГрафиковРабот[0].КонецРабочегоВремени)*60 + Секунда(КопияТаблицыГрафиковРабот[0].КонецРабочегоВремени);
				Если СтрокаТаблицыГраниц.КонецДня = '00010101' ИЛИ ГрафикДатаКонецПоГрафику > СтрокаТаблицыГраниц.КонецДня Тогда
					СтрокаТаблицыГраниц.КонецДня = ГрафикДатаКонецПоГрафику;	
				КонецЕсли;
			КонецЕсли;	
			
		КонецЕсли;
		
		Если СтрокаТаблицыГраниц.НачалоДня = '00010101' Тогда
			СтрокаТаблицыГраниц.НачалоДня = НачалоДня(ДатаОтчета+24*60*60*СчетчикДней);
		КонецЕсли;
		Если СтрокаТаблицыГраниц.КонецДня = '00010101' Тогда
			СтрокаТаблицыГраниц.КонецДня = КонецДня(ДатаОтчета+24*60*60*СчетчикДней);
		КонецЕсли;	
		
	КонецЦикла;	
	
	//определим начальную и конечную даты по существующим документам
	Для СчетчикДней = 0 По Продолжительность-1 Цикл	
		
		//заполним исходные данные
		ДатаНачалоПоГрафику =  ТаблицаГраницГрафика[СчетчикДней].НачалоДня;
		ДатаКонецПоГрафику =  ТаблицаГраницГрафика[СчетчикДней].КонецДня;
		ДатаНачалоПоДокументам = ДатаНачалоПоГрафику;
		ДатаКонецПоДокументам = ДатаКонецПоГрафику;
		ТекущаяДата = ДатаОтчета+24*60*60*СчетчикДней;
		
		//находим начальную дня
		КопияДетальнойТаблицы = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
		КопияДетальнойТаблицы.Сортировать("НачалоВыполнения ВОЗР");
		Для Каждого СтрокаТаблицы Из КопияДетальнойТаблицы Цикл
			//не рассматриваем строки с незаполненной датой
			Если СтрокаТаблицы.НачалоВыполнения = '00010101' Тогда
				Продолжить;
			КонецЕсли;
			//если работа началась еще в предыдущем дне, то дату начала устанавливаем начало дня
			Если СтрокаТаблицы.НачалоВыполнения < НачалоДня(ТекущаяДата) И 
				 СтрокаТаблицы.ОкончаниеВыполнения > НачалоДня(ТекущаяДата) Тогда
				ДатаНачалоПоДокументам = НачалоДня(ТекущаяДата);
				Продолжить;
			КонецЕсли;
			//если работа началась в этом дне...
			Если СтрокаТаблицы.НачалоВыполнения >= НачалоДня(ТекущаяДата) И 
				 СтрокаТаблицы.НачалоВыполнения <= КонецДня(ТекущаяДата) Тогда
				Если СтрокаТаблицы.НачалоВыполнения < ДатаНачалоПоДокументам Тогда
					ДатаНачалоПоДокументам = СтрокаТаблицы.НачалоВыполнения; 	 
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		//находим конечную дату
		КопияДетальнойТаблицы.Сортировать("ОкончаниеВыполнения УБЫВ");
		Для Каждого СтрокаТаблицы Из КопияДетальнойТаблицы Цикл
			//не рассматриваем строки с незаполненной датой
			Если СтрокаТаблицы.ОкончаниеВыполнения = '00010101' Тогда
				Продолжить;
			КонецЕсли;
			//если работа заканчивается в следующем дне, то дату конца устанавливаем конец дня
			Если СтрокаТаблицы.НачалоВыполнения < НачалоДня(ТекущаяДата+24*60*60) И 
				 СтрокаТаблицы.ОкончаниеВыполнения >= НачалоДня(ТекущаяДата+24*60*60) Тогда
				ДатаКонецПоДокументам = КонецДня(ТекущаяДата);
				Продолжить;
			КонецЕсли;
			//если работа заканчивается в этом дне...
			Если СтрокаТаблицы.ОкончаниеВыполнения >= НачалоДня(ТекущаяДата) И 
				 СтрокаТаблицы.ОкончаниеВыполнения <= КонецДня(ТекущаяДата) Тогда
				Если СтрокаТаблицы.ОкончаниеВыполнения > ДатаКонецПоДокументам Тогда
					ДатаКонецПоДокументам = СтрокаТаблицы.ОкончаниеВыполнения; 	 
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		//убедимся, что конец даты по документам не раньше даты отчета
		Если ДатаНачалоПоДокументам < ДатаНачалоПоГрафику  И ДатаКонецПоДокументам > НачалоДня(ДатаНачалоПоГрафику) Тогда
			//если начало даты по документам лежит в другом дне, то время начала отчета = начало дня
			Если НачалоДня(ДатаНачалоПоДокументам) < НачалоДня(ДатаНачалоПоГрафику) Тогда
				ДатаНачалоПоГрафику = НачалоДня(ДатаНачалоПоГрафику);
			Иначе			
				//если начало даты по документам лежит в этом же дне и оно раньше даты по графику, то время начала отчета = время начала по документам
				Если (ДатаНачалоПоГрафику-НачалоДня(ДатаНачалоПоГрафику)) > (ДатаНачалоПоДокументам-НачалоДня(ДатаНачалоПоДокументам)) Тогда
					ДатаНачалоПоГрафику = НачалоДня(ДатаНачалоПоГрафику) + Окр((Час(ДатаНачалоПоДокументам)*60*60)/ДлительностьИнтервала,0,РежимОкругления.Окр15как10)*ДлительностьИнтервала;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//аналогично корректируем время конца дня
		Если ДатаКонецПоДокументам > ДатаКонецПоГрафику  И КонецДня(ДатаКонецПоГрафику) > ДатаНачалоПоДокументам Тогда
			Если КонецДня(ДатаКонецПоДокументам) > КонецДня(ДатаКонецПоГрафику) Тогда
				ДатаКонецПоГрафику = КонецДня(ДатаКонецПоГрафику);
			Иначе			
				Если (ДатаКонецПоГрафику-НачалоДня(ДатаКонецПоГрафику)) < (ДатаКонецПоДокументам-НачалоДня(ДатаКонецПоДокументам)) Тогда
					ДатаКонецПоГрафику = НачалоДня(ДатаНачалоПоГрафику) + Окр(((Час(ДатаКонецПоДокументам)+1)*60*60)/ДлительностьИнтервала,0,РежимОкругления.Окр15как10)*ДлительностьИнтервала;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//заполняем Таблицу границ графика
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Найти(НачалоДня(ТекущаяДата),"Дата");
		СтрокаТаблицыГраниц.НачалоДня = ДатаНачалоПоГрафику;		
		СтрокаТаблицыГраниц.КонецДня = ДатаКонецПоГрафику;		
		
	КонецЦикла;	
	
КонецПроцедуры //СформироватьТаблицуГраницГрафика()

// Формирует детальную таблицу загрузки цехов
// Возвращает таблицу значений
// Параметры:
Процедура СформироватьДетальнуюТаблицуЗагрузкиДляЦехов(ТекущаяЗаявкаНаРемонт,Планирование) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ЗаписьНаРемонт.Ресурс1 КАК Справочник.Цеха) КАК РабочееМесто,
	               |	ДОБАВИТЬКДАТЕ(ЗаписьНаРемонт.Дата, СЕКУНДА, ЧАС(ЗаписьНаРемонт.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ЗаписьНаРемонт.НачалоРабочегоВремени) * 60 + СЕКУНДА(ЗаписьНаРемонт.НачалоРабочегоВремени)) КАК НачалоВыполнения,
	               |	ДОБАВИТЬКДАТЕ(ЗаписьНаРемонт.Дата, СЕКУНДА, ЧАС(ЗаписьНаРемонт.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ЗаписьНаРемонт.КонецРабочегоВремени) * 60 + СЕКУНДА(ЗаписьНаРемонт.КонецРабочегоВремени)) КАК ОкончаниеВыполнения,
	               |	ВЫРАЗИТЬ(ЗаписьНаРемонт.Объект КАК Документ.ЗаявкаНаРемонт) КАК Регистратор,
	               |	ЗаписьНаРемонт.Авторабота КАК Авторабота,
	               |	ЗаписьНаРемонт.Продолжительность КАК Продолжительность,
	               |	ЗаписьНаРемонт.Дата КАК Дата
	               |ПОМЕСТИТЬ
	               |	ЗаписиНаРемонт
	               |ИЗ
	               |	РегистрСведений.ГрафикРаботыРесурсов КАК ЗаписьНаРемонт
	               |ГДЕ
	               |	ЗаписьНаРемонт.Дата МЕЖДУ &НачалоПериода И &КонецПериода И 
	               |	ЗаписьНаРемонт.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПланРемонта)
	               |ИНДЕКСИРОВАТЬ ПО
	               |	РабочееМесто
	               |;
	               |
	               |ВЫБРАТЬ
	               |	Цеха.Ссылка КАК РабочееМесто,
	               |	Цеха.Ссылка.Наименование КАК РабочееМестоНаименование,
	               |	Цеха.КатегорияУпорядочивания,
	               |	0 КАК НомерСтроки,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.НачалоВыполнения, &ПустаяДата) КАК НачалоВыполнения,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.ОкончаниеВыполнения, &ПустаяДата) КАК ОкончаниеВыполнения,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Регистратор, &ПустаяСсылка) КАК Регистратор,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Авторабота, &ПустаяСсылка) КАК Авторабота,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Продолжительность, 0) КАК Продолжительность,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Дата, &ПустаяДата) КАК Дата,
	               |	ВЫБОР
	               |		КОГДА ЗаписиНаРемонт.РабочееМесто ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ФлагНаличияЗаписей
	               |ИЗ
	               |	Цеха КАК Цеха
	               |ЛЕВОЕ СОЕДИНЕНИЕ
	               |	ЗаписиНаРемонт КАК ЗаписиНаРемонт
	               |ПО
	               |	Цеха.Ссылка = ЗаписиНаРемонт.РабочееМесто
	               |ГДЕ
	               |	Цеха.ВидИспользованияРабочегоМеста <> ЗНАЧЕНИЕ(Перечисление.ВидыИспользованияРабочихМест.НеУчаствуетВПланировании)
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	Цеха.Ссылка,
	               |	Цеха.Ссылка.Наименование,
	               |	ВЫБОР
	               |		КОГДА Цеха.ВидИспользованияРабочегоМеста = ЗНАЧЕНИЕ(Перечисление.ВидыИспользованияРабочихМест.ПланированиеПриемкиВыдачи)
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ,
	               |	0,
	               |	&ПустаяДата,
	               |	&ПустаяДата,
	               |	&ПустаяСсылка,
	               |	&ПустаяСсылка,
	               |	0,
	               |	&ПустаяДата,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Цеха КАК Цеха
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КатегорияУпорядочивания,
	               |	РабочееМестоНаименование,
	               |	НачалоВыполнения";
	
	Запрос.УстановитьПараметр("НачалоПериода",НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)));
	Запрос.УстановитьПараметр("ПустаяДата",Дата(1,1,1,0,0,0));
	Запрос.УстановитьПараметр("ПустаяСсылка",Документы.ЗаявкаНаРемонт.ПустаяСсылка());
	ДетальнаяТаблицаЗагрузкиРабочихМест = Запрос.Выполнить().Выгрузить();
	ДетальнаяТаблицаЗагрузкиРабочихМест.Индексы.Добавить("РабочееМесто");
	ДетальнаяТаблицаЗагрузкиРабочихМест.Колонки.Добавить("ИдентификаторРаботы", Новый ОписаниеТипов("Строка"));
	
	//удаляет строки, которые относятся к текущему редактируемому документу		
	Если ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт) Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("Регистратор",ТекущаяЗаявкаНаРемонт);
		
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
            Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
			ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
		КонецЦикла;
	КонецЕсли;
	
	//заново заполняет строки по данным табличной части Планирование
	Если Планирование <> Неопределено Тогда
		Для Каждого СтрокаПланирования Из Планирование Цикл
			Если СтрокаПланирования.НачалоВыполнения <> Дата(1,1,1,0,0,0) И
				СтрокаПланирования.Продолжительность > 0 Тогда  
				
				//получим категорию упорядочивания
				КатегорияУпорядочивания = 2;	
				НайденнаяСтрока = ТаблицаРабочихМест.Найти(СтрокаПланирования.РабочееМесто,"Ссылка");
				Если НайденнаяСтрока <> Неопределено Тогда    
					ВидИспользованияРабочегоМеста = НайденнаяСтрока.ВидИспользованияРабочегоМеста;
					Если ВидИспользованияРабочегоМеста <> Неопределено Тогда
						КатегорияУпорядочивания = ?(ВидИспользованияРабочегоМеста = Перечисления.ВидыИспользованияРабочихМест.ПланированиеПриемкиВыдачи,1,2);
					КонецЕсли;
				Иначе
					Продолжить;
				КонецЕсли;
				
				//отображаться будут только строки планирования, которые попадают в границы календаря
				Если (СтрокаПланирования.НачалоВыполнения <= КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)) И 
					 СтрокаПланирования.ОкончаниеВыполнения >= НачалоДня(ДатаОтчета)) Тогда
						НоваяСтрока = ДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
						НоваяСтрока.КатегорияУпорядочивания = КатегорияУпорядочивания;
						НоваяСтрока.НачалоВыполнения = СтрокаПланирования.НачалоВыполнения;
						НоваяСтрока.ОкончаниеВыполнения = СтрокаПланирования.ОкончаниеВыполнения;
						НоваяСтрока.Авторабота = СтрокаПланирования.Авторабота;
						НоваяСтрока.РабочееМесто = СтрокаПланирования.РабочееМесто;
						НоваяСтрока.РабочееМестоНаименование = СтрокаПланирования.РабочееМесто.Наименование;
						НоваяСтрока.Регистратор = ТекущаяЗаявкаНаРемонт;	
						НоваяСтрока.ИдентификаторРаботы = СтрокаПланирования.ИдентификаторРаботы;
						НоваяСтрока.Продолжительность = СтрокаПланирования.Продолжительность;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(ЦехаОтбор) = Тип("ТаблицаЗначений") И ЦехаОтбор.Количество()>0 Тогда
		Для Каждого ТекСтрока Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
			НайденнаяСтрока = ЦехаОтбор.Найти(ТекСтрока.РабочееМесто, "Цех");
			Если НайденнаяСтрока <> Неопределено Тогда 
				ТекСтрока.НомерСтроки = НайденнаяСтрока.НомерСтроки;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДетальнаяТаблицаЗагрузкиРабочихМест.Сортировать("КатегорияУпорядочивания,РабочееМестоНаименование,НачалоВыполнения");
	
	//удалим строки с незаполненным рабочим местом
	Отбор = Новый Структура();
	Отбор.Вставить("РабочееМесто",Справочники.Цеха.ПустаяСсылка());
	Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
	Для Каждого Строка Из Строки Цикл
        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
		ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
	КонецЦикла;
	
	//в режиме использования графиков рабочих мест удалим рабочие места, для которых не задан график на дату 
	Если ИспользованиеГрафиковРабот > 1 Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("ФлагНаличияЗаписей",Ложь);
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
			ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
			ДанныеТаблицыГрафика = кпПолучитьГрафик(Строка.РабочееМесто.ГрафикРаботы, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета)+60*60*24*(Продолжительность-1));
			Если ДанныеТаблицыГрафика.Количество() = 0 Тогда
		        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
				ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
			ИначеЕсли ОтображатьТолькоРаботающих Тогда
				ЕстьРабочееВремя = Ложь;
				Для Каждого СтрокаГрафика Из ДанныеТаблицыГрафика Цикл
					Если СтрокаГрафика.ВидДня = Перечисления.ВидДня.Рабочий ИЛИ СтрокаГрафика.ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						ЕстьРабочееВремя = Истина;	
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если НЕ ЕстьРабочееВремя Тогда
			        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
					ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицыЗагрузки Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		Если СтрокаТаблицыЗагрузки.ИдентификаторРаботы = "" Тогда
			СтрокаТаблицыЗагрузки.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПорядокРабочихМестИзНастроек();
	
КонецПроцедуры //СформироватьДетальнуюТаблицуЗагрузкиДляЦехов()

// Формирует детальную таблицу загрузки исполнителей
// Возвращает таблицу значений
// Параметры:
Процедура СформироватьДетальнуюТаблицуЗагрузкиДляИсполнителей(ТекущаяЗаявкаНаРемонт,Планирование)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ЗаписьНаРемонт.Ресурс2 КАК Справочник.Сотрудники) КАК РабочееМесто,
	               |	ДОБАВИТЬКДАТЕ(ЗаписьНаРемонт.Дата, СЕКУНДА, ЧАС(ЗаписьНаРемонт.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ЗаписьНаРемонт.НачалоРабочегоВремени) * 60 + СЕКУНДА(ЗаписьНаРемонт.НачалоРабочегоВремени)) КАК НачалоВыполнения,
	               |	ДОБАВИТЬКДАТЕ(ЗаписьНаРемонт.Дата, СЕКУНДА, ЧАС(ЗаписьНаРемонт.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ЗаписьНаРемонт.КонецРабочегоВремени) * 60 + СЕКУНДА(ЗаписьНаРемонт.КонецРабочегоВремени)) КАК ОкончаниеВыполнения,
	               |	ВЫРАЗИТЬ(ЗаписьНаРемонт.Объект КАК Документ.ЗаявкаНаРемонт) КАК Регистратор,
	               |	ЗаписьНаРемонт.Авторабота КАК Авторабота,
	               |	ЗаписьНаРемонт.Продолжительность КАК Продолжительность,
	               |	ЗаписьНаРемонт.Дата КАК Дата
	               |ПОМЕСТИТЬ ЗаписиНаРемонт
	               |ИЗ
	               |	РегистрСведений.ГрафикРаботыРесурсов КАК ЗаписьНаРемонт
	               |ГДЕ
	               |	ЗаписьНаРемонт.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ЗаписьНаРемонт.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПланРемонта)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	РабочееМесто
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Мастера.Ссылка КАК РабочееМесто,
	               |	Мастера.Ссылка.Наименование КАК РабочееМестоНаименование,
	               |	Мастера.ДолжностьПсевдоним КАК ДолжностьПсевдоним,
	               |	Мастера.СотрудникПсевдоним КАК СотрудникПсевдоним,
	               |	0 КАК НомерСтроки,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.НачалоВыполнения, &ПустаяДата) КАК НачалоВыполнения,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.ОкончаниеВыполнения, &ПустаяДата) КАК ОкончаниеВыполнения,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Регистратор, &ПустаяСсылка) КАК Регистратор,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Авторабота, &ПустаяСсылка) КАК Авторабота,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Продолжительность, 0) КАК Продолжительность,
	               |	ЕСТЬNULL(ЗаписиНаРемонт.Дата, &ПустаяДата) КАК Дата,
	               |	ВЫБОР
	               |		КОГДА ЗаписиНаРемонт.РабочееМесто ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ФлагНаличияЗаписей
	               |ИЗ
	               |	Мастера КАК Мастера
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЗаписиНаРемонт КАК ЗаписиНаРемонт
	               |		ПО Мастера.Ссылка = ЗаписиНаРемонт.РабочееМесто
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	Мастера.Ссылка,
	               |	Мастера.Ссылка.Наименование,
	               |	Мастера.ДолжностьПсевдоним,
	               |	Мастера.СотрудникПсевдоним,
	               |	0,
	               |	&ПустаяДата,
	               |	&ПустаяДата,
	               |	&ПустаяСсылка,
	               |	&ПустаяСсылка,
	               |	0,
	               |	&ПустаяДата,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Мастера КАК Мастера
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДолжностьПсевдоним,
	               |	СотрудникПсевдоним,
	               |	НачалоВыполнения";
	
	Запрос.УстановитьПараметр("НачалоПериода",НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)));
	Запрос.УстановитьПараметр("ПустаяДата",Дата(1,1,1,0,0,0));
 	Запрос.УстановитьПараметр("ПустойМастер",Справочники.Сотрудники.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСсылка",Документы.ЗаявкаНаРемонт.ПустаяСсылка());
	ДетальнаяТаблицаЗагрузкиРабочихМест = Запрос.Выполнить().Выгрузить();
	ДетальнаяТаблицаЗагрузкиРабочихМест.Индексы.Добавить("РабочееМесто");
	ДетальнаяТаблицаЗагрузкиРабочихМест.Колонки.Добавить("ИдентификаторРаботы", Новый ОписаниеТипов("Строка"));

	//удаляет строки, которые относятся к текущему редактируемому документу		
	Если ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт) Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("Регистратор",ТекущаяЗаявкаНаРемонт);
		
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
            Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
			ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
		КонецЦикла;
	КонецЕсли;
	
	//заново заполняет строки по данным табличной части Планирование
	Если Планирование <> Неопределено Тогда
		Для Каждого СтрокаПланирования Из Планирование Цикл
			Если СтрокаПланирования.НачалоВыполнения <> Дата(1,1,1,0,0,0) И
				СтрокаПланирования.Продолжительность > 0 Тогда  
				
				//не выводим сотрудников, не участвующих в планировании
				НайденнаяСтрока = ТаблицаРабочихМест.Найти(СтрокаПланирования.Исполнитель,"Ссылка");
				Если НайденнаяСтрока = Неопределено Тогда    
					Продолжить;
				КонецЕсли;
				
				//отображаться будут только строки планирования, которые попадают в границы календаря
				Если (СтрокаПланирования.НачалоВыполнения <= КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)) И 
					 СтрокаПланирования.ОкончаниеВыполнения >= НачалоДня(ДатаОтчета)) Тогда
						НоваяСтрока = ДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
						НоваяСтрока.НачалоВыполнения = СтрокаПланирования.НачалоВыполнения;
						НоваяСтрока.ОкончаниеВыполнения = СтрокаПланирования.ОкончаниеВыполнения;
						НоваяСтрока.Авторабота = СтрокаПланирования.Авторабота;
						НоваяСтрока.РабочееМесто = СтрокаПланирования.Исполнитель;
						НоваяСтрока.РабочееМестоНаименование = СтрокаПланирования.Исполнитель.Наименование;
						Если ЗначениеЗаполнено(СтрокаПланирования.Исполнитель.Должность) И ЗначениеЗаполнено(СтрокаПланирования.Исполнитель.Должность.Псевдоним) Тогда
							НоваяСтрока.ДолжностьПсевдоним = СтрокаПланирования.Исполнитель.Должность.Псевдоним;
						Иначе
							НоваяСтрока.ДолжностьПсевдоним = СтрокаПланирования.Исполнитель.Должность.Наименование;
						КонецЕсли;
						Если ЗначениеЗаполнено(СтрокаПланирования.Исполнитель.Псевдоним) Тогда
							НоваяСтрока.СотрудникПсевдоним = СтрокаПланирования.Исполнитель.Псевдоним;
						Иначе
							НоваяСтрока.СотрудникПсевдоним = СтрокаПланирования.Исполнитель.Наименование;
						КонецЕсли;
						НоваяСтрока.Регистратор = ТекущаяЗаявкаНаРемонт;	
						НоваяСтрока.ИдентификаторРаботы = СтрокаПланирования.ИдентификаторРаботы;
						НоваяСтрока.Продолжительность = СтрокаПланирования.Продолжительность;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если ТипЗнч(ИсполнителиОтбор) = Тип("ТаблицаЗначений") И ИсполнителиОтбор.Количество()>0 Тогда
		Для Каждого ТекСтрока Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
			НайденнаяСтрока = ИсполнителиОтбор.Найти(ТекСтрока.РабочееМесто, "Сотрудник");
			Если НайденнаяСтрока <> Неопределено Тогда 
				ТекСтрока.НомерСтроки = НайденнаяСтрока.НомерСтроки;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДетальнаяТаблицаЗагрузкиРабочихМест.Сортировать("ДолжностьПсевдоним,СотрудникПсевдоним,НачалоВыполнения");
	
	//удалим строки с незаполненным рабочим местом         
	Отбор = Новый Структура();
	Отбор.Вставить("РабочееМесто",Справочники.Сотрудники.ПустаяСсылка());
	Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
	Для Каждого Строка Из Строки Цикл
        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
		ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
	КонецЦикла;
	
	//в режиме использования графиков рабочих мест удалим рабочие места, для которых не задан график на дату 
	Если ИспользованиеГрафиковРабот > 1 Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("ФлагНаличияЗаписей",Ложь);
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
			ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
			ДанныеТаблицыГрафика = кпПолучитьГрафик(Строка.РабочееМесто.ГрафикРаботы, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета)+60*60*24*(Продолжительность-1));
			Если ДанныеТаблицыГрафика.Количество() = 0 Тогда
		        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
				ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
			ИначеЕсли ОтображатьТолькоРаботающих Тогда
				ЕстьРабочееВремя = Ложь;
				Для Каждого СтрокаГрафика Из ДанныеТаблицыГрафика Цикл
					Если СтрокаГрафика.ВидДня = Перечисления.ВидДня.Рабочий ИЛИ СтрокаГрафика.ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						ЕстьРабочееВремя = Истина;	
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если НЕ ЕстьРабочееВремя Тогда
			        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
					ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
	Для Каждого СтрокаТаблицыЗагрузки Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		Если СтрокаТаблицыЗагрузки.ИдентификаторРаботы = "" Тогда
			СтрокаТаблицыЗагрузки.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПорядокРабочихМестИзНастроек();
	
	//БИЗТЕХ КОВАЛЬ 13.09.2024 ++
	//сортировка исполнителей по типу группы для отображения в АРМ для дальнейшего разделения всех исполнителей на группы	УстановитьПорядокРабочихМестПоГруппам();
	УстановитьПорядокРабочихМестПоГруппам();
	//БИЗТЕХ КОВАЛЬ 13.09.2024 --
	
КонецПроцедуры //СформироватьДетальнуюТаблицуЗагрузкиДляИсполнителей()

// Формирует детальную таблицу состояния заявок на ремонт по наличию и состоянию заказ-нарядов
// Возвращает таблицу значений
// Параметры: использует данные таблицы ДетальнаяТаблицаЗагрузкиРабочихМест
Процедура СформироватьТаблицуСостояниеЗаявок()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказНаряд.ДокументОснование КАК ЗаявкаНаРемонт,
	               |	ЗаказНаряд.Ссылка КАК ЗаказНаряд,
	               |	ЗаказНаряд.Состояние
	               |ИЗ
	               |	Документ.ЗаказНаряд КАК ЗаказНаряд
	               |ГДЕ
	               |	ЗаказНаряд.ПометкаУдаления = ЛОЖЬ
	               |	И ЗаказНаряд.ДокументОснование <> &ПустаяСсылка
	               |	И ЗаказНаряд.ДокументОснование В(&СписокРегистраторов)";
	Запрос.УстановитьПараметр("СписокРегистраторов",ДетальнаяТаблицаЗагрузкиРабочихМест.ВыгрузитьКолонку("Регистратор"));
	Запрос.УстановитьПараметр("ПустаяСсылка",Документы.ЗаявкаНаРемонт.ПустаяСсылка());
	ТаблицаСостоянияЗаявок = Запрос.Выполнить().Выгрузить();
	ТаблицаСостоянияЗаявок.Индексы.Добавить("ЗаявкаНаРемонт");
		
КонецПроцедуры //СформироватьТаблицуСостояниеЗаявок()

// Формирует детальную таблицу состояния заявок на ремонт по наличию и состоянию заказ-нарядов
// Возвращает таблицу значений
// Параметры: использует данные таблицы ДетальнаяТаблицаЗагрузкиРабочихМест
Процедура СформироватьТаблицуСостояниеЗаявокВключаяУРВ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Документ.ДокументОснование КАК ЗаявкаНаРемонт,
	               |	Документ.Ссылка КАК ЗаказНаряд,
	               |	Документ.Состояние
	               |ПОМЕСТИТЬ ВсеЗаказНаряды
	               |ИЗ
	               |	Документ.ЗаказНаряд КАК Документ
	               |ГДЕ
	               |	Документ.ПометкаУдаления = ЛОЖЬ
	               |	И Документ.ДокументОснование В(&СписокРегистраторов)
	               |	И Документ.ДокументОснование <> &ПустаяСсылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОткрытыеЗаказНаряды.ЗаказНаряд
	               |ПОМЕСТИТЬ ЗаказНарядыВРаботе
	               |ИЗ
	               |	ВсеЗаказНаряды КАК ОткрытыеЗаказНаряды
	               |ГДЕ
	               |	ОткрытыеЗаказНаряды.ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
	               |	И ОткрытыеЗаказНаряды.ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РегистрацияВремениРаботСрезПоследних.ЗаказНаряд
	               |ПОМЕСТИТЬ ЗаказНарядыИмеющиеРабочиеОтметки
	               |ИЗ
	               |	РегистрСведений.РегистрацияВремениРабот.СрезПоследних(
	               |			,
	               |			ЗаказНаряд В
	               |					(ВЫБРАТЬ
	               |						ЗаказНарядыВРаботе.ЗаказНаряд
	               |					ИЗ
	               |						ЗаказНарядыВРаботе КАК ЗаказНарядыВРаботе)
	               |				И ВидОтметки = ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)) КАК РегистрацияВремениРаботСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РегистрацияВремениРаботСрезПоследних.ЗаказНаряд
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказНарядРаботы.Ссылка КАК ЗаказНаряд
	               |ПОМЕСТИТЬ ЗаказНарядыИмеющиеНеЗакрытыеПакеты
	               |ИЗ
	               |	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
	               |ГДЕ
	               |	ЗаказНарядРаботы.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ЗаказНарядыВРаботе.ЗаказНаряд
	               |			ИЗ
	               |				ЗаказНарядыВРаботе КАК ЗаказНарядыВРаботе)
	               |	И ЗаказНарядРаботы.ПакетРабот <> """"
	               |	И ЗаказНарядРаботы.ПакетЗакрыт = ЛОЖЬ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказНарядРаботы.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокЗаказНарядов.ЗаявкаНаРемонт,
	               |	СписокЗаказНарядов.ЗаказНаряд,
	               |	СписокЗаказНарядов.Состояние,
	               |	ВЫБОР
	               |		КОГДА ИмеющиеРабочиеОтметки.ЗаказНаряд ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК РаботыПоЗаказНарядуНачаты,
	               |	ВЫБОР
	               |		КОГДА ИмеющиеНеЗакрытыеПакеты.ЗаказНаряд ЕСТЬ NULL 
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК РаботыПоЗаказНарядуЗавершены
	               |ИЗ
	               |	ВсеЗаказНаряды КАК СписокЗаказНарядов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЗаказНарядыИмеющиеРабочиеОтметки КАК ИмеющиеРабочиеОтметки
	               |		ПО СписокЗаказНарядов.ЗаказНаряд = ИмеющиеРабочиеОтметки.ЗаказНаряд
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЗаказНарядыИмеющиеНеЗакрытыеПакеты КАК ИмеющиеНеЗакрытыеПакеты
	               |		ПО СписокЗаказНарядов.ЗаказНаряд = ИмеющиеНеЗакрытыеПакеты.ЗаказНаряд";
				   
	Запрос.УстановитьПараметр("ПустаяСсылка",Документы.ЗаявкаНаРемонт.ПустаяСсылка());
	Запрос.УстановитьПараметр("СписокРегистраторов",ДетальнаяТаблицаЗагрузкиРабочихМест.ВыгрузитьКолонку("Регистратор"));
	ТаблицаСостоянияЗаявок = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры //СформироватьТаблицуСостояниеЗаявокВключаяУРВ()

// Формирует таблицу расшифровок расшифровок
//
Процедура СформироватьТаблицуРасшифровок()
	
	// ТаблицаРасшифровок - инициализация
	ТаблицаРасшифровок = Новый ТаблицаЗначений;
	Если РежимКалендаря = 1 Тогда
		ТаблицаРасшифровок.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Цеха"));
	ИначеЕсли РежимКалендаря = 2 Тогда
		ТаблицаРасшифровок.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	КонецЕсли;
	
	// сразу заполним первую строку значением интервала в нужном формате даты
	СлужебнаяСтрока = ТаблицаРасшифровок.Добавить();
	НовыйИнтервал = НачалоДня(ДатаОтчета);
	СтарыйИнтервал = НовыйИнтервал-1;
	ОбщееКоличествоИнтервалов = Продолжительность*24*60*60/ДлительностьИнтервала;
	Для ИндексИнтервала = 0 По ОбщееКоличествоИнтервалов-1 Цикл
		//добавляем колонку, если начало нового дня
		Если НачалоДня(СтарыйИнтервал) <> НачалоДня(НовыйИнтервал) Тогда
			ИмяКолонки = "НоваяДата" + Формат(НовыйИнтервал,"ДФ=ММдд");		
			ТаблицаРасшифровок.Колонки.Добавить(ИмяКолонки,Новый ОписаниеТипов("СписокЗначений"));
			НовоеЗначениеЯчейки = Новый СписокЗначений;
			НовоеЗначениеЯчейки.Добавить(НовыйИнтервал);
			СлужебнаяСтрока[ИмяКолонки] = НовоеЗначениеЯчейки;     
		КонецЕсли;	
		//добавляем колонки в таблицу расшифровок, не выходяшие за график
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Найти(НачалоДня(НовыйИнтервал),"Дата");
		Если НовыйИнтервал >= СтрокаТаблицыГраниц.НачалоДня И НовыйИнтервал < СтрокаТаблицыГраниц.КонецДня Тогда
			ИмяКолонки = "Интервал" + Формат(НовыйИнтервал,"ДФ=ддЧЧмм");		
			ТаблицаРасшифровок.Колонки.Добавить(ИмяКолонки,Новый ОписаниеТипов("СписокЗначений"));
			НовоеЗначениеЯчейки = Новый СписокЗначений;
			НовоеЗначениеЯчейки.Добавить(НовыйИнтервал);
			СлужебнаяСтрока[ИмяКолонки] = НовоеЗначениеЯчейки;
		КонецЕсли;
		СтарыйИнтервал = НовыйИнтервал;			
		НовыйИнтервал = НовыйИнтервал + ДлительностьИнтервала;
	КонецЦикла;
	
	// ЧислоИнтерваловВремени
	ЧислоИнтерваловВремени = ТаблицаРасшифровок.Колонки.Количество();
	
	// ТаблицаРасшифровок - заполнение
	Для Каждого СтрокаТаблицыЗагрузки Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		
		// Получим строку для заполнения или создадим новую
		РезультатПоискаСтроки = ТаблицаРасшифровок.Найти(СтрокаТаблицыЗагрузки.РабочееМесто, "РабочееМесто");
		Если РезультатПоискаСтроки <> Неопределено Тогда
			СтрокаТаблицыРасшифровок = РезультатПоискаСтроки;
		Иначе
			СтрокаТаблицыРасшифровок = ТаблицаРасшифровок.Добавить();
			СтрокаТаблицыРасшифровок.РабочееМесто = СтрокаТаблицыЗагрузки.РабочееМесто;
		КонецЕсли;
		
		СлужебнаяСтрока = ТаблицаРасшифровок[0];
		
		Для ИндексКолонки = 1 По ЧислоИнтерваловВремени-1 Цикл
			
			Если Лев(ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя,8)="НоваяДат" Тогда 
				Продолжить;
			КонецЕсли;	
			
			Если СтрокаТаблицыЗагрузки.НачалоВыполнения < СлужебнаяСтрока[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя][0].Значение+ДлительностьИнтервала
				И СтрокаТаблицыЗагрузки.ОкончаниеВыполнения > СлужебнаяСтрока[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя][0].Значение Тогда
				
				Если обЗначениеНеЗаполнено(СтрокаТаблицыЗагрузки.Регистратор) Тогда
					Представление = "Новая заявка на ремонт"; 
				Иначе
					Представление = ПолучитьПредставлениеЗаявкиНаРемонт(СтрокаТаблицыЗагрузки.Регистратор,ФорматПредставленияЗаявкиНаРемонт);					
				КонецЕсли;
				
				СтрокаТаблицыРасшифровок[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя].Добавить(СтрокаТаблицыЗагрузки.Регистратор,Представление);	
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры //СформироватьТаблицуРасшифровок()

// Формирует таблицу загрузки по данным УРВ
//
Процедура СформироватьТаблицаДанныхУРВ()
	
	// инициализация таблицы
	ТаблицаДанныхУРВ = ТаблицаРасшифровок.Скопировать();
	ТаблицаДанныхУРВ.Очистить();
	НоваяСтрокаТаблицыДанныхУРВ = ТаблицаДанныхУРВ.Добавить();
	Для СчИнтервалов = 0 По ЧислоИнтерваловВремени-1 Цикл
		НоваяСтрокаТаблицыДанныхУРВ[СчИнтервалов] = ТаблицаРасшифровок[0][СчИнтервалов];
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияВремениРабот.Период КАК НачалоИнтервала,
	|	РегистрацияВремениРабот.КонецРаботы КАК КонецИнтервала,
	|	"+?(РежимКалендаря = 2,"РегистрацияВремениРабот.Сотрудник","РегистрацияВремениРабот.РабочееМесто")+" КАК РабочееМесто,
	|	РегистрацияВремениРабот.ЗаказНаряд,
	|	РегистрацияВремениРабот.ЗаказНаряд.ДокументОснование КАК ДокументОснование,
	|	РегистрацияВремениРабот.ВидОтметки
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот
	|ГДЕ
	|	РегистрацияВремениРабот.Период МЕЖДУ &НачалоДня И &КонецДня
	|	И РегистрацияВремениРабот.ВидОтметки <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияВремениРабот.КонецРаботы,
	|	РегистрацияВремениРабот.Период,
	|	РегистрацияВремениРабот.Сотрудник,
	|	РегистрацияВремениРабот.РабочееМесто,
	|	РегистрацияВремениРабот.ЗаказНаряд,
	|	РегистрацияВремениРабот.ЗаказНаряд.ДокументОснование,
	|	РегистрацияВремениРабот.ВидОтметки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачалоИнтервала";
	
	Запрос.УстановитьПараметр("НачалоДня" , ДатаОтчета);
	Запрос.УстановитьПараметр("КонецДня"  , КонецДня(ДатаОтчета));
	Запрос.УстановитьПараметр("ПустаяДата",Дата("00010101"));
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	ВыборкаЗапроса = Запрос.Выполнить().Выгрузить();
	
	// заполним рабочие места из таблицы загрузки
	Для Каждого ТекСтрока Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		РезультатПоискаСтроки = ТаблицаДанныхУРВ.Найти(ТекСтрока.РабочееМесто, "РабочееМесто");
		Если РезультатПоискаСтроки = Неопределено Тогда
			СтрокаТаблРабМест = ТаблицаДанныхУРВ.Добавить();
			СтрокаТаблРабМест.РабочееМесто = ТекСтрока.РабочееМесто;
		КонецЕсли;
	КонецЦикла;

	// заполним таблицу интервалами
	Для Каждого стрВыборки Из ВыборкаЗапроса Цикл
		
		Если НЕ ЗначениеЗаполнено(стрВыборки.КонецИнтервала) Тогда
			Если стрВыборки.НачалоИнтервала <= ТекущаяДата() Тогда
				стрВыборки.КонецИнтервала = ТекущаяДата();
			Иначе
				Продолжить;				
			КонецЕсли;
		КонецЕсли;
		
		РезультатПоискаСтроки = ТаблицаДанныхУРВ.Найти(стрВыборки.РабочееМесто, "РабочееМесто");
		Если РезультатПоискаСтроки <> Неопределено Тогда
			СтрокаТаблУРВ = РезультатПоискаСтроки;
		Иначе
			СтрокаТаблУРВ = ТаблицаДанныхУРВ.Добавить();
			СтрокаТаблУРВ.РабочееМесто = стрВыборки.РабочееМесто;
		КонецЕсли;
		
		СтрокаИнтервалов = ТаблицаДанныхУРВ[0];
		ЧислоИнтервалов  = ТаблицаДанныхУРВ.Колонки.Количество() - 1;
		
		ПерваяЯчейка = Истина; 
		
		Для СчИнтервалов = 1 По ЧислоИнтервалов Цикл
			НачалоИнтервала = СтрокаИнтервалов[СчИнтервалов][0].Значение;
			КонецИнтервала  = ?(СчИнтервалов = ЧислоИнтервалов,КонецДня(ДатаОтчета),СтрокаИнтервалов[СчИнтервалов + 1][0].Значение);
			
			Если (НачалоИнтервала < стрВыборки.КонецИнтервала И КонецИнтервала >= стрВыборки.КонецИнтервала)
				ИЛИ (НачалоИнтервала <= стрВыборки.НачалоИнтервала И КонецИнтервала > стрВыборки.НачалоИнтервала)
				ИЛИ (НачалоИнтервала <= стрВыборки.НачалоИнтервала И КонецИнтервала >= стрВыборки.КонецИнтервала)
				ИЛИ (НачалоИнтервала >= стрВыборки.НачалоИнтервала И КонецИнтервала <= стрВыборки.КонецИнтервала) Тогда
				// добавим документ в ячейку
				ЗначениеЯчейки = СтрокаТаблУРВ[СчИнтервалов];
				Если ТипЗнч(ЗначениеЯчейки) <> Тип("СписокЗначений") Тогда
					ЗначениеЯчейки = Новый СписокЗначений;
				КонецЕсли;
				
				Заявка = Неопределено;
				Если ЗначениеЗаполнено(стрВыборки.ДокументОснование) И (ТипЗнч(стрВыборки.ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРемонт")) Тогда
					Заявка = стрВыборки.ДокументОснование;
				КонецЕсли;
				
				НомерЗН = "";
				Если ПерваяЯчейка И ЗначениеЗаполнено(стрВыборки.ЗаказНаряд) Тогда
			   		НомерЗН = стрВыборки.ЗаказНаряд.Номер;
				КонецЕсли;
				ПерваяЯчейка = Ложь;
				
				МассивДанных = Новый Массив;
				МассивДанных.Добавить(стрВыборки.ВидОтметки);
				МассивДанных.Добавить(Макс(НачалоИнтервала,стрВыборки.НачалоИнтервала));
				МассивДанных.Добавить(Мин(КонецИнтервала,стрВыборки.КонецИнтервала));
				МассивДанных.Добавить(стрВыборки.ЗаказНаряд);
				МассивДанных.Добавить(Заявка);
				МассивДанных.Добавить(НомерЗН);				
				ЗначениеЯчейки.Добавить(МассивДанных,"Массив");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры //СформироватьФактическуюТаблицуЗагрузки()

// Формирует таблицу позиций колонок рабочих мест по данным УРВ
//
Процедура СформироватьТаблицуПозицийРабочихМест()
	
	ТаблицаПозицийРабочихМест = Новый ТаблицаЗначений;
	Если РежимКалендаря = 1 Тогда
		ТаблицаПозицийРабочихМест.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Цеха"));
	ИначеЕсли РежимКалендаря = 2 Тогда
		ТаблицаПозицийРабочихМест.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	КонецЕсли;
	ТаблицаПозицийРабочихМест.Колонки.Добавить("Смещение",Новый ОписаниеТипов("Число"));
	ТаблицаПозицийРабочихМест.Колонки.Добавить("ОтображениеУРВ",Новый ОписаниеТипов("Булево"));
	
	ТекСмещение = 0;
	
	Для СчетчикРабочихМест = 1 По ЧислоРабочихМест Цикл
		
		ТекСмещение = ТекСмещение + 1;
		
		НоваяСтрокаТаблицыПозиций = ТаблицаПозицийРабочихМест.Добавить();
		НоваяСтрокаТаблицыПозиций.РабочееМесто = ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто;			
 		НоваяСтрокаТаблицыПозиций.Смещение = ТекСмещение;
 		НоваяСтрокаТаблицыПозиций.ОтображениеУРВ = Ложь;
		
		Если ОтображатьДанныеУРВ = 1 Тогда
			ТекСмещение = ТекСмещение + 1;
 			НоваяСтрокаТаблицыПозиций.ОтображениеУРВ = Истина;
		ИначеЕсли ОтображатьДанныеУРВ = 2 Тогда
			Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
				Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8) = "НоваяДат" Тогда 
					Продолжить;
				КонецЕсли;			
				ЗначениеЯчейкиУРВ = ТаблицаДанныхУРВ[СчетчикРабочихМест][ИндексИнтервала];
				Если ТипЗнч(ЗначениеЯчейкиУРВ) = Тип("СписокЗначений") И ЗначениеЯчейкиУРВ.Количество() > 0 Тогда
					ТекСмещение = ТекСмещение + 1;
		 			НоваяСтрокаТаблицыПозиций.ОтображениеУРВ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ЧислоЯчеекДиаграммы = ТекСмещение;
	
КонецПроцедуры //СформироватьТаблицуПозицийРабочихМест()

// Формирует таблицы смещений
//
Процедура СформироватьТаблицуСмещений()
	
	// ТаблицаСмещений - инициализация
	ТаблицаСмещений = Новый ТаблицаЗначений;
	ТаблицаСмещений.Колонки.Добавить("ДокументСсылка", Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРемонт"));
	ТаблицаСмещений.Колонки.Добавить("СмещениеКолонки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаСмещений.Колонки.Добавить("СмещениеСтроки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаСмещений.Колонки.Добавить("ИдентификаторРаботы", Новый ОписаниеТипов("Строка"));
	ТаблицаСмещений.Колонки.Добавить("Авторабота",Новый ОписаниеТипов("СправочникСсылка.Автоработы"));
	ТаблицаСмещений.Колонки.Добавить("Продолжительность",Новый ОписаниеТипов("Число"));
	ТаблицаСмещений.Колонки.Добавить("ТекущаяРабота",Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаТаблицыЗагрузки Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		
		СлужебнаяСтрока = ТаблицаРасшифровок[0];
		
		Для ИндексКолонки = 1 По ЧислоИнтерваловВремени-1 Цикл
			
			Если Лев(ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя,8)="НоваяДат" Тогда 
				Продолжить;
			КонецЕсли;	
			
			Если СтрокаТаблицыЗагрузки.НачалоВыполнения < СлужебнаяСтрока[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя][0].Значение+ДлительностьИнтервала
				И СтрокаТаблицыЗагрузки.ОкончаниеВыполнения > СлужебнаяСтрока[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя][0].Значение Тогда
				
				СмещениеТекущегоРабочегоМеста = 0;
				РезультатПоискаСтроки = ТаблицаПозицийРабочихМест.Найти(СтрокаТаблицыЗагрузки.РабочееМесто, "РабочееМесто");
				Если РезультатПоискаСтроки <> Неопределено Тогда
					СмещениеТекущегоРабочегоМеста = РезультатПоискаСтроки.Смещение;
				КонецЕсли;
				
				НоваяСтрокаТаблицыСмещений = ТаблицаСмещений.Добавить();
				НоваяСтрокаТаблицыСмещений.ДокументСсылка = СтрокаТаблицыЗагрузки.Регистратор;
				НоваяСтрокаТаблицыСмещений.ИдентификаторРаботы = СтрокаТаблицыЗагрузки.ИдентификаторРаботы;
				НоваяСтрокаТаблицыСмещений.Авторабота = СтрокаТаблицыЗагрузки.Авторабота;
				НоваяСтрокаТаблицыСмещений.СмещениеКолонки = ИндексКолонки;
				НоваяСтрокаТаблицыСмещений.СмещениеСтроки = СмещениеТекущегоРабочегоМеста;  
				НоваяСтрокаТаблицыСмещений.Продолжительность = Строка(СтрокаТаблицыЗагрузки.Продолжительность); 				
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры //СформироватьТаблицуСмещений()

// Формирует таблицу оформления
//
Процедура СформироватьТаблицуОформления() Экспорт
	
	ТаблицаОформления = Новый ТаблицаЗначений;
	ТаблицаОформления.Колонки.Добавить("АдресЯчейки", Новый ОписаниеТипов("Строка"));
	ТаблицаОформления.Колонки.Добавить("СмещениеКолонки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаОформления.Колонки.Добавить("СмещениеСтроки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаОформления.Колонки.Добавить("ПредставлениеЗаявки");
	ТаблицаОформления.Колонки.Добавить("ЦветФона");
	ТаблицаОформления.Колонки.Добавить("ЦветРамки");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиВерхняя");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиНижняя");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиЛевая");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиПравая");
	ТаблицаОформления.Колонки.Добавить("АктуальноеВремя", Новый ОписаниеТипов("Булево"));
	ТаблицаОформления.Колонки.Добавить("СостояниеЗаявки", Новый ОписаниеТипов("СправочникСсылка.ВидыСостоянийЗаказНарядов"));
	//БИЗТЕХ КОВАЛЬ 12.09.2024 ++
	//картинка в зависимости от состояния заказа
	ТаблицаОформления.Колонки.Добавить("Картинка");
	//БИЗТЕХ КОВАЛЬ 12.09.2024 --
	
	ТекущаяДата = ТекущаяДата();
	
	Для ТекущаяКолонка = 1 По ТаблицаРасшифровок.Количество()-1 Цикл
		
		НайденнаяСтрока = ТаблицаПозицийРабочихМест[ТекущаяКолонка-1];
		Если НайденнаяСтрока = Неопределено Тогда 
			Продолжить; 
		КонецЕсли;
		СмещениеСтроки = НайденнаяСтрока.Смещение;
		
		// Определим, какой график использовать для строки
		Если ИспользованиеГрафиковРабот > 1 Тогда
			ГрафикРабочегоМеста = ТаблицаРасшифровок[ТекущаяКолонка]["РабочееМесто"].ГрафикРаботы;
			Если ГрафикРабочегоМеста <> Неопределено Тогда
				ТаблицаГрафикаСтроки = кпПолучитьГрафик(ГрафикРабочегоМеста, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
			КонецЕсли;
		ИначеЕсли ИспользованиеГрафиковРабот = 1 Тогда
			ТаблицаГрафикаСтроки =  кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
		КонецЕсли;
		
		ЗаявкаПредыдущейЯчейки = Документы.ЗаявкаНаРемонт.ПустаяСсылка();
		
		ЧислоИнтерваловВремени = ТаблицаРасшифровок.Колонки.Количество();
		
		Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
			
			//пропускаем строчку с днём
			Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
				Продолжить;
			КонецЕсли;
					
			ЦветЯчейки = Неопределено;
			ТекстЯчейки = Неопределено;
			РамкаВерхняя = Неопределено;
			РамкаНижняя = Неопределено;
			РамкаЛевая = Неопределено;
			РамкаПравая = Неопределено;
			
			ЯчейкаТаблицыРасшифровок = ТаблицаРасшифровок[ТекущаяКолонка][ИндексИнтервала];
			Если ТипЗнч(ЯчейкаТаблицыРасшифровок) = Тип("СписокЗначений") И ЯчейкаТаблицыРасшифровок.Количество() > 0 Тогда
				
				//получим данные текущей ячейки
				ТекущаяЗаявкаНаРемонт = ЯчейкаТаблицыРасшифровок[0].Значение; 
				ТекущийЗаказНаряд = Неопределено;
				МассивЗаказНарядов = Новый Массив;
				Если ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт) Тогда
					СтруктураОтбора = Новый Структура ("ЗаявкаНаРемонт",ТекущаяЗаявкаНаРемонт);
					МассивЗаказНарядов = ТаблицаСостоянияЗаявок.НайтиСтроки(СтруктураОтбора);
					Если МассивЗаказНарядов.Количество() > 0 Тогда
						ТекущийЗаказНаряд = МассивЗаказНарядов[0].ЗаказНаряд;
					КонецЕсли;
				КонецЕсли;
				
				//проверим наложение заявок друг на друга
				Если ЯчейкаТаблицыРасшифровок.Количество() > 1 И ПроверитьПересечениеЗаявок(ЯчейкаТаблицыРасшифровок,ТекущаяКолонка,ИндексИнтервала) Тогда
					ЦветЯчейки = WebЦвета.Красный;
				КонецЕсли;
				
				Если ЦветЯчейки = Неопределено Тогда
				
					//определим цвет фона
					Если ЗначениеЗаполнено(ТекущийЗаказНаряд) 
						И ЗначениеЗаполнено(ТекущийЗаказНаряд.Состояние) 
						И ЗначениеЗаполнено(ТекущийЗаказНаряд.Состояние.Цвет) 
						И ТекущийЗаказНаряд.Состояние.Цвет.Получить() <> Неопределено Тогда
						ЦветЯчейки = ТекущийЗаказНаряд.Состояние.Цвет.Получить();
					КонецЕсли;
					
					//проверим отклонения 
					Если НЕ ЗначениеЗаполнено(ТекущийЗаказНаряд) Тогда
						//если время начала записи уже прошло, а заказ-наряда нет - присвоим статус "опоздание клиента"
						Если ТекущаяЗаявкаНаРемонт.ДатаНачала < ТекущаяДата() Тогда
							ЦветЯчейки = Справочники.ВидыСостоянийЗаказНарядов.ОпозданиеКлиента.Цвет.Получить();
						КонецЕсли;
					Иначе
						
						Если НЕ ЗапретРедактированияУРВ Тогда
							
							//проверим опоздание начала работ
							ЕстьОпозданиеНачалаРабот = Ложь;
							Если ТекущаяЗаявкаНаРемонт.ДатаНачала < ТекущаяДата() Тогда
								Для Каждого ТекСтрока Из МассивЗаказНарядов Цикл
									
									//если ЗН выполнен или закрыт, значит опоздания по нему нет
									Если ТекСтрока.ЗаказНаряд.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен 
										ИЛИ ТекСтрока.ЗаказНаряд.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
										Продолжить;	
									КонецЕсли;
									
									Если НЕ ТекСтрока.РаботыПоЗаказНарядуНачаты Тогда
										ЕстьОпозданиеНачалаРабот = Истина;
										Прервать;
									КонецЕсли;
									
								КонецЦикла;
							КонецЕсли;
							
							Если ЕстьОпозданиеНачалаРабот Тогда
								
								ЦветЯчейки = Справочники.ВидыСостоянийЗаказНарядов.ОпозданиеНачалаРабот.Цвет.Получить();
								
							Иначе
								
								//если заказ-наряд введен, дата окончания работ уже прошла, а пакеты закрыты не все - присвоим статус "опоздание окончания работ"
								ЕстьОпозданиеОкончанияРабот = Ложь;
								Если ТекущаяЗаявкаНаРемонт.ДатаОкончания < ТекущаяДата() Тогда
									Для Каждого ТекСтрока Из МассивЗаказНарядов Цикл
										
										//если ЗН выполнен или закрыт, значит опоздания по нему нет
										Если ТекСтрока.ЗаказНаряд.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен 
											ИЛИ ТекСтрока.ЗаказНаряд.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
											Продолжить;	
										КонецЕсли;
										
										Если НЕ ТекСтрока.РаботыПоЗаказНарядуЗавершены Тогда
											ЕстьОпозданиеОкончанияРабот = Истина;
											Прервать;
										КонецЕсли;
										
									КонецЦикла;
								КонецЕсли;
								
								Если ЕстьОпозданиеОкончанияРабот Тогда
									ЦветЯчейки = Справочники.ВидыСостоянийЗаказНарядов.ОпозданиеОкончанияРабот.Цвет.Получить();
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
	
				КонецЕсли; 
				
				//БИЗТЕХ 05.12.2023 Коваль А.Д.++
				//Изменение цвета ячейки по типу ремонта 
				//Прописываем стандартный цвет ячейки для замены цветов по состоянию ремонта
				ЦветЯчейки = ЦветаРаскраски.Получить("ЦветЗагрузкаНорм");
				//Если цвет заполнен в справочнике виды ремонта то ставим нужный цвет
				Если ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт.ВидРемонта.ЦветДляАРМ) Тогда
					СоответствиеWebЦвета=Новый Соответствие;
					СоответствиеWebЦвета.Вставить("Аквамарин",WebЦвета.Аквамарин);
					СоответствиеWebЦвета.Вставить("Бежевый",WebЦвета.Бежевый);
					СоответствиеWebЦвета.Вставить("Белый",WebЦвета.Белый);
					СоответствиеWebЦвета.Вставить("Бирюзовый",WebЦвета.Бирюзовый);
					СоответствиеWebЦвета.Вставить("Голубой",WebЦвета.Голубой);
					СоответствиеWebЦвета.Вставить("Желтый",WebЦвета.Желтый);
					СоответствиеWebЦвета.Вставить("Зеленый",WebЦвета.Зеленый);
					СоответствиеWebЦвета.Вставить("Коричневый",WebЦвета.Коричневый);
					СоответствиеWebЦвета.Вставить("Красный",WebЦвета.Красный);
					СоответствиеWebЦвета.Вставить("Оливковый",WebЦвета.Оливковый);
					СоответствиеWebЦвета.Вставить("Оранжевый",WebЦвета.Оранжевый);
					СоответствиеWebЦвета.Вставить("Пурпурный",WebЦвета.Пурпурный);
					СоответствиеWebЦвета.Вставить("Розовый",WebЦвета.Розовый);
					СоответствиеWebЦвета.Вставить("Серый",WebЦвета.Серый);
					СоответствиеWebЦвета.Вставить("Синий",WebЦвета.Синий);
					СоответствиеWebЦвета.Вставить("Фиолетовый",WebЦвета.Фиолетовый);
					СоответствиеWebЦвета.Вставить("Черный",WebЦвета.Черный);
                    ЦветЯчейки = СоответствиеWebЦвета[ТекущаяЗаявкаНаРемонт.ВидРемонта.ЦветДляАРМ];
				КонецЕсли;
				//БИЗТЕХ 05.12.2023 Коваль А.Д.--

				ЦветЯчейки = ?(ЦветЯчейки <> Неопределено,ЦветЯчейки,ЦветаРаскраски.Получить("ЦветЗагрузкаНорм"));	
				
				//сформируем оформление ячейки
				ОчищатьЯчейку = НЕ (ЯчейкаТаблицыРасшифровок.НайтиПоЗначению(ЗаявкаПредыдущейЯчейки) <> ЯчейкаТаблицыРасшифровок.НайтиПоЗначению(ТекущаяЗаявкаНаРемонт)) ;
				Если ПоворотКалендаря = 1 Тогда 
					РамкаВерхняя = ?(ОчищатьЯчейку,ЛинияРамкиВнутренняя,ЛинияРамкиСплошная);
					РамкаНижняя = ЛинияРамкиВнутренняя;
				Иначе
					РамкаЛевая = ?(ОчищатьЯчейку,ЛинияРамкиВнутренняя,ЛинияРамкиСплошная);
					РамкаПравая = ЛинияРамкиВнутренняя;
				КонецЕсли;
				
				//получим представление документа
				Если НЕ ОчищатьЯчейку Тогда       
					Если ЗначениеЗаполнено(ТекущийЗаказНаряд) Тогда
                        ТекстЯчейки = ПолучитьПредставлениеЗаявкиНаРемонт(ТекущийЗаказНаряд,ФорматПредставленияЗаявкиНаРемонт);
					Иначе	
						ТекстЯчейки = ПолучитьПредставлениеЗаявкиНаРемонт(ТекущаяЗаявкаНаРемонт,ФорматПредставленияЗаявкиНаРемонт);
					КонецЕсли;
				КонецЕсли;	
				
				ЗаявкаПредыдущейЯчейки = ТекущаяЗаявкаНаРемонт;
				
			Иначе
				
				Если (ЗаявкаПредыдущейЯчейки <> Неопределено И ЗаявкаПредыдущейЯчейки <> Документы.ЗаявкаНаРемонт.ПустаяСсылка()) Тогда       
					Если ПоворотКалендаря = 1 Тогда 
						РамкаВерхняя = ЛинияРамкиСплошная;
					Иначе
						РамкаЛевая = ЛинияРамкиСплошная;	
					КонецЕсли;
				КонецЕсли;
				ТекстЯчейки = "";
				ЗаявкаПредыдущейЯчейки = Документы.ЗаявкаНаРемонт.ПустаяСсылка();
				
			КонецЕсли;
			
			//выделение текущей даты цветом
			ДатаТекущегоИнтервала = ТаблицаРасшифровок[0][ИндексИнтервала][0].Значение;
			Если ЦветЯчейки = Неопределено И ДатаТекущегоИнтервала <= ТекущаяДата И ДатаТекущегоИнтервала + ДлительностьИнтервала > ТекущаяДата Тогда
	 			ЦветЯчейки = ЦветаРаскраски.Получить("ЦветТекущаяДата");
			КонецЕсли;
			
			//раскрасим ячейку в соответствии с графиком
			Если ИспользованиеГрафиковРабот >= 1 И ЦветЯчейки = Неопределено Тогда
				
				ИнтервалВнеГрафика = Истина;
				Для Сч = 0 По ТаблицаГрафикаСтроки.Количество()-1 Цикл
					СтрокаТаблицыГрафика = ТаблицаГрафикаСтроки[Сч];
					НачалоРабочегоВремени = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.НачалоРабочегоВремени);
					КонецРабочегоВремени = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.КонецРабочегоВремени);
					Если НачалоРабочегоВремени <= ДатаТекущегоИнтервала	И ДатаТекущегоИнтервала < КонецРабочегоВремени Тогда
						Если СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.Работа Тогда
							ЦветЯчейки = ЦветаРаскраски.Получить("ЦветСвободно"); 
							ИнтервалВнеГрафика = Ложь;
							Прервать;	
						ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ОбеденныйПерерыв Тогда
							ЦветЯчейки = ЦветаРаскраски.Получить("ЦветОбеденныйПерерыв");
							ИнтервалВнеГрафика = Ложь;
							Прервать;	
						ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ТехническийПерерыв Тогда
							ЦветЯчейки = ЦветаРаскраски.Получить("ЦветТехническийПерерыв");
							ИнтервалВнеГрафика = Ложь;
							Прервать;
						Иначе
							ЦветЯчейки = ЦветаРаскраски.Получить("ЦветНерабочийИнтервал");
							ИнтервалВнеГрафика = Ложь;
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				// если дата интервала не входит в график, то установим признак нерабочего времени
				Если ИнтервалВнеГрафика Тогда
					ЦветЯчейки = ЦветаРаскраски.Получить("ЦветНеЗаполнено");
				КонецЕсли;
				
			КонецЕсли;
			
			//чередование четных и нечетных ячеек для незаполненных ячеек
			Если ЦветЯчейки = Неопределено ИЛИ ЦветЯчейки = ЦветаРаскраски.Получить("ЦветСвободно") Тогда
				Если ИндексИнтервала%2 = 0 Тогда
					ЦветЯчейки = ЦветаРаскраски.Получить("ЦветСвободно2");
				КонецЕсли;
			КонецЕсли;
			
			//отчеркивание календаря 
			Если ИндексИнтервала = ЧислоИнтерваловВремени-1 
				ИЛИ Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала+1].Имя,8)="НоваяДат" Тогда       
				Если ПоворотКалендаря = 1 Тогда 
					РамкаНижняя = ЛинияРамкиСплошная;
				Иначе
					РамкаПравая = ЛинияРамкиСплошная;
				КонецЕсли;
			КонецЕсли;
			                           
			НоваяСтрокаОформления 						= ТаблицаОформления.Добавить();
			НоваяСтрокаОформления.АдресЯчейки 			= "R" + Строка(ТекущаяКолонка+ОтступСверхуГоризонтально) + "C" + Строка(ИндексИнтервала+ОтступСлеваГоризонтально);
			НоваяСтрокаОформления.СмещениеКолонки 		= ИндексИнтервала;
			НоваяСтрокаОформления.СмещениеСтроки 		= СмещениеСтроки;
			НоваяСтрокаОформления.ЦветРамки 			= ЦветаРаскраски.Получить("ЦветРамкиСтандарт");
			НоваяСтрокаОформления.ЦветФона 				= ?(ЦветЯчейки <> Неопределено,ЦветЯчейки,ЦветаРаскраски.Получить("ЦветСвободно"));
			НоваяСтрокаОформления.ЛинияРамкиВерхняя 	= ?(РамкаВерхняя <> Неопределено, РамкаВерхняя, ?(ПоворотКалендаря = 1, ЛинияРамкиВнутренняя, ЛинияРамкиСплошная));
			НоваяСтрокаОформления.ЛинияРамкиНижняя 		= ?(РамкаНижняя <> Неопределено, РамкаНижняя, ?(ПоворотКалендаря = 1, ЛинияРамкиТочечная, ЛинияРамкиСплошная));
			НоваяСтрокаОформления.ЛинияРамкиЛевая 		= ?(РамкаЛевая <> Неопределено, РамкаЛевая, ?(ПоворотКалендаря = 1, ЛинияРамкиСплошная, ЛинияРамкиВнутренняя));
			НоваяСтрокаОформления.ЛинияРамкиПравая 		= ?(РамкаПравая <> Неопределено, РамкаПравая, ?(ПоворотКалендаря = 1, ЛинияРамкиСплошная, ЛинияРамкиТочечная));
			НоваяСтрокаОформления.ПредставлениеЗаявки 	= ?(ТекстЯчейки <> Неопределено ,ТекстЯчейки, Неопределено);				
			
			//БИЗТЕХ КОВАЛЬ 12.09.2024 ++
			//Логика для картинки
			Если ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт) И ЗначениеЗаполнено(ТекстЯчейки) Тогда
				//Найдем ЗН
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ЗаказНаряд.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.ЗаказНаряд КАК ЗаказНаряд
					|ГДЕ
					|	ЗаказНаряд.ДокументОснование = &ДокументОснование
					|
					|УПОРЯДОЧИТЬ ПО
					|	ЗаказНаряд.Дата УБЫВ";
				
				Запрос.УстановитьПараметр("ДокументОснование", ТекущаяЗаявкаНаРемонт.Ссылка);	
				РезультатЗапроса = Запрос.Выполнить().Выгрузить();
				ЗН = Неопределено;
				Если РезультатЗапроса.Количество() > 0 Тогда
					ЗН = РезультатЗапроса[0].Ссылка;
				Иначе
					ЗН = ТекущийЗаказНаряд;
				КонецЕсли;
				
				//Правила
				Если ЗначениеЗаполнено(ЗН) И ЗН.Проведен Тогда
					НоваяСтрокаОформления.Картинка				= КартинкаПоСостоянию["ЗНОформлен"];	
				ИначеЕсли ЗначениеЗаполнено(ЗН) И ЗН.ПометкаУдаления Тогда 
					НоваяСтрокаОформления.Картинка				= КартинкаПоСостоянию["ЗНПомеченНаУдаление"];
				ИначеЕсли ЗначениеЗаполнено(ЗН) И ЗН.Проведен = Ложь Тогда
					НоваяСтрокаОформления.Картинка				= КартинкаПоСостоянию["ЗННеПроведен"]; 
				ИначеЕсли ЗначениеЗаполнено(ЗН) = Ложь И ТекущаяЗаявкаНаРемонт.ДатаНачала < ТекущаяДата() Тогда
					НоваяСтрокаОформления.Картинка				= КартинкаПоСостоянию["ВремяПрошло"];
				ИначеЕсли ЗначениеЗаполнено(ЗН) = Ложь И ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт.ДатаПодтвержденияЗаписи) Тогда
					НоваяСтрокаОформления.Картинка				= КартинкаПоСостоянию["ЗаявкаПодтверждена"];
				ИначеЕсли ЗначениеЗаполнено(ЗН) = Ложь И НЕ ЗначениеЗаполнено(ТекущаяЗаявкаНаРемонт.ДатаПодтвержденияЗаписи) Тогда
					НоваяСтрокаОформления.Картинка				= КартинкаПоСостоянию["ЗаявкаНеПодтверждена"];
				Иначе
					НоваяСтрокаОформления.Картинка				= Неопределено;
				КонецЕсли;
			Иначе
				НоваяСтрокаОформления.Картинка				= Неопределено;
			КонецЕсли;
			//БИЗТЕХ КОВАЛЬ 12.09.2024 --
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры //СформироватьТаблицуОформления()

// Формирует табличный документ календаря
//
Процедура СформироватьМакетВертикально(ТабДок)
	
	// Расчет различных параметров диаграммы
	ТекущаяДата = ТекущаяДата();
	
	// Формирование шапки диаграммы
	
	Макет = Обработки.АРМЗаписьНаРемонт.ПолучитьМакет("МакетКалендарь");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаВремя1");
	ОбластьЗаголовок.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");
	Если РежимКалендаря = 1 Тогда
		ОбластьЗаголовок.ТекущаяОбласть.Текст = "Цех";
	ИначеЕсли РежимКалендаря = 2 Тогда
		ОбластьЗаголовок.ТекущаяОбласть.Текст = "Исполнитель";
	КонецЕсли;
	ТабДок.Вывести(ОбластьЗаголовок);
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаВремя2");
	ОбластьЗаголовок.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");
	ТабДок.Присоединить(ОбластьЗаголовок);
	
	ОбластьДеньНаКалендаре = ТабДок.Область("R1C1:R1C2");
	ОбластьДеньНаКалендаре.Объединить();	
	
	МассивПредставлений = ПолучитьМассивПредставленийРабочихМест();	
	ДлинаПредставления = ПолучитьМаксимальнуюДлинуСтрокиВМассиве(МассивПредставлений);
	
	Для СчетчикРабочихМест = 1 По ЧислоРабочихМест Цикл
		ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаРабочееМесто");
		ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки =  ШиринаКолонкиВертикально;
		ОбластьЗаголовок.Параметры.РабочееМесто = МассивПредставлений[СчетчикРабочихМест-1];
		ОбластьЗаголовок.Параметры.рсшРабочееМесто = ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто;
		ОбластьЗаголовок.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");
		ТабДок.Присоединить(ОбластьЗаголовок);
		
		НайденнаяСтрока = ТаблицаПозицийРабочихМест[СчетчикРабочихМест-1];
		Если НайденнаяСтрока = Неопределено Тогда 
			Продолжить; 
		ИначеЕсли НайденнаяСтрока.ОтображениеУРВ Тогда
			ОбластьЗаголовокУРВ = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаУРВ");
			ТабДок.Присоединить(ОбластьЗаголовокУРВ);
		КонецЕсли;
	
	КонецЦикла;
	
	// Формирование основной части диаграммы
	
	//обходим таблицу расшифровок и выводим построчно данные каждого интервала
	Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
		
		ИмяКолонкиТР = ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя;
		ТекущаяДатаИнтервала = ТаблицаРасшифровок[0][ИмяКолонкиТР][0].Значение;
		
		//Текущий календарный день
		Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
				
				//колонка с надписью "дата"
				ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("СтрокаТаблицыДата|КолонкаВремя1");
				ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
				ТабДок.Вывести(ОбластьДеньНаКалендаре);
				
				//колонка с надписью "дата"
				ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("СтрокаТаблицыДата|КолонкаВремя2");
				ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
				ТабДок.Присоединить(ОбластьДеньНаКалендаре);
				
				ОбластьДеньНаКалендаре = ТабДок.Область("R" + (ИндексИнтервала+1) + "C1:" + "R" + (ИндексИнтервала+1) + "C2");
				ОбластьДеньНаКалендаре.Объединить();	
				
				Если ЧислоРабочихМест > 0 Тогда
					
					//колонка с датой 
					ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("СтрокаТаблицыДата|КолонкаРабочееМесто");
					ОбластьДеньНаКалендаре.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиВертикально;
				
					ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
					ДатаЗаголовка = ТекущаяДатаИнтервала;
					ОбластьДеньНаКалендаре.ТекущаяОбласть.Текст = Формат(ДатаЗаголовка,"ДЛФ=DD");
					ТабДок.Присоединить(ОбластьДеньНаКалендаре);
				
					ОбластьДеньНаКалендаре = ТабДок.Область("R" + (ИндексИнтервала+1) + "C3:" + "R" + (ИндексИнтервала+1) + "C" + (ЧислоЯчеекДиаграммы+ОтступСлеваВертикально));
					ОбластьДеньНаКалендаре.Обвести(ЛинияРамкиСплошная,ЛинияРамкиСплошная,ЛинияРамкиСплошная,ЛинияРамкиСплошная);
					ОбластьДеньНаКалендаре.Объединить();	
				КонецЕсли;
				
			Продолжить;
		КонецЕсли;	
		
		//выводим левую колонку с временем и раскрашиваем по графику
		ОбластьВремя = Макет.ПолучитьОбласть("СтрокаТаблицыГруппировка|КолонкаВремя1");
		ОбластьВремя.Параметры.ПредставлениеИнтервала = Формат(ТекущаяДатаИнтервала,"ДФ=ЧЧ");			
		ОбластьВремя.Параметры.рсшПредставлениеИнтервала = ТекущаяДатаИнтервала;
		Если ИспользованиеГрафиковРабот >= 1 И ГрафикиРабочихМест.Количество()>0 Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("Дата",НачалоДня(ТекущаяДатаИнтервала));
			Отбор.Вставить("ГрафикРаботы",БазовыйГрафик);
			НайденныеСтрокиГрафика = ГрафикиРабочихМест.НайтиСтроки(Отбор);
			КопияТаблицыГрафиковРабот = ГрафикиРабочихМест.Скопировать(НайденныеСтрокиГрафика);
			
			ИнтервалВнеГрафика = Истина;
			Для Сч = 0 По КопияТаблицыГрафиковРабот.Количество()-1 Цикл
				СтрокаТаблицыГрафика = КопияТаблицыГрафиковРабот[Сч];
				НачалоИнтервалаПоГрафику = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.НачалоРабочегоВремени);
				КонецИнтервалаПоГрафику = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.КонецРабочегоВремени);
				Если НачалоИнтервалаПоГрафику<=ТекущаяДатаИнтервала
					И ТекущаяДатаИнтервала < КонецИнтервалаПоГрафику Тогда
					// если нашли интервал
					Если СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.Работа Тогда
						ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно"); 
						ИнтервалВнеГрафика = Ложь;
						Прервать;	
					ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ОбеденныйПерерыв Тогда
						ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветОбеденныйПерерыв");
						ИнтервалВнеГрафика = Ложь;
						Прервать;	
					ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ТехническийПерерыв Тогда
						ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветТехническийПерерыв");
						ИнтервалВнеГрафика = Ложь;
						Прервать;
					Иначе
						ЦветЯчейки = ЦветаРаскраски.Получить("ЦветНерабочийИнтервал");
						ИнтервалВнеГрафика = Ложь;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			// если дата интервала не входит в график, то установим признак нерабочего времени
			Если ИнтервалВнеГрафика Тогда
				ЦветЯчейки = ЦветаРаскраски.Получить("ЦветНеЗаполнено");
			КонецЕсли;
		КонецЕсли;
		ТабДок.Вывести(ОбластьВремя);
		
		ОбластьВремя2 = Макет.ПолучитьОбласть("СтрокаТаблицыГруппировка|КолонкаВремя2");
		ОбластьВремя2.Параметры.ПредставлениеИнтервала = Формат(ТекущаяДатаИнтервала,"ДФ=мм");			
		ОбластьВремя2.Параметры.рсшПредставлениеИнтервала = ТекущаяДатаИнтервала;
		Если ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка") Тогда
			Если ИндексИнтервала%2 = 0 Тогда
				ОбластьВремя2.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно2");
			КонецЕсли;
		Иначе	
			ОбластьВремя2.ТекущаяОбласть.ЦветФона = ОбластьВремя.ТекущаяОбласть.ЦветФона;
		КонецЕсли;
		
		Если ТекущаяДатаИнтервала <= ТекущаяДата И ТекущаяДатаИнтервала + ДлительностьИнтервала > ТекущаяДата Тогда
			ОбластьВремя2.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветТекущаяДата");
		КонецЕсли;
		
		ТабДок.Присоединить(ОбластьВремя2);
		
		//выводим колонки календаря для текущего интервала времени
		Для СчетчикРабочихМест = 1 По ЧислоРабочихМест Цикл
			
			ОбластьЯчейкаСтроки = Макет.ПолучитьОбласть("СтрокаТаблицыГруппировка|КолонкаРабочееМесто");
			ОбластьЯчейкаСтроки.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиВертикально;
			
			//выделение текущей даты цветом
			Если ТекущаяДатаИнтервала <= ТекущаяДата И ТекущаяДатаИнтервала + ДлительностьИнтервала > ТекущаяДата Тогда
				ОбластьЯчейкаСтроки.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветТекущаяДата");
			КонецЕсли;
			
			//первая ячейка - представление работы
			СтруктураРасшифровки = Новый Структура;
			СтруктураРасшифровки.Вставить("РабочееМесто",ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто);
			СтруктураРасшифровки.Вставить("ДатаИнтервала", ТекущаяДатаИнтервала);
			ЗначениеЯчейки = ТаблицаРасшифровок[СчетчикРабочихМест][ИмяКолонкиТР];
			Если ТипЗнч(ЗначениеЯчейки) = Тип("СписокЗначений") И ЗначениеЯчейки.Количество() > 0 Тогда
				СтруктураРасшифровки.Вставить("Данные",ЗначениеЯчейки);
			Иначе
				СтруктураРасшифровки.Вставить("Данные","СвободнаяЯчейка");
			КонецЕсли;
			ОбластьЯчейкаСтроки.Параметры.рсшСтруктура = СтруктураРасшифровки;
			ТабДок.Присоединить(ОбластьЯчейкаСтроки);
			
			//вторая ячейка - данные УРВ
			НайденнаяСтрока = ТаблицаПозицийРабочихМест[СчетчикРабочихМест-1];
			Если НайденнаяСтрока = Неопределено Тогда 
				Продолжить; 
			ИначеЕсли НайденнаяСтрока.ОтображениеУРВ Тогда
				СтруктураРасшифровкиУРВ = Новый Структура;
				СтруктураРасшифровкиУРВ.Вставить("РабочееМесто",ТаблицаДанныхУРВ[СчетчикРабочихМест].РабочееМесто);
				СтруктураРасшифровкиУРВ.Вставить("ДатаИнтервала", ТекущаяДатаИнтервала);
				СтруктураРасшифровкиУРВ.Вставить("УРВ",Истина);
				
				ОбластьЯчейкаУРВ = Макет.ПолучитьОбласть("СтрокаТаблицыГруппировка|КолонкаУРВ");
				ОбластьЯчейкаУРВ.ТекущаяОбласть.Имя = "Состояние_Пусто_0_"+Строка(ИндексИнтервала)+"_"+Строка(СчетчикРабочихМест);
				ЗначениеЯчейкиУРВ = ТаблицаДанныхУРВ[СчетчикРабочихМест][ИндексИнтервала];
				Если ТипЗнч(ЗначениеЯчейкиУРВ) = Тип("СписокЗначений") И ЗначениеЯчейкиУРВ.Количество() > 0 Тогда
					СтруктураРасшифровкиУРВ.Вставить("Данные",ЗначениеЯчейкиУРВ);
				Иначе
					СтруктураРасшифровкиУРВ.Вставить("Данные","СвободнаяЯчейка");
				КонецЕсли;
				
				ОбластьЯчейкаУРВ.Параметры.СтруктураУРВ = СтруктураРасшифровкиУРВ;
				ТабДок.Присоединить(ОбластьЯчейкаУРВ);
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла;
	
	//объединение интервалов в колонке №1
	Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
		
		ИмяКолонкиТР = ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя;
		ТекущаяДатаИнтервала = ТаблицаРасшифровок[0][ИмяКолонкиТР][0].Значение;
		
		Если Лев(ИмяКолонкиТР,8) = "НоваяДат" Тогда 
			Продолжить;	
		ИначеЕсли Минута(ТекущаяДатаИнтервала) = 0 Тогда
			Если ИнтервалКалендаря = 10 Тогда
				ЧислоКолонок = 6;
			ИначеЕсли ИнтервалКалендаря = 15 Тогда
				ЧислоКолонок = 4;
			ИначеЕсли ИнтервалКалендаря = 20 Тогда
				ЧислоКолонок = 3;
			ИначеЕсли ИнтервалКалендаря = 30 Тогда
				ЧислоКолонок = 2;
			Иначе
				ЧислоКолонок = 1;
			КонецЕсли;
			НачалоОбластиОбъединения = ИндексИнтервала + ОтступСверхуВертикально;
			КонецОбластиОбъединения = НачалоОбластиОбъединения + ЧислоКолонок - 1;
			ОбластьЯчеекРабочееМесто = ТабДок.Область("R" + НачалоОбластиОбъединения + "C1:R" + Мин(КонецОбластиОбъединения,ЧислоИнтерваловВремени) +  "C1");
			
			//выделение текущей даты цветом
			Если НачалоЧаса(ТекущаяДатаИнтервала) <= ТекущаяДата И КонецЧаса(ТекущаяДатаИнтервала) >= ТекущаяДата Тогда
				ОбластьЯчеекРабочееМесто.ЦветФона = ЦветаРаскраски.Получить("ЦветТекущаяДата");
			КонецЕсли;
			
			ОбластьЯчеекРабочееМесто.Объединить();	
		КонецЕсли;			
		
	КонецЦикла;
	
	Для Каждого СтрокаОформления Из ТаблицаОформления Цикл
		
		//ДАННЫЕ ПО РАБОТАМ
		
		ОбластьЯчеекОсновная = ТабДок.Область("R" + Строка(СтрокаОформления.СмещениеКолонки+ОтступСверхуВертикально) + "C" + Строка(СтрокаОформления.СмещениеСтроки + ОтступСлеваВертикально));
		ОбластьЯчеекОсновная.Текст = СтрокаОформления.ПредставлениеЗаявки;
		ОбластьЯчеекОсновная.Обвести(СтрокаОформления.ЛинияРамкиЛевая,СтрокаОформления.ЛинияРамкиВерхняя,СтрокаОформления.ЛинияРамкиПравая,СтрокаОформления.ЛинияРамкиНижняя);
		
		Если СтрокаОформления.ЦветФона <> Неопределено Тогда
			ОбластьЯчеекОсновная.ЦветФона = СтрокаОформления.ЦветФона;
		КонецЕсли;
		
		Если ОбластьЯчеекОсновная.Расшифровка = Неопределено Тогда
			Продолжить;		
		КонецЕсли;
		
		//ДАННЫЕ УРВ
		
		НайденнаяСтрока = ТаблицаПозицийРабочихМест.Найти(СтрокаОформления.СмещениеСтроки,"Смещение");
		Если НайденнаяСтрока <> Неопределено И НайденнаяСтрока.ОтображениеУРВ Тогда 
			ОбластьЯчеекУРВ = ТабДок.Область("R" + Строка(ОбластьЯчеекОсновная.Верх) + "C" + Строка(ОбластьЯчеекОсновная.Лево+1));
			ЛинияРамкиНижняя = ?(СтрокаОформления.ЛинияРамкиНижняя = ЛинияРамкиСплошная, СтрокаОформления.ЛинияРамкиНижняя, ЛинияРамкиВнутренняя);
			ОбластьЯчеекУРВ.Обвести(СтрокаОформления.ЛинияРамкиЛевая,ЛинияРамкиВнутренняя,СтрокаОформления.ЛинияРамкиПравая, ЛинияРамкиНижняя);
			ОбластьЯчеекУРВ.ЦветФона = ПолучитьЦветОформленияЯчейкиУРВ(ОбластьЯчеекОсновная.Расшифровка.Данные,ОбластьЯчеекУРВ.Расшифровка.Данные);
			ТекстЯчейки = ПолучитьТекстОформленияЯчейкиУРВ(ОбластьЯчеекУРВ.Расшифровка.Данные);
			Если ТекстЯчейки <> "" Тогда
				ОбластьЯчеекУРВ.Текст = Прав(ТекстЯчейки,4);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;       
	
КонецПроцедуры

// Формирует табличный документ календаря
//
Процедура СформироватьМакетГоризонтально(ТабДок)
	
	// Расчет различных параметров диаграммы
	ТекущаяДата = ТекущаяДата();
	МассивПредставлений = ПолучитьМассивПредставленийРабочихМест();		
	ДлинаПредставления = ПолучитьМаксимальнуюДлинуСтрокиВМассиве(МассивПредставлений);
	ШиринаКолонкиРабочееМесто = 5+Макс(ДлинаПредставления,10);
	
	// Формирование первой строки

	Макет = Обработки.АРМЗаписьНаРемонт.ПолучитьМакет("МакетГоризонтальный");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаРабочееМесто");
	ОбластьЗаголовок.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");
	Если РежимКалендаря = 1 Тогда
		ОбластьЗаголовок.ТекущаяОбласть.Текст = "Цех";
	ИначеЕсли РежимКалендаря = 2 Тогда
		ОбластьЗаголовок.ТекущаяОбласть.Текст = "Исполнитель";		
	КонецЕсли;
	ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиРабочееМесто;		
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
		
		ИмяКолонкиТР = ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя;
		ТекущаяДатаИнтервала = ТаблицаРасшифровок[0][ИмяКолонкиТР][0].Значение;
		
		Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
			//Если ОбработкаОбъект.ЧислоРабочихМест > 0 Тогда
				ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаДата");
				ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
				ТабДок.Присоединить(ОбластьДеньНаКалендаре);
			//КонецЕсли;
			Продолжить;
		КонецЕсли;	
		
		//выводим левую колонку с временем и раскрашиваем по графику
		ОбластьВремя = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаИнтервал");
		ОбластьВремя.Параметры.ПредставлениеИнтервала = Формат(ТекущаяДатаИнтервала,"ДФ=ЧЧ");			
		ОбластьВремя.Параметры.рсшПредставлениеИнтервала = ТекущаяДатаИнтервала;
		Если ИспользованиеГрафиковРабот >= 1 И ГрафикиРабочихМест.Количество()>0 Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("Дата",НачалоДня(ТекущаяДатаИнтервала));
			Отбор.Вставить("ГрафикРаботы",БазовыйГрафик);
			НайденныеСтрокиГрафика = ГрафикиРабочихМест.НайтиСтроки(Отбор);
			КопияТаблицыГрафиковРабот = ГрафикиРабочихМест.Скопировать(НайденныеСтрокиГрафика);
			
			ИнтервалВнеГрафика = Истина;
			Для Сч = 0 По КопияТаблицыГрафиковРабот.Количество()-1 Цикл
				СтрокаТаблицыГрафика = КопияТаблицыГрафиковРабот[Сч];
				НачалоИнтервалаПоГрафику = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.НачалоРабочегоВремени);
				КонецИнтервалаПоГрафику = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.КонецРабочегоВремени);
				Если НачалоИнтервалаПоГрафику<=ТекущаяДатаИнтервала
					И ТекущаяДатаИнтервала < КонецИнтервалаПоГрафику Тогда
					// если нашли интервал
					Если СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.Работа Тогда
						ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно"); 
						ИнтервалВнеГрафика = Ложь;
						Прервать;	
					ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ОбеденныйПерерыв Тогда
						ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветОбеденныйПерерыв");
						ИнтервалВнеГрафика = Ложь;
						Прервать;	
					ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ТехническийПерерыв Тогда
						ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветТехническийПерерыв");
						ИнтервалВнеГрафика = Ложь;
						Прервать;
					Иначе
						ЦветЯчейки = ЦветаРаскраски.Получить("ЦветНерабочийИнтервал");
						ИнтервалВнеГрафика = Ложь;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			// если дата интервала не входит в график, то установим признак нерабочего времени
			Если ИнтервалВнеГрафика Тогда
				ЦветЯчейки = ЦветаРаскраски.Получить("ЦветНеЗаполнено");
			КонецЕсли;
		КонецЕсли;
		ОбластьВремя.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиГоризонтально;
		ТабДок.Присоединить(ОбластьВремя);
	КонецЦикла;
	
	// Формирование второй строки
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокТаблицы1|КолонкаРабочееМесто");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
		
		ИмяКолонкиТР = ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя;
		ТекущаяДатаИнтервала = ТаблицаРасшифровок[0][ИмяКолонкиТР][0].Значение;
		
		Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
			//Если ОбработкаОбъект.ЧислоРабочихМест > 0 Тогда
				ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаДата");
				ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
				ТабДок.Присоединить(ОбластьДеньНаКалендаре);
			//КонецЕсли;
			Продолжить;
		КонецЕсли;	
		
		//выводим левую колонку с временем и раскрашиваем по графику
		ОбластьВремя = Макет.ПолучитьОбласть("ЗаголовокТаблицы1|КолонкаИнтервал");
		ОбластьВремя.Параметры.ПредставлениеИнтервала = Формат(ТекущаяДатаИнтервала,"ДФ=мм");			
		ОбластьВремя.Параметры.рсшПредставлениеИнтервала = ТекущаяДатаИнтервала;
		
		ОбластьВремя1 = ТабДок.Область("R1C" + (ИндексИнтервала + ОтступСлеваГоризонтально) + ":R1C" + (ИндексИнтервала + ОтступСлеваГоризонтально));
		Если ОбластьВремя1.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка") Тогда
			Если ИндексИнтервала%2 = 0 Тогда
				ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно2");
			КонецЕсли;
		Иначе	
			ОбластьВремя.ТекущаяОбласть.ЦветФона = ОбластьВремя1.ЦветФона;
		КонецЕсли;
		
		Если ТекущаяДатаИнтервала <= ТекущаяДата И ТекущаяДатаИнтервала + ДлительностьИнтервала > ТекущаяДата Тогда
			ОбластьВремя.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветТекущаяДата");
		КонецЕсли;
		
		ОбластьВремя.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиГоризонтально;
		ТабДок.Присоединить(ОбластьВремя);
	КонецЦикла;	
	
	// Формирование основной части диаграммы
	
	Для СчетчикРабочихМест = 1 По ЧислоРабочихМест Цикл
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("СтрокаРабочееМесто|КолонкаРабочееМесто");
        ОбластьЗаголовок.Параметры.РабочееМесто = МассивПредставлений[СчетчикРабочихМест-1];
			
		ОбластьЗаголовок.Параметры.рсшРабочееМесто = ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто;
		
		//БИЗТЕХ КОВАЛЬ 24.09.2024 ++
		//Если по исполнителям то расскрашиваем в зависимости от группы в карточке сотрудника
		Если РежимКалендаря = 2 Тогда
			Сотрудник = ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто;
			ГруппаСотрудника = Сотрудник.ГруппаДляОтображенияВАРМ;
			//В будущем справочник Групп
			ЦветГруппы = новый Соответствие;
			ЦветГруппы.Вставить(Перечисления.ГруппыДляОтображенияВАРМ.Приемка, ЦветаРаскраски.Получить("ЦветНапоминание"));
			ЦветГруппы.Вставить(Перечисления.ГруппыДляОтображенияВАРМ.Ремзона, ЦветаРаскраски.Получить("ЦветЗагрузкаМин"));	
			Если ЗначениеЗаполнено(ГруппаСотрудника) Тогда
				ОбластьЗаголовок.ТекущаяОбласть.ЦветФона = ЦветГруппы.Получить(ГруппаСотрудника);	
			Иначе
				ОбластьЗаголовок.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");
			КонецЕсли;
			
			//БИЗТЕХ КОВАЛЬ 12.09.2025 ++ 
			Если Сотрудник.ГруппаДляОтображенияВАРМ = Перечисления.ГруппыДляОтображенияВАРМ.Приемка Тогда 
				СтрокаССуммойМК = ТаблицаСуммМК.Найти(Сотрудник,"Диспетчер");
				Если СтрокаССуммойМК <> Неопределено Тогда
					СуммаМК = СтрокаССуммойМК.Сумма;
				Иначе
					СуммаМК = 0;
				КонецЕсли;
				//правим представление
				ОбластьЗаголовок.Параметры.РабочееМесто = ОбластьЗаголовок.Параметры.РабочееМесто + Символы.ПС + "Сумма МК: " + СуммаМК + " руб.";
				ОбластьЗаголовок.ВыделенныеОбласти[0].ВысотаСтроки = ОбластьЗаголовок.ВыделенныеОбласти[0].ВысотаСтроки + 11;
			КонецЕсли;
			//БИЗТЕХ КОВАЛЬ 12.09.2025 --
			
		КонецЕсли;
		//БИЗТЕХ КОВАЛЬ 24.09.2024 ++
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ИндексТекущейДаты = -1;
		
		Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
			
			ТекущаяДатаИнтервала = ТаблицаРасшифровок[0][ИндексИнтервала][0].Значение;
			
			//если это первая строчка в дне, выводим шапку с текущим календарным днём
			Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
				Если ЧислоРабочихМест > 0 Тогда
					ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаДата");
					ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
					Если СчетчикРабочихМест = 1 Тогда
						ДатаЗаголовка = ТекущаяДатаИнтервала;
						Если ЧислоЯчеекДиаграммы < 5 Тогда 
							ОбластьДеньНаКалендаре.ТекущаяОбласть.Текст = Формат(ДатаЗаголовка,"ДФ=dd.MM");
						Иначе
							ОбластьДеньНаКалендаре.ТекущаяОбласть.Текст = Формат(ДатаЗаголовка,"ДЛФ=DD");
						КонецЕсли;
					КонецЕсли;
					ТабДок.Присоединить(ОбластьДеньНаКалендаре);
				КонецЕсли;
				Продолжить;
			КонецЕсли;	
			
			СтруктураРасшифровки = Новый Структура;
			СтруктураРасшифровки.Вставить("РабочееМесто",ТаблицаРасшифровок[СчетчикРабочихМест][0]);
			СтруктураРасшифровки.Вставить("ДатаИнтервала", ТекущаяДатаИнтервала);
			ЗначениеЯчейки = ТаблицаРасшифровок[СчетчикРабочихМест][ИндексИнтервала];
			Если ТипЗнч(ЗначениеЯчейки) = Тип("СписокЗначений") И ЗначениеЯчейки.Количество() > 0 Тогда
				СтруктураРасшифровки.Вставить("Данные",ЗначениеЯчейки);
			Иначе
				СтруктураРасшифровки.Вставить("Данные","СвободнаяЯчейка");
			КонецЕсли;
			
			ОбластьЯчейкаСтроки = Макет.ПолучитьОбласть("СтрокаРабочееМесто|КолонкаИнтервал");
			ОбластьЯчейкаСтроки.Параметры.рсшСтруктура = СтруктураРасшифровки;
			ОбластьЯчейкаСтроки.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиГоризонтально;
			ТабДок.Присоединить(ОбластьЯчейкаСтроки);
			
		КонецЦикла;
		
		НайденнаяСтрока = ТаблицаПозицийРабочихМест[СчетчикРабочихМест-1];
		Если НайденнаяСтрока = Неопределено Тогда 
			Продолжить; 
		ИначеЕсли НайденнаяСтрока.ОтображениеУРВ Тогда
			
			ОбластьЯчейкаУРВ = Макет.ПолучитьОбласть("СтрокаУРВ|КолонкаРабочееМесто");
			ТабДок.Вывести(ОбластьЯчейкаУРВ);
			
			Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
				
				//если это первая строчка в дне, выводим шапку с текущим календарным днём
				Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
					Если ЧислоРабочихМест > 0 Тогда
						ОбластьДеньНаКалендаре = Макет.ПолучитьОбласть("СтрокаУРВ|КолонкаДата");
						ОбластьДеньНаКалендаре.ТекущаяОбласть.ЦветФона = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
						ТабДок.Присоединить(ОбластьДеньНаКалендаре);
					КонецЕсли;
					Продолжить;
				КонецЕсли;			
				
				СтруктураРасшифровкиУРВ = Новый Структура;
				СтруктураРасшифровкиУРВ.Вставить("РабочееМесто",ТаблицаДанныхУРВ[СчетчикРабочихМест].РабочееМесто);
				СтруктураРасшифровкиУРВ.Вставить("ДатаИнтервала", ТекущаяДатаИнтервала);
				СтруктураРасшифровкиУРВ.Вставить("УРВ",Истина);
				ЗначениеЯчейкиУРВ = ТаблицаДанныхУРВ[СчетчикРабочихМест][ИндексИнтервала];
				Если ТипЗнч(ЗначениеЯчейкиУРВ) = Тип("СписокЗначений") И ЗначениеЯчейкиУРВ.Количество() > 0 Тогда
					СтруктураРасшифровкиУРВ.Вставить("Данные",ЗначениеЯчейкиУРВ);
					ДанныеТекущейЯчейкиУРВ = ЗначениеЯчейкиУРВ[0];
					ТекДокумент = ДанныеТекущейЯчейкиУРВ.Значение[3];
				Иначе
					СтруктураРасшифровкиУРВ.Вставить("Данные","СвободнаяЯчейка");
				КонецЕсли;
				
				ОбластьЯчейкаУРВ = Макет.ПолучитьОбласть("СтрокаУРВ|КолонкаИнтервал");
				ОбластьЯчейкаУРВ.Параметры.СтруктураУРВ = СтруктураРасшифровкиУРВ;
				ОбластьЯчейкаУРВ.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиГоризонтально;
				ТабДок.Присоединить(ОбластьЯчейкаУРВ);
			КонецЦикла;
		КонецЕсли;
      
	КонецЦикла;
	
	//объединение интервалов в строке №1 и в колонках с датой
	Для ИндексИнтервала = 1 По ЧислоИнтерваловВремени-1 Цикл
		
		ИмяКолонкиТР = ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя;
		ТекущаяДатаИнтервала = ТаблицаРасшифровок[0][ИмяКолонкиТР][0].Значение;
		
		Если Лев(ИмяКолонкиТР,8) = "НоваяДат" Тогда 
			Если ЧислоРабочихМест > 0 Тогда
				ОбластьДата = ТабДок.Область("R" + (ОтступСверхуГоризонтально+1) + "C" + (ИндексИнтервала+1) + ":R" + (ЧислоЯчеекДиаграммы + ОтступСверхуГоризонтально) + "C" + (ИндексИнтервала+1));
				ОбластьДата.Обвести(ЛинияРамкиСплошная,ЛинияРамкиСплошная,ЛинияРамкиСплошная,ЛинияРамкиСплошная);
				ОбластьДата.Объединить();	
			КонецЕсли;
		ИначеЕсли Минута(ТекущаяДатаИнтервала) = 0 Тогда
			Если ИнтервалКалендаря = 10 Тогда
				ЧислоКолонок = 6;
			ИначеЕсли ИнтервалКалендаря = 15 Тогда
				ЧислоКолонок = 4;
			ИначеЕсли ИнтервалКалендаря = 20 Тогда
				ЧислоКолонок = 3;
			ИначеЕсли ИнтервалКалендаря = 30 Тогда
				ЧислоКолонок = 2;
			Иначе
				ЧислоКолонок = 1;
			КонецЕсли;
			НачалоОбластиОбъединения = ИндексИнтервала + ОтступСверхуВертикально;
			КонецОбластиОбъединения = НачалоОбластиОбъединения + ЧислоКолонок - 1;
			ОбластьЯчеекРабочееМесто = ТабДок.Область("R1C" + НачалоОбластиОбъединения + ":R1C" + Мин(КонецОбластиОбъединения,ЧислоИнтерваловВремени));
			
			//выделение текущей даты цветом
			Если НачалоЧаса(ТекущаяДатаИнтервала) <= ТекущаяДата И КонецЧаса(ТекущаяДатаИнтервала) >= ТекущаяДата Тогда
				ОбластьЯчеекРабочееМесто.ЦветФона = ЦветаРаскраски.Получить("ЦветТекущаяДата");
			КонецЕсли;
			
			ОбластьЯчеекРабочееМесто.Объединить();	
		КонецЕсли;			
		
	КонецЦикла;
	
	Для Каждого СтрокаОформления Из ТаблицаОформления Цикл
		
		//ДАННЫЕ ПО РАБОТАМ
		
		ОбластьЯчеекОсновная = ТабДок.Область("R" + Строка(СтрокаОформления.СмещениеСтроки+ОтступСверхуГоризонтально) + "C" + Строка(СтрокаОформления.СмещениеКолонки+ОтступСлеваГоризонтально));
		ОбластьЯчеекОсновная.Текст = СтрокаОформления.ПредставлениеЗаявки;
		ОбластьЯчеекОсновная.Обвести(СтрокаОформления.ЛинияРамкиЛевая,СтрокаОформления.ЛинияРамкиВерхняя,СтрокаОформления.ЛинияРамкиПравая,СтрокаОформления.ЛинияРамкиНижняя);
		
		//БИЗТЕХ Установка картинки из таблицы обормления
		Если ОбластьЯчеекОсновная <> Неопределено И ЗначениеЗаполнено(СтрокаОформления.Картинка) Тогда
			ОбластьЯчеекОсновная.Картинка = СтрокаОформления.Картинка;
			ОбластьЯчеекОсновная.РазмерКартинки = РазмерКартинки.АвтоРазмер;
		КонецЕсли;
		
		Если СтрокаОформления.ЦветФона <> Неопределено Тогда
			ОбластьЯчеекОсновная.ЦветФона = СтрокаОформления.ЦветФона;
		КонецЕсли;

		Если ОбластьЯчеекОсновная.Расшифровка = Неопределено Тогда
			Продолжить;		
		КонецЕсли;
		
		//ДАННЫЕ УРВ
		
		НайденнаяСтрока = ТаблицаПозицийРабочихМест.Найти(СтрокаОформления.СмещениеСтроки,"Смещение");
		Если НайденнаяСтрока <> Неопределено И НайденнаяСтрока.ОтображениеУРВ Тогда 
			ОбластьЯчеекУРВ = ТабДок.Область("R" + Строка(ОбластьЯчеекОсновная.Верх+1) + "C" + Строка(ОбластьЯчеекОсновная.Лево));
			ЛинияРамкиПравая = ?(СтрокаОформления.ЛинияРамкиПравая = ЛинияРамкиСплошная, СтрокаОформления.ЛинияРамкиПравая, ЛинияРамкиВнутренняя);
			ОбластьЯчеекУРВ.Обвести(ЛинияРамкиВнутренняя,СтрокаОформления.ЛинияРамкиВерхняя,ЛинияРамкиПравая,СтрокаОформления.ЛинияРамкиНижняя);
			ОбластьЯчеекУРВ.ЦветФона = ПолучитьЦветОформленияЯчейкиУРВ(ОбластьЯчеекОсновная.Расшифровка.Данные,ОбластьЯчеекУРВ.Расшифровка.Данные);
			ТекстЯчейки = ПолучитьТекстОформленияЯчейкиУРВ(ОбластьЯчеекУРВ.Расшифровка.Данные);
			Если ТекстЯчейки <> "" Тогда
				ОбластьЯчеекУРВ.Текст = Прав(ТекстЯчейки,4);
			КонецЕсли;
		КонецЕсли;
		
		//Заполняем колонку после таблицы справа пустой строкой, чтобы текст не выходил за границы таблицу
		Если СтрокаОформления.СмещениеКолонки = ЧислоИнтерваловВремени-1 Тогда
			ОбластьТекст = ТабДок.Область("R" + Строка(СтрокаОформления.СмещениеСтроки+ОтступСверхуГоризонтально) + "C" + Строка(СтрокаОформления.СмещениеКолонки+1+ОтступСлеваГоризонтально));
			ОбластьТекст.Текст = "";
		КонецЕсли;  
				
	КонецЦикла;
КонецПроцедуры

// Формирует таблицу, содержащую данные легенды
//
Функция СформироватьТаблицуЛегенды()
	
	//получаем служебные цвета
	ЦветаСлужебные = Новый ТаблицаЗначений;
	ЦветаСлужебные.Колонки.Добавить("Представление");
	ЦветаСлужебные.Колонки.Добавить("Цвет");
	
	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Свободно";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветРабота");
	
	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Обеденный перерыв";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветОбеденныйПерерыв");
	
	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Технический перерыв";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветТехническийПерерыв");

	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Нерабочий интервал";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветНерабочийИнтервал");
	
	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Вне графика";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветНеЗаполнено");
	
	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Текущее время";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветТекущаяДата");
	
	НовыйЦвет = ЦветаСлужебные.Добавить();
	НовыйЦвет.Представление = "Служебная ячейка";
	НовыйЦвет.Цвет = ЦветаРаскраски.Получить("ЦветСлужебнаяЯчейка");
		
	//БИЗТЕХ 05.12.2023 Коваль А.Д.++
    //получаем цвета состояний заказ-нарядов
	СоответствиеWebЦвета=Новый Соответствие;
	СоответствиеWebЦвета.Вставить("Аквамарин",WebЦвета.Аквамарин);
	СоответствиеWebЦвета.Вставить("Бежевый",WebЦвета.Бежевый);
	СоответствиеWebЦвета.Вставить("Белый",WebЦвета.Белый);
	СоответствиеWebЦвета.Вставить("Бирюзовый",WebЦвета.Бирюзовый);
	СоответствиеWebЦвета.Вставить("Голубой",WebЦвета.Голубой);
	СоответствиеWebЦвета.Вставить("Желтый",WebЦвета.Желтый);
	СоответствиеWebЦвета.Вставить("Зеленый",WebЦвета.Зеленый);
	СоответствиеWebЦвета.Вставить("Коричневый",WebЦвета.Коричневый);
	СоответствиеWebЦвета.Вставить("Красный",WebЦвета.Красный);
	СоответствиеWebЦвета.Вставить("Оливковый",WebЦвета.Оливковый);
	СоответствиеWebЦвета.Вставить("Оранжевый",WebЦвета.Оранжевый);
	СоответствиеWebЦвета.Вставить("Пурпурный",WebЦвета.Пурпурный);
	СоответствиеWebЦвета.Вставить("Розовый",WebЦвета.Розовый);
	СоответствиеWebЦвета.Вставить("Серый",WebЦвета.Серый);
	СоответствиеWebЦвета.Вставить("Синий",WebЦвета.Синий);
	СоответствиеWebЦвета.Вставить("Фиолетовый",WebЦвета.Фиолетовый);
	СоответствиеWebЦвета.Вставить("Черный",WebЦвета.Черный);
	//Замена легенды состояния заказов на вид ремонта
	// ОРИГИНАЛ ++
	//Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//               |	ВидыСостоянийЗаказНарядов.Ссылка,
	//               |	ВидыСостоянийЗаказНарядов.Представление,
	//               |	ВидыСостоянийЗаказНарядов.Цвет,
	//               |	ВидыСостоянийЗаказНарядов.НедоступноДляВыбора
	//               |ИЗ
	//               |	Справочник.ВидыСостоянийЗаказНарядов КАК ВидыСостоянийЗаказНарядов
	//               |
	//               |УПОРЯДОЧИТЬ ПО
	//               |	ВидыСостоянийЗаказНарядов.Порядок";
	// ОРИГИНАЛ -- 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
               |	ВидыРемонта.Ссылка КАК Ссылка,
               |	ВидыРемонта.Наименование КАК Представление,
               |	ВидыРемонта.ЦветДляАРМ КАК Цвет
               |ИЗ
               |	Справочник.ВидыРемонта КАК ВидыРемонта
			   |ГДЕ
			   |	ВидыРемонта.ЦветДляАРМ <> """"
			   |	И ВидыРемонта.ПометкаУдаления = ЛОЖЬ";
	
	//БИЗТЕХ Коваль А.Д.--
	
	ЦветаЗН = Запрос.Выполнить().Выгрузить();					
	
	Если ОтображатьДанныеУРВ Тогда
		//получаем цвета состояний УРВ
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВидыОтметокВремени.Ссылка,
		               |	ВидыОтметокВремени.Представление,
		               |	ВидыОтметокВремени.Цвет
		               |ИЗ
		               |	Справочник.ВидыОтметокВремени КАК ВидыОтметокВремени";
		ЦветаУРВ = Запрос.Выполнить().Выгрузить();	
	КонецЕсли;
	
	//получаем параметры для формирования табличного документа
	ЧислоСтрок = Макс(ЦветаСлужебные.Количество(),ЦветаЗН.Количество());
	Если ОтображатьДанныеУРВ Тогда
		ЧислоСтрок = Макс(ЧислоСтрок,ЦветаУРВ.Количество());
	КонецЕсли;
	
	//создаем таблицу оформления легенды
	ТаблицаЛегенды = Новый ТаблицаЗначений;
	ТаблицаЛегенды.Колонки.Добавить("ЦветаСлужебные");
	ТаблицаЛегенды.Колонки.Добавить("ЦветаЗН");
	ТаблицаЛегенды.Колонки.Добавить("СостояниеЗнР");
	Если ОтображатьДанныеУРВ Тогда
		ТаблицаЛегенды.Колонки.Добавить("ЦветаУРВ");
	КонецЕсли;
	Для Инд = 1 По ЧислоСтрок Цикл
		ТаблицаЛегенды.Добавить();
	КонецЦикла;      
	
	//заполняем таблицу оформления легенды
	Инд = 0;
	Для Каждого ТекЦвет Из ЦветаСлужебные Цикл
		ДанныеЛегенды = Новый Структура;
		ДанныеЛегенды.Вставить("Представление",ТекЦвет.Представление);
		ДанныеЛегенды.Вставить("Цвет",ТекЦвет.Цвет);
		ТаблицаЛегенды[Инд]["ЦветаСлужебные"] = ДанныеЛегенды;				
		Инд = Инд + 1;
	КонецЦикла;
	
	Инд = 0;
	Для Каждого ТекЦвет Из ЦветаЗН Цикл
		//БИЗТЕХ 05.12.2023 Коваль А.Д. ++
		//Если НЕ ОтображатьДанныеУРВ И ТекЦвет.НедоступноДляВыбора Тогда
		//	Продолжить;
		//КонецЕсли;
		ДанныеЛегенды = Новый Структура;
		ДанныеЛегенды.Вставить("Представление",ТекЦвет.Представление);
		//ДанныеЛегенды.Вставить("Цвет",ТекЦвет.Цвет);
		ДанныеЛегенды.Вставить("Цвет",СоответствиеWebЦвета[ТекЦвет.Цвет]);
		//Если ТекЦвет.Цвет <> Неопределено Тогда
		//	ДанныеЛегенды.Вставить("Цвет",ТекЦвет.Цвет.Получить());
		//КонецЕсли;
		//исключение для состояния "заявка"
		Если ДанныеЛегенды.Цвет = Неопределено И ТекЦвет.Ссылка = Справочники.ВидыСостоянийЗаказНарядов.Заявка Тогда
			ДанныеЛегенды.Вставить("Цвет",ЦветаРаскраски.Получить("ЦветЗагрузкаНорм"));
		КонецЕсли;
		ТаблицаЛегенды[Инд]["ЦветаЗН"] = ДанныеЛегенды;				
		Инд = Инд + 1;
	КонецЦикла;
	
	Если ОтображатьДанныеУРВ Тогда
		Инд = 0;
		Для Каждого ТекЦвет Из ЦветаУРВ Цикл
			ДанныеЛегенды = Новый Структура;
			ДанныеЛегенды.Вставить("Представление",ТекЦвет.Представление);
			Если ТекЦвет.Цвет <> Неопределено Тогда
				ДанныеЛегенды.Вставить("Цвет",ТекЦвет.Цвет.Получить());
			КонецЕсли;
			ТаблицаЛегенды[Инд]["ЦветаУРВ"] = ДанныеЛегенды;				
			Инд = Инд + 1;
		КонецЦикла;
	КонецЕсли;
	
	Инд = 0;
	//Добавим легенду для значков состояния ЗНР
	Для Каждого СостояниеЗнР из КартинкаПоСостоянию Цикл
		Состояние = СостояниеЗнР.Ключ;
		Представление = ПредставлениеСостояний[Состояние];
		Если Инд > ТаблицаЛегенды.Количество() - 1 Тогда
			ТаблицаЛегенды.Добавить();	
		КонецЕсли;
		ДанныеЛегенды = Новый Структура;
		ДанныеЛегенды.Вставить("Представление",	Представление);
		ДанныеЛегенды.Вставить("Картинка",		СостояниеЗнР.Значение); 
		ДанныеЛегенды.Вставить("Цвет", 			WebЦвета.Белый);
		ТаблицаЛегенды[Инд]["СостояниеЗнР"] = ДанныеЛегенды; 
		Инд = Инд + 1;
	КонецЦикла;
	
	
	Возврат ТаблицаЛегенды;
	
КонецФункции

// Формирует табличный документ, содержаший легенду и добавляет к основному табличному документу
//
Процедура СформироватьМакетЛегенды(ТабДок)
	
	Если ТаблицаЛегенды = Неопределено ИЛИ ТаблицаЛегенды.Количество()=0 Тогда
		//при первом запуске необходимо заполнить таблицу
		ТаблицаЛегенды = СформироватьТаблицуЛегенды();
	Иначе
		//при переключение видимости УРВ нужно перезаполнить таблицу
		Если (ОтображатьДанныеУРВ И ТаблицаЛегенды.Колонки.Количество()=2) 
			ИЛИ (НЕ ОтображатьДанныеУРВ И ТаблицаЛегенды.Колонки.Количество()=3) Тогда
			ТаблицаЛегенды = СформироватьТаблицуЛегенды();
		КонецЕсли;
	КонецЕсли;
	
	//получаем параметры для формирования табличного документа
	Макет = Обработки.АРМЗаписьНаРемонт.ПолучитьМакет("Легенда");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок|Группа");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка|Группа");
	ОбластьПусто = Макет.ПолучитьОбласть("Пусто|Группа");
	ИндексПервойСтроки = ТабДок.ВысотаТаблицы+2;
	ЧислоСтрок = ТаблицаЛегенды.Количество();
	Если ПоворотКалендаря = 1 Тогда
		ШиринаКолонки = ТабДок.ПолучитьОбласть("R1C1").ТекущаяОбласть.ШиринаКолонки + ТабДок.ПолучитьОбласть("R1C2").ТекущаяОбласть.ШиринаКолонки;
	Иначе
		ШиринаКолонки = ТабДок.ПолучитьОбласть("R1C1").ТекущаяОбласть.ШиринаКолонки;
	КонецЕсли;
	
	ТабДок.НачатьГруппуСтрок();
	
	//выводим заголовок легенды 
	ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонки;
	ОбластьЗаголовок.Параметры.ЗаголовокКолонки = "Служебные цвета";
	ТабДок.Вывести(ОбластьЗаголовок); 
	
	//БИЗТЕХ 05.12.2023 Коваль А.Д. ++
	//ОбластьЗаголовок.Параметры.ЗаголовокКолонки = "Состояние ремонта";
	ОбластьЗаголовок.Параметры.ЗаголовокКолонки = "Виды ремонта";
	//БИЗТЕХ 05.12.2023 Коваль А.Д. --
	
	ТабДок.Присоединить(ОбластьЗаголовок);
	Если ОтображатьДанныеУРВ Тогда
		ОбластьЗаголовок.Параметры.ЗаголовокКолонки = "Состояние работ в цехе";
		ТабДок.Присоединить(ОбластьЗаголовок);
	КонецЕсли;
	
	//выводим строки легенды
	Для Инд = 0 По ЧислоСтрок-1 Цикл
		
		ДанныеЯчейки = ТаблицаЛегенды[Инд]["ЦветаСлужебные"];
		Если ДанныеЯчейки <> Неопределено Тогда
			ТабДок.Вывести(ОбластьСтрока);
			АдресЯчейки = "R"+(Инд+ИндексПервойСтроки)+"C2";
			ОбластьЯчеек = ТабДок.Область(АдресЯчейки);
			Если ДанныеЯчейки.Представление <> Неопределено Тогда
				ОбластьЯчеек.Текст = ДанныеЯчейки.Представление;
			КонецЕсли;
			Если ДанныеЯчейки.Цвет <> Неопределено Тогда
				ОбластьЯчеек.ЦветФона = ДанныеЯчейки.Цвет;
			КонецЕсли;
		Иначе
			ТабДок.Вывести(ОбластьПусто);
		КонецЕсли;
		
		ДанныеЯчейки = ТаблицаЛегенды[Инд]["ЦветаЗН"];
		Если ДанныеЯчейки <> Неопределено Тогда
			ТабДок.Присоединить(ОбластьСтрока);
			АдресЯчейки = "R"+(Инд+ИндексПервойСтроки)+"C4";
			ОбластьЯчеек = ТабДок.Область(АдресЯчейки);
			Если ДанныеЯчейки.Представление <> Неопределено Тогда
				ОбластьЯчеек.Текст = ДанныеЯчейки.Представление;
			КонецЕсли;
			Если ДанныеЯчейки.Цвет <> Неопределено Тогда
				ОбластьЯчеек.ЦветФона = ДанныеЯчейки.Цвет;
			КонецЕсли;
		Иначе
			ТабДок.Присоединить(ОбластьПусто);
		КонецЕсли;
		
		//БИЗТЕХ КОВАЛЬ ++
		//Легенда для статусов ЗнР
		ДанныеЯчейки = ТаблицаЛегенды[Инд]["СостояниеЗнР"];
		Если ДанныеЯчейки <> Неопределено Тогда
			ТабДок.Присоединить(ОбластьСтрока);
			АдресЯчейки = "R"+(Инд+ИндексПервойСтроки)+"C6";
			ОбластьЯчеек = ТабДок.Область(АдресЯчейки);
			Если ДанныеЯчейки.Представление <> Неопределено Тогда
				ОбластьЯчеек.Текст = ДанныеЯчейки.Представление;
			КонецЕсли;
			Если ДанныеЯчейки.Цвет <> Неопределено Тогда
				ОбластьЯчеек.ЦветФона = ДанныеЯчейки.Цвет;
			КонецЕсли;
			Если ДанныеЯчейки.Картинка <> Неопределено Тогда
				ОбластьЯчеек.Картинка = ДанныеЯчейки.Картинка;
				ОбластьЯчеек.РазмерКартинки = РазмерКартинки.АвтоРазмер;
				ОбластьЯчеек.ВертикальноеПоложениеКартинки = ВертикальноеПоложение.Центр;
			КонецЕсли;
		Иначе
			ТабДок.Присоединить(ОбластьПусто);
		КонецЕсли;
		//БИЗТЕХ КОВАЛЬ --
		
		Если ОтображатьДанныеУРВ Тогда
			ДанныеЯчейки = ТаблицаЛегенды[Инд]["ЦветаУРВ"];
			Если ДанныеЯчейки <> Неопределено Тогда
				ТабДок.Присоединить(ОбластьСтрока);
				АдресЯчейки = "R"+(Инд+ИндексПервойСтроки)+"C6";
				ОбластьЯчеек = ТабДок.Область(АдресЯчейки);
				Если ДанныеЯчейки.Представление <> Неопределено Тогда
					ОбластьЯчеек.Текст = ДанныеЯчейки.Представление;
				КонецЕсли;
				Если ДанныеЯчейки.Цвет <> Неопределено Тогда
					ОбластьЯчеек.ЦветФона = ДанныеЯчейки.Цвет;
				КонецЕсли;
			Иначе
				ТабДок.Присоединить(ОбластьПусто);
			КонецЕсли;
 		КонецЕсли;
		
	КонецЦикла;
	
	ТабДок.ЗакончитьГруппуСтрок(); 		
	
КонецПроцедуры

// Формирует таблицу потенциалов для цехов или мастеров
//
Функция СформироватьТаблицуПотенциаловПоРежимуКалендаря() Экспорт
	
	МВТРабочиеМеста = Новый МенеджерВременныхТаблиц;
	
	Если РежимКалендаря = 1 Тогда
		ТаблицаРабочихМест = ЗаполнитьНастройкиГрафиковРаботДляЦехов();
		Возврат СформироватьТаблицуПотенциаловРабочихМест(1);
	Иначе
		ТаблицаРабочихМест = ЗаполнитьНастройкиГрафиковРаботДляИсполнителей();
		Возврат СформироватьТаблицуПотенциаловРабочихМест(2);
	КонецЕсли;
	
	Если МВТРабочиеМеста <> Неопределено Тогда
		МВТРабочиеМеста.Закрыть();
	КонецЕсли;
	
КонецФункции //СформироватьТаблицуПоказателейЗагрузки()

// БИЗТЕХ КОВАЛЬ 12.09.2025 ++
// Формирует таблицу, содержащую суммы мастеров-приемщиков
Функция СформироватьТаблицуСуммМК()
	
	ИсполнителиПриемки = Новый Массив;
	
	Для СчетчикРабочихМест = 1 По ЧислоРабочихМест Цикл
		
		Исполнитель = ТаблицаРасшифровок[СчетчикРабочихМест].РабочееМесто;
		
		Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
			
			Если Исполнитель.ГруппаДляОтображенияВАРМ = Перечисления.ГруппыДляОтображенияВАРМ.Приемка Тогда
				ИсполнителиПриемки.Добавить(Исполнитель);
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Диспетчер КАК Диспетчер,
		|	СУММА(ЗаявкаНаРемонт.СуммаДокумента) КАК Сумма
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ЗаявкаНаРемонт.Диспетчер В(&ИсполнителиПриемки)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаявкаНаРемонт.Диспетчер";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОтчета));
	Запрос.УстановитьПараметр("ИсполнителиПриемки", ИсполнителиПриемки);
	
	ТаблицаСуммМК = Запрос.Выполнить().Выгрузить();

	Возврат ТаблицаСуммМК;
	
КонецФункции
// БИЗТЕХ КОВАЛЬ 12.09.2025 ++


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДИАГРАММЫ В РЕЖИМЕ МЕСЯЦ

// Формирует таблицу показателей загрузки цехов
// Возвращает таблицу значений
// Параметры:
Функция СформироватьТаблицуПоказателейЗагрузки() Экспорт
	
	МВТРабочиеМеста = Новый МенеджерВременныхТаблиц;
    ТаблицаЗагрузкиЦехов = Новый ТаблицаЗначений;
	ТаблицаЗагрузкиИсполнителей = Новый ТаблицаЗначений;
	
	ТаблицаРабочихМест = ЗаполнитьНастройкиГрафиковРаботДляИсполнителей();
	СформироватьГрафикиРабочихМест(Истина);
	ТаблицаПотенциаловДеталиИсполнители = СформироватьТаблицуПотенциаловРабочихМест(2,Истина);
	
	ТаблицаРабочихМест = ЗаполнитьНастройкиГрафиковРаботДляЦехов();
	СформироватьГрафикиРабочихМест(Истина);
	ТаблицаПотенциаловДеталиЦеха = СформироватьТаблицуПотенциаловРабочихМест(1,Истина);
	
	ТаблицаДат = Новый ТаблицаЗначений;
	ТаблицаДат.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата"));
	НачалоПериода = НачалоМесяца(ДатаОтчета);
	КоличествоДней = (НачалоДня(КонецМесяца(ДатаОтчета)+1) - НачалоДня(НачалоМесяца(ДатаОтчета))) / 86400;
	Для СчетчикДней = 0 По КоличествоДней-1 Цикл
		НоваяСтрока = ТаблицаДат.Добавить();
		НоваяСтрока.Дата = НачалоПериода+60*60*24*СчетчикДней;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	Запрос.УстановитьПараметр("НачалоПериода",НачалоМесяца(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(ДатаОтчета));
	Запрос.УстановитьПараметр("ТаблицаПотенциаловДеталиЦеха",ТаблицаПотенциаловДеталиЦеха);
	Запрос.УстановитьПараметр("ТаблицаПотенциаловДеталиИсполнители",ТаблицаПотенциаловДеталиИсполнители);
	Запрос.УстановитьПараметр("ТаблицаДат",ТаблицаДат);
	Запрос.Текст ="ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ЗаписиНаРемонтЗаМесяц.Ссылка КАК РабочееМесто,
	              |	ЗаписиНаРемонтЗаМесяц.Ссылка.Наименование КАК РабочееМестоНаименование,
	              |	ЗаписиНаРемонтЗаМесяц.ГрафикРаботы КАК ГрафикРаботы,
	              |	ЗаписиНаРемонтЗаМесяц.КатегорияУпорядочивания
	              |ПОМЕСТИТЬ ТаблицаЦехов
	              |ИЗ
	              |	ЦЕХА КАК ЗаписиНаРемонтЗаМесяц
	              |ГДЕ
	              |	НЕ ЗаписиНаРемонтЗаМесяц.ВидИспользованияРабочегоМеста = ЗНАЧЕНИЕ(Перечисление.ВидыИспользованияРабочихМест.НеУчаствуетВПланировании)
	              |
	              |ИНДЕКСИРОВАТЬ ПО
	              |	РабочееМесто
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	МАСТЕРА.Ссылка КАК РабочееМесто,
	              |	МАСТЕРА.СотрудникПсевдоним,
	              |	МАСТЕРА.ДолжностьПсевдоним,
	              |	МАСТЕРА.Ссылка.ГрафикРаботы КАК ГрафикРаботы,
	              |	МАСТЕРА.КатегорияУпорядочивания
	              |ПОМЕСТИТЬ ТаблицаИсполнителей
	              |ИЗ
	              |	Мастера КАК МАСТЕРА
	              |
	              |ИНДЕКСИРОВАТЬ ПО
	              |	РабочееМесто
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ГрафикРаботыРесурсов.Дата КАК Дата,
	              |	ГрафикРаботыРесурсов.Ресурс1 КАК РабочееМесто,
	              |	ВЫРАЗИТЬ(ГрафикРаботыРесурсов.Объект КАК Документ.ЗаявкаНаРемонт).Автомобиль КАК Автомобиль,
	              |	СУММА(ЧАС(ГрафикРаботыРесурсов.Продолжительность) + МИНУТА(ГрафикРаботыРесурсов.Продолжительность) / 60 + СЕКУНДА(ГрафикРаботыРесурсов.Продолжительность) / 3600) КАК Продолжительность
	              |ПОМЕСТИТЬ ЗаписиНаРемонтПоЦехамЗаМесяц
	              |ИЗ
	              |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	              |ГДЕ
	              |	ГрафикРаботыРесурсов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	              |	И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПланРемонта)
	              |	И ГрафикРаботыРесурсов.Ресурс1 В
	              |			(ВЫБРАТЬ
	              |				ТаблицаЦехов.РабочееМесто
	              |			ИЗ
	              |				ТаблицаЦехов КАК ТаблицаЦехов)
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ГрафикРаботыРесурсов.Дата,
	              |	ГрафикРаботыРесурсов.Ресурс1,
	              |	ВЫРАЗИТЬ(ГрафикРаботыРесурсов.Объект КАК Документ.ЗаявкаНаРемонт).Автомобиль
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ГрафикРаботыРесурсов.Дата КАК Дата,
	              |	ГрафикРаботыРесурсов.Ресурс2 КАК РабочееМесто,
	              |	ВЫРАЗИТЬ(ГрафикРаботыРесурсов.Объект КАК Документ.ЗаявкаНаРемонт).Автомобиль КАК Автомобиль,
	              |	СУММА(ЧАС(ГрафикРаботыРесурсов.Продолжительность) + МИНУТА(ГрафикРаботыРесурсов.Продолжительность) / 60 + СЕКУНДА(ГрафикРаботыРесурсов.Продолжительность) / 3600) КАК Продолжительность
	              |ПОМЕСТИТЬ ЗаписиНаРемонтПоИсполнителямЗаМесяц
	              |ИЗ
	              |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	              |ГДЕ
	              |	ГрафикРаботыРесурсов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	              |	И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПланРемонта)
	              |	И ГрафикРаботыРесурсов.Ресурс2 В
	              |			(ВЫБРАТЬ
	              |				ТаблицаИсполнителей.РабочееМесто
	              |			ИЗ
	              |				ТаблицаИсполнителей КАК ТаблицаИсполнителей)
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ГрафикРаботыРесурсов.Дата,
	              |	ГрафикРаботыРесурсов.Ресурс2,
	              |	ВЫРАЗИТЬ(ГрафикРаботыРесурсов.Объект КАК Документ.ЗаявкаНаРемонт).Автомобиль
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТаблицаПотенциалов.Дата,
	              |	ТаблицаПотенциалов.ГрафикРаботы,
	              |	ТаблицаПотенциалов.Потенциал
	              |ПОМЕСТИТЬ ТаблицаПотенциаловДеталиИсполнители
	              |ИЗ
	              |	&ТаблицаПотенциаловДеталиИсполнители КАК ТаблицаПотенциалов
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТаблицаПотенциалов.Дата,
	              |	ТаблицаПотенциалов.ГрафикРаботы,
	              |	ТаблицаПотенциалов.Потенциал
	              |ПОМЕСТИТЬ ТаблицаПотенциаловДеталиЦеха
	              |ИЗ
	              |	&ТаблицаПотенциаловДеталиЦеха КАК ТаблицаПотенциалов
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ТаблицаДат.Дата
	              |ПОМЕСТИТЬ ТаблицаДат
	              |ИЗ
	              |	&ТаблицаДат КАК ТаблицаДат
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТаблицаДат.Дата КАК Дата,
	              |	ТаблицаЦехов.РабочееМесто КАК РабочееМесто,
	              |	ТаблицаЦехов.РабочееМестоНаименование,
	              |	ТаблицаЦехов.ГрафикРаботы,
	              |	ТаблицаЦехов.КатегорияУпорядочивания
	              |ПОМЕСТИТЬ ДатыЦеха
	              |ИЗ
	              |	ТаблицаДат КАК ТаблицаДат,
	              |	ТаблицаЦехов КАК ТаблицаЦехов
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТаблицаДат.Дата КАК Дата,
	              |	ТаблицаИсполнителей.РабочееМесто КАК РабочееМесто,
	              |	ТаблицаИсполнителей.СотрудникПсевдоним,
	              |	ТаблицаИсполнителей.ДолжностьПсевдоним,
	              |	ТаблицаИсполнителей.ГрафикРаботы,
	              |	ТаблицаИсполнителей.КатегорияУпорядочивания
	              |ПОМЕСТИТЬ ДатыИсполнители
	              |ИЗ
	              |	ТаблицаДат КАК ТаблицаДат,
	              |	ТаблицаИсполнителей КАК ТаблицаИсполнителей
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТаблицаДат.Дата КАК Период,
	              |	ПоказателиОптимальнойЗагрузкиРабочихМест.РабочееМесто КАК РабочееМесто,
	              |	МАКСИМУМ(ПоказателиОптимальнойЗагрузкиРабочихМест.Период) КАК ПериодПоказателя
	              |ПОМЕСТИТЬ ДатаСрезПоследнего
	              |ИЗ
	              |	ТаблицаДат КАК ТаблицаДат
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиОптимальнойЗагрузкиРабочихМест КАК ПоказателиОптимальнойЗагрузкиРабочихМест
	              |		ПО ТаблицаДат.Дата >= ПоказателиОптимальнойЗагрузкиРабочихМест.Период
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ТаблицаДат.Дата,
	              |	ПоказателиОптимальнойЗагрузкиРабочихМест.РабочееМесто
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВложеннаяТаблица.Период КАК Период,
	              |	ВложеннаяТаблица.РабочееМесто КАК РабочееМесто,
	              |	СУММА(ВложеннаяТаблица.ПотенциалАвтомобилей) КАК ПотенциалАвтомобилей,
	              |	СУММА(ВложеннаяТаблица.ЗагрузкаАвтомобилиДетали) КАК ЗагрузкаАвтомобилиДетали,
	              |	СУММА(ВложеннаяТаблица.ЗагрузкаНормочасыДетали) КАК ЗагрузкаНормочасыДетали
	              |ПОМЕСТИТЬ ЗагрузкаАвтомобилиДеталиПоЦехам
	              |ИЗ
	              |	(ВЫБРАТЬ
	              |		ДатаПоследнегоПоказателя.Период КАК Период,
	              |		ДатаПоследнегоПоказателя.РабочееМесто КАК РабочееМесто,
	              |		ПоказателиОптимальнойЗагрузкиРабочихМест.КоличествоАвтомобилей КАК ПотенциалАвтомобилей,
	              |		0 КАК ЗагрузкаАвтомобилиДетали,
	              |		0 КАК ЗагрузкаНормочасыДетали
	              |	ИЗ
	              |		ДатаСрезПоследнего КАК ДатаПоследнегоПоказателя
	              |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиОптимальнойЗагрузкиРабочихМест КАК ПоказателиОптимальнойЗагрузкиРабочихМест
	              |			ПО (ДатаПоследнегоПоказателя.РабочееМесто ССЫЛКА Справочник.Цеха)
	              |				И ДатаПоследнегоПоказателя.ПериодПоказателя = ПоказателиОптимальнойЗагрузкиРабочихМест.Период
	              |				И ДатаПоследнегоПоказателя.РабочееМесто = ПоказателиОптимальнойЗагрузкиРабочихМест.РабочееМесто
	              |	ГДЕ
	              |		ДатаПоследнегоПоказателя.РабочееМесто ССЫЛКА Справочник.Цеха
	              |	
	              |	ОБЪЕДИНИТЬ ВСЕ
	              |	
	              |	ВЫБРАТЬ
	              |		ЗаписиНаРемонтЗаМесяцЦеха.Дата,
	              |		ЗаписиНаРемонтЗаМесяцЦеха.РабочееМесто,
	              |		0,
	              |		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаписиНаРемонтЗаМесяцЦеха.Автомобиль),
	              |		СУММА(ЗаписиНаРемонтЗаМесяцЦеха.Продолжительность)
	              |	ИЗ
	              |		ЗаписиНаРемонтПоЦехамЗаМесяц КАК ЗаписиНаРемонтЗаМесяцЦеха
	              |	
	              |	СГРУППИРОВАТЬ ПО
	              |		ЗаписиНаРемонтЗаМесяцЦеха.Дата,
	              |		ЗаписиНаРемонтЗаМесяцЦеха.РабочееМесто) КАК ВложеннаяТаблица
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВложеннаяТаблица.Период,
	              |	ВложеннаяТаблица.РабочееМесто
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВложеннаяТаблица.Период КАК Период,
	              |	ВложеннаяТаблица.РабочееМесто КАК РабочееМесто,
	              |	СУММА(ВложеннаяТаблица.ПотенциалАвтомобилей) КАК ПотенциалАвтомобилей,
	              |	СУММА(ВложеннаяТаблица.ЗагрузкаАвтомобилиДетали) КАК ЗагрузкаАвтомобилиДетали,
	              |	СУММА(ВложеннаяТаблица.ЗагрузкаНормочасыДетали) КАК ЗагрузкаНормочасыДетали
	              |ПОМЕСТИТЬ ЗагрузкаАвтомобилиДеталиПоИсполнителям
	              |ИЗ
	              |	(ВЫБРАТЬ
	              |		ДатаПоследнегоПоказателя.Период КАК Период,
	              |		ДатаПоследнегоПоказателя.РабочееМесто КАК РабочееМесто,
	              |		ПоказателиОптимальнойЗагрузкиРабочихМест.КоличествоАвтомобилей КАК ПотенциалАвтомобилей,
	              |		0 КАК ЗагрузкаАвтомобилиДетали,
	              |		0 КАК ЗагрузкаНормочасыДетали
	              |	ИЗ
	              |		ДатаСрезПоследнего КАК ДатаПоследнегоПоказателя
	              |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиОптимальнойЗагрузкиРабочихМест КАК ПоказателиОптимальнойЗагрузкиРабочихМест
	              |			ПО (ДатаПоследнегоПоказателя.РабочееМесто ССЫЛКА Справочник.Сотрудники)
	              |				И ДатаПоследнегоПоказателя.ПериодПоказателя = ПоказателиОптимальнойЗагрузкиРабочихМест.Период
	              |				И ДатаПоследнегоПоказателя.РабочееМесто = ПоказателиОптимальнойЗагрузкиРабочихМест.РабочееМесто
	              |	ГДЕ
	              |		ДатаПоследнегоПоказателя.РабочееМесто ССЫЛКА Справочник.Сотрудники
	              |	
	              |	ОБЪЕДИНИТЬ ВСЕ
	              |	
	              |	ВЫБРАТЬ
	              |		ЗаписиНаРемонтЗаМесяцИсполнители.Дата,
	              |		ЗаписиНаРемонтЗаМесяцИсполнители.РабочееМесто,
	              |		0,
	              |		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаписиНаРемонтЗаМесяцИсполнители.Автомобиль),
	              |		СУММА(ЗаписиНаРемонтЗаМесяцИсполнители.Продолжительность)
	              |	ИЗ
	              |		ЗаписиНаРемонтПоИсполнителямЗаМесяц КАК ЗаписиНаРемонтЗаМесяцИсполнители
	              |	
	              |	СГРУППИРОВАТЬ ПО
	              |		ЗаписиНаРемонтЗаМесяцИсполнители.Дата,
	              |		ЗаписиНаРемонтЗаМесяцИсполнители.РабочееМесто) КАК ВложеннаяТаблица
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВложеннаяТаблица.Период,
	              |	ВложеннаяТаблица.РабочееМесто
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ЗаписиНаРемонтЗаМесяцИсполнители.Дата КАК Период,
	              |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаписиНаРемонтЗаМесяцИсполнители.Автомобиль) КАК ЗагрузкаАвтомобилиИтоги
	              |ПОМЕСТИТЬ ЗагрузкаАвтомобилиИтогиПоИсполнителям
	              |ИЗ
	              |	ЗаписиНаРемонтПоИсполнителямЗаМесяц КАК ЗаписиНаРемонтЗаМесяцИсполнители
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ЗаписиНаРемонтЗаМесяцИсполнители.Дата
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ЗаписиНаРемонтЗаМесяцЦеха.Дата КАК Период,
	              |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаписиНаРемонтЗаМесяцЦеха.Автомобиль) КАК ЗагрузкаАвтомобилиИтоги
	              |ПОМЕСТИТЬ ЗагрузкаАвтомобилиИтогиПоЦехам
	              |ИЗ
	              |	ЗаписиНаРемонтПоЦехамЗаМесяц КАК ЗаписиНаРемонтЗаМесяцЦеха
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ЗаписиНаРемонтЗаМесяцЦеха.Дата
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |УНИЧТОЖИТЬ ЗаписиНаРемонтПоЦехамЗаМесяц
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |УНИЧТОЖИТЬ ЗаписиНаРемонтПоИсполнителямЗаМесяц
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |УНИЧТОЖИТЬ ДатаСрезПоследнего
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ДатыЦеха.Дата КАК Период,
	              |	ДатыЦеха.РабочееМесто,
	              |	ДатыЦеха.РабочееМестоНаименование КАК РабочееМестоНаименование,
	              |	ДатыЦеха.ГрафикРаботы,
	              |	ДатыЦеха.КатегорияУпорядочивания КАК КатегорияУпорядочивания,
	              |	ВЫБОР
	              |		КОГДА ДатыЦеха.КатегорияУпорядочивания = 2
	              |				И НЕ ТаблицаПотенциаловДеталиЦеха.Потенциал ЕСТЬ NULL 
	              |				И ТаблицаПотенциаловДеталиЦеха.Потенциал > 0
	              |			ТОГДА ТаблицаПотенциаловДеталиЦеха.Потенциал
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПотенциалНормочасов,
	              |	ВЫБОР
	              |		КОГДА ДатыЦеха.КатегорияУпорядочивания = 2
	              |				И НЕ ЗагрузкаАвтомобилиДеталиПоЦехам.ЗагрузкаНормочасыДетали ЕСТЬ NULL 
	              |			ТОГДА ЗагрузкаАвтомобилиДеталиПоЦехам.ЗагрузкаНормочасыДетали
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ЗагрузкаНормочасов,
	              |	ЕСТЬNULL(ЗагрузкаАвтомобилиДеталиПоЦехам.ПотенциалАвтомобилей, 0) КАК ПотенциалАвтомобилей,
	              |	ЕСТЬNULL(ЗагрузкаАвтомобилиДеталиПоЦехам.ЗагрузкаАвтомобилиДетали, 0) КАК ЗагрузкаАвтомобилей,
	              |	ЕСТЬNULL(ЗагрузкаАвтомобилиИтогиПоЦехам.ЗагрузкаАвтомобилиИтоги, 0) КАК ЗагрузкаАвтомобилейИтоги,
	              |	ЕСТЬNULL(ТаблицаПотенциаловДеталиЦеха.Потенциал, -1) КАК ПотенциалПоГрафикам
	              |ИЗ
	              |	ДатыЦеха КАК ДатыЦеха
	              |		ЛЕВОЕ СОЕДИНЕНИЕ ЗагрузкаАвтомобилиДеталиПоЦехам КАК ЗагрузкаАвтомобилиДеталиПоЦехам
	              |		ПО ДатыЦеха.РабочееМесто = ЗагрузкаАвтомобилиДеталиПоЦехам.РабочееМесто
	              |			И ДатыЦеха.Дата = ЗагрузкаАвтомобилиДеталиПоЦехам.Период
	              |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПотенциаловДеталиЦеха КАК ТаблицаПотенциаловДеталиЦеха
	              |		ПО ДатыЦеха.ГрафикРаботы = ТаблицаПотенциаловДеталиЦеха.ГрафикРаботы
	              |			И ДатыЦеха.Дата = ТаблицаПотенциаловДеталиЦеха.Дата
	              |		ЛЕВОЕ СОЕДИНЕНИЕ ЗагрузкаАвтомобилиИтогиПоЦехам КАК ЗагрузкаАвтомобилиИтогиПоЦехам
	              |		ПО ДатыЦеха.Дата = ЗагрузкаАвтомобилиИтогиПоЦехам.Период
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Период,
	              |	КатегорияУпорядочивания,
	              |	РабочееМестоНаименование
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ДатыИсполнители.Дата КАК Период,
	              |	ДатыИсполнители.РабочееМесто,
	              |	ДатыИсполнители.СотрудникПсевдоним,
	              |	ДатыИсполнители.ДолжностьПсевдоним,
	              |	ДатыИсполнители.ГрафикРаботы,
	              |	ДатыИсполнители.КатегорияУпорядочивания КАК КатегорияУпорядочивания,
	              |	ВЫБОР
	              |		КОГДА ДатыИсполнители.КатегорияУпорядочивания = 2
	              |				И НЕ ТаблицаПотенциаловДеталиИсполнители.Потенциал ЕСТЬ NULL 
	              |				И ТаблицаПотенциаловДеталиИсполнители.Потенциал > 0
	              |			ТОГДА ТаблицаПотенциаловДеталиИсполнители.Потенциал
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПотенциалНормочасов,
	              |	ВЫБОР
	              |		КОГДА ДатыИсполнители.КатегорияУпорядочивания = 2
	              |				И НЕ ЗагрузкаАвтомобилиДеталиПоИсполнителям.ЗагрузкаНормочасыДетали ЕСТЬ NULL 
	              |			ТОГДА ЗагрузкаАвтомобилиДеталиПоИсполнителям.ЗагрузкаНормочасыДетали
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ЗагрузкаНормочасов,
	              |	ВЫБОР
	              |		КОГДА ДатыИсполнители.КатегорияУпорядочивания = 1
	              |				И НЕ ЗагрузкаАвтомобилиДеталиПоИсполнителям.ПотенциалАвтомобилей ЕСТЬ NULL 
	              |			ТОГДА ЗагрузкаАвтомобилиДеталиПоИсполнителям.ПотенциалАвтомобилей
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПотенциалАвтомобилей,
	              |	ЕСТЬNULL(ЗагрузкаАвтомобилиДеталиПоИсполнителям.ЗагрузкаАвтомобилиДетали, 0) КАК ЗагрузкаАвтомобилей,
	              |	ЕСТЬNULL(ЗагрузкаАвтомобилиИтогиПоИсполнителям.ЗагрузкаАвтомобилиИтоги, 0) КАК ЗагрузкаАвтомобилейИтоги,
	              |	ЕСТЬNULL(ТаблицаПотенциаловДеталиИсполнители.Потенциал, -1) КАК ПотенциалПоГрафикам
	              |ИЗ
	              |	ДатыИсполнители КАК ДатыИсполнители
	              |		ЛЕВОЕ СОЕДИНЕНИЕ ЗагрузкаАвтомобилиДеталиПоИсполнителям КАК ЗагрузкаАвтомобилиДеталиПоИсполнителям
	              |		ПО ДатыИсполнители.РабочееМесто = ЗагрузкаАвтомобилиДеталиПоИсполнителям.РабочееМесто
	              |			И ДатыИсполнители.Дата = ЗагрузкаАвтомобилиДеталиПоИсполнителям.Период
	              |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПотенциаловДеталиИсполнители КАК ТаблицаПотенциаловДеталиИсполнители
	              |		ПО ДатыИсполнители.ГрафикРаботы = ТаблицаПотенциаловДеталиИсполнители.ГрафикРаботы
	              |			И ДатыИсполнители.Дата = ТаблицаПотенциаловДеталиИсполнители.Дата
	              |		ЛЕВОЕ СОЕДИНЕНИЕ ЗагрузкаАвтомобилиИтогиПоИсполнителям КАК ЗагрузкаАвтомобилиИтогиПоИсполнителям
	              |		ПО ДатыИсполнители.Дата = ЗагрузкаАвтомобилиИтогиПоИсполнителям.Период
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Период,
	              |	ДатыИсполнители.ДолжностьПсевдоним,
	              |	ДатыИсполнители.СотрудникПсевдоним";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
 	ТаблицаЗагрузкиЦехов = МассивРезультатов[17].Выгрузить();
	ТаблицаЗагрузкиЦехов.Индексы.Добавить("Период");
	
	//в режиме использования графиков рабочих мест удалим рабочие места, для которых не задан график на дату 
	Если ИспользованиеГрафиковРабот > 1 Тогда
		Сч = ТаблицаЗагрузкиЦехов.Количество()-1;
		Пока Сч >= 0 Цикл
			Строка = ТаблицаЗагрузкиЦехов[Сч];
			Если Строка.ЗагрузкаАвтомобилей = 0 И Строка.ЗагрузкаНормочасов = 0 Тогда
				Если Строка.ПотенциалПоГрафикам<0 ИЛИ (ОтображатьТолькоРаботающих И Строка.ПотенциалПоГрафикам=0) Тогда
					ТаблицаЗагрузкиЦехов.Удалить(Сч);
				КонецЕсли;
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла;
	КонецЕсли;	
	
	ТаблицаЗагрузкиИсполнителей = МассивРезультатов[18].Выгрузить();
	ТаблицаЗагрузкиИсполнителей.Индексы.Добавить("Период");
	
	//в режиме использования графиков рабочих мест удалим рабочие места, для которых не задан график на дату 
	Если ИспользованиеГрафиковРабот > 1 Тогда
		Сч = ТаблицаЗагрузкиИсполнителей.Количество()-1;
		Пока Сч >= 0 Цикл
			Строка = ТаблицаЗагрузкиИсполнителей[Сч];
			Если Строка.ЗагрузкаАвтомобилей = 0 И Строка.ЗагрузкаНормочасов = 0 Тогда
				Если Строка.ПотенциалПоГрафикам<0 ИЛИ (ОтображатьТолькоРаботающих И Строка.ПотенциалПоГрафикам=0) Тогда
					ТаблицаЗагрузкиИсполнителей.Удалить(Сч);
				КонецЕсли;
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла;
	КонецЕсли;	
	
	Если МВТРабочиеМеста <> Неопределено Тогда
		МВТРабочиеМеста.Закрыть();
	КонецЕсли;
	
	Возврат Истина;
КонецФункции //СформироватьТаблицуПоказателейЗагрузки()

// Формирует таблицу потенциалов на неделю
//
Функция СформироватьТаблицуПотенциаловРабочихМест(РежимПараметр=Неопределено,ДетальныеЗаписи=Ложь) Экспорт
	РежимПараметр = ?(РежимПараметр=Неопределено, РежимКалендаря, РежимПараметр);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Ресурсы.Ссылка КАК РабочееМесто,
	               |	Ресурсы.ГрафикРаботы КАК ГрафикРаботы,
	               |	Ресурсы.КатегорияУпорядочивания КАК КатегорияУпорядочивания
	               |ИЗ
	               |	Мастера КАК Ресурсы
	               |ГДЕ
	               |	Ресурсы.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)";
	Если РежимПараметр = 1 Тогда
    	Запрос.Текст = СтрЗаменить(Запрос.Текст,"Мастера","Цеха");
	КонецЕсли;
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	ВыборкаМастеров = Запрос.Выполнить().Выбрать();
	
	ТаблицаПотенциаловПоГрафикам = Новый ТаблицаЗначений;
	ТаблицаПотенциаловПоГрафикам.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата"));
	ТаблицаПотенциаловПоГрафикам.Колонки.Добавить("ГрафикРаботы",Новый ОписаниеТипов("СправочникСсылка.ГрафикиРаботы"));
	ТаблицаПотенциаловПоГрафикам.Колонки.Добавить("Потенциал",Новый ОписаниеТипов("Число"));
	
	НачалоПериода = НачалоНедели(ДатаОтчета);
	КоличествоДней = 7;
	Если ДетальныеЗаписи Тогда
		НачалоПериода = НачалоМесяца(ДатаОтчета);
		КоличествоДней = (НачалоДня(КонецМесяца(ДатаОтчета)+1) - НачалоДня(НачалоМесяца(ДатаОтчета))) / 86400;
	КонецЕсли;
		
	Пока ВыборкаМастеров.Следующий() Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ГрафикРаботы",ВыборкаМастеров.ГрафикРаботы);
		НайденныеСтроки = ТаблицаПотенциаловПоГрафикам.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0  Тогда
			Продолжить;
		КонецЕсли;
		
		Для СчетчикДней = 0 По КоличествоДней-1 Цикл
			
			ПотенциалДняДляГрафика = 0;
			Если  ГрафикиРабочихМест.Количество()>0 Тогда
				Отбор = Новый Структура;
				ТекущийДень = НачалоПериода+60*60*24*СчетчикДней; 
				Отбор.Вставить("Дата",ТекущийДень);
				Отбор.Вставить("ИспользованиеГрафика",Истина);
				Отбор.Вставить("ГрафикРаботы",ВыборкаМастеров.ГрафикРаботы);
				НайденныеСтрокиГрафика = ГрафикиРабочихМест.НайтиСтроки(Отбор);
				Если НайденныеСтрокиГрафика.Количество()>0 Тогда
					Для Каждого СтрокаГрафика Из НайденныеСтрокиГрафика Цикл
						Если СтрокаГрафика.ВидИнтервала.РабочийИнтервал Тогда
							ПотенциалДняДляГрафика = ПотенциалДняДляГрафика + (СтрокаГрафика.Продолжительность-Дата('00010101'));		
						КонецЕсли;
					КонецЦикла;
				Иначе
					ПотенциалДняДляГрафика = -3600; //не задан график на дату
				КонецЕсли;
			Иначе
				ПотенциалДняДляГрафика = -3600; //не заданы графики работы
			КонецЕсли;
			
			НоваяСтрока = ТаблицаПотенциаловПоГрафикам.Добавить();
			НоваяСтрока.Дата = ТекущийДень;
			НоваяСтрока.ГрафикРаботы = ВыборкаМастеров.ГрафикРаботы;
			НоваяСтрока.Потенциал = ПотенциалДняДляГрафика/3600;
			
		КонецЦикла;
	КонецЦикла;
	
	Если ДетальныеЗаписи Тогда
		Возврат ТаблицаПотенциаловПоГрафикам;
	КонецЕсли;
	
	ТаблицаПотенциаловИтог = Новый ТаблицаЗначений;
	ТаблицаПотенциаловИтог.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата"));
	ТаблицаПотенциаловИтог.Колонки.Добавить("Потенциал",Новый ОписаниеТипов("Число"));
	Для СчетчикДней = 0 По 6 Цикл
		НоваяСтрока = ТаблицаПотенциаловИтог.Добавить();
		НоваяСтрока.Дата = НачалоНедели(ДатаОтчета)+60*60*24*СчетчикДней;
		НоваяСтрока.Потенциал = 0;
	КонецЦикла;
	
	ВыборкаМастеров.Сбросить();
	
	Пока ВыборкаМастеров.Следующий() Цикл
		Если ВыборкаМастеров.КатегорияУпорядочивания = 1 Тогда
			Продолжить;
		КонецЕсли;
		Для СчетчикДней = 0 По 6 Цикл
			Отбор = Новый Структура;
			ДатаНедели = НачалоНедели(ДатаОтчета)+60*60*24*СчетчикДней; 
			Отбор.Вставить("Дата",ДатаНедели);
			Отбор.Вставить("ГрафикРаботы",ВыборкаМастеров.ГрафикРаботы);
			СтрокиПотенциалов = ТаблицаПотенциаловПоГрафикам.НайтиСтроки(Отбор);
			Если СтрокиПотенциалов.Количество() = 0  Тогда
				Продолжить;
			КонецЕсли;
			СтрокаИтогов = ТаблицаПотенциаловИтог.Найти(ДатаНедели,"Дата");
			Если СтрокаИтогов <> Неопределено Тогда
				СтрокаИтогов.Потенциал = СтрокаИтогов.Потенциал + ?(СтрокиПотенциалов[0].Потенциал>0, СтрокиПотенциалов[0].Потенциал, 0) ;	
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	
	Возврат ТаблицаПотенциаловИтог;
	
КонецФункции //СформироватьТаблицуПотенциаловРабочихМест()

// Формирует таблицу фактической загрузки на неделю для таблицы показателей
//
Функция СформироватьТаблицуНедельнойЗагрузкиРабочихМест() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	""Пн"" КАК Название,
	               |	1 КАК Номер,
	               |	1 КАК Сортировка
	               |ПОМЕСТИТЬ НазванияДней
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""Вт"",
	               |	2,
	               |	2
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""Ср"",
	               |	3,
	               |	3
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""Чт"",
	               |	4,
	               |	4
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""Пт"",
	               |	5,
	               |	5
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""Сб"",
	               |	6,
	               |	6
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""Вс"",
	               |	7,
	               |	7
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ЗаписьНаРемонт.Ресурс2 КАК Справочник.Сотрудники) КАК РабочееМесто,
	               |	ДОБАВИТЬКДАТЕ(ЗаписьНаРемонт.Дата, СЕКУНДА, ЧАС(ЗаписьНаРемонт.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ЗаписьНаРемонт.НачалоРабочегоВремени) * 60 + СЕКУНДА(ЗаписьНаРемонт.НачалоРабочегоВремени)) КАК НачалоВыполнения,
	               |	ДОБАВИТЬКДАТЕ(ЗаписьНаРемонт.Дата, СЕКУНДА, ЧАС(ЗаписьНаРемонт.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ЗаписьНаРемонт.КонецРабочегоВремени) * 60 + СЕКУНДА(ЗаписьНаРемонт.КонецРабочегоВремени)) КАК ОкончаниеВыполнения,
	               |	ВЫРАЗИТЬ(ЗаписьНаРемонт.Объект КАК Документ.ЗаявкаНаРемонт) КАК Регистратор,
	               |	ЗаписьНаРемонт.Авторабота КАК Авторабота,
	               |	ЗаписьНаРемонт.Продолжительность КАК Продолжительность,
	               |	ЗаписьНаРемонт.Дата КАК Дата
	               |ПОМЕСТИТЬ ГрафикРаботыРесурсовИндекс
	               |ИЗ
	               |	РегистрСведений.ГрафикРаботыРесурсов КАК ЗаписьНаРемонт
	               |ГДЕ
	               |	ЗаписьНаРемонт.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ЗаписьНаРемонт.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПланРемонта)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	РабочееМесто
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Ресурсы.Ссылка КАК РабочееМесто,
	               |	СУММА(ЧАС(ЗаписиНаРемонт.Продолжительность) * 60 * 60 + МИНУТА(ЗаписиНаРемонт.Продолжительность) * 60 + СЕКУНДА(ЗаписиНаРемонт.Продолжительность)) / 3600 КАК Записано,
	               |	ЗаписиНаРемонт.Дата КАК Дата
	               |ПОМЕСТИТЬ ГрафикРаботыРесурсов
	               |ИЗ
	               |	Мастера КАК Ресурсы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикРаботыРесурсовИндекс КАК ЗаписиНаРемонт
	               |		ПО Ресурсы.Ссылка = ЗаписиНаРемонт.РабочееМесто
	               |ГДЕ
	               |	Ресурсы.КатегорияУпорядочивания = 2
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Ресурсы.Ссылка,
	               |	ЗаписиНаРемонт.Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НазванияДней.Название КАК Название,
	               |	НазванияДней.Сортировка КАК Сортировка,
	               |	НазванияДней.Номер КАК Номер,
	               |	СУММА(ЕСТЬNULL(ГрафикРаботыРесурсов.Записано, 0)) КАК Записано
	               |ИЗ
	               |	НазванияДней КАК НазванияДней
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	               |		ПО (НазванияДней.Номер = ДЕНЬНЕДЕЛИ(ГрафикРаботыРесурсов.Дата))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НазванияДней.Название,
	               |	НазванияДней.Сортировка,
	               |	НазванияДней.Номер
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сортировка";	
	Запрос.МенеджерВременныхТаблиц = МВТРабочиеМеста;
	Запрос.УстановитьПараметр("НачалоПериода",НачалоНедели(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецНедели(ДатаОтчета));
	Если РежимКалендаря = 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Мастера","Цеха");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ЗаписьНаРемонт.Ресурс2 КАК Справочник.Сотрудники",
		"ЗаписьНаРемонт.Ресурс1 КАК Справочник.Цеха");
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если МВТРабочиеМеста <> Неопределено Тогда
		МВТРабочиеМеста.Закрыть();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции //СформироватьТаблицуНедельнойЗагрузкиРабочихМест()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

УстановленОтборИсполнители = Ложь;
УстановленОтборЦеха = Ложь;

// цвета раскраски ячеек
ЦветаРаскраски = Новый Соответствие;
ПолучитьЦветаРаскраски();

// отступы
ОтступСлеваВертикально = 2;
ОтступСверхуВертикально = 1;
ОтступСлеваГоризонтально = 1;
ОтступСверхуГоризонтально = 2;

// линии рамки
ЛинияРамкиВыделенияРаботы = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
ЛинияРамкиВнутренняя = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
ЛинияРамкиСплошная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
ЛинияРамкиТочечная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Точечная,1);
ЛинияРамкиДвойная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Двойная,1);

// Картинка по состоянию ЗнР
КартинкаПоСостоянию = Новый Соответствие;
КартинкаПоСостоянию.Вставить("ЗаявкаНеПодтверждена",БиблиотекаКартинок.ОформлениеКругЖелтый);
КартинкаПоСостоянию.Вставить("ЗаявкаПодтверждена",	БиблиотекаКартинок.ОформлениеЗнакФлажок);
КартинкаПоСостоянию.Вставить("ВремяПрошло",			БиблиотекаКартинок.ОформлениеЗнакВосклицательныйЗнак);
КартинкаПоСостоянию.Вставить("ЗНПомеченНаУдаление",	БиблиотекаКартинок.ОформлениеФлагКрасный);
КартинкаПоСостоянию.Вставить("ЗННеПроведен",		БиблиотекаКартинок.ОформлениеФлагЖелтый);
КартинкаПоСостоянию.Вставить("ЗНОформлен",			БиблиотекаКартинок.ОформлениеФлагЗеленый);

//Представление состояния ЗнР
ПредставлениеСостояний = Новый Соответствие;
ПредставлениеСостояний.Вставить("ЗаявкаНеПодтверждена", "Заявка не подтверждена");
ПредставлениеСостояний.Вставить("ЗаявкаПодтверждена",	"Заявка подтверждена");
ПредставлениеСостояний.Вставить("ВремяПрошло",			"Время начала работ прошло");
ПредставлениеСостояний.Вставить("ЗНПомеченНаУдаление",	"Заказ-наряд помечен на удаление");
ПредставлениеСостояний.Вставить("ЗННеПроведен",		 	"Заказ-наряд не проведен");
ПредставлениеСостояний.Вставить("ЗНОформлен",			"Заказ-наряд проведен");

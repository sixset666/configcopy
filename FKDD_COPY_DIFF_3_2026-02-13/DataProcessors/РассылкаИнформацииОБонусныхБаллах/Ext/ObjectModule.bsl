
Функция киКонтрагентов(Контрагенты)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Объект КАК Контрагент,
	|	КонтактнаяИнформация.Вид,
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект В(&Контрагенты)
	|	И КонтактнаяИнформация.Вид В(&ВидыКИ)
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтактнаяИнформация.Вид,
	|	КонтактнаяИнформация.Объект,
	|	КонтактнаяИнформация.Представление";
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	Запрос.УстановитьПараметр("ВидыКИ", ВидыКИ);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьДанныеДляРассылки() Экспорт
	
	Если СхемаКомпановкиДанных = Неопределено Тогда
		СхемаКомпановкиДанных = ПолучитьМакет("Макет");
		КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпановкиДанных));
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпановкиДанных.НастройкиПоУмолчанию);
	КонецЕсли;
	
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпановкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпановкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроекКомпоновкиДанных.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ПериодТекущий", Дата);
	
	ТаблицаРезультат = Новый ТаблицаЗначений();
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпановкиДанных, КомпоновщикНастроекКомпоновкиДанных.Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ВыполнитьРассылку() Экспорт
	
	Если НЕ ЗначениеЗаполнено(ШаблонРассылкиSMS) И НЕ ЗначениеЗаполнено(ШаблонРассылкиEMail) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеДляРассылки.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// поуичим контактную информацию контрагента
	КИКонтрагентов = КИКонтрагентов(ДанныеДляРассылки.ВыгрузитьКолонку("Контрагент"));
	
	// отправим по e-mail
	ДокументыКОтправке = Новый Массив; ДокументSMS = Неопределено; АдресатыSMS = "";
	Для Каждого Рассылка Из ДанныеДляРассылки Цикл
		Если Рассылка.ИспользоватьEMailРассылку И ЗначениеЗаполнено(ШаблонРассылкиEMail) Тогда
			// проверим есть ли e-mail
			Email = КИКонтрагентов.НайтиСтроки(Новый Структура("Контрагент,Вид", Рассылка.Контрагент, Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний));
			Если Email.Количество() > 0 Тогда
				// создадим документ
				НовоеЭлектронноеПисьмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
				НовоеЭлектронноеПисьмо.Заполнить(Неопределено);
				НовоеЭлектронноеПисьмо.ОбменДанными.Загрузка    = Истина;
				НовоеЭлектронноеПисьмо.УчетнаяЗапись            = УчетнаяЗаписьЭлектроннойПочты;
				НовоеЭлектронноеПисьмо.ОтправительПредставление = УчетнаяЗаписьЭлектроннойПочты.АдресЭлектроннойПочты;
				НовоеЭлектронноеПисьмо.ГруппаПисем              = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие");
				НовоеЭлектронноеПисьмо.ФорматТекста             = Перечисления.ФорматТекста.ПростойТекст;
				НовоеЭлектронноеПисьмо.Комментарий              = "Создан регламентным заданием";
				
				Получатель = НовоеЭлектронноеПисьмо.Получатели.Добавить();
				Получатель.АдресЭлектроннойПочты = Email[0].Представление;
				Получатель.СсылкаНаОбъект        = Рассылка.Контрагент;
				Получатель.Представление         = Email[0].Представление;
				Получатель.КодГруппыАдреса       = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
				
				НовоеЭлектронноеПисьмо.Тема        = "Оповещение о бонусах";
				НовоеЭлектронноеПисьмо.ТекстПисьма = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(ШаблонРассылкиEMail, Рассылка);
				
				НовоеЭлектронноеПисьмо.Записать();
				
				ДокументыКОтправке.Добавить(НовоеЭлектронноеПисьмо);
			КонецЕсли;
		КонецЕсли;
		
		Если Рассылка.ИспользоватьSMSРассылку
			И ЗначениеЗаполнено(ШаблонРассылкиSMS)
			И Рассылка.Контрагент.СогласиеНаПолучениеSMS Тогда
			НомерТелефона = КИКонтрагентов.НайтиСтроки(Новый Структура("Контрагент,Вид", Рассылка.Контрагент, Справочники.ВидыКонтактнойИнформации.ТелефонСотовый));
			Если НомерТелефона.Количество() > 0 Тогда
				Если ДокументSMS = Неопределено Тогда
					ДокументSMS = Документы.SMS.СоздатьДокумент();
					ДокументSMS.Заполнить(Неопределено);
					ДокументSMS.УстановитьНовыйНомер();
					
					ДокументSMS.НомерОтправителя = Константы.смсИмяПользователяПоУмолчанию.Получить();
					ДокументSMS.НачалоОтправки   = ТекущаяДата();
					ДокументSMS.ТекстСообщения   = ШаблонРассылкиSMS;
					
					ДокументSMS.Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;
					ДокументSMS.Комментарий        = "Создан регламентным заданием";
				КонецЕсли;
				
				Получатель = ДокументSMS.Получатели.Добавить();
				Получатель.ВладелецКИ    = Рассылка.Контрагент;
				Получатель.НомерТелефона = НомерТелефона[0].Представление;
				Получатель.Контрагент    = Рассылка.Контрагент;
				
				АдресатыSMS = АдресатыSMS + ?(ПустаяСтрока(АдресатыSMS), "", ", ") + спПолучитьНаименование(Получатель.Контрагент);
				
				Получатель.ТекстСообщения = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(ШаблонРассылкиSMS, Рассылка);;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументыКОтправке.Количество() > 0 И НЕ УчетнаяЗаписьЭлектроннойПочты.Пустая() Тогда
		Попытка
			СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
			СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(УчетнаяЗаписьЭлектроннойПочты, СтруктураРежимовОбмена);
			Если СоединениеСПочтовымСервером <> Неопределено Тогда
				Для Каждого ДокументEMail Из ДокументыКОтправке Цикл
					эпОтправитьПисьмо(ДокументEMail, СоединениеСПочтовымСервером);
					#Если Клиент Тогда
						Сообщить(НСтр("ru = 'Отправлено электронное письмо '") + Строка(ДокументEMail.Ссылка));
					#КонецЕсли
				КонецЦикла;
				эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если ДокументSMS <> Неопределено Тогда
		Попытка
			ДокументSMS.Записать(РежимЗаписиДокумента.Запись);
			#Если Клиент Тогда
				Сообщить(НСтр("ru = 'Сформирован документ sms для адресатов: '") + АдресатыSMS);
			#КонецЕсли
			ДокументSMS.ОтправитьSMS();
		Исключение
			ИнформацияООшибке = ИнформацияОбОшибке();
		КонецПопытки;
	КонецЕсли;
	
КонецФункции

Функция АвтоматическаяРассылка() Экспорт
	
	ДанныеДляРассылки.Загрузить(ПолучитьДанныеДляРассылки());
	
	ВыполнитьРассылку();
	
КонецФункции
// Модуль обработки Поставки деталей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;     // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем тблДетали Экспорт; // Содержит таблицу деталей, которые нужно перемещать
Перем тблДеталиКПеремещению Экспорт; // Содержит таблицу деталей, которые нужно перемещать

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

#Если Клиент Тогда
//Заполняет таблицу деталей и таблицу ЗН
Процедура ЗаполнитьИсходныеДанные() Экспорт
	
	ТипКоличества = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 3));
	
	Если ТипЗнч(тблДетали) = Тип("ТаблицаЗначений") Тогда
		тблДетали.Очистить()
	Иначе
		тблДетали = Новый ТаблицаЗначений;
		тблДетали.Колонки.Добавить("Использование", Новый ОписаниеТипов("Булево"));
		тблДетали.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		тблДетали.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		тблДетали.Колонки.Добавить("СкладКомпании", Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
		тблДетали.Колонки.Добавить("Количество", ТипКоличества);
		тблДетали.Колонки.Добавить("ВПроизводстве", ТипКоличества);
		тблДетали.Колонки.Добавить("Остаток", ТипКоличества);
		тблДетали.Колонки.Добавить("Заказано", ТипКоличества);
		тблДетали.Колонки.Добавить("Резерв", ТипКоличества);
		тблДетали.Колонки.Добавить("ЗаказНаряд", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаряд"));
		тблДетали.Индексы.Добавить("ЗаказНаряд");
		тблДетали.Индексы.Добавить("Использование");
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	СтрокаУсловия="";
	Для Каждого ТекОтбор Из ОтборЗаказНарядов Цикл
		Если ТекОтбор.ПутьКДанным = "Ссылка" ИЛИ НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		СтрокаУсловия=СтрокаУсловия+" И "+Символы.ПС+ СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ТекОтбор.ВидСравнения),"#","ЗаказНарядТовары.Ссылка."+ТекОтбор.ПутьКДанным),"&","&Выб"+ТекОтбор.Имя);
		Запрос.УстановитьПараметр("Выб"+ТекОтбор.Имя, ТекОтбор.Значение);
	КонецЦикла;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТоварыВПроизводствеОстатки.ЗаказНаряд,
	|	ТоварыВПроизводствеОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводствеОстатки.КоличествоОстаток КАК ВПроизводстве
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве.Остатки(,) КАК ТоварыВПроизводствеОстатки";
	ТаблицаТоваровВПроизводстве = Запрос.Выполнить().Выгрузить();
	ТаблицаТоваровВПроизводстве.Индексы.Добавить("ЗаказНаряд");
	ТаблицаТоваровВПроизводстве.Индексы.Добавить("Номенклатура");
	ТаблицаТоваровВПроизводстве.Индексы.Добавить("ХарактеристикаНоменклатуры");
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДеталей.ЗаказНаряд КАК ЗаказНаряд,
	|	ТаблицаДеталей.Дата КАК Дата,
	|	ТаблицаДеталей.Номенклатура КАК Номенклатура,
	|	ТаблицаДеталей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаДеталей.СкладКомпании КАК СкладКомпании,
	|	ТаблицаДеталей.НадоПоЗН КАК НадоПоЗН,
	|	ЕСТЬNULL(ЗаказыПокупателейОстаткиЗаказано.ЗаказаноОстаток, 0) КАК Заказано,
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) КАК Зарезервировано,
	|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0) КАК Остаток,
	|	ВЫБОР
	|		КОГДА ТаблицаДеталей.НадоПоЗН>(ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0)+ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) ТОГДА
	|			ТаблицаДеталей.НадоПоЗН-(ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0)+ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК ТребуетсяЗаказать
	|ИЗ
	|(ВЫБРАТЬ
	|	ЗаказНарядТовары.Ссылка КАК ЗаказНаряд,
	|	ЗаказНарядТовары.Ссылка.Дата КАК Дата,
	|	ЗаказНарядТовары.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказНарядТовары.Ссылка.ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду КАК ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду,
	|	ЗаказНарядТовары.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ЗаказНарядТовары.Номенклатура,
	|	ЗаказНарядТовары.ХарактеристикаНоменклатуры,
	|	СУММА(ЗаказНарядТовары.Количество*ЗаказНарядТовары.Коэффициент) КАК НадоПоЗН,
	|	ЗаказНарядТовары.СкладКомпании
	|ИЗ
	|	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
	|ГДЕ
	|	(НЕ ЗаказНарядТовары.Ссылка.ПометкаУдаления) И 
	|	(ЗаказНарядТовары.Ссылка.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен) И ЗаказНарядТовары.Ссылка.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт))
	|	И (НЕ ЗаказНарядТовары.СкладКомпании = ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)) "+СтрокаУсловия+"
	|СГРУППИРОВАТЬ ПО 
	|	ЗаказНарядТовары.Ссылка,
	|	ЗаказНарядТовары.Номенклатура,
	|	ЗаказНарядТовары.ХарактеристикаНоменклатуры,
	|	ЗаказНарядТовары.СкладКомпании
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказНарядТовары.Количество*ЗаказНарядТовары.Коэффициент)>0) КАК ТаблицаДеталей
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(,) КАК ЗаказыПокупателейОстатки
	|ПО
	|	(ТаблицаДеталей.ДокументОснование = ЗаказыПокупателейОстатки.Заказ ИЛИ 
	|	ВЫБОР 
	|		КОГДА ТаблицаДеталей.ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду ТОГДА
	|			ТаблицаДеталей.ЗаказНаряд = ВЫРАЗИТЬ(ЗаказыПокупателейОстатки.Заказ КАК Документ.ЗаказПокупателя).ДокументОснование
	|		ИНАЧЕ
	|			ТаблицаДеталей.ЗаказНаряд = ВЫРАЗИТЬ(ЗаказыПокупателейОстатки.Заказ КАК Документ.ЗаказПокупателя).ДокументОснование ИЛИ 
	|			ТаблицаДеталей.Контрагент = ЗаказыПокупателейОстатки.Контрагент
	|		КОНЕЦ) И 	
	|	ТаблицаДеталей.Номенклатура = ЗаказыПокупателейОстатки.Номенклатура И
	|	ТаблицаДеталей.СкладКомпании = ЗаказыПокупателейОстатки.СкладКомпании И
	|	ТаблицаДеталей.ХарактеристикаНоменклатуры = ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,) КАК ОстаткиТоваровКомпанииОстатки
	|ПО 
	|	ТаблицаДеталей.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура И
	|	ТаблицаДеталей.СкладКомпании = ОстаткиТоваровКомпанииОстатки.СкладКомпании И
	|	ТаблицаДеталей.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ
	|	ЗаказыПокупателейОстатки.Контрагент,
	|	ЗаказыПокупателейОстатки.Заказ,
	|	ЗаказыПокупателейОстатки.Номенклатура,
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	|	СУММА(ЗаказыПокупателейОстатки.ЗаказаноОстаток) КАК ЗаказаноОстаток
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки КАК ЗаказыПокупателейОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыПокупателейОстатки.Контрагент,
	|	ЗаказыПокупателейОстатки.Заказ,
	|	ЗаказыПокупателейОстатки.Номенклатура,
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры) КАК ЗаказыПокупателейОстаткиЗаказано
	|ПО
	|	(ТаблицаДеталей.ДокументОснование = ЗаказыПокупателейОстаткиЗаказано.Заказ ИЛИ 
	|	ВЫБОР 
	|		КОГДА ТаблицаДеталей.ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду ТОГДА
	|			ТаблицаДеталей.ЗаказНаряд = ВЫРАЗИТЬ(ЗаказыПокупателейОстаткиЗаказано.Заказ КАК Документ.ЗаказПокупателя).ДокументОснование
	|		ИНАЧЕ
	|			ТаблицаДеталей.ЗаказНаряд = ВЫРАЗИТЬ(ЗаказыПокупателейОстаткиЗаказано.Заказ КАК Документ.ЗаказПокупателя).ДокументОснование ИЛИ 
	|			ТаблицаДеталей.Контрагент = ЗаказыПокупателейОстаткиЗаказано.Контрагент
	|		КОНЕЦ) И 	
	|	ТаблицаДеталей.Номенклатура = ЗаказыПокупателейОстаткиЗаказано.Номенклатура И
	|	ТаблицаДеталей.ХарактеристикаНоменклатуры = ЗаказыПокупателейОстаткиЗаказано.ХарактеристикаНоменклатуры

	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ,
	|	ЗаказНаряд,
	|	ТребуетсяЗаказать УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(Дата),
	|	СУММА(ТребуетсяЗаказать),
	|	СУММА(НадоПоЗН),
	|	СУММА(Заказано),
	|	СУММА(Зарезервировано),
	|	СУММА(Остаток)
	|ПО
	|	ЗаказНаряд,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	СкладКомпании";
	
	ТаблицаЗН = Новый ТаблицаЗначений;
	ТаблицаЗН.Колонки.Добавить("ЗаказНаряд",      Новый ОписаниеТипов("ДокументСсылка.ЗаказНаряд"));
	ТаблицаЗН.Колонки.Добавить("КоличествоСтрок", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)));
	
	тблДетали.Очистить();
	ВыборкаЗаказНаряд = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	Пока ВыборкаЗаказНаряд.Следующий() Цикл
		Счетчик = 0;
		ЗаказНаряд = ВыборкаЗаказНаряд.ЗаказНаряд;
		ВыборкаНоменклатуры=ВыборкаЗаказНаряд.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатуры.Следующий() Цикл
			Номенклатура = ВыборкаНоменклатуры.Номенклатура;
			ВыборкаХарактеристик=ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристик.Следующий() Цикл
				ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
				СтрокиПоискаПроизводства = ТаблицаТоваровВПроизводстве.НайтиСтроки(Новый Структура("ЗаказНаряд, Номенклатура, ХарактеристикаНоменклатуры",
				ЗаказНаряд, Номенклатура, ХарактеристикаНоменклатуры));
				ВПроизводстве = 0;
				Если СтрокиПоискаПроизводства.Количество()>0 Тогда
					ВПроизводстве = СтрокиПоискаПроизводства[0].ВПроизводстве;
					ТаблицаТоваровВПроизводстве.Удалить(СтрокиПоискаПроизводства[0]);
				КонецЕсли;
				
				Если ВПроизводстве>=ВыборкаХарактеристик.НадоПоЗН Тогда
					Продолжить;
				КонецЕсли;
				ВыборкаСкладов = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСкладов.Следующий() Цикл
					// по заказам
					
					ВПроизводствеПоСкладу = Мин(ВПроизводстве, ВыборкаСкладов.НадоПоЗН);
					ВПроизводстве = ВПроизводстве - ВПроизводствеПоСкладу;
					
					ОсталосьПереметить = ВыборкаСкладов.НадоПоЗН-ВПроизводствеПоСкладу;
					МожноПереместитьПоЗаказу = Мин(ОсталосьПереметить, ВыборкаСкладов.Заказано, ВыборкаСкладов.Зарезервировано);
					ОсталосьПереметить = ОсталосьПереметить-МожноПереместитьПоЗаказу;
					МожноПереместитьПоСкладу = Мин(ОсталосьПереметить, ВыборкаСкладов.Остаток);
					
					Если (МожноПереместитьПоЗаказу+МожноПереместитьПоСкладу)>0 Тогда
						НоваяСтрока = тблДетали.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСкладов);
						НоваяСтрока.Использование	= Истина;
						НоваяСтрока.Количество		= МожноПереместитьПоЗаказу+МожноПереместитьПоСкладу;
						НоваяСтрока.ВПроизводстве	= ВПроизводствеПоСкладу;
						НоваяСтрока.Заказано		= ВыборкаСкладов.Заказано;
						НоваяСтрока.Остаток			= ВыборкаСкладов.Остаток;
						НоваяСтрока.Резерв			= ВыборкаСкладов.Зарезервировано;
						Счетчик						= Счетчик+1;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		Если Счетчик>0 Тогда
			НоваяСтрока = ТаблицаЗН.Добавить();
			НоваяСтрока.ЗаказНаряд = ВыборкаЗаказНаряд.ЗаказНаряд;
			НоваяСтрока.КоличествоСтрок = Счетчик;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаЗН.ЗаказНаряд,
	|	ТаблицаЗН.КоличествоСтрок
	|ПОМЕСТИТЬ ТаблицаЗН
	|ИЗ
	|	&ТаблицаЗН КАК ТаблицаЗН
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаряд
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЗН.ЗаказНаряд КАК ЗаказНаряд,
	|	ЗаказНаряды.Дата,
	|	ЗаказНаряды.Автор,
	|	ЗаказНаряды.СкидкаНаценка,
	|	ЗаказНаряды.ВалютаДокумента КАК Валюта,
	|	ЗаказНаряды.ДоговорВзаиморасчетов КАК Договор,
	|	ЗаказНаряды.Заказчик,
	|	Истина КАК Использование,
	|	ЗаказНаряды.Контрагент,
	|	ЗаказНаряды.КурсДокумента КАК Курс,
	|	ЗаказНаряды.Организация,
	|	ЗаказНаряды.Состояние КАК Статус,
	|	ЗаказНаряды.СуммаДокумента КАК Сумма,
	|	ТаблицаЗН.КоличествоСтрок
	|ИЗ
	|	ТаблицаЗН КАК ТаблицаЗН
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаряд КАК ЗаказНаряды
	|ПО 
	|	ТаблицаЗН.ЗаказНаряд = ЗаказНаряды.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНаряды.Дата УБЫВ");
	Запрос.УстановитьПараметр("ТаблицаЗН", ТаблицаЗН);
	ЗаказНаряды.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры //ЗаполнитьИсходныеДанные()
#КонецЕсли

//По таблице деталей формирует перемещения.
Процедура СформироватьПеремещения() Экспорт
		
	ЗаказНарядыКПеремещению.Очистить();
	Если ТипЗнч(тблДеталиКПеремещению) = Тип("ТаблицаЗначений") Тогда
		тблДеталиКПеремещению.Очистить()
	Иначе
		ТипКоличества = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 3));
		тблДеталиКПеремещению = Новый ТаблицаЗначений;
		тблДеталиКПеремещению.Колонки.Добавить("Перемещение",);
		тблДеталиКПеремещению.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		тблДеталиКПеремещению.Колонки.Добавить("СкладКомпании", Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
		тблДеталиКПеремещению.Колонки.Добавить("ЗаказНаряд", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаряд"));
		тблДеталиКПеремещению.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		тблДеталиКПеремещению.Колонки.Добавить("КоличествоПоДокументу", ТипКоличества);
		тблДеталиКПеремещению.Колонки.Добавить("КоличествоКПеремещению", ТипКоличества);
		тблДеталиКПеремещению.Колонки.Добавить("Остаток", ТипКоличества);
		тблДеталиКПеремещению.Колонки.Добавить("Резерв", ТипКоличества);
		тблДеталиКПеремещению.Колонки.Добавить("Инд", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 0)));
	КонецЕсли;
	
	СтрокиДляКопирования = тблДетали.НайтиСтроки(Новый Структура("Использование", Истина));
	Если СтрокиДляКопирования.Количество()>0 Тогда
		ПромежуточнаяТаблица = тблДетали.Скопировать(СтрокиДляКопирования, "ЗаказНаряд, СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры, Количество");
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ТаблицаТоваров.ЗаказНаряд,
		|	ТаблицаТоваров.СкладКомпании,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|;
		|ВЫБРАТЬ
		|	ТаблицаТоваров.ЗаказНаряд,
		|	ТаблицаТоваров.ЗаказНаряд.Контрагент КАК Контрагент,
		|	ТаблицаТоваров.ЗаказНаряд.Дата КАК Дата,
		|	ТаблицаТоваров.ЗаказНаряд.ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду КАК ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду,
		|	ТаблицаТоваров.ЗаказНаряд.ДокументОснование КАК ДокументОснование,
		|	ТаблицаТоваров.СкладКомпании,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.Количество
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	ВременнаяТаблицаТоваров КАК ТаблицаТоваров
		|ИНДЕКСИРОВАТЬ ПО
		|	СкладКомпании,
		|	Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	ВременнаяТаблицаТоваров
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказНарядТовары.ЗаказНаряд,
		|	ЗаказНарядТовары.СкладКомпании КАК СкладКомпании,
		|	ЗаказНарядТовары.Номенклатура КАК Номенклатура,
		|	ЗаказНарядТовары.ХарактеристикаНоменклатуры,
		|	СУММА(ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0)) КАК Резерв
		|ИЗ
		|	ТаблицаТоваров КАК ЗаказНарядТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей.Остатки(,) КАК ЗаказыПокупателейОстатки
		|ПО 
		|	(ЗаказНарядТовары.ДокументОснование = ЗаказыПокупателейОстатки.Заказ ИЛИ 
		|	ВЫБОР 
		|		КОГДА ЗаказНарядТовары.ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду ТОГДА
		|			ЗаказНарядТовары.ЗаказНаряд = ВЫРАЗИТЬ(ЗаказыПокупателейОстатки.Заказ КАК Документ.ЗаказПокупателя).ДокументОснование
		|		ИНАЧЕ
		|			ЗаказНарядТовары.ЗаказНаряд = ВЫРАЗИТЬ(ЗаказыПокупателейОстатки.Заказ КАК Документ.ЗаказПокупателя).ДокументОснование ИЛИ 
		|			ЗаказНарядТовары.Контрагент = ЗаказыПокупателейОстатки.Контрагент
		|		КОНЕЦ) И 	
		|	ЗаказНарядТовары.СкладКомпании = ЗаказыПокупателейОстатки.СкладКомпании И
		|	ЗаказНарядТовары.Номенклатура = ЗаказыПокупателейОстатки.Номенклатура И
		|	ЗаказНарядТовары.ХарактеристикаНоменклатуры = ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНарядТовары.ЗаказНаряд,
		|	ЗаказНарядТовары.СкладКомпании,
		|	ЗаказНарядТовары.Номенклатура,
		|	ЗаказНарядТовары.ХарактеристикаНоменклатуры");
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ТаблицаТоваров", ПромежуточнаяТаблица);
		ТаблицаРезервов=Запрос.Выполнить().Выгрузить();
		ТаблицаРезервов.Индексы.Добавить("ЗаказНаряд");
		ТаблицаРезервов.Индексы.Добавить("СкладКомпании");
		ТаблицаРезервов.Индексы.Добавить("Номенклатура");
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.СкладКомпании,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК Остаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,) КАК ОстаткиТоваровКомпанииОстатки
		|ГДЕ
		|	(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток)>0
		|";
		ТаблицаТоваров=Запрос.Выполнить().Выгрузить();
		ТаблицаТоваров.Индексы.Добавить("СкладКомпании");
		ТаблицаТоваров.Индексы.Добавить("Номенклатура");
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаТоваров.ЗаказНаряд,
		|	ТаблицаТоваров.Дата КАК ЗаказНарядДата,
		|	ТаблицаТоваров.ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду КАК ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду,
		|	ТаблицаТоваров.СкладКомпании КАК СкладКомпании,
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.Количество КАК КоличествоПоДокументу
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров
		|УПОРЯДОЧИТЬ ПО
		|	ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду,
		|	ЗаказНарядДата
		|ИТОГИ 
		|	МАКСИМУМ(ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду),
		|	МАКСИМУМ(ЗаказНарядДата),
		|	СУММА(КоличествоПоДокументу)
		|ПО
		|	ЗаказНаряд,
		|	СкладКомпании,
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры";
		
		Инд = 0;
		МассивЗН = Новый Массив;
		СоответствиеЗакрытиеЗаказовПоПодразделению = Новый Соответствие;
		ВыборкаЗаказНаряд = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗаказНаряд.Следующий() Цикл
			ЗаказНаряд=ВыборкаЗаказНаряд.ЗаказНаряд;
			СоздатьЗаказ=Ложь;
			ВыборкаСкладКомпании = ВыборкаЗаказНаряд.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСкладКомпании.Следующий() Цикл
				СкладКомпании=ВыборкаСкладКомпании.СкладКомпании;
				МассивСтрок = Новый Массив;
				СоздатьПеремещение = Ложь;
				ВыборкаНоменклатура = ВыборкаСкладКомпании.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаНоменклатура.Следующий() Цикл
					ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаХарактеристика.Следующий() Цикл
						ОстатокПоДокументу = ВыборкаХарактеристика.КоличествоПоДокументу;
						КоличествоКПеремещению = 0;
						КоличествоКПеремещениюПоОстаткам=0;
						Остаток	= 0;
						Резерв	= 0;
						СтрокиПоиска = ТаблицаРезервов.НайтиСтроки(Новый Структура("ЗаказНаряд, СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры", 
						ВыборкаХарактеристика.ЗаказНаряд, СкладКомпании, ВыборкаХарактеристика.Номенклатура, ВыборкаХарактеристика.ХарактеристикаНоменклатуры));
						Если СтрокиПоиска.Количество()>0 Тогда 
							СтрокаПоиска = СтрокиПоиска[0];
							Резерв	= СтрокаПоиска.Резерв;
							//Остаток = СтрокаПоиска.Остаток;
							КоличествоКПеремещению = Мин(Резерв, ОстатокПоДокументу);
							ОстатокПоДокументу = ОстатокПоДокументу-КоличествоКПеремещению;
							Если КоличествоКПеремещению=СтрокаПоиска.Резерв Тогда
								ТаблицаРезервов.Удалить(СтрокаПоиска);
							Иначе
								СтрокаПоиска.Резерв=СтрокаПоиска.Резерв-КоличествоКПеремещению;
							КонецЕсли;
							//если все перемещаем из резерва найдем остаки по складу
							Если НЕ(ОстатокПоДокументу > 0) Тогда
								СтрокиПоиска = ТаблицаТоваров.НайтиСтроки(Новый Структура("СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры",
								СкладКомпании, ВыборкаХарактеристика.Номенклатура, ВыборкаХарактеристика.ХарактеристикаНоменклатуры));
								Если СтрокиПоиска.Количество()>0 Тогда
									СтрокаПоиска = СтрокиПоиска[0];
									Остаток	= СтрокаПоиска.Остаток;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						Если ОстатокПоДокументу>0 Тогда
							СтрокиПоиска = ТаблицаТоваров.НайтиСтроки(Новый Структура("СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры",
							СкладКомпании, ВыборкаХарактеристика.Номенклатура, ВыборкаХарактеристика.ХарактеристикаНоменклатуры));
							Если СтрокиПоиска.Количество()>0 Тогда
								СтрокаПоиска = СтрокиПоиска[0];
								Остаток	= СтрокаПоиска.Остаток;
								КоличествоКПеремещениюПоОстаткам = Мин(Остаток, ОстатокПоДокументу);
								ОстатокПоДокументу=ОстатокПоДокументу-КоличествоКПеремещениюПоОстаткам;
								Если КоличествоКПеремещениюПоОстаткам=СтрокаПоиска.Остаток Тогда
									ТаблицаТоваров.Удалить(СтрокаПоиска);
								Иначе
									СтрокаПоиска.Остаток=СтрокаПоиска.Остаток-КоличествоКПеремещениюПоОстаткам;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						КоличествоКПеремещению = КоличествоКПеремещению+КоличествоКПеремещениюПоОстаткам;
						Если КоличествоКПеремещению>0 Тогда
							НоваяСтрока = тблДеталиКПеремещению.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаХарактеристика);
							НоваяСтрока.Остаток	= Остаток;
							НоваяСтрока.Резерв	= Резерв;
							НоваяСтрока.КоличествоКПеремещению = КоличествоКПеремещению;	
							СоздатьЗаказ = Истина;
							СоздатьПеремещение = Истина;
							МассивСтрок.Добавить(НоваяСтрока);
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
				Если СоздатьПеремещение Тогда
					Перемещение = Документы.ПеремещениеТоваровВПроизводство.СоздатьДокумент();
					#Если Клиент Тогда
						Перемещение.Дата = РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
					#Иначе
						Перемещение.Дата = КонецДня(ТекущаяДата());
					#КонецЕсли
					Перемещение.СкладКомпании = СкладКомпании;
					ОбработкаЗаполненияОтказ = НЕ дкОбработкаЗаполнения(Перемещение, ЗаказНаряд);
					
					ПодразделениеКомпании = Перемещение.ПодразделениеКомпании;
					Если ЗначениеЗаполнено(ПодразделениеКомпании) Тогда
						ЗакрытиеЗаказовПоПодразделению = СоответствиеЗакрытиеЗаказовПоПодразделению[ПодразделениеКомпании];
						Если ЗакрытиеЗаказовПоПодразделению=Неопределено Тогда
							Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",Перемещение) Тогда
								Перемещение.ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
							Иначе
								Перемещение.ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
							КонецЕсли; 
							СоответствиеЗакрытиеЗаказовПоПодразделению.Вставить(ПодразделениеКомпании, Перемещение.ЗакрытиеЗаказовПоПодразделению);
						Иначе
							Перемещение.ЗакрытиеЗаказовПоПодразделению=ЗакрытиеЗаказовПоПодразделению;
						КонецЕсли; 
					КонецЕсли; 
					// Заполнение ТЧ
					Если ОбработкаЗаполненияОтказ Тогда Продолжить; КонецЕсли;
					Товары = Перемещение.Товары;
					Товары.Очистить();
					Для Каждого ТекСтрока Из МассивСтрок Цикл
						НоваяСтрока = Товары.Добавить();
						НоваяСтрока.Номенклатура = ТекСтрока.Номенклатура;
						Перемещение.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
						НоваяСтрока.Количество = ТекСтрока.КоличествоКПеремещению/НоваяСтрока.Коэффициент;
						ТекСтрока.Перемещение = Перемещение;
						ТекСтрока.Инд = Инд;
					КонецЦикла;
					Инд = Инд+1;
					
					Если СкладКомпании.Розничный Тогда
						Перемещение.ТипЦен = СкладКомпании.ТипЦенРозничнойТорговли;
						Перемещение.ВалютаДокумента = обВалютаТипаЦены(Неопределено,Перемещение.ТипЦен,Ложь);
						СтруктураКурса = обКурсДляВалюты(Перемещение.ВалютаДокумента,Перемещение.Дата);
						КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
						Для Каждого СтрТовар Из Товары Цикл
							СтрТовар.ЦенаРозничная = обПолучитьЦену(Перемещение.ТипЦен,СтрТовар.Номенклатура,Перемещение.Дата,,Перемещение.ВалютаДокумента,Перемещение.КурсДокумента,СтрТовар.ХарактеристикаНоменклатуры,СтрТовар.ЕдиницаИзмерения,СкладКомпании.Подразделение);
							Перемещение.ОбработкаРеквизита("Товары.ЦенаРозничная",СтрТовар);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если СоздатьЗаказ Тогда
				МассивЗН.Добавить(ВыборкаЗаказНаряд.ЗаказНаряд);
			КонецЕсли;
		КонецЦикла;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ЗаказНаряд,
		|	ЗаказНаряд.Автор,
		|	ЗаказНаряд.ВалютаДокумента КАК Валюта,
		|	ЗаказНаряд.ДоговорВзаиморасчетов КАК Договор,
		|	ЗаказНаряд.Заказчик,
		|	ЗаказНаряд.Контрагент,
		|	ЗаказНаряд.КурсДокумента КАК Курс,
		|	ЗаказНаряд.Организация,
		|	ЗаказНаряд.Состояние КАК Статус,
		|	ЗаказНаряд.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Ссылка В(&ВыбЗаказНаряды)
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаряд.Дата";
		Запрос.УстановитьПараметр("ВыбЗаказНаряды", МассивЗН);
		ЗаказНарядыКПеремещению.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
КонецПроцедуры //СформироватьПеремещения()

// Подводит перемещения
Процедура ПровестиПеремещения() Экспорт 
	Если тблДеталиКПеремещению = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиКПеремещению = тблДеталиКПеремещению.Скопировать(,"Перемещение");
	СтрокиКПеремещению.Свернуть("Перемещение");
	Проводить = обПраво("ПеремещениеДеталейВПроизводство", Права);
	Для Каждого ТекПеремещение Из СтрокиКПеремещению Цикл
		Если Проводить Тогда
			Попытка
				ТекПеремещение.Перемещение.Записать(РежимЗаписиДокумента.Проведение);
				#Если Клиент Тогда
					Сообщить("Сформирован и проведен документ перемещения в производство <"+СокрЛП(ТекПеремещение.Перемещение)+">.");
				#КонецЕсли
			Исключение
				#Если Клиент Тогда
					Сообщить("Ошибка проведения документа перемещения в производство <"+СокрЛП(ТекПеремещение.Перемещение)+">.");
				#КонецЕсли
			КонецПопытки;
		Иначе
			Попытка
				ТекПеремещение.Перемещение.Записать(РежимЗаписиДокумента.Запись);
				#Если Клиент Тогда
					Сообщить("Сформирован документ перемещения в производство <"+СокрЛП(ТекПеремещение.Перемещение)+">.
					|Провести его позднее может уполномоченное на это лицо.");
				#КонецЕсли
			Исключение
				#Если Клиент Тогда
					Сообщить("Ошибка записи документа перемещения в производство <"+СокрЛП(ТекПеремещение.Перемещение)+">.");
				#КонецЕсли
			КонецПопытки; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры //ПровестиПеремещения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


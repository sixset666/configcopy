// Модуль объекта обработки "АРМ Подбор автомобилей"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

#Если Клиент Тогда

Перем Права Экспорт; 						// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем СтруктураНеВидимыхОтборов Экспорт; 	//список невидимых отборов


///////////////////////////////////////////////////////////////////////////////
//	ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Формирование текста запроса построителя
Функция ПолучитьТекстПостроителя(ТипЦенДокумента, СтруктураСвойств)
	
	Если НЕ ТипЗнч(СтруктураСвойств) = Тип("Структура") Тогда
		
		СтруктураСвойств = Новый Структура;
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложеннаяТаблица.Свойство КАК Свойство,
	|	ВложеннаяТаблица.Свойство.ТипЗначения КАК ТипЗначения,
	|	ПРЕДСТАВЛЕНИЕ(ВложеннаяТаблица.Свойство) КАК ПредставлениеСвойства
	|ИЗ(
	|	ВЫБРАТЬ
	|		НазначенияСвойств.Свойство КАК Свойство
	|	ИЗ
	|		ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойств
	|	ГДЕ
	|		НазначенияСвойств.Ссылка.ПометкаУдаления = Ложь И 
	|		НазначенияСвойств.Свойство.ПометкаУдаления = Ложь И 
	|		(НазначенияСвойств.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.Справочник_Автомобили) ИЛИ 
	|		НазначенияСвойств.Ссылка.БазовоеНазначение = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.Справочник_Автомобили))
	|	СГРУППИРОВАТЬ ПО
	|		НазначенияСвойств.Свойство) КАК ВложеннаяТаблица
	|";
	
	ТекстСоединенияСвойств = "";
	ТекстОтбораСвойств     = "";
	СвойстваВыборка = Запрос.Выполнить().Выбрать();
	Пока СвойстваВыборка.Следующий() Цикл
		
		ИмяСвойства           = "Свойство"+СтрЗаменить(СвойстваВыборка.Свойство.УникальныйИдентификатор(), "-", "_");
		ПредставлениеСвойства = СвойстваВыборка.ПредставлениеСвойства;
		
		ПредставленияТипа = отПолучитьСтрокуПредставленияТипа(СвойстваВыборка.ТипЗначения);
		
		ПустоеЗначение = """";
		Если ПредставленияТипа = "Строка" Тогда
			ПустоеЗначение = """""";
			ПредставленияТипа = "Строка(" + Строка(СвойстваВыборка.ТипЗначения.КвалификаторыСтроки.Длина) + ")";
		ИначеЕсли ПредставленияТипа = "Число" Тогда
			ПустоеЗначение = "0";
		ИначеЕсли ПредставленияТипа = "Дата" Тогда
			ПустоеЗначение = "ДАТАВРЕМЯ(1,1,1)";
		ИначеЕсли ПредставленияТипа = "Булево" Тогда
			ПустоеЗначение = "ЛОЖЬ";
		Иначе
			ПустоеЗначение = "ЗНАЧЕНИЕ("+ПредставленияТипа+".ПустаяСсылка)";
		КонецЕсли;
		
		ТекстСоединенияСвойств = ТекстСоединенияСвойств + "
		|{ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов" + ИмяСвойства + "
		|ПО
		|	ТаблицаАвтомобилей.Автомобиль = ВЫРАЗИТЬ(ЗначенияСвойствОбъектов" + ИмяСвойства + ".Объект КАК Справочник.Автомобили) И 
		|	ЗначенияСвойствОбъектов" + ИмяСвойства + ".Свойство = &Параметр" + ИмяСвойства + "}";
		
		ТекстОтбораСвойств = ТекстОтбораСвойств + ",
		|	ВЫРАЗИТЬ(ЕСТЬNULL(ЗначенияСвойствОбъектов" + ИмяСвойства + ".Значение, " + ПустоеЗначение + ") КАК " + ПредставленияТипа + ") КАК " + ИмяСвойства;
		
		СтруктураСвойств.Вставить(ИмяСвойства, ПредставлениеСвойства);
		
		ПостроительОтчета.Параметры.Вставить("Параметр" + ИмяСвойства, СвойстваВыборка.Свойство);
		
	КонецЦикла;
	
	РабочийТипЦен = ТипЦенДокумента;
	ТекстЦена = "ТаблицаЦенАвтомобилей.Цена";
	ТекстЦенаМоделей = "ТаблицаЦенАвтомобилей.Цена";
	//// Если расчетная цена получим базовую цену
	Пока РабочийТипЦен.Рассчитывается Цикл
		Если РабочийТипЦен.ПроцентСкидкиНаценки<>0 Тогда
			ТекстЦена = "("+ТекстЦена+"+"+ТекстЦена+"*"+Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
			ТекстЦенаМоделей = "("+ТекстЦенаМоделей+"+"+ТекстЦенаМоделей+"*"+Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
		КонецЕсли;
		РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
	КонецЦикла;
	
	ПостроительОтчета.Параметры.Вставить("ТипЦенПродажи", РабочийТипЦен);
	ПостроительОтчета.Параметры.Вставить("ВалютаТипаЦен", РабочийТипЦен.ВалютаЦены);
	
	Если РабочийТипЦен.ВВалютеУчета Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	КурсыВалют.Валюта КАК Валюта,
		|	(ВЫБОР
		|		КОГДА КурсыВалют.Курс = 0 ИЛИ КурсыВалют.Кратность = 0 ТОГДА
		|			1
		|		ИНАЧЕ
		|			КурсыВалют.Курс/КурсыВалют.Кратность
		|	КОНЕЦ)/&КурсДокументаОснования КАК Коэффициент
		|ПОМЕСТИТЬ
		|	КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних() КАК КурсыВалют
		|;
		|";
	Иначе
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	КурсыВалют.Валюта КАК Валюта,
		|	(ВЫБОР
		|		КОГДА КурсыВалют.Курс = 0 ИЛИ КурсыВалют.Кратность = 0 ТОГДА
		|			1
		|		ИНАЧЕ
		|			КурсыВалют.Курс/КурсыВалют.Кратность
		|	КОНЕЦ)/&КурсДокументаОснования КАК Коэффициент
		|ПОМЕСТИТЬ
		|	КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(, Валюта=&ВалютаТипаЦен) КАК КурсыВалют
		|;
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	  |	ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	  |	МАКСИМУМ(ЕСТЬNULL(ЗаказыПоставщикамНаАвтомобили.ЗаказПоставщику, ЗНАЧЕНИЕ(Документ.ЗаказПоставщикуНаАвтомобиль.ПустаяСсылка))) КАК ЗаказПоставщику,
	  |	МАКСИМУМ(ОстаткиАвтомобилейОстатки.Период) КАК ДатаПоступления,
	  |	МАКСИМУМ(ОстаткиАвтомобилейОстатки.Период) КАК ДатаПервогоПоступления
	  |ПОМЕСТИТЬ ТаблицаДатПоступления
	  |ИЗ
	  |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилейОстатки
	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикамНаАвтомобили КАК ЗаказыПоставщикамНаАвтомобили
	  |		ПО (ОстаткиАвтомобилейОстатки.Регистратор ССЫЛКА Документ.ПоступлениеАвтомобилей)
	  |			И ОстаткиАвтомобилейОстатки.Автомобиль = ЗаказыПоставщикамНаАвтомобили.Автомобиль
	  |ГДЕ
	  |	(ОстаткиАвтомобилейОстатки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	  |				И ОстаткиАвтомобилейОстатки.Количество > 0
	  |			ИЛИ ОстаткиАвтомобилейОстатки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	  |				И ОстаткиАвтомобилейОстатки.Количество < 0)
	  |	И ОстаткиАвтомобилейОстатки.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеАвтомобилей)
	  |{ГДЕ
	  |	ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	  |	ОстаткиАвтомобилейОстатки.Автомобиль.Модель КАК Модель,
	  |	ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	  |	(ВЫБОР
	  |			КОГДА ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	  |				ТОГДА ОстаткиАвтомобилейОстатки.Автомобиль.ТипКузова
	  |			ИНАЧЕ ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации.ТипКузова
	  |		КОНЕЦ) КАК ТипКузова,
	  |	(ВЫБОР
	  |			КОГДА ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	  |				ТОГДА ОстаткиАвтомобилейОстатки.Автомобиль.ТипДвигателя
	  |			ИНАЧЕ ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации.ТипДвигателя
	  |		КОНЕЦ) КАК ТипДвигателя,
	  |	(ВЫБОР
	  |			КОГДА ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	  |				ТОГДА ОстаткиАвтомобилейОстатки.Автомобиль.ТипКПП
	  |			ИНАЧЕ ОстаткиАвтомобилейОстатки.Автомобиль.ВариантКомплектации.ТипКПП
	  |		КОНЕЦ) КАК ТипКПП,
	  |	ОстаткиАвтомобилейОстатки.Автомобиль.ТипСалона КАК ТипСалона,
	  |	ОстаткиАвтомобилейОстатки.Автомобиль.Цвет КАК Цвет}
	  |
	  |СГРУППИРОВАТЬ ПО
	  |	ОстаткиАвтомобилейОстатки.Автомобиль
	  |
	  |ИНДЕКСИРОВАТЬ ПО
	  |	Автомобиль;
	|
	|ВЫБРАТЬ
	|	ТаблицаИнфАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаИнфАвтомобилей.Период КАК Период,
	|	ТаблицаИнфАвтомобилей.Значение КАК Значение
	|ПОМЕСТИТЬ
	|	ТаблицаИнфАвтомобилей
	|ИЗ
	|	РегистрСведений.Автомобили КАК ТаблицаИнфАвтомобилей
	|ГДЕ
	|	ТаблицаИнфАвтомобилей.ВидЗначения = &ВидАвто
	|{ГДЕ
	|	ТаблицаИнфАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаИнфАвтомобилей.Автомобиль.Модель КАК Модель,
	|	ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|	ВЫБОР
	|		КОГДА ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) ТОГДА
	|			ТаблицаИнфАвтомобилей.Автомобиль.ТипКузова
	|	ИНАЧЕ
	|		ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации.ТипКузова
	|	КОНЕЦ КАК ТипКузова,
	|	ВЫБОР
	|		КОГДА ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) 
	|			ТОГДА ТаблицаИнфАвтомобилей.Автомобиль.ТипДвигателя
	|		ИНАЧЕ
	|			ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации.ТипДвигателя
	|	КОНЕЦ КАК ТипДвигателя,
	|	ВЫБОР
	|		КОГДА ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) ТОГДА
	|			ТаблицаИнфАвтомобилей.Автомобиль.ТипКПП
	|		ИНАЧЕ
	|			ТаблицаИнфАвтомобилей.Автомобиль.ВариантКомплектации.ТипКПП
	|	КОНЕЦ КАК ТипКПП,
	|	ТаблицаИнфАвтомобилей.Автомобиль.ТипСалона КАК ТипСалона,
	|	ТаблицаИнфАвтомобилей.Автомобиль.Цвет КАК Цвет}
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаИнфАвтомобилей.Автомобиль КАК Автомобиль,
	|	МАКСИМУМ(ТаблицаИнфАвтомобилей.Период) КАК Период
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаИнфАвтомобилей КАК ТаблицаИнфАвтомобилей
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИнфАвтомобилей.Автомобиль
	|;
	|
	|ВЫБРАТЬ
	|	СрезПоследних.Автомобиль КАК Автомобиль,
	|	ТаблицаИнфАвтомобилей.Значение КАК Значение
	|ПОМЕСТИТЬ
	|	ХарактеристикаАвтомобилей
	|ИЗ
	|	СрезПоследних КАК СрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаИнфАвтомобилей КАК ТаблицаИнфАвтомобилей
	|ПО
	|	СрезПоследних.Период = ТаблицаИнфАвтомобилей.Период И 
	|	СрезПоследних.Автомобиль = ТаблицаИнфАвтомобилей.Автомобиль
	|ИНДЕКСИРОВАТЬ ПО
	|	Автомобиль
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаИнфАвтомобилей
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦенАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаЦенАвтомобилей.Период КАК Период,
	|	ТаблицаЦенАвтомобилей.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦенАвтомобилей
	|ИЗ
	|	РегистрСведений.ЦеныАвтомобилей КАК ТаблицаЦенАвтомобилей
	|ГДЕ
	|	ТаблицаЦенАвтомобилей.Автомобиль ССЫЛКА Справочник.Автомобили И 
	|	ТаблицаЦенАвтомобилей.ТипЦен = &ТипЦенПродажи
	|{ГДЕ
	|	ВЫРАЗИТЬ(Автомобиль КАК Справочник.Автомобили) КАК Автомобиль,
	|	Автомобиль.Модель КАК Модель,
	|	Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|	ВЫБОР
	|		КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) ТОГДА
	|			Автомобиль.ТипКузова
	|	ИНАЧЕ
	|		Автомобиль.ВариантКомплектации.ТипКузова
	|	КОНЕЦ КАК ТипКузова,
	|	ВЫБОР
	|		КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) 
	|			ТОГДА Автомобиль.ТипДвигателя
	|		ИНАЧЕ
	|			Автомобиль.ВариантКомплектации.ТипДвигателя
	|	КОНЕЦ КАК ТипДвигателя,
	|	ВЫБОР
	|		КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) ТОГДА
	|			Автомобиль.ТипКПП
	|		ИНАЧЕ
	|			Автомобиль.ВариантКомплектации.ТипКПП
	|	КОНЕЦ КАК ТипКПП,
	|	Автомобиль.ТипСалона КАК ТипСалона,
	|	Автомобиль.Цвет КАК Цвет}
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦенАвтомобилей.Автомобиль КАК Автомобиль,
	|	МАКСИМУМ(ТаблицаЦенАвтомобилей.Период) КАК Период
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦенАвтомобилей КАК ТаблицаЦенАвтомобилей
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦенАвтомобилей.Автомобиль
	|;
	|
	|ВЫБРАТЬ
	|	СрезПоследних.Автомобиль КАК Автомобиль,
	|	МАКСИМУМ("+ТекстЦена+")*МАКСИМУМ(КурсыВалют.Коэффициент) КАК Цена
	|ПОМЕСТИТЬ
	|	ЦеныАвтомобилей
	|ИЗ
	|	СрезПоследних КАК СрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦенАвтомобилей КАК ТаблицаЦенАвтомобилей
	|ПО
	|	СрезПоследних.Период = ТаблицаЦенАвтомобилей.Период И 
	|	СрезПоследних.Автомобиль = ТаблицаЦенАвтомобилей.Автомобиль
	|"+?(РабочийТипЦен.ВВалютеУчета, "ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалют КАК КурсыВалют
	|ПО
	|	СрезПоследних.Автомобиль.ВалютаУчета = КурсыВалют.Валюта","ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалют КАК КурсыВалют
	|ПО
	|	ИСТИНА")+"
	|СГРУППИРОВАТЬ ПО
	|	СрезПоследних.Автомобиль
	|ИНДЕКСИРОВАТЬ ПО
	|	Автомобиль
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаЦенАвтомобилей
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦенАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации КАК ВариантКомплектации,
	|	ТаблицаЦенАвтомобилей.Период КАК Период,
	|	ТаблицаЦенАвтомобилей.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦенАвтомобилей
	|ИЗ
	|	РегистрСведений.ЦеныАвтомобилей КАК ТаблицаЦенАвтомобилей
	|ГДЕ
	|	ТаблицаЦенАвтомобилей.Автомобиль ССЫЛКА Справочник.Модели И 
	|	ТаблицаЦенАвтомобилей.ТипЦен = &ТипЦенПродажи
	|{ГДЕ
	|	ТаблицаЦенАвтомобилей.Автомобиль КАК Модель,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации КАК ВариантКомплектации,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации.ТипКузова КАК ТипКузова,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации.ТипДвигателя КАК ТипДвигателя,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации.ТипКПП КАК ТипКПП}
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦенАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации КАК ВариантКомплектации,
	|	МАКСИМУМ(ТаблицаЦенАвтомобилей.Период) КАК Период
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦенАвтомобилей КАК ТаблицаЦенАвтомобилей
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦенАвтомобилей.Автомобиль,
	|	ТаблицаЦенАвтомобилей.ВариантКомплектации
	|;
	|
	|ВЫБРАТЬ
	|	СрезПоследних.Автомобиль КАК Модель,
	|	СрезПоследних.ВариантКомплектации КАК ВариантКомплектации,
	|	МАКСИМУМ("+ТекстЦенаМоделей+")*МАКСИМУМ(КурсыВалютМоделей.Коэффициент) КАК Цена
	|ПОМЕСТИТЬ
	|	ЦеныМоделей
	|ИЗ
	|	СрезПоследних КАК СрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦенАвтомобилей КАК ТаблицаЦенАвтомобилей
	|ПО
	|	СрезПоследних.Период = ТаблицаЦенАвтомобилей.Период И 
	|	СрезПоследних.Автомобиль = ТаблицаЦенАвтомобилей.Автомобиль И 
	|	СрезПоследних.ВариантКомплектации = ТаблицаЦенАвтомобилей.ВариантКомплектации
	|"+?(РабочийТипЦен.ВВалютеУчета, "ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалют КАК КурсыВалютМоделей
	|ПО
	|	ВЫБОР
	|		КОГДА СрезПоследних.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) ТОГДА
	|			СрезПоследних.Автомобиль.ВалютаУчета = КурсыВалютМоделей.Валюта
	|		ИНАЧЕ
	|			СрезПоследних.ВариантКомплектации.ВалютаУчета = КурсыВалютМоделей.Валюта
	|	КОНЕЦ","ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалют КАК КурсыВалютМоделей
	|ПО
	|	ИСТИНА")+"
	|СГРУППИРОВАТЬ ПО
	|	СрезПоследних.Автомобиль,
	|	СрезПоследних.ВариантКомплектации
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаЦенАвтомобилей
	|;
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДанных.Заказ) КАК Заказ,
	|	ТаблицаДанных.Автомобиль КАК Автомобиль,
	|	СУММА(ТаблицаДанных.Заказано) КАК Заказано,
	|	СУММА(ТаблицаДанных.Резерв) КАК Резерв,
	|	МАКСИМУМ(ТаблицаДанных.ЗаказПоставщику) КАК ЗаказПоставщику,
	|	МАКСИМУМ(ТаблицаДанных.СкладКомпании) КАК СкладКомпании,
	|	МАКСИМУМ(ТаблицаДанных.СтатусПартии) КАК СтатусПартии,
	|	МАКСИМУМ(ТаблицаДанных.ДатаПоступления) КАК ДатаПоступления,
	|	МАКСИМУМ(ТаблицаДанных.КоличествоДнейНаСкладе) КАК КоличествоДнейНаСкладе,
	|	МАКСИМУМ(ТаблицаДанных.Количество) КАК Количество
	|ПОМЕСТИТЬ
	|	ТаблицаАвтомобилей
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЛОЖЬ КАК ВНаличии,
	|		ЗаказыАвтомобилейОстатки.Заказ КАК Заказ,
	|		ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	|		ЗаказыАвтомобилейОстатки.КоличествоОстаток КАК Заказано,
	|		ЗаказыАвтомобилейОстатки.РезервОстаток КАК Резерв,
	|		ЗНАЧЕНИЕ(Документ.ЗаказПоставщикуНаАвтомобиль.ПустаяСсылка) КАК ЗаказПоставщику,
	|		ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладКомпании,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ПустаяСсылка) КАК СтатусПартии,
	|		ДАТАВРЕМЯ(1,1,1) КАК ДатаПоступления,
	|		0 КАК КоличествоДнейНаСкладе,
	|		0 КАК Количество
	|	ИЗ
	|		РегистрНакопления.ЗаказыАвтомобилей.Остатки(&Момент,
	|			{Автомобиль КАК Автомобиль,
	|			Автомобиль.Модель КАК Модель,
	|			Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипКузова
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипКузова
	|			КОНЕЦ КАК ТипКузова,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипДвигателя
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипДвигателя
	|			КОНЕЦ КАК ТипДвигателя,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипКПП
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипКПП
	|			КОНЕЦ КАК ТипКПП,
	|			Автомобиль.ТипСалона КАК ТипСалона,
	|			Автомобиль.Цвет КАК Цвет}) КАК ЗаказыАвтомобилейОстатки
	|	ГДЕ
	|		ЗаказыАвтомобилейОстатки.КоличествоОстаток <> 0 ИЛИ 
	|		ЗаказыАвтомобилейОстатки.РезервОстаток <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА,
	|		ЗНАЧЕНИЕ(Документ.ЗаказНаАвтомобиль.ПустаяСсылка),
	|		ЗаказыПоставщикамНаАвтомобилиОстатки.Автомобиль,
	|		0,
	|		0,
	|		ЗаказыПоставщикамНаАвтомобилиОстатки.ЗаказПоставщику,
	|		ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ПустаяСсылка),
	|		ДАТАВРЕМЯ(1,1,1),
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки(&Момент, 
	|			{Автомобиль КАК Автомобиль,
	|			Автомобиль.Модель КАК Модель,
	|			Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипКузова
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипКузова
	|			КОНЕЦ КАК ТипКузова,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипДвигателя
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипДвигателя
	|			КОНЕЦ КАК ТипДвигателя,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипКПП
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипКПП
	|			КОНЕЦ КАК ТипКПП,
	|			Автомобиль.ТипСалона КАК ТипСалона,
	|			Автомобиль.Цвет КАК Цвет}) КАК ЗаказыПоставщикамНаАвтомобилиОстатки
	|	ГДЕ
	|		ЗаказыПоставщикамНаАвтомобилиОстатки.КоличествоОстаток <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА,
	|		ЗНАЧЕНИЕ(Документ.ЗаказНаАвтомобиль.ПустаяСсылка),
	|		ОстаткиАвтомобилейОстатки.Автомобиль,
	|		0,
	|		0,
	|		ЕСТЬNULL(ТаблицаДатПоступления.ЗаказПоставщику, ЗНАЧЕНИЕ(Документ.ЗаказПоставщикуНаАвтомобиль.ПустаяСсылка)),
	|		ОстаткиАвтомобилейОстатки.СкладКомпании,
	|		ОстаткиАвтомобилейОстатки.СтатусПартии,
	|		ЕСТЬNULL(ТаблицаДатПоступления.ДатаПоступления, ДАТАВРЕМЯ(1,1,1)),
	|		ЕСТЬNULL(РАЗНОСТЬДАТ(ТаблицаДатПоступления.ДатаПервогоПоступления, &Момент, ДЕНЬ)+1, 0),
	|		ОстаткиАвтомобилейОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ОстаткиАвтомобилей.Остатки(&Момент, 
	|		{
	|			Автомобиль КАК Автомобиль,
	|			Автомобиль.Модель КАК Модель,
	|			Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|			СкладКомпании КАК СкладКомпании,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|			ТОГДА Автомобиль.ТипКузова
	|			ИНАЧЕ Автомобиль.ВариантКомплектации.ТипКузова
	|			КОНЕЦ КАК ТипКузова,
	|			ВЫБОР
	|			КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|			ТОГДА Автомобиль.ТипДвигателя
	|			ИНАЧЕ Автомобиль.ВариантКомплектации.ТипДвигателя
	|			КОНЕЦ КАК ТипДвигателя,
	|			ВЫБОР
	|				КОГДА Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|				ТОГДА Автомобиль.ТипКПП
	|				ИНАЧЕ Автомобиль.ВариантКомплектации.ТипКПП
	|			КОНЕЦ КАК ТипКПП,
	|			Автомобиль.ТипСалона КАК ТипСалона,
	|			Автомобиль.Цвет КАК Цвет}) КАК ОстаткиАвтомобилейОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ТаблицаДатПоступления КАК ТаблицаДатПоступления
	|		ПО
	|			ОстаткиАвтомобилейОстатки.Автомобиль = ТаблицаДатПоступления.Автомобиль) КАК ТаблицаДанных
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Автомобиль
	//anton
	//|ИМЕЮЩИЕ
	//|	МАКСИМУМ(ТаблицаДанных.ВНаличии) = ИСТИНА
	//anton
	|ИНДЕКСИРОВАТЬ ПО
	|	Автомобиль
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаАвтомобилей.Автомобиль.Модель КАК Модель,
	|	ТаблицаАвтомобилей.Автомобиль.VIN КАК VIN,
	|	ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|	ТаблицаАвтомобилей.Заказ КАК Заказ,
	|	ТаблицаАвтомобилей.Автомобиль.Наименование КАК НаименованиеАвтомобиля,
	|	ЕСТЬNULL(ЗаказНаАвтомобиль.СрокСнятияРезерва, ДатаВремя(1, 1, 1)) КАК СрокСнятияРезерва,
	|	ЗаказНаАвтомобиль.Менеджер КАК Менеджер,
	|	ЗаказНаАвтомобиль.Контрагент КАК Контрагент,
	|	ВЫРАЗИТЬ(ЗаказНаАвтомобиль.Комментарий КАК Строка(200)) КАК Комментарий,
	|	ВЫРАЗИТЬ(ТаблицаАвтомобилей.Автомобиль.Комментарий КАК Строка(200)) КАК КомментарийАвтомобиля,
	|	ТаблицаАвтомобилей.СтатусПартии КАК СтатусПартии,
	|	ВЫБОР
	|		КОГДА ЗаказНаАвтомобиль.Номер ЕСТЬ NULL ИЛИ ЗаказНаАвтомобиль.Номер = """" ТОГДА
	|			""<Без заказа>""
	|		ИНАЧЕ
	|			""№ ""+ЗаказНаАвтомобиль.Номер
	|	КОНЕЦ КАК ЗаказПредставление,
	|	ТаблицаАвтомобилей.СкладКомпании КАК СкладКомпании,
	|	ТаблицаАвтомобилей.ДатаПоступления КАК ДатаПоступления,
	|	ТаблицаАвтомобилей.КоличествоДнейНаСкладе КАК КоличествоДнейНаСкладе,
	|	ВЫБОР
	|		КОГДА (НЕ ЦеныАвтомобилей.Цена ЕСТЬ NULL) И (НЕ ЦеныАвтомобилей.Цена = 0) ТОГДА
	|			ЦеныАвтомобилей.Цена
	|		КОГДА НЕ ЦеныМоделей.Цена ЕСТЬ NULL ТОГДА
	|			ЦеныМоделей.Цена
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|		ТОГДА ТаблицаАвтомобилей.Автомобиль.ТипКузова
	|		ИНАЧЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.ТипКузова
	|	КОНЕЦ КАК ТипКузова,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|		ТОГДА ТаблицаАвтомобилей.Автомобиль.ТипДвигателя
	|		ИНАЧЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.ТипДвигателя
	|	КОНЕЦ КАК ТипДвигателя,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|		ТОГДА ТаблицаАвтомобилей.Автомобиль.ТипКПП
	|		ИНАЧЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.ТипКПП
	|	КОНЕЦ КАК ТипКПП,
	|	ТаблицаАвтомобилей.Автомобиль.ТипСалона КАК ТипСалона,
	|	ТаблицаАвтомобилей.Автомобиль.Цвет КАК Цвет,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ХарактеристикаАвтомобилей.Значение, ЗНАЧЕНИЕ(Перечисление.ВидАвтомобиля.Новый)) КАК Перечисление.ВидАвтомобиля) КАК ВидАвтомобиля,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуНаАвтомобиль.СтатусАвтомобиля ЕСТЬ NULL ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатусыАвтомобилей.ПустаяСсылка)
	|		ИНАЧЕ
	|			ЗаказПоставщикуНаАвтомобиль.СтатусАвтомобиля
	|	КОНЕЦ КАК СтатусЗаказаПоставщику,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуНаАвтомобиль.СрокПоставки ЕСТЬ NULL ИЛИ ТаблицаАвтомобилей.Количество > 0 ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатусыАвтомобилей.ПустаяСсылка)
	|		ИНАЧЕ
	|			ЗаказПоставщикуНаАвтомобиль.СрокПоставки
	|	КОНЕЦ КАК СрокПоступления,
	|	ЕСТЬNULL(ЗаказПоставщикуНаАвтомобиль.ВнешнийНомерЗаказа, """") КАК ВнешнийНомерЗаказа,
	|	ТаблицаАвтомобилей.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуНаАвтомобиль.Номер ЕСТЬ NULL ИЛИ ЗаказПоставщикуНаАвтомобиль.Номер = """" ТОГДА
	|			""<Без заказа>""
	|		ИНАЧЕ
	|			""№ ""+ЗаказПоставщикуНаАвтомобиль.Номер
	|	КОНЕЦ КАК ЗаказПоставщикуПредставление,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Заказано > 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК АвтомобильПодЗаказ,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Количество > 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ АвтомобильНаСкладе,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Резерв > 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК АвтомобильВРезерве,
	|	ЕСТЬNULL(-ВзаиморасчетыКомпанииОбороты.СуммаБазОстаток/&КурсДокументаОснования, 0) КАК ВнесеннаяСуммаПредоплаты,
	|	ВЫБОР
	|		КОГДА ЗаказНаАвтомобиль.КурсДокумента ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			(ЗаказНаАвтомобиль.СуммаСкидкиНаАвтомобиль)*ЗаказНаАвтомобиль.КурсДокумента/&КурсДокументаОснования
	|	КОНЕЦ КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА ЗаказНаАвтомобиль.КурсДокумента ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			ЗаказНаАвтомобиль.СуммаДокумента*ЗаказНаАвтомобиль.КурсДокумента/&КурсДокументаОснования
	|	КОНЕЦ КАК СуммаСделки,
	|	ВЫБОР
	|		КОГДА ЗаказНаАвтомобиль.КурсДокумента ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			ЗаказНаАвтомобиль.СуммаПредоплаты*ЗаказНаАвтомобиль.КурсДокумента/&КурсДокументаОснования
	|	КОНЕЦ КАК СуммаПредоплаты,
	|	ВЫБОР
	|		КОГДА ЗаказНаАвтомобиль.КурсДокумента ЕСТЬ NULL ТОГДА
	|			0
	|		КОГДА ВзаиморасчетыКомпанииОбороты.СуммаБазОстаток ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			((ЗаказНаАвтомобиль.СуммаПредоплаты*ЗаказНаАвтомобиль.КурсДокумента) + ВзаиморасчетыКомпанииОбороты.СуммаБазОстаток)/&КурсДокументаОснования
	|	КОНЕЦ КАК ОстатокПредоплаты
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦеныАвтомобилей КАК ЦеныАвтомобилей
	|ПО
	|	ТаблицаАвтомобилей.Автомобиль = ЦеныАвтомобилей.Автомобиль
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦеныМоделей КАК ЦеныМоделей
	|ПО
	|	ТаблицаАвтомобилей.Автомобиль.Модель = ЦеныМоделей.Модель И 
	|	ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЦеныМоделей.ВариантКомплектации
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ЗаказНаАвтомобиль КАК ЗаказНаАвтомобиль
	|ПО
	|	ТаблицаАвтомобилей.Заказ = ЗаказНаАвтомобиль.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ЗаказПоставщикуНаАвтомобиль КАК ЗаказПоставщикуНаАвтомобиль
	|ПО
	|	ТаблицаАвтомобилей.ЗаказПоставщику = ЗаказПоставщикуНаАвтомобиль.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ХарактеристикаАвтомобилей КАК ХарактеристикаАвтомобилей
	|ПО
	|	ТаблицаАвтомобилей.Автомобиль = ХарактеристикаАвтомобилей.Автомобиль
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(,Сделка ССЫЛКА Документ.ЗаказНаАвтомобиль) КАК ВзаиморасчетыКомпанииОбороты
	|ПО
	|	ТаблицаАвтомобилей.Заказ = ВзаиморасчетыКомпанииОбороты.Сделка И 
	|	ВзаиморасчетыКомпанииОбороты.СуммаБазОстаток<0
	|	" + ТекстСоединенияСвойств + "
	|{ГДЕ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаАвтомобилей.Автомобиль.Модель КАК Модель,
	|	ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
	|	ТаблицаАвтомобилей.Заказ КАК Заказ,
	|	ЕСТЬNULL(ЗаказНаАвтомобиль.Заказчик, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Заказчик,
	|	ЕСТЬNULL(ЗаказНаАвтомобиль.СрокСнятияРезерва, ДатаВремя(1, 1, 1)) КАК СрокСнятияРезерва,
	|	ТаблицаАвтомобилей.СтатусПартии КАК СтатусПартии,
	|	ТаблицаАвтомобилей.СкладКомпании КАК СкладКомпании,
	|	ТаблицаАвтомобилей.ДатаПоступления КАК ДатаПоступления,
	|	ТаблицаАвтомобилей.КоличествоДнейНаСкладе КАК КоличествоДнейНаСкладе,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|		ТОГДА ТаблицаАвтомобилей.Автомобиль.ТипКузова
	|		ИНАЧЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.ТипКузова
	|	КОНЕЦ КАК ТипКузова,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|		ТОГДА ТаблицаАвтомобилей.Автомобиль.ТипДвигателя
	|		ИНАЧЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.ТипДвигателя
	|	КОНЕЦ КАК ТипДвигателя,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)
	|		ТОГДА ТаблицаАвтомобилей.Автомобиль.ТипКПП
	|		ИНАЧЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.ТипКПП
	|	КОНЕЦ КАК ТипКПП,
	|	ТаблицаАвтомобилей.Автомобиль.ТипСалона КАК ТипСалона,
	|	ТаблицаАвтомобилей.Автомобиль.Цвет КАК Цвет,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ХарактеристикаАвтомобилей.Значение, ЗНАЧЕНИЕ(Перечисление.ВидАвтомобиля.Новый)) КАК Перечисление.ВидАвтомобиля) КАК ВидАвтомобиля,
	|	ЕСТЬNULL(ЗаказНаАвтомобиль.Менеджер, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуНаАвтомобиль.СтатусАвтомобиля ЕСТЬ NULL ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатусыАвтомобилей.ПустаяСсылка)
	|		ИНАЧЕ
	|			ЗаказПоставщикуНаАвтомобиль.СтатусАвтомобиля
	|	КОНЕЦ КАК СтатусЗаказаПоставщику,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуНаАвтомобиль.СрокПоставки ЕСТЬ NULL ИЛИ ТаблицаАвтомобилей.Количество > 0 ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатусыАвтомобилей.ПустаяСсылка)
	|		ИНАЧЕ
	|			ЗаказПоставщикуНаАвтомобиль.СрокПоставки
	|	КОНЕЦ КАК СрокПоступления,
	|	ЕСТЬNULL(ЗаказПоставщикуНаАвтомобиль.ВнешнийНомерЗаказа, """") КАК ВнешнийНомерЗаказа,
	|	ТаблицаАвтомобилей.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуНаАвтомобиль.Номер ЕСТЬ NULL ИЛИ ЗаказПоставщикуНаАвтомобиль.Номер = """" ТОГДА
	|			""<Без заказа>""
	|		ИНАЧЕ
	|			""№ ""+ЗаказПоставщикуНаАвтомобиль.Номер
	|	КОНЕЦ КАК ЗаказПоставщикуПредставление,
	|	ЕСТЬNULL(ЗаказНаАвтомобиль.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Заказано > 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК АвтомобильПодЗаказ,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Количество > 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ АвтомобильНаСкладе,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.Резерв > 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК АвтомобильВРезерве" + ТекстОтбораСвойств + " }";
	
	Возврат ТекстЗапроса;
	
КонецФункции

//Заполняем начальный текст запроса и список фильтров на закладке АВТОМОБИЛИ
Процедура НачальноеЗаполнениеПостроителя() Экспорт
	
	ТипЦен = обПраво("ОсновнойТипЦенПродажиАвтомобилей", глПрава);
	
	ВалютаДокумента = ТипЦен.ВалютаЦены;
	СтруктураКурса = обКурсДляВалюты(ВалютаДокумента, ТекущаяДата());
	КурсДокументаОснования = СтруктураКурса.Курс/?(СтруктураКурса.Кратность=0,1,СтруктураКурса.Кратность);
	
	СтруктураСвойств = Новый Структура;
	ПостроительОтчета.Текст = ПолучитьТекстПостроителя(ТипЦен, СтруктураСвойств);
	ПостроительОтчета.ЗаполнитьНастройки();
	ПостроительОтчета.Параметры.Вставить("КурсДокументаОснования", КурсДокументаОснования);
	
	СтруктураПредставлениеПолей	= Новый Структура();
	СтруктураПредставлениеПолей.Вставить("Автомобиль",				"Автомобиль");
	СтруктураПредставлениеПолей.Вставить("Модель",					"Модель");
	СтруктураПредставлениеПолей.Вставить("ВариантКомплектации",		"Вариант комплектации");
	СтруктураПредставлениеПолей.Вставить("Заказ",					"Заказ на автомобиль");	
	СтруктураПредставлениеПолей.Вставить("СтатусПартии",			"Статус партии");	
	СтруктураПредставлениеПолей.Вставить("ЗаказПредставление",		"Представление заказа на автомобиль");
	СтруктураПредставлениеПолей.Вставить("ЗаказПоставщику",			"Заказ поставщику");
	СтруктураПредставлениеПолей.Вставить("СтатусЗаказаПоставщику",	"Статус заказа поставщику");
	СтруктураПредставлениеПолей.Вставить("СрокПоступления",			"Срок поступления");
	СтруктураПредставлениеПолей.Вставить("ЗаказПоставщикуПредставление", "Представление заказа поставщику");
	СтруктураПредставлениеПолей.Вставить("ВнешнийНомерЗаказа",		"Внешний № заказа");	
	СтруктураПредставлениеПолей.Вставить("СкладКомпании",			"Склад");
	СтруктураПредставлениеПолей.Вставить("Контрагент",				"Контрагент");
	СтруктураПредставлениеПолей.Вставить("Менеджер",				"Менеджер");
	СтруктураПредставлениеПолей.Вставить("Цена",					"Цена");
	СтруктураПредставлениеПолей.Вставить("Валюта",					"Валюта");
	СтруктураПредставлениеПолей.Вставить("ТипКузова",				"Тип кузова");
	СтруктураПредставлениеПолей.Вставить("ТипДвигателя",			"Тип двигателя");
	СтруктураПредставлениеПолей.Вставить("ТипКПП",					"Тип КПП");
	СтруктураПредставлениеПолей.Вставить("ТипСалона",				"Тип салона");
	СтруктураПредставлениеПолей.Вставить("Цвет",					"Цвет");
	СтруктураПредставлениеПолей.Вставить("ВидАвтомобиля",			"Вид автомобиля");
	СтруктураПредставлениеПолей.Вставить("Комментарий",				"Комментарий");
	СтруктураПредставлениеПолей.Вставить("КомментарийАвтомобиля",	"Комментарий авто");
	СтруктураПредставлениеПолей.Вставить("СрокСнятияРезерва",		"Срок снятия резерва");
	СтруктураПредставлениеПолей.Вставить("АвтомобильНаСкладе",		"Автомобиль на складе");
	СтруктураПредставлениеПолей.Вставить("АвтомобильВРезерве",		"Автомобиль в резерве");
	
	ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей,ПостроительОтчета);
	
	ОтборКоличество = ПостроительОтчета.Отбор.Количество();
	Для к = 1 По ОтборКоличество Цикл
		ПостроительОтчета.Отбор.Удалить(ОтборКоличество - к);
	КонецЦикла;
	
	ПостроительОтчета.Отбор.Добавить("Автомобиль",				"Автомобиль",				"Автомобиль");
	ПостроительОтчета.Отбор.Добавить("АвтомобильВРезерве",		"АвтомобильВРезерве",		"Автомобиль в резерве");
	ПостроительОтчета.Отбор.Добавить("АвтомобильНаСкладе",		"АвтомобильНаСкладе",		"Автомобиль на складе");
	ПостроительОтчета.Отбор.Добавить("АвтомобильПодЗаказ",		"АвтомобильПодЗаказ",		"Автомобиль под заказ");
	ПостроительОтчета.Отбор.Добавить("ВариантКомплектации",		"ВариантКомплектации",		"Вариант комплектации");
	ПостроительОтчета.Отбор.Добавить("ВидАвтомобиля",			"ВидАвтомобиля",			"Вид автомобиля");
	ПостроительОтчета.Отбор.Добавить("ВнешнийНомерЗаказа",		"ВнешнийНомерЗаказа",		"Внешний № заказа");
	ПостроительОтчета.Отбор.Добавить("ДатаПоступления",			"ДатаПоступления",			"Дата поступления");	
	ПостроительОтчета.Отбор.Добавить("Заказ",					"Заказ",					"Заказ на автомобиль");	
	ПостроительОтчета.Отбор.Добавить("ЗаказПоставщику",			"ЗаказПоставщику",			"Заказ поставщику на автомобиль");
	ПостроительОтчета.Отбор.Добавить("Заказчик",				"Заказчик",					"Заказчик");
	ПостроительОтчета.Отбор.Добавить("СрокСнятияРезерва",		"СрокСнятияРезерва",		"Срок снятия резерва");
	ПостроительОтчета.Отбор.Добавить("КоличествоДнейНаСкладе",	"КоличествоДнейНаСкладе",	"Количество дней на складе");
	ПостроительОтчета.Отбор.Добавить("Контрагент",				"Контрагент",				"Контрагент");
	ПостроительОтчета.Отбор.Добавить("Менеджер",				"Менеджер",					"Менеджер");
	ПостроительОтчета.Отбор.Добавить("Модель",					"Модель",					"Модель");	
	ПостроительОтчета.Отбор.Добавить("СкладКомпании",			"СкладКомпании",			"Склад");
	ПостроительОтчета.Отбор.Добавить("СтатусЗаказаПоставщику",	"СтатусЗаказаПоставщику",	"Статус заказа поставщику");
	ПостроительОтчета.Отбор.Добавить("СрокПоступления",			"СрокПоступления",			"Срок поступления");
	ПостроительОтчета.Отбор.Добавить("СтатусПартии",			"СтатусПартии",				"Статус партии");
	ПостроительОтчета.Отбор.Добавить("ТипДвигателя",			"ТипДвигателя",				"Тип двигателя");
	ПостроительОтчета.Отбор.Добавить("ТипКПП",					"ТипКПП",					"Тип КПП");
	ПостроительОтчета.Отбор.Добавить("ТипКузова",				"ТипКузова",				"Тип кузова");
	ПостроительОтчета.Отбор.Добавить("ТипСалона",				"ТипСалона",				"Тип салона");
	ПостроительОтчета.Отбор.Добавить("Цвет",					"Цвет", 					"Цвет");
	
	Для Каждого ТекСвойство Из СтруктураСвойств Цикл
		ПостроительОтчета.Отбор.Добавить(ТекСвойство.Ключ, ТекСвойство.Ключ, ТекСвойство.Значение);
	КонецЦикла;
	
	СтруктураНеВидимыхОтборов = Новый Структура("Модель, ВариантКомплектации, ТипСалона, Цвет, СтатусПартии, ВидАвтомобиля");
	
КонецПроцедуры

// Процедура заполнения представления полей
Процедура ЗаполнитьПредставленияПолей(СтруктураСоответствияИмен,Построитель) Экспорт
	СтруктураКоллекцийПостроителяОтчета=Новый Структура("ДоступныеПоля,ВыбранныеПоля,ИзмеренияКолонки,ИзмеренияСтроки,Отбор");
	
	Для каждого ЭлементСтруктуры Из СтруктураКоллекцийПостроителяОтчета Цикл
		Для каждого ЭлементКоллекции Из Построитель[ЭлементСтруктуры.Ключ] Цикл
			Если СтруктураСоответствияИмен.Свойство(ЭлементКоллекции.ПутьКДанным) Тогда
				ЭлементКоллекции.Представление=СтруктураСоответствияИмен[ЭлементКоллекции.ПутьКДанным];
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла; 
	
КонецПроцедуры // ЗаполнитьПредставленияПолей()

// Формирует отчет по автомобилям
//Виды попадающих в отчет автомобилей:
//а) заказанные нами на склад и ещё не поступившие,
//б) уже имеющиеся у нас
Процедура СформироватьОтчетПоАвтомобилям(ЭтаФорма, ИзмененТипЦен=Ложь) Экспорт
	
	//Заказы с автоработами
	ИспользованиеОтбораПоЗаказам = Неопределено;
	ВидСравненияОтбораПоЗаказам	 = Неопределено;
	ЗначениеОтбораПоЗаказам		 = Неопределено;
	
	СписокНайденныеАвтомобили.Очистить();
	СписокНайденныеОпции.Очистить();
	СписокНайденныеДопОпции.Очистить();
	СписокНайденноеДопОборудование.Очистить();
	
	Если ЗаказыСАвтоработами Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.ДокументОснование КАК ЗаказНаАвтомобиль
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.ДокументОснование ССЫЛКА Документ.ЗаказНаАвтомобиль И 
		|	ЗаявкаНаРемонт.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказНаАвтомобиль.ПустаяСсылка)
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаказНаряд.ДокументОснование
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.ДокументОснование ССЫЛКА Документ.ЗаказНаАвтомобиль И 
		|	ЗаказНаряд.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказНаАвтомобиль.ПустаяСсылка)");
		мЗаказы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗаказНаАвтомобиль");
		ТекОтбор = ПостроительОтчета.Отбор.Заказ;
		
		ИспользованиеОтбораПоЗаказам = ТекОтбор.Использование;
		ВидСравненияОтбораПоЗаказам	 = ТекОтбор.ВидСравнения;
		ЗначениеОтбораПоЗаказам		 = ТекОтбор.Значение;
		
		ТекОтбор.Использование = Истина;
		ТекОтбор.ВидСравнения = ВидСравнения.ВСписке;
		ТекОтбор.Значение.ЗагрузитьЗначения(мЗаказы);
	КонецЕсли;
	ТипЦен = ЭтаФорма.ОсновнойДокумент.ТипЦен;
	Если ИзмененТипЦен Тогда
		НастройкиПостроителя = ПостроительОтчета.ПолучитьНастройки();
		
		СтруктураСвойств = Новый Структура;
		ПостроительОтчета.Текст = ПолучитьТекстПостроителя(ТипЦен, СтруктураСвойств);
		ПостроительОтчета.ЗаполнитьНастройки();
		ПостроительОтчета.УстановитьНастройки(НастройкиПостроителя);
		
		СтруктураПредставлениеПолей	= Новый Структура();
		СтруктураПредставлениеПолей.Вставить("Автомобиль",				"Автомобиль");
		СтруктураПредставлениеПолей.Вставить("Модель",					"Модель");
		СтруктураПредставлениеПолей.Вставить("ВариантКомплектации",		"Вариант комплектации");
		СтруктураПредставлениеПолей.Вставить("Заказ",					"Заказ на автомобиль");
		СтруктураПредставлениеПолей.Вставить("СтатусПартии",			"Статус партии");
		СтруктураПредставлениеПолей.Вставить("ЗаказПредставление",		"Представление заказа на автомобиль");
		СтруктураПредставлениеПолей.Вставить("ЗаказПоставщику",			"Заказ поставщику");
		СтруктураПредставлениеПолей.Вставить("СтатусЗаказаПоставщику",	"Статус заказа поставщику");
		СтруктураПредставлениеПолей.Вставить("СрокПоступления",			"Срок поступления");
		СтруктураПредставлениеПолей.Вставить("ЗаказПоставщикуПредставление",	"Представление заказа поставщику");
		СтруктураПредставлениеПолей.Вставить("ВнешнийНомерЗаказа",		"Внешний № заказа");
		СтруктураПредставлениеПолей.Вставить("СкладКомпании",			"Склад");
		СтруктураПредставлениеПолей.Вставить("Контрагент",				"Контрагент");
		СтруктураПредставлениеПолей.Вставить("Менеджер",				"Менеджер");
		СтруктураПредставлениеПолей.Вставить("Цена",					"Цена");
		СтруктураПредставлениеПолей.Вставить("ТипКузова",				"Тип кузова");
		СтруктураПредставлениеПолей.Вставить("ТипДвигателя",			"Тип двигателя");
		СтруктураПредставлениеПолей.Вставить("ТипКПП",					"Тип КПП");
		СтруктураПредставлениеПолей.Вставить("ТипСалона",				"Тип салона");
		СтруктураПредставлениеПолей.Вставить("Цвет",					"Цвет");
		СтруктураПредставлениеПолей.Вставить("ВидАвтомобиля",			"Вид автомобиля");
		СтруктураПредставлениеПолей.Вставить("Комментарий",				"Комментарий");
		СтруктураПредставлениеПолей.Вставить("КомментарийАвтомобиля",	"Комментарий авто");
		СтруктураПредставлениеПолей.Вставить("СрокСнятияРезерва",		"Срок снятия резерва");
		СтруктураПредставлениеПолей.Вставить("АвтомобильПодЗаказ",		"Автомобиль под заказ");
		СтруктураПредставлениеПолей.Вставить("АвтомобильНаСкладе",		"Автомобиль на складе");
		СтруктураПредставлениеПолей.Вставить("АвтомобильВРезерве",		"Автомобиль в резерве");
		
		ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей,ПостроительОтчета);
	КонецЕсли;
	
	//выполняем запрос
	ПостроительОтчета.Параметры.Вставить("КурсДокументаОснования", ЭтаФорма.ОсновнойДокумент.КурсДокумента);
	ПостроительОтчета.Параметры.Вставить("Момент", ТекущаяДата());
	ПостроительОтчета.Параметры.Вставить("ВидАвто",Перечисления.ДополнительнаяИнформацияАвтомобилей.ВидАвтомобиля);
	ПостроительОтчета.Выполнить();
	
	ЗапросАвтомобилейСОборудованием = Новый Запрос("ВЫБРАТЬ
	|	РезультатЗапроса.Автомобиль,
	|	РезультатЗапроса.СрокСнятияРезерва,
	|	РезультатЗапроса.VIN,
	|	РезультатЗапроса.Модель,
	|	РезультатЗапроса.ВариантКомплектации,
	|	РезультатЗапроса.Заказ,
	|	РезультатЗапроса.СтатусПартии,
	|	РезультатЗапроса.ЗаказПредставление,
	|	РезультатЗапроса.СкладКомпании,
	|	РезультатЗапроса.ДатаПоступления,
	|	РезультатЗапроса.КоличествоДнейНаСкладе,
	|	РезультатЗапроса.Цена,
	|	РезультатЗапроса.ТипКузова,
	|	РезультатЗапроса.ТипДвигателя,
	|	РезультатЗапроса.ТипКПП,
	|	РезультатЗапроса.ТипСалона,
	|	РезультатЗапроса.Цвет,
	|	РезультатЗапроса.ВидАвтомобиля,
	|	РезультатЗапроса.ЗаказПоставщику,
	|	РезультатЗапроса.СтатусЗаказаПоставщику,
	|	РезультатЗапроса.СрокПоступления,
	|	РезультатЗапроса.ЗаказПоставщикуПредставление,
	|	РезультатЗапроса.Заказ,
	|	РезультатЗапроса.ВнешнийНомерЗаказа,
	|	РезультатЗапроса.АвтомобильПодЗаказ,
	|	РезультатЗапроса.АвтомобильНаСкладе,
	|	РезультатЗапроса.АвтомобильВРезерве,
	|	РезультатЗапроса.НаименованиеАвтомобиля,
	|	РезультатЗапроса.Менеджер,
	|	РезультатЗапроса.Контрагент,
	|	РезультатЗапроса.Комментарий,
	|	РезультатЗапроса.ВнесеннаяСуммаПредоплаты КАК ВнесеннаяСуммаПредоплаты,
	|	РезультатЗапроса.СуммаСкидки КАК СуммаСкидки,
	|	РезультатЗапроса.СуммаСделки КАК СуммаСделки,
	|	РезультатЗапроса.СуммаПредоплаты КАК СуммаПредоплаты,
	|	РезультатЗапроса.ОстатокПредоплаты КАК ОстатокПредоплаты,
	|	РезультатЗапроса.КомментарийАвтомобиля
	|ПОМЕСТИТЬ
	|	ТаблицаАвтомобилей
	|ИЗ &РезультатЗапроса КАК РезультатЗапроса
	|
	|ИНДЕКСИРОВАТЬ ПО 
	|	Автомобиль
	|;
	// Оборудование формы
	|
	|ВЫБРАТЬ
	|	РезультатЗапроса.Номенклатура КАК Номенклатура,
	|	РезультатЗапроса.Коэффициент*РезультатЗапроса.Количество КАК Количество
	|ПОМЕСТИТЬ
	|	ВремТаблицаОборудованияФормы
	|ИЗ
	|	&ОборудованиеФормы КАК РезультатЗапроса
	|;
	|
	|ВЫБРАТЬ
	|	РезультатЗапроса.Опция КАК Опция,
	|	РезультатЗапроса.Количество КАК Количество
	|ПОМЕСТИТЬ
	|	ВремТаблицаОпцийФормы
	|ИЗ
	|	&ОпцииФормы КАК РезультатЗапроса
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаОборудованияФормы.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаОборудованияФормы.Количество) КАК Количество
	|ПОМЕСТИТЬ
	|	СокращеннаяТаблицаОборудованияФормы
	|ИЗ
	|	ВремТаблицаОборудованияФормы КАК ТаблицаОборудованияФормы
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОборудованияФормы.Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаОпцийФормы.Опция КАК Опция,
	|	СУММА(ТаблицаОпцийФормы.Количество) КАК Количество
	|ПОМЕСТИТЬ
	|	СокращеннаяТаблицаОпцийФормы
	|ИЗ
	|	ВремТаблицаОпцийФормы КАК ТаблицаОпцийФормы
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОпцийФормы.Опция
	|;
	|
	|УНИЧТОЖИТЬ
	|	ВремТаблицаОборудованияФормы
	|;
	|
	|УНИЧТОЖИТЬ
	|	ВремТаблицаОпцийФормы
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаОборудованияФормы.Номенклатура КАК Номенклатура,
	|	ТаблицаОборудованияФормы.Количество КАК Количество
	|ПОМЕСТИТЬ
	|	ТаблицаОборудованияФормы
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	СокращеннаяТаблицаОборудованияФормы КАК ТаблицаОборудованияФормы
	|ПО
	|	ИСТИНА
	|ГДЕ
	|	ТаблицаАвтомобилей.Модель = &ВыбМодель
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаОпцийФормы.Опция КАК Опция,
	|	ТаблицаОпцийФормы.Количество КАК Количество
	|ПОМЕСТИТЬ
	|	ТаблицаОпцийФормы
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	СокращеннаяТаблицаОпцийФормы КАК ТаблицаОпцийФормы
	|ПО
	|	ИСТИНА
	|ГДЕ
	|	ТаблицаАвтомобилей.Модель = &ВыбМодель
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаАвтомобилей.ВариантКомплектации
	|ПОМЕСТИТЬ
	|	ТаблицаВариантКомплектации
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ИНДЕКСИРОВАТЬ ПО
	|	ВариантКомплектации
	|;
	|
	|ВЫБРАТЬ
	|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
	|	КомплектацияАвтомобилей.Номенклатура КАК Оборудование,
	|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	КомплектацияАвтомобилей.КоличествоОстаток КАК Количество,
	|	КомплектацияАвтомобилей.СуммаПродажиОстаток КАК СуммаПродажи
	|ПОМЕСТИТЬ
	|	ОборудованиеАвтомобилей
	|ИЗ
	|	РегистрНакопления.КомплектацияАвтомобилей.Остатки(,ТИПЗНАЧЕНИЯ(Номенклатура) = ТИП(Справочник.Номенклатура) И Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ ТаблицаАвтомобилей)) КАК КомплектацияАвтомобилей
	|ИНДЕКСИРОВАТЬ ПО
	|	Автомобиль
	|;
	|
	|ВЫБРАТЬ
	|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
	|	КомплектацияАвтомобилей.Номенклатура КАК Оборудование,
	|	КомплектацияАвтомобилей.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ
	|	КомплектацияАвтомобилей
	|ИЗ
	|	РегистрНакопления.КомплектацияАвтомобилей.Остатки(,ТИПЗНАЧЕНИЯ(Номенклатура) = ТИП(Справочник.Опции) И Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ ТаблицаАвтомобилей)) КАК КомплектацияАвтомобилей
	|ИНДЕКСИРОВАТЬ ПО
	|	Автомобиль");
	
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("ОпцииФормы",        ЭтаФорма.ОсновнойДокумент.Опции);
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("ОборудованиеФормы", ЭтаФорма.ОсновнойДокумент.Товары);
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("ВыбМодель",         ЭтаФорма.ОсновнойДокумент.Модель);
	
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("КурсДокументаОснования", ЭтаФорма.ОсновнойДокумент.КурсДокумента);
	ЗапросАвтомобилейСОборудованием.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("РезультатЗапроса", ПостроительОтчета.Результат);
	Выгрузка = ЗапросАвтомобилейСОборудованием.ВыполнитьПакет()[0].Выгрузить();
	Если ЗаказыСАвтоработами Тогда
		ТекОтбор = ПостроительОтчета.Отбор.Заказ;
		ТекОтбор.Использование	= ИспользованиеОтбораПоЗаказам;
		ТекОтбор.ВидСравнения	= ВидСравненияОтбораПоЗаказам;
		ТекОтбор.Значение		= ЗначениеОтбораПоЗаказам;
	КонецЕсли;
	Если Выгрузка[0].Количество=0 Тогда Возврат; КонецЕсли;
	Если обЗначениеНеЗаполнено(ТипЦен) Тогда
		ТипЦен = обПраво("ОсновнойТипЦенПродажиАвтомобилей", глПрава);
	КонецЕсли;
	РабочийТипЦен = ТипЦен;
	ТекстЦена = "ТаблицаЦен.Цена";
	// Если расчетная цена получим базовую цену
	Пока РабочийТипЦен.Рассчитывается Цикл
		Если РабочийТипЦен.ПроцентСкидкиНаценки<>0 Тогда
			ТекстЦена = "("+ТекстЦена+"+"+ТекстЦена+"*"+Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
		КонецЕсли;
		РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
	КонецЦикла;
	
	ЕстьОборудованиеЗаказа = (ЭтаФорма.ОсновнойДокумент.Опции.Количество()+ЭтаФорма.ОсновнойДокумент.Товары.Количество())>0;
	
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("ТипЦенПродажи", РабочийТипЦен);
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("ВалютаТипаЦен", РабочийТипЦен.ВалютаЦены);
	
	ТекРодитель=ЭтаФорма.ОсновнойДокумент.ПодразделениеКомпании;
	ТаблицаПодразделений = Новый ТаблицаЗначений;
	ТаблицаПодразделений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаПодразделений.Колонки.Добавить("Уровень", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(1,0)));
	
	Уровень=0;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		НоваяСтрока = ТаблицаПодразделений.Добавить();
		НоваяСтрока.Подразделение = ТекРодитель;
		НоваяСтрока.Уровень = Уровень;
		ТекРодитель = ТекРодитель.Родитель;
		Уровень=Уровень+1;
	КонецЦикла;
	НоваяСтрока = ТаблицаПодразделений.Добавить();
	НоваяСтрока.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
	НоваяСтрока.Уровень = Уровень;
	
	ЗапросАвтомобилейСОборудованием.УстановитьПараметр("ТаблицаПодразделений", ТаблицаПодразделений);
	
	ЗапросАвтомобилейСОборудованием.Текст = "
	|ВЫБРАТЬ
	|	КурсыВалют.Валюта КАК Валюта,
	|	(ВЫБОР
	|		КОГДА КурсыВалют.Курс = 0 ИЛИ КурсыВалют.Кратность = 0 ТОГДА
	|			1
	|		ИНАЧЕ
	|			КурсыВалют.Курс/КурсыВалют.Кратность
	|	КОНЕЦ)/&КурсДокументаОснования КАК Коэффициент
	|ПОМЕСТИТЬ
	|	КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(, " + ?(РабочийТипЦен.ВВалютеУчета, "", "Валюта = &ВалютаТипаЦен") + ") КАК КурсыВалют
	|;
	|
	|ВЫБРАТЬ
	|	ЦеныОпций.Модель КАК Модель,
	|	ЦеныОпций.ВариантКомплектации КАК ВариантКомплектации,
	|	ЦеныОпций.Опция КАК Опция,
	|	ЦеныОпций.Период КАК Период,
	|	ЦеныОпций.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦенОпций
	|ИЗ
	|	РегистрСведений.ЦеныОпций КАК ЦеныОпций
	|ГДЕ
	|	ЦеныОпций.ТипЦен = &ТипЦенПродажи И 
	|	ВариантКомплектации В (ВЫБРАТЬ ВариантКомплектации ИЗ ТаблицаВариантКомплектации)
	|ИНДЕКСИРОВАТЬ ПО
	|	Модель,
	|	ВариантКомплектации,
	|	Опция
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаВариантКомплектации
	|;
	|
	|ВЫБРАТЬ
	|	ЦеныОпций.Модель КАК Модель,
	|	ЦеныОпций.ВариантКомплектации КАК ВариантКомплектации,
	|	ЦеныОпций.Опция КАК Опция,
	|	МАКСИМУМ(ЦеныОпций.Период) КАК Период
	|ПОМЕСТИТЬ
	|	СрезПоследнихОпции
	|ИЗ
	|	ТаблицаЦенОпций КАК ЦеныОпций
	|СГРУППИРОВАТЬ ПО
	|	ЦеныОпций.Модель,
	|	ЦеныОпций.ВариантКомплектации,
	|	ЦеныОпций.Опция
	|ИНДЕКСИРОВАТЬ ПО
	|	Модель,
	|	ВариантКомплектации,
	|	Опция
	|;
	|
	|ВЫБРАТЬ
	|	СрезПоследнихОпции.Модель КАК Модель,
	|	СрезПоследнихОпции.ВариантКомплектации КАК ВариантКомплектации,
	|	СрезПоследнихОпции.Опция КАК Опция,
	|	МАКСИМУМ("+ТекстЦена+"*КурсыВалют.Коэффициент) КАК Цена
	|ПОМЕСТИТЬ
	|	ЦеныНаОпции
	|ИЗ
	|	СрезПоследнихОпции КАК СрезПоследнихОпции
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦенОпций КАК ТаблицаЦен
	|ПО
	|	СрезПоследнихОпции.Модель              = ТаблицаЦен.Модель И 
	|	СрезПоследнихОпции.ВариантКомплектации = ТаблицаЦен.ВариантКомплектации И 
	|	СрезПоследнихОпции.Опция               = ТаблицаЦен.Опция И 
	|	СрезПоследнихОпции.Период              = ТаблицаЦен.Период"+?(РабочийТипЦен.ВВалютеУчета, "
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалют КАК КурсыВалют
	|ПО
	|	СрезПоследнихОпции.Модель.ВалютаУчета = КурсыВалют.Валюта","
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалют КАК КурсыВалют
	|ПО
	|	ИСТИНА")+"
	|СГРУППИРОВАТЬ ПО
	|	СрезПоследнихОпции.Модель,
	|	СрезПоследнихОпции.ВариантКомплектации,
	|	СрезПоследнихОпции.Опция
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаЦен.Цена)>0
	|ИНДЕКСИРОВАТЬ ПО
	|	Модель,
	|	ВариантКомплектации,
	|	Опция
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследнихОпции
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаЦенОпций
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	КомплектацияАвтомобилей.Оборудование КАК Номенклатура,
	|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	КомплектацияАвтомобилей.Количество КАК Количество,
	|	(ВЫБОР
	|		КОГДА КомплектацияАвтомобилей.Количество = 0 ТОГДА
	|			КомплектацияАвтомобилей.СуммаПродажи
	|		ИНАЧЕ
	|			КомплектацияАвтомобилей.СуммаПродажи/КомплектацияАвтомобилей.Количество
	|	КОНЕЦ)/&КурсДокументаОснования КАК Цена,
	|	КомплектацияАвтомобилей.СуммаПродажи/&КурсДокументаОснования КАК Сумма
	|ПОМЕСТИТЬ
	|	ТаблицаОборудования
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ОборудованиеАвтомобилей КАК КомплектацияАвтомобилей
	|ПО
	|	ТаблицаАвтомобилей.Автомобиль = КомплектацияАвтомобилей.Автомобиль
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	ДокументЗаказНаАвтомобильОпции.Опция КАК Опция,
	|	ДокументЗаказНаАвтомобильОпции.Количество КАК Количество,
	|	ЕСТЬNULL(ЦеныНаОпции.Цена, ЕСТЬNULL(ЦеныНаОпцииМодели.Цена, 0)) КАК Цена,
	|	ДокументЗаказНаАвтомобильОпции.Количество*ЕСТЬNULL(ЦеныНаОпции.Цена, ЕСТЬNULL(ЦеныНаОпцииМодели.Цена, 0)) КАК Сумма
	|ПОМЕСТИТЬ
	|	ТаблицаОпций
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Документ.ЗаказПоставщикуНаАвтомобиль.Опции КАК ДокументЗаказНаАвтомобильОпции
	|ПО
	|	ТаблицаАвтомобилей.АвтомобильНаСкладе = ЛОЖЬ И 
	|	ТаблицаАвтомобилей.ЗаказПоставщику = ДокументЗаказНаАвтомобильОпции.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦеныНаОпции КАК ЦеныНаОпции
	|ПО
	|	ТаблицаАвтомобилей.Модель              = ЦеныНаОпции.Модель И 
	|	ТаблицаАвтомобилей.ВариантКомплектации = ЦеныНаОпции.ВариантКомплектации И 
	|	ДокументЗаказНаАвтомобильОпции.Опция   = ЦеныНаОпции.Опция
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦеныНаОпции КАК ЦеныНаОпцииМодели
	|ПО
	|	ТаблицаАвтомобилей.Модель             = ЦеныНаОпцииМодели.Модель И 
	|	ЦеныНаОпцииМодели.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) И 
	|	ДокументЗаказНаАвтомобильОпции.Опция  = ЦеныНаОпцииМодели.Опция
	|ГДЕ
	|	ТаблицаАвтомобилей.АвтомобильНаСкладе = ЛОЖЬ
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ТаблицаАвтомобилей.Автомобиль,
	|	КомплектацияАвтомобилей.Оборудование,
	|	КомплектацияАвтомобилей.Количество,
	|	ЕСТЬNULL(ЦеныНаОпции.Цена, ЕСТЬNULL(ЦеныНаОпцииМодели.Цена, 0)),
	|	КомплектацияАвтомобилей.Количество*ЕСТЬNULL(ЦеныНаОпции.Цена, ЕСТЬNULL(ЦеныНаОпцииМодели.Цена, 0))
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	|ПО
	|	ТаблицаАвтомобилей.Автомобиль   = КомплектацияАвтомобилей.Автомобиль
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦеныНаОпции КАК ЦеныНаОпции
	|ПО
	|	ТаблицаАвтомобилей.Модель              = ЦеныНаОпции.Модель И 
	|	ТаблицаАвтомобилей.ВариантКомплектации = ЦеныНаОпции.ВариантКомплектации И 
	|	КомплектацияАвтомобилей.Оборудование   = ЦеныНаОпции.Опция
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦеныНаОпции КАК ЦеныНаОпцииМодели
	|ПО
	|	ТаблицаАвтомобилей.Модель             = ЦеныНаОпцииМодели.Модель И 
	|	ЦеныНаОпцииМодели.ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) И 
	|	КомплектацияАвтомобилей.Оборудование  = ЦеныНаОпцииМодели.Опция
	|;
	|
	|УНИЧТОЖИТЬ
	|	ЦеныНаОпции
	|;
	|
	|УНИЧТОЖИТЬ
	|	КомплектацияАвтомобилей
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаОборудования.Автомобиль КАК Автомобиль,
	|	ТаблицаОборудования.Номенклатура КАК Номенклатура,
	|	ТаблицаОборудования.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаОборудования.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаОборудованияФормы.Количество ЕСТЬ NULL ТОГДА 2
	|		КОГДА ТаблицаОборудованияФормы.Количество=0 И ТаблицаОборудования.Количество>0 ТОГДА 2
	|		КОГДА ТаблицаОборудованияФормы.Количество < ТаблицаОборудования.Количество ТОГДА 1
	|		КОГДА ТаблицаОборудованияФормы.Количество > ТаблицаОборудования.Количество ТОГДА -1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТочноеСоответствие,
	|	ТаблицаОборудования.Цена КАК Цена,
	|	ТаблицаОборудования.Сумма КАК Сумма
	|ИЗ
	|	ТаблицаОборудования КАК ТаблицаОборудования
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	СокращеннаяТаблицаОборудованияФормы КАК ТаблицаОборудованияФормы
	|ПО
	|	ТаблицаОборудования.Номенклатура = ТаблицаОборудованияФормы.Номенклатура
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОборудования.Номенклатура.Наименование";
	
	СписокНайденноеДопОборудование.Загрузить(ЗапросАвтомобилейСОборудованием.Выполнить().Выгрузить());
	
	ЗапросАвтомобилейСОборудованием.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаОпций.Автомобиль КАК Автомобиль,
	|	ТаблицаОпций.Опция КАК Опция,
	|	ТаблицаОпций.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаОборудования.Количество ЕСТЬ NULL ТОГДА 2
	|		КОГДА ТаблицаОборудования.Количество=0 И ТаблицаОпций.Количество>0 ТОГДА 2
	|		КОГДА ТаблицаОборудования.Количество < ТаблицаОпций.Количество ТОГДА 1
	|		КОГДА ТаблицаОборудования.Количество > ТаблицаОпций.Количество ТОГДА -1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТочноеСоответствие,
	|	ТаблицаОпций.Цена КАК Цена,
	|	ТаблицаОпций.Сумма КАК Сумма
	|ИЗ
	|	ТаблицаОпций КАК ТаблицаОпций
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	СокращеннаяТаблицаОпцийФормы КАК ТаблицаОборудования
	|ПО
	|	ТаблицаОпций.Опция = ТаблицаОборудования.Опция
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОпций.Опция.Наименование";
	
	СписокНайденныеДопОпции.Загрузить(ЗапросАвтомобилейСОборудованием.Выполнить().Выгрузить());
	
	ЗапросАвтомобилейСОборудованием.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаОборудования.Автомобиль КАК Автомобиль,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ТаблицаОборудования.ЕстьНедостаток)=ИСТИНА ТОГДА -1 
	|		КОГДА МАКСИМУМ(ТаблицаОборудования.ЕстьПревышение)=ИСТИНА ТОГДА 1 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТочноеСоответствие,
	|	СУММА(ТаблицаОборудования.ЦенаОпций) КАК ЦенаОпций,
	|	СУММА(ТаблицаОборудования.ЦенаОборудования) КАК ЦенаОборудования,
	|	СУММА(ТаблицаОборудования.Сумма) КАК Сумма
	|ПОМЕСТИТЬ
	|	ТаблицаИтоговПоОборудованию
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаОборудования.Автомобиль КАК Автомобиль,
	|		ТаблицаОборудования.Номенклатура КАК Номенклатура,
	|		СУММА(ТаблицаОборудования.Количество) КАК Количество,
	|		ВЫБОР
	|			КОГДА СУММА(ТаблицаОборудования.КоличествоФормы) < СУММА(ТаблицаОборудования.Количество) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ КАК ЕстьПревышение,
	|		ВЫБОР
	|			КОГДА СУММА(ТаблицаОборудования.КоличествоФормы) > СУММА(ТаблицаОборудования.Количество) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ КАК ЕстьНедостаток,
	|		СУММА(ТаблицаОборудования.ЦенаОпций) КАК ЦенаОпций,
	|		СУММА(ТаблицаОборудования.ЦенаОборудования) КАК ЦенаОборудования,
	|		СУММА(ТаблицаОборудования.Сумма) КАК Сумма
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаОборудования.Автомобиль КАК Автомобиль,
	|			ТаблицаОборудования.Номенклатура КАК Номенклатура,
	|			ТаблицаОборудования.Количество КАК Количество,
	|			0 КАК КоличествоФормы,
	|			0 КАК ЦенаОпций,
	|			ТаблицаОборудования.Сумма КАК ЦенаОборудования,
	|			ТаблицаОборудования.Сумма КАК Сумма
	|		ИЗ
	|			ТаблицаОборудования КАК ТаблицаОборудования
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаОборудования.Автомобиль,
	|			ТаблицаОборудования.Номенклатура,
	|			0,
	|			ТаблицаОборудования.Количество,
	|			0,
	|			0,
	|			0
	|		ИЗ
	|			ТаблицаОборудованияФормы КАК ТаблицаОборудования
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаОпций.Автомобиль,
	|			ТаблицаОпций.Опция,
	|			ТаблицаОпций.Количество,
	|			0 КАК КоличествоФормы,
	|			ТаблицаОпций.Сумма,
	|			0,
	|			ТаблицаОпций.Сумма
	|		ИЗ
	|			ТаблицаОпций КАК ТаблицаОпций
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаОпций.Автомобиль КАК Автомобиль,
	|			ТаблицаОпций.Опция,
	|			0,
	|			ТаблицаОпций.Количество,
	|			0,
	|			0,
	|			0
	|		ИЗ
	|			ТаблицаОпцийФормы КАК ТаблицаОпций) КАК ТаблицаОборудования
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаОборудования.Автомобиль,
	|		ТаблицаОборудования.Номенклатура) КАК ТаблицаОборудования
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОборудования.Автомобиль
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаОборудования
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаОпций
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	|	ТаблицаАвтомобилей.АвтомобильНаСкладе КАК АвтомобильНаСкладе,
	|	ТаблицаАвтомобилей.АвтомобильВРезерве КАК АвтомобильВРезерве,
	|	ТаблицаАвтомобилей.АвтомобильПодЗаказ КАК АвтомобильПодЗаказ,
	|	ТаблицаАвтомобилей.СрокСнятияРезерва КАК СрокСнятияРезерва,
	|	ВЫБОР
	|		КОГДА ТаблицаАвтомобилей.VIN = """" ТОГДА
	|			""VIN не известен""
	|		ИНАЧЕ
	|			ТаблицаАвтомобилей.VIN
	|	КОНЕЦ КАК VIN,
	|	ТаблицаАвтомобилей.НаименованиеАвтомобиля КАК НаименованиеАвтомобиля,
	|	ТаблицаАвтомобилей.Модель КАК Модель,
	|	ТаблицаАвтомобилей.ВариантКомплектации КАК ВариантКомплектации,
	|	ТаблицаАвтомобилей.Заказ КАК Заказ,
	|	ТаблицаАвтомобилей.СтатусПартии КАК СтатусПартии,
	|	ТаблицаАвтомобилей.ЗаказПредставление КАК ЗаказПредставление,
	|	ТаблицаАвтомобилей.Менеджер КАК Менеджер,
	|	ТаблицаАвтомобилей.ЗаказПоставщику	КАК ЗаказПоставщику,
	|	ТаблицаАвтомобилей.СтатусЗаказаПоставщику КАК СтатусЗаказаПоставщику,
	|	ТаблицаАвтомобилей.СрокПоступления	КАК СрокПоступления,
	|	ТаблицаАвтомобилей.ЗаказПоставщикуПредставление КАК ЗаказПоставщикуПредставление,
	|	ТаблицаАвтомобилей.ВнешнийНомерЗаказа КАК ВнешнийНомерЗаказа,
	|	ТаблицаАвтомобилей.СкладКомпании КАК СкладКомпании,
	|	ТаблицаАвтомобилей.ДатаПоступления КАК ДатаПоступления,
	|	ТаблицаАвтомобилей.КоличествоДнейНаСкладе КАК КоличествоДнейНаСкладе,
	|	ТаблицаАвтомобилей.Контрагент КАК Контрагент,
	|	ТаблицаАвтомобилей.Комментарий КАК Комментарий,
	|	ТаблицаАвтомобилей.Цена КАК ЦенаАвтомобиля,
	|	ТаблицаАвтомобилей.ТипКузова КАК ТипКузова,
	|	ТаблицаАвтомобилей.ТипДвигателя КАК ТипДвигателя,
	|	ТаблицаАвтомобилей.ТипКПП КАК ТипКПП,
	|	ТаблицаАвтомобилей.ТипСалона КАК ТипСалона,
	|	ТаблицаАвтомобилей.Цвет КАК Цвет,
	|	ТаблицаАвтомобилей.ВидАвтомобиля КАК ВидАвтомобиля,
	|	ТаблицаАвтомобилей.КомментарийАвтомобиля КАК КомментарийАвтомобиля,
	|	ТаблицаАвтомобилей.ВнесеннаяСуммаПредоплаты КАК ВнесеннаяСуммаПредоплаты,
	|	ТаблицаАвтомобилей.СуммаСкидки КАК СуммаСкидки,
	|	ТаблицаАвтомобилей.СуммаСделки КАК СуммаСделки,
	|	ТаблицаАвтомобилей.СуммаПредоплаты КАК СуммаПредоплаты,
	|	ТаблицаАвтомобилей.ОстатокПредоплаты КАК ОстатокПредоплаты,
	|	ЕСТЬNULL(ТаблицаОборудования.ТочноеСоответствие, 0) КАК ТочноеСоответствие,
	|	ЕСТЬNULL(ТаблицаОборудования.ЦенаОпций, 0) КАК ЦенаОпций,
	|	ЕСТЬNULL(ТаблицаОборудования.ЦенаОборудования, 0) КАК ЦенаОборудования,
	|	ЕСТЬNULL(ТаблицаОборудования.Сумма, 0)+ТаблицаАвтомобилей.Цена КАК ОбщаяСтоимость
	|ИЗ
	|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ТаблицаИтоговПоОборудованию КАК ТаблицаОборудования
	|ПО
	|	ТаблицаАвтомобилей.Автомобиль = ТаблицаОборудования.Автомобиль
	|УПОРЯДОЧИТЬ ПО
	|	НаименованиеАвтомобиля";
	
	СписокНайденныеАвтомобили.Загрузить(ЗапросАвтомобилейСОборудованием.Выполнить().Выгрузить());
	
КонецПроцедуры // СформироватьОтчетПоАвтомобилям()


///////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Права = глПрава;

#КонецЕсли
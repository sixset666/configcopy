// Модуль обработки
Перем ПрефиксУзлаCML;
Перем НачалоЭлементаCML;
Перем КонецЭлементаCML;
Перем ПрефиксАтрибутаCML;

Перем ПодкаталогКартинок;
Перем ПодкаталогБезопасностиКаталогаВыгрузки;

Перем ПараметрЗапросаHTTP_Инициализация;
Перем ПараметрЗапросаHTTP_ПередачаФайла;
Перем ПараметрЗапросаHTTP_ИмпортФайлаСервером;
Перем ПараметрЗапросаHTTP_ПолучитьДанные; 
Перем ПараметрЗапросаHTTP_УспешноеЗавершениеИмпорта;
     
Перем ОтветСервера_ZIPРазрешен;
Перем ОтветСервера_ОграничениеРазмераФрагментаФайлаОбмена;
Перем ОтветСервера_УспешноеЗавершениеТекущейОперации;
Перем ОтветСервера_АварийноеЗавершениеТекущейОперации;
Перем ОтветСервера_ВыполнениеТекущейОперации;

Перем ПустаяХарактеристикаСсылка;

Перем НаименованиеНалога;

Перем НаименованиеКаталогаТоваровCML;
Перем НаименованиеПакетаПредложенийCML;

Перем БулевоЗначениеCML_Истина;
Перем БулевоЗначениеCML_Ложь;
Перем БулевоЗначениеCML_Да;
Перем БулевоЗначениеCML_Нет;

Перем ВидНоменклатурыCML_Услуга;
Перем ВидНоменклатурыCML_Товар;
Перем ЗначениеCML_ТипНоменклатуры;

Перем мСтруктураИнформацииИсторииОбмена;

Перем мТипНоменклатурыУслуга;	
Перем мТипНоменклатурыТовар;
Перем мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом;

Перем мМассивЗагруженныхДокументов;
Перем мПрефикс;                             // Префикс текущего сайта, используется для префиксации входящего номера документа.
Перем Права;								// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ОшибкиТекстом Экспорт;                // Ошибки в виде текста.
Перем ОшибкиHTMLем 	Экспорт;				// Ошибки в виде html, в случае исключительной ситуации web сервер возвращает причину ошибки и дамп переменных в виде html страницы
Перем ТаблицаФайловОбмена Экспорт;			// Для хранения промежуточных файлов обмена.
Перем ТаблицаУслуг;							// Таблица, в которой хранятся услуги документов

Перем ПодразделениеДляВыборкиТоваров;
Перем КонтрагентДляВыборкиТоваров;
Перем СпособОпределенияЦен;
Перем ПредставленияВалют;
Перем СообщениеОНевыгруженныхТоварах;
Перем ВыгружатьТоварыБезЦены;

///////////////////////////////////////////////////////////////////////////////
//  ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТОВ

// Проверяет. можно ли использовать переданный договор в соответствии с переданными параметрами.
//
// Параметры
//  Договор                                 – ссылка на договор, который нужно проверить,
//  Контрагент                              - ссылка на контрагента, которому должен принадлежать договор,
//  Организация                             - ссылка на организацию, от имени которой должен быть выписан договор,
//  СтруктураПараметровДляПолученияДоговора - структура, содержащая параметры для определения договора:
//                                            список допустимых видов договоров и
//                                            список допустимых способов ведения взаиморасчетов.
//
// Возвращаемое значение:
//   Логическое, Истина - можно использовать, ложь - нельзя.
//
Функция МожноИспользоватьДоговорДляДокумента(Договор, Контрагент, Организация, СтруктураПараметровДляПолученияДоговора) Экспорт

	Перем СписокДопустимыхВидовДоговоров, СписокДопустимыхВидовВзаиморасчетов, ВалютаВзаиморасчетовДоговора, ВидСравненияВалютыВзаиморасчетов;


	Если ( обЗначениеНеЗаполнено(Договор) ) Тогда
		Возврат Ложь; // Если не передали параметры, то считаем, что нельзя использовать.
	КонецЕсли;

	Если ( СтруктураПараметровДляПолученияДоговора <> Неопределено ) Тогда
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовДоговоров", СписокДопустимыхВидовДоговоров);
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовВзаиморасчетов", СписокДопустимыхВидовВзаиморасчетов);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВалютаВзаиморасчетовДоговора", ВалютаВзаиморасчетовДоговора);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВидСравненияВалютыВзаиморасчетов", ВидСравненияВалютыВзаиморасчетов);
	КонецЕсли;

	// Организация должна совпадать.
	Если ( НЕ обЗначениеНеЗаполнено(Организация) И Организация <> Договор.Организация ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Контрагент должен совпадать, если в документе не выбран контрагент, то любой договор не подходит.
	Если ( Контрагент <> Договор.Владелец ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Проверка по виду договора.
	Если ( СписокДопустимыхВидовДоговоров <> Неопределено 
	   И СписокДопустимыхВидовДоговоров.НайтиПоЗначению(Договор.ВидДоговора) = Неопределено ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Проверка по виду взаиморасчетов.
	Если ( СписокДопустимыхВидовВзаиморасчетов <> Неопределено 
	   И СписокДопустимыхВидовВзаиморасчетов.НайтиПоЗначению(Договор.ВедениеВзаиморасчетов) = Неопределено ) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Проверка по валюте взаиморасчетов.
	Если ( ВалютаВзаиморасчетовДоговора <> Неопределено
	   И ВидСравненияВалютыВзаиморасчетов <> Неопределено ) Тогда
		Если ( ВидСравненияВалютыВзаиморасчетов = "=" ) Тогда
			Если ( Договор.ВалютаВзаиморасчетов <> ВалютаВзаиморасчетовДоговора ) Тогда
				Возврат Ложь;
			КонецЕсли;
		Иначе // ВидСравненияВалютыВзаиморасчетов может принимать только два значения: "=" и "<>".
			Если ( Договор.ВалютаВзаиморасчетов = ВалютаВзаиморасчетовДоговора ) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Истина;

КонецФункции // МожноИспользоватьДоговорДляДокумента()

// Функция возвращает таблицу договоров данного контрагента, доступных для выбора
//
// Параметры
//  Контрагент                              - контрагент, по которому определяется договор
//  СтруктураПараметровДляПолученияДоговора - структура, содержащая параметры для определения договора:
//                                            список допустимых видов договоров и
//                                            список допустимых способов ведения взаиморасчетов, не обязательный.
// 	ВозвращатьТолькоПервые                  - булево, Истина - нужно вернуть только первые два договора,
//                                          - ложь - нужно вернуть весь список.
//  Организация                             - необязательный, организация, по которой определяется договор,
//                                            если не передана, то возвращается список договоров по всем ораганизациям.
//  ДополнительныеРеквизиты                 - необязательный, массив дополнительных реквизитов договора,
//                                            которые необходимо вернуть в результате запроса.
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция ПолучитьДоступныеДоговорыКонтрагента(Контрагент, СтруктураПараметровДляПолученияДоговора, ВозвращатьТолькоПервые, Организация = Неопределено, ДополнительныеРеквизиты = Неопределено) Экспорт

	Перем СписокДопустимыхВидовДоговоров, СписокДопустимыхВидовВзаиморасчетов, ВалютаВзаиморасчетовДоговора, ВидСравненияВалютыВзаиморасчетов;

	Если ( СтруктураПараметровДляПолученияДоговора <> Неопределено ) Тогда
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовДоговоров", СписокДопустимыхВидовДоговоров);
		СтруктураПараметровДляПолученияДоговора.Свойство("СписокДопустимыхВидовВзаиморасчетов", СписокДопустимыхВидовВзаиморасчетов);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВалютаВзаиморасчетовДоговора", ВалютаВзаиморасчетовДоговора);
		СтруктураПараметровДляПолученияДоговора.Свойство("ВидСравненияВалютыВзаиморасчетов", ВидСравненияВалютыВзаиморасчетов);
	КонецЕсли;

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ПарКонтрагент",  Контрагент);
	Запрос.УстановитьПараметр("ПарВидДоговора", СписокДопустимыхВидовДоговоров);
	Запрос.УстановитьПараметр("ПарВидВзаиморасчетов", СписокДопустимыхВидовВзаиморасчетов);
	Запрос.УстановитьПараметр("ПарВалютаВзаиморасчетов", ВалютаВзаиморасчетовДоговора);
	Запрос.УстановитьПараметр("ПарОрганизация", Организация);

	ПсевдонимТаблицы = "ДоговорыКонтрагентов"; 
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ"
	+?(ВозвращатьТолькоПервые," ПЕРВЫЕ 2","") + "
	|	ДоговорыКонтрагентов.Ссылка КАК Договор";
	
	Если ( Не обЗначениеНеЗаполнено(ДополнительныеРеквизиты) ) Тогда
		Для Каждого НаименованиеРеквизита Из ДополнительныеРеквизиты Цикл
			Запрос.Текст = Запрос.Текст  + ", " + ПсевдонимТаблицы + "." + НаименованиеРеквизита;
		КонецЦикла; 		
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст  + "
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК " + ПсевдонимТаблицы + "
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)"
	+?(Не обЗначениеНеЗаполнено(Организация), "	И ДоговорыКонтрагентов.Организация = &ПарОрганизация", "") + "
	|	И ДоговорыКонтрагентов.Владелец    = &ПарКонтрагент"
	+?(ВалютаВзаиморасчетовДоговора = Неопределено, "", "
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов " 
	+?(ВидСравненияВалютыВзаиморасчетов = Неопределено, "=", ВидСравненияВалютыВзаиморасчетов) + "(&ПарВалютаВзаиморасчетов)
	|")
	+?(СписокДопустимыхВидовДоговоров = Неопределено, "", "
	|	И ДоговорыКонтрагентов.ВидДоговора В (&ПарВидДоговора)
	|")
	+?(СписокДопустимыхВидовВзаиморасчетов = Неопределено, "", "
	|	И ДоговорыКонтрагентов.ВедениеВзаиморасчетов В (&ПарВидВзаиморасчетов)
	|");

	Если ( НЕ обЗначениеНеЗаполнено(Организация) ) Тогда 

		Запрос.Текст = Запрос.Текст + "
		|	И (ВЫБОР КОГДА ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.Организация = &ПарОрганизация"
		+?(ВалютаВзаиморасчетовДоговора = Неопределено, "", "
		|	И ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.ВалютаВзаиморасчетов " 
		+?(ВидСравненияВалютыВзаиморасчетов = Неопределено, "=", ВидСравненияВалютыВзаиморасчетов) + "&ПарВалютаВзаиморасчетов
		|")
		+?(СписокДопустимыхВидовДоговоров = Неопределено, "", "
		|	И ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.ВидДоговора В (&ПарВидДоговора)
		|")
		+?(СписокДопустимыхВидовВзаиморасчетов = Неопределено, "", "
		|	И ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента.ВедениеВзаиморасчетов В (&ПарВидВзаиморасчетов)
		|")
		+ "
		|	         ТОГДА ДоговорыКонтрагентов.Ссылка  = ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента
		|	         ИНАЧЕ ДоговорыКонтрагентов.Ссылка <> ДоговорыКонтрагентов.Владелец.ОсновнойДоговорКонтрагента КОНЕЦ)";

	КонецЕсли;

	Возврат Запрос.Выполнить();

КонецФункции // ПолучитьДоступныеДоговорыКонтрагента()

// функция возвращает основной договор контрагента
Функция ПолучитьОсновнойДоговорКонтрагента(Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДоговорыКонтрагентов.Ссылка КАК ДоступныйДоговорКонтрагента,
	|	ДоговорыКонтрагентов.Наименование КАК ДоступныйДоговорКонтрагентаНаименование
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО (Контрагенты.Ссылка = &Контрагент)
	|			И Контрагенты.ОсновнойДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ДоговорыКонтрагентов.Владелец = &Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА Контрагенты.ОсновнойДоговорКонтрагента ЕСТЬ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		Возврат Выборка.ДоступныйДоговорКонтрагента;		
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();		
	КонецЕсли;	
		
КонецФункции

// Функция определяет договор, соответствующий указанным значениям
// организации и контрагента, а также переданным параметрам.
// Если всем параметрам удовлетворяет основной договор, то выбирается он.
//
// Параметры
//  Организация                             - организация, по которой определяется договор
//  Контрагент                              - контрагент, по которому определяется договор
//  СтруктураПараметровДляПолученияДоговора - структура, содержащая параметры для определения договора:
//                                            список допустимых видов договоров и
//                                            список допустимых способов ведения взаиморасчетов, не обязательный.
//
// Возвращаемое значение:
//  Договор - договор контрагентов
//
Функция ПолучитьДоговорПоОрганизацииИКонтрагенту(Организация, Контрагент, СтруктураПараметровДляПолученияДоговора = Неопределено) Экспорт

	Перем СписокДопустимыхВидовДоговоров, СписокДопустимыхВидовВзаиморасчетов, ВалютаВзаиморасчетовДоговора, ВидСравненияВалютыВзаиморасчетов;

	Если (обЗначениеНеЗаполнено(Контрагент)) Тогда
		Возврат Неопределено;
	КонецЕсли;

	// Если не передана организация, то проверим, можно ли использовать основной договор. 
	Если ( обЗначениеНеЗаполнено(Организация) ) Тогда
		Договор = ПолучитьОсновнойДоговорКонтрагента(Контрагент);
		Если ( МожноИспользоватьДоговорДляДокумента(Договор, Контрагент, Организация, СтруктураПараметровДляПолученияДоговора) ) Тогда
			Возврат Договор;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;

	РезультатЗапроса = ПолучитьДоступныеДоговорыКонтрагента(Контрагент, СтруктураПараметровДляПолученияДоговора, Истина, Организация );
	ТаблицаДоговоров = РезультатЗапроса.Выгрузить();

	Если ТаблицаДоговоров.Количество() = 1 Тогда
		Возврат ТаблицаДоговоров[0].Договор;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ПолучитьДоговорПоОрганизацииИКонтрагенту()

// функция возвращает основное контактное лицо контрагента
Функция ПолучитьОсновноеКонтактноеЛицоКонтрагента(Контрагент) Экспорт
	ТаблицаВедомых = Неопределено;
	
	Если РаботаСКонтактнымиЛицами.ПолучитьВедомых(Контрагент, ТаблицаВедомых) Тогда
		
		ТаблицаВедомых.Сортировать("Основной", Истина);
		Возврат ТаблицаВедомых[0].Ведомый;
		
	КонецЕсли;
	
	Возврат Справочники.Контрагенты.ПустаяСсылка();
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УПРАВЛЕНИЯ ОБМЕНОМ ЗАКАЗАМИ

Функция ВыгрузитьЗаказыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоОбработанныхДокументовНаВыгрузке)
	
	КаталогОбмена = КаталогВременныхФайлов()+Строка(Новый УникальныйИдентификатор);
	
	КоличествоОбработанныхДокументовНаВыгрузке	= 0;
	Успешно = ВыгрузитьЗаказыВФайл(СтруктураИзменений, СтруктураПараметровСайта, КаталогОбмена, КоличествоОбработанныхДокументовНаВыгрузке);
	
	Если (НЕ Успешно) Тогда
		Возврат Ложь;
	ИначеЕсли (КоличествоОбработанныхДокументовНаВыгрузке = 0) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Успешно = HTTPВыгрузитьНаСервер(СтруктураПараметровСайта, КаталогОбмена, , , "sale");
	
	Попытка
		УдалитьФайлы(КаталогОбмена);
	Исключение
	КонецПопытки;
		
	Возврат Успешно;
	 
КонецФункции

Функция ЗагрузитьЗаказыИзФайла(КоличествоОбработанныхДокументов)
	
	КоличествоОбработанныхДокументов = 0;
	
	Файл = Новый Файл(ФайлЗагрузки);
	Если ( НЕ Файл.Существует() ИЛИ Файл.ЭтоКаталог() ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
	
	ФайлCML				= Новый ТекстовыйДокумент();
	ФайлCML.Прочитать(ФайлЗагрузки);
	СтрокаCML			= ФайлCML.ПолучитьТекст();
	ДеревоДокументов	= РазобратьCML(СтрокаCML);
	
	Если ( ДеревоДокументов = Неопределено ) Тогда	
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки = Истина;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		Возврат Истина;	
	КонецЕсли;
		
	Успешно = ОбработатьДокументы(ДеревоДокументов, КоличествоОбработанныхДокументов);
	Если ( НЕ Успешно ) Тогда
		СообщитьОбОшибкеОбмена("Не удалось обработать документы, загруженные из файла.", Ложь);	
	КонецЕсли;	
	
	мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= Успешно;
	мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
		
	Возврат Истина;
	
КонецФункции

Функция ВыгрузитьЗаказыВФайл(СтруктураИзменений, СтруктураПараметровСайта, Знач КаталогДляВыгрузки = "", КоличествоВыгруженныхЗаказов = 0)
	
	Успешно						= Истина;
	КоличествоВыгруженныхЗаказов= 0;
	
	МассивИзменений   = Новый Массив();
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		МассивИзменений = СтруктураИзменений.Заказы;	
		// из этого массива нужно удалить все только что загруженные заказы
		Для Каждого Эл Из мМассивЗагруженныхДокументов Цикл		
			ИндексЭлемента = МассивИзменений.Найти(Эл);
			Если ( ИндексЭлемента <> Неопределено ) Тогда
				МассивИзменений.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивИзменений.Количество() = 0 Тогда		
			СообщитьПользователю("Изменения заказов не зарегистрированы. Выгрузка заказов не произведена.", Истина,,2); 
			Возврат Истина;	
		КонецЕсли;			
	КонецЕсли;
	
	ОбъектCMLСтрока   = "";
	ТаблицаДокументов = СформироватьCMLСРанееЗагруженнымиЗаказами(ОбъектCMLСтрока, МассивИзменений);
	
	КоличествоВыгруженныхЗаказов = ТаблицаДокументов.Количество();
	
	Если КоличествоВыгруженныхЗаказов = 0 Тогда	
		СообщитьПользователю("Не выгружен ни один заказ.", Истина,,2);
	    Возврат Успешно;
	КонецЕсли;
	
	Если ( ПустаяСтрока(КаталогДляВыгрузки) ) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ИмяФайлаОбмена 		 = "1cbitrix-" + Строка(Новый УникальныйИдентификатор) + ".xml";
	ПолноеИмяФайлаОбмена = КаталогДляВыгрузки + "\" + ИмяФайлаОбмена;
	
	СоздатьКаталог(КаталогДляВыгрузки);
	
	ФайлCMLНаДиске = Новый ТекстовыйДокумент();
	ФайлCMLНаДиске.УстановитьТекст(ОбъектCMLСтрока);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Выгрузка";
	НоваяСтрока.Содержание	= ОбъектCMLСтрока;
	
	Попытка
		ФайлCMLНаДиске.Записать(ПолноеИмяФайлаОбмена, "UTF-8");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось записать CML файл на диск.");
		Возврат Ложь;
	КонецПопытки;
	
	СообщитьПользователю("Выгружено заказов: " + ТаблицаДокументов.Количество(), Истина,,1);	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ УПРАВЛЕНИЯ ПРОФИЛЯМИ (НАСТРОЙКАМИ)

Функция ОпределитьМожноВыгрузитьТовары()
	
	ЕстьКаталогВыгрузки   = Не обЗначениеНеЗаполнено(КаталогВыгрузки);
	ЕстьСайт			  = Не обЗначениеНеЗаполнено(HTTPОбменСервер);
	
	Если ( ВыгружатьНаСайт ) Тогда
		Возврат ЕстьСайт;
	Иначе
		Возврат ЕстьКаталогВыгрузки;
	КонецЕсли;
	
КонецФункции

Функция ОпределитьМожноВыгружатьЗаказы()
	
	ЕстьФайлЗагрузки 			= Не обЗначениеНеЗаполнено(ФайлЗагрузки);
	ЕстьСайт			  		= Не обЗначениеНеЗаполнено(HTTPОбменСервер);	
	ЕстьОрганизация  			= Не обЗначениеНеЗаполнено(Организация);
	ЕстьПараметрыИдентификации  = Не обЗначениеНеЗаполнено(СпособИдентификацииКонтрагентов);
	
	Если ( ВыгружатьНаСайт ) Тогда
		Возврат	ЕстьСайт И ЕстьОрганизация И ЕстьПараметрыИдентификации;
	Иначе
		Возврат ЕстьФайлЗагрузки;
	КонецЕсли;	
	
КонецФункции

////////////////////////////////////////////////////////////////
// СЕРВИСНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ ОБМЕНА

// функция разбирает строку адреса сайта на составные части
// Параметры:
//	АдресСайта - строка с адресом сайта
// Возвращает структуру с частями
//
Функция РазобратьАдресСайта(Знач АдресСайта) Экспорт
	
	АдресСайта = СокрЛП(АдресСайта); 
	
	HTTPСервер		 = "";
	HTTPПорт		 = 0;	
	HTTPАдресСкрипта = "";
	
	Если ( Не обЗначениеНеЗаполнено(АдресСайта) ) Тогда
		
		АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
		АдресСайта = СтрЗаменить(АдресСайта, " ", "");
		АдресСайта = СтрЗаменить(АдресСайта, "http://", "");
		ПозицияСлэша = Найти(АдресСайта, "/");
		Если ( ПозицияСлэша > 0 ) Тогда
			HTTPСервер 		 = Лев(АдресСайта, ПозицияСлэша - 1);	
			HTTPАдресСкрипта = Прав(АдресСайта, СтрДлина(АдресСайта) - ПозицияСлэша);
		Иначе	
			HTTPСервер 		 = АдресСайта;	
			HTTPАдресСкрипта = "";
		КонецЕсли;
		ПозицияДвоеточия = Найти(HTTPСервер, ":");
		Если ПозицияДвоеточия > 0 Тогда
			HTTPСерверСПортом = HTTPСервер;
			HTTPСервер		  = Лев(HTTPСерверСПортом, ПозицияДвоеточия - 1);
			HTTPПортСтрока 	  = Прав(HTTPСерверСПортом, СтрДлина(HTTPСерверСПортом) - ПозицияДвоеточия);
		Иначе
			HTTPПортСтрока = "80";
		КонецЕсли;
		Попытка
			HTTPПорт = Число(HTTPПортСтрока);
		Исключение
			HTTPПорт = 0;
		КонецПопытки;		
		
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("HTTPСервер"	  , HTTPСервер); 
	СтруктураРезультата.Вставить("HTTPАдресСкрипта", HTTPАдресСкрипта);
	СтруктураРезультата.Вставить("HTTPПорт"		  , HTTPПорт);
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Функция возвращает структуру параметров для соединения с сайтом
// Параметры:
//	Объект - соединяемый объект
//	Строка со значением начала адреса скрипта
//
Функция ПолучитьСтруктуруПараметровДляСоединения(Объект, Знач НачалоАдресаСкрипта = "") Экспорт
	
	СтруктураПараметровСайта = Новый Структура();
	Если ( ТипЗнч(Объект) = Тип("ОбработкаОбъект.ОбменССайтом") ) Тогда                                                 
		СтруктураПараметровСайта.Вставить("ИмяПользователя", Объект.УзелОбмена.ПользовательHTTP);
		СтруктураПараметровСайта.Вставить("Пароль"		   , Объект.УзелОбмена.ПарольHTTP);	
	Иначе
		СтруктураПараметровСайта.Вставить("ИмяПользователя", Объект.HTTPОбменИмяПользователя);
		СтруктураПараметровСайта.Вставить("Пароль"		   , Объект.HTTPОбменПароль);		
	КонецЕсли;

	
	Если ( Не ПустаяСтрока(НачалоАдресаСкрипта) ) Тогда
		
		СтруктураАдреса = РазобратьАдресСайта(НачалоАдресаСкрипта);
		
		СтруктураПараметровСайта.Вставить("АдресСкрипта", СтруктураАдреса.HTTPАдресСкрипта);
		СтруктураПараметровСайта.Вставить("Сервер"		, СтруктураАдреса.HTTPСервер);
		СтруктураПараметровСайта.Вставить("Порт"		, Объект.УзелОбмена.ПортHTTP);
		
	Иначе
		
		СтруктураПараметровСайта.Вставить("АдресСкрипта", Объект.HTTPОбменАдресСкрипта);
		СтруктураПараметровСайта.Вставить("Сервер"		, Объект.HTTPОбменСервер);
		СтруктураПараметровСайта.Вставить("Порт"		, Объект.HTTPОбменПорт);

	КонецЕсли;
	
	СтруктураПараметровСайта.Вставить("ПроксиСервер"		 , Объект.HTTPОбменПроксиСервер);
	СтруктураПараметровСайта.Вставить("ПроксиПорт"		     , Объект.HTTPОбменПроксиПорт);
	СтруктураПараметровСайта.Вставить("ПроксиИмяПользователя", Объект.HTTPОбменПроксиИмяПользователя);
	СтруктураПараметровСайта.Вставить("ПроксиПароль"		 , Объект.HTTPОбменПроксиПароль);
	СтруктураПараметровСайта.Вставить("ПроксиИспользование"  , Объект.HTTPОбменПроксиИспользование);	
	
	Возврат СтруктураПараметровСайта;
	
КонецФункции

// Функция возвращает полученные данные с HTTP сервера
// Параметры:
//	Соединение - объект HTTP соединения
//	ПараметрыЗапроса - строка параметров запроса
//	Заголовки - строка заголовков
//	СтрокаСообщенияПользователю - строка сообщения пользователю
//
Функция HTTPПолучитьДанныеССервера(Соединение, ПараметрыЗапроса="", Заголовки="", СтрокаСообщенияПользователю = "") Экспорт
	
	ОтветСервера   = Неопределено; 
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Если Заголовки="" Тогда
		Заголовки=Новый Соответствие;
	КонецЕсли; 
	
	Попытка
		Соединение.Получить(СокрЛП(ПараметрыЗапроса), ИмяФайлаОтвета, Заголовки);
	Исключение
		СтрокаСообщенияПользователю = "Не удалось получить данные с сервера.Проверьте правильность адреса сервера, порт, имя пользователя и пароль,"+Символы.ПС+"а также настройки подключения к Интернет.";
	КонецПопытки;	
	
	ФайлОтвета = Новый Файл(ИмяФайлаОтвета);
	
	Если (ФайлОтвета.Существует()) Тогда		
		ТекстОтвета		= Новый ТекстовыйДокумент();
		ТекстОтвета.Прочитать(ИмяФайлаОтвета);
		Если (ТекстОтвета.КоличествоСтрок() > 0) Тогда
			ОтветСервера				= ТекстОтвета.ПолучитьТекст();	
		Иначе
			СтрокаСообщенияПользователю	= "Получение данных с сервера: Получен пустой ответ сервера."; 	
		КонецЕсли;		
	Иначе	
		СтрокаСообщенияПользователю = "Получение данных с сервера: Ответ сервера не получен."; 
	КонецЕсли;	
	
	Попытка
		УдалитьФайлы(КаталогВременныхФайлов(), ИмяФайлаОтвета);
	Исключение
	КонецПопытки;
	
	Возврат ОтветСервера;
	
КонецФункции

// Функция производит авторизацию на сервере
// Параметры:
//	Соединение - объект HTTP соединения
//	СтруктураПараметровСайта - сутруктура параметров сайта
//	ОтветСервера - хранит ответ сервера
//	СтрокаСообщенияПользователю - строка сообщения пользователю
//	ТипСоединения - тип соединения с сервером
//
Функция HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения = "catalog") Экспорт
	
	Успешно	= Истина;
	ОтобразитьСостояние("Установка соединения с сервером...");

	Соединение = HTTPУстановитьСоединение(СтруктураПараметровСайта);
	 
	Если ( Соединение = Неопределено ) Тогда
		СтрокаСообщенияПользователю = "Не удалось установить соединение с сервером.";
		Возврат Ложь;
	КонецЕсли;
	
	ОтобразитьСостояние("Проверка имени пользователя и пароля...");
	
	ОтветСервера = HTTPПолучитьДанныеССервера(Соединение, СтруктураПараметровСайта.АдресСкрипта + "?type=" + ТипСоединения + "&mode=checkauth");
		
	Если ОтветСервера = Неопределено Тогда 
		СтрокаСообщенияПользователю = "Не удалось установить соединение с сервером. Авторизация пользователя не выполнена." + Символы.ПС + ОписаниеОшибки();
		Возврат Ложь;
	КонецЕсли;
		
	Если НРег(СтрПолучитьСтроку(ОтветСервера,1)) <> "success" Тогда
		СтрокаСообщенияПользователю = "Не удалось установить соединение с сервером. Проверьте имя пользователя и пароль." + Символы.ПС + ОписаниеОшибки();
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// функция устанавливает соединение с сервером
// Параметры:
//	СтруктураПараметровСайта - структура параметров сайта
// Возвращает объект http соединения
//
Функция HTTPУстановитьСоединение(СтруктураПараметровСайта) Экспорт
	
	Соединение		= Неопределено;
	ИнтернетПрокси	= Неопределено;
	
	Если ( СтруктураПараметровСайта.ПроксиИспользование ) Тогда
		ИнтернетПрокси				= Новый ИнтернетПрокси();
		ИнтернетПрокси.Пользователь	= СтруктураПараметровСайта.ПроксиИмяПользователя;
		ИнтернетПрокси.Пароль		= СтруктураПараметровСайта.ПроксиПароль;	
		Если ( СтруктураПараметровСайта.ПроксиПорт = 0 ) Тогда
			ИнтернетПрокси.Установить("HTTP", СтруктураПараметровСайта.ПроксиСервер);
		Иначе	
			ИнтернетПрокси.Установить("HTTP", СтруктураПараметровСайта.ПроксиСервер, СтруктураПараметровСайта.ПроксиПорт);
		КонецЕсли;		
	КонецЕсли;	
	
	Порт = ?(НЕ обЗначениеНеЗаполнено(СтруктураПараметровСайта.Порт), СтруктураПараметровСайта.Порт, 80);
	Попытка	
		Соединение	= Новый HTTPСоединение(СтруктураПараметровСайта.Сервер, Порт, СтруктураПараметровСайта.ИмяПользователя, СтруктураПараметровСайта.Пароль, ИнтернетПрокси);
	Исключение
		СообщитьОбОшибке("Не удалось установить соединение с сервером " + СтруктураПараметровСайта.Сервер + ":" + Строка(СтруктураПараметровСайта.Порт) + ".
			|Проверьте правильность адреса сервера, порт, имя пользователя и пароль.");	
		Соединение = Неопределено;		
	Конецпопытки;	
		
	Возврат Соединение;
	
КонецФункции

// Выполняет тестовое подключение к серверу
Функция ВыполнитьТестовоеПодключениеКСерверуHTTP(Объект, СтрокаСообщенияПользователю, РезультатСоединения = Неопределено, ТипСоединения = "catalog") Экспорт
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект, Объект.СерверHTTP);
		
	Соединение = HTTPУстановитьСоединение(СтруктураПараметровСайта);
	
	Если ( Соединение = Неопределено ) Тогда
		Возврат "Ошибка при установке соединения с сайтом.";
	КонецЕсли;
	
	ОтветСервера		= "";
	РезультатСоединения	= HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	
	Если ( Не РезультатСоединения ) Тогда
		Возврат "Соединение с сайтом не установлено.";
	Иначе
		Возврат "Соединение выполнено успешно.";
	КонецЕсли;
	
КонецФункции

// Настройка построителя отчета
Процедура НастроитьПостроительОтчета(ПостроительОбъект) Экспорт
		
	Если СпособОпределенияЦен=3 Тогда
		ПостроительОбъект.Параметры.Вставить("Контрагент", КонтрагентДляВыборкиТоваров);
	Иначе
		ПостроительОбъект.Параметры.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Ссылка КАК Номенклатура,
		|	ТаблицаНоменклатуры.ТипНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаНоменклатуры.ТипНоменклатуры.ВидНоменклатуры КАК ТипНоменклатуры,
		|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНоменклатуры,
		|	ЕСТЬNULL(ЕдиницыИзмерения.Ссылка, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))  КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1) КАК Коэффициент,
		|	ЗначенияСвойствОбъектов.Свойство КАК СвойствоНоменклатуры,
		|	ЗначенияСвойствОбъектов.Значение КАК СвойствоНоменклатурыЗначение,
		|	ЗначенияСвойствХарактеристик.Свойство КАК ХарактеристикаСвойство,
		|	ЗначенияСвойствХарактеристик.Значение КАК ХарактеристикаЗначениеСвойства	
		|
		|ПОМЕСТИТЬ ТаблицаТоваров
		|
		|ИЗ
		|	Справочник.Номенклатура КАК ТаблицаНоменклатуры
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ПО 
		|	(ТаблицаНоменклатуры.ТипНоменклатуры.ИспользованиеХарактеристик = 1
		|		И ТаблицаНоменклатуры.ТипНоменклатуры = ХарактеристикиНоменклатуры.Владелец
		|		ИЛИ ТаблицаНоменклатуры.ТипНоменклатуры.ИспользованиеХарактеристик = 2
		|		И ТаблицаНоменклатуры.Ссылка = ХарактеристикиНоменклатуры.Владелец)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
		|ПО
		|	(ТаблицаНоменклатуры.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1
		|	И ТаблицаНоменклатуры.ТипНоменклатуры = ЕдиницыИзмерения.Владелец
		|	И ТаблицаНоменклатуры.Ссылка.ОсновнаяЕдиницаИзмерения.Наименование = ЕдиницыИзмерения.Наименование
		|	ИЛИ ТаблицаНоменклатуры.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2
		|	И ТаблицаНоменклатуры.Ссылка = ЕдиницыИзмерения.Владелец)
		|
		|{ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ПО
		|	ЗначенияСвойствОбъектов.Объект ССЫЛКА Справочник.Номенклатура И 
		|	ТаблицаНоменклатуры.Ссылка = ЗначенияСвойствОбъектов.Объект
		|}
		|{ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствХарактеристик
		|ПО
		|	ЗначенияСвойствХарактеристик.Объект ССЫЛКА Справочник.ХарактеристикиНоменклатуры И
		|	ХарактеристикиНоменклатуры.Ссылка = ЗначенияСвойствХарактеристик.Объект}
		|	
		|{ГДЕ
		|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаНоменклатуры.Ссылка.* КАК Номенклатура,
		|	ЗначенияСвойствОбъектов.Свойство.* КАК Свойство,
		|	ЗначенияСвойствОбъектов.Значение.* КАК ЗначениеСвойства}
		|ГДЕ	
		|	ТаблицаНоменклатуры.ЭтоГруппа = ЛОЖЬ
		|//КОММЕНТАРИЙ НА ОТБОР ПО ИЗМЕНЕНИЯМ
		|		
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ТаблицаНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаНоменклатуры.Коэффициент КАК Коэффициент,
		|	ТаблицаНоменклатуры.СвойствоНоменклатуры КАК СвойствоНоменклатуры,
		|	ТаблицаНоменклатуры.СвойствоНоменклатурыЗначение КАК СвойствоНоменклатурыЗначение,
		|	ТаблицаНоменклатуры.ХарактеристикаСвойство КАК ХарактеристикаСвойство,
		|	ТаблицаНоменклатуры.ХарактеристикаЗначениеСвойства КАК ХарактеристикаЗначениеСвойства,
		|	ШтрихКоды.ЕдиницаИзмерения КАК ЕдиницаДляШтрихКода,
		|	ШтрихКоды.ШтрихКод КАК ШтрихКод,
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК Остаток
		|
		|ПОМЕСТИТЬ ТаблицаОстатков
		|
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаНоменклатуры
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|ПО 
		|	ТаблицаНоменклатуры.Номенклатура = ШтрихКоды.Объект
		|			И (НЕ ШтрихКоды.Запрет)
		|			И (ВЫБОР
		|					КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 1 ТОГДА
		|						(ШтрихКоды.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|						И ШтрихКоды.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|					КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 2 ТОГДА 
		|						ШтрихКоды.ЕдиницаИзмерения = ТаблицаНоменклатуры.ЕдиницаИзмерения
		|					КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 3 ТОГДА 
		|						ШтрихКоды.ХарактеристикаНоменклатуры = ТаблицаНоменклатуры.ХарактеристикаНоменклатуры
		|					ИНАЧЕ
		|						(ШтрихКоды.ЕдиницаИзмерения = ТаблицаНоменклатуры.ЕдиницаИзмерения
		|						И ШтрихКоды.ХарактеристикаНоменклатуры = ТаблицаНоменклатуры.ХарактеристикаНоменклатуры)
		|				КОНЕЦ)
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаОтчета, {СкладКомпании.* КАК СкладКомпании}) КАК ОстаткиТоваровКомпанииОстатки
		| ПО ТаблицаНоменклатуры.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|	И ТаблицаНоменклатуры.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
		//|	И ТаблицаНоменклатуры.ЕдиницаИзмерения = ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|УНИЧТОЖИТЬ ТаблицаТоваров
		|;
		|";
	
	ТекРодитель				= ПодразделениеДляВыборкиТоваров;
	ТекстОбъединенияУровней	= "";
	Уровень					= 0;
		
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		ПостроительОбъект.Параметры.Вставить("Подразделение"+Уровень, ТекРодитель);
		ТекстОбъединенияУровней	= ТекстОбъединенияУровней+"
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	&Подразделение"+Уровень+",
		|	"+Уровень;
		ТекРодитель				= ТекРодитель.Родитель;
		Уровень					= Уровень+1;
	КонецЦикла;
		
	ТекстЗапроса	= ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ТаблицаПодразделений.Родитель КАК Родитель,
		|	ТаблицаПодразделений.Уровень КАК Уровень
		|	ПОМЕСТИТЬ ТаблицаПодразделений
		| ИЗ (
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) КАК Родитель,
		|	"+Уровень+" КАК Уровень
		|"+ТекстОбъединенияУровней+") КАК ТаблицаПодразделений
		|ИНДЕКСИРОВАТЬ ПО Родитель
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаЦен.Номенклатура КАК Номенклатура,
		|	ТаблицаЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаПодразделений.Уровень КАК Уровень,
		|	ТаблицаЦен.ТипЦен КАК ТипЦен,
		|	ТаблицаЦен.Период,
		|	"+?(СпособОпределенияЦен=2,"СРЕДНЕЕ(ТаблицаЦен.Цена)","ТаблицаЦен.Цена")+" КАК Цена
		|
		|ПОМЕСТИТЬ
		|	ТаблицаЦен
		|
		|ИЗ
		|	РегистрСведений.Цены.СрезПоследних(&ДатаОтчета, {ТипЦен.* КАК ТипЦен} Контрагент "+?(СпособОпределенияЦен=2,"<>","=")+" &Контрагент И ПодразделениеКомпании В (ВЫБРАТЬ Родитель ИЗ ТаблицаПодразделений)) КАК ТаблицаЦен
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаПодразделений КАК ТаблицаПодразделений
		|ПО 
		|	ТаблицаЦен.ПодразделениеКомпании = ТаблицаПодразделений.Родитель
		|
		|"+?(СпособОпределенияЦен=2,"СГРУППИРОВАТЬ ПО
		|	ТаблицаЦен.Номенклатура,
		|	ТаблицаЦен.ХарактеристикаНоменклатуры,
		|	ТаблицаЦен.ЕдиницаИзмерения,
		|	ТаблицаПодразделений.Уровень,
		|	ТаблицаЦен.ТипЦен,
		|	ТаблицаЦен.Период","")+"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипЦен,
		|	Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаПодразделений
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаЦен.Номенклатура КАК Номенклатура,
		|	ТаблицаЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаЦен.ТипЦен КАК ТипЦен,
		|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
		|ПОМЕСТИТЬ
		|	СрезПоследних
		|ИЗ
		|	ТаблицаЦен КАК ТаблицаЦен
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЦен.Номенклатура,
		|	ТаблицаЦен.ХарактеристикаНоменклатуры,
		|	ТаблицаЦен.ЕдиницаИзмерения,
		|	ТаблицаЦен.ТипЦен
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипЦен,
		|	Номенклатура
		|;
		|
		|ВЫБРАТЬ
		|	СрезПоследних.Номенклатура КАК Номенклатура,
		|	СрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СрезПоследних.ТипЦен КАК ТипЦен,
		|	ТаблицаЦен.Цена КАК Цена,
		|	ТаблицаЦен.Период КАК Период,
		|	ВЫБОР
		|		КОГДА СрезПоследних.ТипЦен.ВВалютеУчета ТОГДА
		|			СрезПоследних.Номенклатура.ВалютаУчета
		|		ИНАЧЕ
		|			СрезПоследних.ТипЦен.ВалютаЦены
		|	КОНЕЦ КАК Валюта
		|ПОМЕСТИТЬ
		|	ЦеныСрезПоследних
		|ИЗ
		|	СрезПоследних КАК СрезПоследних
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаЦен КАК ТаблицаЦен
		|ПО 
		|	СрезПоследних.ТипЦен = ТаблицаЦен.ТипЦен И
		|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
		|	СрезПоследних.ХарактеристикаНоменклатуры = ТаблицаЦен.ХарактеристикаНоменклатуры И 
		|	СрезПоследних.ЕдиницаИзмерения = ТаблицаЦен.ЕдиницаИзмерения И 
		|	СрезПоследних.Уровень = ТаблицаЦен.Уровень
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипЦен,
		|	Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ ТаблицаЦен
		|;
		|
		|ВЫБРАТЬ
		|	СрезПоследних.ТипЦен КАК ТипЦен,
		|	СрезПоследних.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ
		|	ВспомогательнаяСтруктура
		|ИЗ
		|	СрезПоследних КАК СрезПоследних
		|СГРУППИРОВАТЬ ПО
		|	СрезПоследних.ТипЦен,
		|	СрезПоследних.Номенклатура
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипЦен,
		|	Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	СрезПоследних
		|;
		|
		|ВЫБРАТЬ
		|	ВспомогательнаяСтруктура.ТипЦен КАК ТипЦен,
		|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
		|	ТаблицаОстатков.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаОстатков.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ТаблицаОстатков.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблицаОстатков.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОстатков.Коэффициент КАК Коэффициент,
		|	ТаблицаОстатков.СвойствоНоменклатуры КАК СвойствоНоменклатуры,
		|	ТаблицаОстатков.СвойствоНоменклатурыЗначение КАК СвойствоНоменклатурыЗначение,
		|	ТаблицаОстатков.ХарактеристикаСвойство КАК ХарактеристикаСвойство,
		|	ТаблицаОстатков.ХарактеристикаЗначениеСвойства КАК ХарактеристикаЗначениеСвойства,
		|	ТаблицаОстатков.ЕдиницаДляШтрихКода КАК ЕдиницаДляШтрихКода,
		|	ТаблицаОстатков.ШтрихКод КАК ШтрихКод,
		|	ТаблицаОстатков.Остаток КАК Остаток
		|ПОМЕСТИТЬ
		|	СтруктураНоменклатуры
		|ИЗ
		|	ТаблицаОстатков КАК ТаблицаОстатков
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВспомогательнаяСтруктура КАК ВспомогательнаяСтруктура
		|ПО
		|	ТаблицаОстатков.Номенклатура = ВспомогательнаяСтруктура.Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаОстатков
		|;
		|
		|УНИЧТОЖИТЬ
		|	ВспомогательнаяСтруктура
		|;
		|
		|ВЫБРАТЬ
		|	СтруктураНоменклатуры.Номенклатура КАК НоменклатураСсылка,
		|	СтруктураНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СтруктураНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СтруктураНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаСсылка,
		|	СтруктураНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СтруктураНоменклатуры.СвойствоНоменклатуры КАК СвойствоНоменклатуры,
		|	СтруктураНоменклатуры.СвойствоНоменклатурыЗначение КАК СвойствоНоменклатурыЗначение,
		|	СтруктураНоменклатуры.ХарактеристикаСвойство КАК ХарактеристикаСвойство,
		|	СтруктураНоменклатуры.ХарактеристикаЗначениеСвойства КАК ХарактеристикаЗначениеСвойства,
		|	СтруктураНоменклатуры.ЕдиницаДляШтрихКода КАК ЕдиницаДляШтрихКода,
		|	СтруктураНоменклатуры.ШтрихКод КАК ШтрихКод,
		|	СтруктураНоменклатуры.Остаток КАК Остаток,
		|	СтруктураНоменклатуры.ТипЦен КАК ТипЦен,
		|	СтруктураНоменклатуры.Коэффициент КАК Коэффициент,
		|	ЕСТЬNULL(ЦеныСрезПоследних.Валюта, ОбщиеЦены.Валюта) КАК Валюта,
		|	ЕСТЬNULL(ЦеныСрезПоследних.Период, ОбщиеЦены.Период) КАК ДатаЦены,
		|	СтруктураНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЦены,
		|	ВЫБОР
		|		КОГДА СтруктураНоменклатуры.Коэффициент>1 И СтруктураНоменклатуры.ТипЦен.АлгоритмПолученияЦены = ЗНАЧЕНИЕ(Перечисление.АлгоритмПолученияЦены.ПоЕдиницеИзмерения) ТОГДА
		|			ЕСТЬNULL(ЦеныСрезПоследних.Цена, ОбщиеЦены.Цена*СтруктураНоменклатуры.Коэффициент)
		|		КОГДА СтруктураНоменклатуры.Коэффициент>1 ТОГДА 
		|			ЕСТЬNULL(ЦеныСрезПоследних.Цена, ОбщиеЦены.Цена)*СтруктураНоменклатуры.Коэффициент
		|		ИНАЧЕ
		|			ЕСТЬNULL(ЦеныСрезПоследних.Цена, ОбщиеЦены.Цена)
		|	КОНЕЦ КАК Цена
		|
		|ИЗ	
		|	СтруктураНоменклатуры КАК СтруктураНоменклатуры
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ЦеныСрезПоследних КАК ЦеныСрезПоследних
		|ПО 
		|	СтруктураНоменклатуры.Номенклатура = ЦеныСрезПоследних.Номенклатура
		|	И СтруктураНоменклатуры.ТипЦен = ЦеныСрезПоследних.ТипЦен
		|	И (((НЕ СтруктураНоменклатуры.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|	И СтруктураНоменклатуры.ХарактеристикаНоменклатуры = ЦеныСрезПоследних.ХарактеристикаНоменклатуры)
		|	ИЛИ ((НЕ СтруктураНоменклатуры.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))
		|	И СтруктураНоменклатуры.ЕдиницаИзмерения = ЦеныСрезПоследних.ЕдиницаИзмерения))
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ЦеныСрезПоследних КАК ОбщиеЦены
		|ПО 
		|	ОбщиеЦены.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|	И ОбщиеЦены.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|	И СтруктураНоменклатуры.Номенклатура = ОбщиеЦены.Номенклатура
		|	И СтруктураНоменклатуры.ТипЦен = ОбщиеЦены.ТипЦен
		|
		|ИТОГИ
		|	МАКСИМУМ(ЕдиницаИзмерения),
		|	МАКСИМУМ(ЕдиницаДляШтрихКода),
		|	МАКСИМУМ(ВидНоменклатуры),
		|	МАКСИМУМ(ТипНоменклатуры),
		|	МАКСИМУМ(ШтрихКод),
		|	МАКСИМУМ(ХарактеристикаЗначениеСвойства),
		|	МАКСИМУМ(ТипЦен),
		|	МАКСИМУМ(Валюта),
		|	МАКСИМУМ(Цена),
		|	МАКСИМУМ(ЕдиницаИзмеренияЦены),
		|	МАКСИМУМ(Остаток),
		|	МАКСИМУМ(СвойствоНоменклатурыЗначение)
		|ПО
		|	НоменклатураСсылка,
		|	ХарактеристикаСсылка,
		|	ХарактеристикаСвойство,
		|	СвойствоНоменклатуры";
	
	ПостроительОбъект.Текст = ТекстЗапроса;
	
КонецПроцедуры

Процедура ДобавитьФильтрыВПостроительОтчета(МассивИзмененийНоменклатуры)
	
	ТекстПоиска = "//КОММЕНТАРИЙ НА ОТБОР ПО ИЗМЕНЕНИЯМ";
	ТекстЗамены = "	И (ТаблицаНоменклатуры.Ссылка В (&МассивИзмененийНоменклатуры))"; 
	
	СтрокаТекста = ПостроительЗапроса.Текст;
	СтрокаТекста = СтрЗаменить(СтрокаТекста, ТекстПоиска, ТекстЗамены);
	
	ПостроительЗапроса.Текст = СтрокаТекста;
	ПостроительЗапроса.Параметры.Вставить("МассивИзмененийНоменклатуры", МассивИзмененийНоменклатуры);
	
КонецПроцедуры

Процедура УдалитьФильтрыИзПостроителяОтчета()
	
	ТекстПоиска = "ГДЕ (Товары.НоменклатураСсылка В (&МассивИзмененийНоменклатуры))";
	ТекстЗамены = "ГДЕ Истина "; 
	
	СтрокаТекста = ПостроительЗапроса.Текст;
	СтрокаТекста = СтрЗаменить(СтрокаТекста, ТекстПоиска, ТекстЗамены);
	
	ПостроительЗапроса.Текст = СтрокаТекста;
	ПостроительЗапроса.Параметры.Удалить("МассивИзмененийНоменклатуры");
	
КонецПроцедуры

// Заполнение отбора значениями из таблицы значений.
// Соответствие полей устанавливается по представлению и типам значений
//
// Параметры
//  Отбор  – Отбор           – Отбор, который требуется заполнить значениями
//  ТЗ     – ТаблицаЗначений – Таблица содержит значения для отбора.
//                             Структура колонок повторяет структуру отбора
//
Процедура ЗаполнитьОтборПоТаблицеЗначений(Отбор, ТЗ) Экспорт

	Для Каждого ЭлементОтбора Из ТЗ Цикл
	
		СтрокаТаблицы = Неопределено;
		Для каждого стр Из Отбор Цикл
			
			Если ( стр.Представление = ЭлементОтбора.Представление И стр.ТипЗначения = ЭлементОтбора.ТипЗначения ) Тогда
				
				Если (ПустаяСтрока(стр.Представление) И ПустаяСтрока(стр.ПутьКданным)) Тогда
					Продолжить;			
				КонецЕсли;
				СтрокаТаблицы = стр;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если (ПустаяСтрока(ЭлементОтбора.ПутьКданным)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если (СтрокаТаблицы = Неопределено) Тогда
			
			СтрокаТаблицы = Отбор.Добавить(ЭлементОтбора.ПутьКданным, ЭлементОтбора.Имя, ЭлементОтбора.Представление);		
					
		КонецЕсли;
		
		СтрокаТаблицы.ВидСравнения	= ЭлементОтбора.ВидСравнения;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ЭлементОтбора, "Значение, ЗначениеПо, ЗначениеС, Использование");
			
	КонецЦикла;

КонецПроцедуры // УстановитьОтборИзТаблицы(Отбор, ТЗ)

// Заполняет отбор построителя отчета
Процедура ЗаполнитьОтборПостроителя(ПостроительОбъект) Экспорт
	
	ОтборКоличество = ПостроительОбъект.Отбор.Количество();
	Для Н = 1 По ОтборКоличество Цикл
    	ПостроительОбъект.Отбор.Удалить(ОтборКоличество - Н);
	КонецЦикла;	
	
	ПостроительОбъект.Отбор.Добавить("ВидНоменклатуры", , "Вид номенклатуры");
	ПостроительОбъект.ДоступныеПоля.ВидНоменклатуры.Представление	= "Вид номенклатуры";
	ПостроительОбъект.Отбор.ВидНоменклатуры.ВидСравнения	= ВидСравнения.ВСписке;
	ПостроительОбъект.Отбор.ВидНоменклатуры.Использование	= Истина;
	
	Если НЕ ЗначениеЗаполнено(ПостроительОбъект.Отбор.ВидНоменклатуры.Значение) Тогда
		СписокВидовНоменклатуры	= Новый СписокЗначений();
		Для Индекс=0 По Перечисления.ВидыНоменклатуры.Количество()-1 Цикл
			СписокВидовНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Получить(Индекс));	
		КонецЦикла;
		ПостроительОбъект.Отбор.ВидНоменклатуры.Значение		= СписокВидовНоменклатуры;
	КонецЕсли;
	
	ПостроительОбъект.Отбор.Добавить("Номенклатура", , "Номенклатура");
	ПостроительОбъект.ДоступныеПоля.Номенклатура.Представление = "Номенклатура";
	ПостроительОбъект.Отбор.Добавить("ТипЦен", , "Тип цен");
	ПостроительОбъект.ДоступныеПоля.ТипЦен.Представление = "Тип цен";
	ПостроительОбъект.Отбор.Добавить("Свойство", , "Свойство");
	ПостроительОбъект.ДоступныеПоля.Свойство.Представление = "Свойство";
	ПостроительОбъект.Отбор.Добавить("ЗначениеСвойства", , "Значение свойства");
	ПостроительОбъект.ДоступныеПоля.Свойство.Представление = "Значение свойства";	
	ПостроительОбъект.Отбор.Добавить("СкладКомпании", , "Склад");
	ПостроительОбъект.ДоступныеПоля.СкладКомпании.Представление = "Склад";
	
КонецПроцедуры

//Функция возвращает стоимость доставки по заказу покупателя
Функция ПолучитьСтоимостьДоставкиЗаказа(Документ)
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда Возврат 0; КонецЕсли;
	
	ЗапросСвойства	= Новый Запрос();
	ЗапросСвойства.УстановитьПараметр("Назначение" , ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Документ_ЗаказПокупателя);
	ЗапросСвойства.УстановитьПараметр("ИмяСвойства", "Стоимость доставки");
	
	ЗапросСвойства.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НазначенияСвойствОбъектовВидыСвойств.Свойство.Ссылка КАК СвойствоСсылка
		|ИЗ
		|	ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойствОбъектовВидыСвойств
		|ГДЕ
		|	НазначенияСвойствОбъектовВидыСвойств.Ссылка = &Назначение
		|	И НазначенияСвойствОбъектовВидыСвойств.Свойство.Наименование = &ИмяСвойства";
		
	ВыборкаСвойства = ЗапросСвойства.Выполнить().Выбрать();
	Если ( ВыборкаСвойства.Следующий() ) Тогда
		СвойствоСтоимостьДоставки	= ВыборкаСвойства.СвойствоСсылка;
	Иначе
		СвойствоСтоимостьДоставки	= "Стоимость доставки";	
	КонецЕсли;
	
	ЗапросСтоимости			= Новый Запрос();
	ЗапросСтоимости.Текст	= "ВЫБРАТЬ
	                     	  |	СтоимостьДоставки.Объект КАК Объект,
	                     	  |	СтоимостьДоставки.Свойство КАК Свойство,
	                     	  |	СтоимостьДоставки.Значение КАК СтоимостьДоставки
	                     	  |ИЗ
	                     	  |	РегистрСведений.ЗначенияСвойствОбъектов КАК СтоимостьДоставки
	                     	  |ГДЕ
	                     	  |	СтоимостьДоставки.Объект ССЫЛКА Документ.ЗаказПокупателя
	                     	  |	И СтоимостьДоставки.Объект = &Ссылка
	                     	  |	И СтоимостьДоставки.Свойство = &СвойствоСтоимостьДоставки";
	ЗапросСтоимости.УстановитьПараметр("СвойствоСтоимостьДоставки",	СвойствоСтоимостьДоставки);
	ЗапросСтоимости.УстановитьПараметр("Ссылка",					Документ);
	
	Выборка	= ЗапросСтоимости.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтоимостьДоставки;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

// Формирование полной копии отбора
//
// Параметры
//  Отбор  – Отбор – Исходный отбор, копию которого необходимо получить
//
// Возвращаемое значение:
//   ТаблицаЗначений   – Копия исходного отбора
//
Функция ПолучитьКопиюОтбораВТЗ(Отбор) Экспорт

	Копия = Новый ТаблицаЗначений();
	Копия.Колонки.Добавить("ВидСравнения");
	Копия.Колонки.Добавить("Значение");
	Копия.Колонки.Добавить("ЗначениеПо");
	Копия.Колонки.Добавить("ЗначениеС");
	Копия.Колонки.Добавить("Имя");
	Копия.Колонки.Добавить("Использование");
	Копия.Колонки.Добавить("Представление");
	Копия.Колонки.Добавить("ПутьКДанным");
	Копия.Колонки.Добавить("ТипЗначения");
	
	Для каждого ЭлементОтбора Из Отбор Цикл
		ЗаполнитьЗначенияСвойств(Копия.Добавить(), ЭлементОтбора);	
	КонецЦикла;
	
	Возврат Копия;

КонецФункции // ПолучитьКопиюОтбора(Отбор)

// Возвращает заказы с оплатой и отгрузкой по категориям
Функция ПолучитьЗаказыСОплатойИОтгрузкойПоКатегориям(МассивИзменений, МассивЗагруженныхДокументов) Экспорт
	
	Запрос = Новый Запрос();
	КоличествоЗаказовОграничения = МассивИзменений.Количество();
		
	СтрокаОграниченияПоЗаказам = ?(КоличествоЗаказовОграничения = 0, "", "И ЗаказыПокупателей.Заказ В (&МассивДокументовСсылок) ");	
	СтрокаОграниченияПоРасчетам = ?(КоличествоЗаказовОграничения = 0, "", "И ВзаиморасчетыКомпании.Сделка В (&МассивДокументовСсылок) ");
		
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Заказ.Ссылка КАК ДокументСсылка,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЗаказыПокупателей.РеализацияТоваров.Дата) ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Отгружен,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(РасчетыСКонтрагентами.РеализацияТоваров.Дата) ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Оплачен,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЗаказыПокупателей.РеализацияТоваров.Дата) ЕСТЬ NULL 
		|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|		ИНАЧЕ МАКСИМУМ(ЗаказыПокупателей.РеализацияТоваров.Дата)
		|	КОНЕЦ КАК ДатаОтгрузки,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЗаказыПокупателей.РеализацияТоваров.Номер) ЕСТЬ NULL 
		|			ТОГДА 0
		|		ИНАЧЕ МАКСИМУМ(ЗаказыПокупателей.РеализацияТоваров.Номер)
		|	КОНЕЦ КАК НомерОтгрузки,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(РасчетыСКонтрагентами.РеализацияТоваров.Дата) ЕСТЬ NULL 
		|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|		ИНАЧЕ МАКСИМУМ(РасчетыСКонтрагентами.РеализацияТоваров.Дата)
		|	КОНЕЦ КАК ДатаОплаты,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(РасчетыСКонтрагентами.РеализацияТоваров.Номер) ЕСТЬ NULL 
		|			ТОГДА 0
		|		ИНАЧЕ МАКСИМУМ(РасчетыСКонтрагентами.РеализацияТоваров.Номер)
		|	КОНЕЦ КАК НомерОплаты
		|ИЗ
		|	Документ.ЗаказПокупателя КАК Заказ
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ЗаказыПокупателей.Заказ КАК ЗаказПокупателя,
		|			ЗаказыПокупателей.Регистратор КАК РеализацияТоваров,
		|			ЗаказыПокупателей.Сумма КАК Сумма
		|		ИЗ
		|			РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|		ГДЕ
		|			ЗаказыПокупателей.Регистратор ССЫЛКА Документ.РеализацияТоваров
		|			И ЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)"+СтрокаОграниченияПоЗаказам+") КАК ЗаказыПокупателей
		|		ПО Заказ.Ссылка = ЗаказыПокупателей.ЗаказПокупателя.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВзаиморасчетыКомпании.Сделка КАК Сделка,
		|			ВзаиморасчетыКомпании.Регистратор КАК РеализацияТоваров,
		|			ВзаиморасчетыКомпании.Сумма КАК Сумма
		|		ИЗ
		|			РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
		|		ГДЕ
		|			ВзаиморасчетыКомпании.Сделка ССЫЛКА Документ.ЗаказПокупателя
		|			И ВзаиморасчетыКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) "+СтрокаОграниченияПоРасчетам+") КАК РасчетыСКонтрагентами
		|		ПО Заказ.Ссылка = РасчетыСКонтрагентами.Сделка.Ссылка
		|ГДЕ
		|	Заказ.GUIDСайта <> """"
		|";
		
	Если ( КоличествоЗаказовОграничения > 0 ) Тогда
		Запрос.УстановитьПараметр("МассивДокументовСсылок", МассивИзменений);
		Запрос.Текст = Запрос.Текст + " И Заказ.Ссылка В (&МассивДокументовСсылок)";
	ИначеЕсли ( МассивЗагруженныхДокументов.Количество() > 0 ) Тогда
		Запрос.УстановитьПараметр("МассивДокументовСсылокНеВыгружать", МассивЗагруженныхДокументов);
		Запрос.Текст = Запрос.Текст + " И НЕ Заказ.Ссылка В (&МассивДокументовСсылокНеВыгружать)";
	КонецЕсли;	
		
	Запрос.Текст = Запрос.Текст + "
			|СГРУППИРОВАТЬ ПО
			|	Заказ.Ссылка,
			|	ЗаказыПокупателей.Сумма,
			|	РасчетыСКонтрагентами.Сумма";
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДокументов;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБМЕНА ПО HTTP

Функция HTTPЗагрузитьССервера(СтруктураПараметровСайта, ТипСоединения, КоличествоОбработанныхДокументов)
	
	Успешно								= Истина;
	ОтветСервера						= "";
	Соединение							= Неопределено;
	КоличествоОбработанныхДокументов	= 0;
	
	АдресДляРаботы	= СтруктураПараметровСайта.АдресСкрипта + "?type="+ТипСоединения;
	
	СтрокаСообщенияПользователю			= "";
	Успешно	= HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	Если (НЕ ПустаяСтрока(СтрокаСообщенияПользователю)) Тогда
		СообщитьОбОшибкеОбмена(СтрокаСообщенияПользователю, Ложь);
	КонецЕсли;
	
	Если (НЕ Успешно) Тогда Возврат Ложь; КонецЕсли;
			
	КукиИмя      	  = СтрПолучитьСтроку(ОтветСервера,2);
	КукиЗначение 	  = СтрПолучитьСтроку(ОтветСервера,3);
	ЗаголовкиЗапросов = Новый Соответствие;
	ЗаголовкиЗапросов.Вставить("Cookie", КукиИмя + "=" + КукиЗначение);

	ОтобразитьСостояние("Загрузка данных с сервера...");
	
	ИнформацияДляПользователя	= "";
	ОтветСервера				= HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_ПолучитьДанные, ЗаголовкиЗапросов, ИнформацияДляПользователя);
	Если (НЕ ПустаяСтрока(ИнформацияДляПользователя)) Тогда
		СообщитьПользователю("Данные, полученные от сервера:"+Символы.ПС+ИнформацияДляПользователя, Ложь,,1);
	КонецЕсли;
	
	Если (ОтветСервера = Неопределено) Тогда
		СообщитьОбОшибкеОбмена("Не удалось загрузить данные с сервера.", Ложь);
		Возврат Ложь;
	КонецЕсли;
		
	СтрокаCML					= "";
	
	Если (Лев(ОтветСервера, 2) = "PK") Тогда
		СтрокаCML	= РаспаковатьZIPАрхив(ОтветСервера);
	Иначе
		Если (Лев(ОтветСервера, 5) = "<?xml") Тогда
			СтрокаCML = ОтветСервера;
		КонецЕсли;	
	КонецЕсли;						
	
	Если (обЗначениеНеЗаполнено(СтрокаCML)) Тогда
		СообщитьОбОшибкеОбмена("Не удалось прочитать данные, загруженные с сервера.", Ложь);
		Возврат Ложь;		
	КонецЕсли;
		
	ДеревоДокументов	= РазобратьCML(СтрокаCML);
	
	НоваяСтрока				= ТаблицаФайловОбмена.Добавить();
	НоваяСтрока.Дата		= ТекущаяДата();
	НоваяСтрока.Режим		= "Загрузка";
	НоваяСтрока.Содержание	= СтрокаCML;
	
	Если (ДеревоДокументов = Неопределено) Тогда
		СообщитьОбОшибкеОбмена("Не удалось разобрать данные, загруженные с сервера.", Ложь);
		Возврат Ложь;		
	КонецЕсли;
		
	Успешно = ОбработатьДокументы(ДеревоДокументов, КоличествоОбработанныхДокументов);
	
	Если (НЕ Успешно) Тогда
		СообщитьОбОшибкеОбмена("Не удалось обработать документы, загруженные с сервера.", Ложь);
		Возврат Ложь;
	КонецЕсли;
	
	ИнформацияДляПользователя	= "";
	HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы+ПараметрЗапросаHTTP_УспешноеЗавершениеИмпорта, ЗаголовкиЗапросов, ИнформацияДляПользователя);
	Если (НЕ ПустаяСтрока(ИнформацияДляПользователя)) Тогда
		СообщитьПользователю("Данные, полученные от сервера:"+Символы.ПС+ИнформацияДляПользователя, Ложь,,1);
	КонецЕсли;
					
	Возврат Успешно;
	
КонецФункции

Функция HTTPВыгрузитьНаСервер(СтруктураПараметровСайта,	КаталогОбмена, МассивПодкаталогов = Неопределено, ОжидатьЗавершенияИмпортаФайловСервером = Ложь, ТипСоединения = "catalog")
	
	Успешно			= Ложь;
	ОтветСервера	= "";
	Соединение		= Неопределено;
	
	АдресДляРаботы = СтруктураПараметровСайта.АдресСкрипта + "?type=" + ТипСоединения; 
		
	СтрокаСообщенияПользователю = "";
	Успешно = HTTPВыполнитьАвторизациюДляСоединения(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю, ТипСоединения);
	Если (НЕ ПустаяСтрока(СтрокаСообщенияПользователю)) Тогда
		СообщитьОбОшибкеОбмена(СтрокаСообщенияПользователю, Истина);
	КонецЕсли;
	
	Если (НЕ Успешно) Тогда Возврат Ложь; КонецЕсли;	
		
	КукиИмя      	 	= СтрПолучитьСтроку(ОтветСервера, 2);
	КукиЗначение 	 	= СтрПолучитьСтроку(ОтветСервера, 3);
	ЗаголовкиЗапросов = Новый Соответствие;
	ЗаголовкиЗапросов.Вставить("Cookie", КукиИмя + "=" + КукиЗначение);

	ОтобразитьСостояние("Запрос параметров обмена...");
	
	ОтветСервера		= HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_Инициализация, ЗаголовкиЗапросов);
	
	Если (ОтветСервера	= Неопределено) Тогда 
		СообщитьОбОшибкеОбмена("Не удалось получить параметры обмена с сервера.", Истина);
		Возврат Ложь;
	КонецЕсли;
		
	ZIPФайлыРазрешены						= Ложь;
	ОграничениеРазмераФрагментаФайлаОбмена	= 0;
					
	Если (СтрЧислоСтрок(ОтветСервера) <> 2) Тогда	
		СообщитьОбОшибкеОбмена("Не удалось прочитать ответ сервера. Параметры обмена не получены.", Истина);
		Возврат Ложь;
	КонецЕсли;
		
	ZIPФайлыРазрешены	= НРег(СтрПолучитьСтроку(ОтветСервера,1))	= ОтветСервера_ZIPРазрешен;

	Попытка 
		ОграничениеРазмераФрагментаФайлаОбмена = Число(СтрЗаменить(НРег(СтрПолучитьСтроку(ОтветСервера,2)), ОтветСервера_ОграничениеРазмераФрагментаФайлаОбмена, ""));
	Исключение
		СообщитьОбИсключительнойОшибке(Истина);
		ОграничениеРазмераФрагментаФайлаОбмена	= -1;
	КонецПопытки;	
			
	МассивИсходныхCMLФайлов	= НайтиФайлы(КаталогОбмена, "*.xml");
	СписокФайловДляОтправки	= ПолучитьСписокФайловДляОтправки(КаталогОбмена, МассивПодкаталогов);
	
	Для Каждого ТекФайл Из СписокФайловДляОтправки Цикл
		ВспомогательныйФайл		= Новый Файл(ТекФайл.Значение);
		Если (Не Булево(Найти(ТекФайл.Значение,".xml"))) Тогда Продолжить; КонецЕсли;
		Если (ВспомогательныйФайл.Существует()) Тогда
			СодержаниеФайла 	= Новый ТекстовыйДокумент();
			СодержаниеФайла.Прочитать(ТекФайл.Значение);
			Если (СодержаниеФайла.КоличествоСтрок()>0) Тогда
				НоваяСтрока	= ТаблицаФайловОбмена.Добавить();
				НоваяСтрока.Дата		= ТекущаяДата();
				НоваяСтрока.Режим		= "Выгрузка";
				НоваяСтрока.Содержание	= СодержаниеФайла.ПолучитьТекст();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если (ZIPФайлыРазрешены) Тогда
		ОтобразитьСостояние("Подготовка ZIP-архива...");
		СписокФайловДляОтправки	= ПодготовитьZIPАрхивы(СписокФайловДляОтправки, КаталогОбмена);
	КонецЕсли;	
	
	Если (ОграничениеРазмераФрагментаФайлаОбмена > 0) Тогда
		СписокФайловДляОтправки = РазделитьФайлыНаФрагменты(СписокФайловДляОтправки, ОграничениеРазмераФрагментаФайлаОбмена);
	КонецЕсли;	
	
	ВсегоФайлов					= СписокФайловДляОтправки.Количество();
	
	Для Каждого ТекФайл Из СписокФайловДляОтправки Цикл
		ОтобразитьСостояние("Идет отправка файла на сервер (" + Строка(СписокФайловДляОтправки.Индекс(ТекФайл) + 1) + " из " + Строка(ВсегоФайлов) + "): " + ТекФайл.Значение);
		ОтветСервера = HTTPОтправитьФайлНаСервер(ТекФайл.Значение, Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_ПередачаФайла + ТекФайл.Представление, ЗаголовкиЗапросов);
		
		Если (ZIPФайлыРазрешены) Тогда
			Попытка
				УдалитьФайлы(ТекФайл.Значение);
			Исключение
			КонецПопытки;	
		КонецЕсли;	
		
		Если (ОтветСервера = Неопределено) Тогда
			СообщитьОбОшибкеОбмена("Не удалось получить ответ сервера. Файл не отправлен (" + ТекФайл.Значение + ").", Истина);
			Возврат Ложь;
		КонецЕсли;
			
		СостояниеОбмена = НРег(СтрПолучитьСтроку(ОтветСервера,1));
		
		Если (СостояниеОбмена = ОтветСервера_АварийноеЗавершениеТекущейОперации) Тогда
			СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера. Файл не отправлен (" + ТекФайл.Значение + ").", Истина);
			СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
			Возврат Ложь;
			
		ИначеЕсли (СостояниеОбмена = ОтветСервера_УспешноеЗавершениеТекущейОперации) Тогда		
			Если (СтрЧислоСтрок(ОтветСервера)>1) Тогда
				СообщитьПользователю("Получен расширенный статус успешного завершения сеанса", Истина, СтатусСообщения.Информация,1);
				СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
			КонецЕсли;
			
		Иначе
			СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера. Не получен статус завершения операции. Файл не отправлен (" + ТекФайл.Значение + ").", Истина);
			//СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
			Возврат Ложь;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	ИмпортУспешноЗавершен					= Ложь;
	
	Если (ОжидатьЗавершенияИмпортаФайловСервером) Тогда
							
		Для Каждого ТекФайл Из МассивИсходныхCMLФайлов Цикл
			ИмпортПродолжается		= Истина;
			ТекущееСостояние		= "";
		
			Пока ( ИмпортПродолжается ) Цикл			
				ИмпортПродолжается = Ложь;
				ОтобразитьСостояние("Ожидание окончания загрузки данных сервером: " + ТекущееСостояние);
				ТекущееСостояние	= "";
				
				ОтветСервера		= HTTPПолучитьДанныеССервера(Соединение, АдресДляРаботы + ПараметрЗапросаHTTP_ИмпортФайлаСервером + ТекФайл.Имя, ЗаголовкиЗапросов);
				
				Если (ОтветСервера = Неопределено) Тогда 
					
					Успешно			= Ложь;
					СообщитьОбОшибкеОбмена("Не удалось получить текущее состояние процесса обмена. Данные обмена отправлены, но не загружены.", Истина);
					
				ИначеЕсли СтрЧислоСтрок(ОтветСервера) = 0 Тогда
					Успешно			= Ложь;
					СообщитьПользователю("Не удалось прочитать данные о текущем состоянии процесса обмена. Данные обмена отправлены, но не загружены.", Истина,,1);	
				Иначе
					
					СостояниеОбмена	= НРег(СтрПолучитьСтроку(ОтветСервера,1));
					Если (СостояниеОбмена = ОтветСервера_АварийноеЗавершениеТекущейОперации) Тогда
						Успешно = Ложь;
						СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера.", Истина);
						СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
					ИначеЕсли (СостояниеОбмена = ОтветСервера_УспешноеЗавершениеТекущейОперации) Тогда
						ИмпортУспешноЗавершен	= Истина;
					ИначеЕсли (СостояниеОбмена = ОтветСервера_ВыполнениеТекущейОперации) Тогда
						ТекущееСостояние		= ТекущееСостояние + СтрПолучитьСтроку(ОтветСервера, 2);
						ИмпортПродолжается		= Истина;
					Иначе
						Успешно					= Ложь;
						СообщитьОбОшибкеОбмена("Произошла ошибка на стороне сервера. Получен неизвестный статус импорта.", Истина);
						СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера);
					КонецЕсли;
					
				КонецЕсли;					
				
			КонецЦикла;	//Импорт
			
			Если (НЕ ИмпортУспешноЗавершен) Тогда Прервать; КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;								
	
	Возврат Успешно;
	
КонецФункции

Функция HTTPОтправитьФайлНаСервер(ПолноеИмяФайла, Соединение, ПараметрыЗапроса="", Заголовки="")
	
	ОтветСервера		= Неопределено;
	ИмяФайлаОтвета		= ПолучитьИмяВременногоФайла();
	
	Если Заголовки="" Тогда
		Заголовки=Новый Соответствие;
	КонецЕсли; 
	
	Попытка
		Соединение.ОтправитьДляОбработки(ПолноеИмяФайла, СокрЛП(ПараметрыЗапроса), ИмяФайлаОтвета, Заголовки);
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, ОписаниеОшибки())
	КонецПопытки;	
	
	ФайлОтвета			= Новый Файл(ИмяФайлаОтвета);
	
	Если (ФайлОтвета.Существует()) Тогда
		ТекстОтвета 	= Новый ТекстовыйДокумент();
		ТекстОтвета.Прочитать(ИмяФайлаОтвета);
		Если (ТекстОтвета.КоличествоСтрок()>0) Тогда
			ОтветСервера = ТекстОтвета.ПолучитьТекст();	
		Иначе
			СообщитьПользователю("Отправка файла на сервер: Получен пустой ответ сервера.", Истина,,1); 	
		КонецЕсли;		
	Иначе	
		СообщитьПользователю("Отправка файла на сервер: Ответ сервера не получен.", Истина,,1); 
	КонецЕсли;	
	
	Попытка
		УдалитьФайлы(КаталогВременныхФайлов(), ИмяФайлаОтвета);
	Исключение
	КонецПопытки;
	
	Возврат ОтветСервера;	
	
КонецФункции

Функция ПолучитьСписокФайловДляОтправки(КаталогОбмена, МассивПодкаталогов);
	
	СписокФайлов		= Новый СписокЗначений();
	Маска				= "*.*";
	
	ВсеФайлыДляВыгрузки = НайтиФайлы(КаталогОбмена, Маска);
	
	Если ( МассивПодкаталогов <> Неопределено ) Тогда
		
		Для Каждого Подкаталог Из МассивПодкаталогов Цикл
			
			ФайлыВПодкаталоге	= НайтиФайлы(КаталогОбмена + "\" + Подкаталог, Маска);
			Для Каждого ТекФайл Из ФайлыВПодкаталоге Цикл		
				Если (ТекФайл.ЭтоКаталог()) Тогда	
					ФайлыВДобавочномПодкаталоге	= НайтиФайлы(ТекФайл.ПолноеИмя, Маска);
					Для Каждого ТекФайлВПодкаталоге Из ФайлыВДобавочномПодкаталоге Цикл
						Если (НЕ ТекФайлВПодкаталоге.ЭтоКаталог()) Тогда	
							ВсеФайлыДляВыгрузки.Добавить(ТекФайлВПодкаталоге);  								
						КонецЕсли;		
					КонецЦикла;				
				Иначе	
					ВсеФайлыДляВыгрузки.Добавить(ТекФайл);  
				КонецЕсли;			
			КонецЦикла;	
			
		КонецЦикла;			
				
	КонецЕсли;
	
	Для Каждого ТекФайл Из ВсеФайлыДляВыгрузки цикл
		
		Если (НЕ ТекФайл.ЭтоКаталог()) Тогда
			ПолноеИмяФайлаДляСервера = ПодготовитьИмяФайлаДляСервера(ТекФайл, КаталогОбмена);
			СписокФайлов.Добавить(ТекФайл.ПолноеИмя, ПолноеИмяФайлаДляСервера);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат СписокФайлов;
	
КонецФункции

Функция ПодготовитьИмяФайлаДляСервера(ФайлОбъект, КаталогОбмена)
	
	ПолноеИмяФайлаДляСервера = "";	
	Если (Булево(Найти(ФайлОбъект.Имя, ".xml"))) Тогда
		ПолноеИмяФайлаДляСервера	= ФайлОбъект.Имя;
	Иначе	
		//у картинки надо оставить 2 папки и развернуть слэши
		ПолноеИмяФайлаДляСервера	= ФайлОбъект.ПолноеИмя;
		ПутьДляУдаления				= КаталогОбмена + "\";
		ПолноеИмяФайлаДляСервера	= СтрЗаменить(ПолноеИмяФайлаДляСервера, ПутьДляУдаления, "");
		ПолноеИмяФайлаДляСервера	= СтрЗаменить(ПолноеИмяФайлаДляСервера, "\", "/");
	КонецЕсли;	
	ПолноеИмяФайлаДляСервера = УдалитьДополнительныеРасширенияФайла(ПолноеИмяФайлаДляСервера);
	
	Возврат ПолноеИмяФайлаДляСервера;
	
КонецФункции

Функция УдалитьДополнительныеРасширенияФайла(ИсходноеИмяФайла)

	ПозицияТочки 			  = Найти(ИсходноеИмяФайла, ".");
	ИмяФайла    			  = Лев(ИсходноеИмяФайла, ПозицияТочки - 1);
	ПраваяЧастьИсходногоИмени = Прав(ИсходноеИмяФайла, СтрДлина(ИсходноеИмяФайла) - ПозицияТочки);
	ПозицияТочки 			  = Найти(ПраваяЧастьИсходногоИмени, ".");
	Расширение 				  = ПраваяЧастьИсходногоИмени;
	Если (ПозицияТочки > 0) Тогда
		Расширение = Лев(ПраваяЧастьИсходногоИмени, ПозицияТочки - 1);	
	КонецЕсли;	
	Возврат ИмяФайла + "." + Расширение;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ ФАЙЛОВ

Функция РаспаковатьZIPАрхив(СтрокаZIP)
	
	СтрокаСодержимого	= "ZIP_ERROR";
	
	ИмяФайла			= ПолучитьИмяВременногоФайла("zip");
	ИмяКаталога			= КаталогВременныхФайлов()+Строка(Новый УникальныйИдентификатор);
	СоздатьКаталог(ИмяКаталога);
	
	СтрокаВФайл			= Новый ТекстовыйДокумент();
	СтрокаВФайл.УстановитьТекст(СтрокаZIP);
	Попытка
		СтрокаВФайл.Записать(ИмяФайла);
	Исключение
	КонецПопытки;
	
	ЧтениеZIP			= Новый ЧтениеZIPФайла(ИмяФайла);
	ЧтениеZIP.ИзвлечьВсе(ИмяКаталога);
	ЧтениеZIP.Закрыть();
	
	РаспакованныеФайлы	= НайтиФайлы(ИмяКаталога, "*.xml");
	
	Если (РаспакованныеФайлы.Количество() = 1) Тогда
		СтрокаИзФайла 	  = Новый ТекстовыйДокумент();
		СтрокаИзФайла.Прочитать(РаспакованныеФайлы[0].ПолноеИмя);
		СтрокаСодержимого = СтрокаИзФайла.ПолучитьТекст();
	КонецЕсли;
	
	Попытка
		УдалитьФайлы(ИмяФайла);
		УдалитьФайлы(ИмяКаталога);
	Исключение
	КонецПопытки;
	
	Возврат СтрокаСодержимого;
	
КонецФункции

Функция РазделитьФайлыНаФрагменты(СписокФайлов, ОграничениеРазмераФрагмента)
	
	НовыйСписокФайлов = Новый СписокЗначений();
	Для Каждого ТекФайл Из СписокФайлов цикл
		
		ФайлНаДиске	= Новый Файл(ТекФайл.Значение);
		Если (ФайлНаДиске.Размер() > ОграничениеРазмераФрагмента) Тогда
			МассивФрагментов = РазделитьФайл(ФайлНаДиске.ПолноеИмя, ОграничениеРазмераФрагмента);
			Для Каждого НовыйФайл Из МассивФрагментов Цикл
				НовыйСписокФайлов.Добавить(НовыйФайл, ТекФайл.Представление);
			КонецЦикла;	
			УдалитьФайлы(ФайлНаДиске.ПолноеИмя);
		Иначе
			НовыйСписокФайлов.Добавить(ТекФайл.Значение, ТекФайл.Представление);	
		КонецЕсли;	
		
	КонецЦикла;
	Возврат НовыйСписокФайлов;
	
КонецФункции

Функция ПодготовитьZIPАрхивы(СписокФайлов, КаталогОбмена)
	
	ПолноеИмяФайлаАрхива = ПолучитьИмяВременногоФайла("zip");
	ЗаписьАрхива   	     = Новый ЗаписьZipФайла(ПолноеИмяФайлаАрхива);
	
	СоздатьКаталог(КаталогОбмена);
	
	ЗаписьАрхива.Добавить(КаталогОбмена + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ЗаписьАрхива.Записать();
	
	НовыйСписокФайлов = Новый СписокЗначений();
	НовыйСписокФайлов.Добавить(ПолноеИмяФайлаАрхива, ПолучитьИмяФайла(ПолноеИмяФайлаАрхива));
	
	Возврат НовыйСписокФайлов;
	
КонецФункции

Функция ПолучитьИмяФайла(ПолноеИмяФайла)
	
	ИмяФайла = "";
	ПромТекст = СтрЗаменить(ПолноеИмяФайла, "/", "\");
	ПромТекст = СтрЗаменить(ПолноеИмяФайла, "\", Символы.ПС);	
	КоличествоЭлементов = СтрЧислоСтрок(ПромТекст);
	Для Счетчик = 1 По КоличествоЭлементов Цикл
		ИмяФайла = СтрПолучитьСтроку(ПромТекст, Счетчик);
	КонецЦикла;
	Возврат ИмяФайла;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ АНАЛИЗА ИЗМЕНЕНИЙ ДАННЫХ

Процедура ЗаполнитьСтруктуруИзмененийДляУзла(УзелПланаОбмена, СтруктураВозврата)
	
	Если ( обЗначениеНеЗаполнено(УзелПланаОбмена) ) Тогда Возврат; КонецЕсли;
	
	ЗаписьXML		= Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	
	ЭтотУзелСсылка	= ПланыОбмена.ОбменССайтом.ЭтотУзел();	
	ЗаписьСообщения	= ПланыОбмена.СоздатьЗаписьСообщения();
	Если УзелПланаОбмена=ЭтотУзелСсылка Тогда
		СообщитьПользователю("Выбранная настройка обмена должна быть заполнена в другом узле плана обмена ""Обмен с сайтом"".
				|Настройка плана обмена, в адрес которой будет послано сообщение обмена данными, не должна быть пустой и должна не быть ссылкой на ""этот узел""", Истина, СтатусСообщения.Информация, 1);
		Возврат;
	Иначе
		Попытка
			ЗаписьСообщения.НачатьЗапись(ЗаписьXML, УзелПланаОбмена);
		Исключение
			СообщитьПользователю("Некорректно заполнен узел плана обмена ""Обмен с сайтом"""+Символы.ПС+ИнформацияОбОшибке().Описание, Истина, СтатусСообщения.Информация, 1);
			Возврат;
		КонецПопытки;		
	КонецЕсли;

	СтруктураВозврата.НомерСообщенияТовары = ЗаписьСообщения.НомерСообщения;
	СтруктураВозврата.НомерСообщенияЗаказы = ЗаписьСообщения.НомерСообщения;
	
	// Получение Изменений
	ОтобразитьСостояние("Выбор изменений ...");
	Счетчик = 0;

	Выборка = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения); 
	
	Пока Выборка.Следующий() Цикл
		
		Счетчик = Счетчик + 1;
		Если Счетчик % 100 = 0 Тогда
			ОтобразитьСостояние("Обработано объектов: " + Счетчик);
		КонецЕсли;
		
		Данные 	  = Выборка.Получить();
		ТипДанных = ТипЗнч(Данные);              
		                                         
		Если ( ТипДанных = Тип("СправочникОбъект.Номенклатура") ) Тогда		
			СтруктураВозврата.Товары.Добавить(Данные.Ссылка);
			
		ИначеЕсли ( ТипДанных = Тип("ДокументОбъект.ЗаказПокупателя") ) Тогда		
			СтруктураВозврата.Заказы.Добавить(Данные.Ссылка);	
			
		ИначеЕсли ( ТипДанных = Тип("РегистрСведенийНаборЗаписей.КартинкиИФайлы") ) Тогда	
			НаборЗаписейКартинкиИФайлы			= РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей();
			НаборЗаписейКартинкиИФайлы.Отбор.Объект.Установить(Данные.Отбор.Объект.Значение);
			НаборЗаписейКартинкиИФайлы.Отбор.Идентификатор.Установить(Данные.Отбор.Идентификатор.Значение);
			НаборЗаписейКартинкиИФайлы.Прочитать();
			Если (НаборЗаписейКартинкиИФайлы.Количество()>0) Тогда
				СтруктураВозврата.Картинки.Добавить(НаборЗаписейКартинкиИФайлы[0].Объект);	
			КонецЕсли;
			
		ИначеЕСли (ТипЗнч(Данные) = Тип("РегистрСведенийНаборЗаписей.Цены")) Тогда
			
			НаборЗаписейЦены			= РегистрыСведений.Цены.СоздатьНаборЗаписей();
			Для Каждого ЭлементОтбора Из Данные.Отбор Цикл
				Если (ЭлементОтбора.Использование) Тогда
					НаборЗаписейЦены.Отбор[ЭлементОтбора.Имя].ВидСравнения	= ЭлементОтбора.ВидСравнения;
					НаборЗаписейЦены.Отбор[ЭлементОтбора.Имя].Значение		= ЭлементОтбора.Значение;
					НаборЗаписейЦены.Отбор[ЭлементОтбора.Имя].Использование	= Истина;
				КонецЕсли;
			КонецЦикла; 
			НаборЗаписейЦены.Прочитать();
			Для Каждого ПозицияНоменклатуры Из НаборЗаписейЦены Цикл
				СтруктураВозврата.Товары.Добавить(ПозицияНоменклатуры.Номенклатура);	
			КонецЦикла;			
			
		ИначеЕсли (ТипЗнч(Данные) = Тип("РегистрНакопленияНаборЗаписей.ОстаткиТоваровКомпании")) Тогда
			
			НаборЗаписейОстатки			= РегистрыНакопления.ОстаткиТоваровКомпании.СоздатьНаборЗаписей();
			Для Каждого ЭлементОтбора Из Данные.Отбор Цикл
				Если (ЭлементОтбора.Использование) Тогда
					НаборЗаписейОстатки.Отбор[ЭлементОтбора.Имя].ВидСравнения	= ЭлементОтбора.ВидСравнения;
					НаборЗаписейОстатки.Отбор[ЭлементОтбора.Имя].Значение		= ЭлементОтбора.Значение;
					НаборЗаписейОстатки.Отбор[ЭлементОтбора.Имя].Использование	= Истина;
				КонецЕсли;
			КонецЦикла;  
			НаборЗаписейОстатки.Прочитать();
			Для Каждого ПозицияНоменклатуры Из НаборЗаписейОстатки Цикл
				СтруктураВозврата.Товары.Добавить(ПозицияНоменклатуры.Номенклатура);	
			КонецЦикла;			
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

// Возвращает массив узлов для регистрации, РегистрацияДляТоваров - флаг регистрации для товаров
Функция ПолучитьМассивУзловДляРегистрации(РегистрацияДляТоваров = Истина) Экспорт
	
	МассивУзлов = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбменССайтом.Ссылка КАК Ссылка
	               |ИЗ
	               |	ПланОбмена.ОбменССайтом КАК ОбменССайтом
	               |ГДЕ
	               |	ОбменССайтом.Ссылка <> &ЭтотУзел";
				   
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменССайтом.ЭтотУзел());
	МассивУзлов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
	Возврат МассивУзлов;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМАТИРОВАНИЯ ЗНАЧЕНИЙ CML

Функция ФорматКомментарияДляCML(Комментарий)
	Возврат Лев(Комментарий, 3000);
КонецФункции

Функция ФорматНаименованияДляCML(Наименование)
	Возврат Лев(Наименование, 250);
КонецФункции

Функция ФорматДатыДляCML(ЗначениеДата, ВернутьДату = Истина, ВернутьВремя = Ложь)
	
	ДатаСтрока  = Формат(Дата(ЗначениеДата), "ДФ=yyyy-MM-dd");
	ВремяСтрока = Формат(Дата(ЗначениеДата), "ДФ=ЧЧ:мм:сс"); //"ДЛФ=T");	
	Результат   = ""; 
	
	Если ( ВернутьДату И ВернутьВремя ) Тогда
		Результат = ДатаСтрока + "T" + ВремяСтрока;
	ИначеЕсли ( ВернутьДату И (НЕ ВернутьВремя) ) Тогда	
		Результат = ДатаСтрока;
	ИначеЕсли ((НЕ ВернутьДату) И ВернутьВремя ) Тогда		
		Результат = ВремяСтрока;
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции

Функция ФорматВалютыДляCML(ВалютаСсылка)
	
	ТекстВалюты = "???";
	
	Если ТипЗнч(ВалютаСсылка) = Тип("СправочникСсылка.Валюты") Тогда
		НайденнаяСтрока	= ПредставленияВалют.Найти(ВалютаСсылка,"Валюта");
		Если НайденнаяСтрока=Неопределено Тогда
			Если Строка(ВалютаСсылка.Код)="810" Тогда
				ТекстВалюты	= "RUB";
			Иначе
				ТекстВалюты	= ВалютаСсылка.Наименование;
			КонецЕсли;
		Иначе
			ТекстВалюты	= НайденнаяСтрока.Представление;
		КонецЕсли;
	КонецЕсли;	
		
	Возврат Лев(ТекстВалюты, 3);
	
КонецФункции

Функция ПолучитьПоСтавкеНДСЗначениеДляВыгрузки(СтавкаНДС)
	
	Если ( СтавкаНДС = Справочники.СтавкиНДС.БезНДС ) Тогда
		ЗначениеНалога = "Без налога";
	ИначеЕсли ( СтавкаНДС.Ставка = 0 ) Тогда		
		ЗначениеНалога = "0";
	ИначеЕсли ( СтавкаНДС.Ставка = 10 ) Тогда		
		ЗначениеНалога = "10";
	// ставка ндс 10%/110%		
	ИначеЕсли ( СтавкаНДС.Ставка = 18 ) Тогда		
		ЗначениеНалога = "18";
	// ставка НДС 18%/118%				
	ИначеЕсли ( СтавкаНДС.Ставка = 20 ) Тогда				
		ЗначениеНалога = "20";
	// ставка НДС 20%/120%
	Иначе
		ЗначениеНалога = "";	
	КонецЕсли;
	
	Возврат ЗначениеНалога;
	
КонецФункции

Функция ПолучитьПоЗначениюДляВыгрузкиСтавкуНДС(ЗначениеНалога)
	
	Если ЗначениеНалога = "0" Тогда		
		Ставка = 0;
	ИначеЕсли ЗначениеНалога = "10" Тогда		
		Ставка = 10;     
	ИначеЕсли ЗначениеНалога = "10/110" Тогда		
		Ставка = Окр(10/100, 2, 1);
	ИначеЕсли ЗначениеНалога = "18" Тогда		
		Ставка = 18;
	ИначеЕсли ЗначениеНалога = "18/118" Тогда				
		Ставка = Окр(10/100, 2, 1);
	ИначеЕсли ЗначениеНалога = "20" Тогда				
		Ставка = 20
	ИначеЕсли ЗначениеНалога = "20/120" Тогда						
		Ставка = Окр(20/120, 2, 1);
	Иначе
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	СтавкиНДС.Ссылка КАК СтавкаНДС
	      	               |ИЗ
	      	               |	Справочник.СтавкиНДС КАК СтавкиНДС
	      	               |ГДЕ
	      	               |	СтавкиНДС.Ставка = &Ставка");
	Запрос.УстановитьПараметр("Ставка", Ставка);
	РезультатЗапроса	= Запрос.Выполнить();
	
	Если ( Не РезультатЗапроса.Пустой() ) Тогда
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат	Выборка.СтавкаНДС;
	Иначе
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьДеньСледНедели(День)
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, &Приращение) КАК ДеньСледНедели");
	Запрос.УстановитьПараметр("Дата", День);
	Запрос.УстановитьПараметр("Приращение", 7);
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		Возврат Выборка.ДеньСледНедели;	
	Иначе
		Возврат ТекущаяДата();
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ CML ИЗ СПИСКА ЗНАЧЕНИЙ

Процедура ЗаписатьCMLПоСпискуЗначений(ОбъектCML, СписокЗначений);
	
	Для Каждого Элемент Из СписокЗначений Цикл
		Если ( Элемент.Представление = КонецЭлементаCML ) Тогда
			ОбъектCML.ЗаписатьКонецЭлемента();
		ИначеЕсли ( Элемент.Представление = НачалоЭлементаCML ) Тогда	
			ОбъектCML.ЗаписатьНачалоЭлемента(Элемент.Значение);
		ИначеЕсли ( Элемент.Представление = "" ) Тогда
		    ОбъектCML.ЗаписатьТекст(Элемент.Значение);
		ИначеЕсли ( Булево(Найти(Элемент.Представление, ПрефиксУзлаCML)) ) Тогда
			ИмяУзла = СтрЗаменить(Элемент.Представление, ПрефиксУзлаCML, "");
			ЗаписатьТекстовойУзел(ОбъектCML, ИмяУзла, Элемент.Значение);	
		ИначеЕсли ( Булево(Найти(Элемент.Представление, ПрефиксАтрибутаCML)) ) Тогда
			ИмяАтрибута = СтрЗаменить(Элемент.Представление, ПрефиксАтрибутаCML, "");
			ОбъектCML.ЗаписатьАтрибут(ИмяАтрибута, Элемент.Значение);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ДобавитьУзелCML(СписокЗначений, НаименованиеУзла, Значение);
	СписокЗначений.Добавить(Значение, ПрефиксУзлаCML + НаименованиеУзла);
КонецПроцедуры

Процедура ДобавитьНачалоЭлементаCML(СписокЗначений, НаименованиеЭлемента);
	СписокЗначений.Добавить(НаименованиеЭлемента, НачалоЭлементаCML); 
КонецПроцедуры

Процедура ДобавитьАтрибутCML(СписокЗначений, НаименованиеАтрибута, Значение)
	СписокЗначений.Добавить(Значение, ПрефиксАтрибутаCML + НаименованиеАтрибута);
КонецПроцедуры

Процедура ДобавитьТекстCML(СписокЗначений, Значение)
	СписокЗначений.Добавить(Значение, "");
КонецПроцедуры

Процедура ДобавитьКонецЭлементаCML(СписокЗначений);
	СписокЗначений.Добавить("", КонецЭлементаCML); 
КонецПроцедуры

Процедура ЗаполнитьСписокЗначенийОсновныхРеквизитовТовара(СписокЗначенийCML, Товар)
	
	ИдТовара = СформироватьИдентификаторТовара(Товар.НоменклатураСсылка, Товар.ХарактеристикаСсылка);
	
	ДобавитьУзелCML(СписокЗначенийCML, "Ид"				   , ИдТовара); 
	ДобавитьУзелCML(СписокЗначенийCML, "ШтрихКод"		   , Товар.ШтрихКод);
	ДобавитьУзелCML(СписокЗначенийCML, "Артикул"		   , Товар.НоменклатураСсылка.Артикул);
	ДобавитьУзелCML(СписокЗначенийCML, "Наименование"	   , ФорматНаименованияДляCML(Товар.НоменклатураСсылка.Наименование));
	
	// + Производитель
	Если ЗначениеЗаполнено(Товар.НоменклатураСсылка.Производитель) Тогда
		ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "Изготовитель");
		Изготовитель = Товар.НоменклатураСсылка.Производитель;
		ДобавитьУзелCML(СписокЗначенийCML, "Ид", Изготовитель.УникальныйИдентификатор());
		ДобавитьУзелCML(СписокЗначенийCML, "Наименование", Изготовитель.Наименование);
		ДобавитьКонецЭлементаCML(СписокЗначенийCML);
	КонецЕсли;
	// - Производитель
	
	ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "БазоваяЕдиница");	
	ДобавитьАтрибутCML(СписокЗначенийCML, "Код"					   , Товар.ЕдиницаИзмерения.Код);
	
	Если ЗначениеЗаполнено(Товар.ЕдиницаИзмерения) И ЗначениеЗаполнено(Товар.ЕдиницаИзмерения.ЕдиницаПоКлассификатору)
		И ЗначениеЗаполнено(Товар.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.НаименованиеПолное) Тогда
		ДобавитьАтрибутCML(СписокЗначенийCML, "НаименованиеПолное"	   , Товар.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.НаименованиеПолное);
	КонецЕсли;	
	
	ДобавитьАтрибутCML(СписокЗначенийCML, "МеждународноеСокращение", ПолучитьМеждународноеСокращение(Товар.ЕдиницаИзмерения));
	ДобавитьТекстCML(СписокЗначенийCML, Строка(Товар.ЕдиницаИзмерения));
	ДобавитьКонецЭлементаCML(СписокЗначенийCML);	
	
КонецПроцедуры

Процедура ДобавитьЗначениеРеквизитаВСписок(СписокЗначенийCML, Наименование, Значение)
	
	ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "ЗначениеРеквизита");
	ДобавитьУзелCML(СписокЗначенийCML, "Наименование", Наименование);
	ДобавитьУзелCML(СписокЗначенийCML, "Значение"   , Значение);
	ДобавитьКонецЭлементаCML(СписокЗначенийCML);
	
КонецПроцедуры

Процедура ЗаполнитьСписокЗначенийРеквизитовТовара(СтруктураИзменений, СписокЗначенийCML, Товар, КаталогНаДиске, ВыгруженоКартинок)
	
	Если ( Товар.НоменклатураСсылка.Родитель <> Справочники.Номенклатура.ПустаяСсылка() ) Тогда	
		ИдГруппы = СформироватьИдентификаторТовара(Товар.НоменклатураСсылка.Родитель);
		ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "Группы");				
		ДобавитьУзелCML(СписокЗначенийCML		   , "Ид", ИдГруппы);
		ДобавитьКонецЭлементаCML(СписокЗначенийCML);	
	КонецЕсли;	
	
	ДобавитьУзелCML(СписокЗначенийCML, "Описание", ФорматКомментарияДляCML(Товар.НоменклатураСсылка.Комментарий));
	
	Если ( ВыгружатьКартинки ) Тогда 
		
		ЗапросКартинки	 = Новый Запрос("ВЫБРАТЬ
		      	                |	КартинкиИФайлы.Объект КАК Номенклатура
		      	                |ИЗ
		      	                |	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		      	                |ГДЕ
		      	                |	КартинкиИФайлы.Картинка = ИСТИНА
		      	                |	И КартинкиИФайлы.ДанныеВоВнешнемФайле = ЛОЖЬ
		      	                |	И КартинкиИФайлы.Объект = &Номенклатура
		      	                |
		      	                |УПОРЯДОЧИТЬ ПО
		      	                |	КартинкиИФайлы.ДатаСохранения УБЫВ");
		ЗапросКартинки.УстановитьПараметр("Номенклатура", Товар.НоменклатураСсылка);
		РезультатЗапроса	= ЗапросКартинки.Выполнить();
		Если ( НЕ РезультатЗапроса.Пустой() ) Тогда
			ЕстьОсновноеИзображение	= Истина;
			ВыборкаКартинки		= РезультатЗапроса.Выбрать();
		Иначе
			ЕстьОсновноеИзображение	= Ложь;
		КонецЕсли;
	
		Если (ЕстьОсновноеИзображение И ВыборкаКартинки.Следующий()) Тогда
			
			ОсновноеИзображение	= Новый Структура("Номенклатура, Идентификатор, Данные");
				
			Если ( ВыгружатьТолькоИзменения ) Тогда		
				ВыгрузитьДаннуюКартинку = (СтруктураИзменений.Картинки.Найти(Товар.НоменклатураСсылка) <> Неопределено);			
			Иначе
				ВыгрузитьДаннуюКартинку = Истина;
			КонецЕсли;
			
			Если ( ВыгрузитьДаннуюКартинку ) Тогда	
				СтруктураДанныхКартинки	= ВыгрузитьКартинку(Товар.НоменклатураСсылка, КаталогНаДиске);
				Если ( СтруктураДанныхКартинки.Размер <> "0" ) Тогда			
					ДобавитьУзелCML(СписокЗначенийCML, "Картинка", СтруктураДанныхКартинки.Адрес);
					ВыгруженоКартинок	 = ВыгруженоКартинок + 1;	
				КонецЕсли;		
			КонецЕсли;	
			
		Иначе
			// картинки нет вообще
			ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "Картинка");	
			ДобавитьКонецЭлементаCML(СписокЗначенийCML);			
		КонецЕсли;
								
	КонецЕсли;	
	
	СписокЗначенийСвойств	= Новый СписокЗначений();
	Для Каждого СтрокаХарактеристикаСвойство Из Товар.Строки Цикл
		Для Каждого СтрокаСвойствоНоменклатуры Из СтрокаХарактеристикаСвойство.Строки Цикл
			
			Если ( Не обЗначениеНеЗаполнено(СтрокаСвойствоНоменклатуры.СвойствоНоменклатуры)
				И СписокЗначенийСвойств.НайтиПоЗначению(СтрокаСвойствоНоменклатуры.СвойствоНоменклатуры) = Неопределено ) Тогда
				
				СписокЗначенийСвойств.Добавить(СтрокаСвойствоНоменклатуры.СвойствоНоменклатуры, Строка(СтрокаСвойствоНоменклатуры.СвойствоНоменклатурыЗначение));
				
			КонецЕсли;
			
		КонецЦикла;		
	КонецЦикла;	
		
	Если ( СписокЗначенийСвойств.Количество() > 0 ) Тогда
		
		ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "ЗначенияСвойств");
		Для Каждого ЗначениеСвойства Из СписокЗначенийСвойств Цикл
			
			ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "ЗначенияСвойства");				
			Ид = Строка(ЗначениеСвойства.Значение.УникальныйИдентификатор());
			ДобавитьУзелCML(СписокЗначенийCML, "Ид", Ид);
			ДобавитьУзелCML(СписокЗначенийCML, "Значение", ЗначениеСвойства.Представление);
			ДобавитьКонецЭлементаCML(СписокЗначенийCML);
			
		КонецЦикла;	
		ДобавитьКонецЭлементаCML(СписокЗначенийCML);
	КонецЕсли;
	
	Если ( Товар.НоменклатураСсылка.СтавкаНДС <> Справочники.СтавкиНДС.БезНДС ) Тогда		
		ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "СтавкиНалогов");
		ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "СтавкаНалога");
		ДобавитьУзелCML(СписокЗначенийCML, "Наименование", НаименованиеНалога);
		ДобавитьУзелCML(СписокЗначенийCML, "Ставка"      , ПолучитьПоСтавкеНДСЗначениеДляВыгрузки(Товар.НоменклатураСсылка.СтавкаНДС));
		ДобавитьКонецЭлементаCML(СписокЗначенийCML);
		ДобавитьКонецЭлементаCML(СписокЗначенийCML);	
	КонецЕсли;	
	
	ЗаписанЗаголовокВыгрузкиХарактеристик = Ложь;
	
	Если ( (Товар.НоменклатураСсылка.ТипНоменклатуры.ИспользованиеХарактеристик = 1
		ИЛИ Товар.НоменклатураСсылка.ТипНоменклатуры.ИспользованиеХарактеристик = 2)
		И Товар.ХарактеристикаСсылка <> ПустаяХарактеристикаСсылка ) Тогда
		
		Для Каждого СтрокаХарактеристикаСвойство Из Товар.Строки Цикл
			
			Если (обЗначениеНеЗаполнено(СтрокаХарактеристикаСвойство.ХарактеристикаЗначениеСвойства)
				ИЛИ обЗначениеНеЗаполнено(ФорматНаименованияДляCML(СтрокаХарактеристикаСвойство.ХарактеристикаСвойство))) Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Если ( НЕ ЗаписанЗаголовокВыгрузкиХарактеристик) Тогда
						
				ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "ХарактеристикиТовара");
				ЗаписанЗаголовокВыгрузкиХарактеристик = Истина;
				
			КонецЕсли;
			
			ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "ХарактеристикаТовара");
			ДобавитьУзелCML(СписокЗначенийCML, "Наименование", ФорматНаименованияДляCML(СтрокаХарактеристикаСвойство.ХарактеристикаСвойство));
			ДобавитьУзелCML(СписокЗначенийCML, "Значение"    , СтрокаХарактеристикаСвойство.ХарактеристикаЗначениеСвойства);
			ДобавитьКонецЭлементаCML(СписокЗначенийCML);
			
			
		КонецЦикла;
		
		Если ( ЗаписанЗаголовокВыгрузкиХарактеристик ) Тогда
			ДобавитьКонецЭлементаCML(СписокЗначенийCML);
		КонецЕсли;		
							
	КонецЕсли;	
	
	ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "ЗначенияРеквизитов");	
	ДобавитьЗначениеРеквизитаВСписок(СписокЗначенийCML, "ВидНоменклатуры", Товар.ВидНоменклатуры);
	ДобавитьЗначениеРеквизитаВСписок(СписокЗначенийCML, "ТипНоменклатуры", Товар.ТипНоменклатуры);
	ДобавитьЗначениеРеквизитаВСписок(СписокЗначенийCML, "Полное наименование", Товар.НоменклатураСсылка.НаименованиеПолное);		
	
	Если ( ЗначениеЗаполнено(Товар.НоменклатураСсылка.ОсновнаяЕдиницаИзмерения)) Тогда
		// передадим вес товара
		ДобавитьЗначениеРеквизитаВСписок(СписокЗначенийCML, "Вес", мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом * Справочники.Номенклатура.ПолучитьВесНоменклатуры(Товар.НоменклатураСсылка, Товар.НоменклатураСсылка.ОсновнаяЕдиницаИзмерения));
	КонецЕсли;	
	
	ДобавитьКонецЭлементаCML(СписокЗначенийCML);	
	
	СтатусУдаления = "";
	Если ( Товар.НоменклатураСсылка.ПометкаУдаления
	 ИЛИ (НЕ обЗначениеНеЗаполнено(Товар.ХарактеристикаСсылка) И Товар.ХарактеристикаСсылка.ПометкаУдаления) ) Тогда
		СтатусУдаления = "Удален";
	КонецЕсли;
	
	ДобавитьУзелCML(СписокЗначенийCML, "Статус", СтатусУдаления);
	
КонецПроцедуры

Функция ЗаполнитьСписокЗначенийПредложения(СписокЗначенийCML, Товар)

	ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "Цены");
	
	ТаблицаЦен = Новый ТаблицаЗначений();
	ТаблицаЦен.Колонки.Добавить("ТипЦен", Новый ОписаниеТипов("СправочникСсылка.ТипыЦен"));
	ТаблицаЦен.Колонки.Добавить("ДатаЦены", Новый ОписаниеТипов("Дата"));	
	ТаблицаЦен.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаЦен.Колонки.Добавить("ЕдиницаИзмеренияЦены", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТаблицаЦен.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаСвойствоХарактеристики Из Товар.Строки Цикл
		Для Каждого СтрокаСвойствоНоменклатуры Из СтрокаСвойствоХарактеристики.Строки Цикл
			Для Каждого СтрокаТипаЦен Из СтрокаСвойствоНоменклатуры.Строки Цикл
				Если ( НЕ СтрокаТипаЦен.ТипЦен = NULL ) Тогда
					НовСтрока = ТаблицаЦен.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрока, СтрокаТипаЦен);
				КонецЕсли;	
			КонецЦикла;	
			
			// Забираем из первой ветки, а дальше цены дублируются
			Прервать;		
		КонецЦикла;	
	КонецЦикла;	
	
	Если ТаблицаЦен.Количество()>0 И Товар<>Неопределено И ЗначениеЗаполнено(Товар.НоменклатураСсылка)
		И ЗначениеЗаполнено(Товар.НоменклатураСсылка.БазоваяЕдиницаИзмерения) Тогда
		
		Для Каждого СтрокаЕдиницыИзмерения Из ТаблицаЦен Цикл
			СтрокаЕдиницыИзмерения.ЕдиницаИзмеренияЦены	= Товар.ЕдиницаИзмерения;
		КонецЦикла;
	КонецЕсли;
	
	//Оставим в таблице только последние цены
	ЗапросТиповЦен	= Новый Запрос("ВЫБРАТЬ
	              	               |	ТаблицаЦен.ТипЦен,
	              	               |	ТаблицаЦен.ДатаЦены,
	              	               |	ТаблицаЦен.Валюта,
	              	               |	ТаблицаЦен.ЕдиницаИзмеренияЦены,
	              	               |	ТаблицаЦен.Цена
	              	               |ПОМЕСТИТЬ ВременнаяТаблицаЦен
	              	               |ИЗ
	              	               |	&ТаблицаДанных КАК ТаблицаЦен
	              	               |;
	              	               |
	              	               |///////////////////////////////////////////////
	              	               |ВЫБРАТЬ
	              	               |	ПоследниеЦены.ТипЦен,
	              	               |	ПоследниеЦены.Валюта,
	              	               |	ПоследниеЦены.ЕдиницаИзмеренияЦены,
	              	               |	МАКСИМУМ(ПоследниеЦены.ДатаЦены) КАК ДатаЦены,
	              	               |	МАКСИМУМ(ВременнаяТаблицаЦен.Цена) КАК Цена
	              	               |ИЗ
	              	               |	(ВЫБРАТЬ
	              	               |		ВременнаяТаблицаЦен.ТипЦен КАК ТипЦен,
	              	               |		ВременнаяТаблицаЦен.Валюта КАК Валюта,
	              	               |		ВременнаяТаблицаЦен.ЕдиницаИзмеренияЦены КАК ЕдиницаИзмеренияЦены,
	              	               |		МАКСИМУМ(ВременнаяТаблицаЦен.ДатаЦены) КАК ДатаЦены
	              	               |	ИЗ
	              	               |		ВременнаяТаблицаЦен КАК ВременнаяТаблицаЦен
	              	               |	
	              	               |	СГРУППИРОВАТЬ ПО
	              	               |		ВременнаяТаблицаЦен.ТипЦен,
	              	               |		ВременнаяТаблицаЦен.Валюта,
	              	               |		ВременнаяТаблицаЦен.ЕдиницаИзмеренияЦены) КАК ПоследниеЦены
								   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаЦен КАК ВременнаяТаблицаЦен
								   |		ПО ПоследниеЦены.ТипЦен = ВременнаяТаблицаЦен.ТипЦен
								   |			И ПоследниеЦены.ДатаЦены = ВременнаяТаблицаЦен.ДатаЦены
								   |			И ПоследниеЦены.Валюта = ВременнаяТаблицаЦен.Валюта
								   |			И ПоследниеЦены.ЕдиницаИзмеренияЦены = ВременнаяТаблицаЦен.ЕдиницаИзмеренияЦены
								   |СГРУППИРОВАТЬ ПО
								   |	ПоследниеЦены.ТипЦен,
	              	               |	ПоследниеЦены.Валюта,
	              	               |	ПоследниеЦены.ЕдиницаИзмеренияЦены
	              	               |;
	              	               |
	              	               |///////////////////////////////////////////////////
	              	               |УНИЧТОЖИТЬ ВременнаяТаблицаЦен");
	ЗапросТиповЦен.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();							   
	ЗапросТиповЦен.УстановитьПараметр("ТаблицаДанных", ТаблицаЦен);
	ТаблицаЦен	= ЗапросТиповЦен.Выполнить().Выгрузить();
    
	Для Каждого СтрокаЦены Из ТаблицаЦен Цикл
		
		ДобавитьНачалоЭлементаCML(СписокЗначенийCML, "Цена");
		
		ИдТипаЦены = Строка(СтрокаЦены.ТипЦен.УникальныйИдентификатор());
		
		ПредставлениеЦены = СокрЛП(СтрокаЦены.Цена) + " " + НРег(СокрЛП(СтрокаЦены.Валюта)) + " за " + СокрЛП(СтрокаЦены.ЕдиницаИзмеренияЦены);
		ДобавитьУзелCML(СписокЗначенийCML, "Представление", ПредставлениеЦены);
		ДобавитьУзелCML(СписокЗначенийCML, "ИдТипаЦены",    ИдТипаЦены);
		ДобавитьУзелCML(СписокЗначенийCML, "ЦенаЗаЕдиницу", СтрокаЦены.Цена);
		ДобавитьУзелCML(СписокЗначенийCML, "Валюта",        ФорматВалютыДляCML(СтрокаЦены.Валюта));
		ДобавитьУзелCML(СписокЗначенийCML, "Единица",       СтрокаЦены.ЕдиницаИзмеренияЦены);
		ДобавитьУзелCML(СписокЗначенийCML, "Коэффициент",   СтрокаЦены.ЕдиницаИзмеренияЦены.Коэффициент);
		
		ДобавитьКонецЭлементаCML(СписокЗначенийCML);
		
	КонецЦикла;
	
	ДобавитьКонецЭлементаCML(СписокЗначенийCML); 
	ДобавитьУзелCML(СписокЗначенийCML, "Количество", Товар.Остаток);
	
	Возврат ТаблицаЦен.Количество();
	
КонецФункции

Процедура ЗаписатьАтрибутВXML(ОбъектЗаписи, ИмяАтрибута, ЗначениеАтрибута)
	
	СтрокаЗаписи = ПолучитьСтрокуЗаписиДляXML(ЗначениеАтрибута);
	ОбъектЗаписи.ЗаписатьАтрибут(ИмяАтрибута, СтрокаЗаписи);	
	
КонецПроцедуры

Функция ПолучитьМеждународноеСокращение(ЕдиницаИзмерения)
	Если (ЕдиницаИзмерения.Наименование = "шт") Тогда
		Возврат "PCE";	
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "элем") Тогда
		Возврат "NCL";	
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "упак") Тогда
		Возврат "NMP";	
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "тыс.шт") Тогда
		Возврат "MIL";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "т") Тогда
		Возврат "TNE";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "рул") Тогда
		Возврат "NPL";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "пара") Тогда
		Возврат "NPR";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "набор") Тогда
		Возврат "SET";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "кг") Тогда
		Возврат "KGM";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "изд") Тогда
		Возврат "NAR";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "г") Тогда
		Возврат "GRM";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "100 ящ") Тогда
		Возврат "HBX";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "100 шт") Тогда
		Возврат "HBX";
	ИначеЕсли (ЕдиницаИзмерения.Наименование = "100 упак") Тогда
		Возврат "CNP";
	Иначе
		Возврат "";	
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПИСИ ДАННЫХ В CML

Процедура ЗаписатьТекстовойУзел(ОбъектXML, Имя, Значение, ОбязательнаяПроверкаНаПустуюСтроку = Истина)
    
	СтрокаЗаписи = ПолучитьСтрокуЗаписиДляXML(Значение);
	
	Если ( ОбязательнаяПроверкаНаПустуюСтроку И НЕ ЗначениеЗаполнено(СтрокаЗаписи) ) Тогда	
		Возврат;			
	КонецЕсли;	

	ОбъектXML.ЗаписатьНачалоЭлемента(Имя);
	ОбъектXML.ЗаписатьТекст(СтрокаЗаписи);
	ОбъектXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВыгрузитьОсновныеРеквизитыГруппыДляКлассификатора(ОбъектCML, Группа)
	
	ИдГруппы = СформироватьИдентификаторТовара(Группа.Ссылка);
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид", ИдГруппы);
	ИмяГруппы = Группа.Наименование;
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", ФорматНаименованияДляCML(ИмяГруппы));
	
КонецПроцедуры

Процедура ВыгрузитьГруппыРекурсивно(ОбъектCML, ДеревоГрупп);
	
	НужноВыгружатьГруппы = Ложь;
	Для Каждого СтрокаДерева Из ДеревоГрупп Цикл	
		Если ( НЕ СтрокаДерева.ЭтоГруппа ) Тогда Продолжить; КонецЕсли;
		
		Если ( Не НужноВыгружатьГруппы ) Тогда
			ОбъектCML.ЗаписатьНачалоЭлемента("Группы");
			НужноВыгружатьГруппы = Истина;
		КонецЕсли;
		
		ОбъектCML.ЗаписатьНачалоЭлемента("Группа");
		ВыгрузитьОсновныеРеквизитыГруппыДляКлассификатора(ОбъектCML, СтрокаДерева);
		
		Если ( СтрокаДерева.Строки.Количество() > 0 ) Тогда
			ВыгрузитьГруппыРекурсивно(ОбъектCML, СтрокаДерева.Строки);
		КонецЕсли;	
		ОбъектCML.ЗаписатьКонецЭлемента();		
	КонецЦикла;	
	
	Если ( НужноВыгружатьГруппы ) Тогда
		ОбъектCML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузитьОсновныеРеквизитыСвойстваДляКлассификатора(ОбъектCML, СвойствоНоменклатуры)
	
	Ид = СвойствоНоменклатуры.УникальныйИдентификатор();
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид", Ид);
	
	Наименование = СвойствоНоменклатуры.Наименование;
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", ФорматНаименованияДляCML(Наименование));
	
КонецПроцедуры

Функция ПолучитьСтрокуЗаписиДляXML(Значение)
	
	СтрокаЗаписи = Строка(Значение);
		
	Если ( ТипЗнч(Значение) = Тип("Число") ) Тогда
		
		СтрокаЗаписи = СтрЗаменить(СтрокаЗаписи, Символы.НПП, "");
		СтрокаЗаписи = СтрЗаменить(СтрокаЗаписи, ",", ".");
		
	ИначеЕсли ( ТипЗнч(Значение) = Тип("Булево") ) Тогда
		
		Если (Значение) Тогда
			СтрокаЗаписи = "true";
		Иначе
			СтрокаЗаписи = "false";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаЗаписи;
	
КонецФункции

Процедура ВыгрузитьСвойства(ОбъектCML, ВыбранныеСвойства)
	
	Если ( ВыбранныеСвойства.Количество() = 0 ) Тогда Возврат; КонецЕсли;
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Свойства");
	
	Для Каждого Свойство Из ВыбранныеСвойства Цикл  	
		ОбъектCML.ЗаписатьНачалоЭлемента("Свойство");
		ВыгрузитьОсновныеРеквизитыСвойстваДляКлассификатора(ОбъектCML, Свойство);
		ОбъектCML.ЗаписатьКонецЭлемента();	
	КонецЦикла;	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

// Процедура пишет в XML-файл информацию о контрагенте при выгрузке документов
//
// Параметры:
//  ОбъектXML  - объект типа ЗаписьXML
//  Контрагент - справочник-ссылка "Контрагенты"
//  Роль       - строка - значение тэга "Роль"
//
Процедура ВыгрузитьКонтрагентаДок(ОбъектXML, Контрагент, Роль = Неопределено, СтруктурнаяЕдиница = Неопределено, ИмяГруппыВыгрузки = "Контрагент")

	ОбъектXML.ЗаписатьНачалоЭлемента(ИмяГруппыВыгрузки);
	
	ЗаписатьТекстовойУзел(ОбъектXML, "Ид",           Строка(Контрагент.УникальныйИдентификатор()));
	ЗаписатьТекстовойУзел(ОбъектXML, "Наименование", Контрагент.Наименование);
	
	Если ( Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо ) Тогда
		ЗаписатьТекстовойУзел(ОбъектXML, "ОфициальноеНаименование", Контрагент.НаименованиеПолное);
		
		// выгрузка юридического адреса
		Если ( ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") ) Тогда
			ВыгрузитьЮрАдресОрганизации(ОбъектXML, Контрагент);
		КонецЕсли;
		
	Иначе
		ЗаписатьТекстовойУзел(ОбъектXML, "ПолноеНаименование", Контрагент.НаименованиеПолное);
	КонецЕсли;	
	
	ЗаписатьТекстовойУзел(ОбъектXML, "ИНН", Контрагент.ИНН);
	ЗаписатьТекстовойУзел(ОбъектXML, "КПП", Контрагент.КПП);
	ЗаписатьТекстовойУзел(ОбъектXML, "ОКПО", Контрагент.КодПоОКПО);
	
	ВыгрузитьБанковскийСчетОрганизации(ОбъектXML, СтруктурнаяЕдиница);
	
	Если ( Роль <> Неопределено ) Тогда
		ЗаписатьТекстовойУзел(ОбъектXML, "Роль", Роль);
	КонецЕсли;
	
	ОбъектXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВыгрузитьЮрАдресОрганизации(ОбъектXML, Организация)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Поле5,
	               |	КонтактнаяИнформация.Поле6,
	               |	КонтактнаяИнформация.Поле7,
	               |	КонтактнаяИнформация.Поле8,
	               |	КонтактнаяИнформация.Поле9,
	               |	КонтактнаяИнформация.Поле10,
	               |	КонтактнаяИнформация.Комментарий
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |	И КонтактнаяИнформация.Тип = &Тип
	               |	И КонтактнаяИнформация.Вид = &Вид";
				   
	Запрос.УстановитьПараметр("Объект", Организация);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ОбъектXML.ЗаписатьНачалоЭлемента("ЮридическийАдрес");
		
		ЗаписатьТекстовойУзел(ОбъектXML, "Представление", Выборка.Представление);
		ЗаписатьТекстовойУзел(ОбъектXML, "Комментарий", Выборка.Комментарий);
		
		АдресРоссийский = Истина;
		
		Если ( ЗначениеЗаполнено(Выборка.Поле1) ) Тогда
			
			Попытка
				ЧислоИндекса = Число(Выборка.Поле1);
			Исключение
				АдресРоссийский = Ложь;
			КонецПопытки;
			
		КонецЕсли;
			
		// российский адрес
		Если АдресРоссийский Тогда
		
			//Возможные значения: Почтовый индекс, Страна, Регион, Район, Населенный пункт, Город, Улица, Дом, Корпус, Квартира
			ЗаписатьАдресноеПоле(ОбъектXML, "Почтовый индекс", Выборка.Поле1);
			ЗаписатьАдресноеПоле(ОбъектXML, "Регион", Выборка.Поле2);
			ЗаписатьАдресноеПоле(ОбъектXML, "Район", Выборка.Поле3);
			ЗаписатьАдресноеПоле(ОбъектXML, "Населенный пункт", Выборка.Поле4);
			ЗаписатьАдресноеПоле(ОбъектXML, "Город", Выборка.Поле5);
			ЗаписатьАдресноеПоле(ОбъектXML, "Улица", Выборка.Поле6);
			ЗаписатьАдресноеПоле(ОбъектXML, "Дом", Выборка.Поле7);
			ЗаписатьАдресноеПоле(ОбъектXML, "Корпус", Выборка.Поле8);
			ЗаписатьАдресноеПоле(ОбъектXML, "Квартира", Выборка.Поле9);
			
		Иначе
			
			ЗаписатьАдресноеПоле(ОбъектXML, "Страна", Выборка.Поле1);
			
		КонецЕсли;	
		
		ОбъектXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПоСтруктуреСтрокиСФИО(СтруктураДанныхКонтрагента)
	
	ТекущаяСтрока	= "";
	Фамилия			= "";
	Имя				= "";
	Отчество		= "";
	
	СтруктураДанныхКонтрагента.Свойство("Фамилия", Фамилия);
	СтруктураДанныхКонтрагента.Свойство("Имя", Имя);
	СтруктураДанныхКонтрагента.Свойство("Отчество", Отчество);
	
	Если ( ЗначениеЗаполнено(Фамилия) ) Тогда
		ТекущаяСтрока = Фамилия;
	КонецЕсли;
	
	Если ( ЗначениеЗаполнено(Имя) ) Тогда
		ТекущаяСтрока = СокрЛП(ТекущаяСтрока + " " + Имя);
	КонецЕсли;
	
	Если ( ЗначениеЗаполнено(Отчество) ) Тогда
		ТекущаяСтрока = СокрЛП(ТекущаяСтрока + " " + Отчество);
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
	
КонецФункции

Процедура ЗаписатьАдресноеПоле(ОбъектXML, ИмяПоля, Значение)
	
	Если ( обЗначениеНеЗаполнено(Значение) ) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента("АдресноеПоле");
	
	ЗаписатьТекстовойУзел(ОбъектXML, "Тип", ИмяПоля);
	ЗаписатьТекстовойУзел(ОбъектXML, "Значение", Значение);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьКлассификатор(ОбъектCML, МассивНоменклатуры, ИдКаталога, ВыбранныеСвойства, ОрганизацияВладелец)

	ОбъектCML.ЗаписатьНачалоЭлемента("Классификатор");
    ЗаписатьТекстовойУзел(ОбъектCML, "Ид", Строка(ИдКаталога));
	ИмяКлассификатора = "Классификатор (" + НаименованиеКаталогаТоваровCML + ")";
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", ФорматНаименованияДляCML(ИмяКлассификатора));
	
	// владельца нужно выгрузить
	ВыгрузитьКонтрагентаДок(ОбъектCML, ОрганизацияВладелец, , ОрганизацияВладелец.ОсновнойБанковскийСчет, "Владелец");

	ЗапросПоГруппам = Новый Запрос(
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.Наименование,
		|	Номенклатура.ЭтоГруппа
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&МассивНоменклатуры)
		|
		|Итоги ПО
		|	Ссылка ИЕРАРХИЯ");
		
	ЗапросПоГруппам.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
		
	ДеревоГрупп = ЗапросПоГруппам.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ВыгрузитьГруппыРекурсивно(ОбъектCML, ДеревоГрупп.Строки);
	ВыгрузитьСвойства(ОбъектCML, ВыбранныеСвойства);
	
	ОбъектCML.ЗаписатьКонецЭлемента(); 
	
КонецПроцедуры

Функция СформироватьИдентификаторТовара(Номенклатура, Характеристика = Неопределено)

	Если ( обЗначениеНеЗаполнено(Характеристика) ИЛИ Характеристика = ПустаяХарактеристикаСсылка ) Тогда
		Возврат Строка(Номенклатура.УникальныйИдентификатор());
	Иначе	
		Возврат Строка(Номенклатура.УникальныйИдентификатор()) + "#" + Строка(Характеристика.УникальныйИдентификатор());
	КонецЕсли;

КонецФункции

Процедура ВыгрузитьИнформациюОБанке(ОбъектXML, РольБанка, Банк)
	
	Если ( обЗначениеНеЗаполнено(Банк)) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента(РольБанка);
	
	ЗаписатьТекстовойУзел(ОбъектXML, "СчетКорреспондентский", Банк.КоррСчет);
	ЗаписатьТекстовойУзел(ОбъектXML, "Наименование", Банк.Наименование);
	
	Если (Не ПустаяСтрока(Банк.Адрес)) Тогда
		
		ОбъектXML.ЗаписатьНачалоЭлемента("Адрес");
		ЗаписатьТекстовойУзел(ОбъектXML, "Представление", Банк.Адрес);
		ОбъектXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
	ЗаписатьТекстовойУзел(ОбъектXML, "БИК", Банк.Код);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьБанковскийСчетОрганизации(ОбъектXML, БанковскийСчет)
	
	Если ( обЗначениеНеЗаполнено(БанковскийСчет)
		ИЛИ ТипЗнч(БанковскийСчет) <> Тип("СправочникСсылка.БанковскиеСчета")) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента("РасчетныеСчета");
	
	// информация о счете
	ОбъектXML.ЗаписатьНачалоЭлемента("РасчетныйСчет");
	
	ЗаписатьТекстовойУзел(ОбъектXML, "НомерСчета", БанковскийСчет.НомерСчета);
	
	ВыгрузитьИнформациюОБанке(ОбъектXML, "Банк", БанковскийСчет.Банк);
	ВыгрузитьИнформациюОБанке(ОбъектXML, "БанкКорреспондент", БанковскийСчет.БанкДляРасчетов);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
	ОбъектXML.ЗаписатьКонецЭлемента();
		
Конецпроцедуры

Функция ВыгрузитьКартинку(Номенклатура, КаталогНаДиске);
	
	СтруктураРезультата = Новый Структура();
	СтруктураРезультата.Вставить("Адрес",  "");
	СтруктураРезультата.Вставить("Формат", Строка(ФорматКартинки.НеизвестныйФормат));
	СтруктураРезультата.Вставить("Размер", "0");
	
	// Получим картинку
	НаборКартинок = РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей();
	НаборКартинок.Отбор.Объект.Установить(Номенклатура);
	НаборКартинок.Прочитать();
	Если ( НаборКартинок.Количество() = 0 ) Тогда
		Возврат СтруктураРезультата;
	Иначе
		Картинка = НаборКартинок[0].Данные.Получить();
	КонецЕсли;	
	
	Если ( ТипЗнч(Картинка) <> Тип("Картинка") ) Тогда
		Возврат СтруктураРезультата;
	КонецЕсли;
	
	Попытка
		
		ФорматКартинкиОбъекта = Картинка.Формат();
		Если ( ФорматКартинкиОбъекта = ФорматКартинки.НеизвестныйФормат ) Тогда
			СообщитьПользователю("У товара обнаружено основное изображение неизвестного формата: " + Номенклатура, Истина, СтатусСообщения.Информация, 1);
			Возврат СтруктураРезультата;	
		КонецЕсли;
			
		ФорматКартинкиРазрешен = Истина;
		Если ( НЕ (ФорматКартинкиОбъекта = ФорматКартинки.GIF
		 	ИЛИ ФорматКартинкиОбъекта = ФорматКартинки.JPEG
		 	ИЛИ ФорматКартинкиОбъекта = ФорматКартинки.PNG) ) Тогда
			 
			Попытка
				ФорматКартинкиОбъекта = Картинка.Преобразовать(ФорматКартинки.JPEG);		 
			Исключение
				СообщитьОбИсключительнойОшибке(Истина, "Не удалось преобразовать картинку для " + Номенклатура + " из " + Строка(ФорматКартинкиОбъекта) + " в JPEG");
				ФорматКартинкиРазрешен = Ложь;
			КонецПопытки;
			
		КонецЕсли;	 
		
		Если ( НЕ ФорматКартинкиРазрешен ) Тогда Возврат СтруктураРезультата; КонецЕсли;
		
	Исключение
		
		СообщитьОбИсключительнойОшибке(Истина, "Не удалось выгрузить картинку для товара: " + Номенклатура + " " + ОписаниеОшибки());
		ФорматКартинкиРазрешен = Ложь;
		
	КонецПопытки;
		
	РасширениеФайлаКартинки    = "." + НРег(Строка(ФорматКартинкиОбъекта));
	ИмяФайлаКартинки           = Строка(Номенклатура.УникальныйИдентификатор()) + РасширениеФайлаКартинки;
	КаталогПоИмени			   = Лев(ИмяФайлаКартинки, 2);
	КаталогКартинки			   = КаталогНаДиске + "\" + ПодкаталогКартинок + "\" + КаталогПоИмени;
	СоздатьКаталог(КаталогКартинки);
	ПолноеИмяФайлаКартинки     = КаталогКартинки + "\" + ИмяФайлаКартинки;
	ФайлКартинкиНаДиске 	   = Новый Файл(ПолноеИмяФайлаКартинки);
	
	Картинка.Записать(ПолноеИмяФайлаКартинки);		
	
	СтруктураРезультата.Адрес  = ПодкаталогКартинок + "/" + КаталогПоИмени + "/" + ИмяФайлаКартинки;
	СтруктураРезультата.Формат = Строка(ФорматКартинкиОбъекта);
	СтруктураРезультата.Размер = Формат(ФайлКартинкиНаДиске.Размер(), "ЧГ=");
	
	Возврат СтруктураРезультата;
	
КонецФункции

Процедура ДобавитьНовоеЗначениеРеквизита(ОбъектXML, Наименование, Значение, ПустоеЗначение = "", ИмяГруппыЭлементов = "ЗначениеРеквизита")
	
	ЗначениеДляЗаписи = Значение;
	Если ( ЗначениеДляЗаписи = Null ) Тогда
		ЗначениеДляЗаписи = ПустоеЗначение;
	КонецЕсли;
	
	Если ( ЗначениеДляЗаписи = "" ) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента(ИмяГруппыЭлементов);
	
	ЗаписатьТекстовойУзел(ОбъектXML, "Наименование",  Наименование, Ложь);
	ЗаписатьТекстовойУзел(ОбъектXML, "Значение",  ЗначениеДляЗаписи, Ложь);
	
	ОбъектXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Функция СформироватьCMLСРанееЗагруженнымиЗаказами(СтрокаCML, МассивИзменений) 
	
	ОтобразитьСостояние("Поиск открытых заказов, загруженных с " + HTTPОбменСервер + "...");
	ТаблицаДокументов = ПолучитьЗаказыСОплатойИОтгрузкойПоКатегориям(МассивИзменений, мМассивЗагруженныхДокументов);
	
	Если ( ТаблицаДокументов.Количество() = 0 ) Тогда  
		Возврат ТаблицаДокументов;
	КонецЕсли;
	
	ДатаФормирования	= ТекущаяДата();
	ОбъектCML			= ПолучитьОбъектДляЗаписиXML("", ДатаФормирования);
		
	Для Каждого СтрокаТД Из ТаблицаДокументов Цикл
		
		ОбъектCML.ЗаписатьНачалоЭлемента("Документ");
		
		Док = СтрокаТД.ДокументСсылка;
		
		ВыгрузитьЗаказВCML(ОбъектCML, Док);
					
		ОбъектCML.ЗаписатьНачалоЭлемента("ЗначенияРеквизитов");
		
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Номер по 1С", Док.Номер);
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Дата по 1С", ФорматДатыДляCML(Док.Дата));
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "ПометкаУдаления", Док.ПометкаУдаления);
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Проведен", Док.Проведен);
											
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Номер оплаты по 1С", СтрокаТД.НомерОплаты);
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Дата оплаты по 1С", ФорматДатыДляCML(СтрокаТД.ДатаОплаты, Истина, Истина));	
			
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Номер отгрузки по 1С", СтрокаТД.НомерОтгрузки);
		ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "Дата отгрузки по 1С", ФорматДатыДляCML(СтрокаТД.ДатаОтгрузки, Истина, Истина));	
					
		ОбъектCML.ЗаписатьКонецЭлемента();
		ОбъектCML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	СтрокаCML = ОбъектCML.Закрыть();
		
	Возврат ТаблицаДокументов;
	 
КонецФункции

Процедура ВыгрузитьРеквизитыКонтрагентаВCML(ОбъектCML, Контрагент)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Контрагенты");
	ВыгрузитьКонтрагентаДок(ОбъектCML, Контрагент, "Покупатель", Контрагент.ОсновнойБанковскийСчет, "Контрагент");
		
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьТоварыУслугиЗаказаВCML(ОбъектCML, Док)
	
	ОбъектCML.ЗаписатьНачалоЭлемента("Товары");
	
	Для Каждого СтрокаТЧ Из Док.Товары Цикл
		ВыгрузитьТоварДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ);
	КонецЦикла; 	
	
	//Для Каждого СтрокаТЧ Из Док.Услуги Цикл
	//	ВыгрузитьТоварДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ, Истина);
	//КонецЦикла;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВыгрузитьТоварДляЗаказаВCML(ОбъектCML, Док, СтрокаТЧ, ТЧУслуги = Ложь)

	ОбъектCML.ЗаписатьНачалоЭлемента("Товар");
	
	ТЧУслуги	= НЕ ( СтрокаТЧ.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 
		ИЛИ СтрокаТЧ.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2);
	
	Если ( ТЧУслуги ) Тогда
		ИдТовара = СформироватьИдентификаторТовара(СтрокаТЧ.Номенклатура);		
	Иначе
		ИдТовара = СформироватьИдентификаторТовара(СтрокаТЧ.Номенклатура, СтрокаТЧ.ХарактеристикаНоменклатуры);	
	КонецЕсли;	
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид"			, ИдТовара);
	ЗаписатьТекстовойУзел(ОбъектCML, "Артикул"		, СтрокаТЧ.Номенклатура.Артикул);
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование"	, ФорматНаименованияДляCML(СтрокаТЧ.Номенклатура.Наименование));
	
	// + Производитель
	Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура.Производитель) Тогда
		ОбъектCML.ЗаписатьНачалоЭлемента("Изготовитель");
		Изготовитель = СтрокаТЧ.Номенклатура.Производитель;
		ЗаписатьТекстовойУзел(ОбъектCML, "Ид", Изготовитель.УникальныйИдентификатор());
		ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", Изготовитель.Наименование);
		ОбъектCML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	// - Производитель
	
	ОбъектCML.ЗаписатьНачалоЭлемента("БазоваяЕдиница");
	ОбъектCML.ЗаписатьАтрибут("Код"					   , СтрокаТЧ.Номенклатура.БазоваяЕдиницаИзмерения.Код);
	ОбъектCML.ЗаписатьАтрибут("НаименованиеПолное"	   , ФорматНаименованияДляCML(СтрокаТЧ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное));
	ОбъектCML.ЗаписатьАтрибут("МеждународноеСокращение", ПолучитьМеждународноеСокращение(СтрокаТЧ.Номенклатура.БазоваяЕдиницаИзмерения));
	ОбъектCML.ЗаписатьТекст(Строка(СтрокаТЧ.Номенклатура.БазоваяЕдиницаИзмерения));
	ОбъектCML.ЗаписатьКонецЭлемента();
	
	Если ( СтрокаТЧ.Номенклатура.СтавкаНДС <> Справочники.СтавкиНДС.БезНДС ) Тогда
		
		ОбъектCML.ЗаписатьНачалоЭлемента("СтавкиНалогов");
		ОбъектCML.ЗаписатьНачалоЭлемента("СтавкаНалога");
		ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", НаименованиеНалога);
		ЗаписатьТекстовойУзел(ОбъектCML, "Ставка"      , ПолучитьПоСтавкеНДСЗначениеДляВыгрузки(СтрокаТЧ.Номенклатура.СтавкаНДС));
		ОбъектCML.ЗаписатьКонецЭлемента();
		ОбъектCML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;	
	
	Если ( НЕ ТЧУслуги И СтрокаТЧ.ХарактеристикаНоменклатуры <> ПустаяХарактеристикаСсылка ) Тогда
			
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
					   |	ЗначенияСвойствОбъектов.Свойство,
					   |	ЗначенияСвойствОбъектов.Значение
					   |ИЗ
					   |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
					   |ГДЕ
					   |	ЗначенияСвойствОбъектов.Объект = &Объект";
					   
		Запрос.УстановитьПараметр("Объект", СтрокаТЧ.ХарактеристикаНоменклатуры); 			   
		РезультатЗапроса = Запрос.Выполнить();
		
		ЗаписанЗаголовокВыгрузкиХарактеристик = Ложь;
		
		Если ( НЕ РезультатЗапроса.Пустой() ) Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока ( Выборка.Следующий() ) Цикл
				
				Если ( обЗначениеНеЗаполнено(Выборка.Значение)
					ИЛИ обЗначениеНеЗаполнено(ФорматНаименованияДляCML(Выборка.Свойство))) Тогда
					
					Продолжить;
					
				КонецЕсли;					
				
				Если НЕ ЗаписанЗаголовокВыгрузкиХарактеристик Тогда
					
					ОбъектCML.ЗаписатьНачалоЭлемента("ХарактеристикиТовара");
					ЗаписанЗаголовокВыгрузкиХарактеристик = Истина;
					
				КонецЕсли;					
				
				ОбъектCML.ЗаписатьНачалоЭлемента("ХарактеристикаТовара");
				ЗаписатьТекстовойУзел(ОбъектCML, "Наименование"	, ФорматНаименованияДляCML(Выборка.Свойство));
				ЗаписатьТекстовойУзел(ОбъектCML, "Значение"	    , Выборка.Значение);
				ОбъектCML.ЗаписатьКонецЭлемента();	
			КонецЦикла;
			
			Если ( ЗаписанЗаголовокВыгрузкиХарактеристик ) Тогда
				ОбъектCML.ЗаписатьКонецЭлемента();
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	ОбъектCML.ЗаписатьНачалоЭлемента("ЗначенияРеквизитов");
	ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "ВидНоменклатуры", СтрокаТЧ.Номенклатура.ТипНоменклатуры.ВидНоменклатуры);
	ДобавитьНовоеЗначениеРеквизита(ОбъектCML, "ТипНоменклатуры", СтрокаТЧ.Номенклатура.ТипНоменклатуры);
	ОбъектCML.ЗаписатьКонецЭлемента();	
	
	ЗаписатьТекстовойУзел(ОбъектCML, "ЦенаЗаЕдиницу", СтрокаТЧ.Цена);
	ЗаписатьТекстовойУзел(ОбъектCML, "Количество", СтрокаТЧ.Количество);
	ЗаписатьТекстовойУзел(ОбъектCML, "Сумма", СтрокаТЧ.Сумма);
	
	//Если ( ТЧУслуги ) Тогда
	//	ЗаписатьТекстовойУзел(ОбъектCML, "Единица", СтрокаТЧ.Номенклатура.БазоваяЕдиницаИзмерения);
	//	ЗаписатьТекстовойУзел(ОбъектCML, "Коэффициент", "1");
	//Иначе	
		ЗаписатьТекстовойУзел(ОбъектCML, "Единица", СтрокаТЧ.ЕдиницаИзмерения);
		ЗаписатьТекстовойУзел(ОбъектCML, "Коэффициент", СтрокаТЧ.Коэффициент);
	//КонецЕсли;		
	
	Если ( НЕ обЗначениеНеЗаполнено(СтрокаТЧ.СтавкаНДС) И (СтрокаТЧ.Номенклатура.СтавкаНДС <> Справочники.СтавкиНДС.БезНДС) ) Тогда
		ОбъектCML.ЗаписатьНачалоЭлемента("Налоги");
		ОбъектCML.ЗаписатьНачалоЭлемента("Налог");
		ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", НаименованиеНалога);
		ЗаписатьТекстовойУзел(ОбъектCML, "УчтеноВСумме", ?(СтрокаТЧ.СуммаНДС>0,Истина,Ложь));
		ЗаписатьТекстовойУзел(ОбъектCML, "Сумма"	   , СтрокаТЧ.СуммаНДС);
		ЗаписатьТекстовойУзел(ОбъектCML, "Ставка"      , ПолучитьПоСтавкеНДСЗначениеДляВыгрузки(СтрокаТЧ.СтавкаНДС));
		ОбъектCML.ЗаписатьКонецЭлемента();
		ОбъектCML.ЗаписатьКонецЭлемента();
	КонецЕсли;
		
	Если ( СтрокаТЧ.ПроцентСкидки <> 0 ) Тогда
		ОбъектCML.ЗаписатьНачалоЭлемента("Скидки");
		ОбъектCML.ЗаписатьНачалоЭлемента("Скидка");
		ЗаписатьТекстовойУзел(ОбъектCML, "Процент"     , СтрокаТЧ.ПроцентСкидки);
		ЗаписатьТекстовойУзел(ОбъектCML, "УчтеноВСумме", Истина);
		ОбъектCML.ЗаписатьКонецЭлемента();
		ОбъектCML.ЗаписатьКонецЭлемента();	
	КонецЕсли;	
	
	ОбъектCML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВыгрузитьЗаказВCML(ОбъектCML, Док);
	
	//!!! ТекущийВходящийНомер				= ПолучитьСвойствоЗаказа(Док, "Входящий номер");
	//НомерВходящегоЭлектронногоДокумента	= Прав(ТекущийВходящийНомер,СтрДлина(ТекущийВходящийНомер)-СтрДлина(УзелОбмена.Код));
	НомерВходящегоЭлектронногоДокумента = Док.GUIDСайта;

	ДатаВходящегоЭлектронногоДокумента	= ПолучитьСвойствоЗаказа(Док, "Дата входящего документа");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Ид"	, Строка(Док.УникальныйИдентификатор()));
	ЗаписатьТекстовойУзел(ОбъектCML, "Номер", НомерВходящегоЭлектронногоДокумента);
	ЗаписатьТекстовойУзел(ОбъектCML, "Дата" , ФорматДатыДляCML(ДатаВходящегоЭлектронногоДокумента, Истина, Ложь));
	ЗаписатьТекстовойУзел(ОбъектCML, "ХозОперация", "Заказ товара");
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Роль"		  , "Продавец");
	ЗаписатьТекстовойУзел(ОбъектCML, "Валюта"	  , ФорматВалютыДляCML(Док.ВалютаДокумента));
	ЗаписатьТекстовойУзел(ОбъектCML, "Курс"	      , Док.КурсДокумента);
	
	СтоимостьДоставки 	= ПолучитьСтоимостьДоставкиЗаказа(Док);
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Сумма"      , Док.СуммаДокумента+СтоимостьДоставки);
	
	ВыгрузитьРеквизитыКонтрагентаВCML(ОбъектCML, Док.Контрагент);
	//ЗаписатьТекстовойУзел(ОбъектCML, "СрокПлатежа", ФорматДатыДляCML(Док.ДатаОплаты, Истина, Ложь));
	
	ЗаписатьТекстовойУзел(ОбъектCML, "Время", ФорматДатыДляCML(ДатаВходящегоЭлектронногоДокумента, Ложь, Истина));	
	ЗаписатьТекстовойУзел(ОбъектCML, "Комментарий", ФорматКомментарияДляCML(Док.Комментарий));
	
	// Получим сумму НДС по табличной части товары.
	СуммаНДС	= Док.Товары.Итог("СуммаНДС");
	ОбъектCML.ЗаписатьНачалоЭлемента("Налоги");
	ОбъектCML.ЗаписатьНачалоЭлемента("Налог");
	ЗаписатьТекстовойУзел(ОбъектCML, "Наименование", НаименованиеНалога);
	ЗаписатьТекстовойУзел(ОбъектCML, "УчтеноВСумме", ?(СуммаНДС > 0,Истина,Ложь));
	ЗаписатьТекстовойУзел(ОбъектCML, "Сумма", СуммаНДС, Ложь);
	ОбъектCML.ЗаписатьКонецЭлемента();
	ОбъектCML.ЗаписатьКонецЭлемента();			
	
	ВыгрузитьТоварыУслугиЗаказаВCML(ОбъектCML, Док);
	
КонецПроцедуры

Функция ПолучитьСвойствоЗаказа(Заказ, ИмяСвойства)

	Запрос	= Новый Запрос();
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	ЗначенияСвойствОбъектов.Значение КАК ЗначениеСвойства
	            	  |ИЗ
	            	  |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	            	  |ГДЕ
	            	  |	ЗначенияСвойствОбъектов.Объект = &Заказ
	            	  |	И ЗначенияСвойствОбъектов.Свойство.Наименование = &ИмяСвойства";
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("ИмяСвойства", ИмяСвойства);
	РезультатЗапроса = Запрос.Выполнить();
	Если ( РезультатЗапроса.Пустой() ) Тогда
		Возврат "";
	Иначе
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Строка(Выборка.ЗначениеСвойства);
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ УПРАВЛЕНИЯ ВЫГРУЗКОЙ ДАННЫХ

// Процедура осуществляет выгрузку данных в соответствии с переданной в качестве параметра настройки.
Процедура ПроизвестиВыгрузкуДанных() Экспорт
	
	ТаблицаФайловОбмена.Очистить();
	
	мСтруктураИнформацииИсторииОбмена	= Новый Структура("ДатаПоследнейЗагрузки,ДатаПоследнейВыгрузки,ДатаНачалаПоследнейЗагрузки,ДатаНачалаПоследнейВыгрузки,РезультатПоследнейЗагрузки,РезультатПоследнейВыгрузки,КомментарийКЗагрузкеДанных,КомментарийКВыгрузкеДанных");
	мСтруктураИнформацииИсторииОбмена.КомментарийКЗагрузкеДанных = "";
	мСтруктураИнформацииИсторииОбмена.КомментарийКВыгрузкеДанных = "";
	
	мСтруктураИнформацииИсторииОбмена.ДатаНачалаПоследнейВыгрузки = ТекущаяДата();
		
	ЗаполнитьСвойстваПоНастройке(УзелОбмена);
			
	Если ( Не ПустаяСтрока(КаталогВыгрузки) ) Тогда
		ФайлЗагрузки = КаталогВыгрузки + "\order.xml";
		ФайлЗагрузки = СтрЗаменить(ФайлЗагрузки,"\\","\");
	КонецЕсли;
	
	Если ( ВыгружатьТолькоИзменения ) Тогда
		СтруктураИзменений = Новый Структура("Товары, Заказы, Картинки, НомерСообщенияТовары, НомерСообщенияЗаказы", Новый Массив(), Новый Массив(), Новый Массив());			
		ЗаполнитьСтруктуруИзмененийДляУзла(УзелОбмена, СтруктураИзменений);
	КонецЕсли;
	 
		
	Если (ОбменТоварами) Тогда 	
		ТоварыУспешноВыгружены = ВыгрузитьТовары(СтруктураИзменений);	
		Если ( ВыгружатьТолькоИзменения И ТоварыУспешноВыгружены ) Тогда
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, СтруктураИзменений.НомерСообщенияТовары);		
		КонецЕсли;	
	Иначе
		ТоварыУспешноВыгружены = Истина;
	КонецЕсли;
		
	Если (ОбменЗаказами) Тогда
		ЗаказыУспешноВыгружены = ВыполнитьОбменЗаказами(СтруктураИзменений);		
		Если ( ВыгружатьТолькоИзменения И ЗаказыУспешноВыгружены ) Тогда		
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, СтруктураИзменений.НомерСообщенияЗаказы);			
		КонецЕсли;		
	Иначе
	    ЗаказыУспешноВыгружены = Истина;
	КонецЕсли;
			
	мСтруктураИнформацииИсторииОбмена.ДатаПоследнейВыгрузки			= ТекущаяДата();
	мСтруктураИнформацииИсторииОбмена.РезультатПоследнейВыгрузки	= ТоварыУспешноВыгружены И ЗаказыУспешноВыгружены;
			
	ЗаписатьИнформациюВПротоколОбмена(мСтруктураИнформацииИсторииОбмена);
		
КонецПроцедуры

// Процедура осуществляет заполнение переменных обработки значениями
// из настройки связи с сайтом.
Процедура ЗаполнитьСвойстваПоНастройке(УзелОбмена) Экспорт
	
	НачалоАдресаСкрипта					= УзелОбмена.СерверHTTP;
	СтруктураАдреса						= РазобратьАдресСайта(НачалоАдресаСкрипта);	
	HTTPОбменПорт						= УзелОбмена.ПортHTTP;
	HTTPОбменСервер						= СтруктураАдреса.HTTPСервер;
	НачалоАдресаСкрипта					= СтруктураАдреса.HTTPАдресСкрипта;
	
	ВыгружатьТолькоИзменения			= УзелОбмена.ВыгружатьТолькоИзменения;
	ВыгружатьНаСайт						= Истина; 
	
	Если ( ПустаяСтрока(УзелОбмена.КаталогОбменаHTTP) ) Тогда
		КаталогВыгрузки	= КаталогВременныхФайлов();
	Иначе
		ПоследнийСимвол = Сред(УзелОбмена.КаталогОбменаHTTP, СтрДлина(УзелОбмена.КаталогОбменаHTTP), 1);
		Если ( ПоследнийСимвол <> "\" ) Тогда
			КаталогВыгрузки = УзелОбмена.КаталогОбменаHTTP + "\";			
		Иначе
			КаталогВыгрузки	= УзелОбмена.КаталогОбменаHTTP;
		КонецЕсли;
		ФайлЗагрузки = КаталогВыгрузки + "order.xml";
	КонецЕсли;	
	
	мПрефикс							= СокрЛП(УзелОбмена.Код);				
	HTTPОбменИмяПользователя			= УзелОбмена.ПользовательHTTP;
	HTTPОбменПароль						= УзелОбмена.ПарольHTTP;
	HTTPОбменАдресСкрипта				= НачалоАдресаСкрипта;
	HTTPОбменПроксиИспользование		= ?(обЗначениеНеЗаполнено(УзелОбмена.ПользовательПроксиHTTP),Ложь,Истина);
	HTTPОбменПроксиИмяПользователя		= УзелОбмена.ПользовательПроксиHTTP;
	HTTPОбменПроксиСервер				= УзелОбмена.СерверПроксиHTTP;
	HTTPОбменПроксиПорт					= УзелОбмена.ПортПроксиHTTP;
	ГруппаДляНовыхКонтрагентов			= УзелОбмена.ГруппаДляНовыхКонтрагентов;
	Организация							= УзелОбмена.Организация;
	Менеджер							= УзелОбмена.Менеджер;
	Подразделение						= УзелОбмена.Подразделение;
	ТипЦены								= УзелОбмена.ТипЦены;
	ЗаписыватьДокументыТекущейДатой		= УзелОбмена.ЗаписыватьДокументыТекущейДатой;
	ПроводитьДокументы					= УзелОбмена.ПроводитьДокументы;
	ПроводитьДокументыОперативно		= УзелОбмена.ПроводитьДокументыОперативно;
	СпособИдентификацииКонтрагентов		= УзелОбмена.СпособИдентификацииКонтрагентов;
	ВыгружатьКартинки					= УзелОбмена.ВыгружатьКартинки;
	ОбменТоварами						= УзелОбмена.ОбменТоварами;
	ОбменЗаказами						= УзелОбмена.ОбменЗаказами;
	ПодразделениеДляВыборкиТоваров		= ?(обЗначениеНеЗаполнено(УзелОбмена.ПодразделениеДляВыборкиТоваров),Справочники.ПодразделенияКомпании.ОсновноеПодразделение,УзелОбмена.ПодразделениеДляВыборкиТоваров);
	КонтрагентДляВыборкиТоваров			= УзелОбмена.КонтрагентДляВыборкиТоваров;
	СпособОпределенияЦен				= УзелОбмена.ВидЦенКонтрагентаДляВыборкиТоваров;
	Если СпособОпределенияЦен=0 Тогда
		СпособОпределенияЦен	= 1;
	КонецЕсли;
	ВыгружатьТоварыБезЦены				= УзелОбмена.ВыгружатьТоварыБезЦены;
	ПредставленияВалют					= УзелОбмена.ПредставленияВалют.Выгрузить().Скопировать();
		
КонецПроцедуры

Функция ВыгрузитьТовары(СтруктураИзменений)
	
	Если ( Не ОпределитьМожноВыгрузитьТовары() ) Тогда
		#Если Клиент Тогда
		СообщитьПользователю("Выгрузка товаров не произведена.", Истина,,2);
		#КонецЕсли
		Возврат Истина;	
	КонецЕсли;
	
	HTTPОбменАдресСкрипта	  = НачалоАдресаСкрипта;
	КаталогВыгрузкиЗащищенный = КаталогВыгрузки+ПодкаталогБезопасностиКаталогаВыгрузки;
	
	СтруктураРезультата		  = ВыгрузитьТоварыВКаталогНаДиске(СтруктураИзменений, КаталогВыгрузкиЗащищенный);
	
	ВыгруженоОбъектов = СтруктураРезультата.ВыгруженоТоваров
						  + СтруктураРезультата.ВыгруженоКартинок
						  + СтруктураРезультата.ВыгруженоПредложений;
						  	
	Если ВыгруженоОбъектов > 0 Тогда					  
		СообщениеПользователю = "Выгружено товаров в файл обмена : " + СтруктураРезультата.ВыгруженоТоваров;
		Если ВыгружатьКартинки Тогда                                      
			СообщениеПользователю = СообщениеПользователю + Символы.ПС + "Выгружено картинок в файл обмена: " + СтруктураРезультата.ВыгруженоКартинок;
		КонецЕсли;	
		
	Иначе			
		СообщениеПользователю = "Изменения товаров не зарегистрированы. Выгрузка товаров не произведена.";		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(СообщениеОНевыгруженныхТоварах) Тогда
		#Если Клиент Тогда
		Сообщить("Товары не попавшие в выгрузке из-за отсутствия цены:"+Символы.ПС+СообщениеОНевыгруженныхТоварах);	
		#КонецЕсли
		СообщениеОНевыгруженныхТоварах	= "";
	КонецЕсли;	
	
	СообщитьПользователю(СообщениеПользователю, Истина,,2);
	
	Успешно = Ложь;				  
	
	Если ( ВыгружатьНаСайт И ВыгруженоОбъектов > 0 ) Тогда
		Успешно = ВыгрузитьКаталогПредложенияНаСайт(КаталогВыгрузкиЗащищенный);						
	КонецЕсли;
	
	Если ( Успешно ) Тогда	
		Если ( ВыгруженоОбъектов > 0 ) Тогда
			СообщитьПользователю("Выгрузка товаров успешно завершена", Истина,,2);
		КонецЕсли;		
	ИначеЕсли (ВыгруженоОбъектов > 0) Тогда
		СообщитьПользователю("Выгрузка товаров завершена с ошибками!!!", Истина,,2);
	КонецЕсли;	
	
	Возврат Успешно;
		
КонецФункции

Функция ПолучитьОбъектДляЗаписиXML(ИмяФайла, ДатаФормирования)
	
	ЗаписьXML = Новый ЗаписьXML();
	
	Если ( ПустаяСтрока(ИмяФайла) ) Тогда
		ЗаписьXML.УстановитьСтроку("UTF-8");
	Иначе
		ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8"); 
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("КоммерческаяИнформация");
	ЗаписьXML.ЗаписатьАтрибут("ВерсияСхемы", "2.04");
	ЗаписьXML.ЗаписатьАтрибут("ДатаФормирования", ФорматДатыДляCML(ДатаФормирования, Истина, Истина));
	
	Возврат ЗаписьXML;
	
КонецФункции

Функция ВыгрузитьТоварыВКаталогНаДиске(СтруктураИзменений, КаталогНаДиске) 
	
	НастроитьПостроительОтчета(ПостроительЗапроса);
	
	ТаблицаОтбора = УзелОбмена.СохраненныеНастройкиПостроителя.Получить();
	Если ТаблицаОтбора <> Неопределено Тогда 
		ЗаполнитьОтборПоТаблицеЗначений(ПостроительЗапроса.Отбор, ТаблицаОтбора);
	КонецЕсли;	
	
	Если ВыгружатьТолькоИзменения Тогда
		
		МассивИзмененийНоменклатуры = СтруктураИзменений.Товары;	
		Если ( МассивИзмененийНоменклатуры.Количество() = 0 ) Тогда			
			Возврат Новый Структура("ВыгруженоТоваров,ВыгруженоКартинок,ВыгруженоПредложений", 0, 0, 0);	
		КонецЕсли;			
		ДобавитьФильтрыВПостроительОтчета(МассивИзмененийНоменклатуры);			
				
	Иначе	
		
		УдалитьФильтрыИзПостроителяОтчета();
		
	КонецЕсли;	
	
	ИмяФайлаКаталога = КаталогНаДиске + "\import.xml";
	ИмяФайлаПрайса	 = КаталогНаДиске + "\offers.xml";
	СоздатьКаталог(КаталогНаДиске);
	
	Попытка
		УдалитьФайлы(КаталогНаДиске, "*.*");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, );
	КонецПопытки;		
	
	ДатаОтчета = ТекущаяДата();
	
	ПостроительЗапроса.Параметры.Вставить("ДатаОтчета", ДатаОтчета);
	ПостроительЗапроса.Выполнить();	
	
	ОтобразитьСостояние("Подготовка данных...");
	
	ВыбранныеСвойства = Новый СписокЗначений;
	
	ТаблицаРезультатаЗапроса = ПостроительЗапроса.Результат.Выгрузить();
	
	ТабСвойств			= ТаблицаРезультатаЗапроса.Скопировать();
	ТабСвойств.Свернуть("СвойствоНоменклатуры");
	ВыбранныеСвойства	= ТабСвойств.ВыгрузитьКолонку("СвойствоНоменклатуры");
	
	ЭлементNULL = ВыбранныеСвойства.Найти(NULL);     
	Если ( ЭлементNULL <> Неопределено ) Тогда
		ВыбранныеСвойства.Удалить(ЭлементNULL);
	КонецЕсли;
	
	ТабСвойств = Неопределено;
	
	// требуется неизменный UUID каталога для проекта.
	// По организации взять UUID не можем, поскольку не ограничиваем
	// выгрузку рамками одной организации, выгружаем по предприятию в целом.
	// Поэтому, чтобы нигде не хранить UUID, получаем его из валюты
	// регламентированного учета, считая ее самой "стабильной" единицей
	// данных в рамках ИБ.
	
	ИдКаталога   = Строка(Константы.ВалютаУправленческогоУчетаКомпании.Получить().УникальныйИдентификатор());
	
	ОтобразитьСостояние("Выгрузка классификатора...");
	
	ОбъектCMLКаталог	= ПолучитьОбъектДляЗаписиXML(ИмяФайлаКаталога, ДатаОтчета);
	
	ТаблицаНоменклатуры	= ТаблицаРезультатаЗапроса.Скопировать();
	ТаблицаНоменклатуры.Свернуть("НоменклатураСсылка");
	
	МассивНоменклатуры	= ТаблицаНоменклатуры.ВыгрузитьКолонку("НоменклатураСсылка");
	
	ОрганизацияВладелец = Неопределено;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Разрешенные первые 1
	               |	Организации.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Организации КАК Организации";
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		ОрганизацияВладелец = Выборка.Ссылка;	
	КонецЕсли;	
	
	ВыгрузитьКлассификатор(ОбъектCMLКаталог, МассивНоменклатуры, ИдКаталога, ВыбранныеСвойства, ОрганизацияВладелец);
	
	ОтобразитьСостояние("Подготовка данных...");	
	
	ОбъектCMLПакетПредложений	= ПолучитьОбъектДляЗаписиXML(ИмяФайлаПрайса, ДатаОтчета);
	СтруктураРезультата			= СформироватьCMLКаталогаПредложений(СтруктураИзменений, ОбъектCMLКаталог, ОбъектCMLПакетПредложений, ИдКаталога, ПостроительЗапроса.Результат, КаталогНаДиске, ОрганизацияВладелец);
	
	ОбъектCMLКаталог.ЗаписатьКонецЭлемента();
	ОбъектCMLКаталог.Закрыть();
	
	ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();
	ОбъектCMLПакетПредложений.Закрыть();

	Возврат СтруктураРезультата;
	
КонецФункции

Функция ВыгрузитьКаталогПредложенияНаСайт(КаталогНаДиске)
	
	СтруктураПараметровСайта = ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);
	
	МассивПодкаталогов = Новый Массив();
	Если ( ВыгружатьКартинки ) Тогда
		МассивПодкаталогов.Добавить(ПодкаталогКартинок);
	КонецЕсли;	
	
	Успешно = HTTPВыгрузитьНаСервер(СтруктураПараметровСайта, КаталогНаДиске, МассивПодкаталогов, Истина, "catalog");
		
	Попытка
		УдалитьФайлы(КаталогНаДиске, "*.*");
	Исключение
		СообщитьОбИсключительнойОшибке(Истина, );
	КонецПопытки;
	
	Возврат Успешно;
	
КонецФункции

Функция СформироватьCMLКаталогаПредложений(СтруктураИзменений, ОбъектCMLКаталог, ОбъектCMLПакетПредложений, ИдКаталога, РезультатЗапроса, КаталогНаДиске, ОрганизацияВладелец)
	
	// Заголовок каталога
	ОбъектCMLКаталог.ЗаписатьНачалоЭлемента("Каталог");
	ЗаписатьАтрибутВXML(ОбъектCMLКаталог, "СодержитТолькоИзменения", ВыгружатьТолькоИзменения);
	ЗаписатьТекстовойУзел(ОбъектCMLКаталог, "Ид", Строка(ИдКаталога));
	ЗаписатьТекстовойУзел(ОбъектCMLКаталог, "ИдКлассификатора", Строка(ИдКаталога));
	ЗаписатьТекстовойУзел(ОбъектCMLКаталог, "Наименование", НаименованиеКаталогаТоваровCML);
	
	ВыгрузитьКонтрагентаДок(ОбъектCMLКаталог, ОрганизацияВладелец, , ОрганизацияВладелец.ОсновнойБанковскийСчет, "Владелец");
	
	// Заголовок пакета предложений
	ОбъектCMLПакетПредложений.ЗаписатьНачалоЭлемента("ПакетПредложений");
	ЗаписатьАтрибутВXML(ОбъектCMLПакетПредложений, "СодержитТолькоИзменения", ВыгружатьТолькоИзменения);
	ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "Ид", Строка(ИдКаталога) + "#");
	ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "Наименование"	   , НаименованиеПакетаПредложенийCML);
	ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "ИдКаталога"	   , Строка(ИдКаталога));
	ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "ИдКлассификатора", Строка(ИдКаталога));
	
	ВыгрузитьКонтрагентаДок(ОбъектCMLПакетПредложений, ОрганизацияВладелец, , ОрганизацияВладелец.ОсновнойБанковскийСчет, "Владелец");	
	
	// Типы цен для пакета предложений
	ТаблицаТиповЦен	= РезультатЗапроса.Выгрузить();
	ТаблицаТиповЦен.Свернуть("ТипЦен");
	СтрокаNULL		= ТаблицаТиповЦен.Найти(NULL, "ТипЦен");
	Если ( СтрокаNULL <> Неопределено ) Тогда
		ТаблицаТиповЦен.Удалить(СтрокаNULL);
	КонецЕсли;	
	
	Если ( ТаблицаТиповЦен.Количество()>0 ) Тогда	
		ОбъектCMLПакетПредложений.ЗаписатьНачалоЭлемента("ТипыЦен");
		Для Каждого СтрокаТипЦены Из ТаблицаТиповЦен Цикл
			
			ОбъектCMLПакетПредложений.ЗаписатьНачалоЭлемента("ТипЦены");
			Ид	= Строка(СтрокаТипЦены.ТипЦен.УникальныйИдентификатор());
			ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "Ид", Ид);
			ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "Наименование", ФорматНаименованияДляCML(СтрокаТипЦены.ТипЦен));
			ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "Валюта",       ФорматВалютыДляCML(СтрокаТипЦены.ТипЦен.ВалютаЦены));
			ОбъектCMLПакетПредложений.ЗаписатьНачалоЭлемента("Налог");
			ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "Наименование", НаименованиеНалога);
			ЗаписатьТекстовойУзел(ОбъектCMLПакетПредложений, "УчтеноВСумме", СтрокаТипЦены.ТипЦен.ЦенаВключаетНДС);
			ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();
			ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();
			
		КонецЦикла;
		ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();	
	КонецЕсли;	
	
	РезультатДерево	= РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Всего 			  			 = РезультатДерево.Строки.Количество();
	Выгружено 		  			 = 0;
	ВыгруженоКартинок 			 = 0;
	ВыгруженоПредложений		 = 0;
	
	Если ( Всего > 0 ) Тогда
		
		ОбъектCMLКаталог.ЗаписатьНачалоЭлемента("Товары");
		ОбъектCMLПакетПредложений.ЗаписатьНачалоЭлемента("Предложения");
		
		Для Каждого СтрокаГруппировкиНоменклатураСсылка Из РезультатДерево.Строки  Цикл
			
			Для Каждого СтрокаГруппировкиХарактеристикаСсылка Из СтрокаГруппировкиНоменклатураСсылка.Строки  Цикл
				
				// В выгрузку номенклатуры добавляются только товары, имеющие цену.
				// О не имеющих - писать сообщение в лог обмена.			
				Если НЕ ВыгружатьТоварыБезЦены Тогда
					Если СтрокаГруппировкиХарактеристикаСсылка.Цена=0 ИЛИ СтрокаГруппировкиХарактеристикаСсылка.Цена = NULL Тогда
						СообщениеОНевыгруженныхТоварах	= СообщениеОНевыгруженныхТоварах + "Не установлена цена для номенклатуры <"+Строка(СтрокаГруппировкиХарактеристикаСсылка.НоменклатураСсылка)+">"+Символы.ПС;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				// Каталог
				СписокЗначенийCML = Новый СписокЗначений;
				ЗаполнитьСписокЗначенийОсновныхРеквизитовТовара(СписокЗначенийCML, СтрокаГруппировкиХарактеристикаСсылка);
				ЗаполнитьСписокЗначенийРеквизитовТовара(СтруктураИзменений, СписокЗначенийCML, СтрокаГруппировкиХарактеристикаСсылка, КаталогНаДиске, ВыгруженоКартинок);
				Выгружено = Выгружено + 1;
				
				ОбъектCMLКаталог.ЗаписатьНачалоЭлемента("Товар");
				ЗаписатьCMLПоСпискуЗначений(ОбъектCMLКаталог, СписокЗначенийCML);
				ОбъектCMLКаталог.ЗаписатьКонецЭлемента();	
				
				Если Выгружено % 100 = 0 Тогда
					ОтобразитьСостояние("Выгружено объектов: " + Выгружено + ". Выгружается: " + СтрокаГруппировкиХарактеристикаСсылка.НоменклатураСсылка);
				КонецЕсли;
				
				// Предложения
				СписокЗначенийCML	= Новый СписокЗначений;
				Кратко				= Истина;
				ЗаполнитьСписокЗначенийОсновныхРеквизитовТовара(СписокЗначенийCML, СтрокаГруппировкиХарактеристикаСсылка);
				КоличествоЦен = ЗаполнитьСписокЗначенийПредложения(СписокЗначенийCML, СтрокаГруппировкиХарактеристикаСсылка);
				
				Если КоличествоЦен > 0 Тогда
					ВыгруженоПредложений = ВыгруженоПредложений + 1;
					ОбъектCMLПакетПредложений.ЗаписатьНачалоЭлемента("Предложение");
					ЗаписатьCMLПоСпискуЗначений(ОбъектCMLПакетПредложений, СписокЗначенийCML);
					ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЦикла;
		
		ОбъектCMLКаталог.ЗаписатьКонецЭлемента();
		ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
	// Завершаем каталог
	ОбъектCMLКаталог.ЗаписатьКонецЭлемента();
	
	// Завершаем пакет предложений
	ОбъектCMLПакетПредложений.ЗаписатьКонецЭлемента();

	СтруктураВозврата = Новый Структура("ВыгруженоТоваров,ВыгруженоКартинок,ВыгруженоПредложений", Выгружено, ВыгруженоКартинок, ВыгруженоПредложений);
	
	Возврат СтруктураВозврата;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОПИСАНИЯ И ВЫВОДА ОШИБОК И СООБЩЕНИЙ

Процедура ЗаписатьИнформациюВПротоколОбмена(СтруктураИнформации)
	
	ДатаПоследнегоОбмена	= ТекущаяДата();					
	Если (НЕ обЗначениеНеЗаполнено(СтруктураИнформации.ДатаПоследнейЗагрузки)) Тогда	
		ДатаПоследнейЗагрузки		=  СтруктураИнформации.ДатаПоследнейЗагрузки;
		РезультатПоследнейЗагрузки	= СтруктураИнформации.РезультатПоследнейЗагрузки; 	
	КонецЕсли;
	
	Если ( НЕ обЗначениеНеЗаполнено(СтруктураИнформации.ДатаПоследнейВыгрузки)) Тогда	
		ДатаПоследнейВыгрузки		= СтруктураИнформации.ДатаПоследнейВыгрузки;
		РезультатПоследнейВыгрузки	= СтруктураИнформации.РезультатПоследнейВыгрузки;				
	КонецЕсли;	
							
КонецПроцедуры

Процедура ОтобразитьСостояние(ТекстСообщения)
	#Если Клиент Тогда
	Состояние(ТекстСообщения);
	#КонецЕсли	
КонецПроцедуры

Процедура СообщитьПользователю(ТекстСообщения, ИнформацияОВыгрузке, Статус = Неопределено, ВыводитьВРежимеОтладки=0)
	
	Если ( ВыводитьВРежимеОтладки > 0 ) Тогда
		// Определим является ли сообщение html документом по наличию ключевых тегов.
		Если ( Булево(Найти(ТекстСообщения,"<table")) Или Булево(Найти(ТекстСообщения,"<html")) Или Булево(Найти(ТекстСообщения,"<body"))  ) Тогда
			ОшибкиHTMLем	= ТекстСообщения;	
		Иначе
			ОшибкиТекстом	= ОшибкиТекстом + ?(ПустаяСтрока(ОшибкиТекстом),"",Символы.ПС) + ТекстСообщения;	
		КонецЕсли;
	КонецЕсли;	
	
	// Если сообщение слишком длинное, то не следует выводить его в окне служебных сообщений,
	// т.к. это приводит к падению производительности обработки.
	Если ( СтрДлина(ТекстСообщения)>255 ) Тогда Возврат; КонецЕсли;
	
	#Если Клиент Тогда
		Если (ВыводитьВРежимеОтладки = 0 Или ВыводитьВРежимеОтладки = 2) Тогда
			Сообщить(ТекстСообщения, ?(ТипЗнч(Статус)=Тип("СтатусСообщения"), Статус, СтатусСообщения.Обычное));	
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

// Процедура выводит текст ответа сервера, если в нем имеется
// расширенное описание ошибки (строки, начиная со 2-й)
//
// Параметры:
//  ОтветСервера - строка, текст ответа сервера
//
Процедура СообщитьРасширенноеОписаниеОтветаСервера(ОтветСервера)
	
	ТекстСообщения = "Ответ сервера:" + Символы.ПС;
	
	Если (СтрЧислоСтрок(ОтветСервера) > 1) Тогда
		Для ТекСтрока = 2 по СтрЧислоСтрок(ОтветСервера) Цикл
			ТекстСообщения = ТекстСообщения + СтрПолучитьСтроку(ОтветСервера, ТекСтрока) + Символы.ПС;
		КонецЦикла;	
	КонецЕсли;
	
	СообщитьПользователю(ТекстСообщения, Истина, СтатусСообщения.Информация,1);	

КонецПроцедуры	

// Процедура выводит текст сообщения об ошибке обмена
// и текст "Обмен не выполнен."
//
// Параметры:
//  ТекстСообщения - строка, текст сообщения
//
Процедура СообщитьОбОшибкеОбмена(ТекстСообщения, ИнформацияОВыгрузке)
	
	ТекстСообщения = ТекстСообщения + Символы.ПС + "Обмен не выполнен";
	СообщитьПользователю(ТекстСообщения, ИнформацияОВыгрузке, СтатусСообщения.Важное,2);	
	
КонецПроцедуры	

// Процедура выводит текст сообщения об исключительной ошибке
//
// Параметры:
//
Процедура СообщитьОбИсключительнойОшибке(ИнформацияОВыгрузке, ТекстНачалаСообщения =  "", ТекстОкончанияСообщения = "")
	
	РасширеннаяИнформацияОбОшибке = ИнформацияОбОшибке();
	ТекстСообщения = "";
	Если (Не обЗначениеНеЗаполнено(ТекстНачалаСообщения)) Тогда
		ТекстСообщения = ТекстНачалаСообщения + Символы.ПС;
	КонецЕсли;	
	ТекстСообщения = ТекстСообщения
				   + "Произошла ошибка: "
				   + РасширеннаяИнформацияОбОшибке.Описание;
	
	Если (НЕ Строка(РасширеннаяИнформацияОбОшибке.Причина) = "ИнформацияОбОшибке") Тогда
		ТекстСообщения = ТекстСообщения
					   + ". По причине: "
					   + РасширеннаяИнформацияОбОшибке.Причина;
	КонецЕсли;	
	
	ТекстСообщения = ТекстСообщения + Символы.ПС + ТекстОкончанияСообщения;
	
	СообщитьПользователю(ТекстСообщения, ИнформацияОВыгрузке, СтатусСообщения.Важное, 1);	
	
КонецПроцедуры	

// Выводит сообщение об ошибке и выставляет параметр Отказ в "Истина". 
// В случае работы на клиенте или на сервере выводит в окно сообщений,
// в случае внешнего соединения вызывает исключение.
//
// Параметры:
//  ТекстСообщения - строка, текст сообщения.
//  Отказ          - булево, признак отказа (необязательный).
//
Процедура СообщитьОбОшибке(ТекстСообщения, Отказ = Ложь, Заголовок = "", Статус = Неопределено) Экспорт

	Если (Статус = Неопределено) Тогда
		Статус = СтатусСообщения.Важное;
	КонецЕсли;
	
	ТекстСообщения = СформироватьТекстСообщения(ТекстСообщения);
	Отказ = Истина;
	
	#Если ВнешнееСоединение Тогда	
		Если (НЕ обЗначениеНеЗаполнено(Заголовок)) Тогда
			ТекстСообщения = Заголовок + Символы.ПС + ТекстСообщения;
			Заголовок = "";
		КонецЕсли;
		
		ВызватьИсключение (ТекстСообщения);	
	#Иначе	
		Если ( НЕ обЗначениеНеЗаполнено(Заголовок)) Тогда
			Сообщить(Заголовок);
			Заголовок = "";
		КонецЕсли;	
		Сообщить(ТекстСообщения, Статус);	
	#КонецЕсли
	
КонецПроцедуры // СообщитьОбОшибке()

// Функция убирает из текста сообщения служебную информацию
//
// Параметры
//  ТекстСообщения, Строка, исходный текст сообщения//
// Возвращаемое значение:
//   Строка
//
Функция СформироватьТекстСообщения(Знач ТекстСообщения) Экспорт

	НачалоСлужебногоСообщения    = Найти(ТекстСообщения, "{");
	ОкончаниеСлужебногоСообщения = Найти(ТекстСообщения, "}:");
	
	Если (ОкончаниеСлужебногоСообщения > 0 
		И НачалоСлужебногоСообщения > 0 
		И НачалоСлужебногоСообщения < ОкончаниеСлужебногоСообщения) Тогда
		
		ТекстСообщения = Лев(ТекстСообщения, (НачалоСлужебногоСообщения - 1)) +
		                 Сред(ТекстСообщения, (ОкончаниеСлужебногоСообщения + 2));
						 
	КонецЕсли;
	
	Возврат СокрЛП(ТекстСообщения);

КонецФункции // ()

Процедура ДобавитьТехническуюИнформацияВСообщение(СокращенноеСообщение, СообщениеСТехИнформацией)
	
	Если (СокращенноеСообщение = СообщениеСТехИнформацией) Тогда
		Возврат;
	КонецЕсли;
	
	СокращенноеСообщение = СокращенноеСообщение + Символы.ПС + Символы.ПС + "Техническая информация:" + Символы.ПС + СообщениеСТехИнформацией;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ ЗАГРУЖЕННЫХ ДАННЫХ

Функция ОбработатьНачалоЭлемента(ОбъектCML, Знач ИмяЭлемента, ДеревоДокументов);
	
	Успешно = Истина;
	
	ТекущаяСтрокаДерева  	  = Неопределено;
	ТекущаяСтрокаТоваровУслуг = Неопределено;
	
	КоличествоДокументов		= ДеревоДокументов.Строки.Количество();
	Если ( КоличествоДокументов > 0 ) Тогда
		ТекущаяСтрокаДерева    = ДеревоДокументов.Строки[КоличествоДокументов - 1];
		КоличествоТоваровУслуг = ТекущаяСтрокаДерева.Строки.Количество();
		
		Если ( КоличествоТоваровУслуг > 0 ) Тогда
        	ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];
		КонецЕсли;		
	КонецЕсли;	
	
	Если (ИмяЭлемента = "Документ") Тогда
		
		НовыйДокумент 				   = Документы.ЗаказПокупателя.СоздатьДокумент();
		ОбработкаЗаполненияНовогоОбъекта(НовыйДокумент);
		НовыйДокумент.УстановитьНовыйНомер();
		
		НовСтрокаДерева 			   = ДеревоДокументов.Строки.Добавить();
		НовСтрокаДерева.ДокументОбъект = НовыйДокумент;
		НовСтрокаДерева.СтавкаНДС 	   = Справочники.СтавкиНДС.БезНДС;
	    НовСтрокаДерева.СтруктураДанныхКонтрагента = Новый Структура();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент" ) Тогда
		
		ДеревоАдресов = Новый ДеревоЗначений();
		ДеревоАдресов.Колонки.Добавить("ВидАдреса");
		ДеревоАдресов.Колонки.Добавить("Представление");
		ДеревоАдресов.Колонки.Добавить("Комментарий");
		ДеревоАдресов.Колонки.Добавить("ПолеТип");
		ДеревоАдресов.Колонки.Добавить("ПолеЗначение");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
		ТаблицаКонтактов = Новый ТаблицаЗначений();
		ТаблицаКонтактов.Колонки.Добавить("КонтактТип");
		ТаблицаКонтактов.Колонки.Добавить("КонтактЗначение");
		ТаблицаКонтактов.Колонки.Добавить("КонтактКомментарий");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
		
		ТаблицаКонтактныхЛиц = Новый ТаблицаЗначений();
		ТаблицаКонтактныхЛиц.Колонки.Добавить("Наименование");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);		
	
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева				= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса	= "АдресРегистрации";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 				= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
		НовСтрокаДерева				= СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);		
	
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева				= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса	= "ЮридическийАдрес";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 				= ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
		НовСтрокаДерева 			= СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);		
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес" ) Тогда	
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева	= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса = "Адрес";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле" ) Тогда		
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 	= ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
		НовСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт" ) Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактов", ТаблицаКонтактов);
		ТаблицаКонтактов.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Представители.Представитель.Контрагент" ) Тогда	
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
		ТаблицаКонтактныхЛиц.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
				
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар" ) Тогда
		
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.БазоваяЕдиница" ) Тогда		
		
		Пока ( ОбъектCML.ПрочитатьАтрибут() ) Цикл
			Если ( ОбъектCML.Имя = "Код" ) Тогда
				КодБазовойЕдиницы = ОбъектCML.Значение;
				ТекущаяСтрокаТоваровУслуг.ТоварУслугаБазоваяЕдиницаКод = КодБазовойЕдиницы;
				Прервать;
			КонецЕсли;	
			
		КонецЦикла
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита" ) Тогда
		
		ТекущаяСтрокаТоваровУслуг.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка" ) Тогда
		
		ТекущаяСтрокаТоваровУслуг.Строки.Добавить();
		
	ИначеЕсли ( ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита" ) Тогда
		
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Функция РазобратьCML(СтрокаCML)
	
	Успешно		= Истина;
	ОбъектCML	= Новый ЧтениеXML();
	
	Попытка
		ОбъектCML.УстановитьСтроку(СтрокаCML);
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь);
		Возврат Неопределено;
	КонецПопытки;
	
	ПоследовательностьЭлементов = "";
	
	ДеревоДокументов = Новый ДеревоЗначений();
	
	ДеревоДокументов.Колонки.Добавить("ДокументОбъект");
	ДеревоДокументов.Колонки.Добавить("НомерВходящий");
	ДеревоДокументов.Колонки.Добавить("ДатаВходящегоДокумента");
	ДеревоДокументов.Колонки.Добавить("РанееЗагруженныйДокументСсылка", Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя"));
	ДеревоДокументов.Колонки.Добавить("ЕстьСсылкиНаРанееЗагруженныйДокумент", Новый ОписаниеТипов("Булево"));
	ДеревоДокументов.Колонки.Добавить("СтруктураДанныхКонтрагента");
	ДеревоДокументов.Колонки.Добавить("СтавкаНДС");
	
	// Подчинение документу
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаТипНоменклатуры");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаНаименование");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаБазоваяЕдиницаКод");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаБазоваяЕдиница");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКоэффициент");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаСумма");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКомментарий");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаЦенаЗаЕдиницу");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКоличество");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаИд");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаХарактеристика");
	ДеревоДокументов.Колонки.Добавить("СвойствоНаименование");
	ДеревоДокументов.Колонки.Добавить("СвойствоЗначение");
	
	// Подчинение товарам/услугам
	ДеревоДокументов.Колонки.Добавить("СуммаСкидки");
	ДеревоДокументов.Колонки.Добавить("СкидкаВСумме");
	ДеревоДокументов.Колонки.Добавить("ЗначениеРеквизитаНаименование");
	ДеревоДокументов.Колонки.Добавить("ЗначениеРеквизитаЗначение");
	
	Пока ( Истина ) Цикл	
		ОчереднойУзелCMLПрочитан = Ложь;	
		Попытка
			ОчереднойУзелCMLПрочитан = ОбъектCML.Прочитать();
		Исключение
			Успешно = Ложь;
			СообщитьОбИсключительнойОшибке(Ложь);
			Прервать;
		КонецПопытки;	
		
		Если ( НЕ ОчереднойУзелCMLПрочитан ) Тогда Прервать; КонецЕсли;	
		
		ТипУзла = ОбъектCML.ТипУзла;
		ИмяУзла = ОбъектCML.Имя;
		
		Если (ТипУзла = ТипУзлаXML.НачалоЭлемента) Тогда		
			ПоследовательностьЭлементов = ДобавитьЭлементКПоследовательности(ПоследовательностьЭлементов, ИмяУзла);
			Успешно = ОбработатьНачалоЭлемента(ОбъектCML, ПоследовательностьЭлементов, ДеревоДокументов);
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось обработать начало элемента (" + ПоследовательностьЭлементов + ").", Ложь,,1);
				Прервать;
			КонецЕсли;			
		ИначеЕсли ( ТипУзла = ТипУзлаXML.КонецЭлемента ) Тогда
			ПоследовательностьЭлементов = УдалитьПоследнийЭлементИзПоследовательности(ПоследовательностьЭлементов);		
		ИначеЕсли ( ТипУзла = ТипУзлаXML.Текст ) Тогда			
			ЗначениеЭлемента = ОбъектCML.Значение;  
			Успешно = ОбработатьЗначениеЭлемента(ПоследовательностьЭлементов, ЗначениеЭлемента, ДеревоДокументов);
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось обработать значение элемента (" + ПоследовательностьЭлементов + ") = (" + ЗначениеЭлемента + ").", Ложь,,1);
				Прервать;
			КонецЕсли;			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбъектCML.Закрыть();
	
	Если ( НЕ Успешно ) Тогда ДеревоДокументов = Неопределено; КонецЕсли;	
		
	Возврат ДеревоДокументов;
	
КонецФункции

Функция ДобавитьЭлементКПоследовательности(Знач ПоследовательностьЭлементов, Знач ИмяУзла)
	
	ИсключатьИзПоследовательности	= Новый Массив();
	ИсключатьИзПоследовательности.Добавить("КоммерческаяИнформация");
	
	Если ( ИсключатьИзПоследовательности.Найти(ИмяУзла) = Неопределено ) Тогда		
		Если ( НЕ ПоследовательностьЭлементов = "" ) Тогда
			ПоследовательностьЭлементов = ПоследовательностьЭлементов + ".";	
		КонецЕсли;	
		ПоследовательностьЭлементов = ПоследовательностьЭлементов + ИмяУзла;	
	КонецЕсли;
	
	Возврат ПоследовательностьЭлементов;

КонецФункции

Функция ОбработатьЗначениеЭлемента(Знач ИмяЭлемента, Знач ЗначениеЭлемента, ДеревоДокументов);
	
	Успешно 			 = Истина;
	КоличествоДокументов = ДеревоДокументов.Строки.Количество();
	ИмяТекущегоЭлемента  = ПолучитьИмяЭлементаИзПоследовательности(ИмяЭлемента);
	ДеревоАдресов		 = Неопределено;
	ТаблицаКонтактов	 = Неопределено;
	ТаблицаКонтактныхЛиц = Неопределено;
	
	Если ( КоличествоДокументов > 0 ) Тогда
		
		ТекущаяСтрокаДерева       = ДеревоДокументов.Строки[КоличествоДокументов - 1];
	    ТекущийДокумент 	      = ТекущаяСтрокаДерева.ДокументОбъект;
		
		ТекущаяСтрокаТоваровУслуг = Неопределено;
		ТекущаяСтрокаСкидок 	  = Неопределено;
		
		КоличествоТоваровУслуг    = ТекущаяСтрокаДерева.Строки.Количество();
		
		Если ( КоличествоТоваровУслуг > 0 ) Тогда
			ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];	
			КоличествоСкидок    	  = ТекущаяСтрокаТоваровУслуг.Строки.Количество();			
			Если ( КоличествоСкидок > 0 ) Тогда
				ТекущаяСтрокаСкидок = ТекущаяСтрокаТоваровУслуг.Строки[КоличествоСкидок - 1];	
			КонецЕсли;		
		КонецЕсли;	
		
		Если ( ИмяЭлемента = "Документ.Ид" ) Тогда
		
		ИначеЕсли ( ИмяЭлемента = "Документ.Номер" ) Тогда
			
			ТекущийДокумент.Организация				= Организация; 
			ТекущийДокумент.Автор					= Менеджер;
			ТекущийДокумент.ПодразделениеКомпании	= Подразделение;
			ТекущийДокумент.Комментарий				= "№ "+ЗначениеЭлемента+" "+HTTPОбменСервер;
			ТекущийДокумент.ВидОплаты				= Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			ТекущийДокумент.ТипЦен					= ТипЦены;
			
			ТекущаяСтрокаДерева.НомерВходящий = мПрефикс+ЗначениеЭлемента;
			
			ОтобразитьСостояние("Обработка документа CML: " + мПрефикс + ТекущаяСтрокаДерева.НомерВходящий);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Дата" ) Тогда
			
			ДатаВремя = ОбработатьДатуВремяCML(ЗначениеЭлемента);
			Если ( ЗаписыватьДокументыТекущейДатой ) Тогда
				ТекущийДокумент.Дата = ТекущаяДата();
			Иначе	
				ТекущийДокумент.Дата = ДатаВремя;
			КонецЕсли;	
			ТекущаяСтрокаДерева.ДатаВходящегоДокумента	= Строка(ДатаВремя);
					
		ИначеЕсли ( ИмяЭлемента = "Документ.Время" ) Тогда
			
			Если ( НЕ ЗаписыватьДокументыТекущейДатой ) Тогда
				ТекущийДокумент.Дата						= ОбработатьДатуВремяCML(ЗначениеЭлемента, ТекущийДокумент.Дата);
				ТекущаяСтрокаДерева.ДатаВходящегоДокумента	= Строка(ТекущийДокумент.Дата);		
			КонецЕсли;
												 
		ИначеЕсли ( ИмяЭлемента = "Документ.ХозОперация" ) Тогда
			
			Если ( НЕ ЗначениеЭлемента = "Заказ товара" ) Тогда	
				СообщитьОбОшибкеОбмена("Ошибка в значении узла <Документ>.<ХозОперация> документа CML (" + ЗначениеЭлемента + ").", Ложь);
				Успешно = Ложь;
			Иначе
				ТекущийДокумент.ХозОперация	= Справочники.ХозОперации.ЗаказПокупателя;
			КонецЕсли;	
							
		ИначеЕсли ( ИмяЭлемента = "Документ.Валюта" ) Тогда
			
			ТекущийДокумент.ВалютаДокумента = ОбработатьВалютуCML(ЗначениеЭлемента);
			Если ( обЗначениеНеЗаполнено(ТекущийДокумент.ВалютаДокумента) ) Тогда
				СообщитьОбОшибкеОбмена("Ошибка в значении узла <Документ>.<Валюта> документа CML (" + ЗначениеЭлемента + ").", Ложь);
				Успешно = Ложь;
			КонецЕсли;	
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Курс" ) Тогда
			
			КурсCML							= Число(ЗначениеЭлемента);
			ТекущийДокумент.КурсДокумента	= КурсCML; //КурсВзаиморасчетов
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Сумма" ) Тогда
							
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Наименование" ) Тогда
			
      		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Наименование", ЗначениеЭлемента); 
							
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Роль" ) Тогда	
			
			Если ( ЗначениеЭлемента = "Покупатель" ) Тогда
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ВидКонтрагента", Перечисления.ВидыКонтрагентов.Покупатель);				
			Иначе
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ВидКонтрагента", Перечисления.ВидыКонтрагентов.Прочее);	
			КонецЕсли;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Комментарий" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Комментарий", ЗначениеЭлемента); 
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ПолноеНаименование"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.ОфициальноеНаименование" ) Тогда

			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("НаименованиеПолное", ЗначениеЭлемента); 
			
			// ЮрФизЛицо
			Если ИмяЭлемента = "Документ.Контрагенты.Контрагент.ПолноеНаименование" Тогда
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ФормаСобственности", Перечисления.ФормыСобственности.ЧастноеЛицо);
			Иначе
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ФормаСобственности", Перечисления.ФормыСобственности.ЮридическоеЛицо);
			КонецЕсли;
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Фамилия" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Фамилия", ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Имя" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Имя", ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Отчество" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Отчество", ЗначениеЭлемента);			
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ДатаРождения" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДатаРождения", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.МестоРождения" Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("МестоРождения", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Пол" Тогда
			
			Если ( Булево(Найти(ЗначениеЭлемента,"уж")) ) Тогда
				ПолФизЛица	= Перечисления.ПолФизическихЛиц.Мужской;	
			ИначеЕсли ( Булево(Найти(ЗначениеЭлемента,"ен")) ) Тогда
				ПолФизЛица	= Перечисления.ПолФизическихЛиц.Женский;	
			Иначе
				ПолФизЛица	= Перечисления.ПолФизическихЛиц.НеУказан;	
			КонецЕсли;		
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Пол", ПолФизЛица); 
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ИНН" ) Тогда
			
			Если ( НЕ ПустаяСтрока(ЗначениеЭлемента) ) Тогда
				ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ИНН", ЗначениеЭлемента);
			КонецЕсли;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.КПП" ) Тогда		
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("КПП", ЗначениеЭлемента);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.ВидДокумента"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.Серия"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.Номер"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.ДатаВыдачи"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.КемВыдан" ) Тогда		
			
			СохраненноеЗначение = "";
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДокументУдостоверяющийЛичность", СохраненноеЗначение); 
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДокументУдостоверяющийЛичность", СохраненноеЗначение + ЗначениеЭлемента + " "); 
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.Представление"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.Комментарий" ) Тогда
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева						= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента]	= ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле.Тип"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле.Значение" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева			= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
			СтрокаАдресногоПоля		= СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
					
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.Представление"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.Комментарий" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле.Тип"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле.Значение" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.КПП" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("КПП", ЗначениеЭлемента); 

		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.ОсновнойВидДеятельности" ) Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ОсновнойВидДеятельности", ЗначениеЭлемента); 
							
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Представление"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Комментарий" ) Тогда	
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле.Тип"	
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле.Значение" ) Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Тип" 
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Значение"
			  ИЛИ ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Комментарий" ) Тогда		
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактов", ТаблицаКонтактов);
			СтрокаТаблицы = ТаблицаКонтактов[ТаблицаКонтактов.Количество() - 1];
			СтрокаТаблицы["Контакт" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Контрагенты.Контрагент.Представители.Представитель.Контрагент.Наименование" ) Тогда		
			  
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
			СтрокаТаблицы		= ТаблицаКонтактныхЛиц[ТаблицаКонтактныхЛиц.Количество() - 1];
			СтрокаТаблицы["Наименование"] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Комментарий" ) Тогда	
			
			ТекущийДокумент.Комментарий = ТекущийДокумент.Комментарий + ": " + ЗначениеЭлемента;
						
		ИначеЕсли ИмяЭлемента = "Документ.Налоги.Налог.УчтеноВСумме" Тогда
			
			//ТекущийДокумент.СуммаВключаетНДС = Ложь;
			//Если ( ЗначениеЭлемента = БулевоЗначениеCML_Истина ) Тогда
			//	ТекущийДокумент.СуммаВключаетНДС = Истина;
			//КонецЕсли;	
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Налоги.Налог.Ставка" ) Тогда
			
			//ТекущийДокумент.УчитыватьНДС = ЗначениеЗаполнено(ЗначениеЭлемента);
			//Если ( ТекущийДокумент.УчитыватьНДС ) Тогда
			//	ТекущаяСтрокаДерева.СтавкаНДС = ПолучитьПоЗначениюДляВыгрузкиСтавкуНДС(ЗначениеЭлемента);						
			//КонецЕсли;	
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.СтавкиНалогов.СтавкаНалога.Ставка" Тогда
			
			//ТекущийДокумент.УчитыватьНДС = ЗначениеЗаполнено(ЗначениеЭлемента);
			//Если ( ТекущийДокумент.УчитыватьНДС ) Тогда			
			//	ТекущаяСтрокаТоваровУслуг.СтавкаНДС = ПолучитьПоЗначениюДляВыгрузкиСтавкуНДС(ЗначениеЭлемента);						
			//КонецЕсли;			
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Скидки.Скидка.Наименование" ) Тогда
			
			ТекущийДокумент.СкидкаНаценка	= Справочники.ТипыСкидок.НайтиПоНаименованию(ЗначениеЭлемента);		
				
		ИначеЕсли ( ИмяЭлемента = "Документ.Скидки.Скидка.Сумма" ) Тогда
			 
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаДерева.СуммаСкидки = СуммаЧисло;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Скидки.Скидка.УчтеноВСумме" ) Тогда
			
			ТекущаяСтрокаДерева.СкидкаВСумме = Ложь;
			Если ( ЗначениеЭлемента = БулевоЗначениеCML_Истина ) Тогда
				ТекущаяСтрокаДерева.СкидкаВСумме = Истина;
			КонецЕсли;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Ид"
		      ИЛИ ИмяЭлемента = "Документ.Товары.Товар.Наименование" ) Тогда
			  
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		ИначеЕсли (ИмяЭлемента = "Документ.Товары.Товар.БазоваяЕдиница") Тогда 	
			
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента]	= ЗначениеЭлемента;
			ТекущаяСтрокаТоваровУслуг["ТоварУслугаКоэффициент"]				= 1;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.ЦенаЗаЕдиницу"
			  ИЛИ ИмяЭлемента = "Документ.Товары.Товар.Сумма"
		      ИЛИ ИмяЭлемента = "Документ.Товары.Товар.Количество" ) Тогда
			  
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента] = Число(ЗначениеЭлемента);			  
						
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита.Наименование"
			  ИЛИ ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита.Значение" ) Тогда
			  
			ТекущаяСтрокаСкидок["ЗначениеРеквизита" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
						
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка.Сумма" ) Тогда
			
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаСкидок.СуммаСкидки = СуммаЧисло;
			
		ИначеЕсли ( ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка.УчтеноВСумме" ) Тогда
			
			ТекущаяСтрокаСкидок.СкидкаВСумме = Ложь;
			Если ЗначениеЭлемента = БулевоЗначениеCML_Истина Тогда
				ТекущаяСтрокаСкидок.СкидкаВСумме = Истина;
			КонецЕсли;	
			
		ИначеЕсли ( ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита.Наименование"
			  ИЛИ ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита.Значение" ) Тогда
			
			ТекущаяСтрокаТоваровУслуг["Свойство" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат Успешно;

КонецФункции

Функция УдалитьПоследнийЭлементИзПоследовательности(Знач ПоследовательностьЭлементов);
	
	ПромСтрока 			= СтрЗаменить(ПоследовательностьЭлементов, ".", Символы.ПС);
	КоличествоЭлементов = СтрЧислоСтрок(ПромСтрока);
	
	ПоследовательностьЭлементов	= "";
	Если ( КоличествоЭлементов > 0 ) Тогда
		КоличествоЭлементов = КоличествоЭлементов - 1;
		Для Счетчик = 1 По КоличествоЭлементов Цикл
			ПоследовательностьЭлементов	= ПоследовательностьЭлементов + "." + СтрПолучитьСтроку(ПромСтрока, Счетчик);
		КонецЦикла;	
		ПоследовательностьЭлементов = Прав(ПоследовательностьЭлементов, СтрДлина(ПоследовательностьЭлементов) - 1);
	КонецЕсли;
	
	Возврат ПоследовательностьЭлементов;
КонецФункции

Функция ПолучитьИмяЭлементаИзПоследовательности(Знач ПоследовательностьЭлементов)
	
	ПромСтрока 			= СтрЗаменить(ПоследовательностьЭлементов, ".", Символы.ПС);
	КоличествоЭлементов = СтрЧислоСтрок(ПромСтрока);
	
	ИмяПоследнегоЭлемента = "";
	Если ( КоличествоЭлементов > 0 ) Тогда
		ИмяПоследнегоЭлемента = СтрПолучитьСтроку(ПромСтрока, КоличествоЭлементов);
	КонецЕсли;
	
	Возврат ИмяПоследнегоЭлемента;
	
КонецФункции

Функция ОбработатьДокументы(ДеревоДокументов, КоличествоОбработанныхДокументов)
	
	Успешно							= Истина;
	КоличествоОбработанныхДокументов= 0;
	
	СтруктураСтатистики = Новый Структура();
	СтруктураСтатистики.Вставить("Создано"  , 0);
	СтруктураСтатистики.Вставить("Обновлено", 0);
	СтруктураСтатистики.Вставить("Пропущено", 0);
	СтруктураСтатистики.Вставить("ОплаченСписок", Новый Массив());
	СтруктураСтатистики.Вставить("ДоставкаРазрешенаСписок", Новый Массив());
	СтруктураСтатистики.Вставить("ФинальныйСтатусСписок", Новый Массив());
	
	ОтобразитьСостояние("Поиск ранее загруженных документов...");
	
	ПолучитьРанееЗагруженныеДокументы(ДеревоДокументов);
	
	//НачатьТранзакцию();
	
	Успешно = ИдентифицироватьКонтрагентов(ДеревоДокументов); 
	
	Если НЕ Успешно Тогда
		//ОтменитьТранзакцию();
		СообщитьПользователю("Не удалось найти/создать контрагента.", Ложь,,1);
		Возврат Ложь;
	КонецЕсли;
		
	Успешно = ИдентифицироватьНоменклатуру(ДеревоДокументов);
	Если НЕ Успешно Тогда
		//ОтменитьТранзакцию();
		СообщитьПользователю("Не удалось найти/создать номенклатуру.", Ложь,,1);
		Возврат Ложь;
	КонецЕсли;
	
	МассивДокументовДляПроведения = Новый Массив();
	Успешно		= СоздатьОбновитьДокументы(ДеревоДокументов, СтруктураСтатистики, МассивДокументовДляПроведения, мМассивЗагруженныхДокументов);	
	Если ( НЕ Успешно ) Тогда
		СообщитьПользователю("Не удалось создать/обновить документы.", Ложь,,1);
		Возврат Ложь;
	КонецЕсли;
	
	МассивОтклоненныхДокументов = Новый Массив();
	Успешно = ЗаписатьСвойстваДокументов(ДеревоДокументов, СтруктураСтатистики, МассивОтклоненныхДокументов);
	Если ( НЕ Успешно ) Тогда
		СообщитьПользователю("Не удалось записать свойства документов.", Ложь,,1);
		Возврат Ложь;
	КонецЕсли;
			
	//ЗафиксироватьТранзакцию();
	
	ОтобразитьСостояние("Проведение загруженных документов...");

	// если документы нужно проводить, то попытаемся их провести
	РежимПроведения = ?(ПроводитьДокументыОперативно, РежимПроведенияДокумента.Оперативный, РежимПроведенияДокумента.Неоперативный);
	Для Каждого ДокументПроведения Из МассивДокументовДляПроведения Цикл
		
		Если МассивОтклоненныхДокументов.Найти(ДокументПроведения.Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка			
			ДокументПроведения.ПометкаУдаления = Ложь;
			ДокументПроведения.ОбменДанными.Загрузка = Ложь;
			ДокументПроведения.Записать(РежимЗаписиДокумента.Проведение, РежимПроведения);							
		Исключение
			СообщитьОбИсключительнойОшибке(Ложь, "Ошибка при проведении документа: " + Строка(ДокументПроведения) + Символы.ПС + ОписаниеОшибки());
			Возврат Ложь;		
		КонецПопытки;
		
	КонецЦикла;
	
	Для Каждого ДокументПроведения Из МассивОтклоненныхДокументов Цикл
		
		Попытка	
			ДокОбъект = ДокументПроведения.ПолучитьОбъект();
			ДокОбъект.ПометкаУдаления = Истина;
			ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);						
		Исключение
			СообщитьОбИсключительнойОшибке(Ложь, "Ошибка при отмене проведения документа: " + Строка(ДокументПроведения) + Символы.ПС + ОписаниеОшибки());
			Возврат Ложь;			
		КонецПопытки;
		
	КонецЦикла;
	
	// не нужно регистрировать изменения для только что загруженных документов
	Если ( мМассивЗагруженныхДокументов.Количество() <> 0 ) Тогда
		МассивУзлов = ПолучитьМассивУзловДляРегистрации(Ложь);
		
		Для Каждого СсылкаНаДокумент Из мМассивЗагруженныхДокументов Цикл
			ПланыОбмена.УдалитьРегистрациюИзменений(МассивУзлов, СсылкаНаДокумент);
		КонецЦикла;				
	КонецЕсли;
	
	Отступ = Символы.НПП + Символы.НПП;
	КоличествоОбработанныхДокументов = ДеревоДокументов.Строки.Количество();
		
	Если ( КоличествоОбработанныхДокументов > 0 ) Тогда
		
		ТекстСообщения = "Успешно получено и обработано документов: " + Строка(КоличествоОбработанныхДокументов);
	
	
		ТекстСообщения = ТекстСообщения + Символы.ПС + "Список обработанных документов: ";		
		Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
			ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + СтрокаДД.ДокументОбъект.Ссылка;
		КонецЦикла;
		
		ТекстСообщения = ТекстСообщения + Символы.ПС + "В том числе:";
		Если СтруктураСтатистики.Создано <> 0 Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + "создано новых: " + СтруктураСтатистики.Создано;
		КонецЕсли;
		
		Если ( СтруктураСтатистики.Обновлено <> 0 ) Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + "обновлено: " + СтруктураСтатистики.Обновлено;
		КонецЕсли;
		
		Если ( СтруктураСтатистики.Пропущено <> 0 ) Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + "пропущено: " + СтруктураСтатистики.Пропущено;
		КонецЕсли;
		
		Если ( СтруктураСтатистики.ОплаченСписок.Количество() > 0 ) Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + "Получено оплаченных документов: " + СтруктураСтатистики.ОплаченСписок.Количество();
			Для Каждого Док Из СтруктураСтатистики.ОплаченСписок Цикл
				ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + Док;
			КонецЦикла;
		КонецЕсли;	
		
		Если ( СтруктураСтатистики.ДоставкаРазрешенаСписок.Количество() > 0 ) Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + "Получено документов с разрешенной доставкой: " + СтруктураСтатистики.ДоставкаРазрешенаСписок.Количество();
			Для Каждого Док Из СтруктураСтатистики.ДоставкаРазрешенаСписок Цикл
				ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + Док;
			КонецЦикла;
		КонецЕсли;	
		
		Если ( СтруктураСтатистики.ФинальныйСтатусСписок.Количество() > 0 ) Тогда
			ТекстСообщения = ТекстСообщения + Символы.ПС + "Получено документов в финальном статусе: " + СтруктураСтатистики.ФинальныйСтатусСписок.Количество();
			Для Каждого Док Из СтруктураСтатистики.ФинальныйСтатусСписок Цикл
				ТекстСообщения = ТекстСообщения + Символы.ПС + Отступ + Док;
			КонецЦикла;
		КонецЕсли;
		
		СообщитьПользователю(ТекстСообщения, Ложь,,1);
		
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Процедура ПолучитьРанееЗагруженныеДокументы(ДеревоДокументов)
	
	МассивНомеров = ДеревоДокументов.Строки.ВыгрузитьКолонку("НомерВходящий");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивНомеров", МассивНомеров);
	// !!! misely Запрос.УстановитьПараметр("ИмяСвойства", "Входящий номер");
	
	Запрос.Текст = 
	//"ВЫБРАТЬ
	//|	ЗначенияСвойствОбъектов.Объект.Ссылка КАК ДокументСсылка,
	//|	ЗначенияСвойствОбъектов.Значение КАК НомерВходящий
	//|ИЗ
	//|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	//|ГДЕ
	//|	ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ЗаказПокупателя
	//|	И ЗначенияСвойствОбъектов.Свойство.Наименование = &ИмяСвойства
	//|	И ЗначенияСвойствОбъектов.Значение В(&МассивНомеров)
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	ЗначенияСвойствОбъектов.Объект.Ссылка,
	//|	ЗначенияСвойствОбъектов.Значение"; 		   
	// + !!! misely
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК ДокументСсылка,
	|	ЗаказПокупателя.GUIDСайта КАК НомерВходящий
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.GUIDСайта В(&МассивНомеров)";
	// - !!! misely
	РезультатЗапроса = Запрос.Выполнить();	
	Если ( РезультатЗапроса.Пустой() ) Тогда
		Возврат;
	КонецЕсли;
	
	МассивНайденныхСсылокНаДокументы = Новый Массив();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл              
		СтрокаДД = ДеревоДокументов.Строки.Найти(Выборка.НомерВходящий, "НомерВходящий");
		СтрокаДД.РанееЗагруженныйДокументСсылка = Выборка.ДокументСсылка;				
		МассивНайденныхСсылокНаДокументы.Добавить(Выборка.ДокументСсылка);	
	КонецЦикла;
	
	
	// ищем ссылки на ранее загруженные документы
	ТаблицаСсылок = НайтиПоСсылкам(МассивНайденныхСсылокНаДокументы);
		
	Для Каждого СтрокаТС Из ТаблицаСсылок Цикл
		
		Если ( Метаданные.РегистрыСведений.Содержит(СтрокаТС.Метаданные)
			ИЛИ СтрокаТС.Данные = СтрокаТС.Ссылка ) Тогда	
			Продолжить;	
		КонецЕсли;
		
		СтрокаДД = ДеревоДокументов.Строки.Найти(СтрокаТС.Ссылка, "РанееЗагруженныйДокументСсылка");
		Если СтрокаДД<>Неопределено Тогда
			СтрокаДД.ЕстьСсылкиНаРанееЗагруженныйДокумент = Истина;
		КонецЕсли;
		
	КонецЦикла;	
	
	
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		Если ( обЗначениеНеЗаполнено(СтрокаДД.РанееЗагруженныйДокументСсылка) ) Тогда
			Продолжить;
		КонецЕсли;
			
		ДокументОбъект = СтрокаДД.РанееЗагруженныйДокументСсылка.ПолучитьОбъект();

		Если ( НЕ СтрокаДД.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, СтрокаДД.ДокументОбъект, , "Номер"); 
			ДокументОбъект.Товары.Очистить();
			
		КонецЕсли;	
		
		СтрокаДД.ДокументОбъект = ДокументОбъект;		
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИдентифицироватьКонтрагентов(ДеревоДокументов)
	
	Успешно = Истина;
	
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		Если ( СтрокаДД.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ТипЗнч(СтрокаДД.СтруктураДанныхКонтрагента)=Тип("Структура") И СтрокаДД.СтруктураДанныхКонтрагента.Свойство("Наименование") Тогда
			НаименованиеКонтрагента	= СтрокаДД.СтруктураДанныхКонтрагента.Наименование;
			ОтобразитьСостояние("Идентификация контрагента: " + СтрокаДД.СтруктураДанныхКонтрагента.Наименование);
		Иначе
			НаименованиеКонтрагента	= "";
		КонецЕсли;
		
		Запрос = Новый Запрос();
		
		ИНН = "";
		СтрокаДД.СтруктураДанныхКонтрагента.Свойство("ИНН", ИНН);
		Запрос.УстановитьПараметр("Наименование", НаименованиеКонтрагента);
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка Как Контрагент
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|";
			
		НуженПоискПоИнн = (СпособИдентификацииКонтрагентов = "ИНН") И ( НЕ обЗначениеНеЗаполнено(ИНН));
			
		Если ( НуженПоискПоИнн ) Тогда
			Запрос.Текст = Запрос.Текст + " Контрагенты.ИНН = &ИНН ";
		Иначе
			Запрос.Текст = Запрос.Текст + " Контрагенты.Наименование = &Наименование ";
		КонецЕсли;
				
		Выборка = Запрос.Выполнить().Выбрать();	
		Если (Выборка.Следующий()) Тогда
			КонтрагентСсылка	= Выборка.Контрагент;
			КонтрагентОбъект		= КонтрагентСсылка.ПолучитьОбъект();
		Иначе
			КонтрагентОбъект = СоздатьКонтрагента(СтрокаДД.СтруктураДанныхКонтрагента);
			КонтрагентСсылка = КонтрагентОбъект.Ссылка;
		КонецЕсли;
		
		Если (обЗначениеНеЗаполнено(КонтрагентСсылка)) Тогда
			Успешно = Ложь;
			Прервать;
		КонецЕсли;
		
		ЗаполнитьПодчиненныеДанныеКонтрагента(КонтрагентСсылка, СтрокаДД.СтруктураДанныхКонтрагента);
		
		Док				= СтрокаДД.ДокументОбъект;
		Док.Контрагент	= КонтрагентСсылка;
		
		ДопПараметры	= Новый Структура(); 		
		ЕстьТипЦен		= (Док.Метаданные().Реквизиты.Найти("ТипЦен")<>Неопределено);
		
		//ЗаказыНаОтгрузку	 
		ВидДоговора	= Перечисления.ВидыДоговоров.Продажа;
		ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
		ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());		 
		ДопПараметры.Вставить("ТипЦенПродажи",?(ЕстьТипЦен,Док.ТипЦен,Справочники.ТипыЦен.ПустаяСсылка()));			  
	
		Попытка
			ТекущийДоговор				= Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Док.Контрагент,ВидДоговора,Док,ДопПараметры);
			Док.ДоговорВзаиморасчетов	= ТекущийДоговор;
		Исключение
			Успешно	= Ложь;
			СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки(), "Не удалось получить договор взаиморасчетов для контрагента"+Строка(КонтрагентСсылка.Наименование));
			Прервать;
		КонецПопытки;
		
		Если (обЗначениеНеЗаполнено(Док.ДоговорВзаиморасчетов)) Тогда						
				Док.ДоговорВзаиморасчетов = СоздатьДоговорПоПараметрам(Док, Док.Контрагент, Док.Организация, Док.ВалютаДокумента);	
		КонецЕсли;
				
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция СоздатьДоговорПоПараметрам(Док, Контрагент, Организация, ВалютаВзаиморасчетов)
	
	НайденныйДоговорОбъект = Справочники.ДоговорыВзаиморасчетов.СоздатьЭлемент();
	ОбработкаЗаполненияНовогоОбъекта(НайденныйДоговорОбъект);
	НайденныйДоговорОбъект.УстановитьНовыйКод();
	
	Если ( обЗначениеНеЗаполнено(ВалютаВзаиморасчетов) ) Тогда
		ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
	НайденныйДоговорОбъект.Наименование	= "Договор в "+ВалютаВзаиморасчетов+" от "+Док.Дата;
	
	НайденныйДоговорОбъект.ВалютаВзаиморасчетов		= ВалютаВзаиморасчетов;
	НайденныйДоговорОбъект.Организация				= Организация;
	НайденныйДоговорОбъект.Подразделение			= Подразделение;
	НайденныйДоговорОбъект.ДатаНачала				= Док.Дата;
	НайденныйДоговорОбъект.Владелец           		= Контрагент;
	НайденныйДоговорОбъект.ВидДоговора 				= Перечисления.ВидыДоговоров.Продажа;
	НайденныйДоговорОбъект.ВидОплаты				= Док.ВидОплаты;
	НайденныйДоговорОбъект.АвтоЗакрытиеСделок		= обПраво("АвтоЗакрытиеСделок",,,Организация);
	НайденныйДоговорОбъект.ЕдиницаИзмеренияАвтоработВПечатныхФормах = Перечисления.ЕдиницаИзмеренияАвтоработВПечатныхФормах.ЕдиницаИзмеренияЧас;
	
	ПоискПоВидуДеятельности = (НЕ Док = Неопределено) И зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон");
	
	Если ПоискПоВидуДеятельности И Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.Документооборот.Подсистемы.Автосалон.Состав.Содержит(Док.Метаданные()) Тогда
		НайденныйДоговорОбъект.ДляАвтосалона  = Истина;
	Иначе
		НайденныйДоговорОбъект.ДляАвтосервиса = Истина;
	КонецЕсли;
	
	Попытка
		НайденныйДоговорОбъект.Записать();
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки(), "Не удалось записать договор контрагента.");
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НайденныйДоговорОбъект.Ссылка;
		
КонецФункции

Функция СоздатьКонтрагента(СтруктураДанныхКонтрагента)
	
	НовыйКонтрагент				= Справочники.Контрагенты.СоздатьЭлемент();
	ОбработкаЗаполненияНовогоОбъекта(НовыйКонтрагент);
	НовыйКонтрагент.УстановитьНовыйКод();
	НовыйКонтрагент.Родитель	= ГруппаДляНовыхКонтрагентов;
	ЗаполнитьЗначенияСвойств(НовыйКонтрагент, СтруктураДанныхКонтрагента);
	
	СтрокаФИО = ПолучитьПоСтруктуреСтрокиСФИО(СтруктураДанныхКонтрагента);
	
	Если ( Не ПустаяСтрока(СтрокаФИО) И СтрокаФИО <> НовыйКонтрагент.НаименованиеПолное ) Тогда	
		НовыйКонтрагент.НаименованиеПолное = НовыйКонтрагент.НаименованиеПолное + " [" + СтрокаФИО + "]";	
	КонецЕсли;	
	
	Если Не ПустаяСтрока(СтрокаФИО) И НовыйКонтрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		ПозицияПробела = Найти(СтрокаФИО," ");
		Если ПозицияПробела = 0 Тогда
			НовыйКонтрагент.Фамилия = СтрокаФИО;
		Иначе
			НовыйКонтрагент.Фамилия = СокрЛП(Лев(СтрокаФИО,ПозицияПробела));
		КонецЕсли;
	ИначеЕсли ПустаяСтрока(СтрокаФИО) И НовыйКонтрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		НовыйКонтрагент.Фамилия = "Данные не указаны.";
	КонецЕсли;
	
	Попытка
		НовыйКонтрагент.Записать();
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
	КонецПопытки;	
	
	Возврат НовыйКонтрагент.Ссылка;
	
КонецФункции

Функция ОпределитьПоТипуИмяПоляКонтактнойИнформации(ИмяТипа)
	
	ИмяПоля = "Поле10";
	
	Если ( ИмяТипа = "Почтовый индекс"
		ИЛИ ИмяТипа = "Страна") Тогда
		ИмяПоля = "Поле1";
	ИначеЕсли ( ИмяТипа = "Регион" ) Тогда
		ИмяПоля = "Поле2";
	ИначеЕсли ( ИмяТипа = "Район" ) Тогда
		ИмяПоля = "Поле3";
	ИначеЕсли ( ИмяТипа = "Населенный пункт" ) Тогда
		ИмяПоля = "Поле4";
	ИначеЕсли ( ИмяТипа = "Город" ) Тогда
		ИмяПоля = "Поле5";
	ИначеЕсли ( ИмяТипа = "Улица" ) Тогда
		ИмяПоля = "Поле6";
	ИначеЕсли ( ИмяТипа = "Дом" ) Тогда
		ИмяПоля = "Поле7";
	ИначеЕсли ( ИмяТипа = "Корпус" ) Тогда
		ИмяПоля = "Поле8";
	ИначеЕсли ( ИмяТипа = "Квартира" ) Тогда
		ИмяПоля = "Поле9";
	Конецесли;		
	
	Возврат ИмяПоля;
	
КонецФункции

Процедура ОпределитьПоданнымТипВидКонтактнойИнформации(КонтактТип, ТипКИ, ВидКИ)
	
	Если ( КонтактТип = "ТелефонРабочий" ) Тогда
		
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон;
		ВидКИ = Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный;
		
	ИначеЕсли ( КонтактТип = "Почта" ) Тогда
		
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		ВидКИ = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодчиненныеДанныеКонтрагента(КонтрагентСсылка, СтруктураДанныхКонтрагента)
	
	//Адреса
	ДеревоАдресов	= СтруктураДанныхКонтрагента.ДеревоАдресов;
	Для Каждого СтрокаДерева Из ДеревоАдресов.Строки Цикл
		
		НаборКИ	= РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборКИ.Отбор.Объект.Установить(КонтрагентСсылка);
		НаборКИ.Отбор.Тип.Установить(Перечисления.ТипыКонтактнойИнформации.Адрес);
		НаборКИ.Отбор.Вид.Установить(Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		
		СтрокаКи				= НаборКИ.Добавить();
		СтрокаКи.Объект			= КонтрагентСсылка;
		СтрокаКи.Тип			= Перечисления.ТипыКонтактнойИнформации.Адрес;
		СтрокаКи.Вид			= Справочники.ВидыКонтактнойИнформации.АдресЮридический;
		
		СтрокаКи.Комментарий	= СтрокаДерева.Комментарий;
		СтрокаКи.Представление	= СтрокаДерева.Представление;
		
		УточнениеСтрокой	= "";
		Для Каждого СтрокаУточнения Из СтрокаДерева.Строки Цикл
			ИмяПоля				= ОпределитьПоТипуИмяПоляКонтактнойИнформации(СтрокаУточнения.ПолеТип);
			СтрокаКи[ИмяПоля]	= СтрокаУточнения.ПолеЗначение;
			УточнениеСтрокой	= УточнениеСтрокой + СтрокаУточнения.ПолеЗначение + ";";
		КонецЦикла;
		
		Если ПустаяСтрока(СтрокаКи.Представление)
			И (НЕ ПустаяСтрока(УточнениеСтрокой)) Тогда
			СтрокаКи.Представление	= УточнениеСтрокой;
		КонецЕсли;
		
		Попытка
			НаборКИ.Записать();
		Исключение КонецПопытки;
		
	КонецЦикла;	
	
	//контакты
	ТаблицаКонтактов	= СтруктураДанныхКонтрагента.ТаблицаКонтактов;
	Для Каждого СтрокаДерева Из ТаблицаКонтактов Цикл
		
		//КонтактТип
		//КонтактЗначение
		//КонтактКомментарий
		
		ТипКИ = Неопределено;
		ВидКИ = Неопределено;
		
		ОпределитьПоданнымТипВидКонтактнойИнформации(СтрокаДерева.КонтактТип, ТипКИ, ВидКИ);
		
		Если ( обЗначениеНеЗаполнено(ТипКИ) ИЛИ обЗначениеНеЗаполнено(ВидКИ) ) Тогда
			Продолжить;		
		КонецЕсли;
		
		НаборКИ	= РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборКИ.Отбор.Объект.Установить(КонтрагентСсылка);
		НаборКИ.Отбор.Тип.Установить(ТипКИ);
		НаборКИ.Отбор.Вид.Установить(ВидКИ);
		
		СтрокаКи			= НаборКИ.Добавить();
		СтрокаКи.Объект		= КонтрагентСсылка;
		СтрокаКи.Тип		= ТипКИ;
		СтрокаКи.Вид		= ВидКИ;
		
		СтрокаКи.Комментарий	= СтрокаДерева.КонтактКомментарий;
		СтрокаКи.Представление	= СтрокаДерева.КонтактЗначение;
		
		Попытка
			НаборКИ.Записать();				
		Исключение	КонецПопытки;
	
	КонецЦикла;
	
	// контактные лица
	ТаблицаКонтактныхЛиц	= СтруктураДанныхКонтрагента.ТаблицаКонтактныхЛиц;
	ТаблицаКонтактныхЛиц.Свернуть("Наименование");
	Для Каждого СтрокаКЛ Из ТаблицаКонтактныхЛиц Цикл
		
		ЗапросКЛ = Новый Запрос;
		ЗапросКЛ.Текст =
		"ВЫБРАТЬ
		|	КонтактныеЛицаКонтрагентов.Ведомый
		|ИЗ
		|	РегистрСведений.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
		|ГДЕ
		|	КонтактныеЛицаКонтрагентов.Ведущий = &Ведущий
		|	И КонтактныеЛицаКонтрагентов.Использование = ИСТИНА
		|	И КонтактныеЛицаКонтрагентов.Ведомый.Наименование = &Наименование";
		ЗапросКЛ.УстановитьПараметр("Ведущий", КонтрагентСсылка);
		ЗапросКЛ.УстановитьПараметр("Наименование", СтрокаКЛ.Наименование);
		ВыборкаКЛ = ЗапросКЛ.Выполнить().Выбрать();
		
		КонтактноеЛицоКонтрагента = ?(ВыборкаКЛ.Следующий(), ВыборкаКЛ.Ведомый, Неопределено);
		
		Если ЗначениеЗаполнено(КонтактноеЛицоКонтрагента) Тогда
			Продолжить;
		КонецЕсли;
		
		Элемент = Справочники.Контрагенты.СоздатьЭлемент();
		ОбработкаЗаполненияНовогоОбъекта(Элемент);
		Элемент.УстановитьНовыйКод();
		
		Элемент.Родитель           = ГруппаДляНовыхКонтрагентов;
		Элемент.Наименование       = СтрокаКЛ.Наименование;
		Элемент.НаименованиеПолное = СтрокаКЛ.Наименование;
		
		// заполним фио из наименования
		Если Элемент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
			НаменованиеСтр = СтрокаКЛ.Наименование;
			Если Не ПустаяСтрока(НаменованиеСтр) Тогда
				ПозицияПробела = Найти(НаменованиеСтр," ");
				Если ПозицияПробела = 0 Тогда
					Элемент.Фамилия = НаменованиеСтр;
				Иначе
					Элемент.Фамилия = СокрЛП(Лев(НаменованиеСтр,ПозицияПробела));
				КонецЕсли;
			ИначеЕсли ПустаяСтрока(НаменованиеСтр) Тогда
				Элемент.Фамилия = "Данные не указаны.";
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			Элемент.Записать();
			ТаблицаКЛ = РаботаСКонтактнымиЛицами.ИнициализироватьТаблицуЗаписей();
			Запись = ТаблицаКЛ.Добавить();
			Запись.Ведущий       = КонтрагентСсылка;
			Запись.Ведомый       = Элемент.Ссылка;
			Запись.Использование = Истина;
			Запись.ВидОтношений  = Справочники.ВидыВзаимоотношений.КонтактноеЛицо;
			
			РаботаСКонтактнымиЛицами.ЗаписатьВедущих(Элемент.Ссылка, ТаблицаКЛ);
		Исключение
			СообщитьПользователю(ОписаниеОшибки(), Истина);
		КонецПопытки;		
	КонецЦикла;
	
	//свойства
		
КонецПроцедуры

Функция ОпределитьТипНоменклатурыПоВиду(ВидНоменклатуры)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТипыНоменклатуры.Ссылка КАК ТипНоменклатуры
		|ИЗ
		|	Справочник.ТипыНоменклатуры КАК ТипыНоменклатуры
		|ГДЕ
		|	ТипыНоменклатуры.ВидНоменклатуры = &ВидНоменклатуры";
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ( РезультатЗапроса.Пустой() ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].ТипНоменклатуры;
		
КонецФункции

Функция ИнициализироватьВидыНоменкалатуры()
	
	Если ( Не обЗначениеНеЗаполнено(мТипНоменклатурыТовар)
		И Не обЗначениеНеЗаполнено(мТипНоменклатурыУслуга) ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	мТипНоменклатурыТовар	= ОпределитьТипНоменклатурыПоВиду(Перечисления.ВидыНоменклатуры.Товар);
	мТипНоменклатурыУслуга	= ОпределитьТипНоменклатурыПоВиду(Перечисления.ВидыНоменклатуры.Услуга);	
	
	Если ( Не обЗначениеНеЗаполнено(мТипНоменклатурыТовар)
		И Не обЗначениеНеЗаполнено(мТипНоменклатурыУслуга) ) Тогда
		Возврат Истина;	
	КонецЕсли;
	
	Если ( обЗначениеНеЗаполнено(мТипНоменклатурыТовар) ) Тогда
		СообщитьПользователю("Не удалось найти тип номенклатуры: Товар", Ложь,,1);		
	КонецЕсли;
	
	Если ( обЗначениеНеЗаполнено(мТипНоменклатурыУслуга) ) Тогда
		СообщитьПользователю("Не удалось найти тип номенклатуры: Услуга", Ложь,,1);	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьВидНоменклатуры(ИмяВидаНоменклатурыCML)
	
	ТекВидНоменклатуры	= Неопределено;
	Для Каждого Элемент Из Метаданные.Перечисления.ВидыНоменклатуры.ЗначенияПеречисления Цикл
		Если (Элемент.Имя = ИмяВидаНоменклатурыCML) Тогда
			Выполнить("ТекВидНоменклатуры = Перечисления.ВидыНоменклатуры."+Элемент.Имя);	
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ТекВидНоменклатуры
	
КонецФункции

Функция ПолучитьТипНоменклатуры(ИмяТипаНоменклатурыCML, ВидНоменклатурыCML)
	
	ВидНеЗаполнен	= обЗначениеНезаполнено(ВидНоменклатурыCML);
	Запрос = Новый Запрос();
	Запрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
	            	  |	ТипыНоменклатуры.Ссылка КАК Ссылка
	            	  |ИЗ
	            	  |	Справочник.ТипыНоменклатуры КАК ТипыНоменклатуры
	            	  |ГДЕ
	            	  |	ТипыНоменклатуры.Наименование = &ИмяТипаНоменклатурыCML	
					  |"+?(ВидНеЗаполнен,"","И ТипыНоменклатуры.ВидНоменклатуры = &ВидНоменклатурыCML");
	Запрос.УстановитьПараметр("ИмяТипаНоменклатурыCML",ИмяТипаНоменклатурыCML);
	Если ( НЕ ВидНеЗаполнен ) Тогда
		Запрос.УстановитьПараметр("ВидНоменклатурыCML", ВидНоменклатурыCML);
	КонецЕсли;
	РезультатЗапроса	= Запрос.Выполнить();
	Если ( РезультатЗапроса.Пустой() ) Тогда
		Возврат Неопределено;	
	Иначе
		Выборка	= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;	
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИдНоменклатуры(Знач Ид)
	
	ПозицияРазделителя = Найти(Ид, "#");
	Если ( ПозицияРазделителя > 0 ) Тогда
		ИдНоменклатуры = Лев(Ид, ПозицияРазделителя - 1);
	Иначе
		ИдНоменклатуры = Ид;
	КонецЕсли;	
	
	Возврат ИдНоменклатуры;
	
КонецФункции

Функция ПолучитьИдХарактеристики(Знач Ид)
	
	ПозицияРазделителя = Найти(Ид, "#");
	Если ( ПозицияРазделителя > 0 ) Тогда
		ИдХарактеристики = Прав(Ид, СтрДлина(Ид) - ПозицияРазделителя);
	Иначе
		ИдХарактеристики = "";
	КонецЕсли;	
	
	Возврат ИдХарактеристики;
	
КонецФункции

Функция ВыполнитьПоискНоменклатурыХарактеристикиПоСсылкам(СтрокаТовара, Номенклатура, ХарактеристикаНоменклатуры)
	
	Если ( обЗначениеНеЗаполнено(СтрокаТовара.ТоварУслугаИд) ) Тогда
		Возврат Ложь;
	КонецЕсли;
			
	Попытка
		
		ИдНоменклатуры	= ПолучитьИдНоменклатуры(СтрокаТовара.ТоварУслугаИд);
		Номенклатура	= Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдНоменклатуры));
		Если ( Номенклатура = Справочники.Номенклатура.ПустаяСсылка() ) Тогда			
			Возврат Ложь;
		КонецЕсли;
			
		Если ( Номенклатура.ПолучитьОбъект() = Неопределено ) Тогда
			// Объект не найден
			СообщитьПользователю("Номенклатура не найдена по уникальному идентификатору: " + ИдНоменклатуры, Ложь,,1);
			Возврат Ложь;
		КонецЕсли;	
			
		Если ( Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик > 2 ) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ИдХарактеристики = ПолучитьИдХарактеристики(СтрокаТовара.ТоварУслугаИд);
		Если ( ПустаяСтрока(ИдХарактеристики) ) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдХарактеристики));
		
		Если ( ХарактеристикаНоменклатуры = ПустаяХарактеристикаСсылка ) Тогда
			Возврат Истина;
		КонецЕсли;
			
		Если ( ХарактеристикаНоменклатуры.ПолучитьОбъект() = Неопределено ) Тогда
			СообщитьПользователю("Объект <ХарактеристикаНоменклатуры> не найден: " + Строка(ИдХарактеристики)
				+ ". Будет создан новый объект.", Ложь,,1);		
			Возврат Ложь;	
		КонецЕсли;			
						
	Исключение	
		Возврат Ложь;	
	КонецПопытки;
	
	Возврат Истина;
		
КонецФункции

Функция НайтиСоздатьНоменклатуру(СтрокаТовара, Знач ТипНоменклатуры, СтавкаНДС, ХарактеристикаНоменклатуры = Неопределено)
	
	Номенклатура				= Справочники.Номенклатура.ПустаяСсылка();
	ХарактеристикаНоменклатуры	= Неопределено;
	
	УспешноНайдено = ВыполнитьПоискНоменклатурыХарактеристикиПоСсылкам(СтрокаТовара, Номенклатура, ХарактеристикаНоменклатуры);
	Если УспешноНайдено Тогда
		Возврат Номенклатура;
	КонецЕсли;		
	
	ХарактеристикаНоменклатуры = Неопределено;
	
	Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(Лев(СтрокаТовара.ТоварУслугаНаименование, Метаданные.Справочники.Номенклатура.ДлинаНаименования));
	Если ( Не обЗначениеНеЗаполнено(Номенклатура) ) Тогда
		Возврат Номенклатура;
	КонецЕсли;
		
	ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
		                                               
	Если ( Не обЗначениеНеЗаполнено(СтрокаТовара.ТоварУслугаБазоваяЕдиницаКод) ) Тогда
		ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(СтрокаТовара.ТоварУслугаБазоваяЕдиницаКод);
	КонецЕсли;	
	
	Если ( обЗначениеНеЗаполнено(ЕдиницаПоКлассификатору)
		И ЗначениеЗаполнено(СтрокаТовара.ТоварУслугаБазоваяЕдиница) ) Тогда	
		ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию(СтрокаТовара.ТоварУслугаБазоваяЕдиница, Истина);	
	КонецЕсли;	
	
	// Тип номенклатуры услуга предопределенный
	Если (ТипНоменклатуры = мТипНоменклатурыУслуга) Тогда
		ЕдиницаПоКлассификатору	= ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения;
	КонецЕсли;
	
	ЗапросПоЕдиницеХранения	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                       	               |	ЕдиницыИзмерения.Ссылка
	                       	               |ИЗ
	                       	               |	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	                       	               |ГДЕ
	                       	               |	ЕдиницыИзмерения.Наименование = &Наименование");
	ЗапросПоЕдиницеХранения.УстановитьПараметр("Наименование",ЕдиницаПоКлассификатору.Наименование);
	ВыборкаЕдиницыХранения	= ЗапросПоЕдиницеХранения.Выполнить().Выбрать();
	Если (ВыборкаЕдиницыХранения.Следующий()) Тогда
		ЕдиницаХраненияОстатков							= ВыборкаЕдиницыХранения.Ссылка;	
	Иначе
		ЕдиницаХраненияОстатков = Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
		ОбработкаЗаполненияНовогоОбъекта(ЕдиницаХраненияОстатков);
		ЕдиницаХраненияОстатков.УстановитьНовыйКод();
		ЕдиницаХраненияОстатков.Наименование            = ЕдиницаПоКлассификатору.Наименование;
		ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору = ЕдиницаПоКлассификатору;
		ЕдиницаХраненияОстатков.Коэффициент             = 1;
		ЕдиницаХраненияОстатков.Владелец                = ТипНоменклатуры;
		
		Попытка
			ЕдиницаХраненияОстатков.Записать();
		Исключение
			СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
			Возврат Справочники.Номенклатура.ПустаяСсылка();
		КонецПопытки;
	
	КонецЕсли;
	
	Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
	ОбработкаЗаполненияНовогоОбъекта(Номенклатура);
	
	Номенклатура.УстановитьНовыйКод();
	Номенклатура.Наименование 			 	= СтрокаТовара.ТоварУслугаНаименование;	
	Номенклатура.ВидНоменклатуры 		 	= ТипНоменклатуры.ВидНоменклатуры;
	Номенклатура.ТипНоменклатуры 		 	= ТипНоменклатуры;	
	Номенклатура.БазоваяЕдиницаИзмерения	= ЕдиницаПоКлассификатору;
	Номенклатура.ОсновнаяЕдиницаИзмерения	= ЕдиницаХраненияОстатков;
	Номенклатура.Комментарий 			 	= СтрокаТовара.ТоварУслугаКомментарий;
	Номенклатура.НаименованиеИностранное	= СтрокаТовара.ТоварУслугаНаименование;
	Номенклатура.НаименованиеПолное		 	= СтрокаТовара.ТоварУслугаНаименование;
	Номенклатура.ВалютаУчета				= СтрокаТовара.Родитель.ДокументОбъект.ВалютаДокумента;
	Номенклатура.СтавкаНДС 				 	= СтавкаНДС;
	Номенклатура.ОсновнаяЕдиницаИзмерения	= ЕдиницаХраненияОстатков.Ссылка;
	
	Попытка
		//Особенность альфа-авто: Автосервис - для услуги должен быть заполнен № по каталогу.
		НомерПоКаталогуУникальный	= обПраво("НомерПоКаталогуУникальный",Права);
		Если (ТипЗнч(НомерПоКаталогуУникальный)=Тип("Булево") И ТипНоменклатуры = мТипНоменклатурыУслуга И НомерПоКаталогуУникальный) Тогда		
			ГСЧ = Новый ГенераторСлучайныхЧисел(4709555);
			Номенклатура.Артикул	= мПрефикс+ГСЧ.СлучайноеЧисло(0, 4294967295);  
		КонецЕсли; 
	Исключение КонецПопытки;
	
	Если ( Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ) Тогда
		Номенклатура.СпособРаспределенияДопРасходов	= Перечисления.СпособыРаспределенияДопРасходов.ПоСумме;
	КонецЕсли;
	
	Попытка
		Номенклатура.Записать();
		Если ( ОбменТоварами И ВыгружатьТолькоИзменения ) Тогда
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, Номенклатура.Ссылка);
		КонецЕсли;
	Исключение
		СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
		Возврат Справочники.Номенклатура.ПустаяСсылка();	
	КонецПопытки;	
		
	Возврат Номенклатура.Ссылка;	
	
КонецФункции

Функция ИдентифицироватьНоменклатуру(ДеревоДокументов)
	
	Успешно = Истина;
	Успешно = ИнициализироватьВидыНоменкалатуры();
	Если ( НЕ Успешно ) Тогда Возврат Ложь; КонецЕсли;
	ТаблицаУслуг.Очистить();
	
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		Если ( СтрокаДД.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			Продолжить;
		КонецЕсли;	
		
		ОтобразитьСостояние("Идентификация товаров в документе: " + СтрокаДД.ДокументОбъект);
		
		Для Каждого ТоварУслугаСвойство Из СтрокаДД.Строки Цикл
			
			Если ( Не обЗначениеНеЗаполнено(ТоварУслугаСвойство.СвойствоНаименование) ) Тогда
				Продолжить;
			КонецЕсли;
			
			ВидНоменклатурыCML		= Неопределено;
			ТипНоменклатурыCML		= Неопределено;
			ИмяТипаНоменклатурыCML	= "";
			ИмяВидаНоменклатурыCML	= "";
			
			Для Каждого ПодчиненнаяСтрокаТовараУслуги Из ТоварУслугаСвойство.Строки Цикл
				
				Если ( НЕ обЗначениеНеЗаполнено(ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаНаименование)) Тогда
					Если (ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаНаименование = "ТипНоменклатуры") Тогда	
						ИмяТипаНоменклатурыCML	= ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаЗначение;
					ИначеЕсли (ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаНаименование = "ВидНоменклатуры") Тогда
						ИмяВидаНоменклатурыCML	= ПодчиненнаяСтрокаТовараУслуги.ЗначениеРеквизитаЗначение; 	
					КонецЕсли;
				КонецЕсли;	
				
			КонецЦикла;
			
			ВидНоменклатурыCML	= ПолучитьВидНоменклатуры(ИмяВидаНоменклатурыCML);
			ТипНоменклатурыCML	= ПолучитьТипНоменклатуры(ИмяТипаНоменклатурыCML, ВидНоменклатурыCML);
			
			Если (обЗначениеНеЗаполнено(ТипНоменклатурыCML)) Тогда
				Если ( ИмяВидаНоменклатурыCML = ВидНоменклатурыCML_Услуга ) Тогда
					ТипНоменклатурыCML	= мТипНоменклатурыУслуга;
				Иначе
					ТипНоменклатурыCML	= мТипНоменклатурыТовар;
				КонецЕсли;
			КонецЕсли;
			
			Если ( обЗначениеНеЗаполнено(ВидНоменклатурыCML) ) Тогда
				ВидНоменклатурыCML	= ТипНоменклатурыCML.ВидНоменклатуры;	
			КонецЕсли;
			
			ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			СтавкаНДСНоменклатуры = ?(ЗначениеЗаполнено(ТоварУслугаСвойство.СтавкаНДС), ТоварУслугаСвойство.СтавкаНДС, СтрокаДД.СтавкаНДС);
			
			
			Если ( ИмяВидаНоменклатурыCML = ВидНоменклатурыCML_Услуга ) Тогда					
				Номенклатура = НайтиСоздатьНоменклатуру(ТоварУслугаСвойство, мТипНоменклатурыУслуга, СтавкаНДСНоменклатуры);		
			Иначе	
				Номенклатура = НайтиСоздатьНоменклатуру(ТоварУслугаСвойство, мТипНоменклатурыТовар, СтавкаНДСНоменклатуры, ХарактеристикаНоменклатуры);		
			КонецЕсли;
			
			Если (обЗначениеНеЗаполнено(Номенклатура)) Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Если ( Номенклатура.ТипНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ) Тогда	
				
				// Если в заказе, загружаемом с сайта попадает услуга, то она не записывается в табличную ЗаказаПокупателя,
				// а воспринимается как свойство документа (Вид, СтоимостьДоставки)
				
				НовСтрока					= ТаблицаУслуг.Добавить();
				НовСтрока.Документ			= СтрокаДД.ДокументОбъект;
				НовСтрока.ВидДоставки		= Номенклатура;
				НовСтрока.СтоимостьДоставки	= ТоварУслугаСвойство.ТоварУслугаСумма;
		
			Иначе	
				
				НовСтрока = СтрокаДД.ДокументОбъект.Товары.Добавить();
				
				НовСтрока.Номенклатура     			= Номенклатура;	
				НовСтрока.ЕдиницаИзмерения 		    = Номенклатура.ОсновнаяЕдиницаИзмерения;				
				НовСтрока.Количество   	   		 	= ТоварУслугаСвойство.ТоварУслугаКоличество;
				НовСтрока.Коэффициент      		 	= Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				НовСтрока.Цена						= ТоварУслугаСвойство.ТоварУслугаЦенаЗаЕдиницу;
				НовСтрока.Сумма						= ТоварУслугаСвойство.ТоварУслугаСумма;
				
				Если ( ЗначениеЗаполнено(ТоварУслугаСвойство.СтавкаНДС) ) Тогда
					НовСтрока.СтавкаНДС    = ТоварУслугаСвойство.СтавкаНДС;
				Иначе
					НовСтрока.СтавкаНДС    = Номенклатура.СтавкаНДС;
				КонецЕсли;
							
				Если (обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры)) Тогда
					Если (НЕ обЗначениеНеЗаполнено(Номенклатура))
						И (НЕ обЗначениеНеЗаполнено(Номенклатура.ТипНоменклатуры))
						И (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=3) Тогда
						НовСтрока.ХарактеристикаНоменклатуры = Неопределено;
					Иначе
						НовСтрока.ХарактеристикаНоменклатуры = СоздатьХарактеристикуНаОсновеНоменклатуры(Номенклатура);
					КонецЕсли;
				Иначе
					НовСтрока.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;	
				КонецЕсли;
				
				Для Каждого СвойствоТовара Из ТоварУслугаСвойство.Строки Цикл
					Если (СвойствоТовара.СкидкаВСумме = Ложь) Тогда
						НовСтрока.СуммаСкидки	=	СвойствоТовара.СуммаСкидки;
						СтрокаДД.ДокументОбъект.ОбработкаРеквизита("Товары.СуммаСкидки",НовСтрока);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				СтрокаДД.ДокументОбъект.ОбработкаРеквизита("Товары.СтавкаНДС",НовСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
				
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция СоздатьХарактеристикуНаОсновеНоменклатуры(Номенклатура)
	ТекХарактеристика	= Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Номенклатура.НаименованиеПолное);
	Если (обЗначениеНеЗаполнено(ТекХарактеристика)) Тогда
		НоваяХарактеристика	= Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
		ОбработкаЗаполненияНовогоОбъекта(НоваяХарактеристика);
		НоваяХарактеристика.УстановитьНовыйКод();
		Если (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1) Тогда
			НоваяХарактеристика.Владелец	= Номенклатура.ТипНоменклатуры;	
		ИначеЕсли (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик > 1) Тогда
			НоваяХарактеристика.Владелец	= Номенклатура;
		КонецЕсли;
		НоваяХарактеристика.Наименование	= Номенклатура.НаименованиеПолное;
		Попытка
			НоваяХарактеристика.Записать();
		Исключение
			СообщитьПользователю(ОписаниеОшибки(),Ложь,,1);
			НоваяХарактеристика = Неопределено;
		КонецПопытки;
		Возврат НоваяХарактеристика.Ссылка;
	Иначе
		Возврат ТекХарактеристика;
	КонецЕсли;
КонецФункции

Функция ОбработатьДатуВремяCML(ДатаВремяСтрока, НачальнаяДата = Неопределено)
	
	ДатаВремя = Неопределено;	
	Если ( ЗначениеЗаполнено(НачальнаяДата) ) Тогда
		Время 	  = СтрЗаменить(ДатаВремяСтрока, ":", "");
		ДатаВремя = Дата(Формат(НачальнаяДата, "ДФ=yyyyMMdd") + Время);
	Иначе	
		ДатаВремя = Дата(СтрЗаменить(ДатаВремяСтрока, "-", "") + "000000");	
	КонецЕсли;	
	
	Возврат ДатаВремя;
КонецФункции

Функция ОбработатьВалютуCML(КодВалютыСтрока)
	Валюта = Справочники.Валюты.НайтиПоНаименованию(КодВалютыСтрока);
	Возврат Валюта;
КонецФункции

Функция СоздатьОбновитьДокументы(ДеревоДокументов, СтруктураСтатистики, МассивДокументовДляПроведения, МассивЗагруженныхДокументов)
	
	Успешно = Истина;
	Для Каждого Док Из ДеревоДокументов.Строки Цикл
		
		// если на документ есть ссылки, то он вообще пропускается и не загружается
		Если ( Док.ЕстьСсылкиНаРанееЗагруженныйДокумент ) Тогда
			СтруктураСтатистики.Пропущено = СтруктураСтатистики.Пропущено + 1;
			Продолжить;
		КонецЕсли;
		
		Если ( НЕ обЗначениеНеЗаполнено(Док.РанееЗагруженныйДокументСсылка)) Тогда
			СтруктураСтатистики.Обновлено = СтруктураСтатистики.Обновлено + 1;
		Иначе
			СтруктураСтатистики.Создано   = СтруктураСтатистики.Создано + 1;
		КонецЕсли;	
		
		//Чтение значения для срока поставки
		Попытка
			СрокПоставкиПокупателюПоУмолчанию	= обПраво("СрокПоставкиПокупателюПоУмолчанию", Права);
		Исключение
			СрокПоставкиПокупателюПоУмолчанию	= 7;
		КонецПопытки;
		
		СтруктураОтбора					= Новый Структура("Организация,Объект", Док.ДокументОбъект.Контрагент, Перечисления.ВидыОбъектовСведений.СрокПоставки);
		СтруктураСведений				= РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(?(обЗначениеНеЗаполнено(Док.ДокументОбъект.Дата),ТекущаяДата(),Док.ДокументОбъект.Дата),СтруктураОтбора);
		СрокПоставкиДней				= ?(обЗначениеНеЗаполнено(СтруктураСведений.Значение),СрокПоставкиПокупателюПоУмолчанию,СтруктураСведений.Значение);
		Док.ДокументОбъект.СрокПоставки	= НачалоДня(Док.ДокументОбъект.Дата)+СрокПоставкиДней*60*60*24;	
		
		Попытка
			
			// отдельная работа с комментарием		
			Если ( Не ПустаяСтрока(Док.ДокументОбъект.Комментарий)) Тогда
			
				Попытка
					СтрокаИмениСайта = Док.Строки.Найти("Сайт", "СвойствоНаименование");
					
					Если (СтрокаИмениСайта = Неопределено) Тогда
						ИмяСайта = HTTPОбменСервер;
					Иначе					
						ИмяСайта = СтрокаИмениСайта.СвойствоЗначение;
					КонецЕсли;
					
					Док.ДокументОбъект.Комментарий   = Док.ДокументОбъект.Комментарий + " " + ИмяСайта;
				
				Исключение
				КонецПопытки;
				
			КонецЕсли;			
			
			//Проверка на заполненность суммы документа
			Если ТипЗнч(Док.ДокументОбъект)=Тип("ДокументОбъект.ЗаказПокупателя")
				И ТипЗнч(ДеревоДокументов)=Тип("ДеревоЗначений")
				И ДеревоДокументов.Колонки.Найти("ТоварУслугаСумма")<>Неопределено
				И НЕ ЗначениеЗаполнено(Док.ДокументОбъект.СуммаДокумента) Тогда
				РасчетнаяСуммаДокумента	= Док.Строки.Итог("ТоварУслугаСумма");
				Если РасчетнаяСуммаДокумента>0 Тогда
					Док.ДокументОбъект.СуммаДокумента	= РасчетнаяСуммаДокумента;	
				КонецЕсли;
			КонецЕсли;
			
			Док.ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);

			СсылкаНаДокумент = Док.ДокументОбъект.Ссылка;
			МассивЗагруженныхДокументов.Добавить(СсылкаНаДокумент);
			
			Если ( ПроводитьДокументы ИЛИ Док.ДокументОбъект.Проведен ) Тогда
				МассивДокументовДляПроведения.Добавить(Док.ДокументОбъект);
			КонецЕсли;
			
		Исключение	
			СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
		
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция ЗаписатьСвойстваДокументов(ДеревоДокументов, СтруктураСтатистики, МассивОтклоненныхДокументов)
	
	Успешно = Истина;
	Для Каждого СтрокаДД Из ДеревоДокументов.Строки Цикл
		
		//в качестве свойства устанавливается входящий номер электронного документа.	   
		// !!! misely Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Входящий номер", СтрокаДД.НомерВходящий);			
		//Если ( НЕ Успешно ) Тогда
		//	СообщитьПользователю("Не удалось установить свойство документа - Входящий номер", Ложь,,1);
		//	Прервать;
		//КонецЕсли;	
		
		//в качестве свойства устанавливается дата входящего электронного документа.	   
		Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Дата входящего документа", СтрокаДД.ДатаВходящегоДокумента);			
		Если ( НЕ Успешно ) Тогда
			СообщитьПользователю("Не удалось установить свойство документа - Дата входящего документа", Ложь,,1);
			Прервать;
		КонецЕсли;
		
		// ВидДоставки и СтоимостьДоставки записываются как свойства заказа.
		ОтборУслуг = Новый Структура("Документ", СтрокаДД.ДокументОбъект);		
		НайденныеСтроки	= ТаблицаУслуг.НайтиСтроки(ОтборУслуг);
		Если ( НайденныеСтроки.Количество() > 0 ) Тогда
			Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Вид доставки", НайденныеСтроки[0].ВидДоставки);	
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось установить свойство документа - Вид доставки", Ложь,,1);
				Прервать;
			КонецЕсли;
			
			Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Стоимость доставки", НайденныеСтроки[0].СтоимостьДоставки);	
			Если ( НЕ Успешно ) Тогда
				СообщитьПользователю("Не удалось установить свойство документа - Стоимость доставки", Ложь,,1);
				Прервать;
			КонецЕсли;			
		КонецЕсли;
		
		//Адрес контрагента также должен попасть в свойства заказа
		Если ТипЗнч(СтрокаДД.СтруктураДанныхКонтрагента)=Тип("Структура")
			И ТипЗнч(СтрокаДД.СтруктураДанныхКонтрагента.ДеревоАдресов)=Тип("ДеревоЗначений") Тогда
			
			Для Каждого СтрокаДереваАдресов Из СтрокаДД.СтруктураДанныхКонтрагента.ДеревоАдресов.Строки Цикл			
				Если СтрокаДереваАдресов.ВидАдреса="АдресРегистрации" И ЗначениеЗаполнено(СтрокаДереваАдресов.Представление) Тогда
					Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, "Адрес", СтрокаДереваАдресов.Представление);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Для Каждого ТоварУслугаСвойство Из СтрокаДД.Строки Цикл
		
			Если ( обЗначениеНеЗаполнено(ТоварУслугаСвойство.СвойствоНаименование)) Тогда
				Продолжить;
			КонецЕсли;
				
			ИмяСвойства      = ТоварУслугаСвойство.СвойствоНаименование;
			ЗначениеСвойства = ТоварУслугаСвойство.СвойствоЗначение;
			
			Если (НЕ обЗначениеНеЗаполнено(ИмяСвойства) И НЕ обЗначениеНеЗаполнено(ЗначениеСвойства)) Тогда
			   
				Успешно	= НайтиСоздатьУстановитьСвойствоДокумента(СтрокаДД.ДокументОбъект.Ссылка, ИмяСвойства, ЗначениеСвойства);
				
				Если ( НЕ Успешно ) Тогда
					СообщитьПользователю("Не удалось установить свойство документа - " + ИмяСвойства, Ложь,,1);
					Прервать;
				КонецЕсли;	
									
				Если (ТоварУслугаСвойство.СвойствоЗначение = БулевоЗначениеCML_Истина)
					ИЛИ (ТоварУслугаСвойство.СвойствоЗначение = БулевоЗначениеCML_Да)  Тогда					
				
					Если ( ТоварУслугаСвойство.СвойствоНаименование = "Заказ оплачен") Тогда
						СтруктураСтатистики.ОплаченСписок.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					ИначеЕсли (ТоварУслугаСвойство.СвойствоНаименование = "Доставка разрешена") Тогда
						СтруктураСтатистики.ДоставкаРазрешенаСписок.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					ИначеЕсли (ТоварУслугаСвойство.СвойствоНаименование = "Финальный статус") Тогда
						СтруктураСтатистики.ФинальныйСтатусСписок.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					ИначеЕсли (ТоварУслугаСвойство.СвойствоНаименование = "Отменен") Тогда
						МассивОтклоненныхДокументов.Добавить(СтрокаДД.ДокументОбъект.Ссылка);
					КонецЕСли;
				КонецЕсли;
			   
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЦикла;	
	
	Возврат Успешно;
	
КонецФункции

Функция НайтиСоздатьУстановитьСвойствоДокумента(ОбъектСсылка, ИмяСвойства, ЗначениеСвойства);
	
	Успешно = Истина;
	
	СвойствоСсылка 	 	= ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка();
	НазначениеСсылка	= ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Документ_ЗаказПокупателя;
	ТипНазначенияСвойств= Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Назначение" , НазначениеСсылка);
	Запрос.УстановитьПараметр("ИмяСвойства", ИмяСвойства);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НазначенияСвойствОбъектовВидыСвойств.Свойство.Ссылка КАК СвойствоСсылка
		|ИЗ
		|	ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойствОбъектовВидыСвойств
		|ГДЕ
		|	НазначенияСвойствОбъектовВидыСвойств.Ссылка = &Назначение
		|	И НазначенияСвойствОбъектовВидыСвойств.Свойство.Наименование = &ИмяСвойства";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если ( Выборка.Следующий() ) Тогда
		СвойствоСсылка = Выборка.СвойствоСсылка;	
	КонецЕсли;	
	
	Если ( СвойствоСсылка = ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка() ) Тогда
		
		ТекСвойство	= ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию(ИмяСвойства);
		Если (обЗначениеНеЗаполнено(ТекСвойство)) Тогда
			НовоеСвойство = ПланыВидовХарактеристик.СвойстваОбъектов.СоздатьЭлемент();
			ОбработкаЗаполненияНовогоОбъекта(НовоеСвойство);
			НовоеСвойство.УстановитьНовыйКод();
			НовоеСвойство.Наименование 		 = ИмяСвойства;
			
			Если (ИмяСвойства = "Вид доставки") Тогда	
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("СправочникСсылка.Номенклатура");				
			ИначеЕсли (ИмяСвойства = "Дата входящего документа") Или (ИмяСвойства = "Дата изменения статуса")
				 Или (ИмяСвойства = "Дата разрешения доставки") Тогда
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.Дата));
			ИначеЕсли (ИмяСвойства = "Заказ оплачен") Или (ИмяСвойства = "Доставка разрешена")
				Или (ИмяСвойства = "Отменен") Или (ИмяСвойства = "Финальный статус")Тогда
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОтветов");	
			ИначеЕсли (ИмяСвойства = "Стоимость доставки") Тогда
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный));	
			//ИначеЕсли (ИмяСвойства = "Входящий номер") Тогда
			//	НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Число",
			//	Новый КвалификаторыЧисла(10,,ДопустимыйЗнак.Неотрицательный));
			ИначеЕсли (ИмяСвойства = "Сайт") Тогда	
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("ПланОбменаСсылка.ОбменССайтом");
			Иначе
				НовоеСвойство.ТипЗначения		= Новый ОписаниеТипов("Строка");
			КонецЕсли;
				
			Попытка 
				НовоеСвойство.Записать();
			Исключение
				СообщитьПользователю("Не удалось записать свойство " + ИмяСвойства, Ложь,,1);
				Возврат Ложь;
			КонецПопытки;
			СвойствоСсылка = НовоеСвойство.Ссылка;
		Иначе
			СвойствоСсылка = ТекСвойство;
		КонецЕсли;
		
		НазначениеДляЗаказа			= ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Документ_ЗаказПокупателя.ПолучитьОбъект();
		НовоеНазначение				= НазначениеДляЗаказа.ВидыСвойств.Добавить();
		НовоеНазначение.Свойство	= СвойствоСсылка;
		НазначениеДляЗаказа.Записать();
		
	КонецЕсли;	
	
	//Преобразование типа значения свойства
	ТекЗначениеСвойства				= ЗначениеСвойства;
	
	Если (Не СвойствоСсылка.ТипЗначения.СодержитТип(ТипЗнч(ТекЗначениеСвойства))) Тогда
		Если (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("Число"))) Тогда
			Попытка
				ТекЗначениеСвойства	= Число(Строка(ЗначениеСвойства));
			Исключение
				Возврат Ложь;
			КонецПопытки;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("Дата"))) Тогда
			Если Найти(ЗначениеСвойства, "-")=5 ИЛИ Найти(ЗначениеСвойства, ".")=5 Тогда
				РазбираемыйГод		= Сред(ЗначениеСвойства,0,4);
				РазбираемыйМесяц	= Сред(ЗначениеСвойства,6,2);
				РазбираемоеЧисло	= Сред(ЗначениеСвойства,9,2);
				ТекЗначение			= ""+РазбираемоеЧисло+"."+РазбираемыйМесяц+"."+РазбираемыйГод;
				Если СтрДлина(ЗначениеСвойства)>10 Тогда
					РазбираемыйЧас		= Сред(ЗначениеСвойства,12,2);
					РазбираемаяМинута	= Сред(ЗначениеСвойства,15,2);
					РазбираемаяСекунда	= Сред(ЗначениеСвойства,18,2);
					ТекЗначение			= ТекЗначение + " "+РазбираемыйЧас+":"+РазбираемаяМинута+":"+РазбираемаяСекунда;
				КонецЕсли;
			Иначе
				ЗначениеСвойства= СтрЗаменить(ЗначениеСвойства,"-",".");				
				ТекЗначение		= ЗначениеСвойства;
			КонецЕсли;
			Попытка
				ТекЗначениеСвойства	= Дата(ТекЗначение);
			Исключение
				Возврат Ложь;
			КонецПопытки;			
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("Строка"))) Тогда
			Попытка
				ТекЗначениеСвойства	= Строка(ЗначениеСвойства);
			Исключение
				Возврат Ложь;
			КонецПопытки;			
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура"))) Тогда
			ЗапросНоменклатуры	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			                  	               |	Номенклатура.Ссылка
			                  	               |ИЗ
			                  	               |	Справочник.Номенклатура КАК Номенклатура
			                  	               |ГДЕ
			                  	               |	Номенклатура.Наименование = &Наименование
			                  	               |	И (НЕ Номенклатура.ПометкаУдаления)");
			ЗапросНоменклатуры.УстановитьПараметр("Наименование", Строка(ЗначениеСвойства));
			РезультатЗапросаНоменклатуры	= ЗапросНоменклатуры.Выполнить();
			Если (РезультатЗапросаНоменклатуры.Пустой()) Тогда
				Возврат Ложь;	
			Иначе
				ВыборкаНоменклатуры	= РезультатЗапросаНоменклатуры.Выбрать();
				ВыборкаНоменклатуры.Выбрать();
				ТекЗначениеСвойства	= ВыборкаНоменклатуры.Ссылка;
			КонецЕсли;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("ПеречислениеСсылка.ВариантыОтветов"))) Тогда
			Если ( Строка(ЗначениеСвойства) = БулевоЗначениеCML_Истина Или Строка(ЗначениеСвойства) = БулевоЗначениеCML_Да ) Тогда
				ТекЗначениеСвойства	= перечисления.ВариантыОтветов.Да;	
			ИначеЕсли ( Строка(ЗначениеСвойства) = БулевоЗначениеCML_Ложь Или Строка(ЗначениеСвойства) = БулевоЗначениеCML_Нет) Тогда
				ТекЗначениеСвойства	= перечисления.ВариантыОтветов.Нет;	
			Иначе
				Возврат Ложь;	
			КонецЕсли;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("ПланОбменаСсылка.ОбменССайтом"))) Тогда
			ТекЗначениеСвойства		= УзелОбмена;
		ИначеЕсли (СвойствоСсылка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойств"))) Тогда
			ЗапросЗначенияСвойства	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			                      	               |	ЗначенияСвойств.Ссылка
			                      	               |ИЗ
			                      	               |	Справочник.ЗначенияСвойств КАК ЗначенияСвойств
			                      	               |ГДЕ
			                      	               |	ЗначенияСвойств.Наименование = &Наименование
			                      	               |	И (НЕ ЗначенияСвойств.ПометкаУдаления)");
			ЗапросЗначенияСвойства.УстановитьПараметр("Наименование", Строка(ЗначениеСвойства));
			РезультатЗапросаЗначенияСвойства	= ЗапросЗначенияСвойства.Выполнить();
			Если (РезультатЗапросаЗначенияСвойства.Пустой()) Тогда
				НовоеЗначениеСвойства	= Справочники.ЗначенияСвойств.СоздатьЭлемент();
				ОбработкаЗаполненияНовогоОбъекта(НовоеЗначениеСвойства);
				НовоеЗначениеСвойства.УстановитьНовыйКод();
				НовоеЗначениеСвойства.Наименование	= ЗначениеСвойства;
				НовоеЗначениеСвойства.Владелец		= СвойствоСсылка;
				Попытка
					НовоеЗначениеСвойства.Записать();
				Исключение
					Возврат Ложь;
				КонецПопытки;
				ТекЗначениеСвойства	= НовоеЗначениеСвойства;
			Иначе
				ВыборкаЗначенияСвойства	= РезультатЗапросаЗначенияСвойства.Выбрать();
				ВыборкаЗначенияСвойства.Выбрать();
				ВыборкаЗначенияСвойства.Следующий();
				ТекЗначениеСвойства	= ВыборкаЗначенияСвойства.Ссылка;
			КонецЕсли;			
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
			
	Запись = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
	Запись.Объект	= ОбъектСсылка;
	Запись.Свойство = СвойствоСсылка;
	Запись.Значение = ТекЗначениеСвойства;
	
	Попытка
		Запись.Записать();
	Исключение
		Успешно = Ложь;
		СообщитьОбИсключительнойОшибке(Ложь, "Не удалось записать значение свойства документа " + ИмяСвойства);	
	КонецПопытки;	
	
	Возврат Успешно;
	
КонецФункции

Процедура ОбработкаЗаполненияНовогоОбъекта(ТекОбъект)
	
	//Попытка вызова метода заполнения объекта
	Попытка
		ТекОбъект.Заполнить(Неопределено);	
	Исключение КонецПопытки;
	
	//Установка флага загрузки при обмене данными
	Попытка
		ТекОбъект.ОбменДанными.Загрузка	= Истина;
	Исключение КонецПопытки;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УПРАВЛЕНИЯ ОБМЕНОМ ЗАКАЗАМИ

Функция ВыполнитьОбменЗаказами(СтруктураИзменений)
	
	Если ( НЕ ОпределитьМожноВыгружатьЗаказы() ) Тогда
		СообщитьПользователю("Выгрузка заказов не произведена.", Истина,,2);
		Возврат Истина;	
	КонецЕсли;
	
	мМассивЗагруженныхДокументов = Новый Массив();
	КоличествоОбработанныхДокументовНаЗагрузке = 0;
	КоличествоОбработанныхДокументовНаВыгрузке = 0;
	
	СтруктураПараметровСайта	= ПолучитьСтруктуруПараметровДляСоединения(ЭтотОбъект);	
	
	Если ( ВыгружатьНаСайт ) Тогда		
		HTTPОбменАдресСкрипта									= НачалоАдресаСкрипта;		
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки = Текущаядата();
		
		УспешноЗагружено	= HTTPЗагрузитьССервера(СтруктураПараметровСайта, "sale", КоличествоОбработанныхДокументовНаЗагрузке);
		
		мСтруктураИнформацииИсторииОбмена.РезультатПоследнейЗагрузки	= УспешноЗагружено;
		мСтруктураИнформацииИсторииОбмена.ДатаПоследнейЗагрузки			= Текущаядата();
	
		УспешноВыгружено	= ВыгрузитьЗаказыНаСайт(СтруктураИзменений, СтруктураПараметровСайта, КоличествоОбработанныхДокументовНаВыгрузке);
	Иначе	
		УспешноЗагружено = ЗагрузитьЗаказыИзФайла(КоличествоОбработанныхДокументовНаЗагрузке);
		УспешноВыгружено = ВыгрузитьЗаказыВФайл(СтруктураИзменений, СтруктураПараметровСайта, КаталогВыгрузки, КоличествоОбработанныхДокументовНаВыгрузке);	
	КонецЕсли;	
	
	Успешно = УспешноЗагружено И УспешноВыгружено;
	
	Если ( Успешно ) Тогда
		Если ( КоличествоОбработанныхДокументовНаЗагрузке + КоличествоОбработанныхДокументовНаВыгрузке > 0 ) Тогда
			СообщитьПользователю("Обмен заказами успешно завершен", Истина,,2);
		КонецЕсли;	
	ИначеЕсли (КоличествоОбработанныхДокументовНаВыгрузке>0) Тогда
		СообщитьПользователю("Обмен заказами завершен с ошибками!!!", Истина,,2);
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Функция ПроверитьОсновнойДоговорКонтрагента(КонтрагентСсылка, ДокОбъект)
	
	Успешно = Истина;
	
	Если ( обЗначениеНеЗаполнено(ДокОбъект.Организация)
		Или обЗначениеНеЗаполнено(ДокОбъект.ПодразделениеКомпании) ) Тогда		
		СообщитьПользователю("Не удалось определить основной договор контрагента (не найдена организация(подразделение)).", Ложь,,1);
		Возврат Ложь;		
	КонецЕсли;
	
	// ищем договор по контрагенту, организации и валюте
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	               |	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	               |ИЗ
	               |	Справочник.ДоговорыВзаиморасчетов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	ДоговорыКонтрагентов.Организация = &Организация
				   |	И ДоговорыКонтрагентов.Подразделение = &Подразделение
				   |	И ДоговорыКонтрагентов.ВидДоговора = Значение(Перечисление.ВидыДоговоров.Продажа)";
				   	               //|	И ДоговорыКонтрагентов.ВидВзаиморасчетов =
								   //|Значение(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам)
				   
	Запрос.УстановитьПараметр("Организация", ДокОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение", ДокОбъект.ПодразделениеКомпании);
	Выборка = Запрос.Выполнить().Выбрать();
	                        
	ТекущийДоговор			= Неопределено;	
	ВалютаВзаиморасчетов	= ДокОбъект.ВалютаДокумента;
		
	Если ( обЗначениеНеЗаполнено(ВалютаВзаиморасчетов)) Тогда
		ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
	Пока ( Выборка.Следующий() ) Цикл		
		ТекущийДоговор = Выборка.Ссылка;
		Если ( Выборка.ВалютаВзаиморасчетов = ВалютаВзаиморасчетов ) Тогда
			Прервать;
		КонецЕсли;	
	КонецЦикла;
    	
	Если ( обЗначениеНеЗаполнено(ТекущийДоговор) ) Тогда
		
		ТекущийДоговор = СоздатьДоговорПоПараметрам(ДокОбъект, КонтрагентСсылка, ДокОбъект.Организация, ВалютаВзаиморасчетов);
		
		Если ( обЗначениеНеЗаполнено(ТекущийДоговор) ) Тогда
			Возврат Ложь;
		КонецЕсли;	
				
	КонецЕсли;
	
	//Попытка	
		ДокОбъект.ДоговорВзаиморасчетов		= ТекущийДоговор;
		//КонтрагентОбъект								= КонтрагентСсылка.ПолучитьОбъект();
		//КонтрагентОбъект.ОсновнойДоговорКонтрагента	= ТекущийДоговор;
		//КонтрагентОбъект.Записать();
	//Исключение
	//	СообщитьОбИсключительнойОшибке(Ложь, ОписаниеОшибки(), "Не удалось записать основной договор контрагенту.");
	//	Успешно = Ложь;
	//КонецПопытки;		
		
	Возврат Успешно;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ПрефиксУзлаCML	   = "CMLУзел.";
ПрефиксАтрибутаCML = "CMLАтрибут.";
НачалоЭлементаCML  = "CMLНачалоЭлемента";
КонецЭлементаCML   = "CMLКонецЭлемента";

ПодкаталогКартинок 					   = "import_files";
ПодкаталогБезопасностиКаталогаВыгрузки = "1cbitrix";

ПараметрЗапросаHTTP_Инициализация       	  = "&mode=init";
ПараметрЗапросаHTTP_ПередачаФайла       	  = "&mode=file&filename=";
ПараметрЗапросаHTTP_ИмпортФайлаСервером		  = "&mode=import&filename=";
ПараметрЗапросаHTTP_ПолучитьДанные	    	  = "&mode=query"; 
ПараметрЗапросаHTTP_УспешноеЗавершениеИмпорта = "&mode=success";

ОтветСервера_ZIPРазрешен							= "zip=yes";
ОтветСервера_ОграничениеРазмераФрагментаФайлаОбмена = "file_limit=";
ОтветСервера_УспешноеЗавершениеТекущейОперации 		= "success";
ОтветСервера_АварийноеЗавершениеТекущейОперации		= "failure";
ОтветСервера_ВыполнениеТекущейОперации 				= "progress";

ПустаяХарактеристикаСсылка	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
НаименованиеНалога 			= "НДС";

НаименованиеКаталогаТоваровCML   = "Каталог товаров";
НаименованиеПакетаПредложенийCML = "Пакет предложений";

БулевоЗначениеCML_Истина 	= "true";
БулевоЗначениеCML_Да		= "Да";
БулевоЗначениеCML_Ложь 		= "false";
БулевоЗначениеCML_Нет		= "Нет";

ВидНоменклатурыCML_Услуга 	= "Услуга";
ВидНоменклатурыCML_Товар  	= "Товар";
ЗначениеCML_ТипНоменклатуры = "ТипНоменклатуры";

//мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом =
//Константы.КоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом.Получить();
Если ( обЗначениеНеЗаполнено(мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом)) Тогда
	мКоэффициентПересчетаВесаТоваровВГраммыДляОбменаССайтом = 1;
КонецЕсли;

ПостроительЗапроса	= Новый ПостроительЗапроса();
мПрефикс			= "";
ОшибкиHTMLем		= "";							
ОшибкиТекстом		= "";

ТаблицаФайловОбмена	= Новый ТаблицаЗначений();
ТаблицаФайловОбмена.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата",,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Дата");
ТаблицаФайловОбмена.Колонки.Добавить("Режим",Новый ОписаниеТипов("Строка",Новый КвалификаторыСтроки(20)),"Режим");
ТаблицаФайловОбмена.Колонки.Добавить("Содержание",Новый ОписаниеТипов("Строка"),"Содержание");

ТаблицаУслуг		= Новый ТаблицаЗначений();
ТаблицаУслуг.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументОбъект.ЗаказПокупателя"),"Документ");
ТаблицаУслуг.Колонки.Добавить("ВидДоставки", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"),"ВидДоставки");
ТаблицаУслуг.Колонки.Добавить("СтоимостьДоставки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный)),"СтоимостьДоставки");

ПредставленияВалют	= Новый ТаблицаЗначений();

Права				= Неопределено;

СообщениеОНевыгруженныхТоварах	= "";
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


#Если Клиент Тогда

// Вывод результата обновления ставки НДС в объектах.
//
Процедура ВывестиОтчет(ТаблицаРезультат) Экспорт
	МакетОтчета = ПолучитьМакет("Отчет");
	
	ПолеОтчета = Новый ТабличныйДокумент;
	
	Шапка    = МакетОтчета.ПолучитьОбласть("Шапка");
	Группа   = МакетОтчета.ПолучитьОбласть("ГруппаОбъекта");
	ИмяОбъекта = МакетОтчета.ПолучитьОбласть("ИмяОбъекта");
	Строка   = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал   = МакетОтчета.ПолучитьОбласть("Подвал");
	ПолеОтчета.Вывести(Шапка);
	
	ПолеОтчета.НачатьАвтогруппировкуСтрок();
	
	ТекущийТипОбъекта = "";
	ТекущийОбъект     = "";
	
	Для Каждого ТекущаяСтрока Из ТаблицаРезультат Цикл
		
		Если ТекущийТипОбъекта <> ТекущаяСтрока.ТипОбъекта Тогда
			Группа.Параметры.ГруппаОбъектов = ТекущаяСтрока.ТипОбъекта;
			ПолеОтчета.Вывести(Группа, 0,, Истина);
			ТекущийТипОбъекта = ТекущаяСтрока.ТипОбъекта;
		КонецЕсли;
		
		Если ТекущийОбъект <> ТекущаяСтрока.ИмяОбъекта Тогда
			ИмяОбъекта.Параметры.ИмяОбъекта = ТекущаяСтрока.ИмяОбъекта;
			ПолеОтчета.Вывести(ИмяОбъекта, 1,, Истина);
			ТекущийОбъект = ТекущаяСтрока.ИмяОбъекта;
		КонецЕсли;
		
		Строка.Параметры.Объект    = СокрЛП(Строка(ТекущаяСтрока.Объект));
		Строка.Параметры.Документ  = ТекущаяСтрока.Объект;
		Строка.Параметры.Результат = ТекущаяСтрока.Перезаписано;
		ПолеОтчета.Вывести(Строка, 2,, Истина);
		
	КонецЦикла;
	
	ПолеОтчета.ЗакончитьАвтоГруппировкуСтрок();
	
	ПолеОтчета.Вывести(Подвал);
	
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ФормаПечати.ОбъектПредставление = "Результат перезаполнения ставки НДС";
	ТабличныйДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабличныйДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабличныйДокумент.ПолеСлева				= 9;
	ТабличныйДокумент.ПолеСправа			= 6;
	//Выведем печатную форму	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ПолеОтчета);
	ФормаПечати.ОткрытьМодально();
	
КонецПроцедуры // ВывестиОтчет()

#КонецЕсли



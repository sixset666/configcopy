
Функция СоздатьДокументыПоДеревуРазмещения(ПараметрыДокумента, СтруктураШапкиДокумента=Неопределено, Отказ) Экспорт
	
	ВидДокумента = ПараметрыДокумента.ВидДокумента;
	ДеревоРазмещения = ПараметрыДокумента.ДеревоРазмещения;
	
	Результат = Новый Структура;
	
	Если ВидДокумента = "РеализацияТоваров" ИЛИ ВидДокумента = "Чек" Тогда
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ИмяПеременнойОбработки", "НетОбработки"); // Поставим такое имя, что бы у нас не рассчитывались скидки!
		ДопПараметры.Вставить("НеРассчитыватьСкидки", Истина);
		
		РеализацииТоваров = Новый Массив;
		ПеремещенияТоваров = Новый Массив;
		
		ДатаРеализации = ТекущаяДата();
		ДатаПеремещения = ДатаРеализации - 1;
		
		Если ПараметрыДокумента.РазделятьПоСкладам Тогда
			// Перемещений нет
			// По количеству складов будет количество реализаций
			Для Каждого СтрокаВидПредложения Из ДеревоРазмещения.Строки Цикл
				Для Каждого СтрокаРазмещение Из СтрокаВидПредложения.Строки Цикл
					ЕстьПомеченные = Ложь;
					Для Каждого СтрокаКорзины Из СтрокаРазмещение.Строки Цикл
						Если СтрокаКорзины.Пометка Тогда
							ЕстьПомеченные = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если ЕстьПомеченные Тогда
						Реализация = Документы[ВидДокумента].СоздатьДокумент();
						//Реализация.БлокироватьПерерасчетСкидок = Истина;
						Реализация.ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
						Реализация.Дата = ДатаРеализации;
						Реализация.Заполнить(Неопределено);
						Реализация.СкладКомпании = СтрокаРазмещение.Размещение;
						Реализация.ОбработкаРеквизита("СкладКомпании");
						Если СтруктураШапкиДокумента <> Неопределено Тогда
							Если ТипЗнч(СтруктураШапкиДокумента.Клиент) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(СтруктураШапкиДокумента.Клиент) Тогда
								Реализация.Контрагент = СтруктураШапкиДокумента.Клиент;
								Реализация.ОбработкаРеквизита("Контрагент");
							КонецЕсли;
						КонецЕсли;
						Реализация.Товары.Очистить();
						
						Для Каждого СтрокаКорзины Из СтрокаРазмещение.Строки Цикл
							Если СтрокаКорзины.Пометка Тогда
								НоваяСтрока = Реализация.Товары.Добавить();
								НоваяСтрока.Номенклатура = СтрокаКорзины.Номенклатура;
								Реализация.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока,, ДопПараметры);
								НоваяСтрока.Цена = СтрокаКорзины.Цена;
								Реализация.ОбработкаРеквизита("Товары.Цена", НоваяСтрока,, ДопПараметры);
								НоваяСтрока.Количество = СтрокаКорзины.Количество;
								Реализация.ОбработкаРеквизита("Товары.Количество", НоваяСтрока,, ДопПараметры);
							КонецЕсли;
						КонецЦикла;
						
						//Реализация.БлокироватьПерерасчетСкидок = Ложь;
						Реализация.Записать();
						РеализацииТоваров.Добавить(Реализация);
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			// Реализация одна со склада отгрузки
			// Может быть несколько перемещений
			СтрокиДляРеализации = Новый Массив;
			СтрокиДляПеремещения = Новый Соответствие;
			
			Для Каждого СтрокаВидПредложения Из ДеревоРазмещения.Строки Цикл
				Для Каждого СтрокаРазмещение Из СтрокаВидПредложения.Строки Цикл
					Для Каждого СтрокаКорзины Из СтрокаРазмещение.Строки Цикл
						Если СтрокаКорзины.Пометка Тогда
							СтрокиДляРеализации.Добавить(СтрокаКорзины);
							Если СтрокаКорзины.Размещение <> ПараметрыДокумента.СкладОтгрузки И ПараметрыДокумента.Перемещать Тогда
								Если СтрокиДляПеремещения.Получить(СтрокаКорзины.Размещение) = Неопределено Тогда
									СтрокиДляПеремещения.Вставить(СтрокаКорзины.Размещение, Новый Массив);
								КонецЕсли;
								СтрокиДляПеремещения[СтрокаКорзины.Размещение].Добавить(СтрокаКорзины);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
			Если СтрокиДляРеализации.Количество() > 0 Тогда
				Реализация = Документы[ВидДокумента].СоздатьДокумент();
				//Реализация.БлокироватьПерерасчетСкидок = Истина;
				Реализация.ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
				Реализация.Дата = ДатаРеализации;
				Реализация.Заполнить(Неопределено);
				Реализация.СкладКомпании = ПараметрыДокумента.СкладОтгрузки;
				Реализация.ОбработкаРеквизита("СкладКомпании");
				Если СтруктураШапкиДокумента <> Неопределено Тогда
					Если ТипЗнч(СтруктураШапкиДокумента.Клиент) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(СтруктураШапкиДокумента.Клиент) Тогда
						Реализация.Контрагент = СтруктураШапкиДокумента.Клиент;
						Реализация.ОбработкаРеквизита("Контрагент");
					КонецЕсли;
				КонецЕсли;
				Реализация.Товары.Очистить();
				
				Для Каждого СтрокаКорзины Из СтрокиДляРеализации Цикл
					НоваяСтрока = Реализация.Товары.Добавить();
					НоваяСтрока.Номенклатура = СтрокаКорзины.Номенклатура;
					Реализация.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока,, ДопПараметры);
					НоваяСтрока.Цена = СтрокаКорзины.Цена;
					Реализация.ОбработкаРеквизита("Товары.Цена", НоваяСтрока,, ДопПараметры);
					НоваяСтрока.Количество = СтрокаКорзины.Количество;
					Реализация.ОбработкаРеквизита("Товары.Количество", НоваяСтрока,, ДопПараметры);
				КонецЦикла;
				
				//Реализация.БлокироватьПерерасчетСкидок = Ложь;
				Реализация.Записать();
				РеализацииТоваров.Добавить(Реализация);
			КонецЕсли;
			
			Если СтрокиДляПеремещения.Количество() > 0 Тогда
				Для Каждого КлючЗначение Из СтрокиДляПеремещения Цикл
					Перемещение = Документы.ПеремещениеТоваров.СоздатьДокумент();
					Перемещение.Дата = ДатаПеремещения;
					Перемещение.Заполнить(Неопределено);
					Перемещение.СкладКомпании = КлючЗначение.Ключ;
					Перемещение.ОбработкаРеквизита("СкладКомпании");
					Перемещение.СкладПолучатель = ПараметрыДокумента.СкладОтгрузки;
					Перемещение.ОбработкаРеквизита("СкладПолучатель");
					Перемещение.Товары.Очистить();
					
					Для Каждого СтрокаКорзины Из КлючЗначение.Значение Цикл
						НоваяСтрока = Перемещение.Товары.Добавить();
						НоваяСтрока.Номенклатура = СтрокаКорзины.Номенклатура;
						Перемещение.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.Количество = СтрокаКорзины.Количество;
						Перемещение.ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
					КонецЦикла;
					
					Перемещение.Записать();
					ПеремещенияТоваров.Добавить(Перемещение);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Результат.Вставить("РеализацииТоваров", РеализацииТоваров);
		Результат.Вставить("ПеремещенияТоваров", ПеремещенияТоваров);
		
	Иначе
		
		ЗаказПокупателя = Документы[ВидДокумента].СоздатьДокумент();
		
		ЗаказПокупателя.Дата = ТекущаяДата();
	
		Если ВидДокумента = "ЗаказПокупателя" Тогда
			//ЗаказПокупателя.БлокироватьПерерасчетСкидок = Истина;
			ЗаказПокупателя.ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
			Если СтруктураШапкиДокумента = Неопределено Тогда
				ЗаказПокупателя.Заполнить(Неопределено);
			Иначе
				Если СтруктураШапкиДокумента.Свойство("ДокументОснование") И ЗначениеЗаполнено(СтруктураШапкиДокумента.ДокументОснование) Тогда
					ЗаказПокупателя.Заполнить(СтруктураШапкиДокумента.ДокументОснование);
					ЗаказПокупателя.Товары.Очистить();
				Иначе
					ЗаказПокупателя.Заполнить(Неопределено);
					Если ТипЗнч(СтруктураШапкиДокумента.Автомобиль) = Тип("СправочникСсылка.Автомобили") И ЗначениеЗаполнено(СтруктураШапкиДокумента.Автомобиль) Тогда
						ЗаказПокупателя.Автомобиль = СтруктураШапкиДокумента.Автомобиль;
						ЗаказПокупателя.ОбработкаРеквизита("Автомобиль");
					КонецЕсли;
					Если ТипЗнч(СтруктураШапкиДокумента.Клиент) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(СтруктураШапкиДокумента.Клиент) Тогда
						ЗаказПокупателя.Контрагент = СтруктураШапкиДокумента.Клиент;
						ЗаказПокупателя.ОбработкаРеквизита("Контрагент");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ЗаказПокупателя.СрокПоставки = ЗаказПокупателя.Дата;
			//ЗаказПокупателя.БлокироватьПерерасчетСкидок = Ложь;
			
		Иначе
			ЗаказПокупателя.Заполнить(Неопределено);
		КонецЕсли;
		
		ЗаказПокупателя.Записать();
		
		Результат.Вставить("ЗаказПокупателя", ЗаказПокупателя);
		
		Для Каждого СтрокаВидПредложения Из ДеревоРазмещения.Строки Цикл
			
			Если СтрокаВидПредложения.ВидПредложения = 1 Тогда // Склад компании
				
				Если СтрокаВидПредложения.Строки.Количество() > 0 Тогда
					Резервирование = СоздатьЗаказПоСкладу(ЗаказПокупателя, СтрокаВидПредложения, ПараметрыДокумента.Проводить И ПараметрыДокумента.Резервировать);
				КонецЕсли;
				
			ИначеЕсли СтрокаВидПредложения.ВидПредложения = 2 Тогда // Заказ поставщику
				
				Если СтрокаВидПредложения.Строки.Количество() > 0 Тогда
					Размещение = СоздатьЗаказПоЗаказуПоставщику(ЗаказПокупателя, СтрокаВидПредложения, ПараметрыДокумента.Проводить И ПараметрыДокумента.Размещать);
				КонецЕсли;
				
			ИначеЕсли СтрокаВидПредложения.ВидПредложения = 3 Тогда // Локальный прайс-лист
				
				Если СтрокаВидПредложения.Строки.Количество() > 0 Тогда
					СоздатьЗаказПоПрайсЛисту(ЗаказПокупателя, СтрокаВидПредложения, Ложь);
				КонецЕсли;
				
			ИначеЕсли СтрокаВидПредложения.ВидПредложения = 4 Тогда // Веб прайс-лист
				// Заказ по веб прайс-листу, нужно предварительно сохранить в регистр ПрайсЛистыКонтрагентов
				Если СтрокаВидПредложения.Строки.Количество() > 0 Тогда
					СоздатьЗаказПоПрайсЛисту(ЗаказПокупателя, СтрокаВидПредложения, Истина);
				КонецЕсли;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		Результат.Вставить("Размещение", Размещение);
		Результат.Вставить("Резервирование", Резервирование);
		
	КонецЕсли;
	
	Возврат ОбработатьСтруктуруДокументов(ПараметрыДокумента, Результат, Отказ);
	
КонецФункции

Процедура СоздатьЗаказПоПрайсЛисту(ЗаказПокупателя, СтрокаВидПредложения, ЗаписыватьПрайсЛист = Ложь)
	
	Если ЗаписыватьПрайсЛист Тогда
		ДатаПрайсЛиста = ТекущаяДата();
		Сч = 0;
		Для Каждого СтрокаРазмещения ИЗ СтрокаВидПредложения.Строки Цикл
			
			НаборЗаписейПрайсЛистВременный = РегистрыСведений.ПрайсЛистыКонтрагентовВременный.СоздатьНаборЗаписей();
			НаборЗаписейПрайсЛистВременный.Отбор.ПрайсЛист.Установить(СтрокаРазмещения.Размещение, Истина);
			НаборЗаписейПрайсЛистВременный.Отбор.ДатаЗаписи.Установить(ДатаПрайсЛиста, Истина);
			НаборЗаписейПрайсЛистВременный.Прочитать();
			
			Для Каждого СтрокаКорзины ИЗ СтрокаРазмещения.Строки Цикл
				Сч = Сч+1;
				СтрокаНабораВременный = НаборЗаписейПрайсЛистВременный.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНабораВременный, СтрокаКорзины, "Артикул, АртикулДляПоиска, Производитель, Наименование, КлючСтрокиПоставщика, Цена, Валюта, СрокПоставки, СрокПоставкиМинимальный, СрокПоставкиМаксимальный");
				СтрокаНабораВременный.ПрайсЛист = СтрокаРазмещения.Размещение;
				СтрокаНабораВременный.ДатаЗаписи = ДатаПрайсЛиста;
				СтрокаНабораВременный.КодПредложения = Сч;
				СтрокаНабораВременный.ЗапретЗагрузки = Истина;
			КонецЦикла;
			
			Если НаборЗаписейПрайсЛистВременный.Количество() > 0 Тогда
				НаборЗаписейПрайсЛистВременный.Записать();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Попытка
		СрокПоставки = ЗаказПокупателя.СрокПоставки;
	Исключение
	КонецПопытки;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяПеременнойОбработки", "НетОбработки"); // Поставим такое имя, что бы у нас не рассчитывались скидки!
	ДопПараметры.Вставить("НеРассчитыватьСкидки", Истина);
	
	Для Каждого СтрокаРазмещения ИЗ СтрокаВидПредложения.Строки Цикл
		
		Для Каждого СтрокаКорзины ИЗ СтрокаРазмещения.Строки Цикл
			
			Если СтрокаКорзины.Пометка Тогда
				
				НоваяСтрока = ЗаказПокупателя.Товары.Добавить();
				
				НоваяСтрока.КлючСтрокиПоставщика = СтрокаКорзины.КлючСтрокиПоставщика;
				Попытка
					НоваяСтрока.Поставщик = СтрокаКорзины.Размещение;
				Исключение
					НоваяСтрока.ПрайсЛист = СтрокаКорзины.Размещение;
				КонецПопытки;
				
				Если СрокПоставки <> Неопределено Тогда
					СрокПоставки = Макс(СрокПоставки, КонецДня(ЗаказПокупателя.Дата + СтрокаКорзины.СрокПоставкиМаксимальный));
					НоваяСтрока.СрокПоставкиВСтроке = СтрокаКорзины.СрокПоставки; // Строка
				КонецЕсли;
				
				НоваяСтрока.Номенклатура = СтрокаКорзины.Номенклатура;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока,, ДопПараметры);
				НоваяСтрока.Цена = СтрокаКорзины.Цена;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Цена", НоваяСтрока,, ДопПараметры);
				НоваяСтрока.Количество = СтрокаКорзины.Количество;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Количество", НоваяСтрока,, ДопПараметры);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если СрокПоставки <> Неопределено Тогда
		ЗаказПокупателя.СрокПоставки = СрокПоставки;
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьЗаказПоЗаказуПоставщику(ЗаказПокупателя, СтрокаВидПредложения, Размещать)
	
	Размещение = Неопределено;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяПеременнойОбработки", "НетОбработки"); // Поставим такое имя, что бы у нас не рассчитывались скидки!
	ДопПараметры.Вставить("НеРассчитыватьСкидки", Истина);
	
	Для Каждого СтрокаРазмещения ИЗ СтрокаВидПредложения.Строки Цикл
		
		Для Каждого СтрокаКорзины ИЗ СтрокаРазмещения.Строки Цикл
			
			Если СтрокаКорзины.Пометка Тогда
				
				НоваяСтрока = ЗаказПокупателя.Товары.Добавить();
				
				Попытка
					НоваяСтрока.Поставщик = СтрокаКорзины.Размещение.Контрагент;
				Исключение
				КонецПопытки;
				
				НоваяСтрока.Номенклатура = СтрокаКорзины.Номенклатура;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока,, ДопПараметры);
				НоваяСтрока.Цена = СтрокаКорзины.Цена;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Цена", НоваяСтрока,, ДопПараметры);
				НоваяСтрока.Количество = СтрокаКорзины.Количество;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Количество", НоваяСтрока,, ДопПараметры);
				
				Если Размещение = Неопределено И Размещать Тогда
					Размещение = Документы.РаспределениеЗаказаПокупателя.СоздатьДокумент();
					Размещение.Дата = ТекущаяДата();
					Размещение.Заполнить(ЗаказПокупателя.Ссылка);
					Размещение.Товары.Очистить();
				КонецЕсли;
				
				Если Размещение <> Неопределено Тогда
					НоваяСтрокаРазмещение = Размещение.Товары.Добавить();
					НоваяСтрокаРазмещение.Номенклатура = СтрокаКорзины.Номенклатура;
					Размещение.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрокаРазмещение);
					НоваяСтрокаРазмещение.Количество = СтрокаКорзины.Количество;
					Размещение.ОбработкаРеквизита("Товары.Количество", НоваяСтрокаРазмещение);
					НоваяСтрокаРазмещение.ЗаказПоставщику = СтрокаКорзины.Размещение;
					Размещение.ОбработкаРеквизита("Товары.ЗаказПоставщику", НоваяСтрокаРазмещение);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Размещение;
	
КонецФункции

Функция СоздатьЗаказПоСкладу(ЗаказПокупателя, СтрокаВидПредложения, Резервировать)
	
	Резервирование = Неопределено;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяПеременнойОбработки", "НетОбработки"); // Поставим такое имя, что бы у нас не рассчитывались скидки!
	ДопПараметры.Вставить("НеРассчитыватьСкидки", Истина);
	
	Для Каждого СтрокаРазмещения ИЗ СтрокаВидПредложения.Строки Цикл
		
		Для Каждого СтрокаКорзины  ИЗ СтрокаРазмещения.Строки Цикл
			
			Если СтрокаКорзины.Пометка Тогда
				
				НоваяСтрока = ЗаказПокупателя.Товары.Добавить();
				
				Попытка
					НоваяСтрока.Поставщик = СтрокаКорзины.Размещение;
				Исключение
				КонецПопытки;
				
				НоваяСтрока.Номенклатура = СтрокаКорзины.Номенклатура;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока,, ДопПараметры);
				НоваяСтрока.Цена = СтрокаКорзины.Цена;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Цена", НоваяСтрока,, ДопПараметры);
				НоваяСтрока.Количество = СтрокаКорзины.Количество;
				ЗаказПокупателя.ОбработкаРеквизита("Товары.Количество", НоваяСтрока,, ДопПараметры);
				
				Если Резервирование = Неопределено И Резервировать Тогда
					Резервирование = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
					Резервирование.Дата = ТекущаяДата();
					Резервирование.Заполнить(ЗаказПокупателя.Ссылка);
					Резервирование.Товары.Очистить();
				КонецЕсли;
				
				Если Резервирование <> Неопределено Тогда
					НоваяСтрокаРезервирование = Резервирование.Товары.Добавить();
					НоваяСтрокаРезервирование.Номенклатура = СтрокаКорзины.Номенклатура;
					Резервирование.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрокаРезервирование);
					НоваяСтрокаРезервирование.Количество = СтрокаКорзины.Количество;
					Резервирование.ОбработкаРеквизита("Товары.Количество", НоваяСтрокаРезервирование);
					НоваяСтрокаРезервирование.МестоРазмещения = СтрокаКорзины.Размещение;
					Резервирование.ОбработкаРеквизита("Товары.МестоРазмещения", НоваяСтрокаРезервирование);
					НоваяСтрокаРезервирование.ЗаказПокупателя = ЗаказПокупателя.Ссылка;
					Резервирование.ОбработкаРеквизита("Товары.ЗаказПокупателя", НоваяСтрокаРезервирование);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Резервирование;
	
КонецФункции

Функция ОбработатьСтруктуруДокументов(ПараметрыДокумента, СтруктураДокументов, Отказ)
	
	МассивСозданныхДокументов = Новый Массив;
	
	Если СтруктураДокументов.Свойство("ЗаказПокупателя") Тогда
		
		ЗаказПокупателя = СтруктураДокументов.ЗаказПокупателя;
		//Если ТипЗнч(ЗаказПокупателя) = Тип("ДокументОбъект.ЗаказПокупателя") Тогда
		//	ЗаказПокупателя.БлокироватьПерерасчетСкидок = Ложь;
		//КонецЕсли;
		
		Если ПараметрыДокумента.Проводить Тогда
			Попытка
				ЗаказПокупателя.Записать(РежимЗаписиДокумента.Проведение);
				Сообщить("Создан и проведен документ " + ЗаказПокупателя.Ссылка);
			Исключение
				ЗаказПокупателя.Записать(РежимЗаписиДокумента.Запись);
				Сообщить("Не удалось провести документ. Документ записан " + ЗаказПокупателя.Ссылка);
				Сообщить(ОписаниеОшибки());
				Отказ = Истина;
			КонецПопытки;
		Иначе
			ЗаказПокупателя.Записать(РежимЗаписиДокумента.Запись);
			Сообщить("Записан документ " + ЗаказПокупателя.Ссылка);
		КонецЕсли;
		
		МассивСозданныхДокументов.Добавить(ЗаказПокупателя.Ссылка);
		
		Если СтруктураДокументов.Резервирование <> Неопределено Тогда
			
			Резервирование = СтруктураДокументов.Резервирование;
			
			Если Отказ Тогда
				Сообщить("Создание документа резервирования отменено", СтатусСообщения.Важное);
			Иначе
				Попытка
					Резервирование.Записать(РежимЗаписиДокумента.Проведение);
					Сообщить("Создан и проведен документ " + Резервирование.Ссылка);
				Исключение
					Резервирование.Записать(РежимЗаписиДокумента.Запись);
					Сообщить("Не удалось провести документ. Документ записан " + Резервирование.Ссылка);
					Сообщить(ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
			
		Если СтруктураДокументов.Размещение <> Неопределено Тогда
			
			Размещение = СтруктураДокументов.Размещение;
			
			Если Отказ Тогда
				Сообщить("Создание документа размещения отменено", СтатусСообщения.Важное);
			Иначе
				Попытка
					Размещение.Записать(РежимЗаписиДокумента.Проведение);
					Сообщить("Создан и проведен документ " + Размещение.Ссылка);
				Исключение
					Размещение.Записать(РежимЗаписиДокумента.Запись);
					Сообщить("Не удалось провести документ. Документ записан " + Размещение.Ссылка);
					Сообщить(ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураДокументов.Свойство("РеализацииТоваров") Тогда
		
		Если СтруктураДокументов.Свойство("ПеремещенияТоваров") И СтруктураДокументов.ПеремещенияТоваров.Количество() > 0 Тогда
			
			Для Каждого Перемещение Из СтруктураДокументов.ПеремещенияТоваров Цикл
				
				Если Перемещение.Товары.Количество() > 0 Тогда
					
					Если ПараметрыДокумента.Проводить Тогда
						Попытка
							Перемещение.Записать(РежимЗаписиДокумента.Проведение);
							Сообщить("Создан и проведен документ " + Перемещение.Ссылка);
						Исключение
							Перемещение.Записать(РежимЗаписиДокумента.Запись);
							Сообщить("Не удалось провести документ. Документ записан " + Перемещение.Ссылка);
							Сообщить(ОписаниеОшибки());
						КонецПопытки;
					Иначе
						Перемещение.Записать(РежимЗаписиДокумента.Запись);
						Сообщить("Записан документ " + Перемещение.Ссылка);
					КонецЕсли;
					
				КонецЕсли;
				
				МассивСозданныхДокументов.Добавить(Перемещение.Ссылка);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Для Каждого Реализация Из СтруктураДокументов.РеализацииТоваров Цикл
			
			Если ПараметрыДокумента.Проводить Тогда
				Попытка
					Реализация.Записать(РежимЗаписиДокумента.Проведение);
					Сообщить("Создан и проведен документ " + Реализация.Ссылка);
				Исключение
					Отказ = Истина;
					Сообщить("Не удалось провести документ. Документ записан " + Реализация.Ссылка);
					Сообщить(ОписаниеОшибки());
				КонецПопытки;
			Иначе
				Реализация.Записать(РежимЗаписиДокумента.Запись);
				Сообщить("Записан документ " + Реализация.Ссылка);
			КонецЕсли;
			
			МассивСозданныхДокументов.Добавить(Реализация.Ссылка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивСозданныхДокументов;
	
КонецФункции


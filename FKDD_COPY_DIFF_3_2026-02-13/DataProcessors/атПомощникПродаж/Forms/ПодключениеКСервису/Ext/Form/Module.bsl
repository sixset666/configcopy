//////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ
//  Обработка-шаблон помощника состоит из т.н. поставляемой и переопределяемой части.
// При реализации переопределяемой части обработки достигается необходимая
// прикладная логика работы помощника.
// Логика навигации по страницам помощника и отображение элементов формы, 
// а также вызовы обработчиков событий выполняются автоматически из поставляемой части обработки,
// которая не требует внесения изменений.
//
//  Разработка переопределяемой части.
// Переопределяемая часть помощника состоит из визуальной и программной части.
// При разработке визуальной переопределяемой части следует разработать содержание
// страниц групп "ПанельОсновная", "ПанельНавигации" и "ПанельДекорации".
// Группа "ПанельДекорации" является опциональной и может быть исключена (удалена).
// Следует отметить, что имена групп "ПанельОсновная", "ПанельНавигации"
// и "ПанельДекорации" являются зарезервированными и не подлежат изменению.
// Создание и размещение полей на страницах формы не ограничено.
// Также не накладывается ограничения на создание реквизитов формы и реквизитов объекта,
// а также команд формы и пр.
//
//  После разработки визуальной части помощника следует создать программный код 
// в переопределяемой части модуля формы помощника. Разработку программного кода 
// начинают с описания таблиц переходов.
// Для каждого возможного сценария работы помощника определяется т.н. таблица переходов.
// Каждая таблица переходов определяет только один из возможных сценариев работы помощника.
// Таблица переходов представляет собой таблицу значений, каждая строка которой описывает
// один шаг помощника, она определяет внешний вид помощника на каждом шаге и обработчики событий,
// которые соответствуют текущему шагу помощника. 
// Таблиц переходов может быть несколько, но текущая таблица переходов всегда одна, 
// т.е. навигация по страницам помощника выполняется только по одной из таблиц переходов.
// Переключение между таблицами переходов (сценариями работы помощника) обычно выполняется
// в зависимости от выполняемых пользователем действий.
//
//  Обработчики событий переходов.
// Прикладная логика работы помощника реализуется посредством обработчиков событий переходов
// по страницам помощника. Выделяют три обработчика событий: "При открытии", "При переходе далее"
// и "При переходе назад". Обработчики событий выполняются в контексте клиента(*).
// При необходимости можно выполнить контекстный или неконтекстный вызов сервера из обработчика.
// Обработчики событий представляют собой функции с набором параметров. Используя параметры обработчиков
// можно добиться требуемого поведения переходов по страницам помощника. Возвращаемое значение функции-обработчика
// никак не анализируется, поэтому рекомендуется в качестве возвращаемого значения обработчиков
// указывать значение Неопределено или ничего не возвращать. Имя функции в модуле содержит обязательный префикс
// "Подключаемый_".
// При указании имени обработчика в таблице переходов имя обработчика следует задавать без префикса "Подключаемый_".
//
//  (*)Примечание:
// Если известно, что на клиенте никаких прикладных действий выполняться не будет,
// то выполнение обработчиков событий можно перенести в контекст сервера. Для этого необходимо
// установить серверный контекст у процедур блока "ВНУТРЕННИЕ ПРОЦЕДУРЫ И ФУНКЦИИ",
// а именно, для следующих процедур установить директиву компиляции &НаСервере:
// ИзменитьПорядковыйНомерПерехода, УстановитьПорядковыйНомерПерехода, ПорядковыйНомерПереходаПриИзменении,
// ПолучитьКнопкуФормыПоИмениКоманды.
//
// Эта обработка демонстрирует только один сценарий работы помощника (одна таблица переходов).
//////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Устанавливаем текущую таблицу переходов.
	ТаблицаПереходовПоСценарию1();
	
	// Позиционируемся на первом шаге помощника.
	ПорядковыйНомерПерехода = ?(РегистрацияЗавершена, 6, ?(ЗаявкаПринята, 5, ПерваяСтраница));
	УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СохранитьРегистрационныеДанные();
	
	//ТекстПредупреждения = НСтр("ru = 'Закрыть помощник?'");
	//ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
	//	ЭтотОбъект, Отказ, ТекстПредупреждения, "ЗакрытьФормуБезусловно");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	ИзменитьПорядковыйНомерПерехода(+1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	ИзменитьПорядковыйНомерПерехода(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	ЗакрытьФормуБезусловно = Истина;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	СохранитьРегистрационныеДанные();
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Поставляемая часть

&НаКлиенте
Процедура ИзменитьПорядковыйНомерПерехода(Итератор)
	
	ОчиститьСообщения();
	
	УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + Итератор);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядковыйНомерПерехода(Знач Значение)
	
	ЭтоПереходДалее = Значение > ПорядковыйНомерПерехода;
	
	ПорядковыйНомерПерехода = Значение;
	
	Если ПорядковыйНомерПерехода < 0 Тогда
		
		ПорядковыйНомерПерехода = 0;
		
	КонецЕсли;
	
	ПорядковыйНомерПереходаПриИзменении(ЭтоПереходДалее);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядковыйНомерПереходаПриИзменении(Знач ЭтоПереходДалее)
	
	// Выполняем обработчики событий перехода.
	ВыполнитьОбработчикиСобытийПерехода(ЭтоПереходДалее);
	
	// Устанавливаем отображение страниц.
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	Элементы.ПанельОсновная.ТекущаяСтраница  = Элементы[СтрокаПереходаТекущая.ИмяОсновнойСтраницы];
	Элементы.ПанельНавигации.ТекущаяСтраница = Элементы[СтрокаПереходаТекущая.ИмяСтраницыНавигации];
	
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяСтраницыДекорации) Тогда
		
		Элементы.ПанельДекорации.ТекущаяСтраница = Элементы[СтрокаПереходаТекущая.ИмяСтраницыДекорации];
		
	КонецЕсли;
	
	// Устанавливаем текущую кнопку по умолчанию.
	КнопкаДалее = ПолучитьКнопкуФормыПоИмениКоманды(Элементы.ПанельНавигации.ТекущаяСтраница, "КомандаДалее");
	
	Если КнопкаДалее <> Неопределено Тогда
		
		КнопкаДалее.КнопкаПоУмолчанию = Истина;
		
	Иначе
		
		КнопкаГотово = ПолучитьКнопкуФормыПоИмениКоманды(Элементы.ПанельНавигации.ТекущаяСтраница, "КомандаГотово");
		
		Если КнопкаГотово <> Неопределено Тогда
			
			КнопкаГотово.КнопкаПоУмолчанию = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоПереходДалее И СтрокаПереходаТекущая.ДлительнаяОперация Тогда
		
		ПодключитьОбработчикОжидания("ВыполнитьОбработчикДлительнойОперации", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработчикиСобытийПерехода(Знач ЭтоПереходДалее)
	
	// Обработчики событий переходов.
	Если ЭтоПереходДалее Тогда
		
		СтрокиПерехода = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода - 1));
		
		Если СтрокиПерехода.Количество() > 0 Тогда
			
			СтрокаПерехода = СтрокиПерехода[0];
			
			// Обработчик ПриПереходеДалее.
			Если Не ПустаяСтрока(СтрокаПерехода.ИмяОбработчикаПриПереходеДалее)
				И Не СтрокаПерехода.ДлительнаяОперация Тогда
				
				ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ)";
				ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПерехода.ИмяОбработчикаПриПереходеДалее);
				
				Отказ = Ложь;
				
				А = Вычислить(ИмяПроцедуры);
				
				Если Отказ Тогда
					
					УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
					
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		СтрокиПерехода = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода + 1));
		
		Если СтрокиПерехода.Количество() > 0 Тогда
			
			СтрокаПерехода = СтрокиПерехода[0];
			
			// Обработчик ПриПереходеНазад.
			Если Не ПустаяСтрока(СтрокаПерехода.ИмяОбработчикаПриПереходеНазад)
				И Не СтрокаПерехода.ДлительнаяОперация Тогда
				
				ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ)";
				ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПерехода.ИмяОбработчикаПриПереходеНазад);
				
				Отказ = Ложь;
				
				А = Вычислить(ИмяПроцедуры);
				
				Если Отказ Тогда
					
					УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
					
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	Если СтрокаПереходаТекущая.ДлительнаяОперация И Не ЭтоПереходДалее Тогда
		
		УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
		Возврат;
	КонецЕсли;
	
	// обработчик ПриОткрытии
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяОбработчикаПриОткрытии) Тогда
		
		ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ, ПропуститьСтраницу, ЭтоПереходДалее)";
		ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПереходаТекущая.ИмяОбработчикаПриОткрытии);
		
		Отказ = Ложь;
		ПропуститьСтраницу = Ложь;
		
		А = Вычислить(ИмяПроцедуры);
		
		Если Отказ Тогда
			
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
			
			Возврат;
			
		ИначеЕсли ПропуститьСтраницу Тогда
			
			Если ЭтоПереходДалее Тогда
				
				УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
				
			Иначе
				
				УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
				
			КонецЕсли;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработчикДлительнойОперации()
	
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	// Обработчик ОбработкаДлительнойОперации.
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяОбработчикаДлительнойОперации) Тогда
		
		ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ, ПерейтиДалее)";
		ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПереходаТекущая.ИмяОбработчикаДлительнойОперации);
		
		Отказ = Ложь;
		ПерейтиДалее = Истина;
		
		А = Вычислить(ИмяПроцедуры);
		
		Если Отказ Тогда
			
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
			
			Возврат;
			
		ИначеЕсли ПерейтиДалее Тогда
			
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
			
			Возврат;
			
		КонецЕсли;
		
	Иначе
		
		УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКнопкуФормыПоИмениКоманды(ЭлементФормы, ИмяКоманды)
	
	Для Каждого Элемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			
			ЭлементФормыПоИмениКоманды = ПолучитьКнопкуФормыПоИмениКоманды(Элемент, ИмяКоманды);
			
			Если ЭлементФормыПоИмениКоманды <> Неопределено Тогда
				
				Возврат ЭлементФормыПоИмениКоманды;
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Элемент) = Тип("КнопкаФормы")
			И Найти(Элемент.ИмяКоманды, ИмяКоманды) > 0 Тогда
			
			Возврат Элемент;
			
		Иначе
			
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Переопределяемая часть - Обработчики событий переходов.

// Обработчик выполняется при открытии страницы помощника "СтраницаОдин".
//
// Параметры:
//
//  Отказ - Булево - флаг отказа от открытия страницы;
//			если в обработчике поднять этот флаг, то переход на страницу выполнен не будет,
//			останется открытой предыдущая страница помощника согласно направлению перехода (вперед или назад).
//
//  ПропуститьСтраницу - Булево. Если поднять этот флаг, то страница будет пропущена
//			и управление перейдет на следующую страницу помощника согласно направлению перехода (вперед или назад).
//
//  ЭтоПереходДалее (только чтение) - Булево - флаг определяет направление перехода.
//			Истина - выполняется переход далее; Ложь - выполняется переход назад.
&НаКлиенте
Функция Подключаемый_СтраницаОдин_ПриОткрытии(Отказ, ПропуститьСтраницу, Знач ЭтоПереходДалее)
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	СтатусСервера = Цел(КодСостояния/10);
	СтатусПользователя = КодСостояния%10;
	
	Если СтатусСервера = 3 Тогда
		
		Элементы.ДекорацияОдин.Заголовок = 
		"Сервер не доступен!
		|
		|Проверьте правильность параметров подключения!";
		Элементы.атАдресСервиса.ТолькоПросмотр = Ложь;
		Элементы.атПортСервиса.ТолькоПросмотр = Ложь;
		
	Иначе
		
		Элементы.ДекорацияОдин.Заголовок =
		"Сервер доступен.
		|
		|Нажмите кнопку ""Далее"" для начала регистрации партнера.";
		
		Элементы.атАдресСервиса.ТолькоПросмотр = Истина;
		Элементы.атПортСервиса.ТолькоПросмотр = Истина;
		
		Если ЭтоПереходДалее Тогда
			ПропуститьСтраницу = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Обработчик перехода далее (на следующую страницу) при уходе со страницы помощника "СтраницаОдин".
//
// Параметры:
// Отказ - Булево - флаг отказа от выполнения перехода далее;
//					если в обработчике поднять этот флаг, то переход на следующую страницу выполнен не будет.
//
&НаКлиенте
Функция Подключаемый_СтраницаОдин_ПриПереходеДалее(Отказ)
	
	Ошибка = "";
	Если Не Элементы.атАдресСервиса.ТолькоПросмотр Тогда
		ПараметрыСервиса = Новый Структура;
		ПараметрыСервиса.Вставить("АдресСервиса", атАдресСервиса);
		ПараметрыСервиса.Вставить("ПортСервиса", атПортСервиса);
		
		Ошибка = "";
		Если СохранитьПараметрыСервисаНаСервере(ПараметрыСервиса) Тогда
			СервисДоступенНаСервере(Ошибка);
			// Для остальных форм оповестим
			//Оповестить("ОбновитьПрава", ПараметрыСеанса.Пользователь);
		КонецЕсли;
	КонецЕсли;
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	СтатусСервера = Цел(КодСостояния/10);
	СтатусПользователя = КодСостояния%10;
	
	Если СтатусСервера = 3 Тогда
		Предупреждение(Ошибка);
		
		Элементы.атАдресСервиса.ТолькоПросмотр = Ложь;
		Элементы.атПортСервиса.ТолькоПросмотр = Ложь;
		
		Отказ = Истина;
	Иначе
		Элементы.атАдресСервиса.ТолькоПросмотр = Истина;
		Элементы.атПортСервиса.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецФункции

// Обработчик перехода назад (на предыдущую страницу) при уходе со страницы помощника "СтраницаОдин".
//
// Параметры:
// Отказ - Булево - флаг отказа от выполнения перехода назад;
//					если в обработчике поднять этот флаг, то переход на предыдущую страницу выполнен не будет.
//
&НаКлиенте
Функция Подключаемый_СтраницаОдин_ПриПереходеНазад(Отказ)
	
	
КонецФункции


&НаКлиенте
Функция Подключаемый_СтраницаДва_ПриОткрытии(Отказ, ПропуститьСтраницу, Знач ЭтоПереходДалее)
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаДва_ПриПереходеДалее(Отказ)
	// Заполнили параметры Организации и Подразделения
	Отказ = НЕ ПроверитьЗаполнениеПараметров("Организация");
КонецФункции


&НаКлиенте
Функция Подключаемый_СтраницаЧетыре_ПриОткрытии(Отказ, ПропуститьСтраницу, Знач ЭтоПереходДалее)
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаЧетыре_ПриПереходеДалее(Отказ)
	// Заполнили параметры Администратора
	Отказ = НЕ ПроверитьЗаполнениеПараметров("Пользователь");
КонецФункции


&НаКлиенте
Функция Подключаемый_СтраницаПять_ПриОткрытии(Отказ, ПропуститьСтраницу, Знач ЭтоПереходДалее)
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПять_ПриПереходеДалее(Отказ)
	
	Если ПроверитьЗаполнениеПараметров() Тогда
		
		Ошибка = "";
		Результат = ВыполнитьРегистрацияПартнераНаСервере(ПолучитьСтруктуруПараметров(), Ошибка);
		
		Если Результат = Неопределено Тогда
			Предупреждение("Ошибка при обработке запроса на сервере:"+Символы.ПС+Ошибка);
			Отказ = Истина;
		Иначе
			ЗаявкаПринята = Истина;
		КонецЕсли;
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецФункции


&НаКлиенте
Функция Подключаемый_СтраницаШесть_ПриОткрытии(Отказ, ПропуститьСтраницу, Знач ЭтоПереходДалее)
	Элементы.ДекорацияШесть.Заголовок =
	"Заявка на регистрацию партнера принята. На Вашу почту "+ПользовательЭлектроннаяПочта+" отправлен код для подтверждения регистрации.
	|
	|Для продолжения введете полученный код.";
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаШесть_ПриПереходеДалее(Отказ)
	
	Если НЕ ПустаяСтрока(СокрЛП(КодПодтверждения)) Тогда
		
		Ошибка = "";
		Результат = ВыполнитьПодтверждениеПодключенияНаСервере(КодПодтверждения, Ошибка);
		
		Если Результат = Неопределено Тогда
			Предупреждение("Ошибка при обработке запроса на сервере:"+Символы.ПС+Ошибка);
			Отказ = Истина;
			КодПодтверждения = "";
		Иначе
			РегистрацияЗавершена = Истина;
		КонецЕсли;
		
	Иначе
		
		Предупреждение("Не указан Код подтверждения!");
		Отказ = Истина;
		
	КонецЕсли;
	
КонецФункции


&НаКлиенте
Функция Подключаемый_СтраницаСемь_ПриОткрытии(Отказ, ПропуститьСтраницу, Знач ЭтоПереходДалее)
	Элементы.ДекорацияСемь.Заголовок =
	"Заявка на регистрацию получена сервисом!
	|Вам на почту "+ПользовательЭлектроннаяПочта+" будет отправлено письмо с подтверждением и регистрационными данными.
	|
	|Введите логин и пароль партнера из полученного письма.";
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаСемь_ПриПереходеДалее(Отказ)
	
	Если ПустаяСтрока(атЛогин) Тогда
		Отказ = Истина;
		Сообщить("Не заполнен логин партнера!");
	КонецЕсли;
	
	Если ПустаяСтрока(атПароль) Тогда
		Отказ = Истина;
		Сообщить("Не заполнен пароль партнера!");
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		ПараметрыСервиса = Новый Структура;
		ПараметрыСервиса.Вставить("ПользовательПартнера", атЛогин);
		ПараметрыСервиса.Вставить("ПарольПартнера", атПароль);
		
		Ошибка = "";
		Если СохранитьПараметрыСервисаНаСервере(ПараметрыСервиса, Истина, глПрава) Тогда
			СервисДоступенНаСервере(Ошибка);
			// Для остальных форм оповестим
			Оповестить("ОбновитьПрава", ПараметрыСеанса.Пользователь);
		КонецЕсли;
		
		КодСостояния = ПараметрыСеанса.атКодСостояния;
		СтатусСервера = Цел(КодСостояния/10);
		СтатусПользователя = КодСостояния%10;
		
		Если СтатусСервера = 4 Тогда
			Закрыть(Истина);
		Иначе
			Предупреждение(Ошибка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции



////////////////////////////////////////////////////////////////////////////////
// Переопределяемая часть - Инициализация переходов помощника.

// Процедура определяет таблицу переходов по сценарию №1.
//
&НаКлиенте
Процедура ТаблицаПереходовПоСценарию1()
	
	ТаблицаПереходов.Очистить();
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 1;
	Переход.ИмяОсновнойСтраницы            = "СтраницаОдин";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииНачало";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииНачало";
	Переход.ИмяОбработчикаПриОткрытии      = "СтраницаОдин_ПриОткрытии";
	Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаОдин_ПриПереходеДалее";
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 2;
	Переход.ИмяОсновнойСтраницы            = "СтраницаДва";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииПродолжение";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииНачало";
	Переход.ИмяОбработчикаПриОткрытии      = "СтраницаДва_ПриОткрытии";
	Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаДва_ПриПереходеДалее";
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 3;
	Переход.ИмяОсновнойСтраницы            = "СтраницаЧетыре";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииПродолжение";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииПродолжение";
	Переход.ИмяОбработчикаПриОткрытии      = "СтраницаЧетыре_ПриОткрытии";
	Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаЧетыре_ПриПереходеДалее";
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 4;
	Переход.ИмяОсновнойСтраницы            = "СтраницаПять";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииПродолжение";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииПродолжение";
	Переход.ИмяОбработчикаПриОткрытии      = "СтраницаПять_ПриОткрытии";
	Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаПять_ПриПереходеДалее";
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 5;
	Переход.ИмяОсновнойСтраницы            = "СтраницаШесть";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииПродолжение";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииПродолжение";
	Переход.ИмяОбработчикаПриОткрытии      = "СтраницаШесть_ПриОткрытии";
	Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаШесть_ПриПереходеДалее";
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 6;
	Переход.ИмяОсновнойСтраницы            = "СтраницаСемь";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииПродолжение";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииОкончание";
	Переход.ИмяОбработчикаПриОткрытии      = "СтраницаСемь_ПриОткрытии";
	Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаСемь_ПриПереходеДалее";
	
	Переход = ТаблицаПереходов.Добавить();
	Переход.ПорядковыйНомерПерехода        = 7;
	Переход.ИмяОсновнойСтраницы            = "СтраницаВосемь";
	Переход.ИмяСтраницыНавигации           = "СтраницаНавигацииОкончание";
	Переход.ИмяСтраницыДекорации           = "СтраницаДекорацииОкончание";
	//Переход.ИмяОбработчикаПриОткрытии      = "СтраницаВосемь_ПриОткрытии";
	//Переход.ИмяОбработчикаПриПереходеДалее = "СтраницаВосемь_ПриПереходеДалее";
	
КонецПроцедуры

#КонецОбласти


#Область ДобавленыеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СохранитьПараметрыСервисаНаСервере(Знач ПараметрыСервиса=Неопределено, Знач ИспользоватьИнтеграцию=Неопределено, ЗначенияПрав=Неопределено)
	
	ЕстьИзменения = Ложь;
	
	Если ПараметрыСервиса <> Неопределено Тогда
		Для Каждого КлючЗначение Из ПараметрыСервиса Цикл
			
			СтароеЗначение = атСервер.ПолучитьНастройку("Партнер", КлючЗначение.Ключ);
			Если СтароеЗначение <> КлючЗначение.Значение Тогда
				ЕстьИзменения = Истина;
				атСервер.ЗаписатьНастройку("Партнер", КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если ИспользоватьИнтеграцию <> Неопределено Тогда
		СтароеИспользоватьИнтеграцию = обПраво("атИспользоватьИнтеграцию");
		Если СтароеИспользоватьИнтеграцию <> ИспользоватьИнтеграцию Тогда
			ЕстьИзменения = Истина;
			атСервер.СохранитьПравоНастройку(Неопределено, ПредопределенноеЗначение("ПланВидовХарактеристик.ПраваИНастройки.атИспользоватьИнтеграцию"), ИспользоватьИнтеграцию, ЗначенияПрав);
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		сфЗаписатьДанныеСеансаОбновитьПрава();
	КонецЕсли;
	
	Возврат ЕстьИзменения;
КонецФункции
 
&НаСервереБезКонтекста
Процедура СервисДоступенНаСервере(Ошибка="")
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ПараметрыСеанса.атКодСостояния = 0;
	атСервер.ИнициализироватьКодСостояния();
	атСервер.СервисДоступен(Ошибка, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьСтруктуруОрганизации()
	Возврат Новый Структура("Наименование, НаименованиеПолное, ИНН, КПП, АдресЮридический, АдресФактический, Телефон, ЭлектроннаяПочта", Неопределено, Неопределено, Неопределено, "", Неопределено, "", Неопределено, Неопределено);
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьСтруктуруПодразделения()
	Возврат Новый Структура("Наименование, АдресФактический, Телефон", "", "", "");
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьСтруктуруПользователя()
	//Возврат Новый Структура("Наименование, Пароль, Телефон, ЭлектроннаяПочта", Неопределено, Неопределено, "", Неопределено);
	Возврат Новый Структура("Наименование, Телефон, ЭлектроннаяПочта", Неопределено, "", Неопределено);
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьСтруктуру(Знач Структура, НеЗаполненыеПоля = "")
	ЕстьНеЗаполненыеПоля = Ложь;
	Для Каждого КлючЗначение Из Структура Цикл
		Если КлючЗначение.Значение = Неопределено Тогда
			ЕстьНеЗаполненыеПоля = Истина;
			НеЗаполненыеПоля = НеЗаполненыеПоля+КлючЗначение.Ключ+"; ";
		КонецЕсли;
	КонецЦикла;
	Возврат ЕстьНеЗаполненыеПоля;
КонецФункции

&НаСервереБезКонтекста
Функция ОрганизацияПриИзмененииНаСервере(Знач Организация)
	
	Результат = Новый Структура("ОрганизацияНаименование, ОрганизацияНаименованиеПолное, ОрганизацияИНН,
								|ОрганизацияКПП, ОрганизацияАдресФактический, ОрганизацияАдресЮридический,
								|ОрганизацияТелефон, ОрганизацияЭлектроннаяПочта");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Организации.Наименование КАК ОрганизацияНаименование,
	|	Организации.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	Организации.ИНН КАК ОрганизацияИНН,
	|	Организации.КПП КАК ОрганизацияКПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Результат.Вставить("ОрганизацияАдресФактический", киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресФактический));
	Результат.Вставить("ОрганизацияАдресЮридический", киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический));
	Результат.Вставить("ОрганизацияТелефон",          киПолучитьПредставлениеКИ(Организация, Перечисления.ТипыКонтактнойИнформации.Телефон));
	Результат.Вставить("ОрганизацияЭлектроннаяПочта", киПолучитьПредставлениеКИ(Организация, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодразделениеКомпанииПриИзмененииНаСервере(Знач ПодразделениеКомпании)
	
	Результат = Новый Структура("ПодразделениеКомпанииНаименование, ПодразделениеКомпанииАдресФактический, ПодразделениеКомпанииТелефон");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодразделенияКомпании.Наименование КАК ПодразделениеКомпанииНаименование
	|ИЗ
	|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
	|ГДЕ
	|	ПодразделенияКомпании.Ссылка = &ПодразделениеКомпании";
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Результат.Вставить("ПодразделениеКомпанииАдресФактический", киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.АдресФактический));
	Результат.Вставить("ПодразделениеКомпанииТелефон",          киПолучитьПредставлениеКИ(ПодразделениеКомпании, Перечисления.ТипыКонтактнойИнформации.Телефон));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПользовательПриИзмененииНаСервере(Знач Пользователь)
	
	Результат = Новый Структура("ПользовательНаименование, ПользовательТелефон, ПользовательЭлектроннаяПочта");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Пользователи.Наименование КАК ПользовательНаименование,
	|	Пользователи.Сотрудник
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Ссылка = &Пользователь";
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		
		Если НЕ Выборка.Сотрудник.Пустая() Тогда
			Результат.Вставить("ПользовательТелефон",          киПолучитьПредставлениеКИ(Выборка.Сотрудник, Перечисления.ТипыКонтактнойИнформации.Телефон));
			Результат.Вставить("ПользовательЭлектроннаяПочта", киПолучитьПредставлениеКИ(Выборка.Сотрудник, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВыполнитьРегистрацияПартнераНаСервере(Знач ПараметрыПартнера, Ошибка)
	
	Возврат атСервер.ВыполнитьРегистрацияПартнера(ПараметрыПартнера, Ошибка);
	
КонецФункции

&НаСервереБезКонтекста
Функция ВыполнитьПодтверждениеПодключенияНаСервере(Знач КодПодтверждения, Ошибка)
	
	Возврат атСервер.ВыполнитьПодтверждениеПодключения(КодПодтверждения, Ошибка);
	
КонецФункции


&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ДанныеОрганизации = ОрганизацияПриИзмененииНаСервере(Организация);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	ДанныеПодразделения = ПодразделениеКомпанииПриИзмененииНаСервере(ПодразделениеКомпании);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеПодразделения);
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	
	ДанныеПользователя = ПользовательПриИзмененииНаСервере(Пользователь);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеПользователя);
	
КонецПроцедуры


&НаКлиенте
Функция ПолучитьСтруктуруПараметров()
	
	СтруктураОрганизации   = СоздатьСтруктуруОрганизации();
	СтруктураПодразделения = СоздатьСтруктуруПодразделения();
	СтруктураПользователя  = СоздатьСтруктуруПользователя();
	
	ЗаполнитьСтруктуру("Организация", СтруктураОрганизации);
	ЗаполнитьСтруктуру("ПодразделениеКомпании", СтруктураПодразделения);
	ЗаполнитьСтруктуру("Пользователь", СтруктураПользователя);
	
	Результат = Новый Структура("Данные", Новый Структура("Партнер, ОсновноеПодразделение, Администратор", СтруктураОрганизации, СтруктураПодразделения, СтруктураПользователя));
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСтруктуру(Знач ИмяОбъекта, Структура)
	
	Для Каждого КлючЗначение Из Структура Цикл
		ПустоеЗначениеПоля = КлючЗначение.Значение;
		Если ЗначениеЗаполнено(ЭтаФорма[ИмяОбъекта + КлючЗначение.Ключ]) Тогда
			Значение = ЭтаФорма[ИмяОбъекта + КлючЗначение.Ключ];
		Иначе
			Значение = ПустоеЗначениеПоля;
		КонецЕсли;
		Структура[КлючЗначение.Ключ] = Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьРегистрационныеДанные()
	
	РегистрационныеДанные = Новый Структура;
	
	СтруктураОрганизации = СоздатьСтруктуруОрганизации();
	ЗаполнитьСтруктуру("Организация", СтруктураОрганизации);
	Для Каждого КлючЗначение Из СтруктураОрганизации Цикл
		РегистрационныеДанные.Вставить("Организация"+КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	СтруктураПодразделения = СоздатьСтруктуруПодразделения();
	ЗаполнитьСтруктуру("ПодразделениеКомпании", СтруктураПодразделения);
	Для Каждого КлючЗначение Из СтруктураПодразделения Цикл
		РегистрационныеДанные.Вставить("ПодразделениеКомпании"+КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	СтруктураПользователя = СоздатьСтруктуруПользователя();
	ЗаполнитьСтруктуру("Пользователь", СтруктураПользователя);
	Для Каждого КлючЗначение Из СтруктураПользователя Цикл
		РегистрационныеДанные.Вставить("Пользователь"+КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	РегистрационныеДанные.Вставить("ЗаявкаПринята", ЗаявкаПринята);
	РегистрационныеДанные.Вставить("КодПодтверждения", КодПодтверждения);
	РегистрационныеДанные.Вставить("РегистрацияЗавершена", РегистрацияЗавершена);
	
	атСервер.ЗаписатьНастройку("Партнер", "РегистрационныеДанные",, РегистрационныеДанные);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеПараметров(Страница="")
	
	Если Страница = "Организация" ИЛИ Страница = "" Тогда
		
		СтруктураОрганизации = СоздатьСтруктуруОрганизации();
		ЗаполнитьСтруктуру("Организация", СтруктураОрганизации);
		НеЗаполненыеПоля = "";
		Если ПроверитьСтруктуру(СтруктураОрганизации, НеЗаполненыеПоля) Тогда
			Предупреждение("Для организации не заполнены обязательные поля: " + НеЗаполненыеПоля);
			Возврат Ложь;
		КонецЕсли;
		
		СтруктураПодразделения = СоздатьСтруктуруПодразделения();
		ЗаполнитьСтруктуру("ПодразделениеКомпании", СтруктураПодразделения);
		НеЗаполненыеПоля = "";
		Если ПроверитьСтруктуру(СтруктураПодразделения, НеЗаполненыеПоля) Тогда
			Предупреждение("Для подразделения не заполнены обязательные поля: " + НеЗаполненыеПоля);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Страница = "Пользователь" ИЛИ Страница = "" Тогда
		
		СтруктураПользователя  = СоздатьСтруктуруПользователя();
		ЗаполнитьСтруктуру("Пользователь", СтруктураПользователя);
		
		НеЗаполненыеПоля = "";
		Если ПроверитьСтруктуру(СтруктураПользователя, НеЗаполненыеПоля) Тогда
			Предупреждение("Для пользователя не заполнены обязательные поля: " + НеЗаполненыеПоля);
			Возврат Ложь;
		КонецЕсли;
		//Если ПользовательПароль <> ПользовательПарольПодтверждение Тогда
		//	Предупреждение("Пароль администратора не подтвержден!");
		//	Возврат Ложь;
		//КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПодключения(РезультатЗапроса)
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОчиститьТелефон(Телефон)
	Возврат СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(Телефон, "+7", ""), "(", ""), "-", ""), " ", ""), ")", "");
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	атСервер.СервисДоступен(, Ложь);
	
	СтатусСервера = Цел(ПараметрыСеанса.атКодСостояния/10);
	//(0, "Сервис не используется");
	//(1, "Не указаны параметры партнера"); // Партнер еще не подключен к сервису? Сервер доступен
	//(2, "Параметры партнера заполнены"); // Статус сервера не проверялся
	//(3, "Сервер не доступен"); // Пинг не прошел
	//(4, "Сервис доступен"); // Ок, Пинг партнера прошел
	//(5, "Ошибка данных партнера"); // Err, Пинг партнера не прошел
	//(6, "Доступ запрещен"); // Err, Пинг партнера прошел, Доступ запрещен
	
	Если СтатусСервера = 4 Тогда
		Сообщить("Компания уже является партнером сервиса ""Помощник продаж""");
		Отказ = Истина;
	КонецЕсли;
	
	РегистрационныеДанные = атСервер.ПолучитьДополнительныеДанные("Партнер", "РегистрационныеДанные");
	Если ЗначениеЗаполнено(РегистрационныеДанные) И ТипЗнч(РегистрационныеДанные) = Тип("Структура") Тогда
		//ЗаполнитьЗначенияСвойств(ЭтаФорма, РегистрационныеДанные,, "ОрганизацияТелефон, ПодразделениеКомпанииТелефон, ПользовательТелефон");
		//ОрганизацияТелефон = ОчиститьТелефон(РегистрационныеДанные.ОрганизацияТелефон);
		//ПодразделениеКомпанииТелефон = ОчиститьТелефон(РегистрационныеДанные.ПодразделениеКомпанииТелефон);
		//ПользовательТелефон = ОчиститьТелефон(РегистрационныеДанные.ПользовательТелефон);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, РегистрационныеДанные);
	КонецЕсли;
	
	СписокВыбора = атПовторноеИспользование.АдресСервераСписокВыбора();
	Для Каждого ЭлементСписка Из СписокВыбора Цикл
		Элементы.атАдресСервиса.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	атАдресСервиса = атСервер.ПолучитьНастройку("Партнер", "АдресСервиса");
	атПортСервиса = атСервер.ПолучитьНастройку("Партнер", "ПортСервиса");
	
	Если Не ЗначениеЗаполнено(атАдресСервиса) Тогда
		// Первое открытие
		ВыбранноеЗначение = Элементы.атАдресСервиса.СписокВыбора[0].Значение;
		Поз_ = Найти(ВыбранноеЗначение, ":");
		Если Поз_ > 0 Тогда
			атАдресСервиса = СокрЛП(Лев(ВыбранноеЗначение, Поз_-1));
			атПортСервиса = Число(Сред(ВыбранноеЗначение, Поз_+1));
		Иначе
			атАдресСервиса = ВыбранноеЗначение;
			атПортСервиса = 80;
		КонецЕсли;
		
		атСервер.ЗаписатьНастройку("Партнер", "АдресСервиса", атАдресСервиса);
		атСервер.ЗаписатьНастройку("Партнер", "ПортСервиса", атПортСервиса);
		ПерваяСтраница = 1;
	Иначе
		ПерваяСтраница = 2;
	КонецЕсли;
	
	// Для последней страницы
	атЛогин = атСервер.ПолучитьНастройку("Партнер", "ПользовательПартнера");
	атПароль = атСервер.ПолучитьНастройку("Партнер", "ПарольПартнера");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Организация = ОткрытьФормуМодально("Справочник.Организации.ФормаВыбора");
	ДанныеОрганизации = ОрганизацияПриИзмененииНаСервере(Организация);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеОрганизации);
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеКомпанииНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПодразделениеКомпании = ОткрытьФормуМодально("Справочник.ПодразделенияКомпании.ФормаВыбора");
	ДанныеПодразделения = ПодразделениеКомпанииПриИзмененииНаСервере(ПодразделениеКомпании);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеПодразделения);
КонецПроцедуры

&НаКлиенте
Процедура ПользовательНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Пользователь = ОткрытьФормуМодально("Справочник.Пользователи.ФормаВыбора");
	ДанныеПользователя = ПользовательПриИзмененииНаСервере(Пользователь);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеПользователя);
КонецПроцедуры

&НаКлиенте
Процедура атАдресСервисаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Поз_ = Найти(ВыбранноеЗначение, ":");
	ВыбранноеЗначение = Сред(ВыбранноеЗначение, Поз_+1);
	Поз_ = Найти(ВыбранноеЗначение, ":");
	Если Поз_ > 0 Тогда
		атАдресСервиса = Лев(ВыбранноеЗначение, Поз_-1);
		атПортСервиса = Число(Сред(ВыбранноеЗначение, Поз_+1));
	Иначе
		атАдресСервиса = ВыбранноеЗначение;
		атПортСервиса = 80;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТелефонОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура ТелефонИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
КонецПроцедуры


#КонецОбласти


////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция ПолучитьДанныеОбъектаНаСервере(Знач СтруктураСловаря, Знач Код=Неопределено, ВыбранноеЗначение=Неопределено)
	Возврат атСервер.ПолучитьОбъект(СтруктураСловаря, Код, ВыбранноеЗначение);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСопоставленнуюСсылкуНаСервере(Знач Объект, Знач ВозвращатьНаименование=Неопределено, ЗаписыватьОбъект=Ложь)
	Возврат атСервер.ПолучитьСопоставленнуюСсылку(Объект, ВозвращатьНаименование, ЗаписыватьОбъект);
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьОбъектИБНаСервере(Знач ИмяСправочника, Знач СтруктураСловаря, Знач ОбменДаннымиЗагрузка=Истина)
	Возврат атСервер.СоздатьОбъектИБ(ИмяСправочника, СтруктураСловаря, ОбменДаннымиЗагрузка);
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеСервиса(ПроизводительКод)
	
	Если ЭтаФорма.Модифицированность И ДанныеОбъекта.Код <> ПроизводительКод Тогда
		ЭтаФорма.Записать();
		ЭтаФорма.Модифицированность = Ложь;
		ДанныеОбъекта = Неопределено;
	КонецЕсли;
	
	Код = ПроизводительКод;
	Наименование = "";
	ВходитВГруппу = "";
	АртикулыОбязательны = Истина;
	НаименованияУникальны = Ложь;
	АдресЛоготипа = "";
	КонтактнаяИнформация = "";
	СинонимыСервис.Очистить();
	ДонорыСервис.Очистить();
	
	// Сначала проверим есть ли сопоставленная ссылка
	СтруктураСловаря = Новый Структура("Тип, Код", "Производители", ПроизводительКод);
	Производитель = ПолучитьСопоставленнуюСсылкуНаСервере(СтруктураСловаря,, Ложь);
	
	Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И Производитель.Пустая() Тогда
		Сообщить("Выбранного производителя запрещено использовать в программе!");
		Элементы.ВходитВГруппу.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
		Элементы.Наименование.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
	Иначе
		Элементы.ВходитВГруппу.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
		Элементы.Наименование.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Производитель) Тогда
		// Ранее был сопоставлен, оповещать никого не будем
		ДанныеОбъекта = ПолучитьДанныеОбъектаНаСервере(СтруктураСловаря);
		
	Иначе
		// При получении данных могли создать нового производителя или найти какого-нибудь
		ВыбранноеЗначение = Неопределено;
		ДанныеОбъекта = ПолучитьДанныеОбъектаНаСервере(СтруктураСловаря,, ВыбранноеЗначение);
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			Производитель = ВыбранноеЗначение;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Производитель) Тогда
		// Форма существующего объекта
		ЗначениеВРеквизитФормы(Производитель.ПолучитьОбъект(), "ПроизводительОбъект");
		Элементы.ГруппаИБ.Заголовок = "Производитель: " + Производитель.Наименование;
	Иначе
		// Форма нового объекта!!
		ЗначениеВРеквизитФормы(Справочники.Производители.СоздатьЭлемент(), "ПроизводительОбъект");
		Элементы.ГруппаИБ.Заголовок = "Производитель: Новый ...";
	КонецЕсли;
	
	Наименование = ДанныеОбъекта.Наименование;
	ВходитВГруппу = ДанныеОбъекта.ВходитВГруппу.Наименование;
	АдресЛоготипа = ДанныеОбъекта.Логотип;
	Если ДанныеОбъекта.Свойство("АртикулНеОбязательны") Тогда
		АртикулыОбязательны = Не ДанныеОбъекта.АртикулыНеОбязательны;
	Иначе
		АртикулыОбязательны = Истина;
	КонецЕсли;
	Если ДанныеОбъекта.Свойство("НаименованияУникальны") Тогда
		НаименованияУникальны = ДанныеОбъекта.НаименованияУникальны;
	Иначе
		НаименованияУникальны = Ложь;
	КонецЕсли;
	
	Для Каждого Синоним ИЗ ДанныеОбъекта.Синонимы Цикл
		Если НЕ ПустаяСтрока(Синоним.Синоним) Тогда
			НоваяСтрока = СинонимыСервис.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Синоним);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Донор ИЗ ДанныеОбъекта.Доноры Цикл
		ПроизводительДонор = ПолучитьСопоставленнуюСсылкуНаСервере(Донор.Производитель, Истина, Истина);
		Если ТипЗнч(ПроизводительДонор) = Тип("СправочникСсылка.Производители") И ПроизводительДонор.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ДонорыСервис.Добавить();
		НоваяСтрока.ПроизводительКод = Донор.Производитель.Код;
		НоваяСтрока.Производитель = ПроизводительДонор;
	КонецЦикла;
	
	Сч = 0;
	Для Каждого ЭлементКонтИнф ИЗ ДанныеОбъекта.КонтактнаяИнформация Цикл
		Сч = Сч + 1;
		ЗначениеКонтакта = ЭлементКонтИнф.Вид + ": " + ЭлементКонтИнф.Представление;
		Если Сч = 1 Тогда
			КонтактнаяИнформация = ЗначениеКонтакта;
		Иначе
			КонтактнаяИнформация = КонтактнаяИнформация + Символы.ПС + ЗначениеКонтакта;
		КонецЕсли;
	КонецЦикла;
	
	СравнитьДанные();
	
КонецПроцедуры

&НаСервере
Процедура СравнитьДанные()
	
	Для Каждого СтрокаТЗ ИЗ СинонимыСервис Цикл
		СтрокаТЗ.НетВИБ = ПроизводительОбъект.Синонимы.НайтиСтроки(Новый Структура("Синоним", СтрокаТЗ.Синоним)).Количество() = 0;
	КонецЦикла;
	
	Для Каждого СтрокаТЗ ИЗ ДонорыСервис Цикл
		СтрокаТЗ.НетВИБ = Истина;
		Если ТипЗнч(СтрокаТЗ.Производитель) = Тип("СправочникСсылка.Производители") Тогда
			СтрокаТЗ.НетВИБ = ПроизводительОбъект.ДонорыАртикулов.НайтиСтроки(Новый Структура("Производитель", СтрокаТЗ.Производитель)).Количество() = 0;
		КонецЕсли;
	КонецЦикла;
	
	УправлениеВидимостьюКнопок();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюКнопок()
	
	Если ПроизводительОбъект.Ссылка.Пустая() Тогда
		Элементы.ГруппаИБ.Заголовок = "Производитель: Новый ...";
		Элементы.ОбновитьВсе.Заголовок = "Заполнить все ->>";
		Элементы.ВыбратьПроизводителя.Видимость = Истина;
		Элементы.СоздатьПроизводителя.Видимость = Ложь;
	Иначе
		Элементы.ГруппаИБ.Заголовок = "Производитель: " + ПроизводительОбъект.Наименование;
		Элементы.ОбновитьВсе.Заголовок = "Обновить все ->>";
		Элементы.ВыбратьПроизводителя.Видимость = Ложь;
		Элементы.СоздатьПроизводителя.Видимость = Истина;
	КонецЕсли;
	
	Родитель = ПолучитьСопоставленнуюСсылкуНаСервере(ДанныеОбъекта.ВходитВГруппу, Ложь, Ложь);
	Элементы.ОбновитьГруппу.Доступность = НЕ ((ЗначениеЗаполнено(ПроизводительОбъект.Родитель) И ПроизводительОбъект.Родитель = Родитель)
												ИЛИ (Не ЗначениеЗаполнено(ПроизводительОбъект.Родитель) И НЕ ЗначениеЗаполнено(Родитель)));
	Элементы.ОбновитьНаименование.Доступность = ПроизводительОбъект.Наименование <> Наименование;
	Элементы.ОбновитьФлаги.Доступность = ПроизводительОбъект.АртикулыОбязательны <> АртикулыОбязательны ИЛИ ПроизводительОбъект.НаименованияУникальны <> НаименованияУникальны;
	Элементы.ОбновитьСинонимы.Доступность = СинонимыСервис.НайтиСтроки(Новый Структура("НетВИБ", Истина)).Количество() > 0;
	Элементы.ОбновитьДоноры.Доступность = ДонорыСервис.НайтиСтроки(Новый Структура("НетВИБ", Истина)).Количество() > 0;
	
	Элементы.ОбновитьВсе.Доступность = Элементы.ОбновитьГруппу.Доступность
										ИЛИ Элементы.ОбновитьНаименование.Доступность
										ИЛИ Элементы.ОбновитьФлаги.Доступность
										ИЛИ Элементы.ОбновитьСинонимы.Доступность
										ИЛИ Элементы.ОбновитьДоноры.Доступность;
										
КонецПроцедуры


&НаСервере
Процедура СкопироватьСинонимы()
	
	Для Каждого СтрокаТЗ Из СинонимыСервис Цикл
		Если НЕ СтрокаТЗ.НетВИБ Тогда
			Продолжить;
		КонецЕсли;
		НайденныеСтроки = ПроизводительОбъект.Синонимы.НайтиСтроки(Новый Структура("Синоним", СтрокаТЗ.Синоним));
		Если НайденныеСтроки.Количество() = 0 Тогда
			НовыйСиноним = ПроизводительОбъект.Синонимы.Добавить();
			НовыйСиноним.Синоним = СтрокаТЗ.Синоним;
		Иначе
			Для Каждого СтрокаСиноним Из НайденныеСтроки Цикл
				СтрокаСиноним.Ошибка = СтрокаТЗ.Ошибка;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьДоноров()
	
	Для Каждого Донор Из ДонорыСервис Цикл
		
		Если НЕ Донор.НетВИБ Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ (ТипЗнч(Донор.Производитель) = Тип("СправочникСсылка.Производители") И ЗначениеЗаполнено(Донор.Производитель)) Тогда
			// Попытаемся найти производителя
			СтруктураСловаря = Новый Структура("ЭтоСсылка, Тип, Код, Наименование", Истина, "Производители", Донор.ПроизводительКод, ""+Донор.Производитель);
			Донор.Производитель = атСервер.НайтиСоответствиеЭлементу(СтруктураСловаря);
			Если ЗначениеЗаполнено(Донор.Производитель) Тогда
				атСервер.СопоставитьСловарюСсылку(СтруктураСловаря, Донор.Производитель);
			Иначе
				//Донор.Производитель = СоздатьОбъектИБНаСервере("Производители", СтруктураСловаря, Истина);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Донор.Производитель) Тогда
			НоваяСтрока = ПроизводительОбъект.ДонорыАртикулов.Добавить();
			НоваяСтрока.Производитель = Донор.Производитель;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НовыйПроизводитель()
	// Форма нового объекта!!
	ЗначениеВРеквизитФормы(Справочники.Производители.СоздатьЭлемент(), "ПроизводительОбъект");
	СравнитьДанные();
КонецПроцедуры

&НаСервере
Процедура УстановитьПроизводителя(ПроизводительСсылка)
	ЗначениеВРеквизитФормы(ПроизводительСсылка.ПолучитьОбъект(), "ПроизводительОбъект");
	СравнитьДанные();
	ЭтаФорма.Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьТекущуюФорму()
	СтруктураФормы = Новый Структура("ДанныеЭлемента, Производитель", ДанныеОбъекта, ПроизводительОбъект.Ссылка);
	ОповеститьОВыборе(СтруктураФормы);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ОбновитьГруппу(Команда)
	ПроизводительОбъект.Родитель = ПолучитьСопоставленнуюСсылкуНаСервере(ДанныеОбъекта.ВходитВГруппу,, Истина);
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаименование(Команда)
	ПроизводительОбъект.Наименование = Наименование;
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФлаги(Команда)
	ПроизводительОбъект.АртикулыОбязательны = АртикулыОбязательны;
	ПроизводительОбъект.НаименованияУникальны = НаименованияУникальны;
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСинонимы(Команда)
	СкопироватьСинонимы();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоноры(Команда)
	СкопироватьДоноров();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВсе(Команда)
	Если Элементы.ОбновитьГруппу.Доступность Тогда
		ОбновитьГруппу(Команда);
	КонецЕсли;
	Если Элементы.ОбновитьНаименование.Доступность Тогда
		ОбновитьНаименование(Команда);
	КонецЕсли;
	Если Элементы.ОбновитьФлаги.Доступность Тогда
		ОбновитьФлаги(Команда);
	КонецЕсли;
	Если Элементы.ОбновитьСинонимы.Доступность Тогда
		ОбновитьСинонимы(Команда);
	КонецЕсли;
	Если Элементы.ОбновитьДоноры.Доступность Тогда
		ОбновитьДоноры(Команда);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроизводителя(Команда)
	
	ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
	ПроизводительСсылка = ОткрытьФормуМодально("Справочник.Производители.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
	Если ЗначениеЗаполнено(ПроизводительСсылка) Тогда
		УстановитьПроизводителя(ПроизводительСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроизводителя(Команда)
	НовыйПроизводитель();
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ФОРМЫ

&НаКлиенте
Процедура ПриИзмененииРеквизита(Элемент)
	СравнитьДанные();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
КонецПроцедуры //ПриОткрытии()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПараметрыСеанса.атКодСостояния%10 <> 3 Тогда // Не авторизован
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры //ПриСозданииНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "атПомощникПродаж_ОбъектПроизводитель_ПриОткрытии" Тогда
		Если ТипЗнч(Параметр) = Тип("Строка") Тогда
			ЗаполнитьДанныеСервиса(Параметр);
		ИначеЕсли ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Код") Тогда
			ЗаполнитьДанныеСервиса(Параметр.Код);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	//ЭтаФорма.Модифицированность = Ложь;
	атКлиент.УстановитьСоответствие(ДанныеОбъекта, ПроизводительОбъект.Ссылка, Истина);
	УправлениеВидимостьюКнопок();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

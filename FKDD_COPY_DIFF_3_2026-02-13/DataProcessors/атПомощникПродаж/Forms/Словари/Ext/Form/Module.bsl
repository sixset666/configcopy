&НаКлиенте
Перем ОтборВладелецКод;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПараметрыСеанса.атКодСостояния%10 <> 3 Тогда // Не авторизован
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗначениеВДанныеФормы(атПовторноеИспользование.ПолучитьТаблицуСловарей(), ТаблицаСловарей);
	
	Для Каждого СтрокаСловаря Из ТаблицаСловарей Цикл
		
		Если СтрокаСловаря.ФормаСписка <> "Словари" Тогда
			Продолжить;
		КонецЕсли;
		
		КомандаФормы = Команды.Добавить(СтрокаСловаря.Имя);
		КомандаФормы.Действие = "ВыборВидаСловаря";
		ЗаголовокКнопки = СокрЛП(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрокаСловаря.Представление, "(Партнер)", ""), "(Общие)", ""), "(Сервис)", ""));
		ЭлементФормы = Элементы.Добавить(СтрокаСловаря.Имя, Тип("КнопкаФормы"), Элементы.КоманднаяПанельСловариОбщие);
		
		//Если Найти(СтрокаСловаря.Представление, "(Сервис)") > 0 Тогда
		//	ЗаголовокКнопки = СокрЛП(СтрЗаменить(СтрокаСловаря.Представление, "(Сервис)", ""));
		//	ЭлементФормы = Элементы.Добавить(СтрокаСловаря.Имя, Тип("КнопкаФормы"), Элементы.КоманднаяПанельСловариСервис);
		//ИначеЕсли Найти(СтрокаСловаря.Представление, "(Партнер)") > 0 Тогда
		//	ЗаголовокКнопки = СокрЛП(СтрЗаменить(СтрокаСловаря.Представление, "(Партнер)", ""));
		//	ЭлементФормы = Элементы.Добавить(СтрокаСловаря.Имя, Тип("КнопкаФормы"), Элементы.КоманднаяПанельСловариПартнер);
		//Иначе
		//	ЗаголовокКнопки = СокрЛП(СтрЗаменить(СтрокаСловаря.Представление, "(Общие)", ""));
		//	ЭлементФормы = Элементы.Добавить(СтрокаСловаря.Имя, Тип("КнопкаФормы"), Элементы.КоманднаяПанельСловариОбщие);
		//КонецЕсли;
		
		ЭлементФормы.Заголовок = ЗаголовокКнопки;
		ЭлементФормы.ИмяКоманды = СтрокаСловаря.Имя;
		
	КонецЦикла;
	
	Отбор = Словари.Отбор.Элементы;
	
	УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тип");
	УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	УсловиеОтбора.ПравоеЗначение = "";
	УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Отбор = СловариВидыСвойств.Отбор.Элементы;
	
	УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВладелецКод");
	УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	УсловиеОтбора.ПравоеЗначение = "";
	УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	//УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тип");
	//УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	//УсловиеОтбора.ПравоеЗначение = "ВидыСвойств";
	//УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	//УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВладелецТип");
	//УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	//УсловиеОтбора.ПравоеЗначение = "ТипыНоменклатуры";
	//УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Отбор = СловариЗначенияСвойств.Отбор.Элементы;
	
	УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВладелецКод");
	УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	УсловиеОтбора.ПравоеЗначение = "";
	УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	//УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тип");
	//УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	//УсловиеОтбора.ПравоеЗначение = "ЗначенияСвойств";
	//УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	//УсловиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//УсловиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВладелецТип");
	//УсловиеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	//УсловиеОтбора.ПравоеЗначение = "ВидыСвойств";
	//УсловиеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Если Параметры.Свойство("ТекущаяСтраница") Тогда
		ВидСловаря = Параметры.ТекущаяСтраница;
	Иначе
		ВидСловаря = "Валюты";
	КонецЕсли;
	
	ВыбратьВидСловаря(ВидСловаря);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "атПомощникПродаж_Словари_ПриОткрытии" Тогда
		
		ВыбратьВидСловаря(Параметр);
		
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_ПользовательИзменен" Тогда
		
		Если ТекущаяСтраница = "Пользователи" Тогда
			Элементы.СловариПользователи.Обновить();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_УстановленоСоответствие" Тогда
		
		СсылкаСловаря = Параметр.СсылкаСловаря;
		//СсылкаИБ = Параметр.СсылкаИБ;
		
		Если СсылкаСловаря.Тип = "Пользователи" И ТекущаяСтраница = "Пользователи" Тогда
			Элементы.СловариПользователи.Обновить();
			
		Иначе
			Если СсылкаСловаря.Тип = ТекущаяСтраница Тогда
				Элементы.Словари.Обновить();
				
			ИначеЕсли СсылкаСловаря.Тип = "ВидыСвойств" И ТекущаяСтраница = "ТипыНоменклатуры" Тогда
				Элементы.СловариВидыСвойств.Обновить();
				
			ИначеЕсли СсылкаСловаря.Тип = "ЗначенияСвойств" И ТекущаяСтраница = "ТипыНоменклатуры" Тогда
				Элементы.СловариЗначенияСвойств.Обновить();
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьУсловноеОформление()
	
	УдаляемыеЭлементыОформления = Новый Массив;
	
	ЭлементыОформления = Словари.УсловноеОформление.Элементы;
	Для каждого ЭлементОформления Из ЭлементыОформления Цикл
		Если ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			УдаляемыеЭлементыОформления.Добавить(ЭлементОформления);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ЭлементОформления Из УдаляемыеЭлементыОформления Цикл
		ЭлементыОформления.Удалить(ЭлементОформления);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАвтосинхронизацию(Знач ВидСловаря, Знач ИмяЭлементаСинхронизации, Знач ИмяЭлементаСоздания)
	
	МассивСтрок = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", ВидСловаря));
	
	Если МассивСтрок.Количество() > 0 Тогда 
		СтрокаСловаря = МассивСтрок[0];
	КонецЕсли;
	
	Если СтрокаСловаря = Неопределено Тогда
		Сообщить("Неизвестный словарь сервиса - "+ВидСловаря);
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаСловаря.РеквизитСинхронизации) Тогда
		
		//Элементы[ИмяЭлементаСинхронизации].Видимость = Истина;
		Элементы[ИмяЭлементаСинхронизации].Доступность = Ложь;
		//Элементы[ИмяЭлементаСоздания].Видимость = Ложь;
		Элементы[ИмяЭлементаСоздания].Доступность = Ложь;
		ЭтаФорма[ИмяЭлементаСинхронизации] = Ложь;
		ЭтаФорма[ИмяЭлементаСоздания] = Ложь;
		
	Иначе
		
		//Элементы[ИмяЭлементаСинхронизации].Видимость = Истина;
		
		АвтосинхронизацияСловаря = атСервер.ПолучитьНастройку(СтрокаСловаря.Имя, "Автосинхронизация");
		Если АвтосинхронизацияСловаря = Неопределено Тогда
			ЭтаФорма[ИмяЭлементаСинхронизации] = СтрокаСловаря.Автосинхронизация <> Неопределено И СтрокаСловаря.Автосинхронизация;
		Иначе
			ЭтаФорма[ИмяЭлементаСинхронизации] = СтрокаСловаря.Автосинхронизация <> Неопределено И АвтосинхронизацияСловаря;
		КонецЕсли;
		Элементы[ИмяЭлементаСинхронизации].Доступность = СтрокаСловаря.Автосинхронизация <> Неопределено;
		
		АвтосозданиеСловаря = атСервер.ПолучитьНастройку(СтрокаСловаря.Имя, "Автосоздание");
		Если АвтосозданиеСловаря = Неопределено Тогда
			ЭтаФорма[ИмяЭлементаСоздания] = СтрокаСловаря.Автосоздание <> Неопределено И СтрокаСловаря.Автосоздание;
		Иначе
			ЭтаФорма[ИмяЭлементаСоздания] = СтрокаСловаря.Автосоздание <> Неопределено И АвтосозданиеСловаря;
		КонецЕсли;
		//Элементы[ИмяЭлементаСоздания].Видимость = Автосинхронизация;
		Элементы[ИмяЭлементаСоздания].Доступность = Автосинхронизация И СтрокаСловаря.Автосоздание <> Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВидСловаря(ВидСловаря)
	
	Если ТекущаяСтраница = ВидСловаря Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтрок = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", ВидСловаря));
	
	Если МассивСтрок.Количество() > 0 Тогда 
		СтрокаСловаря = МассивСтрок[0];
	КонецЕсли;
	
	Если СтрокаСловаря = Неопределено Тогда
		Сообщить("Неизвестный словарь сервиса - "+ВидСловаря);
		Возврат;
	КонецЕсли;
	
	ТекущаяСтраница = ВидСловаря;
	Заголовок = "Словарь: " + ТекущаяСтраница;
	Элементы.ФормаСписка.Заголовок = "Список элементов словаря: "+ТекущаяСтраница;
	
	Для Каждого КомандаСловаря ИЗ Элементы.КоманднаяПанельСловариОбщие.ПодчиненныеЭлементы Цикл
		Если КомандаСловаря.Пометка Тогда
			КомандаСловаря.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	Для Каждого КомандаСловаря ИЗ Элементы.КоманднаяПанельСловариПартнер.ПодчиненныеЭлементы Цикл
		Если КомандаСловаря.Пометка Тогда
			КомандаСловаря.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	Для Каждого КомандаСловаря ИЗ Элементы.КоманднаяПанельСловариСервис.ПодчиненныеЭлементы Цикл
		Если КомандаСловаря.Пометка Тогда
			КомандаСловаря.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Элементы[ТекущаяСтраница].Пометка = Истина;
	
	Словари.Отбор.Элементы[0].ПравоеЗначение = ТекущаяСтраница;
	//Элементы.Словари.Обновить();
	
	Элементы.ПолучитьИзСервиса.Доступность = СтрокаСловаря.ПолучатьСписок;
	
	Элементы.СловариЗначение.Видимость = ЗначениеЗаполнено(СтрокаСловаря.ИмяСправочника);
	
	Если ЗначениеЗаполнено(СтрокаСловаря.ИмяСправочника) Тогда
		Если СтрокаСловаря.ИмяСправочника = "СвойстваОбъектов" Тогда
			ТипЗначенияПоляВвода = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка."+СтрокаСловаря.ИмяСправочника);
			ПустоеЗначение = ПредопределенноеЗначение("ПланВидовХарактеристик."+СтрокаСловаря.ИмяСправочника+".ПустаяСсылка");
		Иначе
			ТипЗначенияПоляВвода = Новый ОписаниеТипов("СправочникСсылка."+СтрокаСловаря.ИмяСправочника);
			ПустоеЗначение = ПредопределенноеЗначение("Справочник."+СтрокаСловаря.ИмяСправочника+".ПустаяСсылка");
		КонецЕсли;
		Элементы.СловариЗначение.ОграничениеТипа = ТипЗначенияПоляВвода;
		Элементы.СловариЗначение.ДоступныеТипы = ТипЗначенияПоляВвода;
	Иначе
		ПустоеЗначение = Неопределено;
	КонецЕсли;
	Словари.Параметры.Элементы[0].Использование = Истина;
	Словари.Параметры.Элементы[0].Значение = ПустоеЗначение;
	
	ЗаполнитьАвтосинхронизацию(ТекущаяСтраница, "Автосинхронизация", "Автосоздание");
	
	Если Элементы.ФормаСписка.Видимость <> (ТекущаяСтраница <> "Пользователи") Тогда
		Элементы.ФормаСписка.Видимость = ТекущаяСтраница <> "Пользователи";
	КонецЕсли;
	
	Если ТекущаяСтраница = "ТипыНоменклатуры" ИЛИ ТекущаяСтраница = "Каталоги" Тогда
		Элементы.Словари.ВысотаВСтрокахТаблицы = 6;
		Если ТекущаяСтраница = "ТипыНоменклатуры" Тогда
			ЗаполнитьАвтосинхронизацию("ВидыСвойств", "АвтосинхронизацияВидыСвойств", "АвтосозданиеВидыСвойств");
			ЗаполнитьАвтосинхронизацию("ЗначенияСвойств", "АвтосинхронизацияЗначенияСвойств", "АвтосозданиеЗначенияСвойств");
		КонецЕсли;
	Иначе
		Элементы.Словари.ВысотаВСтрокахТаблицы = 0;
	КонецЕсли;
	
	Если ТекущаяСтраница = "ВебПрайсЛисты" Тогда
		Элементы.ПодключитьПрайсЛист.Видимость = Истина;
		Элементы.ОтключитьПрайсЛист.Видимость = Истина;
		ПолучитьПодключенныеУслугиНаСервере();
	Иначе
		Элементы.ПодключитьПрайсЛист.Видимость = Ложь;
		Элементы.ОтключитьПрайсЛист.Видимость = Ложь;
		УдалитьУсловноеОформление();
	КонецЕсли;
	
	Если ТекущаяСтраница = "Номенклатура" Тогда
		Элементы.НоменклатурнаяГруппа.Видимость = Истина;
	Иначе
		Элементы.НоменклатурнаяГруппа.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаТипыНоменклатуры.Видимость = ТекущаяСтраница = "ТипыНоменклатуры";
	Элементы.ГруппаКаталоги.Видимость = ТекущаяСтраница = "Каталоги";
	Элементы.ГруппаПользователи.Видимость = ТекущаяСтраница = "Пользователи";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборВидаСловаря(Команда)
	
	ВыбратьВидСловаря(Команда.Имя);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьИзСервисаНаСервере(Знач ИмяСловаря, Знач Отбор = Неопределено)
	
	атСервер.ОбновитьСписокСловаря(ИмяСловаря, Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзСервиса(Команда)
	
	Если ЗначениеЗаполнено(ТекущаяСтраница) Тогда
		ПолучитьИзСервисаНаСервере(ТекущаяСтраница);
		Элементы.Словари.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОбъектНаСервере(Знач СтруктураСловаря, Знач Код="")
	
	Возврат атСервер.ПолучитьОбъект(СтруктураСловаря, Код);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДополнительныеПараметрыНаСервере(Знач СтруктураСловаря, Знач Код="")
	
	Возврат атСервер.ПолучитьДополнительныеПараметры(СтруктураСловаря, Код);
	
КонецФункции


&НаКлиенте
Процедура СловариВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборСловаря(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	Элемент.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборСловаря(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСловаря = ТекСтрока.Тип;
	
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", ИмяСловаря))[0];
	
	Если Прав(Поле.Имя, 8) = "Значение" Тогда
		
		Если ПустаяСтрока(НастройкаСловаря.ИмяСправочника) Тогда
			Предупреждение("Тип "+ИмяСловаря+" не синхронизируется с ИБ!");
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		
		СписокДействий = Новый СписокЗначений;
		//СписокДействий.Добавить("ОткрытьЗапись", "Открыть запись");
		
		Если ТекСтрока.ЗапрещеноИспользование Тогда // Сопоставлено пустому значению
			
			СписокДействий.Добавить("Выбрать", "Выбрать элемент из списка");
			СписокДействий.Добавить("Очистить", "Отменить запрет использования");
			
		ИначеЕсли ЗначениеЗаполнено(ТекСтрока.Значение) И ТекСтрока.ЕстьСоответствие Тогда // Сопоставлено реальному значению
			
			СписокДействий.Добавить("Открыть", "Открыть карточку");
			СписокДействий.Добавить("Очистить", "Отменить соответствие");
			СписокДействий.Добавить("Запретить", "Запретить использование");
			
		ИначеЕсли ЗначениеЗаполнено(ТекСтрока.Значение) И НЕ ТекСтрока.ЕстьСоответствие Тогда
			
			СписокДействий.Добавить(ТекСтрока.Значение, "Выбрать <"+Строка(ТекСтрока.Значение)+">");
			СписокДействий.Добавить("Выбрать", "Выбрать элемент из списка");
			СписокДействий.Добавить("Запретить", "Запретить использование");
			
		Иначе
			
			СписокДействий.Добавить("Создать", "Создать новый элемент");
			СписокДействий.Добавить("Выбрать", "Выбрать элемент из списка");
			СписокДействий.Добавить("Запретить", "Запретить использование");
			
		КонецЕсли;
		
		
		Действие = ВыбратьИзСписка(СписокДействий);
		Если Действие = Неопределено Тогда
			
			Возврат;
			
		//ИначеЕсли Действие.Значение = "ОткрытьЗапись" Тогда
		//	
		//	СтандартнаяОбработка = Истина;
		//	
		ИначеЕсли Действие.Значение = "Открыть" Тогда
			
			ОткрытьЗначение(ТекСтрока.Значение);
			
		ИначеЕсли Действие.Значение = "Создать" Тогда
			
			СтруктураОбъекта = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод, Наименование");
			ЗаполнитьЗначенияСвойств(СтруктураОбъекта, ТекСтрока);
			
			Если НастройкаСловаря.ИмяСправочника = "ЗначенияСвойств" Тогда
				// Владельца возьмем из таблицы ВидыСвойств
				Владелец = Элементы.СловариВидыСвойств.ТекущиеДанные.Значение;
				
				Если НЕ ЗначениеЗаполнено(Владелец) Тогда
					Предупреждение("Владелец значений свойств не сопоставлен!");
					Возврат;
				КонецЕсли;
				
				СтруктураОбъекта.Вставить("Владелец", Владелец);
				
			ИначеЕсли НастройкаСловаря.ИмяСправочника = "ПрайсЛистыКонтрагентов" Тогда
				
				ДанныеЭлемента = ПолучитьОбъектНаСервере(СтруктураОбъекта);
				Если ДанныеЭлемента = Неопределено Тогда
					Предупреждение("Не найдены данные веб прайс-листа! Создание карточки невозможно.");
					Возврат;
				КонецЕсли;
				
				ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
				
				Владелец = ОткрытьФормуМодально("Справочник.Контрагенты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
				
				Если Не ЗначениеЗаполнено(Владелец) Тогда
					//Предупреждение("Владелец прайс-листа не определен!");
					Возврат;
				КонецЕсли;
				
				ДанныеЭлемента.Вставить("Владелец", Владелец);
				
				//ДопПараметры = ПолучитьДополнительныеПараметрыНаСервере(СтруктураОбъекта);
				//
				//Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
				//	Для Каждого КлючЗначение Из ДопПараметры Цикл
				//		ДанныеЭлемента.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
				//	КонецЦикла;
				//КонецЕсли;
				
				Основание = Новый Массив;
				Основание.Добавить(ДанныеЭлемента);
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("Основание", Основание);
				
				ОткрытьФорму("Справочник.ПрайсЛистыКонтрагентов.ФормаОбъекта", ПараметрыФормы);
				
				Возврат;
				
			КонецЕсли;
			
			атКлиент.УстановитьСоответствие(СтруктураОбъекта, атСервер.СоздатьОбъектИБ(НастройкаСловаря.ИмяСправочника, СтруктураОбъекта));
			
		ИначеЕсли Действие.Значение = "Выбрать" Тогда
			
			ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
			
			Если НастройкаСловаря.ИмяСправочника = "СвойстваОбъектов" Тогда
				
				ВыбранноеЗначение = ОткрытьФормуМодально("ПланВидовХарактеристик.СвойстваОбъектов.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
				
			ИначеЕсли НастройкаСловаря.ИмяСправочника = "ЗначенияСвойств" Тогда
				// Владельца возьмем из таблицы ВидыСвойств
				Владелец = Элементы.СловариВидыСвойств.ТекущиеДанные.Значение;
				
				Если Владелец = Неопределено Тогда
					Сообщить("Не сопоставлен владелец для значений свойств!");
					Возврат;
				ИначеЕсли Не ЗначениеЗаполнено(Владелец) Тогда
					Сообщить("Не указан владелец для значений свойств!");
					Возврат;
				КонецЕсли;
				
				Отбор = Новый Структура("Владелец", Владелец);
				ПараметрыФормы.Вставить("Отбор", Отбор);
				
				ВыбранноеЗначение = ОткрытьФормуМодально("Справочник.ЗначенияСвойств.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
				
			ИначеЕсли НастройкаСловаря.ИмяСправочника = "ПрайсЛистыКонтрагентов" Тогда
				
				Запрос = Новый Запрос;
				
				Запрос.УстановитьПараметр("ВидПрайсЛиста", Перечисления.ВидыПрайсЛистов.ВебПрайсЛист);
				Запрос.УстановитьПараметр("Тип", ТекСтрока.Тип);
				
				Запрос.Текст =
					"ВЫБРАТЬ
					|	ПрайсЛистыКонтрагентов.Ссылка
					|ИЗ
					|	Справочник.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атСловари КАК атСловари
					|		ПО (атСловари.Тип = &Тип)
					|			И ПрайсЛистыКонтрагентов.Ссылка = атСловари.Значение
					|ГДЕ
					|	ПрайсЛистыКонтрагентов.ВидПрайсЛиста = &ВидПрайсЛиста
					|	И атСловари.Код ЕСТЬ NULL ";
					
				РезультатЗапроса = Запрос.Выполнить();
				Выборка = РезультатЗапроса.Выбрать();
				
				СписокПрайсов = Новый СписокЗначений;
				Пока Выборка.Следующий() Цикл
					СписокПрайсов.Добавить(Выборка.Ссылка);
				КонецЦикла;
				
				Форма = Справочники.ПрайсЛистыКонтрагентов.ПолучитьФормуВыбора(, Элемент, УникальныйИдентификатор);
				
				Форма.Отбор.Ссылка.ВидСравнения = ВидСравнения.ВСписке;
				Форма.Отбор.Ссылка.Значение = СписокПрайсов;
				Форма.Отбор.Ссылка.Использование = Истина;
				
				ВыбранноеЗначение = Форма.ОткрытьМодально();
			
			Иначе
				ВыбранноеЗначение = ОткрытьФормуМодально("Справочник." + НастройкаСловаря.ИмяСправочника + ".ФормаВыбора", ПараметрыФормы, ЭтаФорма);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
				СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод, Наименование");
				ЗаполнитьЗначенияСвойств(СтруктураСловаря, ТекСтрока);
				атКлиент.УстановитьСоответствие(СтруктураСловаря, ВыбранноеЗначение, Истина);
			КонецЕсли;
			
		ИначеЕсли Действие.Значение = "Запретить" Тогда
			
			Если НастройкаСловаря.ИмяСправочника = "СвойстваОбъектов" Тогда
				Менеджер = ПланыВидовХарактеристик[НастройкаСловаря.ИмяСправочника];
			Иначе
				Менеджер = Справочники[НастройкаСловаря.ИмяСправочника];
			КонецЕсли;
			
			СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод, Наименование");
			ЗаполнитьЗначенияСвойств(СтруктураСловаря, ТекСтрока);
			атКлиент.УстановитьСоответствие(СтруктураСловаря, Менеджер.ПустаяСсылка(), Истина);
			
		ИначеЕсли Действие.Значение = "Очистить" Тогда
			
			СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод, Наименование");
			ЗаполнитьЗначенияСвойств(СтруктураСловаря, ТекСтрока);
			атКлиент.УстановитьСоответствие(СтруктураСловаря, Неопределено, Истина);
			
		Иначе
			
			СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод, Наименование");
			ЗаполнитьЗначенияСвойств(СтруктураСловаря, ТекСтрока);
			атКлиент.УстановитьСоответствие(СтруктураСловаря, ТекСтрока.Значение, Истина);
			
		КонецЕсли;
		
	Иначе
		
		ФормаОбъекта = НастройкаСловаря.ФормаОбъекта;
		СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод");
		ЗаполнитьЗначенияСвойств(СтруктураСловаря, ТекСтрока);
		ОткрытьФорму("Обработка.атПомощникПродаж.Форма."+ФормаОбъекта,,, ФормаОбъекта);
		Оповестить("атПомощникПродаж_"+ФормаОбъекта+"_ПриОткрытии", СтруктураСловаря);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СловариПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтраница = "ТипыНоменклатуры" Тогда
		
		ТипНоменклатурыКод = Элемент.ТекущиеДанные.Код;
		Элементы.ГруппаВидыСвойств.Заголовок = "Виды свойств ("+Элемент.ТекущиеДанные.Наименование+")";
		
		Для каждого ЭлементОтбора Из СловариВидыСвойств.Отбор.Элементы Цикл
			Если ЭлементОтбора.ЛевоеЗначение = ОтборВладелецКод Тогда
				Если ЭлементОтбора.ПравоеЗначение <> ТипНоменклатурыКод Тогда
					//ОтборОбновлен = Истина;
					ЭлементОтбора.ПравоеЗначение = ТипНоменклатурыКод;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		//Если ОтборОбновлен = Истина Тогда
		//	Элементы.СловариВидыСвойств.Обновить();
		//КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = "Каталоги" Тогда
		
	ИначеЕсли ТекущаяСтраница = "ВебПрайсЛисты" Тогда
		// Нужно управлять доступностью кнопок ПодключитьПрайсЛист и ОтключитьПрайсЛист
		Если ПодключенныеПрайсЛисты.НайтиПоЗначению(Элемент.ТекущиеДанные.Код) = Неопределено Тогда
			Элементы.ПодключитьПрайсЛист.Доступность = Истина;
			Элементы.ОтключитьПрайсЛист.Доступность = Ложь;
		Иначе
			Элементы.ПодключитьПрайсЛист.Доступность = Ложь;
			Элементы.ОтключитьПрайсЛист.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СловариПередНачаломИзменения(Элемент, Отказ)
	//Отказ = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьНастройкуНаСервере(ИмяСловаря, ВидНастройки, Значение)
	
	атСервер.ЗаписатьНастройку(ИмяСловаря, ВидНастройки, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтосинхронизацияПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере(ТекущаяСтраница, "Автосинхронизация", Автосинхронизация);
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", ТекущаяСтраница))[0];
	НастройкаСловаря.Автосинхронизация = Автосинхронизация;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтосозданиеПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере(ТекущаяСтраница, "Автосоздание", Автосоздание);
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", ТекущаяСтраница))[0];
	НастройкаСловаря.Автосоздание = Автосоздание;
	
КонецПроцедуры


//////////////////////////////////////////////////////////
//////////////////// НОМЕНКЛАТУРА ////////////////////////

&НаКлиенте
Процедура НоменклатурнаяГруппаПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере(ТекущаяСтраница, "НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	
КонецПроцедуры

//////////////////////////////////////////////////////////
//////////////////// ТИПЫ НОМЕНКЛАТУРЫ ///////////////////


&НаКлиенте
Процедура ПолучитьИзСервисаВидыСвойств(Команда)
	ТекСтрока = Элементы.Словари.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Предупреждение("Не выбран тип номенклатуры!");
	Иначе
		Отбор = Новый Структура("Владелец", ТекСтрока.Код);
		ПолучитьИзСервисаНаСервере("ВидыСвойств", Отбор);
		Элементы.СловариВидыСвойств.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзСервисаЗначенияСвойств(Команда)
	ТекСтрока = Элементы.СловариВидыСвойств.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Предупреждение("Не выбран вид свойств!");
	Иначе
		Отбор = Новый Структура("Владелец", ТекСтрока.Код);
		ПолучитьИзСервисаНаСервере("ЗначенияСвойств", Отбор);
		Элементы.СловариЗначенияСвойств.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СловариВидыСвойствПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		ВидСвойстваКод = "";
		Элементы.ГруппаЗначенияСвойств.Заголовок = "Значения свойств (...)";
	Иначе
		ВидСвойстваКод = Элемент.ТекущиеДанные.Код;
		Элементы.ГруппаЗначенияСвойств.Заголовок = "Значения свойств ("+Элемент.ТекущиеДанные.Наименование+")";
	КонецЕсли;
	
	Для каждого ЭлементОтбора Из СловариЗначенияСвойств.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ОтборВладелецКод Тогда
			Если ЭлементОтбора.ПравоеЗначение <> ВидСвойстваКод Тогда
				//ОтборОбновлен = Истина;
				ЭлементОтбора.ПравоеЗначение = ВидСвойстваКод;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//Если ОтборОбновлен = Истина Тогда
	//	Элементы.СловариЗначенияСвойств.Обновить();
	//КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура АвтосинхронизацияВидыСвойствПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере("ВидыСвойств", "Автосинхронизация", АвтосинхронизацияВидыСвойств);
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", "ВидыСвойств"))[0];
	НастройкаСловаря.Автосинхронизация = АвтосинхронизацияВидыСвойств;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтосозданиеВидыСвойствПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере("ВидыСвойств", "Автосоздание", АвтосозданиеВидыСвойств);
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", "ВидыСвойств"))[0];
	НастройкаСловаря.Автосоздание = АвтосозданиеВидыСвойств;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтосинхронизацияЗначенияСвойствПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере("ЗначенияСвойств", "Автосинхронизация", АвтосинхронизацияЗначенияСвойств);
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", "ЗначенияСвойств"))[0];
	НастройкаСловаря.Автосинхронизация = АвтосинхронизацияЗначенияСвойств;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтосозданиеЗначенияСвойствПриИзменении(Элемент)
	
	ЗаписатьНастройкуНаСервере("ЗначенияСвойств", "Автосоздание", АвтосозданиеЗначенияСвойств);
	НастройкаСловаря = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", "ЗначенияСвойств"))[0];
	НастройкаСловаря.Автосоздание = АвтосозданиеЗначенияСвойств;
	
КонецПроцедуры


//////////////////////////////////////////////////////////
//////////////////// ПОЛЬЗОВАТЕЛИ ////////////////////////

&НаКлиенте
Процедура СловариПользователиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Код", "");
	ПараметрыФормы.Вставить("Наименование", "Новый пользователь");
	ПараметрыФормы.Вставить("Администратор", Ложь);
	ПараметрыФормы.Вставить("ИмяМетода", "Создать");
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.ОбъектПользователь", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПользовательУдалитьНаСервере(Знач Логин, Ошибка="")
	
	Если Не ЗначениеЗаполнено(Логин) Тогда
		Ошибка = "Не заполнен Логин";
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Отбор", Новый Структура("Код", Логин));
	ТекстСообщения = атСервер.ПолучитьТекстСообщения(СтруктураПараметров);
	РезультатЗапроса = атСервер.ВыполнитьЗапрос("dictionaries", "Пользователи", ТекстСообщения, "Удалить",, Ошибка);
	
	Если РезультатЗапроса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.атСловари.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Тип.Установить("Пользователи", Истина);
	НаборЗаписей.Отбор.Код.Установить(Логин, Истина);
	НаборЗаписей.Записать();
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПользовательУдалитьНастройкиНаСервере(Логин)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.Объект
	|ИЗ
	|	РегистрСведений.атНастройки КАК Настройки
	|ГДЕ
	|	Настройки.ВидНастройки = ""Логин""
	|	И (ВЫРАЗИТЬ(Настройки.Значение КАК СТРОКА(9))) = &Логин";
	Запрос.УстановитьПараметр("Логин", Логин);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Запись = РегистрыСведений.атНастройки.СоздатьМенеджерЗаписи();
		Запись.ВидНастройки = "Логин";
		Запись.Объект = Выборка.Объект;
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Запись.Удалить();
		КонецЕсли;
		
		ЗаписьПароля = РегистрыСведений.атНастройки.СоздатьМенеджерЗаписи();
		ЗаписьПароля.ВидНастройки = "Пароль";
		ЗаписьПароля.Объект = Выборка.Объект;
		ЗаписьПароля.Прочитать();
		Если ЗаписьПароля.Выбран() Тогда
			ЗаписьПароля.Удалить();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СловариПользователиПередУдалением(Элемент, Отказ)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = "";
	Если Не ПользовательУдалитьНаСервере(ТекСтрока.Код, Ошибка) Тогда
		Предупреждение(Ошибка);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПользовательУдалитьНастройкиНаСервере(ТекСтрока.Код);
	
КонецПроцедуры

&НаКлиенте
Процедура СловариПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Прав(Поле.Имя, 8) = "Значение" Тогда
		
		СписокДействий = Новый СписокЗначений;
		
		Если ЗначениеЗаполнено(ТекСтрока.Значение) Тогда
			
			СписокДействий.Добавить("Открыть", "Открыть пользователя");
			СписокДействий.Добавить("Выбрать", "Выбрать из списка");
			СписокДействий.Добавить("Очистить", "Отменить соответствие");
			
		Иначе
			
			СписокДействий.Добавить("Выбрать", "Выбрать из списка");
			
		КонецЕсли;
		
		Действие = ВыбратьИзСписка(СписокДействий);
		Если Действие = Неопределено Тогда
			
			Возврат;
			
		ИначеЕсли Действие.Значение = "Открыть" Тогда
			
			ОткрытьЗначение(ТекСтрока.Значение);
			
		ИначеЕсли Действие.Значение = "Выбрать" Тогда
			
			ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
			
			ВыбранноеЗначение = ОткрытьФормуМодально("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
			
			Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
				ЗаписатьНастройкуНаСервере(ВыбранноеЗначение, "Логин", ТекСтрока.Код);
				Элементы.СловариПользователи.Обновить();
			КонецЕсли;
			
		ИначеЕсли Действие.Значение = "Очистить" Тогда
			
			ЗаписатьНастройкуНаСервере(ТекСтрока.Значение, "Логин", "");
			Элементы.СловариПользователи.Обновить();
			
		КонецЕсли;
		
	Иначе
		
		ПользовательОбъект = ПолучитьОбъектНаСервере("Пользователи", ТекСтрока.Код);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Код", ТекСтрока.Код);
		ПараметрыФормы.Вставить("Наименование", ТекСтрока.Наименование);
		ПараметрыФормы.Вставить("Администратор", ПользовательОбъект.Администратор);
		ПараметрыФормы.Вставить("ИмяМетода", "Изменить");
		ОткрытьФорму("Обработка.атПомощникПродаж.Форма.ОбъектПользователь", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СловариПользователиПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательОбъект = ПолучитьОбъектНаСервере("Пользователи", ТекСтрока.Код);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Код", ТекСтрока.Код);
	ПараметрыФормы.Вставить("Наименование", ТекСтрока.Наименование);
	ПараметрыФормы.Вставить("Администратор", ПользовательОбъект.Администратор);
	ПараметрыФормы.Вставить("ИмяМетода", "Изменить");
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.ОбъектПользователь", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователя(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Код", "");
	ПараметрыФормы.Вставить("Наименование", "Новый пользователь");
	ПараметрыФормы.Вставить("Администратор", Ложь);
	ПараметрыФормы.Вставить("ИмяМетода", "Создать");
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.ОбъектПользователь", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПользователя(Команда)
	
	ТекСтрока = Элементы.СловариПользователи.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = "";
	Если Не ПользовательУдалитьНаСервере(ТекСтрока.Код, Ошибка) Тогда
		Предупреждение(Ошибка);
		Возврат;
	КонецЕсли;
	
	ПользовательУдалитьНастройкиНаСервере(ТекСтрока.Код);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПарольПользователя(Команда)
	
	ТекСтрока = Элементы.СловариПользователи.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Код", ТекСтрока.Код);
	ПараметрыФормы.Вставить("Наименование", ТекСтрока.Наименование);
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.УстановитьПароль", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры


/////////////////////////////////////////////////////////////
//////////////////// ВЕБ ПРАЙС-ЛИСТЫ ////////////////////////


&НаСервере
Процедура ПолучитьПодключенныеУслугиНаСервере()
	
	УдалитьУсловноеОформление();
	
	Результат = атСервер.ПолучитьПодключенныеУслуги();
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Не Результат.Свойство("ВебПрайсЛисты") Тогда
			Возврат;
		КонецЕсли;
		
		ЭлементыОформления = Словари.УсловноеОформление.Элементы;
		
		ПодключенныеПрайсЛисты.Очистить();
		
		СписокВебПрайсЛистов = Новый СписокЗначений;
		СписокПодключенных = Новый СписокЗначений;
		СписокОжидающих = Новый СписокЗначений;
		Для каждого Элемент Из Результат.ВебПрайсЛисты Цикл
			Если Элемент.Статус = "Подключен" Тогда
				СписокПодключенных.Добавить(Элемент.ВебПрайсЛист.Код);
				СписокВебПрайсЛистов.Добавить(Элемент.ВебПрайсЛист.Код);
				ПодключенныеПрайсЛисты.Добавить(Элемент.ВебПрайсЛист.Код);
			ИначеЕсли Элемент.Статус = "ОжидаетПодключения" Тогда
				СписокОжидающих.Добавить(Элемент.ВебПрайсЛист.Код);
				СписокВебПрайсЛистов.Добавить(Элемент.ВебПрайсЛист.Код);
				ПодключенныеПрайсЛисты.Добавить(Элемент.ВебПрайсЛист.Код);
			КонецЕсли;
		КонецЦикла;
		
		// недоступные
		НовыйЭлементОформления = ЭлементыОформления.Добавить();
		НовыйЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ТемноСерый);
		ЭлементОтбора = НовыйЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Код");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
		ЭлементОтбора.ПравоеЗначение = СписокВебПрайсЛистов;
		
		// подключенные
		НовыйЭлементОформления = ЭлементыОформления.Добавить();
		НовыйЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПоля);
		ЭлементОтбора = НовыйЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Код");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение = СписокПодключенных;
		
		НовыйЭлементОформления = ЭлементыОформления.Добавить();
		НовыйЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", "Подключен");
		ЭлементОтбора = НовыйЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Код");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение = СписокПодключенных;
		ОформляемоеПоле = НовыйЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Примечание");
		ОформляемоеПоле.Использование = Истина;
		
		// ожидающие подключения
		НовыйЭлементОформления = ЭлементыОформления.Добавить();
		НовыйЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстИнформационнойНадписи);
		ЭлементОтбора = НовыйЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Код");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение = СписокОжидающих;
		
		НовыйЭлементОформления = ЭлементыОформления.Добавить();
		НовыйЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", "Ожидает");
		ЭлементОтбора = НовыйЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Код");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение = СписокОжидающих;
		ОформляемоеПоле = НовыйЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Примечание");
		ОформляемоеПоле.Использование = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодключитьПрайсЛистНаСервере(ВебПрайсЛистКод)
	Результат = атСервер.ПодключитьВебПрайсЛист(ВебПрайсЛистКод);
	ПолучитьПодключенныеУслугиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьПрайсЛист(Команда)
	Если ТекущаяСтраница <> "ВебПрайсЛисты" Тогда
		Возврат;
	КонецЕсли;
	ТекДанные = Элементы.Словари.ТекущиеДанные;
	Если ТекДанные = Неопределено ИЛИ НЕ ЗначениеЗаполнено(ТекДанные.Значение) Тогда
		// Подключать будем только сопоставленные прайс-листы
		Возврат;
	КонецЕсли;
	ПодключитьПрайсЛистНаСервере(ТекДанные.Код);
КонецПроцедуры

&НаСервере
Процедура ОтключитьВебПрайсЛистНаСервере(ВебПрайсЛистКод)
	Результат = атСервер.ОтключитьВебПрайсЛист(ВебПрайсЛистКод);
	ПолучитьПодключенныеУслугиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьПрайсЛист(Команда)
	Если ТекущаяСтраница <> "ВебПрайсЛисты" Тогда
		Возврат;
	КонецЕсли;
	ТекДанные = Элементы.Словари.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОтключитьВебПрайсЛистНаСервере(ТекДанные.Код);
КонецПроцедуры


ОтборВладелецКод = Новый ПолеКомпоновкиДанных("ВладелецКод");

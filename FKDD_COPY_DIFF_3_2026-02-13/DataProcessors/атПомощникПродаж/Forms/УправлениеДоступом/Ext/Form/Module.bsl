
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПараметрыСеанса.атКодСостояния%10 <> 3 Тогда // Не авторизован
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьДоступПользователяКСервисамНаСервере(Знач Данные, Ошибка="")
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("partner", "УстановитьДоступПользователяКСервисам", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаКлиенте
Процедура ДоступКСервисуПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ДоступПользователей.ТекущиеДанные;
	КодСервиса = СтрЗаменить(Элемент.Имя, "ДоступПользователейСервис_","");
	Логин = ТекСтрока.Код;
	ЗначениеДоступа = ТекСтрока["Сервис_" + КодСервиса];
	
	СтруктураПользователь = Новый Структура("Пользователь",Логин);
	МассивСервисов = Новый Массив;
	МассивСервисов.Добавить(Новый Структура("Сервис, Доступен", КодСервиса, ЗначениеДоступа));
	
	Ошибка = "";
	Данные = Новый Структура("Пользователь, Сервисы", Логин, МассивСервисов);
	Результат = УстановитьДоступПользователяКСервисамНаСервере(Данные, Ошибка);
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		// Усановим код состояния 3 для пользователей с этим логином
		УстановитьКодСостояния(Логин);
	Иначе
		Предупреждение(Ошибка);
		ТекСтрока["Сервис_" + КодСервиса] = Не ЗначениеДоступа; // отменим изменение галочки
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьКодСостояния(Логин)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	атНастройки.Объект КАК ПользовательИБ
		|ИЗ
		|	РегистрСведений.атНастройки КАК атНастройки
		|ГДЕ
		|	атНастройки.ВидНастройки = ""Логин""
		|	И атНастройки.Значение = &Логин
		|";
		
	Запрос.УстановитьПараметр("Логин", Логин);
	
	МассивПользователей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПользовательИБ");
	Если МассивПользователей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// получим список активных пользователей
	МассивСоединений = ПолучитьСоединенияИнформационнойБазы();
	Если МассивСоединений = Неопределено Тогда
		ТекстСообщения = "При записи обновления прав не удалось получить список соединений с текущей базой - возможно нет права на получение списка активных пользователей";
		ЗаписьЖурналаРегистрации("Обновление прав", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Для Инд = 0 По МассивСоединений.ВГраница() Цикл
		ТекСоединение = МассивСоединений[Инд];
		Если НЕ ТекСоединение.ИмяПриложения = "1CV8" Тогда
			Продолжить;
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ТекСоединение.Пользователь) Тогда
			Продолжить;
		КонецЕсли; 
		// пишем в регистр
		ТекПользователь = Справочники.Пользователи.НайтиПоКоду(ТекСоединение.Пользователь.Имя);
		Если ТекПользователь.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		Если МассивПользователей.Найти(ТекПользователь) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ТекНомерСоединения = ТекСоединение.НомерСоединения;
		Если ТекНомерСоединения = НомерСоединенияИнформационнойБазы() Тогда
			Продолжить; // самих себя не будем оповещать
		КонецЕсли; 
		Режим = 3;
		Попытка 
			НаборЗаписей = РегистрыСведений.РежимыРаботыПользователей.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Пользователь.Установить(ТекПользователь);
			НаборЗаписей.Отбор.НомерСоединения.Установить(ТекНомерСоединения);
			НаборЗаписей.Отбор.Режим.Установить(Режим); 
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				Запись = НаборЗаписей.Добавить();
				Запись.Пользователь		= ТекПользователь;
				Запись.НомерСоединения	= ТекНомерСоединения;
				Запись.Режим 			= Режим;
			Иначе
				Запись = НаборЗаписей[0];
			КонецЕсли;
			Запись.Начало = ТекущаяДата();
			ТекстСообщения = "C " +	Формат(Запись.Начало,"ДФ='дд.ММ.гггг ЧЧ:мм:сс'") + " был подключен обработчик обновления прав, установлен режим " + СокрЛП(Режим) + " (Номер соединения: " + СокрЛП(ТекНомерСоединения) + ")";
			Запись.Текст = ТекстСообщения;
			НаборЗаписей.Записать();
		Исключение
		КонецПопытки;
	КонецЦикла; 
	
КонецПроцедуры


&НаСервере
Процедура ПриОткрытииНаСервере(Отказ, Ошибка)
	
	Если Не ПараметрыСеанса.атДоступныеСервисы.Администратор Тогда
		Ошибка = "Недостаточно прав для управления пользователями";
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МассивРеквизитов = Новый Массив;
	
	// получим колонки таблицы доступа
	Результат = атСервер.ПолучитьПодключенныеУслуги();
	Сервисы = Результат.Сервисы;
	Для каждого ЭлементМассива Из Сервисы Цикл
		Если Не ЭлементМассива.РазрешеноУправлятьДоступом Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеРеквизита = Новый РеквизитФормы("Сервис_" + ЭлементМассива.Сервис.Код, Новый ОписаниеТипов("Булево"), "ДоступПользователей", ЭлементМассива.Сервис.Наименование);
		МассивРеквизитов.Добавить(ОписаниеРеквизита);
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	Для каждого ЭлементМассива Из Сервисы Цикл
		Если Не ЭлементМассива.РазрешеноУправлятьДоступом Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементФормы = Элементы.Добавить("ДоступПользователейСервис_" + ЭлементМассива.Сервис.Код, Тип("ПолеФормы"), Элементы.ДоступПользователей);
		ЭлементФормы.ПутьКДанным = "ДоступПользователей.Сервис_" + ЭлементМассива.Сервис.Код;
		ЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;
		ЭлементФормы.УстановитьДействие("ПриИзменении", "ДоступКСервисуПриИзменении");
		
		Если Не ЭлементМассива.РазрешеноУправлятьДоступом Тогда
			ЭлементФормы.Видимость = Ложь;
		КонецЕсли;
		// добавим действие при изменении
	КонецЦикла;
	
	// получим строки таблицы доступа
	Результат = атСервер.ВыполнитьЗапрос("dictionaries", "Пользователи",, "Список");
	Для каждого ЭлементМассива Из Результат Цикл
		НоваяСтрока = ДоступПользователей.Добавить();
		НоваяСтрока.Код = ЭлементМассива.Код;
		НоваяСтрока.Наименование = ЭлементМассива.Наименование;
		
		// получим галочки из сервиса
		ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Новый Структура("Пользователь", СокрЛП(НоваяСтрока.Код))));
		ДоступныеСервисы = атСервер.ВыполнитьЗапрос("partner", "УстановитьДоступПользователяКСервисам", ТелоЗапроса);
		
		//НоваяСтрока.ЭтоАдминистратор = ДоступныеСервисы.Администратор;
		Для каждого СтрокаСервиса Из ДоступныеСервисы.Сервисы Цикл
			Если Не СтрокаСервиса.Доступен Тогда
				Продолжить;
			КонецЕсли;
			
			КодСервиса = СтрокаСервиса.Сервис.Код;
			НоваяСтрока["Сервис_" + КодСервиса] = СтрокаСервиса.Доступен;
		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Ошибка = "";
	
	ПриОткрытииНаСервере(Отказ, Ошибка);
	
	Если Отказ Тогда
		Предупреждение(Ошибка);
	КонецЕсли;
	
КонецПроцедуры


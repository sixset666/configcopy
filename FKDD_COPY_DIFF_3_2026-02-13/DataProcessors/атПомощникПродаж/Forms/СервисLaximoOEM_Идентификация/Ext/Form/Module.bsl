

&НаСервереБезКонтекста
Функция ListCatalogs(Ошибка)
	ТелоЗапроса = "{""Данные"":{""Locale"":""ru_RU""}}";
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "ListCatalogs", ТелоЗапроса,,, Ошибка);
КонецФункции


&НаКлиенте
Процедура ЗаполнитьВсеКаталоги(Отказ)
	
	КаталогиВсе.Очистить();
	КаталогиVIN.Очистить();
	КаталогиFrame.Очистить();
	КаталогиWizard.Очистить();
	
	Ошибка = "";
	СтруктураРезультата = ListCatalogs(Ошибка);
	
	Если Не ЗначениеЗаполнено(СтруктураРезультата) ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
		Предупреждение("Каталоги Laximo не доступны!"+Символы.ПС+Ошибка);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанных Из СтруктураРезультата.row Цикл
		СтрокаКаталогаВсе = КаталогиВсе.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКаталогаВсе, СтрокаДанных);
		Если СтрокаКаталогаВсе.supportvinsearch = Истина Тогда
			НовСтрокаКаталога = КаталогиVIN.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрокаКаталога, СтрокаДанных);
		КонецЕсли;
		Если СтрокаКаталогаВсе.supportframesearch = Истина Тогда
			НовСтрокаКаталога = КаталогиFrame.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрокаКаталога, СтрокаДанных);
		КонецЕсли;
		Если СтрокаКаталогаВсе.supportparameteridentification2 = Истина Тогда
			НовСтрокаКаталога = КаталогиWizard.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрокаКаталога, СтрокаДанных);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПорядокВыбораWizard = "Произвольно";
	
	РабочийЛистОбъект = Параметры.Автомобиль;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ атКлиент.АвторизацияПройдена() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьВсеКаталоги(Отказ);
	
	Если ЗначениеЗаполнено(РабочийЛистОбъект) Тогда
		Элементы.КаталогиFrameframeexample.Видимость = Ложь;
		Элементы.КаталогиVINvinexample.Видимость = Ложь;
		РабочийЛистИсточник = ?(ТипЗнч(РабочийЛистОбъект) = Тип("СправочникСсылка.Модели"), "Модель", "Автомобиль");
		РабочийЛистОбъектПриИзмененииНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	
	Если (ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("Форма") И ЭтаФорма.ВладелецФормы.ЭлементыФормы.Найти("Автомобиль") <> Неопределено)
		ИЛИ (ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("ФормаКлиентскогоПриложения") И ЭтаФорма.ВладелецФормы.Элементы.Найти("Автомобиль") <> Неопределено) Тогда
		
		Если РабочийЛистОбъект <> ЭтаФорма.ВладелецФормы.Автомобиль Тогда
			РабочийЛистОбъект = ЭтаФорма.ВладелецФормы.Автомобиль;
			РабочийЛистИсточник = ?(ТипЗнч(РабочийЛистОбъект) = Тип("СправочникСсылка.Модели"), "Модель", "Автомобиль");
			РабочийЛистОбъектПриИзмененииНаКлиенте();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогиVINВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "КаталогиVINvinexample" Тогда
		РабочийЛистОбъект = КаталогиVIN.Получить(ВыбраннаяСтрока).vinexample;
		РабочийЛистОбъектПриИзмененииНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ПроверитьНомер();
	ПоискМодификаций(КаталогиVIN.Получить(ВыбраннаяСтрока).Code, "VIN");
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогиFrameВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "КаталогиFrameframeexample" Тогда
		РабочийЛистОбъект = КаталогиFrame.Получить(ВыбраннаяСтрока).frameexample;
		РабочийЛистОбъектПриИзмененииНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ПроверитьНомер();
	ПоискМодификаций(КаталогиFrame.Получить(ВыбраннаяСтрока).Code, "Frame");
	
КонецПроцедуры

&НаКлиенте
Процедура Поиск(Команда)
	ПроверитьНомер();
	ПоискМодификаций();
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиГодыВыпускаВДанных(Знач СтруктураДанных)
	
	СтруктураГодов = Новый Структура("month_from, year_from, month_to, year_to, МесяцВыпуска, ГодВыпуска",0,1,0,3000,0,0);
	Если СтруктураДанных = Неопределено Тогда
		Возврат СтруктураГодов;
	КонецЕсли;
		
	ДатаВыпуска = "";
	ModelYearFrom = "";
	ModelYearTo = "";
	
	Если ТипЗнч(СтруктураДанных) = Тип("Структура") Тогда
		
		Если СтруктураДанных.Свойство("Manufactured") И ЗначениеЗаполнено(СтруктураДанных.Manufactured) Тогда
			ДатаВыпуска = СтруктураДанных.Manufactured;
		Иначе
			Если СтруктураДанных.Свойство("Date") И ЗначениеЗаполнено(СтруктураДанных.Date) Тогда
				ДатаВыпуска = СтруктураДанных.Date;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураДанных.Свойство("ModelYearFrom", ModelYearFrom);
		СтруктураДанных.Свойство("ModelYearTo", ModelYearTo);
		
	ИначеЕсли  ТипЗнч(СтруктураДанных) = Тип("ТаблицаЗначений") Тогда// ТаблицаЗначений name key value
		НайденнаяСтрока = СтруктураДанных.Найти("date", "key");
		Если НайденнаяСтрока<>Неопределено И ЗначениеЗаполнено(НайденнаяСтрока.value) Тогда
			ДатаВыпуска = НайденнаяСтрока.value;
		Иначе
			НайденнаяСтрока = СтруктураДанных.Найти("manufactured", "key");
			Если НайденнаяСтрока<>Неопределено И ЗначениеЗаполнено(НайденнаяСтрока.value) Тогда
				ДатаВыпуска = НайденнаяСтрока.value;
			КонецЕсли;
		КонецЕсли;
		
		НайденнаяСтрока = СтруктураДанных.Найти("modelyearfrom", "key");
		Если НайденнаяСтрока<>Неопределено И ЗначениеЗаполнено(НайденнаяСтрока.value) Тогда
			ModelYearFrom = НайденнаяСтрока.value;
		КонецЕсли;
		
		НайденнаяСтрока = СтруктураДанных.Найти("modelyearto", "key");
		Если НайденнаяСтрока<>Неопределено И ЗначениеЗаполнено(НайденнаяСтрока.value) Тогда
			ModelYearTo = НайденнаяСтрока.value;
		КонецЕсли;

	Иначе
		//массив
		ДатаВыпуска = "";
		Для Каждого СтрокаМассива Из СтруктураДанных Цикл	// на самом деле у СтрокаМассива тип - структура, а у СтруктураДанных - массив :(
			Если (СтрокаМассива.key = "date" И СтрокаМассива.value <> "") ИЛИ
				(СтрокаМассива.key = "manufactured" И СтрокаМассива.value <> "")Тогда
				ДатаВыпуска = СтрокаМассива.value;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаМассива Из СтруктураДанных Цикл	// на самом деле у СтрокаМассива тип - структура, а у СтруктураДанных - массив :(
			Если (СтрокаМассива.key = "modelyearfrom" И СтрокаМассива.value <> "") Тогда
				ModelYearFrom = СтрокаМассива.value;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Для Каждого СтрокаМассива Из СтруктураДанных Цикл	// на самом деле у СтрокаМассива тип - структура, а у СтруктураДанных - массив :(
			Если (СтрокаМассива.key = "modelyearto" И СтрокаМассива.value <> "") Тогда
				ModelYearTo = СтрокаМассива.value;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаВыпуска) Тогда
		атСервер.ПрочитатьГодИМесяц(ДатаВыпуска, СтруктураГодов.ГодВыпуска, СтруктураГодов.МесяцВыпуска);
	КонецЕсли;
	Если ЗначениеЗаполнено(ModelYearFrom) Тогда
		атСервер.ПрочитатьГодИМесяц(ModelYearFrom, СтруктураГодов.year_from, СтруктураГодов.month_from);
	КонецЕсли;
	Если ЗначениеЗаполнено(ModelYearTo) Тогда
		атСервер.ПрочитатьГодИМесяц(ModelYearTo, СтруктураГодов.year_to, СтруктураГодов.month_to);
	КонецЕсли;
	
	Возврат СтруктураГодов;
	
КонецФункции

&НаСервереБезКонтекста
Функция РазобратьСтрокуСвойств(Знач СтрокаДляРазбора, Знач Разделитель="")
	
	МассивСтроки = Новый Массив();
	
	Если Не ЗначениеЗаполнено(Разделитель) Тогда //Определяем разделитель
		ЧислоСтрок = СтрЧислоСтрок(СтрокаДляРазбора);
		ЧислоТочекСЗапятой = СтрЧислоВхождений(СтрокаДляРазбора, ";");
		ЧислоЗапятых = СтрЧислоВхождений(СтрокаДляРазбора, ",");
		Если ЧислоСтрок > ЧислоТочекСЗапятой+1 И ЧислоСтрок > ЧислоЗапятых+1 Тогда
			Разделитель = Символы.ПС;
		Иначе
			Если ЧислоЗапятых > ЧислоТочекСЗапятой Тогда
				Разделитель = ",";
			Иначе
				Разделитель = ";";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Пока СтрДлина(СтрокаДляРазбора) > 0 Цикл
		ПозицияРазделителя = Найти(СтрокаДляРазбора, Разделитель);
		Если ПозицияРазделителя = 0 Тогда
			ПозицияРазделителя = СтрДлина(СтрокаДляРазбора) + 1;
		КонецЕсли;
		СтрокаПараметр = Лев(СтрокаДляРазбора, ПозицияРазделителя - 1);
		СтрокаДляРазбора = Сред(СтрокаДляРазбора, ПозицияРазделителя + 1);
		МассивСтроки.Добавить(УбратьКавычки(СтрокаПараметр));
	КонецЦикла;
	
	Если МассивСтроки.Количество() = 0 Тогда
		МассивСтроки.Добавить("");
	КонецЕсли;
	
	Возврат МассивСтроки;
	
КонецФункции

&НаСервереБезКонтекста
Функция УбратьКавычки(Знач СтрокаПараметр)
	СтрокаПараметр = СокрЛП(СтрокаПараметр);
	Если Лев(СтрокаПараметр, 1) = """" Тогда
		СтрокаПараметр = Сред(СтрокаПараметр, 2);
	КонецЕсли;
	Если Прав(СтрокаПараметр, 1) = """" Тогда
		СтрокаПараметр = Лев(СтрокаПараметр, СтрДлина(СтрокаПараметр)-1);
	КонецЕсли;
	СтрокаПараметр = СокрЛП(СтрокаПараметр);
	Возврат СтрокаПараметр;
КонецФункции

&НаКлиенте
Процедура ОткрытьКаталог(Команда)
	
	ТекСтрока = Элементы.Модификации.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда
		Предупреждение("Не выбрана строка модификации!");
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = ТекСтрока.ПараметрыМодификации;
	Если ТипЗнч(ПараметрыЗапроса) <> Тип("Структура")
		ИЛИ (Не (ЗначениеЗаполнено(ПараметрыЗапроса.Catalog)
			//И ЗначениеЗаполнено(ПараметрыЗапроса.VehicleId)
			И ЗначениеЗаполнено(ПараметрыЗапроса.ssd))) Тогда
		// Что очень странно ))
		Предупреждение("Параметры модификации не заполнены!");
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	Если ЗначениеЗаполнено(РабочийЛистОбъект) Тогда
		
		Результат = SaveInHistoryНаСервере(ПараметрыЗапроса);
		
		Если Результат = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// При сохранении в результате возвращаются ссылки на словари
		ПараметрыОткрытия.Вставить("Источник", РабочийЛистИсточник);
		ПараметрыОткрытия.Вставить("Объект", РабочийЛистОбъект);
		ПараметрыОткрытия.Вставить("Каталог", Результат.Каталог);
		ПараметрыОткрытия.Вставить("Модель", Результат.Модель);
		ПараметрыОткрытия.Вставить("Модификация", Результат.Модификация);
		
		
		// Параметры модификации возьмем далее из рабочего листа!
		
	Иначе
		ПараметрыОткрытия.Вставить("Каталог", Новый Структура("Тип, Код, Наименование", "Каталоги", "", "Laximo.OEM"));
		ПараметрыОткрытия.Вставить("Модель", Новый Структура("Тип, Код, Наименование", "Модели", "", ТекСтрока.Каталог));
		ПараметрыОткрытия.Вставить("Модификация", Новый Структура("Тип, Код, Наименование", "Модификации", "", ТекСтрока.Наименование));
		
	КонецЕсли;
	
	ПараметрыОткрытия.Вставить("ПараметрыЗапроса", Новый Структура);
	Для Каждого КлючЗначение ИЗ ПараметрыЗапроса Цикл
		ПараметрыОткрытия.ПараметрыЗапроса.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.СервисLaximoOEM_Каталог",, ?(ЭтаФорма.ВладелецФормы <> Неопределено, ЭтаФорма.ВладелецФормы, ЭтаФорма));
	Оповестить("атПомощникПродаж_СервисLaximoOEM_Каталог_ПараметрыОткрытия", ПараметрыОткрытия, ?(ЭтаФорма.ВладелецФормы <> Неопределено, ЭтаФорма.ВладелецФормы, ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура МодификацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьКаталог(Неопределено);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьНомерНаСервере(Номер)
	Возврат атСервер.ПроверитьНомер(Номер);
КонецФункции

&НаКлиенте
Процедура ПроверитьНомер()
	
	ПараметрыНомера = ПроверитьНомерНаСервере(Номер);
	
	ВидНомера = ПараметрыНомера.ВидНомера;
	
	Если ЗначениеЗаполнено(ПараметрыНомера.ВидНомера) ИЛИ Не (ЗначениеЗаполнено(ПараметрыНомера.Номер)) Тогда
		Элементы.Номер.ЦветРамки = ЦветаСтиля.ЦветРамки;
	Иначе
		Элементы.Номер.ЦветРамки = ЦветаСтиля.ТекстПредупреждающейНадписи;
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция FindVehicleByНаСервере(Знач ВидНомера, Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "FindVehicleBy"+ВидНомера, ТелоЗапроса,,, Ошибка);
КонецФункции

&НаКлиенте
Процедура ПоискМодификаций(Каталог="", ВидНомера="")
	
	Модификации.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ПараметрыНомера.ВидНомера) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Каталог) И ВидНомера <> ПараметрыНомера.ВидНомера Тогда
		Предупреждение("Номер не соответствует выбранному каталогу. Поиск не возможен!");
		Возврат;
	КонецЕсли;
	
	Если ПараметрыНомера.ВидНомера = "VIN" Тогда
		Элементы.СтраницыИдентификации.ТекущаяСтраница = Элементы.СтраницаИдентификацияПоVIN;
		Данные = Новый Структура("Catalog, VIN", Каталог, Номер);
	ИначеЕсли ПараметрыНомера.ВидНомера = "Frame" Тогда
		Элементы.СтраницыИдентификации.ТекущаяСтраница = Элементы.СтраницаИдентификацияПоFrame;
		Данные = Новый Структура("Catalog, Frame, FrameNo", Каталог, ПараметрыНомера.КодКузова, ПараметрыНомера.НомерКузова);
	КонецЕсли;
	
	Ошибка = "";
	СтруктураРезультата = FindVehicleByНаСервере(ПараметрыНомера.ВидНомера, Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено Тогда
		Предупреждение(Ошибка);
		Возврат;
	КонецЕсли;
	
	Если Не (ЗначениеЗаполнено(СтруктураРезультата) И СтруктураРезультата.Свойство("row") И ЗначениеЗаполнено(СтруктураРезультата.row)) Тогда
		Предупреждение("Для "+ПараметрыНомера.ВидНомера+": "+ВРег(ПараметрыНомера.Номер)+" нет информации в сервисе.");
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
		СтрокиДанных = СтруктураРезультата.row;
	Иначе
		СтрокиДанных = Новый Массив;
		СтрокиДанных.Добавить(СтруктураРезультата.row);
	КонецЕсли;
	
	Для Каждого СтрокаДанных Из СтрокиДанных Цикл
		Если ПустаяСтрока(СтрокаДанных.Catalog) Тогда
			СтрокаДанных.Catalog = Каталог;
		КонецЕсли;
		
		СтрокаКаталога = Неопределено;
		СтрокиКаталога = КаталогиВсе.НайтиСтроки(Новый Структура("code", СтрокаДанных.Catalog));
		Если СтрокиКаталога.Количество() > 0 Тогда
			СтрокаКаталога = СтрокиКаталога[0];
		КонецЕсли;
		
		Если СтрокаКаталога = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаМодификации = Модификации.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаМодификации, СтрокаДанных);
		
		СтруктураДатВыпуска = НайтиГодыВыпускаВДанных(СтрокаДанных.attribute);
		СтрокаМодификации.МесяцВыпуска = СтруктураДатВыпуска.МесяцВыпуска;
		СтрокаМодификации.ГодВыпуска = СтруктураДатВыпуска.ГодВыпуска;
		
		//Заполним свойства модификации
		Для Каждого attribute Из СтрокаДанных.attribute Цикл
			Строка = СтрокаМодификации.СвойстваМодификации.ПолучитьЭлементы().Добавить();
			Строка.Заголовок = attribute.name;
			
			Если СтрДлина(attribute.value) >= 50 ИЛИ НРег(attribute.name) = "options" Тогда
				МассивСвойств = РазобратьСтрокуСвойств(attribute.value);
				Если МассивСвойств.Количество()>1 Тогда
					Для Каждого Свойство2 Из МассивСвойств Цикл
						Строка2 = Строка.ПолучитьЭлементы().Добавить();
						Строка2.ЗначениеСвойства = Свойство2;
					КонецЦикла;
				Иначе
					Строка.ЗначениеСвойства = МассивСвойств[0];
				КонецЕсли;
			Иначе
				Строка.ЗначениеСвойства = attribute.value;
			КонецЕсли;
		КонецЦикла;
		//
		
		ПреобразоватьАттрибутыВРеквизиты(СтрокаДанных);
		
		СтрокаМодификации.Каталог = СтрокаКаталога.Name+" ("+СтрокаКаталога.Version+")";
		
		Модель = "";
		Если СтрокаДанных.Свойство("Model") Тогда
			Если ТипЗнч(СтрокаДанных.Model) = Тип("Структура") Тогда
				Модель = СтрокаДанных.Model.value;
			Иначе
				Модель = СтрокаДанных.Model;
			КонецЕсли;
		КонецЕсли;
		
		Модификация = "";
		Если СтрокаДанных.Свойство("Modification") Тогда
			Если ТипЗнч(СтрокаДанных.Modification) = Тип("Структура") Тогда
				Модификация = СтрокаДанных.Modification.value;
			Иначе
				Модификация = СтрокаДанных.Modification;
			КонецЕсли;
		КонецЕсли;
		
		МодельАвтомобиля = СокрЛП(Модель+" "+Модификация);
		
		СтрокаМодификации.Код = Число(СтрокаДанных.VehicleId);
		
		СтрокаМодификации.Наименование = СокрЛП(СтрокаДанных.Name)+?(ПустаяСтрока(МодельАвтомобиля), "", " ("+СокрЛП(МодельАвтомобиля)+")");
		
		ПараметрыМодификации = Новый Структура;
		ПараметрыМодификации.Вставить("Name", СтрокаМодификации.Наименование);
		ПараметрыМодификации.Вставить("Wizard", Ложь);
		ПараметрыМодификации.Вставить("catalog", СтрокаДанных.Catalog);
		ПараметрыМодификации.Вставить("vehicleid", Число(СтрокаДанных.VehicleId));
		ПараметрыМодификации.Вставить("ssd", СтрокаДанных.ssd);
		
		СтрокаМодификации.ПараметрыМодификации = ПараметрыМодификации;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьМодификацииWizard(Команда)
	ПоискПоWizard();
КонецПроцедуры

&НаСервереБезКонтекста
Функция FindVehicleByWizard2(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "FindVehicleByWizard2", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаКлиенте
Процедура ПоискПоWizard()
	
	Модификации.Очистить();
	
	Ошибка = "";
	Данные = Новый Структура("Catalog, ssd", Wizard.Catalog, Wizard.Ssd);
	СтруктураРезультата = FindVehicleByWizard2(Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
		Сообщить(Ошибка);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтруктураРезультата.row) Тогда
		Предупреждение("В каталоге не найдено модификаций по выбранным параметрам ");
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
		СтрокиДанных = СтруктураРезультата.row;
	Иначе
		СтрокиДанных = Новый Массив;
		СтрокиДанных.Добавить(СтруктураРезультата.row);
	КонецЕсли;
	
	Для Каждого СтрокаДанных Из СтрокиДанных Цикл
		
		Если ПустаяСтрока(СтрокаДанных.Catalog) Тогда
			СтрокаДанных.Catalog = Wizard.Catalog;
		КонецЕсли;
		
		НайденныеКаталоги = КаталогиWizard.НайтиСтроки(Новый Структура("code", СтрокаДанных.Catalog));
		Если НайденныеКаталоги.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Каталог = НайденныеКаталоги[0];
		
		СтрокаМодификации = Модификации.Добавить();
		СтрокаМодификации.Wizard = Истина;
		
		//Заполним свойства модификации
		Для Каждого attribute Из СтрокаДанных.attribute Цикл
			Строка = СтрокаМодификации.СвойстваМодификации.ПолучитьЭлементы().Добавить();
			Строка.Заголовок = attribute.name;
			
			Если СтрДлина(attribute.value) >= 50 ИЛИ НРег(attribute.name) = "options" Тогда
				МассивСвойств = РазобратьСтрокуСвойств(attribute.value);
				Если МассивСвойств.Количество()>1 Тогда
					Для Каждого Свойство2 Из МассивСвойств Цикл
						Строка2 = Строка.ПолучитьЭлементы().Добавить();
						Строка2.ЗначениеСвойства = Свойство2;
					КонецЦикла;
				Иначе
					Строка.ЗначениеСвойства = МассивСвойств[0];
				КонецЕсли;
			Иначе
				Строка.ЗначениеСвойства = attribute.value;
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(СтрокаМодификации, СтрокаДанных);
		
		СтрокаМодификации.Каталог = Каталог.Name+" ("+Каталог.Version+") - wizard";
		
		СтруктураДатВыпуска = НайтиГодыВыпускаВДанных(СтрокаДанных.attribute);
		СтрокаМодификации.ГодВыпуска = СтруктураДатВыпуска.ГодВыпуска;
		СтрокаМодификации.МесяцВыпуска = СтруктураДатВыпуска.МесяцВыпуска;
		
		ПреобразоватьАттрибутыВРеквизиты(СтрокаДанных); // Каждый аттрибут стал Структурой значений
		
		Модель = "";
		Если СтрокаДанных.Свойство("Model") Тогда
			Если ТипЗнч(СтрокаДанных.Model) = Тип("Структура") Тогда
				Модель = СтрокаДанных.Model.value;
			Иначе
				Модель = СтрокаДанных.Model;
			КонецЕсли;
		КонецЕсли;
		
		Модификация = "";
		Если СтрокаДанных.Свойство("Modification") Тогда
			Если ТипЗнч(СтрокаДанных.Modification) = Тип("Структура") Тогда
				Модификация = СтрокаДанных.Modification.value;
			Иначе
				Модификация = СтрокаДанных.Modification;
			КонецЕсли;
		КонецЕсли;
		
		МодельАвтомобиля = СокрЛП(Модель+" "+Модификация);
		
		СтрокаМодификации.Код = Число(СтрокаДанных.VehicleId);
		СтрокаМодификации.Наименование = СокрЛП(СтрокаДанных.Name)+?(ПустаяСтрока(МодельАвтомобиля), "", " ("+СокрЛП(МодельАвтомобиля)+")");
		
		ПараметрыМодификации = Новый Структура;
		ПараметрыМодификации.Вставить("Name", СтрокаМодификации.Наименование);
		ПараметрыМодификации.Вставить("Wizard", Истина);
		ПараметрыМодификации.Вставить("catalog", СтрокаДанных.Catalog);
		ПараметрыМодификации.Вставить("vehicleid", Число(СтрокаДанных.VehicleId));
		ПараметрыМодификации.Вставить("ssd", СтрокаДанных.ssd);
		
		СтрокаМодификации.ПараметрыМодификации = ПараметрыМодификации;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПреобразоватьАттрибутыВРеквизиты(СтрокаДанных)
	Если НЕ ЗначениеЗаполнено(СтрокаДанных) Тогда
		Возврат;
	КонецЕсли;
	
	attribute = Новый Массив;
	Если СтрокаДанных.Свойство("attribute", attribute) = Истина Тогда
		Если ТипЗнч(attribute) = Тип("Массив") Тогда
			Для Каждого СтрокаМассива  Из attribute Цикл
				Попытка
					СтрокаДанных.Вставить(СтрокаМассива.key, Новый Структура("name, value", СтрокаМассива.name, СтрокаМассива.value)); 
				Исключение
					Попытка
						СтрокаДанных.Вставить("err_"+СтрокаМассива.key, Новый Структура("name, value", СтрокаМассива.name, СтрокаМассива.value)); 
					Исключение
					КонецПопытки;
				КонецПопытки;
			КонецЦикла;
		ИначеЕсли ТипЗнч(attribute) = Тип("Структура") Тогда
			Попытка
				СтрокаДанных.Вставить(attribute.key, Новый Структура("name, value", attribute.name, attribute.value)); 
			Исключение
				Попытка
					СтрокаДанных.Вставить("err_"+attribute.key, Новый Структура("name, value", attribute.name, attribute.value)); 
				Исключение
				КонецПопытки;
			КонецПопытки;
		КонецЕсли;
		СтрокаДанных.Удалить("attribute");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция GetWizard2(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "GetWizard2", ТелоЗапроса,,, Ошибка);
КонецФункции


&НаКлиенте
Процедура ЗаполнитьТаблицуWizard()
	
	Модификации.Очистить();
	ТаблицаWizard.Очистить();
	Элементы.ПоказатьМодификацииWizard.Доступность = Ложь;
	
	Если Не ЗначениеЗаполнено(Wizard.Catalog) Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = "";
	Данные = Новый Структура("Catalog, ssd", Wizard.Catalog, Wizard.Ssd);
	СтруктураРезультата = GetWizard2(Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
		Сообщить(Ошибка);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
		СтрокиДанных = СтруктураРезультата.row;
	Иначе
		СтрокиДанных = Новый Массив;
		СтрокиДанных.Добавить(СтруктураРезультата.row);
	КонецЕсли;
	
	Для Каждого СтрокаДанных ИЗ СтрокиДанных Цикл
		СтрокаПараметров = ТаблицаWizard.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПараметров, СтрокаДанных);
		
		Если СтрокаДанных.Свойство("options") И СтрокаДанных.options.Свойство("row") Тогда
			Если ТипЗнч(СтрокаДанных.options.row) = Тип("Массив") Тогда
				Для Каждого option Из СтрокаДанных.options.row Цикл
					СтрокаПараметров.Options.Добавить(option.key, Строка(option.value));
				КонецЦикла;
			Иначе
				СтрокаПараметров.Options.Добавить(СтрокаДанных.options.row.key, Строка(СтрокаДанных.options.row.value));
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаWizard.Количество() > 0 Тогда
		Элементы.ПоказатьМодификацииWizard.Доступность = ТаблицаWizard[0].AllowListVehicles;
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция GetWizardNextStep2(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "GetWizardNextStep2", ТелоЗапроса,,, Ошибка);
КонецФункции 

&НаКлиенте
Процедура ЗаполнитьТаблицуWizardNextStep()
	
	Модификации.Очистить();
	ТаблицаWizard.Очистить();
	Элементы.ПоказатьМодификацииWizard.Доступность = Ложь;
	
	СписокВыбора = Элементы.ТаблицаWizardValue.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(Wizard.Catalog) Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = "";
	Данные = Новый Структура("Catalog, ssd", Wizard.Catalog, Wizard.Ssd);
	СтруктураРезультата = GetWizardNextStep2(Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("currentStep")  ИЛИ НЕ СтруктураРезультата.Свойство("previousStep") Тогда
		Предупреждение(Ошибка);
		Возврат;
	КонецЕсли;
	
	//previousStep
	Если СтруктураРезультата.previousStep.Свойство("row") Тогда
		Если ТипЗнч(СтруктураРезультата.previousStep.row) = Тип("Массив") Тогда
			СтрокиДанных = СтруктураРезультата.previousStep.row;
		Иначе
			СтрокиДанных = Новый Массив;
			СтрокиДанных.Добавить(СтруктураРезультата.previousStep.row);
		КонецЕсли;
		Для Каждого СтрокаДанных ИЗ СтрокиДанных Цикл
			СтрокаПараметров = ТаблицаWizard.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПараметров, СтрокаДанных);
		КонецЦикла;
	КонецЕсли;
	
	//currentStep
	currentStep = СтруктураРезультата.currentStep;
	
	Если currentStep.Свойство("AllowListVehicles") Тогда
		Элементы.ПоказатьМодификацииWizard.Доступность = Истина;
		ПоискПоWizard();
		Возврат;
	КонецЕсли;
	
	СтрокаПараметров = ТаблицаWizard.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПараметров, currentStep);
	
	Если ТипЗнч(currentStep.options.row) = Тип("Массив") Тогда
		Для Каждого option Из currentStep.options.row Цикл
			СтрокаПараметров.Options.Добавить(option.key, Строка(option.value));
		КонецЦикла;
	Иначе
		СтрокаПараметров.Options.Добавить(currentStep.options.row.key, Строка(currentStep.options.row.value));
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из СтрокаПараметров.Options Цикл
		СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьWizard(Команда)
	
	Для Каждого СтрокаКаталог Из КаталогиWizard Цикл
		СтрокаКаталог.Выбран = Ложь;
	КонецЦикла;
	
	Wizard = Новый Структура;
	Wizard.Вставить("Catalog", "");
	Wizard.Вставить("Ssd", "");
	
	ТекДанные = Элементы.КаталогиWizard.ТекущиеДанные;
	Если ТекДанные <> Неопределено И ЗначениеЗаполнено(ТекДанные.Code) Тогда
		Wizard.Catalog = ТекДанные.Code;
		ТекДанные.Выбран = Истина;
	КонецЕсли;
	
	Если ПорядокВыбораWizard = "Последовательно" Тогда
		ЗаполнитьТаблицуWizardNextStep();
	Иначе
		ЗаполнитьТаблицуWizard();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоследовательныйВыборПриИзменении(Элемент)
	ИнициализироватьWizard(Неопределено);
КонецПроцедуры


&НаКлиенте
Процедура КаталогиПараметрыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	// Нужно инициализировать таблицу подбора
	СтандартнаяОбработка = Ложь;
	ИнициализироватьWizard(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаWizardПередНачаломИзменения(Элемент, Отказ)
	
	ТекСтрока = Элементы.ТаблицаWizard.ТекущиеДанные;
	
	Если ТекСтрока.Automatic Тогда
		Отказ = Истина;
		Возврат;
	Иначе
		СписокOptions = Элементы.ТаблицаWizardValue.СписокВыбора;
		СписокOptions.Очистить();
		Для Каждого Option Из ТекСтрока.Options Цикл
			СписокOptions.Добавить(Option.Значение, Option.Представление, Option.Представление=ТекСтрока.Value);
		КонецЦикла;
		
		Элементы.ТаблицаWizardValue.КнопкаВыпадающегоСписка = ТекСтрока.Options.Количество() > 0;
		Элементы.ТаблицаWizardValue.РедактированиеТекста = ТекСтрока.Options.Количество() > 0;
		Элементы.ТаблицаWizardValue.КнопкаОчистки = ЗначениеЗаполнено(ТекСтрока.Value);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаWizardПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования ИЛИ Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если ТекСтрока.Value <> "" И ТекСтрока.Options.Количество() > 0 Тогда
		
		Для Каждого ЭлементСписка Из ТекСтрока.Options Цикл
			Если ТекСтрока.Value = ЭлементСписка.Представление Тогда
				ВыбранноеЗначение = ЭлементСписка.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ВыбранноеЗначение = Неопределено Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ТекСтрока.Ssd = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаWizardПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования ИЛИ Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если (ТекСтрока.Value = "" И ЗначениеЗаполнено(ТекСтрока.ssd))
		ИЛИ (ТекСтрока.Options.Количество() > 0 И ЗначениеЗаполнено(ТекСтрока.ssd)) Тогда
		// Очистили параметр
		//или Произвели выбор из списка параметров
		
		Wizard.Ssd = ТекСтрока.ssd;
		Если ПорядокВыбораWizard = "Последовательно" Тогда
			ЗаполнитьТаблицуWizardNextStep();
		Иначе
			ЗаполнитьТаблицуWizard();
		КонецЕсли;
		
	Иначе
		// Не понятно что сделали ))
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаWizardValueОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.ТаблицаWizard.ТекущиеДанные;
	ЭлементСписка = ТекСтрока.Options.НайтиПоЗначению(ВыбранноеЗначение);
	
	Если ЭлементСписка = Неопределено Тогда
		Элементы.ТаблицаWizard.ЗакончитьРедактированиеСтроки(Истина);
	Иначе
		ТекСтрока.Value = ЭлементСписка.Представление;
		ТекСтрока.Ssd = ЭлементСписка.Значение;
		Элементы.ТаблицаWizard.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаWizardValueОчистка(Элемент, СтандартнаяОбработка)
	
	Элементы.ТаблицаWizard.ТекущиеДанные.Value = "";
	Элементы.ТаблицаWizard.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзмененииНаКлиенте()
	
	ПроверитьНомер();
	
	Если Элементы.Номер.СписокВыбора.НайтиПоЗначению(Номер) = Неопределено Тогда
		Элементы.Номер.СписокВыбора.Добавить(ПараметрыНомера.Номер, ПараметрыНомера.Номер + " введено вручную");
	КонецЕсли;
	
	Элементы.Номер.КнопкаВыпадающегоСписка = Элементы.Номер.СписокВыбора.Количество() > 1;
	
	ПоискМодификаций();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	НомерПриИзмененииНаКлиенте();
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиАвтомобильПоНомеру(Номер)
	Возврат Справочники.Автомобили.НайтиПоРеквизиту("VIN", Номер);
КонецФункции

&НаКлиенте
Процедура НомерИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	Если Элементы.Номер.ЦветРамки = ЦветаСтиля.ТекстПредупреждающейНадписи Тогда
		Элементы.Номер.ЦветРамки = ЦветаСтиля.ЦветРамки;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
КонецПроцедуры


&НаСервереБезКонтекста
Функция SaveInHistory(Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "SaveInHistory", ТелоЗапроса,, Ложь, Ошибка);
КонецФункции


&НаКлиенте
Функция SaveInHistoryНаСервере(ПараметрыЗапроса)
	
	Данные = Новый Структура;
	Если ТипЗнч(РабочийЛистОбъект) = Тип("СправочникСсылка.Автомобили") Тогда
		Данные.Вставить("КодИБ", РабочийЛистОбъект.Код);
		Данные.Вставить("Наименование", РабочийЛистОбъект.Наименование);
		Данные.Вставить("Марка", РабочийЛистОбъект.Модель.Производитель.Наименование);
	ИначеЕсли ТипЗнч(РабочийЛистОбъект) = Тип("СправочникСсылка.Модели") Тогда
		Данные.Вставить("ВидНомера", "Модель");
		Данные.Вставить("Номер", ""+РабочийЛистОбъект);
		Данные.Вставить("Марка", РабочийЛистОбъект.Производитель.Наименование);
	Иначе
		Данные.Вставить("ВидНомера", "Номер");
		Данные.Вставить("Номер", РабочийЛистОбъект);
	КонецЕсли;
	
	Данные.Вставить("Параметры", Новый Структура);
	Для Каждого КлючЗначение ИЗ ПараметрыЗапроса Цикл
		Данные.Параметры.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	Данные.Параметры.Вставить("Filter", Неопределено); // Если в сервисе сохранен Filter, то он не будет перезаписан!
	
	Ошибка = "";
	Результат = SaveInHistory(Данные, Ошибка);
	
	Если Результат = Неопределено Тогда
		Предупреждение(Ошибка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиАРМКорзина(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Клиент", Неопределено);
	ПараметрыОткрытия.Вставить("Автомобиль", РабочийЛистОбъект);
	
	ФормаФункциональнойПанели = Обработки.ФункциональнаяПанель.ПолучитьФорму();
	Если ФормаФункциональнойПанели.Открыта() Тогда
		ФормаАРМ = Обработки.АРМКорзина.ПолучитьФорму(, ФормаФункциональнойПанели);
		Если ФормаАРМ.Открыта() Тогда
			Оповестить("АктивизироватьАРМКорзина", ПараметрыОткрытия);
		Иначе
			ФормаФункциональнойПанели.ОткрытьАРМ("АРМКорзина", ФормаФункциональнойПанели, ПараметрыОткрытия, ЭтаФорма);
		КонецЕсли;
	Иначе
		ФормаФункциональнойПанели.ЗапускатьАРМПоУмолчанию = Ложь;
		ФормаФункциональнойПанели.Открыть();
		ФормаФункциональнойПанели.ОткрытьАРМ("АРМКорзина", ФормаФункциональнойПанели, ПараметрыОткрытия, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РабочийЛистОбъектПриИзмененииНаКлиенте()
	
	Номер = "";
	Элементы.Номер.СписокВыбора.Очистить();
	//Элементы.Номер.КнопкаСпискаВыбора = Ложь;
	//Элементы.Номер.РежимВыбораИзСписка = Ложь;
	Элементы.Номер.КнопкаВыпадающегоСписка = Ложь;
	
	Если ТипЗнч(РабочийЛистОбъект) = Тип("СправочникСсылка.Автомобили") Тогда
		Если ЗначениеЗаполнено(РабочийЛистОбъект.VIN) Тогда
			Если Не ЗначениеЗаполнено(Номер) Тогда
				Номер = РабочийЛистОбъект.VIN;
			КонецЕсли;
			Элементы.Номер.СписокВыбора.Добавить(РабочийЛистОбъект.VIN, РабочийЛистОбъект.VIN + " VIN");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РабочийЛистОбъект.ОригинальныйVIN) Тогда
			Если Не ЗначениеЗаполнено(Номер) Тогда
				Номер = РабочийЛистОбъект.ОригинальныйVIN;
			КонецЕсли;
			Элементы.Номер.СписокВыбора.Добавить(РабочийЛистОбъект.ОригинальныйVIN, РабочийЛистОбъект.ОригинальныйVIN + " VIN (ориг)");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РабочийЛистОбъект.НомерКузова) Тогда
			Если Не ЗначениеЗаполнено(Номер) Тогда
				Номер = РабочийЛистОбъект.НомерКузова;
			КонецЕсли;
			Элементы.Номер.СписокВыбора.Добавить(РабочийЛистОбъект.НомерКузова, РабочийЛистОбъект.НомерКузова + " Номер кузова");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РабочийЛистОбъект.НомерШасси) Тогда
			Если Не ЗначениеЗаполнено(Номер) Тогда
				Номер = РабочийЛистОбъект.НомерШасси;
			КонецЕсли;
			Элементы.Номер.СписокВыбора.Добавить(РабочийЛистОбъект.НомерШасси, РабочийЛистОбъект.НомерШасси + " Номер шасси");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РабочийЛистОбъект.НомерДвигателя) Тогда
			Если Не ЗначениеЗаполнено(Номер) Тогда
				Номер = РабочийЛистОбъект.НомерДвигателя;
			КонецЕсли;
			Элементы.Номер.СписокВыбора.Добавить(РабочийЛистОбъект.НомерДвигателя, РабочийЛистОбъект.НомерДвигателя + " Номер двигателя");
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(РабочийЛистОбъект) = Тип("Строка") Тогда
		Номер = РабочийЛистОбъект;
		Элементы.Номер.СписокВыбора.Добавить(РабочийЛистОбъект, РабочийЛистОбъект + " введен вручную");
	КонецЕсли;
	
	//Элементы.Номер.КнопкаСпискаВыбора = Элементы.Номер.СписокВыбора.Количество() > 0;
	//Элементы.Номер.РежимВыбораИзСписка = Элементы.Номер.СписокВыбора.Количество() > 0;
	Элементы.Номер.КнопкаВыпадающегоСписка = Элементы.Номер.СписокВыбора.Количество() > 1;
	
	ПроверитьНомер();
	
	ПоискМодификаций();
	
КонецПроцедуры

&НаКлиенте
Процедура РабочийЛистОбъектПриИзменении(Элемент)
	РабочийЛистОбъектПриИзмененииНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура НомерОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Номер = ВыбранноеЗначение;
	ПроверитьНомер();
	ПоискМодификаций();
КонецПроцедуры




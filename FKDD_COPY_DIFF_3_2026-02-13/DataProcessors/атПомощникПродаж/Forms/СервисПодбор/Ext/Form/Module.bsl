&НаКлиенте
Перем СтарыйАвтомобиль;

#Область КомандыФормы

&НаСервере
Процедура ДобавитьКомандуНаФорму(ИмяПодменю, ИдентификаторЗапроса, ИдентификаторНабора=Неопределено, ЗаголовокКнопки="", ПометкаКнопки=Ложь)
	
	ИмяКнопки = ИмяПодменю + ИдентификаторЗапроса + ?(ИдентификаторНабора=Неопределено, "", "_"+ИдентификаторНабора);
	
	НоваяКоманда = Команды.Найти(ИмяКнопки);
	Если НоваяКоманда = Неопределено Тогда
		НоваяКоманда = Команды.Добавить(ИмяКнопки);
	КонецЕсли;
	НоваяКоманда.Заголовок = ЗаголовокКнопки;
	НоваяКоманда.Действие = "Выбрать" + ИмяПодменю;
	
	НоваяКнопка = Элементы.Найти(ИмяКнопки);
	Если НоваяКнопка = Неопределено Тогда
		НоваяКнопка = Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), Элементы[ИмяПодменю]);
	КонецЕсли;
	НоваяКнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	НоваяКнопка.Заголовок = ЗаголовокКнопки;
	НоваяКнопка.ИмяКоманды = НоваяКоманда.Имя;
	НоваяКнопка.Пометка = ПометкаКнопки;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКомандуНаФорме(ИмяПодменю, ИдентификаторЗапроса, ИдентификаторНабора=Неопределено)
	
	ИмяКоманды = ИмяПодменю + ИдентификаторЗапроса + ?(ИдентификаторНабора=Неопределено, "", "_"+ИдентификаторНабора);
	
	Команда = Команды.Найти(ИмяКоманды);
	Если Команда <> Неопределено Тогда
		Команды.Удалить(Команда);
	КонецЕсли;
	
	Кнопка = Элементы.Найти(ИмяКоманды);
	Если Кнопка <> Неопределено Тогда
		Элементы.Удалить(Кнопка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПодменю(ИмяПодменю)
	
	МассивКоманд = Новый Массив;
	
	Для Каждого КомандаНабора Из Команды Цикл
		Если Найти(КомандаНабора.Имя, ИмяПодменю) > 0 Тогда
			МассивКоманд.Добавить(КомандаНабора.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИмяКоманды Из МассивКоманд Цикл
		КомандаНабора = Команды.Найти(ИмяКоманды);
		Команды.Удалить(КомандаНабора);
		
		Кнопка = Элементы.Найти(ИмяКоманды);
		Если Кнопка <> Неопределено Тогда
			Элементы.Удалить(Кнопка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СоставПодменю(ИмяПодменю)
	
	МассивИдентификаторов = Новый Массив;
	
	Для Каждого КомандаНабора Из Команды Цикл
		Если Найти(КомандаНабора.Имя, ИмяПодменю) > 0 Тогда
			ИдентификаторЗапроса_Идентификатор = СтрЗаменить(КомандаНабора.Имя, ИмяПодменю, "");
			Поз_ = Найти(ИдентификаторЗапроса_Идентификатор, "_");
			Если Поз_ > 0 Тогда
				СтруктураИдентификаторы = Новый Структура("ИдентификаторЗапроса, Идентификатор", Лев(ИдентификаторЗапроса_Идентификатор, Поз_-1), Сред(ИдентификаторЗапроса_Идентификатор, Поз_+1));
			Иначе
				СтруктураИдентификаторы = Новый Структура("ИдентификаторЗапроса", ИдентификаторЗапроса_Идентификатор);
			КонецЕсли;
			МассивИдентификаторов.Добавить(СтруктураИдентификаторы);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивИдентификаторов;
	
КонецФункции

&НаКлиенте
Процедура ИнициализироватьКаталог(Команда)
	
	ОчиститьПодменю("КаталогиАвтомобиляПодменю");
	ОчиститьПодменю("НаборыСвойствПодменю");
	
	Производитель = "";
	Элементы.Производитель.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(Каталог) Тогда
		Каталог = Элементы.Каталог.СписокВыбора[0].Значение;
	КонецЕсли;
	
	КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
	СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование", КаталогЗначение.Значение, КаталогЗначение.Представление));
	
	Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
		ДобавитьКомандуНаФорму("КаталогиАвтомобиляПодменю", СтрокаКаталога.ИдентификаторЗапроса,, СтрокаКаталога.МаркаНаименование + ": " + СтрокаКаталога.МодельНаименование + ?(ЗначениеЗаполнено(СтрокаКаталога.МодификацияНаименование), "; "+СтрокаКаталога.МодификацияНаименование, ""));
	КонецЦикла;
	
	Если СтрокиКаталога.Количество() = 1 Тогда
		ЗаполнитьМаркиКаталога(СтрокиКаталога[0].Марка, СтрокиКаталога[0].Модель, СтрокиКаталога[0].Модификация);
	Иначе
		ЗаполнитьМаркиКаталога();
	КонецЕсли;
	
	Элементы.СохранитьНаборСвойств.Доступность = Ложь;
	Элементы.УдалитьНаборСвойств.Видимость = СоставПодменю("НаборыСвойствПодменю").Количество() > 0;
	
	РазвернутьВсеДерево(СвойстваКаталогов.ПолучитьЭлементы(), "СвойстваКаталогов");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьСвойства(Команда)
	
	ОчиститьПодменю("КаталогиАвтомобиляПодменю");
	ОчиститьПодменю("НаборыСвойствПодменю");
	
	Производитель = "";
	Элементы.Производитель.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(ТипНоменклатуры) Тогда
		ТипНоменклатуры = Элементы.ТипНоменклатуры.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипНоменклатуры) Тогда
		ТипНоменклатурыЗначение = Элементы.ТипНоменклатуры.СписокВыбора.НайтиПоЗначению(ТипНоменклатуры);
		СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование", ТипНоменклатурыЗначение.Значение, ТипНоменклатурыЗначение.Представление));
		
		Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
			Для Каждого СтрокаНабора Из СтрокаКаталога.Параметры Цикл
				ДобавитьКомандуНаФорму("НаборыСвойствПодменю", СтрокаКаталога.ИдентификаторЗапроса, СтрокаНабора.Идентификатор, СтрокаНабора.Наименование);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Элементы.СохранитьНаборСвойств.Доступность = Ложь;
	Элементы.УдалитьНаборСвойств.Видимость = СоставПодменю("НаборыСвойствПодменю").Количество() > 0;
	
	ЗаполнитьДеревоСвойств();
	//РазвернутьВсеДерево(ДеревоСвойств.ПолучитьЭлементы(), "ДеревоСвойств");
	
КонецПроцедуры //СброситьОтборСвойств()

&НаКлиенте
Процедура ПоказатьНоменклатуру(Команда)
	
	ЗаполнитьНоменклатуру();
	
КонецПроцедуры //ПоказатьНоменклатуру()

&НаКлиенте
Процедура СохранитьВыборДляАвтомобиля(Команда)
	
	Если Не ЗначениеЗаполнено(РабочийЛистОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПредопределенныхНаборов = ЕстьВеткаДерева("НаборСвойств", "СвойстваКаталогов", "ИдентификаторГруппы");
	
	Если СтрокаПредопределенныхНаборов = Неопределено ИЛИ СтрокаПредопределенныхНаборов.ПолучитьЭлементы().Количество() = 0 Тогда
		Предупреждение("Для выбранной модификации нет наборов свойств!");
		Возврат;
	КонецЕсли;
	
	НоваяСтрокаКаталога = Новый Структура("Каталог, КаталогНаименование, Марка, МаркаНаименование, Модель, МодельНаименование, Модификация, МодификацияНаименование");
	
	Если ВидПоиска = "ПоМоделиАвтомобиля" Тогда
		
		Для Каждого СтрокаДерева Из СвойстваКаталогов.ПолучитьЭлементы() Цикл
			Если ПустаяСтрока(СтрокаДерева.ЗначениеКод) Тогда
				Продолжить;
			КонецЕсли;
			Попытка
				НоваяСтрокаКаталога[СтрокаДерева.ИдентификаторГруппы] = СтрокаДерева.ЗначениеКод;
				НоваяСтрокаКаталога[СтрокаДерева.ИдентификаторГруппы+"Наименование"] = СтрокаДерева.Значение;
			Исключение КонецПопытки;
		КонецЦикла;
		
		КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
		НоваяСтрокаКаталога.Каталог = КаталогЗначение.Значение;
		НоваяСтрокаКаталога.КаталогНаименование = КаталогЗначение.Представление;
		
		СтруктураПоиска = Новый Структура("Каталог, КаталогНаименование, Марка, Модель, Модификация");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, НоваяСтрокаКаталога);
		
	Иначе
		
		ТипНоменклатурыЗначение = Элементы.ТипНоменклатуры.СписокВыбора.НайтиПоЗначению(ТипНоменклатуры);
		НоваяСтрокаКаталога.Каталог = ТипНоменклатурыЗначение.Значение;
		НоваяСтрокаКаталога.КаталогНаименование = ТипНоменклатурыЗначение.Представление;
		
		СтруктураПоиска = Новый Структура("Каталог, КаталогНаименование");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, НоваяСтрокаКаталога);
		
	КонецЕсли;
	
	СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(СтруктураПоиска);
	Если СтрокиКаталога.Количество() = 0 Тогда
		СтрокаКаталога = КаталогиАвтомобиля.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКаталога, НоваяСтрокаКаталога);
	Иначе
		СтрокаКаталога = СтрокиКаталога[0];
	КонецЕсли;
	
	ОбновитьЗапрос(СтрокаКаталога, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаборСвойств(Команда)
	ДобавитьНаборСвойствНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНаборСвойств(Команда)
	УдалитьНаборСвойствНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиАРМКорзина(Команда)
	
	
	ПараметрыОткрытия = Новый Структура;
	Если РабочийЛистИсточник = "Автомобиль" Тогда
		ПараметрыОткрытия.Вставить("Клиент", Неопределено);
		ПараметрыОткрытия.Вставить("Автомобиль", РабочийЛистОбъект);
	Иначе
		ПараметрыОткрытия.Вставить("Клиент", РабочийЛистОбъект);
		ПараметрыОткрытия.Вставить("Автомобиль", Неопределено);
	КонецЕсли;
	
	ФормаФункциональнойПанели = Обработки.ФункциональнаяПанель.ПолучитьФорму();
	Если ФормаФункциональнойПанели.Открыта() Тогда
		ФормаАРМ = Обработки.АРМКорзина.ПолучитьФорму(, ФормаФункциональнойПанели);
		Если ФормаАРМ.Открыта() Тогда
			Оповестить("АктивизироватьАРМКорзина", ПараметрыОткрытия);
		Иначе
			ФормаФункциональнойПанели.ОткрытьАРМ("АРМКорзина", ФормаФункциональнойПанели, ПараметрыОткрытия, ЭтаФорма);
		КонецЕсли;
	Иначе
		ФормаФункциональнойПанели.ЗапускатьАРМПоУмолчанию = Ложь;
		ФормаФункциональнойПанели.Открыть();
		ФормаФункциональнойПанели.ОткрытьАРМ("АРМКорзина", ФормаФункциональнойПанели, ПараметрыОткрытия, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьНаборыСвойствПодменю(Команда)
	
	ИдентификаторЗапроса_Идентификатор = СтрЗаменить(Команда.Имя, "НаборыСвойствПодменю", "");
	
	Поз_ = Найти(ИдентификаторЗапроса_Идентификатор, "_");
	ИдентификаторЗапроса = Лев(ИдентификаторЗапроса_Идентификатор, Поз_-1);
	Идентификатор = Сред(ИдентификаторЗапроса_Идентификатор, Поз_+1);
	
	СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", ИдентификаторЗапроса));
	Если СтрокиКаталога.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиНабора = СтрокиКаталога[0].Параметры.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
	Если СтрокиНабора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборСвойств = СтрокиНабора[0];
	
	ВыбранныйНаборСвойств.Очистить();
	ВыбранныеСвойства = Новый Массив;
	
	Для Каждого СтрокаСостава Из НаборСвойств.СоставНабора Цикл
		Если СтрокаСостава.Предопределенное Тогда
			ВыбранныйНаборСвойств.Добавить(СтрокаСостава.ВидСвойства);
		КонецЕсли;
		ВыбранныеСвойства.Добавить(Новый Структура("ВидСвойства, ТипЗначения, Значение", СтрокаСостава.ВидСвойства, СтрокаСостава.ТипЗначения, СтрокаСостава.Значение));
	КонецЦикла;
	
	ТипНоменклатуры = НаборСвойств.ТипНоменклатуры;
	
	ЗаполнитьДеревоСвойств(ВыбранныеСвойства);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКаталогиАвтомобиляПодменю(Команда)
	
	ОчиститьПодменю("НаборыСвойствПодменю");
	
	СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", СтрЗаменить(Команда.Имя, "КаталогиАвтомобиляПодменю", "")));
	
	Если СтрокиКаталога.Количество() > 0 Тогда
		ЗаполнитьМаркиКаталога(СтрокиКаталога[0].Марка, СтрокиКаталога[0].Модель, СтрокиКаталога[0].Модификация);
	Иначе
		ЗаполнитьМаркиКаталога();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Разворачивает любое дерево
//
// Параметры
//  КоллекцияЭлементов	– КоллекцияЭлементов	– Коллекция разворачиваемых элементов
//  ИмяЭлементаФормы	– Строка	– Имя разворачиваемого дерева формы
//
&НаКлиенте
Процедура РазвернутьВсеДерево(КоллекцияЭлементов, ИмяЭлементаФормы)
	
	Для каждого ЭлементКоллекции Из КоллекцияЭлементов Цикл
		
		Если НЕ ЗначениеЗаполнено(ЭлементКоллекции.Значение) Тогда
		
			Элементы[ИмяЭлементаФормы].Развернуть(ЭлементКоллекции.ПолучитьИдентификатор());
			ВложенныеЭлементыКоллекции = ЭлементКоллекции.ПолучитьЭлементы();
			
			Если (ВложенныеЭлементыКоллекции.Количество() > 0) Тогда
				РазвернутьВсеДерево(ВложенныеЭлементыКоллекции, ИмяЭлементаФормы);
			Иначе
				Прервать;
			КонецЕсли;
			
		Иначе
			
			Элементы[ИмяЭлементаФормы].Свернуть(ЭлементКоллекции.ПолучитьИдентификатор());
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры //РазвернутьВсеДерево()

&НаСервереБезКонтекста
Функция КоллекцииИдентичны(Знач КоллекцияСтрок1, Знач КоллекцияСтрок2, Знач ИменаКолонок = "", Знач ИсключаяКолонки = "", Знач УчитыватьПоследовательностьСтрок = Ложь)
	Возврат атСервер.КоллекцииИдентичны(КоллекцияСтрок1, КоллекцияСтрок2, ИменаКолонок, ИсключаяКолонки, УчитыватьПоследовательностьСтрок);
КонецФункции

&НаСервереБезКонтекста
Функция ДоступныеКаталогиНаСервере()
	Возврат атСервер.ВыполнитьЗапросSelection("ДоступныеКаталоги");
КонецФункции

&НаСервереБезКонтекста
Функция ДоступныеТипыНоменклатурыНаСервере()
	Возврат атСервер.ВыполнитьЗапросSelection("ДоступныеТипыНоменклатуры");
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВидыСвойствНаСервере(Знач Отбор, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("ПолучитьВидыСвойств", Отбор,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция МаркиАвтомобилейНаСервере(Знач Отбор, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("МаркиАвтомобилей", Отбор,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция МоделиАвтомобилейНаСервере(Знач Отбор, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("МоделиАвтомобилей", Отбор,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция МодификацииАвтомобилейНаСервере(Знач Отбор, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("МодификацииАвтомобилей", Отбор,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция НаборыСвойствНаСервере(Знач Отбор, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("НаборыСвойств", Отбор,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруНаСервере(Знач Отбор, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("ПолучитьНоменклатуру", Отбор,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция РабочийЛистПользователяНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "РабочийЛистПользователя", ТелоЗапроса,, Ложь, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция SaveInHistoryНаСервере(Знач Данные, Ошибка)
	Возврат атСервер.ВыполнитьЗапросSelection("SaveInHistory",, Данные, Ложь, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ДобавитьЗапросНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "ДобавитьЗапрос", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ДобавитьНаборСвойствНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "ДобавитьНаборСвойств", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция УдалитьНаборСвойствНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "УдалитьНаборСвойств", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ДобавитьНомерДеталиНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "ДобавитьНомерДетали", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ИзменитьНомерДеталиНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "ИзменитьНомерДетали", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция УдалитьНомерДеталиНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "УдалитьНомерДетали", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция НайтиАвтомобильПоНомеру(Номер)
	Возврат Справочники.Автомобили.НайтиПоРеквизиту("VIN", Номер);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСопоставленнуюСсылкуНаСервере(Знач Объект, Знач ВозвращатьНаименование=Неопределено, ЗаписыватьОбъект=Ложь)
	Возврат атСервер.ПолучитьСопоставленнуюСсылку(Объект, ВозвращатьНаименование, ЗаписыватьОбъект);
КонецФункции

&НаСервереБезКонтекста
Функция НайтиНоменклатуруНаСервере(АртикулДляПоиска, Производитель)
	Возврат Справочники.Номенклатура.НайтиНоменклатуру(АртикулДляПоиска, Производитель);
КонецФункции

&НаСервереБезКонтекста
Функция ХешированиеДанныхНаСервере(Данные)
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Для Каждого Значение Из Данные Цикл
		Хеш.Добавить(Значение);
	КонецЦикла;
	Возврат СтрЗаменить(Хеш.ХешСумма, " ", "");
КонецФункции

&НаКлиенте
Функция ПолучитьВеткуДерева(ЗначениеРеквизита, ИмяЭлементаФормы = "СвойстваКаталогов", ИмяРеквизитаЭлемента = "ИдентификаторГруппы")
	
	Для Каждого СтрокаДерева Из ЭтаФорма[ИмяЭлементаФормы].ПолучитьЭлементы() Цикл
		Если СтрокаДерева[ИмяРеквизитаЭлемента] = ЗначениеРеквизита Тогда
			Возврат СтрокаДерева;
		КонецЕсли;
	КонецЦикла;
	
	СтрокаДерева = ЭтаФорма[ИмяЭлементаФормы].ПолучитьЭлементы().Добавить();
	СтрокаДерева[ИмяРеквизитаЭлемента] = ЗначениеРеквизита;
	
	Возврат СтрокаДерева;
	
КонецФункции

&НаКлиенте
Функция ЕстьВеткаДерева(ЗначениеРеквизита, ИмяЭлементаФормы = "СвойстваКаталогов", ИмяРеквизитаЭлемента = "ИдентификаторГруппы")
	
	Для Каждого СтрокаДерева Из ЭтаФорма[ИмяЭлементаФормы].ПолучитьЭлементы() Цикл
		Если СтрокаДерева[ИмяРеквизитаЭлемента] = ЗначениеРеквизита Тогда
			Возврат СтрокаДерева;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ атКлиент.АвторизацияПройдена() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ВидПоиска) Тогда
		ВидПоиска = "ПоМоделиАвтомобиля";
	КонецЕсли;
	
	// Стартовая инициализация без автомобиля
	
	Каталог = "";
	ТипНоменклатуры = "";
	
	Элементы.Каталог.СписокВыбора.Очистить();
	СвойстваКаталогов.ПолучитьЭлементы().Очистить();
	
	Элементы.ТипНоменклатуры.СписокВыбора.Очистить();
	ДеревоСвойств.ПолучитьЭлементы().Очистить();
	
	Элементы.СохранитьНаборСвойств.Видимость = Ложь;
	Элементы.СохранитьНаборСвойств.Доступность = Ложь;
	Элементы.УдалитьНаборСвойств.Видимость = Ложь;
	
	//Заполним каталоги
	РезультатЗапроса = ДоступныеКаталогиНаСервере();
	Если РезультатЗапроса <> Неопределено Тогда
		Для Каждого Структура ИЗ РезультатЗапроса Цикл
			Элементы.Каталог.СписокВыбора.Добавить(Структура.Код, Структура.Наименование);
		КонецЦикла;
	КонецЕсли;
	
	//Заполним типы номенклатуры
	РезультатЗапроса = ДоступныеТипыНоменклатурыНаСервере();
	Если РезультатЗапроса <> Неопределено Тогда
		Для Каждого Структура ИЗ РезультатЗапроса Цикл
			Элементы.ТипНоменклатуры.СписокВыбора.Добавить(Структура.Код, Структура.Наименование);
		КонецЦикла;
	КонецЕсли;
	
	Если Элементы.Каталог.СписокВыбора.Количество() = 0 ИЛИ Элементы.ТипНоменклатуры.СписокВыбора.Количество() = 0 Тогда
		Предупреждение("Нет доступных каталогов для поиска!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	//Если (ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("Форма") И ЭтаФорма.ВладелецФормы.ЭлементыФормы.Найти("Автомобиль") <> Неопределено)
	//	ИЛИ (ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("ФормаКлиентскогоПриложения") И ЭтаФорма.ВладелецФормы.Элементы.Найти("Автомобиль") <> Неопределено) Тогда
	//	ИнициализироватьПодбор(ЭтаФорма.ВладелецФормы.Автомобиль);
	//КонецЕсли;
	
КонецПроцедуры //ПриОткрытии()


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "атПомощникПродаж_РабочийЛист_Записан" Тогда
		Если Параметр.Источник = РабочийЛистИсточник И Параметр.Объект = РабочийЛистОбъект Тогда
			ЗаполнитьРабочийЛист();
			УправлениеВидомПоиска();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_СервисПодбор_ПараметрыОткрытия" Тогда
		
		Если Источник = ЭтаФорма.ВладелецФормы Тогда
		
			НовыйКаталог = Неопределено;
			Если Параметр.Свойство("Каталог") Тогда
				Если ТипЗнч(Параметр.Каталог) = Тип("Структура") Тогда
					НовыйКаталог = Параметр.Каталог.Код;
				Иначе
					НовыйКаталог = Параметр.Каталог;
				КонецЕсли;
			КонецЕсли;
			
			НовыйТипНоменклатуры = Неопределено;
			Если Параметр.Свойство("ТипНоменклатуры") Тогда
				Если ТипЗнч(Параметр.ТипНоменклатуры) = Тип("Структура") Тогда
					НовыйТипНоменклатуры = Параметр.ТипНоменклатуры.Код;
				Иначе
					НовыйТипНоменклатуры = Параметр.ТипНоменклатуры;
				КонецЕсли;
			КонецЕсли;
			
			ИнициализироватьПодбор(Параметр.Источник, Параметр.Объект, НовыйКаталог, НовыйТипНоменклатуры);
			
			Если Параметр.Свойство("ПараметрыДетали") И ЗначениеЗаполнено(Параметр.ПараметрыДетали) Тогда
				ЗаполнитьДеревоСвойств(Параметр.ПараметрыДетали);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_НомерДетали_Удален" Тогда
		
		Если Параметр.Источник = РабочийЛистИсточник И Параметр.Объект = РабочийЛистОбъект Тогда
			УдаляемыеСтроки = НомераДеталей.НайтиСтроки(Новый Структура("Идентификатор", Параметр.Идентификатор));
			Для Каждого НомерДетали Из УдаляемыеСтроки Цикл
				НомераДеталей.Удалить(НомераДеталей.Индекс(НомерДетали));
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_УстановленоСоответствие" Тогда
		
		СсылкаСловаря = Параметр.СсылкаСловаря;
		СсылкаИБ = Параметр.СсылкаИБ;
		
		ЕстьИзменения = Ложь;
		
		Если СсылкаСловаря.Тип = "Номенклатура" Тогда
			
			ЕстьНоменклатура = ТипЗнч(СсылкаИБ) = Тип("СправочникСсылка.Номенклатура") И ЗначениеЗаполнено(СсылкаИБ);
			Если ЕстьНоменклатура Тогда
				ПроизводительНоменклатуры = СсылкаИБ.Производитель;
				АртикулНоменклатуры = СсылкаИБ.АртикулДляПоиска;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(АртикулНоменклатуры) И ЗначениеЗаполнено(ПроизводительНоменклатуры) Тогда
				Данные = Новый Массив;
				Данные.Добавить(ПроизводительНоменклатуры.Код);
				Данные.Добавить("_");
				Данные.Добавить(АртикулНоменклатуры);
				Идентификатор = ХешированиеДанныхНаСервере(Данные);
				
				СтруктураОтбораХеш = Новый Структура("Идентификатор", Идентификатор);
			Иначе
				СтруктураОтбораХеш = Новый Структура("НоменклатураКод", СсылкаСловаря.Код);
			КонецЕсли;
			
			НайденныеСтроки = НомераДеталей.НайтиСтроки(СтруктураОтбораХеш);
			Для каждого Строка Из НайденныеСтроки Цикл
				ЕстьИзменения = Истина;
				Строка.Номенклатура = СсылкаИБ;
				Строка.НоменклатураКод = СсылкаСловаря.Код;
			КонецЦикла;
			
			//НайденныеСтроки = НомераДеталейМенеджера.НайтиСтроки(СтруктураОтбораХеш);
			//Для каждого Строка Из НайденныеСтроки Цикл
			//	ЕстьИзменения = Истина;
			//	Строка.Номенклатура = СсылкаИБ;
			//	Строка.НоменклатураКод = СсылкаСловаря.Код;
			//КонецЦикла;
			
			НайденныеСтроки = Номенклатура.НайтиСтроки(СтруктураОтбораХеш);
			Для каждого Строка Из НайденныеСтроки Цикл
				Строка.Номенклатура = СсылкаИБ;
				Строка.НоменклатураКод = СсылкаСловаря.Код;
				Строка.ЕстьНоменклатура = ЕстьНоменклатура;
			КонецЦикла;
			
		ИначеЕсли СсылкаСловаря.Тип = "Производители" Тогда
			
			СсылкаИБ = ?(ЗначениеЗаполнено(СсылкаИБ), СсылкаИБ, СсылкаСловаря.Наименование);
			ЕстьПроизводитель = ТипЗнч(СсылкаИБ) = Тип("СправочникСсылка.Производители") И ЗначениеЗаполнено(СсылкаИБ);
			СтруктураОтбора = Новый Структура("ПроизводительКод", СсылкаСловаря.Код);
			
			НайденныеСтроки = Номенклатура.НайтиСтроки(СтруктураОтбора);
			Для Каждого Строка Из НайденныеСтроки Цикл
				
				Если Строка.Производитель <> СсылкаИБ И ЗначениеЗаполнено(Строка.АртикулДляПоиска) Тогда
					Данные = Новый Массив;
					Если ЕстьПроизводитель Тогда
						Данные.Добавить(СсылкаИБ.Код);
					Иначе
						Данные.Добавить(Строка.ПроизводительКод);
					КонецЕсли;
					Данные.Добавить("_");
					Данные.Добавить(Строка.АртикулДляПоиска);
					
					Строка.Идентификатор = ХешированиеДанныхНаСервере(Данные);
					Строка.Производитель = СсылкаИБ;
				КонецЕсли;
				
				Строка.ЕстьПроизводитель = ЕстьПроизводитель;
				
			КонецЦикла;
			
			НайденныеСтроки = НомераДеталей.НайтиСтроки(СтруктураОтбора);
			Для Каждого Строка Из НайденныеСтроки Цикл
				
				Если Строка.Производитель <> СсылкаИБ И ЗначениеЗаполнено(Строка.АртикулДляПоиска) Тогда
					
					ЕстьИзменения = Истина;
					
					Данные = Новый Массив;
					Если ЕстьПроизводитель Тогда
						Данные.Добавить(СсылкаИБ.Код);
					Иначе
						Данные.Добавить(Строка.ПроизводительКод);
					КонецЕсли;
					Данные.Добавить("_");
					Данные.Добавить(Строка.АртикулДляПоиска);
					
					Строка.Идентификатор = ХешированиеДанныхНаСервере(Данные);
					Строка.Производитель = СсылкаИБ;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



#Область ОбработчикиДляРабочегоЛиста

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЛокальныйРабочийЛистНаСервере(Знач Источник, Знач Объект, Знач Автор = Неопределено, Знач ПолучатьЗапросы = Истина, Знач ПолучатьДетали = Истина)
	Возврат атСервер.ПолучитьЛокальныйРабочийЛист(Источник, Объект, Автор, ПолучатьЗапросы, ПолучатьДетали);
КонецФункции


&НаСервере
Процедура ЗаполнитьРабочийЛист()
	
	КаталогиАвтомобиля.Очистить();
	НомераДеталей.Очистить();
	СписокВыбораНаборов.Очистить();
	ВыбранныйНаборСвойств.Очистить();
	
	РабочийЛист = ПолучитьЛокальныйРабочийЛистНаСервере(РабочийЛистИсточник, РабочийЛистОбъект);
	
	Если РабочийЛист = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторовЗапроса = Новый Массив;
	
	Если РабочийЛист.Свойство("Запросы") Тогда
		Для Каждого Запрос Из РабочийЛист.Запросы Цикл
			ИдентификаторЗапроса = СтрЗаменить(Запрос.Идентификатор, "-", "");
			Если Запрос.Сервис = "Подбор" И МассивИдентификаторовЗапроса.Найти(ИдентификаторЗапроса) = Неопределено Тогда
				МассивИдентификаторовЗапроса.Добавить(ИдентификаторЗапроса);
				СтрокаКаталога = КаталогиАвтомобиля.Добавить();
				СтрокаКаталога.Каталог = Запрос.КаталогКод;
				СтрокаКаталога.КаталогНаименование = Запрос.Каталог;
				Если ЗначениеЗаполнено(Запрос.Марка) Тогда
					СтрокаКаталога.Марка = Запрос.МаркаКод;
					СтрокаКаталога.МаркаНаименование = Запрос.Марка;
				КонецЕсли;
				Если ЗначениеЗаполнено(Запрос.Модель) Тогда
					СтрокаКаталога.Модель = Запрос.МодельКод;
					СтрокаКаталога.МодельНаименование = Запрос.Модель;
				КонецЕсли;
				Если ЗначениеЗаполнено(Запрос.Модификация) Тогда
					СтрокаКаталога.Модификация = Запрос.МодификацияКод;
					СтрокаКаталога.МодификацияНаименование = Запрос.Модификация;
				КонецЕсли;
				СтрокаКаталога.ИдентификаторЗапроса = ИдентификаторЗапроса;
				Если ЗначениеЗаполнено(Запрос.Параметры) Тогда
					Попытка
						// Попробуем получить массив сохраненных наборов
						ПараметрыЗапроса = ЗначениеИзСтрокиВнутр(Запрос.Параметры);
					Исключение КонецПопытки;
					Если ПараметрыЗапроса <> Неопределено Тогда
						Для Каждого Параметр Из ПараметрыЗапроса Цикл
							СтрокаНабора = СтрокаКаталога.Параметры.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаНабора, Параметр,, "СоставНабора");
							Для Каждого СтруктураСвойства Из Параметр.СоставНабора Цикл
								СтрокаСостава = СтрокаНабора.СоставНабора.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаСостава, СтруктураСвойства);
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Заполним номера деталей
	Если РабочийЛист.Свойство("НомераДеталей") Тогда
		Для Каждого НомерДетали Из РабочийЛист.НомераДеталей Цикл
			ИдентификаторЗапроса = СтрЗаменить(НомерДетали.ИдентификаторЗапроса, "-", "");
			Если МассивИдентификаторовЗапроса.Найти(ИдентификаторЗапроса) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаДетали = НомераДеталей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДетали, НомерДетали,, "ИдентификаторЗапроса, Параметры");
			СтрокаДетали.ИдентификаторЗапроса = ИдентификаторЗапроса;
		КонецЦикла;
	КонецЕсли;
	
	НаборыДеталей = НомераДеталей.Выгрузить(,"ИмяНабора");
	НаборыДеталей.Свернуть("ИмяНабора");
	НаборыДеталей.Сортировать("ИмяНабора Возр");
	СписокВыбораНаборов.ЗагрузитьЗначения(НаборыДеталей.ВыгрузитьКолонку("ИмяНабора"));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьЗапросРабочегоЛистаНаСервере(Знач Источник, Знач Объект, Знач Каталог, Знач Марка, Знач Модель, Знач Модификация, ПараметрыЗапроса=Неопределено, ИдентификаторЗапроса="")
	Возврат атСервер.ОбновитьЗапросРабочегоЛиста(Источник, Объект, Новый Структура("Код, Наименование", "", "Подбор"), Каталог, Марка, Модель, Модификация, ПараметрыЗапроса, ИдентификаторЗапроса);
КонецФункции

&НаКлиенте
Процедура ОбновитьЗапрос(СтрокаКаталога, ПараметрыЗапроса=Неопределено)
	Если ОбновитьЗапросРабочегоЛистаНаСервере(РабочийЛистИсточник, РабочийЛистОбъект,
		Новый Структура("Код, Наименование", СтрокаКаталога.Каталог, СтрокаКаталога.КаталогНаименование),
		Новый Структура("Код, Наименование", СтрокаКаталога.Марка, СтрокаКаталога.МаркаНаименование),
		Новый Структура("Код, Наименование", СтрокаКаталога.Модель, СтрокаКаталога.МодельНаименование),
		Новый Структура("Код, Наименование", СтрокаКаталога.Модификация, СтрокаКаталога.МодификацияНаименование),
		ПараметрыЗапроса,
		СтрокаКаталога.ИдентификаторЗапроса) = Истина Тогда
		
		Если ВидПоиска = "ПоМоделиАвтомобиля" Тогда
			ДобавитьКомандуНаФорму("КаталогиАвтомобиляПодменю", СтрокаКаталога.ИдентификаторЗапроса,, СтрокаКаталога.МаркаНаименование + ": " + СтрокаКаталога.МодельНаименование + ?(ЗначениеЗаполнено(СтрокаКаталога.МодификацияНаименование), "; "+СтрокаКаталога.МодификацияНаименование, ""));
		КонецЕсли;
		
		// Оповещаем в любом случае!
		СтруктураОповещения = Новый Структура;
		СтруктураОповещения.Вставить("Источник", РабочийЛистИсточник);
		СтруктураОповещения.Вставить("Объект",   РабочийЛистОбъект);
		Оповестить("атПомощникПродаж_Запрос_Добавлен", СтруктураОповещения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ДобавитьНаборСвойствНаКлиенте(ДобавляемНоменклатуру = Ложь)
	Если Не ЗначениеЗаполнено(РабочийЛистОбъект) Тогда
		Предупреждение("Не выбран автомобиль!");
		Возврат Неопределено;
	КонецЕсли;
	ВыбранныеСвойства = ПолучитьВыбранныеСвойства();
	Если ВыбранныеСвойства.Количество() = 0 Тогда
		Предупреждение("Не выбраны свойства!");
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяСтрокаКаталога = Новый Структура("Каталог, КаталогНаименование, Марка, МаркаНаименование, Модель, МодельНаименование, Модификация, МодификацияНаименование");
	
	Если ВидПоиска = "ПоМоделиАвтомобиля" Тогда
		Для Каждого СтрокаДерева Из СвойстваКаталогов.ПолучитьЭлементы() Цикл
			Если ПустаяСтрока(СтрокаДерева.ЗначениеКод) Тогда
				Продолжить;
			КонецЕсли;
			Попытка
				НоваяСтрокаКаталога[СтрокаДерева.ИдентификаторГруппы] = СтрокаДерева.ЗначениеКод;
				НоваяСтрокаКаталога[СтрокаДерева.ИдентификаторГруппы+"Наименование"] = СтрокаДерева.Значение;
			Исключение КонецПопытки;
		КонецЦикла;
		
		КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
		НоваяСтрокаКаталога.Каталог = КаталогЗначение.Значение;
		НоваяСтрокаКаталога.КаталогНаименование = КаталогЗначение.Представление;
		
		СтруктураПоиска = Новый Структура("Каталог, КаталогНаименование, Марка, Модель, Модификация");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, НоваяСтрокаКаталога);
	Иначе
		ТипНоменклатурыЗначение = Элементы.ТипНоменклатуры.СписокВыбора.НайтиПоЗначению(ТипНоменклатуры);
		НоваяСтрокаКаталога.Каталог = ТипНоменклатурыЗначение.Значение;
		НоваяСтрокаКаталога.КаталогНаименование = ТипНоменклатурыЗначение.Представление;
		
		СтруктураПоиска = Новый Структура("Каталог, КаталогНаименование");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, НоваяСтрокаКаталога);
	КонецЕсли;
	
	СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(СтруктураПоиска);
	Если СтрокиКаталога.Количество() = 0 Тогда
		СтрокаКаталога = КаталогиАвтомобиля.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКаталога, НоваяСтрокаКаталога);
	Иначе
		СтрокаКаталога = СтрокиКаталога[0];
		Для Каждого НаборСвойств Из СтрокаКаталога.Параметры Цикл
			Если КоллекцииИдентичны(НаборСвойств.СоставНабора, ВыбранныеСвойства, "ВидСвойства, ТипЗначения, Значение",, Ложь) Тогда
				// Уже есть такой набор свойств
				Если ДобавляемНоменклатуру Тогда
					Возврат СтрокаКаталога;
				КонецЕсли;
				Предупреждение("Такой набор свойств уже сохранен!");
				Возврат Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Добавляем набор свойств
	Количество = 1;
	
	СтрокаНабора = СтрокаКаталога.Параметры.Добавить();
	СтрокаНабора.Идентификатор   = СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
	СтрокаНабора.ТипНоменклатуры = ТипНоменклатуры;
	СтрокаНабора.Количество      = Количество;
	
	Наименование = "";
	Для Каждого ВыбранноеСвойство Из ВыбранныеСвойства Цикл
		//Наименование = Наименование + СтрокаСостава.ВидСвойстваНаименование + "-" + СтрокаСостава.ЗначениеПредставление + "; ";
		Наименование = Наименование + ВыбранноеСвойство.ЗначениеПредставление + ", ";
		СтрокаСостава = СтрокаНабора.СоставНабора.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСостава, ВыбранноеСвойство);
	КонецЦикла;
	Наименование = ЛЕВ(СокрЛП(Наименование), 200);
	СтрокаНабора.Наименование = Наименование;
	
	МассивПараметров = Новый Массив;
	Для Каждого Параметр Из СтрокаКаталога.Параметры Цикл
		СтруктураПараметра = Новый Структура("Идентификатор, ТипНоменклатуры, Наименование, Количество, ЕдиницаИзмерения, СоставНабора");
		ЗаполнитьЗначенияСвойств(СтруктураПараметра, Параметр,, "СоставНабора");
		МассивСвойств = Новый Массив;
		Для Каждого СтрокаСостава Из Параметр.СоставНабора Цикл
			СтруктураСвойства = Новый Структура("ВидСвойства, ВидСвойстваНаименование, ТипЗначения, Значение, ЗначениеПредставление, Предопределенное");
			ЗаполнитьЗначенияСвойств(СтруктураСвойства, СтрокаСостава);
			МассивСвойств.Добавить(СтруктураСвойства);
		КонецЦикла;
		СтруктураПараметра.СоставНабора = МассивСвойств;
		МассивПараметров.Добавить(СтруктураПараметра);
	КонецЦикла;
	
	ОбновитьЗапрос(СтрокаКаталога, МассивПараметров);
	
	ДобавитьКомандуНаФорму("НаборыСвойствПодменю", СтрокаКаталога.ИдентификаторЗапроса, СтрокаНабора.Идентификатор, СтрокаНабора.Наименование);
	
	Возврат СтрокаКаталога;
	
КонецФункции

&НаКлиенте
Процедура УдалитьНаборСвойствНаКлиенте()
	
	МассивИД = СоставПодменю("НаборыСвойствПодменю");
	
	Если МассивИД.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаВыбора = Новый ТаблицаЗначений;
	ТаблицаВыбора.Колонки.Добавить("Номер", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный)));
	ТаблицаВыбора.Колонки.Добавить("Наименование");
	
	Сч = 0;
	Для Каждого СтруктураИД Из МассивИД Цикл
		Сч = Сч + 1;
		СтрокаКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", СтруктураИД.ИдентификаторЗапроса))[0];
		СтрокаПараметры = СтрокаКаталога.Параметры.НайтиСтроки(Новый Структура("Идентификатор", СтруктураИД.Идентификатор))[0];
		СтрокаТаблицы = ТаблицаВыбора.Добавить();
		СтрокаТаблицы.Номер = Сч;
		СтрокаТаблицы.Наименование = СтрокаПараметры.Наименование;
	КонецЦикла;
	
	Если ТаблицаВыбора.Количество() > 0 Тогда
		ВыбраннаяСтрока = ТаблицаВыбора.ВыбратьСтроку("Выберите удаляемый набор");
	КонецЕсли;
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураИД = МассивИД[ВыбраннаяСтрока.Номер-1];
	
	СтрокаКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", СтруктураИД.ИдентификаторЗапроса))[0];
	СтрокаПараметры = СтрокаКаталога.Параметры.НайтиСтроки(Новый Структура("Идентификатор", СтруктураИД.Идентификатор))[0];
	СтрокаКаталога.Парметры.Удалить(СтрокаПараметры);
	
	МассивПараметров = Новый Массив;
	Для Каждого Параметр Из СтрокаКаталога.Параметры Цикл
		СтруктураПараметра = Новый Структура("Идентификатор, ТипНоменклатуры, Наименование, Количество, СоставНабора");
		ЗаполнитьЗначенияСвойств(СтруктураПараметра, Параметр);
		МассивПараметров.Добавить(СтруктураПараметра);
	КонецЦикла;
	
	ОбновитьЗапрос(СтрокаКаталога, МассивПараметров);
	
	УдалитьКомандуНаФорме("НаборыСвойствПодменю", СтруктураИД.ИдентификаторЗапроса, СтруктураИД.Идентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНомерДеталиНаКлиенте(Знач ИмяНабора, Знач СтрокаНоменклатура, Знач Количество)
	
	// Сначала нужно создать номенклатуру по данным из сервиса?????
	
	СтрокаКаталога = ДобавитьНаборСвойствНаКлиенте(Истина);
	
	Если СтрокаКаталога = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеСвойства = ПолучитьВыбранныеСвойства();
	
	ПараметрыДетали = Новый Структура;
	ПараметрыДетали.Вставить("ТипНоменклатуры",  ТипНоменклатуры);
	ПараметрыДетали.Вставить("Наименование",     "");
	ПараметрыДетали.Вставить("Количество",       Количество);
	ПараметрыДетали.Вставить("ЕдиницаИзмерения", ""); // Неизвестно ))
	ПараметрыДетали.Вставить("СоставНабора",     Новый Массив);
	
	Для Каждого ВыбранноеСвойство Из ВыбранныеСвойства Цикл
		//Наименование = Наименование + СтрокаСостава.ВидСвойстваНаименование + "-" + СтрокаСостава.ЗначениеПредставление + "; ";
		ПараметрыДетали.Наименование = ПараметрыДетали.Наименование + ВыбранноеСвойство.ЗначениеПредставление + ", ";
		СтруктураСвойства = Новый Структура("ВидСвойства, ВидСвойстваНаименование, ТипЗначения, Значение, ЗначениеПредставление, Предопределенное");
		ЗаполнитьЗначенияСвойств(СтруктураСвойства, ВыбранноеСвойство);
		ПараметрыДетали.СоставНабора.Добавить(СтруктураСвойства);
	КонецЦикла;
	
	СтрокаДетали = НомераДеталей.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаДетали, СтрокаНоменклатура);
	СтрокаДетали.ИдентификаторЗапроса = СтрокаКаталога.ИдентификаторЗапроса;
	СтрокаДетали.ИмяНабора = ИмяНабора;
	СтрокаДетали.Количество = Количество;
	
	Если СписокВыбораНаборов.НайтиПоЗначению(ИмяНабора) = Неопределено Тогда
		СписокВыбораНаборов.Добавить(ИмяНабора);
		СписокВыбораНаборов.СортироватьПоЗначению(НаправлениеСортировки.Возр);
	КонецЕсли;
	
	СтруктураОповещения = Новый Структура;
	СтруктураОповещения.Вставить("Источник",             РабочийЛистИсточник);
	СтруктураОповещения.Вставить("Объект",               РабочийЛистОбъект);
	СтруктураОповещения.Вставить("ИдентификаторЗапроса", СтрокаКаталога.ИдентификаторЗапроса);
	СтруктураОповещения.Вставить("Идентификатор",        СтрокаДетали.Идентификатор);
	СтруктураОповещения.Вставить("ИмяНабора",            СтрокаДетали.ИмяНабора);
	СтруктураОповещения.Вставить("Артикул",              СтрокаДетали.Артикул);
	СтруктураОповещения.Вставить("АртикулДляПоиска",     СтрокаДетали.АртикулДляПоиска);
	СтруктураОповещения.Вставить("Производитель",        ?(ТипЗнч(СтрокаДетали.Производитель) = Тип("СправочникСсылка.Производители"), СтрокаДетали.Производитель, Новый Структура("Тип, Код, Наименование", "Производители", СтрокаДетали.ПроизводительКод, ""+СтрокаДетали.Производитель)));
	СтруктураОповещения.Вставить("Номенклатура",         Новый Структура("Тип, Код, Наименование", "Номенклатура", СтрокаДетали.НоменклатураКод, ""+СтрокаДетали.Номенклатура));
	СтруктураОповещения.Вставить("Наименование",         ""+СтрокаДетали.Номенклатура);
	СтруктураОповещения.Вставить("Количество",           СтрокаДетали.Количество);
	СтруктураОповещения.Вставить("Параметры",            ПараметрыДетали);
	
	Оповестить("атПомощникПродаж_НомерДетали_Добавлен", СтруктураОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНомерДеталиНаКлиенте(Знач ИмяНабора, Знач Идентификатор)
	// Удаляем деталь из набора
	УдаляемыеСтроки = НомераДеталей.НайтиСтроки(Новый Структура("ИмяНабора, Идентификатор, Автор", ИмяНабора, Идентификатор, ПараметрыСеанса.Пользователь));
	Для Каждого НомерДетали Из УдаляемыеСтроки Цикл
		НомераДеталей.Удалить(НомераДеталей.Индекс(НомерДетали));
		
		СтруктураОповещения = Новый Структура;
		СтруктураОповещения.Вставить("Источник",             РабочийЛистИсточник);
		СтруктураОповещения.Вставить("Объект",               РабочийЛистОбъект);
		СтруктураОповещения.Вставить("ИдентификаторЗапроса", НомерДетали.ИдентификаторЗапроса);
		СтруктураОповещения.Вставить("ИмяНабора",            ИмяНабора);
		СтруктураОповещения.Вставить("АртикулДляПоиска",     НомерДетали.АртикулДляПоиска);
		СтруктураОповещения.Вставить("Производитель",        ?(ТипЗнч(НомерДетали.Производитель) = Тип("СправочникСсылка.Производители"), НомерДетали.Производитель, Новый Структура("Тип, Код, Наименование", "Производители", НомерДетали.ПроизводительКод, ""+НомерДетали.Производитель)));
		Оповестить("атПомощникПродаж_НомерДетали_Удален",    СтруктураОповещения);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти



#Область ПодборПоМоделиАвтомобиля

&НаКлиенте
Процедура ЗаполнитьМаркиКаталога(Знач ВыбраннаяМарка=Неопределено, Знач ВыбраннаяМодель=Неопределено, Знач ВыбраннаяМодификация=Неопределено)
	
	// Очистим свойства
	ТипНоменклатуры = "";
	ДеревоСвойств.ПолучитьЭлементы().Очистить();
	
	Элементы.СохранитьНаборСвойств.Доступность = Ложь;
	
	ВыбранныйНаборСвойств.Очистить();
	
	// Очистим свойства каталога
	СвойстваКаталогов.ПолучитьЭлементы().Очистить();
	
	Если ПустаяСтрока(Каталог) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяМарка = Неопределено Тогда
		КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
		СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование", КаталогЗначение.Значение, КаталогЗначение.Представление));
		Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
			Если ВыбраннаяМарка = Неопределено Тогда
				ВыбраннаяМарка = СтрокаКаталога.Марка;
			ИначеЕсли ВыбраннаяМарка <> СтрокаКаталога.Марка Тогда
				// Для автомобиля сохранено несколько марок
				ВыбраннаяМарка = "";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтрокаДерева = ПолучитьВеткуДерева("Марка", "СвойстваКаталогов", "ИдентификаторГруппы");
	СтрокаДерева.ТипНоменклатуры     = "Марка";
	СтрокаДерева.ТипНоменклатурыКод  = "";
	СтрокаДерева.ИдентификаторГруппы = "Марка";
	СтрокаДерева.Значение            = "";
	СтрокаДерева.ЗначениеКод         = "";
	
	//Заполним марки
	Ошибка = "";
	Отбор = Новый Структура("Каталог", Каталог);
	РезультатЗапроса = МаркиАвтомобилейНаСервере(Отбор, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		Для Каждого Структура ИЗ РезультатЗапроса Цикл
			НоваяСтрока = СтрокаДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Значение = Структура.Марка.Наименование;
			НоваяСтрока.ЗначениеКод = Структура.Марка.Код;
			НоваяСтрока.ИдентификаторГруппы = "Марка";
			НоваяСтрока.Популярная = Структура.Популярная;
			НоваяСтрока.Избранное = Структура.Избранное;
			Если НоваяСтрока.ЗначениеКод = ВыбраннаяМарка Тогда
				СтрокаДерева.Выбран = Истина;
				СтрокаДерева.Значение = Структура.Марка.Наименование;
				СтрокаДерева.ЗначениеКод = Структура.Марка.Код;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьМоделиКаталога(СтрокаДерева.ЗначениеКод, ВыбраннаяМодель, ВыбраннаяМодификация);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМоделиКаталога(ВыбраннаяМарка = "", Знач ВыбраннаяМодель=Неопределено, Знач ВыбраннаяМодификация=Неопределено)
	
	//ВидПоиска = "ПоМоделиАвтомобиля"
	
	СтрокаДерева = ПолучитьВеткуДерева("Модель", "СвойстваКаталогов", "ИдентификаторГруппы");
	СтрокаДерева.ПолучитьЭлементы().Очистить();
	СтрокаДерева.ТипНоменклатуры = "Модель";
	СтрокаДерева.ТипНоменклатурыКод = ВыбраннаяМарка;
	СтрокаДерева.ИдентификаторГруппы = "Модель";
	СтрокаДерева.Значение = "";
	СтрокаДерева.ЗначениеКод = "";
	СтрокаДерева.ЕстьМодификации = Ложь;
	СтрокаДерева.ЕстьНаборыСвойств = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыбраннаяМарка) Тогда
		// Очистим модификации
		ЗаполнитьМодификацииКаталога();
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяМодель = Неопределено Тогда
		КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
		СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование, Марка", КаталогЗначение.Значение, КаталогЗначение.Представление, ВыбраннаяМарка));
		Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
			Если ВыбраннаяМодель = Неопределено Тогда
				ВыбраннаяМодель = СтрокаКаталога.Модель;
			ИначеЕсли ВыбраннаяМодель <> СтрокаКаталога.Модель Тогда
				// Для автомобиля сохранено несколько моделей
				ВыбраннаяМодель = "";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Ошибка = "";
	Отбор = Новый Структура("Каталог, Марка", Каталог, ВыбраннаяМарка);
	РезультатЗапроса = МоделиАвтомобилейНаСервере(Отбор, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		Для Каждого Структура ИЗ РезультатЗапроса Цикл
			НоваяСтрока = СтрокаДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.ТипНоменклатурыКод = ВыбраннаяМарка;
			НоваяСтрока.Значение = Структура.Модель.Наименование;
			НоваяСтрока.ЗначениеКод = Структура.Модель.Код;
			НоваяСтрока.ИдентификаторГруппы = "Модель";
			НоваяСтрока.ЕстьНаборыСвойств = Структура.ЕстьНаборыСвойств;
			НоваяСтрока.ЕстьМодификации = Структура.ЕстьМодификации;
			Если НоваяСтрока.ЗначениеКод = ВыбраннаяМодель Тогда
				СтрокаДерева.Выбран = Истина;
				СтрокаДерева.Значение = Структура.Модель.Наименование;
				СтрокаДерева.ЗначениеКод = Структура.Модель.Код;
				СтрокаДерева.ЕстьНаборыСвойств = Структура.ЕстьНаборыСвойств;
				СтрокаДерева.ЕстьМодификации = Структура.ЕстьМодификации;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьМодификацииКаталога(СтрокаДерева.ЗначениеКод, ВыбраннаяМодификация, СтрокаДерева.ЕстьМодификации, СтрокаДерева.ЕстьНаборыСвойств);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМодификацииКаталога(ВыбраннаяМодель = "", Знач ВыбраннаяМодификация=Неопределено, ЕстьМодификации = Ложь, ЕстьНаборыСвойств = Ложь)
	
	//ВидПоиска = "ПоМоделиАвтомобиля"
	
	СтрокаДерева = ПолучитьВеткуДерева("Модификация", "СвойстваКаталогов", "ИдентификаторГруппы");
	СтрокаДерева.ПолучитьЭлементы().Очистить();
	СтрокаДерева.ТипНоменклатуры = "Модификация";
	СтрокаДерева.ТипНоменклатурыКод = ВыбраннаяМодель;
	СтрокаДерева.ИдентификаторГруппы = "Модификация";
	СтрокаДерева.Значение = "";
	СтрокаДерева.ЗначениеКод = "";
	
	Если Не ЗначениеЗаполнено(ВыбраннаяМодель) Тогда
		ЗаполнитьНаборыСвойствКаталога();
		Возврат;
	КонецЕсли;
	
	Если НЕ ЕстьМодификации Тогда
		ЗаполнитьНаборыСвойствКаталога(ВыбраннаяМодель);
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяМодификация = Неопределено Тогда
		КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
		СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование, Модель", КаталогЗначение.Значение, КаталогЗначение.Представление, ВыбраннаяМодель));
		Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
			Если ВыбраннаяМодификация = Неопределено Тогда
				ВыбраннаяМодификация = СтрокаКаталога.Модификация;
			ИначеЕсли ВыбраннаяМодификация <> СтрокаКаталога.Модификация Тогда
				// Для автомобиля сохранено несколько модификаций
				ВыбраннаяМодификация = "";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	//Заполним модификации
	Ошибка = "";
	Отбор = Новый Структура("Каталог, Модель", Каталог, ВыбраннаяМодель);
	РезультатЗапроса = МодификацииАвтомобилейНаСервере(Отбор, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		Для Каждого Структура ИЗ РезультатЗапроса Цикл
			НоваяСтрока = СтрокаДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.ТипНоменклатурыКод = ВыбраннаяМодель;
			НоваяСтрока.Значение = Структура.Наименование;
			НоваяСтрока.ЗначениеКод = Структура.Код;
			НоваяСтрока.ИдентификаторГруппы = "Модификация";
			Если НоваяСтрока.ЗначениеКод = ВыбраннаяМодификация Тогда
				СтрокаДерева.Выбран = Истина;
				СтрокаДерева.ТипНоменклатурыКод = ВыбраннаяМодель;
				СтрокаДерева.Значение = Структура.Наименование;
				СтрокаДерева.ЗначениеКод = Структура.Код;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьНаборыСвойствКаталога(ВыбраннаяМодель, СтрокаДерева.ЗначениеКод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаборыСвойствКаталога(ВыбраннаяМодель = "", ВыбраннаяМодификация = "")
	
	//ВидПоиска = "ПоМоделиАвтомобиля"
	
	// Очистим подменю Наборы свойств
	ОчиститьПодменю("НаборыСвойствПодменю");
	
	Элементы.СохранитьВыбор.Доступность = Ложь;
	
	СтрокаДерева = ПолучитьВеткуДерева("НаборСвойств", "СвойстваКаталогов", "ИдентификаторГруппы");
	СтрокаДерева.ПолучитьЭлементы().Очистить();
	СтрокаДерева.ТипНоменклатуры = "Наборы свойств";
	СтрокаДерева.ТипНоменклатурыКод = "";
	СтрокаДерева.ИдентификаторГруппы = "НаборСвойств";
	СтрокаДерева.Значение = "";
	СтрокаДерева.ЗначениеКод = "";
	
	Если Не ЗначениеЗаполнено(ВыбраннаяМодель) Тогда
		Возврат;
	КонецЕсли;
	
	КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
	СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование, Модель, Модификация", КаталогЗначение.Значение, КаталогЗначение.Представление, ВыбраннаяМодель, ""));
	Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
		Для Каждого Параметр Из СтрокаКаталога.Параметры Цикл
			// Заполним подменю меню наборов свойств
			ДобавитьКомандуНаФорму("НаборыСвойствПодменю", СтрокаКаталога.ИдентификаторЗапроса, Параметр.Идентификатор, Параметр.Наименование);
		КонецЦикла;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ВыбраннаяМодификация) Тогда
		КаталогЗначение = Элементы.Каталог.СписокВыбора.НайтиПоЗначению(Каталог);
		СтрокиКаталога = КаталогиАвтомобиля.НайтиСтроки(Новый Структура("Каталог, КаталогНаименование, Модель, Модификация", КаталогЗначение.Значение, КаталогЗначение.Представление, ВыбраннаяМодель, ВыбраннаяМодификация));
		Для Каждого СтрокаКаталога Из СтрокиКаталога Цикл
			Для Каждого Параметр Из СтрокаКаталога.Параметры Цикл
				// Заполним подменю меню наборов свойств
				ДобавитьКомандуНаФорму("НаборыСвойствПодменю", СтрокаКаталога.ИдентификаторЗапроса, Параметр.Идентификатор, Параметр.Наименование);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	//Заполним наборы свойств
	Ошибка = "";
	Отбор = Новый Структура("Каталог, Модель, Модификация", Каталог, ВыбраннаяМодель, ВыбраннаяМодификация);
	РезультатЗапроса = НаборыСвойствНаСервере(Отбор, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		
		Для Каждого Структура ИЗ РезультатЗапроса Цикл
			
			НоваяСтрока = СтрокаДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.ИдентификаторГруппы = Структура.ИдентификаторНабора;
			НоваяСтрока.ТипНоменклатуры     = Структура.ТипНоменклатуры.Наименование;
			НоваяСтрока.ТипНоменклатурыКод  = Структура.ТипНоменклатуры.Код;
			НоваяСтрока.Значение            = Структура.НаименованиеНабора;
			НоваяСтрока.ЗначениеКод         = "";
			НоваяСтрока.Количество          = Структура.Количество;
			НоваяСтрока.ЕдиницаИзмерения    = Структура.ЕдиницаИзмерения;
			
			Описание = "";
			Для Каждого ЗначениеСвойства Из Структура.ЗначенияСвойств Цикл
				
				СтрокаСвойства = НоваяСтрока.ПолучитьЭлементы().Добавить();
				СтрокаСвойства.ИдентификаторГруппы = "ЗначениеСвойства";
				СтрокаСвойства.ТипНоменклатуры = ЗначениеСвойства.ВидСвойства.Наименование;
				СтрокаСвойства.ТипНоменклатурыКод = ЗначениеСвойства.ВидСвойства.Код;
				СтрокаСвойства.ТипЗначения = ЗначениеСвойства.ТипЗначения;
				
				Представление = "";
				
				Если ЗначениеСвойства.ТипЗначения = "ЗначенияСвойств" Тогда
					СтрокаСвойства.Значение = ЗначениеСвойства.Значение.Наименование;
					СтрокаСвойства.ЗначениеКод = ЗначениеСвойства.Значение.Код;
					СтрокаСвойства.Описание = ЗначениеСвойства.Значение.Примечание;
					
					Представление = ЗначениеСвойства.Значение.Наименование;
					
				Иначе
					СтрокаСвойства.ЗначениеКод = "";
					Если ЗначениеСвойства.ТипЗначения = "Число" Тогда
						СтрокаСвойства.Значение = Число(ЗначениеСвойства.Значение);
						Представление = Формат(СтрокаСвойства.Значение, "ЧН=0; ЧГ=0");
					ИначеЕсли ЗначениеСвойства.ТипЗначения = "Булево" Тогда
						СтрокаСвойства.Значение = Булево(ЗначениеСвойства.Значение);
						Представление = Формат(СтрокаСвойства.Значение, "БЛ=x; БИ=v");
					ИначеЕсли ЗначениеСвойства.ТипЗначения = "Строка" Тогда
						СтрокаСвойства.Значение = Строка(ЗначениеСвойства.Значение);
						Представление = СтрокаСвойства.Значение;
					КонецЕсли;
				КонецЕсли;
				
				Описание = Описание + СтрокаСвойства.Значение + "; ";
				
			КонецЦикла;
			
			ТекстКоличество = "";
			Если ЗначениеЗаполнено(Структура.Количество) Тогда
				ТекстКоличество = "; "+Формат(Структура.Количество, "ЧГ=0") + " " + ?(ЗначениеЗаполнено(Структура.ЕдиницаИзмерения), Структура.ЕдиницаИзмерения, "шт");
			КонецЕсли;
			
			НоваяСтрока.Описание = ?(ЗначениеЗаполнено(Структура.Описание), Структура.Описание, Описание)+ТекстКоличество;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.СохранитьВыбор.Доступность = СтрокаДерева.ПолучитьЭлементы().Количество() > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваКаталоговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	СтрокаВерхнегоУровня = ТекущиеДанные;
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		// Клик по верхнему уровню
		Если Не ТекущиеДанные.Выбран Тогда
			Возврат;
		КонецЕсли;
		
		ТекущиеДанные.Выбран = Ложь;
		ТекущиеДанные.Значение = "";
		ТекущиеДанные.ЗначениеКод = "";
		ТекущиеДанные.Описание = "";
		
	Иначе
		Если Родитель.ПолучитьРодителя() = Неопределено Тогда
			// Клик по второму уровню всегда переносим на верх
			Если Родитель.Выбран
				И Родитель.Значение = ТекущиеДанные.Значение
				И Родитель.ЗначениеКод = ТекущиеДанные.ЗначениеКод Тогда
				Возврат;
			КонецЕсли;
			Родитель.Выбран = Истина;
			Родитель.Значение = ТекущиеДанные.Значение;
			Родитель.ЗначениеКод = ТекущиеДанные.ЗначениеКод;
			Родитель.Описание = ТекущиеДанные.Описание;
			
			СтрокаВерхнегоУровня = Родитель;
			
		Иначе
			// Клик по третьему уровню ЗначнияСвойств
			РодительВерхнегоУровня = Родитель.ПолучитьРодителя();
			Если РодительВерхнегоУровня.Выбран
				И РодительВерхнегоУровня.Значение = Родитель.Значение
				И РодительВерхнегоУровня.ЗначениеКод = Родитель.ЗначениеКод Тогда
				Возврат;
			КонецЕсли;
			РодительВерхнегоУровня.Выбран = Истина;
			РодительВерхнегоУровня.Значение = Родитель.Значение;
			РодительВерхнегоУровня.ЗначениеКод = Родитель.ЗначениеКод;
			РодительВерхнегоУровня.Описание = Родитель.Описание;
			
			СтрокаВерхнегоУровня = РодительВерхнегоУровня;
			
		КонецЕсли;
	КонецЕсли;
	
	ТипНоменклатуры = "";
	ВыбранныйНаборСвойств.Очистить();
	Для Каждого СтрокаДерева Из ДеревоСвойств.ПолучитьЭлементы() Цикл
		СтрокаДерева.Заблокировано = Ложь;
	КонецЦикла;
	
	ВыбранныеСвойства = Неопределено;
	
	Если ТекущиеДанные.ИдентификаторГруппы = "Марка" Тогда
		Если Родитель = Неопределено Тогда
			ЗаполнитьМоделиКаталога();
		Иначе
			ЗаполнитьМоделиКаталога(ТекущиеДанные.ЗначениеКод);
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ИдентификаторГруппы = "Модель" Тогда
		Если Родитель = Неопределено Тогда
			ЗаполнитьМодификацииКаталога();
		Иначе
			ЗаполнитьМодификацииКаталога(ТекущиеДанные.ЗначениеКод,, ТекущиеДанные.ЕстьМодификации, ТекущиеДанные.ЕстьНаборыСвойств);
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ИдентификаторГруппы = "Модификация" Тогда
		Если Родитель = Неопределено Тогда
			ЗаполнитьНаборыСвойствКаталога(ТекущиеДанные.ТипНоменклатурыКод);
		Иначе
			ЗаполнитьНаборыСвойствКаталога(ТекущиеДанные.ТипНоменклатурыКод, ТекущиеДанные.ЗначениеКод);
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ИдентификаторГруппы = "НаборСвойств" Тогда
		// Это только верхний уровень бывает
		// поэтому очистим дерево свойств заново
		
	ИначеЕсли ТекущиеДанные.ИдентификаторГруппы = "ЗначениеСвойства" Тогда
		// Это только третий уровень бывает
		ТипНоменклатуры = Родитель.ТипНоменклатурыКод;
		ВыбранныеСвойства = Новый Массив;
		Описание = "";
		Для Каждого СтрокаЗначениеСвойства Из Родитель.ПолучитьЭлементы() Цикл
			ВыбранныйНаборСвойств.Добавить(СтрокаЗначениеСвойства.ТипНоменклатурыКод);
			Описание = Описание + СтрокаЗначениеСвойства.Значение + "; ";
			Если СтрокаЗначениеСвойства.ТипЗначения = "ЗначенияСвойств" Тогда
				ВыбранныеСвойства.Добавить(Новый Структура("ВидСвойства, ТипЗначения, Значение", СтрокаЗначениеСвойства.ТипНоменклатурыКод, СтрокаЗначениеСвойства.ТипЗначения, СтрокаЗначениеСвойства.ЗначениеКод));
			Иначе
				ВыбранныеСвойства.Добавить(Новый Структура("ВидСвойства, ТипЗначения, Значение", СтрокаЗначениеСвойства.ТипНоменклатурыКод, СтрокаЗначениеСвойства.ТипЗначения, СтрокаЗначениеСвойства.Значение));
			КонецЕсли;
		КонецЦикла;
		//СтрокаВерхнегоУровня.Описание = Описание;
		
	Иначе
		ТипНоменклатуры = ТекущиеДанные.ТипНоменклатурыКод;
		ВыбранныеСвойства = Новый Массив;
		Описание = "";
		Для Каждого СтрокаЗначениеСвойства Из ТекущиеДанные.ПолучитьЭлементы() Цикл
			ВыбранныйНаборСвойств.Добавить(СтрокаЗначениеСвойства.ТипНоменклатурыКод);
			Описание = Описание + СтрокаЗначениеСвойства.Значение + "; ";
			Если СтрокаЗначениеСвойства.ТипЗначения = "ЗначенияСвойств" Тогда
				ВыбранныеСвойства.Добавить(Новый Структура("ВидСвойства, ТипЗначения, Значение", СтрокаЗначениеСвойства.ТипНоменклатурыКод, СтрокаЗначениеСвойства.ТипЗначения, СтрокаЗначениеСвойства.ЗначениеКод));
			Иначе
				ВыбранныеСвойства.Добавить(Новый Структура("ВидСвойства, ТипЗначения, Значение", СтрокаЗначениеСвойства.ТипНоменклатурыКод, СтрокаЗначениеСвойства.ТипЗначения, СтрокаЗначениеСвойства.Значение));
			КонецЕсли;
		КонецЦикла;
		//СтрокаВерхнегоУровня.Описание = Описание;
		
	КонецЕсли;
	
	// Развернем дерево, где не заполнены значения
	РазвернутьВсеДерево(СвойстваКаталогов.ПолучитьЭлементы(), "СвойстваКаталогов");
	
	ЗаполнитьДеревоСвойств(ВыбранныеСвойства);
	
	// Развернем дерево, где не заполнены значения
	//РазвернутьВсеДерево(ДеревоСвойств.ПолучитьЭлементы(), "ДеревоСвойств");
	
КонецПроцедуры //СвойстваКаталоговВыбор()

#КонецОбласти



#Область ПоискПоСвойствам

&НаКлиенте
Процедура ЗаполнитьДеревоСвойств(ВыбранныеСвойства = Неопределено)
	
	// Очистим свойства
	ДеревоСвойств.ПолучитьЭлементы().Очистить();
	Номенклатура.Очистить();
	
	Элементы.СохранитьНаборСвойств.Доступность = Ложь;
	
	//Элементы.УдалитьНаборСвойств.Доступность = ВыбранныйНаборСвойств.Количество() > 0;
	
	Если ПустаяСтрока(ТипНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ТипНоменклатуры, Производитель", ТипНоменклатуры, Производитель);
	Если ВыбранныеСвойства <> Неопределено Тогда
		Отбор.Вставить("ВыбранныеСвойства", ВыбранныеСвойства);
	КонецЕсли;
	
	Ошибка = "";
	РезультатЗапроса = ПолучитьВидыСвойствНаСервере(Отбор, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		
		Элементы.Производитель.СписокВыбора.Очистить();
		Для Каждого Структура ИЗ РезультатЗапроса.Производители Цикл
			
			Элементы.Производитель.СписокВыбора.Добавить(Структура.Производитель.Код, Структура.Производитель.Наименование);
			
			Если Структура.Выбран Тогда
				Производитель = Структура.Производитель.Код;
			КонецЕсли;
			
		КонецЦикла;
		
		//Элементы.МодельНоменклатуры.СписокВыбора.Очистить();
		//Для Каждого Структура ИЗ РезультатЗапроса.МоделиНоменклатуры Цикл
		//	
		//	Элементы.МодельНоменклатуры.СписокВыбора.Добавить(Структура.МодельНоменклатуры.Код, Структура.МодельНоменклатуры.Наименование, Структура.Выбран);
		//	
		//	Если Структура.Выбран Тогда
		//		МодельНоменклатуры = Структура.МодельНоменклатуры.Код;
		//	КонецЕсли;
		//	
		//КонецЦикла;
		
		Для Каждого Структура ИЗ РезультатЗапроса.ВидыСвойств Цикл
			
			ИдентификаторГруппы = "ВидСвойства" + Структура.ВидСвойства.Код;
			
			СтрокаДерева = ПолучитьВеткуДерева(ИдентификаторГруппы, "ДеревоСвойств", "ИдентификаторГруппы");
			СтрокаДерева.ПолучитьЭлементы().Очистить();
			
			СтрокаДерева.ВидСвойства = Структура.ВидСвойства.Наименование;
			СтрокаДерева.ВидСвойстваКод = Структура.ВидСвойства.Код;
			СтрокаДерева.ИдентификаторГруппы = ИдентификаторГруппы;
			
			СтрокаСвойства = ВыбранныйНаборСвойств.НайтиПоЗначению(Структура.ВидСвойства.Код);
			СтрокаДерева.Заблокировано = СтрокаСвойства <> Неопределено И СтрокаСвойства.Пометка;
			
			Если СтрокаДерева.Заблокировано Тогда
				ЕстьЗаблокированныеСвойства = Истина;
			КонецЕсли;
			
			Если Структура.Выбрано Тогда
				
				ЕстьВыбранныеСвойства = Истина;
				
				ЗначениеСвойства = Структура.ЗначенияСвойств[0];
				
				СтрокаДерева.Выбрано = Истина;
				СтрокаДерева.ТипЗначения = Структура.ТипЗначения;
				
				Если Структура.ТипЗначения = "ЗначенияСвойств" Тогда
					СтрокаДерева.Значение = ЗначениеСвойства.Наименование;
					СтрокаДерева.ЗначениеКод = ЗначениеСвойства.Код;
					СтрокаДерева.Примечание = ЗначениеСвойства.Примечание;
				Иначе
					СтрокаДерева.ЗначениеКод = "";
					Если Структура.ТипЗначения = "Число" Тогда
						СтрокаДерева.Значение = Число(ЗначениеСвойства);
					ИначеЕсли Структура.ТипЗначения = "Булево" Тогда
						СтрокаДерева.Значение = Булево(ЗначениеСвойства);
					ИначеЕсли Структура.ТипЗначения = "Строка" Тогда
						СтрокаДерева.Значение = Строка(ЗначениеСвойства);
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				СтрокаДерева.Выбрано = Ложь;
				СтрокаДерева.ТипЗначения = "";
				СтрокаДерева.Значение = "";
				СтрокаДерева.ЗначениеКод = "";
				
				Для Каждого ЗначениеСвойства Из Структура.ЗначенияСвойств Цикл
					
					НоваяСтрока = СтрокаДерева.ПолучитьЭлементы().Добавить();
					
					НоваяСтрока.ВидСвойстваКод = Структура.ВидСвойства.Код;
					НоваяСтрока.ИдентификаторГруппы = ИдентификаторГруппы;
					НоваяСтрока.ТипЗначения = Структура.ТипЗначения;
					
					Если Структура.ТипЗначения = "ЗначенияСвойств" Тогда
						НоваяСтрока.Значение = ЗначениеСвойства.Наименование;
						НоваяСтрока.ЗначениеКод = ЗначениеСвойства.Код;
						НоваяСтрока.Примечание = ЗначениеСвойства.Примечание;
					Иначе
						НоваяСтрока.ЗначениеКод = "";
						Если Структура.ТипЗначения = "Число" Тогда
							НоваяСтрока.Значение = Число(ЗначениеСвойства);
						ИначеЕсли Структура.ТипЗначения = "Булево" Тогда
							НоваяСтрока.Значение = Булево(ЗначениеСвойства);
						ИначеЕсли Структура.ТипЗначения = "Строка" Тогда
							НоваяСтрока.Значение = Строка(ЗначениеСвойства);
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.СохранитьНаборСвойств.Доступность = (ЕстьВыбранныеСвойства = Истина);
	Элементы.ОчиститьНаборСвойств.Видимость = (ЕстьЗаблокированныеСвойства = Истина);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьВыбранныеСвойства()
	
	ВыбранныеСвойства = Неопределено;
	
	Для Каждого СтрокаДерева Из ДеревоСвойств.ПолучитьЭлементы() Цикл
		Если Не ЗначениеЗаполнено(СтрокаДерева.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Если ВыбранныеСвойства = Неопределено Тогда
			ВыбранныеСвойства = Новый Массив;
		КонецЕсли;
		СтруктураСвойства = Новый Структура("ВидСвойства, ВидСвойстваНаименование, ТипЗначения, Значение, ЗначениеПредставление, Предопределенное", СтрокаДерева.ВидСвойстваКод, ""+СтрокаДерева.ВидСвойства, СтрокаДерева.ТипЗначения, "", ""+СтрокаДерева.Значение, СтрокаДерева.Заблокировано);
		Если СтрокаДерева.ТипЗначения = "ЗначенияСвойств" Тогда
			СтруктураСвойства.Значение = СтрокаДерева.ЗначениеКод;
		Иначе
			СтруктураСвойства.Значение = СтрокаДерева.Значение;
		КонецЕсли;
		ВыбранныеСвойства.Добавить(СтруктураСвойства);
	КонецЦикла;
	
	Возврат ВыбранныеСвойства;
	
КонецФункции

#КонецОбласти



#Область УправлениеВидомПоиска

&НаКлиенте
Процедура УправлениеВидомПоиска(НовыйВидПоиска=Неопределено)
	
	ВыбранныйНаборСвойств.Очистить();
	Для Каждого СтрокаДерева Из ДеревоСвойств.ПолучитьЭлементы() Цикл
		СтрокаДерева.Заблокировано = Ложь;
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(НовыйВидПоиска) ИЛИ ВидПоиска <> НовыйВидПоиска Тогда
		Если ЗначениеЗаполнено(НовыйВидПоиска) Тогда
			ВидПоиска = НовыйВидПоиска;
		КонецЕсли;
		Если ВидПоиска = "ПоМоделиАвтомобиля" И Элементы.Каталог.СписокВыбора.Количество() > 0 Тогда
			Элементы.ГруппаПоискПоКаталогам.Видимость = Истина;
			Элементы.ТипНоменклатуры.Видимость = Ложь;
			Элементы.ГруппаПоискПоСвойствам.Отображение = ОтображениеОбычнойГруппы.Нет;
			Элементы.ГруппаПоискПоСвойствам.ОтображатьЗаголовок = Ложь;
		Иначе
			ВидПоиска = "ПоСвойствам";
			Элементы.ГруппаПоискПоКаталогам.Видимость = Ложь;
			Элементы.ТипНоменклатуры.Видимость = Истина;
			Элементы.ГруппаПоискПоСвойствам.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			Элементы.ГруппаПоискПоСвойствам.ОтображатьЗаголовок = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидПоиска = "ПоМоделиАвтомобиля" Тогда
		ИнициализироватьКаталог(Неопределено);
	Иначе
		ИнициализироватьСвойства(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПоискаПриИзменении(Элемент)
	
	УправлениеВидомПоиска();
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогПриИзменении(Элемент)
	
	ИнициализироватьКаталог(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	
	ИнициализироватьСвойства(Неопределено);
	
КонецПроцедуры //ТипНоменклатурыПриИзменении()

#КонецОбласти


&НаКлиенте
Процедура ИнициализироватьПодбор(Знач НовыйИсточник, Знач НовыйОбъект, НовыйКаталог = Неопределено, НовыйТипНоменклатуры = Неопределено)
	
	НовыйВидПоиска = Неопределено;
	
	Если ЗначениеЗаполнено(НовыйКаталог) И Каталог <> НовыйКаталог Тогда
		НовыйВидПоиска = "ПоМоделиАвтомобиля";
		Каталог = НовыйКаталог;
	ИначеЕсли ЗначениеЗаполнено(НовыйТипНоменклатуры) И ТипНоменклатуры <> НовыйТипНоменклатуры Тогда
		НовыйВидПоиска = "ПоСвойствам";
		ТипНоменклатуры = НовыйТипНоменклатуры;
	КонецЕсли;
	
	Если РабочийЛистОбъект <> НовыйОбъект ИЛИ РабочийЛистИсточник <> НовыйИсточник Тогда
		РабочийЛистОбъект = НовыйОбъект;
		РабочийЛистИсточник = НовыйИсточник;
		ЗаполнитьРабочийЛист();
	КонецЕсли;
	
	УправлениеВидомПоиска(НовыйВидПоиска);
	
КонецПроцедуры


&НаКлиенте
Процедура ПроизводительПриИзменении(Элемент)
	
	ТекущаяСтрока = Неопределено;
	ТекущиеДанные = Элементы.ДеревоСвойств.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущаяСтрока = Новый Структура("ИдентификаторГруппы, Значение", ТекущиеДанные.ИдентификаторГруппы, ТекущиеДанные.Значение);
	КонецЕсли;
	
	ЗаполнитьДеревоСвойств(ПолучитьВыбранныеСвойства());
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Для Каждого СтрокаДерева Из ДеревоСвойств.ПолучитьЭлементы() Цикл
			Если СтрокаДерева.ИдентификаторГруппы = ТекущаяСтрока.ИдентификаторГруппы
				И СтрокаДерева.Значение = ТекущаяСтрока.Значение Тогда
				Элементы.ДеревоСвойств.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//ПроизводительКод = "";
	ВыбранныеСвойства = Неопределено;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	
	Если Родитель = Неопределено Тогда
		Если ТекущиеДанные.Заблокировано
			ИЛИ Не ТекущиеДанные.Выбрано Тогда
			Возврат;
		КонецЕсли;
		ТекущиеДанные.Выбрано = Ложь;
		ТекущиеДанные.ТипЗначения = "";
		ТекущиеДанные.Значение = "";
		ТекущиеДанные.ЗначениеКод = "";
		ТекущиеДанные.Примечание = "";
		ТекущиеДанные.Избранное = Ложь;
		
	Иначе
		Если Родитель.Заблокировано
			ИЛИ Родитель.Значение = ТекущиеДанные.Значение
				И Родитель.ЗначениеКод = ТекущиеДанные.ЗначениеКод
				И Родитель.ТипЗначения = ТекущиеДанные.ТипЗначения Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Родитель, ТекущиеДанные);
		Родитель.Выбрано = Истина;
		
	КонецЕсли;
	
	ТекущаяСтрока = Новый Структура("ИдентификаторГруппы, Значение", ТекущиеДанные.ИдентификаторГруппы, ТекущиеДанные.Значение);
	
	ЗаполнитьДеревоСвойств(ПолучитьВыбранныеСвойства());
	
	Для Каждого СтрокаДерева Из ДеревоСвойств.ПолучитьЭлементы() Цикл
		Если СтрокаДерева.ИдентификаторГруппы = ТекущаяСтрока.ИдентификаторГруппы
			И СтрокаДерева.Значение = ТекущаяСтрока.Значение Тогда
			Элементы.ДеревоСвойств.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры //ДеревоСвойствВыбор()

&НаКлиенте
Процедура ЗаполнитьНоменклатуру()
	
	Номенклатура.Очистить();
	
	Элементы.ОтборПроизводительНоменклатуры.СписокВыбора.Очистить();
	Элементы.ОтборМодельНоменклатуры.СписокВыбора.Очистить();
	
	ВыбранныеСвойства = ПолучитьВыбранныеСвойства();
	
	Если ПустаяСтрока(ТипНоменклатуры) ИЛИ ВыбранныеСвойства = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = "";
	Отбор = Новый Структура("ТипНоменклатуры, Производитель, ВыбранныеСвойства, ТолькоВНаличии", ТипНоменклатуры, Производитель, ВыбранныеСвойства, Ложь);
	РезультатЗапроса = ПолучитьНоменклатуруНаСервере(Отбор, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		
		Для Каждого ПозицияИзСервиса ИЗ РезультатЗапроса.Номенклатура Цикл
			
			ПроизводительСтроки = ПолучитьСопоставленнуюСсылкуНаСервере(ПозицияИзСервиса.Производитель,, Истина);
			Если ТипЗнч(ПроизводительСтроки) = Тип("СправочникСсылка.Производители") И ПроизводительСтроки.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаНоменклатуры = Номенклатура.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаНоменклатуры, ПозицияИзСервиса,, "Номенклатура, Производитель");
			
			СтрокаНоменклатуры.ВНаличии = ПозицияИзСервиса.ВНаличии;
			СтрокаНоменклатуры.МодельНоменклатуры = ПозицияИзСервиса.МодельНоменклатуры;
			
			СтрокаНоменклатуры.ПроизводительКод = ПозицияИзСервиса.Производитель.Код;
			Если ЗначениеЗаполнено(ПроизводительСтроки) Тогда
				СтрокаНоменклатуры.ЕстьПроизводитель = Истина;
				СтрокаНоменклатуры.Производитель = ПроизводительСтроки;
			Иначе
				СтрокаНоменклатуры.ЕстьПроизводитель = Ложь;
				СтрокаНоменклатуры.Производитель = ПозицияИзСервиса.Производитель.Наименование;
			КонецЕсли;
			
			Если ТипЗнч(ПозицияИзСервиса.Номенклатура) = Тип("Структура") Тогда
				СтрокаНоменклатуры.НоменклатураКод = ПозицияИзСервиса.Номенклатура.Код;
				СтрокаНоменклатуры.Номенклатура = ПозицияИзСервиса.Номенклатура.Наименование;
				НоменклатураСтроки = ПолучитьСопоставленнуюСсылкуНаСервере(ПозицияИзСервиса.Номенклатура,, Ложь);
			Иначе
				НоменклатураСтроки = Неопределено;
				СтрокаНоменклатуры.Номенклатура = ПозицияИзСервиса.Наименование;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоменклатураСтроки) И ЗначениеЗаполнено(ПроизводительСтроки) И ЗначениеЗаполнено(ПозицияИзСервиса.АртикулДляПоиска) Тогда
				НоменклатураСтроки = НайтиНоменклатуруНаСервере(ПозицияИзСервиса.АртикулДляПоиска, ПроизводительСтроки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НоменклатураСтроки) Тогда
				СтрокаНоменклатуры.Номенклатура = НоменклатураСтроки;
				СтрокаНоменклатуры.ЕстьНоменклатура = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаНоменклатуры.АртикулДляПоиска) Тогда
				Данные = Новый Массив;
				Данные.Добавить(?(ЗначениеЗаполнено(ПроизводительСтроки), ПроизводительСтроки.Код, ПозицияИзСервиса.Производитель.Код));
				Данные.Добавить("_");
				Данные.Добавить(СтрокаНоменклатуры.АртикулДляПоиска);
				Идентификатор = ХешированиеДанныхНаСервере(Данные);
			ИначеЕсли ЗначениеЗаполнено(НоменклатураСтроки) Тогда
				СтрокаНоменклатуры.Номенклатура = НоменклатураСтроки;
				Идентификатор = "" + НоменклатураСтроки.УникальныйИдентификатор();
			Иначе
				// Тяжелый случай идентификатор строки соберем из НоменклатураКод!
				Идентификатор = СтрокаНоменклатуры.НоменклатураКод;
			КонецЕсли;
			СтрокаНоменклатуры.Идентификатор = Идентификатор;
			
			// Теперь надо найти в рабочем листе
			СтрокаНоменклатуры.Пометка = НомераДеталей.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор)).Количество()>0;
			
			Если Элементы.ОтборПроизводительНоменклатуры.СписокВыбора.НайтиПоЗначению(СтрокаНоменклатуры.Производитель) = Неопределено Тогда
				Элементы.ОтборПроизводительНоменклатуры.СписокВыбора.Добавить(СтрокаНоменклатуры.Производитель, ""+СтрокаНоменклатуры.Производитель);
			КонецЕсли;
			
			Если Элементы.ОтборМодельНоменклатуры.СписокВыбора.НайтиПоЗначению(ПозицияИзСервиса.МодельНоменклатуры) = Неопределено Тогда
				Элементы.ОтборМодельНоменклатуры.СписокВыбора.Добавить(ПозицияИзСервиса.МодельНоменклатуры);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.ОтборПроизводительНоменклатуры.СписокВыбора.СортироватьПоПредставлению();
	Элементы.ОтборМодельНоменклатуры.СписокВыбора.СортироватьПоЗначению();
	
	Если Элементы.ОтборПроизводительНоменклатуры.СписокВыбора.Количество() = 1 Тогда
		ОтборПроизводительНоменклатуры = Элементы.ОтборПроизводительНоменклатуры.СписокВыбора[0].Значение;
		ОтборПроизводительНоменклатурыПриИзменении(Неопределено);
	Иначе
		Если ЗначениеЗаполнено(ОтборПроизводительНоменклатуры) И Элементы.ОтборПроизводительНоменклатуры.СписокВыбора.НайтиПоЗначению(ОтборПроизводительНоменклатуры) = Неопределено Тогда
			ОтборПроизводительНоменклатуры = "";
			ОтборПроизводительНоменклатурыПриИзменении(Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ОтборМодельНоменклатуры.СписокВыбора.Количество() = 1 Тогда
		ОтборМодельНоменклатуры = Элементы.ОтборМодельНоменклатуры.СписокВыбора[0].Значение;
		ОтборМодельНоменклатурыПриИзменении(Неопределено);
	Иначе
		Если ЗначениеЗаполнено(ОтборМодельНоменклатуры) И Элементы.ОтборМодельНоменклатуры.СписокВыбора.НайтиПоЗначению(ОтборМодельНоменклатуры) = Неопределено Тогда
			ОтборМодельНоменклатуры = "";
			ОтборМодельНоменклатурыПриИзменении(Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //ЗаполнитьНоменклатуру()


&НаКлиенте
Функция СформироватьСписокВыбораНаборов(Знач Идентификатор, ПредлагаемоеИмя)
	
	Если Не ЗначениеЗаполнено(РабочийЛистОбъект) Тогда
		Возврат Новый СписокЗначений;
	КонецЕсли;
	
	СписокВыбора = СписокВыбораНаборов.Скопировать();
	
	Для Каждого ЭлементСписка Из СписокВыбора Цикл
		Если ЭлементСписка.Значение = "" Тогда
			ЭлементСписка.Представление = "< Не указано >";
		КонецЕсли;
		ЭлементСписка.Пометка = НомераДеталей.НайтиСтроки(Новый Структура("ИмяНабора, Идентификатор, Автор", ЭлементСписка.Значение, Идентификатор, ПараметрыСеанса.Пользователь)).Количество() > 0;
	КонецЦикла;
	
	ИмяНовогоНабора = Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd");
	
	ПредлагаемоеИмя = ИмяНовогоНабора; 
	
	//Сч = 0;
	//Пока СписокВыбора.НайтиПоЗначению(ПредлагаемоеИмя) <> Неопределено Цикл
	//	Сч = Сч+1;
	//	ПредлагаемоеИмя = ИмяНовогоНабора + " (" + Сч +")"; 
	//КонецЦикла;
	
	СписокВыбора.Добавить(ПредлагаемоеИмя, "в < Новый набор >", Ложь);
	
	Возврат СписокВыбора;
	
КонецФункции

&НаКлиенте
Процедура НоменклатураВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекСтрока = Элемент.ТекущиеДанные;
	
	ИмяНовогоНабора = "";
	
	СписокВыбора = СформироватьСписокВыбораНаборов(ТекСтрока.Идентификатор, ИмяНовогоНабора);
	
	//Если СписокВыбора.Количество() = 1 Тогда
	//	ВыбранныйПункт = СписокВыбора[0];
	//Иначе
		ВыбранныйПункт = ВыбратьИзМеню(СписокВыбора, ТекущийЭлемент);
		Если ВыбранныйПункт = Неопределено Тогда
			Возврат;
		КонецЕсли;
	//КонецЕсли;
	
	//Если ВыбранныйПункт.Значение = "ОткрытьКарточку" Тогда
	//	
	//	ОткрытьОбъектНоменклатура(ТекСтрока, Ложь);
	//	
	//ИначеЕсли ВыбранныйПункт.Значение = "ОткрытьКарточкуПроизводителя" Тогда
	//	
	//	ОткрытьОбъектНоменклатура(ТекСтрока, Ложь);
	//	
	//ИначеЕсли ВыбранныйПункт.Значение = "ОткрытьНоменклатуру" Тогда
	//	
	//	ОткрытьОбъектНоменклатура(ТекСтрока, Истина);
	//	
	//Иначе
		Если ВыбранныйПункт.Пометка Тогда
			
			УдалитьНомерДеталиНаКлиенте(ВыбранныйПункт.Значение, ТекСтрока.Идентификатор);
			
			Если НомераДеталей.НайтиСтроки(Новый Структура("Идентификатор", ТекСтрока.Идентификатор)).Количество() = 0 Тогда
				СтрокиНоменклатуры = Номенклатура.НайтиСтроки(Новый Структура("Идентификатор", ТекСтрока.Идентификатор));
				Для Каждого СтрокаНоменклатуры Из СтрокиНоменклатуры Цикл
					СтрокаНоменклатуры.Пометка = Ложь;
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			
			ИмяНабора = ВыбранныйПункт.Значение;
			
			Если ИмяНабора = ИмяНовогоНабора Тогда
				#Если ТонкийКлиент Тогда
					
				#Иначе
					Если Не ВвестиСтроку(ИмяНабора, "Имя нового набора", 100, Ложь) Тогда
						Возврат;
					КонецЕсли;
				#КонецЕсли
			КонецЕсли;
			
			ДобавитьНомерДеталиНаКлиенте(ВыбранныйПункт.Значение, ТекСтрока, 1);
			ТекСтрока.Пометка = Истина;
			
		КонецЕсли;
	//КонецЕсли;
	
КонецПроцедуры //ПодобраннаяНоменклатураВыбор()

&НаКлиенте
Процедура ОтборМодельНоменклатурыПриИзменении(Элемент)
	
	ОтборСтрок = Новый Структура(Элементы.Номенклатура.ОтборСтрок);
	
	Если ЗначениеЗаполнено(ОтборМодельНоменклатуры) Тогда
		ОтборСтрок.Вставить("МодельНоменклатуры", ОтборМодельНоменклатуры);
	Иначе
		ОтборСтрок.Удалить("МодельНоменклатуры");
	КонецЕсли;
	
	Элементы.Номенклатура.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПроизводительНоменклатурыПриИзменении(Элемент)
	
	ОтборСтрок = Новый Структура(Элементы.Номенклатура.ОтборСтрок);
	
	Если ЗначениеЗаполнено(ОтборПроизводительНоменклатуры) Тогда
		ОтборСтрок.Вставить("Производитель", ОтборПроизводительНоменклатуры);
	Иначе
		ОтборСтрок.Удалить("Производитель");
	КонецЕсли;
	
	Элементы.Номенклатура.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);
	
КонецПроцедуры


&НаКлиенте
Процедура ТолькоИзСправочникаПриИзменении(Элемент)
	
	ОтборСтрок = Новый Структура(Элементы.Номенклатура.ОтборСтрок);
	
	Если ТолькоИзСправочника Тогда
		ОтборСтрок.Вставить("ЕстьНоменклатура", Истина);
	Иначе
		ОтборСтрок.Удалить("ЕстьНоменклатура");
	КонецЕсли;
	
	Элементы.Номенклатура.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);
	
КонецПроцедуры


&НаКлиенте
Процедура ТолькоВНаличииПриИзменении(Элемент)
	
	ОтборСтрок = Новый Структура(Элементы.Номенклатура.ОтборСтрок);
	
	Если ТолькоВНаличии Тогда
		ОтборСтрок.Вставить("ВНаличии", Истина);
	Иначе
		ОтборСтрок.Удалить("ВНаличии");
	КонецЕсли;
	
	Элементы.Номенклатура.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНаборСвойств(Команда)
	ВыбранныйНаборСвойств.Очистить();
	Для Каждого СтрокаДерева Из ДеревоСвойств.ПолучитьЭлементы() Цикл
		СтрокаДерева.Заблокировано = Ложь;
	КонецЦикла;
	//Элементы.УдалитьНаборСвойств.Доступность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектПроизводитель(ТекСтрока)
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекСтрока.ПроизводительКод) Тогда
		
		ОткрытьФорму("Обработка.атПомощникПродаж.Форма.ОбъектПроизводитель",,, "ОбъектПроизводитель");
		Оповестить("атПомощникПродаж_ОбъектПроизводитель_ПриОткрытии", ТекСтрока.ПроизводительКод);
		
	ИначеЕсли ТекСтрока.ЕстьПроизводитель Тогда
		
		ОткрытьЗначение(ТекСтрока.Производитель);
		
	Иначе
		Предупреждение("Нет информации для открытия карточки!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектНоменклатура(ТекСтрока, ОткрыватьНоменклатуру=Истина)
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекСтрока.ЕстьНоменклатура И ОткрыватьНоменклатуру Тогда
		
		ОткрытьЗначение(ТекСтрока.Номенклатура);
		
	ИначеЕсли ЗначениеЗаполнено(ТекСтрока.НоменклатураКод) ИЛИ (ЗначениеЗаполнено(ТекСтрока.Артикул) И ЗначениеЗаполнено(ТекСтрока.ПроизводительКод)) Тогда
		
		ПараметрыФормы = Новый Структура("ЭтоСсылка, Тип, Код, ВладелецТип, ВладелецКод, Наименование, Артикул, Производитель", Истина, "Номенклатура", ТекСтрока.НоменклатураКод, "", "", ""+ТекСтрока.Номенклатура, ТекСтрока.Артикул, ТекСтрока.ПроизводительКод);
		ОткрытьФорму("Обработка.атПомощникПродаж.Форма.ОбъектНоменклатура",, ЭтаФорма, "ОбъектНоменклатура");
		Оповестить("атПомощникПродаж_ОбъектНоменклатура_ПриОткрытии", ПараметрыФормы);
		
	Иначе
		Предупреждение("Нет информации для открытия карточки!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	ОткрытьОбъектНоменклатура(Элементы.Номенклатура.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = Неопределено Тогда
		Производитель = "";
	Иначе
		Производитель = ВыбранноеЗначение;
	КонецЕсли;
	ПроизводительПриИзменении(Неопределено);
КонецПроцедуры




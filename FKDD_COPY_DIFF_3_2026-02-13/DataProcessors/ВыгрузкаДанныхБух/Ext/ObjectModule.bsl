// Модуль обработки ВыгрузкаДанныхБух 2004 - 2005 (CheR, SirK 08.06.2005), 2007 PoAr, 2008 - 2009 OvcM

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем ДокXML;             // XML файл-выгрузки
Перем Текст;              // Текстовый файл выгрузки
Перем тзЭлементов;        // Таблица значений выгружаемых элементов справочника
Перем тзДоков;            // Таблица значений выгружаемых документов
Перем ПервыйОбъект;       // признак первого объекта
Перем спТаблицВыгрузки;   // Идентификаторы табличных частей для выгрузки
Перем ОбщееОписание;      // Буферизированная строка для записи в текстовый файл
Перем КореньСправочники;  // Корень дерева выгруженных объектов для справочников
Перем КореньДокументы;    // Корень дерева выгруженных объектов для документов
Перем спТаблицВыгрузкиГТД Экспорт; // Список документов, для которых в ТЧ, необходимо добавить реквизит ГТД при выгрузке

Перем спТаблицВыгрузкиГТДДоп Экспорт; // Список документов, для которых в ТЧ, необходимо добавить реквизит ГТД при выгрузке


Перем кАвтосервис Экспорт; // Признак того, что конфигурация Автосалон или Автосервис
Перем кУР Экспорт;		  // Признак того, что конфигурация Управление рестораном	
Перем кАмбулатория Экспорт;		  // Признак того, что конфигурация Амбулатория	
Перем спДокументовБезРегл Экспорт; // СписокДокументов, которые нужно выгрузить, но у которых нет реквизита регл. учет.
Перем спДокументовСпец Экспорт;   	// Список документов с особенностями, которые нельзя включить в общие группы выборки
Перем тзДвиженияКВыгрузке; //Таблица содержащая движения которые нужно выгружать. Движения партий выгружается всегда.
Перем спДокументыСОснованием Экспорт; //Список документов, у которых обязательно выгружать основание
Перем спСправочникиКонтИнф Экспорт; //Список справочников, для которых необходимо выгружать контактную информацию
Перем спСправочникиСпецРеквизиты Экспорт; //Список справочников, для которых надо выгрузить специальную дополнительную информацию
Перем спНеВыгружать Экспорт; // Список документов, которые не будут выгруженны
Перем ИмяФайла Экспорт; // Имя файла для выгрузки
Перем ИмяКонфигурации; // Имя конфигурации
Перем ОбщиеТипы Экспорт; // Типы общие для различных конфигураций
Перем ТипыАльфа Экспорт; // Типы из Альфы
Перем ЕстьТаблицаСписанийАвтомобилей Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция получает список документов для выгрузки из конфигурации "Амбулатория"
Функция ПолучитьДокументыАмбулатории()
	
	сзДокументы = Новый СписокЗначений;
	сзДокументы.Добавить("Вакцинация");
	сзДокументы.Добавить("МедицинскийОсмотр");
	сзДокументы.Добавить("ПриемСпециалиста");
	сзДокументы.Добавить("ПроцедурноОперационнаяКарта");
	сзДокументы.Добавить("ДиагностическиеИсследования");
	сзДокументы.Добавить("ТалонАмбулаторногоПациента");
	сзДокументы.Добавить("ОбращениеГоспитализация");
	сзДокументы.Добавить("ВыбытиеИзСтационара");
	сзДокументы.Добавить("ЗачетОказанныхУслуг");
	Возврат сзДокументы;
	
КонецФункции //ПолучитьДокументыАмбулатории() 

//Функция возвращает Таблицу списания номенклатуры с номерами ГТД,
//с учетом партий, по которым происходят движения документа-основания
Функция СформироватьТЧ_С_ГТД(Основание)
	
	Если кУР Тогда 
		Товары = Новый ТаблицаЗначений();
	Иначе	
		
		Запрос = Новый Запрос();
		ИмяПартионногоРегистра 		= "ПартииТоваровКомпании";
	    ДокументОснованиеМетаданные = Основание.Метаданные();
		ИмяДокументаОснования 		= ДокументОснованиеМетаданные.Имя;
	    ЗнакДвиженияПоПартиям 		= ?(ИмяДокументаОснования = "Инвентаризация","-","");
	    РеквизитНоменклатура = ?(ИмяДокументаОснования = "ПересортицаТоваров","НоменклатураРасход","Номенклатура");

		Запрос.Текст = "ВЫБРАТЬ
									|	ТабличнаяЧастьТовары.Номенклатура,
									|	" + ЗнакДвиженияПоПартиям + "ТабличнаяЧастьТовары.Количество КАК Количество,
									|	ТабличнаяЧастьТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
									|	ТабличнаяЧастьТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмеренияПартии,
									|	ТабличнаяЧастьТовары.Коэффициент КАК Коэффициент,
									|	ТабличнаяЧастьТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК КоэффициентПартии,
									|	ТабличнаяЧастьТовары.Ссылка,
									|	ТабличнаяЧастьТовары.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
									|	ПартииТоваровСГТД.Партия КАК Партия,
									|	ПартииТоваровСГТД.ГТД КАК ГТД,
									|	ЕстьNull(ПартииТоваровСГТД.Количество,0) КАК КоличествоПартии,
									|   ЕстьNull(ПартииТоваровСГТД.КоличествоСГТД,0) КАК КоличествоСГТД
									|
									|ИЗ (ВЫБРАТЬ 
									|	ТабличнаяЧастьТоварыИсходная." + РеквизитНоменклатура + " КАК Номенклатура,
									|	СУММА(ТабличнаяЧастьТоварыИсходная.Количество) КАК Количество,
									|	ТабличнаяЧастьТоварыИсходная.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
									|	ТабличнаяЧастьТоварыИсходная.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмеренияПартии,
									|	ТабличнаяЧастьТоварыИсходная.Коэффициент КАК Коэффициент,
									|	ТабличнаяЧастьТоварыИсходная.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК КоэффициентПартии,
									|	ТабличнаяЧастьТоварыИсходная.Ссылка,
									|	МИНИМУМ(ТабличнаяЧастьТоварыИсходная.НомерСтроки) КАК НомерСтрокиДокумента
									|ИЗ
									|	Документ." + ИмяДокументаОснования + ".Товары КАК ТабличнаяЧастьТоварыИсходная
									|ГДЕ
									|	ТабличнаяЧастьТоварыИсходная.Ссылка = &ДокументОснование
									|СГРУППИРОВАТЬ ПО
									|	ТабличнаяЧастьТоварыИсходная." + РеквизитНоменклатура + ",
									|	ТабличнаяЧастьТоварыИсходная.ЕдиницаИзмерения,
									|	ТабличнаяЧастьТоварыИсходная.Номенклатура.ОсновнаяЕдиницаИзмерения,
									|	ТабличнаяЧастьТоварыИсходная.Коэффициент,
									|	ТабличнаяЧастьТоварыИсходная.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент,
									|	ТабличнаяЧастьТоварыИсходная.Ссылка
									|) КАК ТабличнаяЧастьТовары
									|	
									|ЛЕВОЕ СОЕДИНЕНИЕ (
									|
									|ВЫБРАТЬ
									|	ПартииТоваровКомпании.Номенклатура,
									|	ПартииТоваровКомпании.Партия,
									|	ВЫБОР
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПоступлениеТоваров ТОГДА
									|			ПоступлениеТоваровТовары.ГТД
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ВводОстатковТоваров ТОГДА
									|			ВводОстатковТоваровТовары.ГТД
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.Инвентаризация ТОГДА
									|			ИнвентаризацияТовары.ГТД
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПеремещениеТоваров ТОГДА
									|			ПеремещениеТоваровТовары.ГТД
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПересортицаТоваров ТОГДА
									|			ПересортицаТоваровТовары.ГТД
									|		ИНАЧЕ
									|			NULL
									|	КОНЕЦ КАК ГТД,
									|	ВЫБОР
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПоступлениеТоваров ТОГДА
									|			ПоступлениеТоваровТовары.Количество*ПоступлениеТоваровТовары.Коэффициент
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ВводОстатковТоваров ТОГДА
									|			ВводОстатковТоваровТовары.Количество*ВводОстатковТоваровТовары.Коэффициент
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.Инвентаризация ТОГДА
									|			ИнвентаризацияТовары.Количество*ИнвентаризацияТовары.Коэффициент
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПеремещениеТоваров ТОГДА
									|			ПеремещениеТоваровТовары.Количество*ПеремещениеТоваровТовары.Коэффициент
									|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПересортицаТоваров ТОГДА
									|			ПересортицаТоваровТовары.Количество*ПересортицаТоваровТовары.Коэффициент
									|		ИНАЧЕ
									|			0
									|	КОНЕЦ КАК КоличествоСГТД,
									|	ПартииТоваровКомпании.Количество КАК Количество
									|ИЗ 
									|   РегистрНакопления."+ИмяПартионногоРегистра+" КАК ПартииТоваровКомпании
									|	
									|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
									|		ПО ПартииТоваровКомпании.Партия = ПоступлениеТоваровТовары.Ссылка 
									|		И ПартииТоваровКомпании.Номенклатура = ПоступлениеТоваровТовары.Номенклатура 
									|		И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры
									|	
									|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковТоваров.Товары КАК ВводОстатковТоваровТовары
									|		ПО ПартииТоваровКомпании.Партия = ВводОстатковТоваровТовары.Ссылка 
									|		И ПартииТоваровКомпании.Номенклатура = ВводОстатковТоваровТовары.Номенклатура 
									|		И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ВводОстатковТоваровТовары.ХарактеристикаНоменклатуры
									|
									|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
									|		ПО ПартииТоваровКомпании.Партия = ИнвентаризацияТовары.Ссылка 
									|		И ПартииТоваровКомпании.Номенклатура = ИнвентаризацияТовары.Номенклатура 
									|		И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ИнвентаризацияТовары.ХарактеристикаНоменклатуры
									|
									|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
									|		ПО ПартииТоваровКомпании.Партия = ПеремещениеТоваровТовары.Ссылка 
									|		И ПартииТоваровКомпании.Номенклатура = ПеремещениеТоваровТовары.Номенклатура 
									|		И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры
									|
									|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
									|		ПО ПартииТоваровКомпании.Партия = ПересортицаТоваровТовары.Ссылка 
									|		И ПартииТоваровКомпании.Номенклатура = ПересортицаТоваровТовары.Номенклатура 
									|		И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ПересортицаТоваровТовары.ХарактеристикаНоменклатуры
									|
									|ГДЕ
									|	(ПартииТоваровКомпании.Регистратор = &ДокументОснование) И (ПартииТоваровКомпании.ВидДвижения = &ВидДвиженияПоПартиям)
									|	И(ПартииТоваровКомпании.Количество) > 0 
									|
									|) КАК ПартииТоваровСГТД
									|	ПО ТабличнаяЧастьТовары.Номенклатура = ПартииТоваровСГТД.Номенклатура
									|
									|УПОРЯДОЧИТЬ ПО
									|ТабличнаяЧастьТовары.НомерСтрокиДокумента
									|";
									
		ЗнакДвиженияПоПартиям 		= "";
		ИмяПартионногоРегистра 		= "ПартииТоваровКомпании";
	    ДокументОснованиеМетаданные = Основание.Метаданные();
		ИмяДокументаОснования 		= ДокументОснованиеМетаданные.Имя;
	    Запрос.УстановитьПараметр("ВидДвиженияПоПартиям",ВидДвиженияНакопления.Расход);	
		Запрос.УстановитьПараметр("ЗнакДвиженияПоПартиям", ЗнакДвиженияПоПартиям);	
	    Запрос.УстановитьПараметр("ИмяПартионногоРегистра", ИмяПартионногоРегистра);
		
		Запрос.УстановитьПараметр("ДокументОснование",Основание);
		Выборка = Запрос.Выполнить().Выгрузить();
		МассивТиповН = Новый Массив;
		МассивТиповН.Добавить(Тип("СправочникСсылка.Номенклатура"));
		МассивТиповГТД = Новый Массив;
		МассивТиповГТД.Добавить(Тип("СправочникСсылка.ГТД"));
		ЧисловойТип = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 3));
		Товары = Новый ТаблицаЗначений();
		Товары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов(МассивТиповН));
		Товары.Колонки.Добавить("Количество", ЧисловойТип);
		Товары.Колонки.Добавить("Коэффициент",ЧисловойТип);
		Товары.Колонки.Добавить("Сумма", ЧисловойТип);
		Товары.Колонки.Добавить("ГТД", Новый ОписаниеТипов(МассивТиповГТД));
		ТекНомерСтроки = 0;
		СтрокаТоваров=Неопределено;
		ОсталосьКоличествоПоТекНоменклатура = 0;
		ОсталосьСуммаВсегоПоТекНоменклатура = 0;
		ОсталосьСуммаНДСПоТекНоменклатура = 0;
		Для каждого СтрокаВыборки Из Выборка Цикл
			Если СтрокаВыборки.НомерСтрокиДокумента <> ТекНомерСтроки Тогда
				Если СтрокаТоваров<>Неопределено И ОсталосьКоличествоПоТекНоменклатура<>0 Тогда
					СтрокаТоваров.Количество=СтрокаТоваров.Количество+ОсталосьКоличествоПоТекНоменклатура;
				КонецЕсли; 
				СтрокаТоваровСуммаВсего=0; СтрокаТоваровСуммаНДС=0;
				ТекНомерСтроки = СтрокаВыборки.НомерСтрокиДокумента;
				СтрокаТоваров=Неопределено;
				ОсталосьКоличествоПоТекНоменклатура = СтрокаВыборки.Количество*СтрокаВыборки.Коэффициент;
				БазовоеКоличество = СтрокаВыборки.Количество*СтрокаВыборки.Коэффициент;
			КонецЕсли;
			Если ОсталосьКоличествоПоТекНоменклатура <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			Если СтрокаВыборки.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
				КоличествоВДокумент = ОсталосьКоличествоПоТекНоменклатура;
			Иначе
				Если СтрокаВыборки.КоличествоПартии<=0 Тогда
					Продолжить;
				КонецЕсли; 
				Если СтрокаВыборки.ГТД<>NULL И СтрокаВыборки.КоличествоСГТД<=0 Тогда
					Продолжить;
				КонецЕсли; 
				Если СтрокаВыборки.ГТД=NULL Тогда
					КоличествоВДокумент = МИН(СтрокаВыборки.КоличествоПартии,ОсталосьКоличествоПоТекНоменклатура);
				Иначе
					КоличествоВДокумент = МИН(СтрокаВыборки.КоличествоПартии,СтрокаВыборки.КоличествоСГТД,ОсталосьКоличествоПоТекНоменклатура);
				КонецЕсли; 
			КонецЕсли;
			СтрокиПартии=Выборка.НайтиСтроки(Новый Структура("Номенклатура,Партия",СтрокаВыборки.Номенклатура,СтрокаВыборки.Партия));
			Для каждого СтрокаПартии Из СтрокиПартии Цикл
				СтрокаПартии.КоличествоПартии=СтрокаПартии.КоличествоПартии-КоличествоВДокумент;
				Если СтрокаВыборки.ГТД=СтрокаПартии.ГТД Тогда
					СтрокаПартии.КоличествоСГТД=СтрокаПартии.КоличествоСГТД-КоличествоВДокумент;
				КонецЕсли; 
			КонецЦикла;
			ОсталосьКоличествоПоТекНоменклатура = ОсталосьКоличествоПоТекНоменклатура - КоличествоВДокумент;
			НайденныеСтроки=Товары.НайтиСтроки(Новый Структура("Номенклатура,ГТД",СтрокаВыборки.Номенклатура,СтрокаВыборки.ЕдиницаИзмерения,?(СтрокаВыборки.ГТД=NULL,Справочники.ГТД.ПустаяСсылка(),СтрокаВыборки.ГТД)));
			Если НайденныеСтроки.Количество()=0 Тогда
				СтрокаТоваров=Товары.Добавить();
			Иначе
				СтрокаТоваров=НайденныеСтроки[0];
			КонецЕсли;
				СтрокаТоваров.Номенклатура=СтрокаВыборки.Номенклатура;
				СтрокаТоваров.Количество=СтрокаТоваров.Количество+КоличествоВДокумент/СтрокаВыборки.Коэффициент;
				СтрокаТоваров.ГТД=?(СтрокаВыборки.ГТД=NULL,Справочники.ГТД.ПустаяСсылка(),СтрокаВыборки.ГТД);
		КонецЦикла;
		Если СтрокаТоваров<>Неопределено Тогда
			СтрокаТоваров.Количество=СтрокаТоваров.Количество+ОсталосьКоличествоПоТекНоменклатура;
		КонецЕсли; 
    КонецЕсли;
		
	Возврат Товары;
	
КонецФункции //СформироватьТЧ_С_ГТД	

//Функция возвращает строку из таблицы тзДвиженияКВыгрузке, если находит в ней переданные имена регистра и документа
Функция СтрокаВыгрузитьДвижения(ИмяРегистра,ИмяДокумента,ИмяКонфигурации="")
	Строка = тзДвиженияКВыгрузке.Найти(ИмяРегистра,"Наименование");
	Если Строка <> Неопределено Тогда
		спДокументы = Строка.Документы;
		Если НЕ (спДокументы = Неопределено ИЛИ спДокументы.Количество() = 0) и спДокументы.НайтиПоЗначению(ИмяДокумента) = Неопределено Тогда
			Строка = Неопределено;
		Иначе
			Если ИмяКонфигурации <> "" И Найти(Метаданные.Имя,Строка.Конфигурация)=0 Тогда
				Строка = Неопределено;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	Возврат Строка;
КонецФункции

Функция ДобавлятьДокументыПоСсылкам(ИмяДокумента)
КонецФункции

// Возвращает строку - внутреннее представление переданного значения
Функция ПредставлениеРеквизита(Знач ЗнРекв, УказыватьТип=Ложь) Экспорт
	
	// Работаем только с ссылочными типами
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗнРекв)) или
		 Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗнРекв)) или
		 Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗнРекв)) Тогда
		 
		// Для пустых значений
		Если ЗнРекв = Неопределено Тогда ЗнРекв = ?(XMLФорматВыгрузки, "", """""");
		ИначеЕсли ЗнРекв.Пустая() Тогда ЗнРекв = ?(XMLФорматВыгрузки, "", """""");
		
		// Для перечислений	вернем полное имя + значение перечисления
		ИначеЕсли Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗнРекв)) Тогда
		    МД = ЗнРекв.Метаданные();
		    ЗнРекв = МД.ПолноеИмя() + "." + МД.ЗначенияПеречисления[Перечисления[МД.Имя].Индекс(ЗнРекв)].Имя;
			
			Если НЕ XMLФорматВыгрузки Тогда ЗнРекв = """" + ЗнРекв + """"; КонецЕсли;
			
		Иначе // Для остальных типов - уникальный идентификатор
			
			СтрТипа = "";
			Если УказыватьТип Тогда
				Тип = ТипЗнч(ЗнРекв);
				//Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда СтрТипа = "Справочник." + ЗнРекв.Метаданные().Имя + ".";
				//ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда СтрТипа = "Документ." + ЗнРекв.Метаданные().Имя + ".";
				Если Перечисления.ТипВсеСсылки().СодержитТип(Тип) Тогда СтрТипа = "Перечисление." + ЗнРекв.Метаданные().Имя + ".";
				КонецЕсли;
			КонецЕсли;
			
			ЗнРекв = СтрТипа + ЗнРекв.УникальныйИдентификатор();
			
			Если НЕ XMLФорматВыгрузки Тогда ЗнРекв = """" + ЗнРекв + """"; КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЗнРекв) = Тип("Число") Тогда
		ЗнРекв = Формат(ЗнРекв, "ЧГ=0;ЧРД='.';ЧН=");
	
	Иначе // ну и для всех остальных вернем текст, лишенный "мешающих" символов
		ЗнРекв = СокрЛП(СтрЗаменить(ЗнРекв, Символы.ПС, "‡"));
		ЗнРекв = СокрЛП(СтрЗаменить(ЗнРекв, Символы.ВК, "±"));
		ЗнРекв = СокрЛП(СтрЗаменить(ЗнРекв, """", "¤"));
		ЗнРекв = СокрЛП(СтрЗаменить(ЗнРекв, ",", "†"));
		
		Если НЕ XMLФорматВыгрузки Тогда ЗнРекв = """" + ЗнРекв + """"; КонецЕсли;
	КонецЕсли;
	
	Возврат ЗнРекв;
	
КонецФункции	//	ПредставлениеРеквизита()

// Процедура поиска данных ссылочного типа в обрабатываемом объекте
Процедура ДобавитьПоСсылкам(Знач ЗнРекв, Знач Уровень=1, Владелец) Экспорт
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли

	// Проверим, может хватит "углубляться" по ссылкам?
	Если Уровень > 99 Тогда Возврат; КонецЕсли;
	
	// Работаем только со справочниками и документами                                                           ПолучитьИмяПредопределенного
	ТекТипЗн = ТипЗнч(ЗнРекв);
	Если ЗнРекв = Неопределено Тогда Возврат; КонецЕсли;
	Если (НЕ Справочники.ТипВсеСсылки().СодержитТип(ТекТипЗн)) и (НЕ Документы.ТипВсеСсылки().СодержитТип(ТекТипЗн)) Тогда Возврат; КонецЕсли;
	Если ЗнРекв.Пустая() Тогда Возврат; КонецЕсли;
	Если спНеВыгружать.Найти(ТекТипЗн) <> Неопределено Тогда Возврат; КонецЕсли;
	Если ТипЗнч(Владелец)=Тип("ДокументСсылка.ЗаказНаряд") И ТекТипЗн=Тип("СправочникСсылка.Автомобили") И НЕ (Справочники.ВидыРемонта.ПолучитьИмяПредопределенного(Владелец.ВидРемонта)="КомплектацияАвтомобиля" ИЛИ Владелец.ВидРемонта.НаСебестоимость) Тогда
		Возврат;
	КонецЕсли;
	// Отсечем элементы/документы другой организации
	Если НЕ ВыбОрганизация.Пустая() Тогда
		Попытка
			Если ЗнРекв.Организация <> ВыбОрганизация И  Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗнРекв)) Тогда
				Возврат;
			КонецЕсли;
		Исключение КонецПопытки;	
	КонецЕсли;
	
	// Добавим сам объект для которого ищем ссылки в таблицу выгружаемых объектов
	Если Документы.ТипВсеСсылки().СодержитТип(ТекТипЗн) Тогда
		//Если заполнена датаа ввода остатков то отсекаем все документы ранее этой даты
		Если ЗначениеЗаполнено(ДатаВводаОстатковВБухгалтерии) И ЗнРекв.Дата < ДатаВводаОстатковВБухгалтерии Тогда
			Возврат;
		КонецЕсли;
		//Если у документа нет реквизита РегламентированныйУчет, то мы его не выгружаем.
		Если НЕ дкДокументЕстьРеквизитШапки("РегламентированныйУчет", ЗнРекв.Метаданные().Имя) 
			И спДокументовБезРегл.НайтиПоЗначению(ЗнРекв.Метаданные().Имя) = Неопределено
			И спДокументовСпец.НайтиПоЗначению(ЗнРекв.Метаданные().Имя) = Неопределено Тогда 
			Возврат; 
		КонецЕсли;					
		ТекСтрока = тзДоков.Найти(ЗнРекв, "Ссылка");
		Если ТекСтрока <> Неопределено Тогда 
			Если ТекСтрока.Уровень > Уровень Тогда ТекСтрока.Уровень = Уровень; КонецЕсли;
			Возврат; 
		КонецЕсли;
		ТекСтрока = тзДоков.Добавить();
		Если ПоказыватьДеревоСсылок Тогда
			//Работа с деревом ссылок
			СтрокаРодитель = ДеревоСсылок.Строки.Найти(Владелец, "Ссылка", Истина);
			Если СтрокаРодитель = Неопределено Тогда
				НовСтрокаДерева = ДеревоСсылок.Строки.Добавить();
				НовСтрокаДерева.Ссылка = ЗнРекв;
				НовСтрокаДерева.ТипЗначения = ЗнРекв.Метаданные().ПолноеИмя();
			ИначеЕсли Владелец <> ЗнРекв Тогда
				НовСтрокаДерева = СтрокаРодитель.Строки.Добавить();
				НовСтрокаДерева.Ссылка = ЗнРекв;
				НовСтрокаДерева.ТипЗначения = ЗнРекв.Метаданные().ПолноеИмя();
			КонецЕсли;
		КонецЕсли;
	Иначе
		ТекСтрока = тзЭлементов.Найти(ЗнРекв, "Ссылка");
		Если ТекСтрока <> Неопределено Тогда
			Если ТекСтрока.Уровень > Уровень и ЗнРекв.ЭтоГруппа Тогда ТекСтрока.Уровень = Уровень; КонецЕсли;
			Возврат;
		КонецЕсли;
		ТекСтрока = тзЭлементов.Добавить();
		Если ПоказыватьДеревоСсылок Тогда
			//Работа с деревом ссылок
			СтрокаРодитель = ДеревоСсылок.Строки.Найти(Владелец, "Ссылка", Истина);
			Если СтрокаРодитель = Неопределено Тогда
				НовСтрокаДерева = ДеревоСсылок.Строки.Добавить();
				НовСтрокаДерева.Ссылка = ЗнРекв;
				НовСтрокаДерева.ТипЗначения = ЗнРекв.Метаданные().ПолноеИмя();
			ИначеЕсли Владелец <> ЗнРекв Тогда
				НовСтрокаДерева = СтрокаРодитель.Строки.Добавить();
				НовСтрокаДерева.Ссылка = ЗнРекв;
				НовСтрокаДерева.ТипЗначения = ЗнРекв.Метаданные().ПолноеИмя();
			КонецЕсли;
		КонецЕсли;
		
		// Для справочников, обязательно выгрузим владельца и родителя
		Если ЗнРекв.Владелец <> Неопределено Тогда
			Если НЕ ЗнРекв.Владелец.Пустая() Тогда
				ДобавитьПоСсылкам(ЗнРекв.Владелец, Уровень-1, ЗнРекв);
			КонецЕсли;
		КонецЕсли;
		Если ЗнРекв.Родитель <> Неопределено Тогда
			Если НЕ ЗнРекв.Родитель.Пустая() Тогда
				ДобавитьПоСсылкам(ЗнРекв.Родитель, Уровень-1, ЗнРекв);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекСтрока.Тип     = ТипЗнч(ЗнРекв);
	ТекСтрока.Уровень = Уровень;
	ТекСтрока.Ссылка  = ЗнРекв;
	
	Уровень = Уровень + 1;
	
	// Пройдемся по реквизитам объекта
	Для Каждого Рекв Из ЗнРекв.Метаданные().Реквизиты Цикл
		ДобавитьПоСсылкам(ЗнРекв[Рекв.Имя], Уровень, ЗнРекв);
	КонецЦикла;
	
	//Выгрузим виды контактной информации
	Если спСправочникиКонтИнф.НайтиПоЗначению(ТекТипЗн) <> Неопределено  Тогда
		ТекстЗапросаКонтИнф =  "ВЫБРАТЬ
							   |	КонтактнаяИнформация.Вид КАК ВидИнф
							   |ИЗ
							   |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
							   |ГДЕ
							   |	КонтактнаяИнформация.Объект = &ТекОбъект
							   |
							   |УПОРЯДОЧИТЬ ПО
							   |	ВидИнф" ;
		ЗапросКонтИнф = Новый Запрос();
		ЗапросКонтИнф.УстановитьПараметр("ТекОбъект", ЗнРекв);
	   	ЗапросКонтИнф.Текст = ТекстЗапросаКонтИнф;
		РезЗапросаКонтИнф  	= ЗапросКонтИнф.Выполнить(); 
		ВыборкаКонтИнф = РезЗапросаКонтИнф.Выбрать();
	   	Пока ВыборкаКонтИнф.Следующий() Цикл
			ДобавитьПоСсылкам(ВыборкаКонтИнф.ВидИнф, -1, ЗнРекв);
		КонецЦикла;		
	КонецЕсли;
	
	//Выгрузим дополнительные реквизиты для справочников
	НайденнаяСтрока = спСправочникиСпецРеквизиты.Найти(ТекТипЗн, "Тип");
	Если НайденнаяСтрока <> Неопределено Тогда
		ЗапросСпецРеквизиты = Новый запрос();
		ЗапросСпецРеквизиты.Текст = НайденнаяСтрока.ТекстЗапроса;
		Попытка												
			Выполнить(НайденнаяСтрока.ПарметрыЗапроса);
			ВыборкаСпецРеквизитов = ЗапросСпецРеквизиты.Выполнить().Выбрать();
			Пока ВыборкаСпецРеквизитов.Следующий() Цикл
				Элемент = ВыборкаСпецРеквизитов.Значение;
				Если ЗначениеЗаполнено(Элемент) Тогда 
					НайденнаяСтрока.Объекты.Вставить(ЗнРекв.Наименование, Элемент);
					ДобавитьПоСсылкам(ВыборкаСпецРеквизитов.Значение, 1, ЗнРекв);
				КонецЕсли;	
			КонецЦикла;
		Исключение Сообщить(ИнформацияОбОшибке().Описание);
		КонецПопытки;	
	КонецЕсли;	
		
	
	// переберем табличные части и движения по партиям товаров
	// причем из аналитики партионного движения нас интересует только номенклатура и склад
	Если Документы.ТипВсеСсылки().СодержитТип(ТекТипЗн) Тогда
		// Переберем табличную часть
		ИмяТаблицыВыгрузки = Неопределено;
		ЗнСпискаТаблиц = спТаблицВыгрузки.НайтиПоЗначению(ЗнРекв.Метаданные().Имя);
		Если ЗнСпискаТаблиц <> Неопределено Тогда
			Если ЗнСпискаТаблиц.Представление <> "ПартииТоваров" И СтрокаВыгрузитьДвижения(ЗнСпискаТаблиц.Представление,ЗнРекв.Метаданные().Имя) = Неопределено  Тогда
				ИмяТаблицыВыгрузки = ЗнСпискаТаблиц.Представление;
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяТаблицыВыгрузки = Неопределено Тогда
			Для Каждого ТекТабл Из ЗнРекв.Метаданные().ТабличныеЧасти Цикл
				Для Каждого СтрокаТабл Из ЗнРекв[ТекТабл.Имя] Цикл
					Для Каждого Рекв Из ТекТабл.Реквизиты Цикл
						ДобавитьПоСсылкам(СтрокаТабл[Рекв.Имя], Уровень, ЗнРекв);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			ТекТабл = ЗнРекв.Метаданные().ТабличныеЧасти[ИмяТаблицыВыгрузки];
			Если ТекТабл <> Неопределено Тогда
				Для Каждого СтрокаТабл Из ЗнРекв[ТекТабл.Имя] Цикл
					Для Каждого Рекв Из ТекТабл.Реквизиты Цикл
						ДобавитьПоСсылкам(СтрокаТабл[Рекв.Имя], Уровень, ЗнРекв);
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
        
		//Здесь делается запрос к движениям для того, чтобы добавить по ссылка номенклатуры, так как не вся
		//номенклатур участвующая в движениях может оказаться в табличной части и значит не вся будет добавлена.
		//А здесь ее и добавляем. И с ХозОперациями тоже самое
		Для Каждого ТекДвижение	ИЗ ЗнРекв.Метаданные().Движения Цикл
			Если Найти(ТекДвижение.Имя, "ПартииТоваров") > 0 Тогда
				Если кУР Тогда
					ТекстЗапроса = "ВЫБРАТЬ
					 |ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
					 |ПартииТоваровОтданные.ХозОперация КАК ХозОперация,
					 |ПартииТоваровОтданные.Партия КАК Партия
					 |ИЗ
					 |РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
					 |ГДЕ
					 |ПартииТоваровОтданные.Регистратор = &ТекОбъект
					 |И ПартииТоваровОтданные.УправленческийУчет = " + УправленческийУчет + "
					 |УПОРЯДОЧИТЬ ПО
					 |Номенклатура";
				 Иначе
					ТекстЗапроса = "ВЫБРАТЬ
					 |ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
					 |ПартииТоваровОтданные.ХозОперация КАК ХозОперация,
					 |ПартииТоваровОтданные.Партия КАК Партия
					 |ИЗ
					 |РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
					 |ГДЕ
					 |ПартииТоваровОтданные.Регистратор = &ТекОбъект
					 |УПОРЯДОЧИТЬ ПО
					 |Номенклатура";
				 КонецЕсли;				 
				
				Запрос = Новый Запрос();
				Запрос.УстановитьПараметр("ТекОбъект", ЗнРекв);
				Запрос.Текст = ТекстЗапроса;
				
				РезЗапросаПартииТоваров  = Запрос.Выполнить();
				НеВыгружатьПартииТоваров = РезЗапросаПартииТоваров.Пустой();
				
				Если НеВыгружатьПартииТоваров Тогда
					Если кУР Тогда 
						ТекстЗапроса = "ВЫБРАТЬ
						 |ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
						 |ПартииТоваровКомпании.ХозОперация КАК ХозОперация,
						 |ПартииТоваровКомпании.Партия КАК Партия
						 |ИЗ
						 |РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
						 |ГДЕ
						 |ПартииТоваровКомпании.Регистратор = &ТекОбъект
						 |И ПартииТоваровКомпании.УправленческийУчет = " + УправленческийУчет + "
						 |УПОРЯДОЧИТЬ ПО
						 |Номенклатура";
					 Иначе
						ТекстЗапроса = "ВЫБРАТЬ
						 |ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
						 |ПартииТоваровКомпании.ХозОперация КАК ХозОперация,
						 |ПартииТоваровКомпании.Партия КАК Партия
						 |ИЗ
						 |РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
						 |ГДЕ
						 |ПартииТоваровКомпании.Регистратор = &ТекОбъект
						 |УПОРЯДОЧИТЬ ПО
						 |Номенклатура";						 
					 КонецЕсли;
					
					Запрос = Новый Запрос();
					Запрос.УстановитьПараметр("ТекОбъект", ЗнРекв);
					Запрос.Текст = ТекстЗапроса;
					
					РезЗапросаПартииТоваров  = Запрос.Выполнить();
					НеВыгружатьПартииТоваров = РезЗапросаПартииТоваров.Пустой();
				КонецЕсли;
				
				ВыборкаПартииТоваров = РезЗапросаПартииТоваров.Выбрать();
				
				Пока ВыборкаПартииТоваров.Следующий() Цикл
					ДобавитьПоСсылкам(ВыборкаПартииТоваров.Номенклатура, Уровень, ЗнРекв);
					ДобавитьПоСсылкам(ВыборкаПартииТоваров.ХозОперация, Уровень, ЗнРекв);
					Если ВыгружатьДокументыПоДвижениямПартий И ВыборкаПартииТоваров.Партия.Дата >= ДатаВводаОстатковВБухгалтерии Тогда
						ДобавитьПоСсылкам(ВыборкаПартииТоваров.Партия, Уровень, ЗнРекв);
					КонецЕсли;
				КонецЦикла;				
			ИначеЕсли Найти(ТекДвижение.Имя, "ТоварыВПроизводстве") > 0 И ВыгружатьДокументыПоДвижениямПартий Тогда
				ТекстЗапроса = "ВЫБРАТЬ
				               |	ТоварыВПроизводстве.Партия
				               |ИЗ
				               |	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
				               |ГДЕ
				               |	ТоварыВПроизводстве.Регистратор = &Регистратор";						 

				Запрос = Новый Запрос();
				Запрос.УстановитьПараметр("Регистратор", ЗнРекв);
				Запрос.Текст = ТекстЗапроса;
				
				РезЗапросаПартииТоваров  = Запрос.Выполнить();
				НеВыгружатьПартииТоваров = РезЗапросаПартииТоваров.Пустой();
				
				ВыборкаПартииТоваров = РезЗапросаПартииТоваров.Выбрать();
				
				Пока ВыборкаПартииТоваров.Следующий() Цикл
					Если ВыборкаПартииТоваров.Партия.Дата >= ДатаВводаОстатковВБухгалтерии Тогда
						ДобавитьПоСсылкам(ВыборкаПартииТоваров.Партия, Уровень, ЗнРекв);
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли Найти(ТекДвижение.Имя, "ОстаткиАвтомобилей") > 0 И ВыгружатьДокументыПоДвижениямПартий Тогда
				ТекстЗапроса = "ВЫБРАТЬ
				               |	ОстаткиАвтомобилей.Партия
				               |ИЗ
				               |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				               |ГДЕ
				               |	ОстаткиАвтомобилей.Регистратор = &Регистратор";						 

				Запрос = Новый Запрос();
				Запрос.УстановитьПараметр("Регистратор", ЗнРекв);
				Запрос.Текст = ТекстЗапроса;
				
				РезЗапросаПартииТоваров  = Запрос.Выполнить();
				НеВыгружатьПартииТоваров = РезЗапросаПартииТоваров.Пустой();
				
				ВыборкаПартииТоваров = РезЗапросаПартииТоваров.Выбрать();
				
				Пока ВыборкаПартииТоваров.Следующий() Цикл
					Если ВыборкаПартииТоваров.Партия.Дата >= ДатаВводаОстатковВБухгалтерии Тогда
						ДобавитьПоСсылкам(ВыборкаПартииТоваров.Партия, Уровень, ЗнРекв);
					КонецЕсли;
				КонецЦикла;				
			КонецЕсли;
			
			//А здесь из нашего списка движений добавляем.			
			//Строка = тзДвиженияКВыгрузке.Найти(ТекДвижение.Имя,"Наименование");
			Строка = СтрокаВыгрузитьДвижения(ТекДвижение.Имя,ЗнРекв.Метаданные().Имя);
			Если Строка <> Неопределено и Строка.ДобавлятьПоСсылкам Тогда
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Регистратор",ЗнРекв);
				Запрос.Текст = Строка.ТекстЗапроса;
				РезультатЗапроса = Запрос.Выполнить();				
				Если НЕ РезультатЗапроса.Пустой() Тогда
					Колонки = РезультатЗапроса.Колонки;
					Выборка = РезультатЗапроса.Выбрать();
					Пока Выборка.Следующий() Цикл
						Если Колонки.Найти("Номенклатура") <> Неопределено Тогда
							ДобавитьПоСсылкам(Выборка.Номенклатура,Уровень, ЗнРекв);
						КонецЕсли;
						Если Колонки.Найти("Автомобиль") <> Неопределено Тогда
							ДобавитьПоСсылкам(Выборка.Автомобиль,Уровень, ЗнРекв);
						КонецЕсли;
						Если Колонки.Найти("ХозОперация") <> Неопределено Тогда 
							ДобавитьПоСсылкам(Выборка.ХозОперация,Уровень, ЗнРекв); 
						КонецЕсли;
						Если Колонки.Найти("ДокументПередачи") <> Неопределено Тогда
							ДобавитьПоСсылкам(Выборка.ДокументПередачи,Уровень, ЗнРекв); 
						КонецЕсли;
					КонецЦикла;					
				КонецЕсли;			
			КонецЕсли;			
		КонецЦикла;
		
		//А здесь сформируем или возьмем существующий СФ и по нему пройдемся на тот случай, если ГТД мы не выгружали еще
		ЗагружатьИзСчетФактуры = Ложь;
		Если НЕ (ТипЗнч(ЗнРекв) = Тип("ДокументСсылка.ВозвратПоставщику") И ЗнРекв.ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия) Тогда
			Если спТаблицВыгрузкиГТД.НайтиПоЗначению(ЗнРекв.Метаданные().Имя) <> Неопределено Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	СчетФактураВыданный.Ссылка КАК СчетФактура
					|ИЗ
					|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
					|ГДЕ
					|	СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ и
					|	СчетФактураВыданный.ДокументОснование = &ДокументОснование";
				Запрос.УстановитьПараметр("ДокументОснование",ЗнРекв);
				РезультатЗапроса = Запрос.Выполнить();
				СчетФактураНеВведен = РезультатЗапроса.Пустой();
				Если СчетФактураНеВведен Тогда				
					СчетФактура = Документы.СчетФактураВыданный;
				КонецЕсли;												
					
				Если СчетФактураНеВведен Тогда
					СчетФактура = СчетФактура.СоздатьДокумент();
					СчетФактура.Заполнить(ЗнРекв);
					ЗагружатьИзСчетФактуры = Истина;
				Иначе
					Выборка =РезультатЗапроса.Выбрать();
					Выборка.Следующий();
					СчетФактура = Выборка.СчетФактура;
				КонецЕсли;				
				ТекТабл = СчетФактура.Товары;
				ЗагружатьИзСчетФактуры = Истина;
			КонецЕсли;   
			Если ЗагружатьИзСчетФактуры Тогда
				Для каждого ТекСтрока из ТекТабл Цикл
					Если НЕ ТекСтрока.ГТД.Пустая() Тогда ДобавитьПоСсылкам(ТекСтрока.ГТД, Уровень, ЗнРекв); КонецЕсли;
				КонецЦикла;		
			КонецЕсли;					
		КонецЕсли;	
		
		Если ЗнРекв <> Неопределено Тогда
			Если спТаблицВыгрузкиГТДДоп.НайтиПоЗначению(ЗнРекв.Метаданные().Имя) <> Неопределено Тогда
	        	ТекТабл = СформироватьТЧ_С_ГТД(ЗнРекв);
				Для каждого ТекСтрока из ТекТабл Цикл
					Если НЕ ТекСтрока.ГТД.Пустая() Тогда ДобавитьПоСсылкам(ТекСтрока.ГТД, Уровень, ЗнРекв); КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;	

	КонецЕсли;
	
		
КонецПроцедуры	//	ДобавитьПоСсылкам()

// Выгружает структуру реквизитов или значения реквизитов
Процедура ВыгрузитьРеквизиты(НаборРеквизитов, Ссылка, ЭтоСтруктура=Ложь, ДополнительныйРеквизит=Неопределено) Экспорт		  
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли

	Если ДополнительныйРеквизит <> Неопределено Тогда 
		Если ТипЗнч(ДополнительныйРеквизит)=Тип("Структура") И ДополнительныйРеквизит.Имя="ВидПлатежа" Тогда
			СтрокаЗначенийРеквизитов	= Новый Структура();
			РеквизитыТаблица				= Новый ТаблицаЗначений();
			РеквизитыТаблица.Колонки.Добавить("Имя");
			РеквизитыТаблица.Колонки.Добавить("Синоним");
			РеквизитыТаблица.Колонки.Добавить("Комментарий");
			РеквизитыТаблица.Колонки.Добавить("Тип");
			
			Для Каждого СтрокаРеквизита Из НаборРеквизитов Цикл
				СтрокаДанных			= РеквизитыТаблица.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанных, СтрокаРеквизита);
				Если НЕ ЭтоСтруктура Тогда
					СтрокаЗначенийРеквизитов.Вставить(СтрокаРеквизита.Имя, Ссылка[СтрокаРеквизита.Имя]);
				КонецЕсли;
			КонецЦикла;
			СтрокаДанных				= РеквизитыТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанных, ДополнительныйРеквизит);
			
			Если НЕ ЭтоСтруктура Тогда
				Если ТипЗнч(Ссылка.ПлатежноеПоручениеОснование)=Тип("ДокументСсылка.ПлатежноеПоручение")
					И ЗначениеЗаполнено(Ссылка.ПлатежноеПоручениеОснование) Тогда
					СтрокаЗначенийРеквизитов.Вставить("ВидПлатежа", Ссылка.ПлатежноеПоручениеОснование.ВидПлатежа);
				Иначе
					СтрокаЗначенийРеквизитов.Вставить("ВидПлатежа", "");
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ЭтоСтруктура Тогда
				Ссылка			= СтрокаЗначенийРеквизитов;
			КонецЕсли;
			ТаблицаРеквизитов	= РеквизитыТаблица; 
		ИначеЕсли ТипЗнч(ДополнительныйРеквизит)=Тип("Структура") И ДополнительныйРеквизит.Имя="НомерИсправления" Тогда 
			
			СтрокаЗначенийРеквизитов	= Новый Структура();
			РеквизитыТаблица				= Новый ТаблицаЗначений();
			РеквизитыТаблица.Колонки.Добавить("Имя");
			РеквизитыТаблица.Колонки.Добавить("Синоним");
			РеквизитыТаблица.Колонки.Добавить("Комментарий");
			РеквизитыТаблица.Колонки.Добавить("Тип");
			
			Для Каждого СтрокаРеквизита Из НаборРеквизитов Цикл
				СтрокаДанных			= РеквизитыТаблица.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанных, СтрокаРеквизита);
				Если НЕ ЭтоСтруктура Тогда
					СтрокаЗначенийРеквизитов.Вставить(СтрокаРеквизита.Имя, Ссылка[СтрокаРеквизита.Имя]);
				КонецЕсли;
			КонецЦикла;
			СтрокаДанных				= РеквизитыТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанных, ДополнительныйРеквизит);
			
			Если НЕ ЭтоСтруктура Тогда
				СтрокаЗначенийРеквизитов.Вставить("НомерИсправления", "0");
			КонецЕсли;
			
			ТаблицаРеквизитов	= РеквизитыТаблица; 	
		Иначе
			СчетФактура					= Документы.СчетФактураВыданный;		
			РеквизитыСФ					= СчетФактура.СоздатьДокумент().Метаданные().ТабличныеЧасти.Товары.Реквизиты;		
			РеквизитыТаблица			= Новый ТаблицаЗначений;
			РеквизитыТаблица.Колонки.Добавить("Имя");
			РеквизитыТаблица.Колонки.Добавить("Комментарий");
			РеквизитыТаблица.Колонки.Добавить("Синоним");
			РеквизитыТаблица.Колонки.Добавить("Тип");								
						
			Для каждого Рекв из РеквизитыСФ Цикл 
				НоваяСтрока				= РеквизитыТаблица.Добавить();				
				НоваяСтрока.Имя			= Рекв.Имя;
				НоваяСтрока.Комментарий	= Рекв.Комментарий;
				НоваяСтрока.Синоним		= Рекв.Синоним;
				НоваяСтрока.Тип			= Рекв.Тип;								
			КонецЦикла;				
			НоваяСтрока					= РеквизитыТаблица.Добавить();				
			НоваяСтрока.Имя				= ДополнительныйРеквизит.Имя;
			НоваяСтрока.Комментарий		= ДополнительныйРеквизит.Комментарий;
			НоваяСтрока.Синоним			= ДополнительныйРеквизит.Синоним;
			НоваяСтрока.Тип				= ДополнительныйРеквизит.Тип;				
			ТаблицаРеквизитов			= РеквизитыТаблица;
		КонецЕсли;
	Иначе
		ТаблицаРеквизитов = НаборРеквизитов;
	КонецЕсли;		
	    
	Для Каждого Рекв Из ТаблицаРеквизитов Цикл
		СтрТипов      = "";
		Квалификаторы = ?((ТипЗнч(Рекв) <> Тип("КолонкаРезультатаЗапроса")) И (ТипЗнч(Рекв) <> Тип("КолонкаТаблицыЗначений")), Рекв.Тип, Рекв.ТипЗначения);
		ТипыРеквизита = Квалификаторы.Типы();
		СоставнойТип  = Ложь;
		ПервыйТип     = Истина;
		
		Для Каждого Тип Из ТипыРеквизита Цикл
			Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда ТипСтр = "Справочник";
			ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда ТипСтр = "Документ";
			ИначеЕсли Перечисления.ТипВсеСсылки().СодержитТип(Тип) Тогда ТипСтр = "Перечисление";
			Иначе ТипСтр = Строка(Тип);
			КонецЕсли;
			
			Если Найти(СтрТипов, ТипСтр) = 0 Тогда
				СтрТипов = СтрТипов + ?(СтрТипов = "", "" , ";") + ТипСтр;
			КонецЕсли;
			
			Если ТипЗнч(Рекв) <> Тип("КолонкаРезультатаЗапроса") и НЕ ПервыйТип Тогда
				СоставнойТип = Истина;
			КонецЕсли;
			
			Если ПервыйТип Тогда ПервыйТип = Ложь; КонецЕсли;
		КонецЦикла;
		
		// Если ссылка на объект пуста, то выгрузим структуру реквизитов
		Если ЭтоСтруктура Тогда
			Длина    = Квалификаторы.КвалификаторыСтроки.Длина;
			Точность = 0;
			Если Длина = 0 Тогда
				Длина    = Квалификаторы.КвалификаторыЧисла.Разрядность;
				Точность = Квалификаторы.КвалификаторыЧисла.РазрядностьДробнойЧасти;
			КонецЕсли;
			
			СтрТипов = СтрЗаменить(СтрЗаменить(СтрТипов, "Null, ", ""), ", Null", "");
			СтрТипов = СтрЗаменить(СтрЗаменить(СтрТипов, "Null; ", ""), "; Null", "");
			СтрТипов = СтрЗаменить(СтрЗаменить(СтрТипов, "Null,",  ""), ",Null",  "");
			СтрТипов = СтрЗаменить(СтрЗаменить(СтрТипов, "Null;",  ""), ";Null",  "");
			
			Если XMLФорматВыгрузки Тогда
				// Запишем структуру реквизита в файл-выгрузки
				ДокXML.ЗаписатьНачалоЭлемента("Реквизит");
				ДокXML.ЗаписатьАтрибут("Наименование", Рекв.Имя);
				ДокXML.ЗаписатьАтрибут("Тип",          СтрТипов);
				ДокXML.ЗаписатьАтрибут("Длина",        СокрЛП(Длина));
				ДокXML.ЗаписатьАтрибут("Точность",     СокрЛП(Точность));
				ДокXML.ЗаписатьАтрибут("СоставнойТип", СокрЛП(СоставнойТип));
				ДокXML.ЗаписатьКонецЭлемента();
				
			Иначе
				ОбщееОписание = ОбщееОписание + "{""" + Рекв.Имя + """,""" + СтрТипов + """," + СокрЛП(Длина) + "," + СокрЛП(Точность) + ",""" + СокрЛП(СоставнойТип) + """}";
			КонецЕсли;
			
		Иначе
			
			СтрокаВыписки = Ложь;
			Попытка 
				Если Строка(Ссылка) = "ДокументТабличнаяЧастьСтрока.Выписка.Состав" и Рекв.Имя = "ПлатежноеПоручениеОснование" Тогда
					СтрокаВыписки = Истина;
				КонецЕсли;
			Исключение КонецПопытки;
			
			//Если выгружаем реквизиты из табличной части выписки, то вместо платежных поручений выгружаем представление,
			//чтобы потом можно было достать из него номер и дату.
			//А попытка исключение для того, что иногда передаются ВыборкаРезультатаЗапроса, а ее не определишь.
			Если СтрокаВыписки Тогда
				ЗначениеРеквизита = Строка(Ссылка[Рекв.Имя]);
				
			ИначеЕсли Рекв.Имя="Партия" И Ссылка[Рекв.Имя] <> Неопределено И ЗначениеЗаполнено(ДатаВводаОстатковВБухгалтерии) И Ссылка[Рекв.Имя].Дата < ДатаВводаОстатковВБухгалтерии ИЛИ (Рекв.Имя="Партия" И НЕ ВыгружатьДокументыПоДвижениямПартий) Тогда
				//Партии не подлежат выгрузке по ссылке
				ЗначениеРеквизита	= Неопределено; 
				
				// для документа доп расходы выгружаем партии, т.к в БП в документе необходимо заполнять документ партии
				Если  ПроцедурыОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Ссылка, "Хозоперация")
					И Ссылка.Хозоперация =Справочники.ХозОперации.ПоступлениеДопРасходов Тогда 
					
					ЗначениеРеквизита = Ссылка[Рекв.Имя];
				КонецЕсли;
			Иначе					
				//Здесь смотрится а не является ли документ по ссылке не регламентированного учета или может
				//у него вообще нет этого реквизита. Если так, то он и не выгружается.
				//Для УР такого не было, поэтому так и оставил.
				//Потом может будет необходимость здесь разделить на управленческий и на регламентированный учет.
				ЗначениеРеквизита = Неопределено; 
				Если  Рекв.Имя = "НомерИсправления" И 
					(ТипЗнч(Ссылка) = Тип("ДокументСсылка.КорректировкаПоступления") ИЛИ ТипЗнч(Ссылка) = Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей")) Тогда
					  ЗначениеРеквизита = ПолучитьНомерИсправления(Ссылка);
				ИначеЕсли (Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Ссылка[Рекв.Имя]))) Тогда
					Если (Ссылка[Рекв.Имя] <> Неопределено) 
						И (
							(
							 Ссылка[Рекв.Имя].Метаданные().Реквизиты.Найти("РегламентированныйУчет")<>Неопределено ИЛИ Рекв.Имя="ПлатежноеПоручениеОснование" ИЛИ (Рекв.Имя="Партия" И Ссылка[Рекв.Имя].Метаданные().Имя="ПересортицаТоваров")
							 ИЛИ спДокументовБезРегл.НайтиПоЗначению(Ссылка[Рекв.Имя].Метаданные().Имя) <> Неопределено ИЛИ спДокументовСпец.НайтиПоЗначению(Ссылка[Рекв.Имя].Метаданные().Имя) <> Неопределено
							)
						  ) Тогда
						ЗначениеРеквизита=Ссылка[Рекв.Имя];
					КонецЕсли;					
				Иначе
					ЗначениеРеквизита = Ссылка[Рекв.Имя];
				КонецЕсли;
				
				Если Рекв.Имя="КодМаркировки" Тогда
					// Удалим запрещенный символ - разделитель ГС1
					ЗначениеРеквизита = СтрЗаменить(ЗначениеРеквизита, Символ(29), "\x1d");
				КонецЕсли;
				
				//КонецЕсли; 
			КонецЕсли;
	
			
			Если XMLФорматВыгрузки Тогда
				// Запишем значение реквизита в файл-выгрузки
				ДокXML.ЗаписатьАтрибут(Рекв.Имя, СокрЛП(ПредставлениеРеквизита(ЗначениеРеквизита, СоставнойТип)));
				
			Иначе	
				ОбщееОписание = ОбщееОписание + "," + СокрЛП(ПредставлениеРеквизита(ЗначениеРеквизита, СоставнойТип))
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ XMLФорматВыгрузки Тогда
		Если НЕ ЭтоСтруктура Тогда ОбщееОписание = ОбщееОписание + "}"; КонецЕсли;
		
		Текст.ЗаписатьСтроку(ОбщееОписание);
	КонецЕсли;
	
КонецПроцедуры	//	ВыгрузитьРеквизиты()

// Выгружает объект в файл-выгрузки и добавляет "связанные" значения реквизитов
Процедура ВыгрузитьОбъект(ТекСтрокаОбъекта) Экспорт
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли

	Ссылка   = ТекСтрокаОбъекта.Ссылка;
	ТекТипЗн = ТипЗнч(Ссылка);
	ИмяДокумента = Ссылка.Метаданные().Имя;
	
	// Попытаемся получить движения по регистрам "Партии товаров"
	// по выгружаемому объекту
	НеВыгружатьПартииТоваров = Истина;
	//НеВыгружатьТоварыВПроизводстве = Истина;
	Для Каждого Строка Из тзДвиженияКВыгрузке Цикл
		Строка.НеВыгружать = Истина;
	КонецЦикла;	
	
	ЭтоДокумент = Документы.ТипВсеСсылки().СодержитТип(ТекТипЗн);
	
	// Естественно только для документов ...
	Если ЭтоДокумент Тогда
		СкладМПЗ = "";
		Попытка СкладМПЗ = Ссылка.СкладКомпании;
		Исключение
		КонецПопытки;
		
		Если кУР Тогда	
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПартииТоваровКомпании.СкладКомпании КАК СкладКомпании,
			|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
			|	ПартииТоваровКомпании.ВидДвижения КАК ВидДвижения,
			|	ПартииТоваровКомпании.СтатусПартии КАК СтатусПартии,				
			|	ПартииТоваровКомпании.ХозОперация КАК ХозОперация,
			|	ПартииТоваровКомпании.Партия КАК Партия,
			|	ПартииТоваровКомпании.Количество КАК Количество,
			|	ПартииТоваровКомпании.Сумма КАК Сумма,
			|	ПартииТоваровКомпании.СуммаНДС КАК СуммаНДС,
			|	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС
			|ИЗ
			|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
			|ГДЕ
			|	ПартииТоваровКомпании.Регистратор = &ТекОбъект
			|	И ПартииТоваровКомпании.УправленческийУчет = " + УправленческийУчет + "
			|				
			|УПОРЯДОЧИТЬ ПО
			|	СкладКомпании,
			|	Номенклатура,
			|   СтатусПартии,
			|	ВидДвижения";
		Иначе
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПартииТоваровКомпании.СкладКомпании КАК СкладКомпании,
			|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
			|	ПартииТоваровКомпании.ВидДвижения КАК ВидДвижения,
			|	ПартииТоваровКомпании.СтатусПартии КАК СтатусПартии,				
			|	ПартииТоваровКомпании.ХозОперация КАК ХозОперация,
			|	ПартииТоваровКомпании.Партия КАК Партия,
			|	ПартииТоваровКомпании.Количество КАК Количество,
			|	ПартииТоваровКомпании.Сумма КАК Сумма,
			|	ПартииТоваровКомпании.СуммаНДС КАК СуммаНДС,
			|	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС
			|ИЗ
			|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
			|ГДЕ
			|	ПартииТоваровКомпании.Регистратор = &ТекОбъект
			|				
			|УПОРЯДОЧИТЬ ПО
			|	СкладКомпании,
			|	Номенклатура,
			|   СтатусПартии,
			|	ВидДвижения";
		КонецЕсли;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("ТекОбъект", Ссылка);
		Запрос.Текст = ТекстЗапроса;
		
		РезЗапросаПартииТоваров  = Запрос.Выполнить();
		НеВыгружатьПартииТоваров = РезЗапросаПартииТоваров.Пустой();
		
		Если НеВыгружатьПартииТоваров Тогда
			
			Если кУР Тогда
				ТекстЗапроса = 
				"ВЫБРАТЬ
				|	&СкладМПЗ КАК СкладКомпании,
				|	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
				|	ПартииТоваровОтданные.ВидДвижения КАК ВидДвижения,
				|	&Статус КАК СтатусПартии,								
				|	ПартииТоваровОтданные.ХозОперация КАК ХозОперация,
				|	ПартииТоваровОтданные.Партия КАК Партия,
				|	ПартииТоваровОтданные.Количество КАК Количество,
				|	ПартииТоваровОтданные.Сумма КАК Сумма,
				|	ПартииТоваровОтданные.СуммаУпр КАК СуммаУпр,
				|	ПартииТоваровОтданные.СуммаНДС КАК СуммаНДС
				|ИЗ
				|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
				|ГДЕ
				|	ПартииТоваровОтданные.Регистратор = &ТекОбъект
				|	И ПартииТоваровОтданные.УправленческийУчет = " + УправленческийУчет + "
				|
				|УПОРЯДОЧИТЬ ПО
				|	СкладКомпании,
				|	Номенклатура,
				|	СтатусПартии,
				|	ВидДвижения";
			Иначе
				ТекстЗапроса = 
				"ВЫБРАТЬ
				|	&СкладМПЗ КАК СкладКомпании,
				|	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
				|	ПартииТоваровОтданные.ВидДвижения КАК ВидДвижения,
				|	&Статус КАК СтатусПартии,								
				|	ПартииТоваровОтданные.ХозОперация КАК ХозОперация,
				|	ПартииТоваровОтданные.Партия КАК Партия,
				|	ПартииТоваровОтданные.Количество КАК Количество,
				|	ПартииТоваровОтданные.Сумма КАК Сумма,
				|	ПартииТоваровОтданные.СуммаУпр КАК СуммаУпр,
				|	ПартииТоваровОтданные.СуммаНДС КАК СуммаНДС
				|ИЗ
				|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
				|ГДЕ
				|	ПартииТоваровОтданные.Регистратор = &ТекОбъект
				|
				|УПОРЯДОЧИТЬ ПО
				|	СкладКомпании,
				|	Номенклатура,
				|	СтатусПартии,
				|	ВидДвижения";			
			КонецЕсли;
			
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("ТекОбъект", Ссылка);
			Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыПартий.ТоварКупленный);
			Запрос.УстановитьПараметр("СкладМПЗ", СкладМПЗ);
			Запрос.Текст = ТекстЗапроса;
			
			РезЗапросаПартииТоваров  = Запрос.Выполнить();
			НеВыгружатьПартииТоваров = РезЗапросаПартииТоваров.Пустой();
		КонецЕсли;
		
		Если НЕ НеВыгружатьПартииТоваров Тогда
			ВыборкаПартииТоваров = РезЗапросаПартииТоваров.Выбрать();
		КонецЕсли;
		
		Для Каждого Строка Из тзДвиженияКВыгрузке Цикл
			СтрокаДвижения = СтрокаВыгрузитьДвижения(Строка.Наименование,ИмяДокумента,ИмяКонфигурации);
			Если СтрокаДвижения = Неопределено Тогда Продолжить; КонецЕсли;
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Регистратор",Ссылка);
			Запрос.Текст = Строка.ТекстЗапроса;
			РезЗапроса = Запрос.Выполнить();
			Строка.НеВыгружать = РезЗапроса.Пустой();
			Строка.РезультатЗапроса = РезЗапроса; 			
		КонецЦикла;		
	КонецЕсли;
	
	//Выгрузим контактную информацию если необходимо,
	//"представление" вида информации необходимо для загрузки в версию Бухгалтерии 4.5
	ВыгружатьКонтИнф = Ложь;
	ЕстьКонтИнф = ?(спСправочникиКонтИнф.НайтиПоЗначению(ТекТипЗн)<>Неопределено, Истина,Ложь);
	Если ЕстьКонтИнф Тогда 
		ТекстЗапросаКонтИнф =  "ВЫБРАТЬ
		                       |	КонтактнаяИнформация.Тип КАК ТипИнф,
		                       |	КонтактнаяИнформация.Вид КАК ВидИнф,
		                       |	КонтактнаяИнформация.ЗначениеПоУмолчанию,
		                       |	КонтактнаяИнформация.Поле1 КАК Индекс,
		                       |	КонтактнаяИнформация.Поле2 КАК Регион,
		                       |	КонтактнаяИнформация.Поле3 КАК Район,
		                       |	КонтактнаяИнформация.Поле4 КАК Город,
		                       |	КонтактнаяИнформация.Поле5 КАК НасПункт,
		                       |	КонтактнаяИнформация.Поле6 КАК Улица,
		                       |	КонтактнаяИнформация.Поле7 КАК Дом,
		                       |	КонтактнаяИнформация.Поле8 КАК Корпус,
		                       |	КонтактнаяИнформация.Поле9 КАК Квартира,
		                       |	КонтактнаяИнформация.Представление,
		                       |	КонтактнаяИнформация.Поле10 КАК ПолеДоп,
		                       |	ПРЕДСТАВЛЕНИЕ(КонтактнаяИнформация.Вид) КАК СтрокаВид,
		                       |	КонтактнаяИнформация.КраткоеПредставление
		                       |ИЗ
		                       |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		                       |ГДЕ
		                       |	КонтактнаяИнформация.Объект = &ТекОбъект
		                       |	И КонтактнаяИнформация.Тип В(&Тип)
		                       |
		                       |УПОРЯДОЧИТЬ ПО
		                       |	ТипИнф,
		                       |	ВидИнф" ;
		СписокТипИнф = Новый СписокЗначений();
		СписокТипИнф.Добавить(Перечисления.ТипыКонтактнойИнформации.Адрес);
		СписокТипИнф.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
		ЗапросКонтИнф = Новый Запрос();
		ЗапросКонтИнф.УстановитьПараметр("ТекОбъект", Ссылка);
        ЗапросКонтИнф.УстановитьПараметр("Тип", СписокТипИнф);
       	ЗапросКонтИнф.Текст = ТекстЗапросаКонтИнф;
        РезЗапросаКонтИнф  	= ЗапросКонтИнф.Выполнить();
		ВыгружатьКонтИнф 	= НЕ РезЗапросаКонтИнф.Пустой();
		
		Если ВыгружатьКонтИнф Тогда
			ВыборкаКонтИнф = РезЗапросаКонтИнф.Выбрать();
		КонецЕсли;	
		
	КонецЕсли;
	
	//Получим таблицу списаний автомобилей для "Реализации автомобилей"
	Если кАвтосервис И ИмяДокумента="РеализацияАвтомобилей" Тогда	
		ТаблицаСписанийАвтомобилей		= ПолучитьТаблицуСписанийАвтомобилей(Ссылка);
		ЕстьТаблицаСписанийАвтомобилей	= ТаблицаСписанийАвтомобилей.Количество()>0;
	ИначеЕсли ИмяДокумента<>"РеализацияАвтомобилей" Тогда
		ЕстьТаблицаСписанийАвтомобилей	= Ложь;
	КонецЕсли;
	
	// Для первого объекта (т.е. если объекты данного вида еще не выгружались)
	// добавим системную информацию и структуру реквизитов
	Если ПервыйОбъект Тогда
		Если XMLФорматВыгрузки Тогда
			ДокXML.ЗаписатьНачалоЭлемента("Структура");
			Если Справочники.ТипВсеСсылки().СодержитТип(ТекТипЗн) Тогда
				// попытаемся получить идентификаторы владельцев
				ИдентификаторыВладельцев = "";
				Для Каждого Владелец Из Ссылка.Метаданные().Владельцы Цикл
					ИдентификаторыВладельцев = ИдентификаторыВладельцев + ?(ИдентификаторыВладельцев = "", "", ";") + Владелец.Имя;
				КонецЦикла;
				
				ДокXML.ЗаписатьАтрибут("ДлинаКода",         СокрЛП(Ссылка.Метаданные().ДлинаКода));
				ДокXML.ЗаписатьАтрибут("ДлинаНаименования", СокрЛП(Ссылка.Метаданные().ДлинаНаименования));
				ДокXML.ЗаписатьАтрибут("КоличествоУровней", СокрЛП(Ссылка.Метаданные().КоличествоУровней));
				ДокXML.ЗаписатьАтрибут("Владельцы",         ИдентификаторыВладельцев);
				
			Иначе
				ДокXML.ЗаписатьАтрибут("ДлинаНомера", СокрЛП(Ссылка.Метаданные().ДлинаНомера));
			КонецЕсли;
			
			Если ТекТипЗн=Тип("ДокументСсылка.КорректировкаПоступления") ИЛИ ТекТипЗн=Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
				ДополнительныйРеквизитНомерИсправления	= Новый Структура(
					"Имя,Тип,Синоним,Комментарий",
					"НомерИсправления",
					Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Номер исправления","");										
				ВыгрузитьРеквизиты(Ссылка.Метаданные().Реквизиты, Ссылка,Истина,ДополнительныйРеквизитНомерИсправления); 
			Иначе		
				ВыгрузитьРеквизиты(Ссылка.Метаданные().Реквизиты, Ссылка, Истина); 
			КонецЕсли;
			
			НайденнаяСтрока = спСправочникиСпецРеквизиты.Найти(ТекТипЗн, "Тип");
			Если НайденнаяСтрока <> Неопределено Тогда
				Если НайденнаяСтрока.Объекты.Количество() > 0 Тогда
					НаборРеквизитов = Новый ТаблицаЗначений();
					НаборРеквизитов.Колонки.Добавить("Имя");
					НаборРеквизитов.Колонки.Добавить("Тип");
					НаборРеквизитов.Колонки.Добавить(НайденнаяСтрока.Реквизит);
					СтрокаДанных = НаборРеквизитов.Добавить();
					СтрокаДанных.Имя = НайденнаяСтрока.Реквизит;
					МассивТипов = Новый Массив();
					МассивТипов.Добавить(ТипЗнч(НайденнаяСтрока.Значение));
					СтрокаДанных.Тип = Новый ОписаниеТипов(МассивТипов);
					ВыгрузитьРеквизиты(НаборРеквизитов, Ссылка, Истина);
                КонецЕсли;
			КонецЕсли;
			
			ИмяТаблицыВыгрузки = Неопределено;
			ЗнСпискаТаблиц = спТаблицВыгрузки.НайтиПоЗначению(Ссылка.Метаданные().Имя);
			Если ЗнСпискаТаблиц <> Неопределено Тогда
				Если ЗнСпискаТаблиц.Представление <> "ПартииТоваров" И СтрокаВыгрузитьДвижения(ЗнСпискаТаблиц.Представление,ИмяДокумента) =Неопределено Тогда					
					ИмяТаблицыВыгрузки = ЗнСпискаТаблиц.Представление;
				КонецЕсли;
			КонецЕсли;
			
			Если ИмяТаблицыВыгрузки = Неопределено Тогда
				Для Каждого ТекТабл Из Ссылка.Метаданные().ТабличныеЧасти Цикл
					ДокXML.ЗаписатьНачалоЭлемента("Таблица");
					ДокXML.ЗаписатьАтрибут("Наименование", ТекТабл.Имя);
					Если ТекТипЗн=Тип("ДокументСсылка.Выписка") И ТекТабл.Имя="Состав" Тогда
						ДополнительныйРеквизитВидПлатежа	= Новый Структура("Имя,Тип,Синоним,Комментарий", "ВидПлатежа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Вид платежа","");	
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина, ДополнительныйРеквизитВидПлатежа);
					Иначе
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина);
					КонецЕсли;
					ДокXML.ЗаписатьКонецЭлемента();
				КонецЦикла;
				
			Иначе
				ТекТабл = Ссылка.Метаданные().ТабличныеЧасти[ИмяТаблицыВыгрузки];
				Если ТекТабл <> Неопределено Тогда
					ДокXML.ЗаписатьНачалоЭлемента("Таблица");
					ДокXML.ЗаписатьАтрибут("Наименование", ТекТабл.Имя);
					Если ТекТипЗн=Тип("ДокументСсылка.Выписка") И ТекТабл.Имя="Состав" Тогда
						ДополнительныйРеквизитВидПлатежа	= Новый Структура("Имя,Тип,Синоним,Комментарий", "ВидПлатежа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Вид платежа","");
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина, ДополнительныйРеквизитВидПлатежа);
					Иначе
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина);
					КонецЕсли;
					ДокXML.ЗаписатьКонецЭлемента();
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьКонтИнф Тогда
				ДокXML.ЗаписатьНачалоЭлемента("Таблица");
				ДокXML.ЗаписатьАтрибут("Наименование", "КонтактнаяИнформация");
				ВыгрузитьРеквизиты(РезЗапросаКонтИнф.Колонки, ВыборкаКонтИнф, Истина);
				ДокXML.ЗаписатьКонецЭлемента();
			КонецЕсли;	
				
			Если ЭтоДокумент Тогда
				Для Каждого ТекДвижение	ИЗ Ссылка.Метаданные().Движения Цикл
					Если Найти(ТекДвижение.Имя, "ПартииТоваров") > 0 Тогда
						ДокXML.ЗаписатьНачалоЭлемента("Таблица");
						ДокXML.ЗаписатьАтрибут("Наименование", "ПартииТоваров");
						ВыгрузитьРеквизиты(РезЗапросаПартииТоваров.Колонки, ВыборкаПартииТоваров, Истина);
						ДокXML.ЗаписатьКонецЭлемента();
						//Прервать;
					КонецЕсли;
					//Строка = тзДвиженияКВыгрузке.Найти(ТекДвижение.Имя,"Наименование");
					Строка = СтрокаВыгрузитьДвижения(ТекДвижение.Имя,ИмяДокумента);
					Если Строка <> Неопределено Тогда
						ДокXML.ЗаписатьНачалоЭлемента("Таблица");
						ДокXML.ЗаписатьАтрибут("Наименование", Строка.Наименование);
						ВыгрузитьРеквизиты(Строка.РезультатЗапроса.Колонки, Строка.РезультатЗапроса.Выбрать(), Истина);
						ДокXML.ЗаписатьКонецЭлемента();
						//Прервать;
					КонецЕсли;											
				КонецЦикла;
				
				Если НЕ (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратПоставщику") И Ссылка.ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия) Тогда
					//Здесь выгрузим структуру табличной части СФ
					Если спТаблицВыгрузкиГТД.НайтиПоЗначению(Ссылка.Метаданные().Имя) <> Неопределено Тогда					
						СчетФактура = Документы.СчетФактураВыданный;		
						РеквизитыСФ = СчетФактура.СоздатьДокумент().Метаданные().ТабличныеЧасти.Товары.Реквизиты;		
						Если Ссылка.Метаданные().Имя = "ОтчетКомиссионера" Тогда
							ДополнительныйРеквизит = Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Вознаграждение;
						ИначеЕсли Ссылка.Метаданные().Имя = "ЗаказНаряд" И Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("Источник")<> Неопределено Тогда
							ДополнительныйРеквизит = Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Источник;
						КонецЕсли;																			
						ДокXML.ЗаписатьНачалоЭлемента("Таблица");
						ДокXML.ЗаписатьАтрибут("Наименование", "СФ");
						ВыгрузитьРеквизиты(РеквизитыСФ, "", Истина,ДополнительныйРеквизит);
						ДокXML.ЗаписатьКонецЭлемента();
					КонецЕсли;
				КонецЕсли;
				
				
				Если спТаблицВыгрузкиГТДДоп.НайтиПоЗначению(Ссылка.Метаданные().Имя) <> Неопределено Тогда					
						ТекТабл = СформироватьТЧ_С_ГТД(Ссылка);		
						РеквизитыСФ = ТекТабл.Колонки;		
						ДокXML.ЗаписатьНачалоЭлемента("Таблица");
						ДокXML.ЗаписатьАтрибут("Наименование", "СФ");
						ВыгрузитьРеквизиты(РеквизитыСФ, "", Истина);
						ДокXML.ЗаписатьКонецЭлемента();
				КонецЕсли;

				Если ЕстьТаблицаСписанийАвтомобилей Тогда
					ДокXML.ЗаписатьНачалоЭлемента("Таблица");
					ДокXML.ЗаписатьАтрибут("Наименование", "СуммыСписанийАвтомобилей");
					ВыгрузитьРеквизиты(ТаблицаСписанийАвтомобилей.Колонки, ТаблицаСписанийАвтомобилей, Истина);
					ДокXML.ЗаписатьКонецЭлемента();					
				КонецЕсли;				
				
			КонецЕсли;
			
			ДокXML.ЗаписатьКонецЭлемента();
			
		Иначе
			Если ЭтоДокумент Тогда
				ОбщееОписание = "{""Документ." + Ссылка.Метаданные().Имя + ""","+СокрЛП(Ссылка.Метаданные().ДлинаНомера)+"}";
				
			Иначе // попытаемся получить идентификаторы владельцев
				ИдентификаторВладельца = "";
				Для Каждого Владелец Из Ссылка.Метаданные().Владельцы Цикл
					ИдентификаторВладельца = ИдентификаторВладельца + ?(ИдентификаторВладельца = "", "", ";") + Владелец.Имя;
				КонецЦикла;
				
				ДлинаКода         = СокрЛП(Ссылка.Метаданные().ДлинаКода);
				ДлинаНаименования = СокрЛП(Ссылка.Метаданные().ДлинаНаименования);
				КоличествоУровней = СокрЛП(Ссылка.Метаданные().КоличествоУровней);
				
				ОбщееОписание = "{""Справочник." + Ссылка.Метаданные().Имя + """," + ДлинаКода + "," + ДлинаНаименования + ",""" + ИдентификаторВладельца + """}";
				
			КонецЕсли;
			
			Текст.ЗаписатьСтроку(ОбщееОписание);
			
			ОбщееОписание = "  ";
			
			Если ТекТипЗн=Тип("ДокументСсылка.КорректировкаПоступления") ИЛИ ТекТипЗн=Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
				ДополнительныйРеквизитНомерИсправления	= Новый Структура(
					"Имя,Тип,Синоним,Комментарий",
					"НомерИсправления",
					Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Номер исправления","");										
				ВыгрузитьРеквизиты(Ссылка.Метаданные().Реквизиты, Ссылка,,ДополнительныйРеквизитНомерИсправления); 
			Иначе		
				ВыгрузитьРеквизиты(Ссылка.Метаданные().Реквизиты, Ссылка); 
			КонецЕсли;
			
			ЗнСпискаТаблиц = спТаблицВыгрузки.НайтиПоЗначению(Ссылка.Метаданные().Имя);
			Если ЗнСпискаТаблиц <> Неопределено Тогда
				Если ЗнСпискаТаблиц.Представление <> "ПартииТоваров" И СтрокаВыгрузитьДвижения(ЗнСпискаТаблиц.Представление,ИмяДокумента) = Неопределено Тогда
					ТекТабл = Ссылка.Метаданные().ТабличныеЧасти[ЗнСпискаТаблиц.Представление];
					ОбщееОписание = "  {""Таблица." + ТекТабл.Имя + """}";
					ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина);
				КонецЕсли;
			КонецЕсли;
			
			ИмяТаблицыВыгрузки = Неопределено;
			ЗнСпискаТаблиц = спТаблицВыгрузки.НайтиПоЗначению(Ссылка.Метаданные().Имя);
			Если ЗнСпискаТаблиц <> Неопределено Тогда
				Если ЗнСпискаТаблиц.Представление <> "ПартииТоваров" И СтрокаВыгрузитьДвижения(ЗнСпискаТаблиц.Представление,ИмяДокумента) = Неопределено  Тогда
					ИмяТаблицыВыгрузки = ЗнСпискаТаблиц.Представление;
				КонецЕсли;
			КонецЕсли;
			
			Если ИмяТаблицыВыгрузки = Неопределено Тогда
				Для Каждого ТекТабл Из Ссылка.Метаданные().ТабличныеЧасти Цикл
					ОбщееОписание = "  {""Таблица." + ТекТабл.Имя + """}";
					Если ТекТипЗн=Тип("ДокументСсылка.Выписка") И ТекТабл.Имя="Состав" Тогда
						ДополнительныйРеквизитВидПлатежа	= Новый Структура("Имя,Тип,Синоним,Комментарий", "ВидПлатежа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Вид платежа","");	
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина, ДополнительныйРеквизитВидПлатежа);
					Иначе
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина);
					КонецЕсли;
				КонецЦикла;
				
			Иначе
				ТекТабл = Ссылка.Метаданные().ТабличныеЧасти[ИмяТаблицыВыгрузки];
				Если ТекТабл <> Неопределено Тогда
					ОбщееОписание = "  {""Таблица." + ТекТабл.Имя + """}";
					Если ТекТипЗн=Тип("ДокументСсылка.Выписка") И ТекТабл.Имя="Состав" Тогда
						ДополнительныйРеквизитВидПлатежа	= Новый Структура("Имя,Тип,Синоним,Комментарий", "ВидПлатежа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Вид платежа","");	
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина, ДополнительныйРеквизитВидПлатежа);
					Иначе
						ВыгрузитьРеквизиты(ТекТабл.Реквизиты, Ссылка[ТекТабл.Имя], Истина);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		
			Если ЕстьКонтИнф Тогда
				ОбщееОписание = "  {""Таблица.КонтактнаяИнформация""}";
				ВыгрузитьРеквизиты(РезЗапросаКонтИнф.Колонки, ВыборкаКонтИнф, Истина);
			КонецЕсли;	
						
			Если ЭтоДокумент Тогда
				Для Каждого ТекДвижение	ИЗ Ссылка.Метаданные().Движения Цикл
					Если Найти(ТекДвижение.Имя, "ПартииТоваров") > 0 Тогда
						ОбщееОписание = "  {""Таблица.ПартииТоваров""}";
						ВыгрузитьРеквизиты(РезЗапросаПартииТоваров.Колонки, ВыборкаПартииТоваров, Истина);						
						//Прервать;
					КонецЕсли;
					Строка = СтрокаВыгрузитьДвижения(ТекДвижение.Имя,ИмяДокумента);
					Если Строка <> Неопределено Тогда
						ОбщееОписание = "  {""Таблица."+Строка.Наименование+"""}";
						ВыгрузитьРеквизиты(Строка.РезультатЗапроса.Колонки, Строка.РезультатЗапроса.Выбрать(), Истина);						
						//Прервать;
					КонецЕсли;					
					
				КонецЦикла;
				//Здесь выгрузим структуру табличной части СФ
				Если НЕ (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратПоставщику") И Ссылка.ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия) Тогда
					Если спТаблицВыгрузкиГТД.НайтиПоЗначению(Ссылка.Метаданные().Имя) <> Неопределено Тогда					
						СчетФактура = Документы.СчетФактураВыданный;		
						РеквизитыСФ = СчетФактура.СоздатьДокумент().Метаданные().ТабличныеЧасти.Товары.Реквизиты;		
						ДополнительныйРеквизит = Неопределено;
						Если Ссылка.Метаданные().Имя = "ОтчетКомиссионера" Тогда
							ДополнительныйРеквизит = Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Вознаграждение;
						ИначеЕсли Ссылка.Метаданные().Имя = "ЗаказНаряд" И Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("Источник")<>Неопределено Тогда
							ДополнительныйРеквизит = Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Источник;		
						КонецЕсли;																								
							
						ОбщееОписание = "  {""Таблица.СФ""}";
						ВыгрузитьРеквизиты(РеквизитыСФ, "", Истина,ДополнительныйРеквизит);									
					КонецЕсли;								
				КонецЕсли;
				
				Если спТаблицВыгрузкиГТДДоп.НайтиПоЗначению(Ссылка.Метаданные().Имя) <> Неопределено Тогда					
					ТекТабл = СформироватьТЧ_С_ГТД(Ссылка);		
					РеквизитыСФ = ТекТабл.Колонки;		
					ОбщееОписание = "  {""Таблица.СФ""}";
					ВыгрузитьРеквизиты(РеквизитыСФ, "", Истина);									
				КонецЕсли;	
								
				Если ЕстьТаблицаСписанийАвтомобилей Тогда
					ОбщееОписание = "  {""Таблица.СуммыСписанийАвтомобилей""}";
					ВыгрузитьРеквизиты(ТаблицаСписанийАвтомобилей.Колонки, ТаблицаСписанийАвтомобилей, Истина);
				КонецЕсли;				
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	
	Если XMLФорматВыгрузки Тогда
		// Теперь системная информация самого объекта
		ДокXML.ЗаписатьНачалоЭлемента("Объект");
		ДокXML.ЗаписатьАтрибут("_1С_ИдентификаторБД", СокрЛП(Ссылка.УникальныйИдентификатор()));
		ДокXML.ЗаписатьАтрибут("_1С_Уровень",         СокрЛП(ТекСтрокаОбъекта.Уровень));
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Ссылка)) Тогда
			ДокXML.ЗаписатьАтрибут("_1С_Владелец",     ?(Ссылка.Владелец = Неопределено, "", СокрЛП(Ссылка.Владелец.УникальныйИдентификатор())));
			Попытка
				ДокXML.ЗаписатьАтрибут("_1С_Родитель",     ?(Ссылка.Родитель.Пустая(), "", СокрЛП(Ссылка.Родитель.УникальныйИдентификатор())));
			Исключение
				ДокXML.ЗаписатьАтрибут("_1С_Родитель",     "");
			КонецПопытки; 
			ДокXML.ЗаписатьАтрибут("_1С_ЭтоГруппа",    СокрЛП(Ссылка.ЭтоГруппа));
			ДокXML.ЗаписатьАтрибут("_1С_Код",          СокрЛП(Ссылка.Код));
			ДокXML.ЗаписатьАтрибут("_1С_Наименование", СокрЛП(Ссылка.Наименование));
			ДокXML.ЗаписатьАтрибут("_1С_НаименованиеПредопределённого", ?(Ссылка.Предопределенный,Справочники[Ссылка.Метаданные().Имя].ПолучитьИмяПредопределенного(Ссылка),""));
			ДокXML.ЗаписатьАтрибут("_1С_ПометкаУдаления", СокрЛП(Ссылка.ПометкаУдаления));
		Иначе
			ДокXML.ЗаписатьАтрибут("_1С_Номер",    	СокрЛП(Ссылка.Номер));
			ДокXML.ЗаписатьАтрибут("_1С_Дата",     	СокрЛП(Ссылка.Дата));
			ДокXML.ЗаписатьАтрибут("_1С_Проведен", 	СокрЛП(Ссылка.Проведен));
			ДокXML.ЗаписатьАтрибут("_1С_ПометкаУдаления", СокрЛП(Ссылка.ПометкаУдаления));
		КонецЕсли;
		
	Иначе
		ИдентификаторБД = СокрЛП(Ссылка.УникальныйИдентификатор());
		Уровень = СокрЛП(ТекСтрокаОбъекта.Уровень);
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Ссылка)) Тогда
			Владелец =  ?(Ссылка.Владелец = Неопределено, "", СокрЛП(Ссылка.Владелец.УникальныйИдентификатор()));
			Попытка
				Родитель =  ?(Ссылка.Родитель.Пустая(), "", СокрЛП(Ссылка.Родитель.УникальныйИдентификатор()));
			Исключение
				Родитель =  "";
			КонецПопытки; 
			ЭтоГруппа =  СокрЛП(Ссылка.ЭтоГруппа);
			Код =  СокрЛП(Ссылка.Код);
			Наименование = СокрЛП(Ссылка.Наименование);
			ОбщееОписание = "    {" + Уровень + ",""" + ИдентификаторБД + """,""" + Владелец + """,""" + Родитель + """,""" + ЭтоГруппа + """,""" + ПредставлениеРеквизита(Код) + """," + ПредставлениеРеквизита(Наименование);
		Иначе
			НомерДок = СокрЛП(Ссылка.Номер);
			ДатаДок  = СокрЛП(Ссылка.Дата);
			Проведен = СокрЛП(Ссылка.Проведен);
			ОбщееОписание = "    {" + Уровень + ",""" + ИдентификаторБД + """,""" + НомерДок + """,""" + ДатаДок + """,""" + Проведен + """";
		КонецЕсли;
	КонецЕсли;
	
	// Ну и выгрузим значения реквизитов  
	Если ТекТипЗн=Тип("ДокументСсылка.КорректировкаПоступления") ИЛИ ТекТипЗн=Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
		ДополнительныйРеквизитНомерИсправления	= Новый Структура(
			"Имя,Тип,Синоним,Комментарий",
			"НомерИсправления",
			Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Номер исправления","");										
		ВыгрузитьРеквизиты(Ссылка.Метаданные().Реквизиты, Ссылка,,ДополнительныйРеквизитНомерИсправления); 
	Иначе		
		ВыгрузитьРеквизиты(Ссылка.Метаданные().Реквизиты, Ссылка); 
	КонецЕсли;

	НайденнаяСтрока			= спСправочникиСпецРеквизиты.Найти(ТекТипЗн, "Тип");
	Если НайденнаяСтрока <> Неопределено Тогда
		ДопОбъекты 			= НайденнаяСтрока.Объекты;
		ОбъектДляВыгрузки 	= ДопОбъекты.Получить(Ссылка.Наименование);
		Если (ДопОбъекты.Количество() > 0) И (ОбъектДляВыгрузки <> Неопределено) Тогда
			НаборРеквизитов	= Новый ТаблицаЗначений();
			НаборРеквизитов.Колонки.Добавить("Имя");
			НаборРеквизитов.Колонки.Добавить("Тип");
			НаборРеквизитов.Колонки.Добавить(НайденнаяСтрока.Реквизит);
			СтрокаДанных	= НаборРеквизитов.Добавить();
			СтрокаДанных.Имя= НайденнаяСтрока.Реквизит;
			МассивТипов		= Новый Массив();
			МассивТипов.Добавить(ТипЗнч(ОбъектДляВыгрузки));
			СтрокаДанных.Тип= Новый ОписаниеТипов(МассивТипов);
			СтрокаДанных[НайденнаяСтрока.Реквизит] = ОбъектДляВыгрузки;
			ВыгрузитьРеквизиты(НаборРеквизитов, СтрокаДанных);
		КонецЕсли;
	КонецЕсли;

	ИмяТаблицыВыгрузки	= Неопределено;
	ЗнСпискаТаблиц		= спТаблицВыгрузки.НайтиПоЗначению(Ссылка.Метаданные().Имя);
	Если ЗнСпискаТаблиц <> Неопределено Тогда
		Если ЗнСпискаТаблиц.Представление <> "ПартииТоваров" И СтрокаВыгрузитьДвижения(ЗнСпискаТаблиц.Представление,ИмяДокумента) = Неопределено Тогда
			ИмяТаблицыВыгрузки	= ЗнСпискаТаблиц.Представление;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяТаблицыВыгрузки = Неопределено Тогда
		Для Каждого ТекТабл Из Ссылка.Метаданные().ТабличныеЧасти Цикл
			Если XMLФорматВыгрузки Тогда
				ДокXML.ЗаписатьНачалоЭлемента("Таблица");
				ДокXML.ЗаписатьАтрибут("Наименование", ТекТабл.Имя);
			КонецЕсли;
			
			Для Каждого ТекСтрока Из Ссылка[ТекТабл.Имя] Цикл
				Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;
				
				ОбщееОписание = "    {""Таблица." + ТекТабл.Имя + """";
				//Для выписки необходимо выгрузить "Вид платежа" платежного документа основания
				Если ТекТипЗн=Тип("ДокументСсылка.Выписка") И ТекТабл.Имя="Состав" Тогда					
					ДополнительныйРеквизитВидПлатежа	= Новый Структура("Имя,Тип,Синоним,Комментарий", "ВидПлатежа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Вид платежа","");										
					ВыгрузитьРеквизиты(ТекТабл.Реквизиты, ТекСтрока,,ДополнительныйРеквизитВидПлатежа);
				Иначе
					ВыгрузитьРеквизиты(ТекТабл.Реквизиты, ТекСтрока);
				КонецЕсли;
				
				Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
			КонецЦикла;
			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		КонецЦикла;
		
	Иначе
		ТекТабл = Ссылка.Метаданные().ТабличныеЧасти[ИмяТаблицыВыгрузки];
		Если ТекТабл <> Неопределено Тогда
			Если XMLФорматВыгрузки Тогда
				ДокXML.ЗаписатьНачалоЭлемента("Таблица");
				ДокXML.ЗаписатьАтрибут("Наименование", ТекТабл.Имя);
			КонецЕсли;
			
			Для Каждого ТекСтрока Из Ссылка[ТекТабл.Имя] Цикл
				Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;
				
				ОбщееОписание = "    {""Таблица." + ТекТабл.Имя + """";
				//Для выписки необходимо выгрузить "Вид платежа" платежного документа основания
				Если ТекТипЗн=Тип("ДокументСсылка.Выписка") И ТекТабл.Имя="Состав" Тогда
					ДополнительныйРеквизитВидПлатежа	= Новый Структура("Имя,Тип,Синоним,Комментарий", "ВидПлатежа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)),"Вид платежа","");										
					ВыгрузитьРеквизиты(ТекТабл.Реквизиты, ТекСтрока,,ДополнительныйРеквизитВидПлатежа);
				Иначе
					ВыгрузитьРеквизиты(ТекТабл.Реквизиты, ТекСтрока);
				КонецЕсли;
				
				Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
			КонецЦикла;
			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если НЕ НеВыгружатьПартииТоваров Тогда
		Если XMLФорматВыгрузки Тогда
			ДокXML.ЗаписатьНачалоЭлемента("Таблица");
			ДокXML.ЗаписатьАтрибут("Наименование", "ПартииТоваров");
		КонецЕсли;
		
		Пока ВыборкаПартииТоваров.Следующий() Цикл
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;
			
			ОбщееОписание = "    {""Таблица.ПартииТоваров""";
			ВыгрузитьРеквизиты(РезЗапросаПартииТоваров.Колонки, ВыборкаПартииТоваров);
			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		КонецЦикла;	
		
		Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
	КонецЕсли;
	
	Если ВыгружатьКонтИнф Тогда
		Если XMLФорматВыгрузки Тогда
			ДокXML.ЗаписатьНачалоЭлемента("Таблица");
			ДокXML.ЗаписатьАтрибут("Наименование", "КонтактнаяИнформация");
		КонецЕсли;
		
		Пока ВыборкаКонтИнф.Следующий() Цикл
				
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;
			
			ОбщееОписание = "    {""Таблица.КонтактнаяИнформация""";
			ВыгрузитьРеквизиты(РезЗапросаКонтИнф.Колонки, ВыборкаКонтИнф);
			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		КонецЦикла;	
		
		Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
	КонецЕсли;	
	
	
	//Здесь нужно выгрузить все наши движения, которые мы насобирали и именно в той последовательности, в 
	//которой мы записывали структурную информацию.
	
	Если ЭтоДокумент Тогда
		Для Каждого ТекДвижение	ИЗ Ссылка.Метаданные().Движения Цикл
			Строка = СтрокаВыгрузитьДвижения(ТекДвижение.Имя,ИмяДокумента);
			Если Строка <> Неопределено И НЕ Строка.НеВыгружать Тогда
				Если XMLФорматВыгрузки Тогда
					ДокXML.ЗаписатьНачалоЭлемента("Таблица");
					ДокXML.ЗаписатьАтрибут("Наименование", Строка.Наименование);
				КонецЕсли;
				
				Выборка = Строка.РезультатЗапроса.Выбрать();
		
				Пока Выборка.Следующий() Цикл
					Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;
			
					ОбщееОписание = "    {""Таблица."+Строка.Наименование+"""";
					ВыгрузитьРеквизиты(Строка.РезультатЗапроса.Колонки, Выборка);
			
					Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
				КонецЦикла;	
		
				Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
			КонецЕсли;										
		КонецЦикла;
	КонецЕсли;		
	
	//Выгрузим в нужных документах табличную часть счета фактуры
	Если ЭтоДокумент и спТаблицВыгрузкиГТД.НайтиПоЗначению(Ссылка.Метаданные().Имя) <> Неопределено И НЕ (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратПоставщику") И Ссылка.ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	СчетФактураВыданный.Ссылка КАК СчетФактура
			|ИЗ
			|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
			|ГДЕ
			|	СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ и
			|	СчетФактураВыданный.ДокументОснование = &ДокументОснование";
		Запрос.УстановитьПараметр("ДокументОснование",Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		СчетФактураНеВведен = РезультатЗапроса.Пустой();
		Если СчетФактураНеВведен Тогда				
			СчетФактура = Документы.СчетФактураВыданный;
		КонецЕсли;														
				
		Если СчетФактураНеВведен Тогда
			СчетФактура = СчетФактура.СоздатьДокумент();
			Основание = Ссылка;
			СчетФактура.Заполнить(Основание);
			ЗагружатьИзСчетФактуры = Истина;
		Иначе
			Выборка =РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			СчетФактура = Выборка.СчетФактура;
		КонецЕсли;				
		
		ТекТаблСФ = СчетФактура.Товары;
		РеквизитыСФ = СчетФактура.Метаданные().ТабличныеЧасти.Товары.Реквизиты;				
		ЗагружатьИзСчетФактуры = Истина;				
		
		Если XMLФорматВыгрузки Тогда
			ДокXML.ЗаписатьНачалоЭлемента("Таблица");
			ДокXML.ЗаписатьАтрибут("Наименование", "СФ");
		КонецЕсли;		
		
		Для Каждого ТекСтрока Из ТекТаблСФ Цикл				
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;					
			ОбщееОписание = "    {""Таблица.СФ""";
			
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда					
				//Подменем строку. - для Отчета комиссионеру добавим вознаграждение
				//Проблема в том, что если несколько строк в сф соотв одной строке в тч документа,
				//то и вознаграждение должно уменьшится, пока этого не делаю.
					
				мСтрока = Новый Соответствие;
				Для каждого рекв из РеквизитыСФ цикл
					мСтрока.Вставить(рекв.Имя,ТекСтрока[рекв.Имя]);
				КонецЦикла;
				ИскомаяСтрокаОК = Ссылка["Товары"].Найти(ТекСтрока.Номенклатура,"Номенклатура");
				Если ИскомаяСтрокаОК <> Неопределено Тогда
					мСтрока.Вставить("Вознаграждение",ИскомаяСтрокаОК.Вознаграждение);					
				КонецЕсли;						
				//Это истина в конце нужна потому что РеквизитыСФ сейчас соответсвие. А там все проверяется
				//на табличные части, документы и т.д. Не красиво, но работает.
				ВыгрузитьРеквизиты(РеквизитыСФ, мСтрока,,Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Вознаграждение);						
			ИначеЕсли	Ссылка.Метаданные().Имя = "ЗаказНаряд" И Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Найти("Источник")<>Неопределено Тогда
				мСтрока = Новый Соответствие;
				Для каждого рекв из РеквизитыСФ цикл
					мСтрока.Вставить(рекв.Имя,ТекСтрока[рекв.Имя]);
				КонецЦикла;
				ИскомаяСтрокаЗН = Ссылка["Товары"].Найти(ТекСтрока.Номенклатура,"Номенклатура");
				Если ИскомаяСтрокаЗН <> Неопределено Тогда
					мСтрока.Вставить("Источник",ИскомаяСтрокаЗН.Источник);					
				КонецЕсли;						
				ВыгрузитьРеквизиты(РеквизитыСФ, мСтрока,,Ссылка.Метаданные().ТабличныеЧасти.Товары.Реквизиты.Источник);
			Иначе						
				ВыгрузитьРеквизиты(РеквизитыСФ, ТекСтрока);
			КонецЕсли;	
			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;					
		КонецЦикла;				
		форма = СчетФактура.ПолучитьФорму();
		Если форма.Открыта() Тогда Форма.Закрыть(); КонецЕсли;	
		
		Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
	КонецЕсли;			
	
	
	//Обработаем документы, для которых нет возможности открыть счет-фактуру, 
	//но которые могут списать товары с ГТД при переносе
	Если (ЭтоДокумент) И (спТаблицВыгрузкиГТДДоп.НайтиПоЗначению(Ссылка.Метаданные().Имя) <> Неопределено) Тогда
		
		ТекТаблСФ = СформироватьТЧ_С_ГТД(Ссылка);				
		РеквизитыСФ = ТекТаблСФ.Колонки;				
		
		ЗагружатьИзСчетФактуры = Истина;				
		
		Если XMLФорматВыгрузки Тогда
			ДокXML.ЗаписатьНачалоЭлемента("Таблица");
			ДокXML.ЗаписатьАтрибут("Наименование", "СФ");
		КонецЕсли;		
		
		Для Каждого ТекСтрока Из ТекТаблСФ Цикл				
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;					
			ОбщееОписание = "    {""Таблица.СФ""";
			
			ВыгрузитьРеквизиты(РеквизитыСФ, ТекСтрока);
		    			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;					
			
		КонецЦикла;				
				
		Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
	КонецЕсли;
	
	Если ЭтоДокумент И ЕстьТаблицаСписанийАвтомобилей Тогда
		Если XMLФорматВыгрузки Тогда
			ДокXML.ЗаписатьНачалоЭлемента("Таблица");
			ДокXML.ЗаписатьАтрибут("Наименование", "СуммыСписанийАвтомобилей");
		КонецЕсли;
		
		Для Каждого ТекСтрока Из ТаблицаСписанийАвтомобилей Цикл
				
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьНачалоЭлемента("Строка"); КонецЕсли;
			
			ОбщееОписание = "    {""Таблица.СуммыСписанийАвтомобилей""";
			ВыгрузитьРеквизиты(ТаблицаСписанийАвтомобилей.Колонки, ТекСтрока);
			
			Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		КонецЦикла;	
		
		Если XMLФорматВыгрузки Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
	КонецЕсли;	
	
	Если XMLФорматВыгрузки Тогда
		ДокXML.ЗаписатьКонецЭлемента();
		
	ИначеЕсли ЭтоДокумент Тогда
		Текст.ЗаписатьСтроку("");
	КонецЕсли;
	
	// Обновим информацию о выгруженных объектах
	Если ПоказатьВыгруженные Тогда
		КореньТипа = ?(ЭтоДокумент, КореньДокументы, КореньСправочники);
		КореньВидаСтр = СтрЗаменить(ТекТипЗн, ?(ЭтоДокумент, "Документ ссылка: ", "Справочник ссылка: "), "");
		
		КореньТипа.Количество = КореньТипа.Количество + 1;
		
		КореньВида = КореньТипа.Строки.Найти(КореньВидаСтр, "Объект");
		Если КореньВида = Неопределено Тогда
			КореньВида = КореньТипа.Строки.Добавить();
			КореньВида.Объект = КореньВидаСтр;
		КонецЕсли;
		КореньВида.Количество = КореньВида.Количество + 1;
		
		СтрокаОбъекта = КореньВида.Строки.Добавить();
		СтрокаОбъекта.Объект = Ссылка;
	КонецЕсли;
	
КонецПроцедуры	//	ВыгрузитьОбъект()

//Функция проверяет сущестование пути к каталогу
Функция ПроверитьСуществованияФайла(ВыбКаталог) Экспорт
	КаталогПроверки = Новый Файл(ВыбКаталог);
	
	Если НЕ КаталогПроверки.Существует() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

// Процедура инициации/выполнения запроса выгружаемых документов
Процедура Выгрузить() Экспорт
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	Состояние("Ждите. Идет поиск ссылок в документах ...");
	#КонецЕсли

	НачалоРаботы = ТекущаяДата();
	ЕстьОтборПоКонтрагенту = ?(спКонтрагенты.Количество() > 0, Истина, Ложь);
	ЕстьОтборПоПодразделению = ?(спПодразделения.Количество() > 0, Истина, Ложь);
	
	// начинаем формировать текст запроса
	ТекстЗапроса		= "";
	ТекстЗапросаСпец	= "";
	ПервыйЗапросСпец	= Истина;
	ПервыйЗапрос		= Истина;
	Для Каждого ТекСтрока Из ВидыДокументов Цикл
		Если ТекСтрока.Пометка = Неопределено Тогда ТекСтрока.Пометка = Ложь КонецЕсли;
		ЕстьПодразделение = ?(Метаданные.Документы[ТекСтрока.Объект].Реквизиты.Найти("ПодразделениеКомпании") = Неопределено, Ложь, Истина);
		Если (ТекСтрока.Пометка) И (спДокументовСпец.НайтиПоЗначению(ТекСтрока.Объект) <> Неопределено) Тогда 
			
			// объединим таблицы документов.
			Если Не(ПустаяСтрока(ТекстЗапросаСпец)) Тогда 
				ТекстЗапросаСпец = ТекстЗапросаСпец + "
				|ОБЪЕДИНИТЬ";
				ПервыйЗапросСпец	= Ложь;
			КонецЕсли;
			
			СтрУсловийСпец = "";
			Если ТекСтрока.ЕстьКонтрагент Тогда
				СтрУсловийСпец = ?(ЕстьОтборПоКонтрагенту, " ГДЕ
				|	Документ.Контрагент В (&спКонтрагент)", "");
			КонецЕсли;
			Если ЕстьПодразделение Тогда
				СтрУсловийСпец = ?(ЕстьОтборПоПодразделению, " ГДЕ
				|	Документ.ПодразделениеКомпании В (&спПодразделение)", "");
			КонецЕсли;
			
			Если НЕ обЗначениеНеЗаполнено(НачалоПериода) Тогда
				СтрУсловийСпец = СтрУсловийСпец + ?(СтрУсловийСпец = "", "ГДЕ
				|	", "
				|	И ") + "(Дата >= &НачалоПериода)";
			КонецЕсли;	
			Если НЕ обЗначениеНеЗаполнено(КонецПериода) Тогда
				СтрУсловийСпец = СтрУсловийСпец + ?(СтрУсловийСпец = "", "ГДЕ
				|	", "
				|	И ") + "(Дата <= &КонецПериода)";
			КонецЕсли;	
			Если НЕ обЗначениеНеЗаполнено(ВыбОрганизация) Тогда
				СтрУсловийСпец = СтрУсловийСпец + ?(СтрУсловийСпец = "", "ГДЕ
				|	", "
				|	И ") + "(Организация = &Организация)";
			КонецЕсли;
			Если ТекСтрока.Объект = "ЗаказНаряд" И Документы.ЗаказНаряд.ПустаяСсылка().Метаданные().Реквизиты.Найти("ДатаЗакрытия")<>Неопределено Тогда
				ТекстЗапросаСпец = ТекстЗапросаСпец + "
				|ВЫБРАТЬ
				|	""" + ТекСтрока.Объект + """ "+?(ПервыйЗапросСпец, " КАК Вид","")+",
				|	Документ.ДатаЗакрытия "+?(ПервыйЗапросСпец," КАК Дата","")+",
				|	Документ.Ссылка "+?(ПервыйЗапросСпец," КАК Ссылка","") + ?(обЗначениеНеЗаполнено(ВыбОрганизация), "", ",
				|	Документ.Организация "+?(ПервыйЗапросСпец," КАК Организация","")) + "
				|ИЗ 
				|	Документ." + ТекСтрока.Объект + " КАК Документ 
				|" + СтрЗаменить(СтрУсловийСпец,"Дата","ДатаЗакрытия");
			Иначе
				ТекстЗапросаСпец = ТекстЗапросаСпец + "
				|ВЫБРАТЬ
				|	""" + ТекСтрока.Объект + """ "+?(ПервыйЗапросСпец, " КАК Вид","")+",
				|	Документ.Дата "+?(ПервыйЗапросСпец," КАК Дата","")+",
				|	Документ.Ссылка "+?(ПервыйЗапросСпец," КАК Ссылка","") + ?(обЗначениеНеЗаполнено(ВыбОрганизация), "", ",
				|	Документ.Организация "+?(ПервыйЗапросСпец," КАК Организация","")) + "
				|ИЗ 
				|	Документ." + ТекСтрока.Объект + " КАК Документ 
				|" + СтрУсловийСпец;
			КонецЕсли;

			Продолжить;
		КонецЕсли;	
		
		Если НЕ ТекСтрока.Пометка Тогда Продолжить; КонецЕсли;
		
		// объединим таблицы документов.
		Если Не(ПустаяСтрока(ТекстЗапроса)) Тогда 
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ";
			ПервыйЗапрос	= Ложь;
		КонецЕсли;
		
		СтрУсловий = "";
		Если ТекСтрока.ЕстьКонтрагент Тогда
			СтрУсловий = СтрУсловий + ?(ЕстьОтборПоКонтрагенту, " ГДЕ
			|	Документ.Контрагент В(&спКонтрагент)", "");
		КонецЕсли;
		Если ЕстьПодразделение Тогда
			СтрУсловий = СтрУсловий + ?(ЕстьОтборПоПодразделению, " ГДЕ
			|	Документ.ПодразделениеКомпании В (&спПодразделение)", "");
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(НачалоПериода) Тогда
			СтрУсловий = СтрУсловий + ?(СтрУсловий = "", " ГДЕ
			|	", "
			|	И ") +"(Дата >= &НачалоПериода)";
		КонецЕсли;	
		Если НЕ обЗначениеНеЗаполнено(КонецПериода) Тогда
			СтрУсловий = СтрУсловий + ?(СтрУсловий = "",  "ГДЕ
			|	", "
			|	И ") + "(Дата <= &КонецПериода)";
		КонецЕсли;	
		Если НЕ обЗначениеНеЗаполнено(ВыбОрганизация) Тогда
			СтрУсловий = СтрУсловий + ?(СтрУсловий = "", "ГДЕ
			|	", "
			|	И ") + "(Организация = &Организация)";
		КонецЕсли;
		Если ТекСтрока.Объект = "ЗаказНаряд" Тогда
			Если Метаданные.Справочники.Найти("ВидыСостоянийЗаказНарядов")<>Неопределено Тогда
				СтрУсловий = СтрУсловий + ?(СтрУсловий = "", "ГДЕ
				|	", "
				|	И ") + "(Документ.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт))";
			КонецЕсли;
		КонецЕсли;
				
		//Сформируем строку с условием из документов без реквизита регламентированный учет
		ДокументБезРегл	= спДокументовБезРегл.НайтиПоЗначению(ТекСтрока.Объект);
				
		СтрУсловий = СтрУсловий + ?(ПустаяСтрока(СтрУсловий), "ГДЕ
		|	", "
		|	И ") + ?(ДокументБезРегл=Неопределено,"(Документ.РегламентированныйУчет = ИСТИНА) ","")+"
		|	"+?(ТекСтрока.Объект= "ПлатежноеПоручение","", ?(ДокументБезРегл=Неопределено,"И ","")+" (Документ.Проведен)");

		
		Если ТекСтрока.Объект = "ЗаказНаряд" И Документы.ЗаказНаряд.ПустаяСсылка().Метаданные().Реквизиты.Найти("ДатаЗакрытия")<>Неопределено Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	""" + ТекСтрока.Объект + """ "+?(ПервыйЗапрос,"КАК Вид","")+",
			|	Документ.ДатаЗакрытия "+?(ПервыйЗапрос," КАК Дата","")+",
			|	Документ.Ссылка " +?(ПервыйЗапрос," КАК Ссылка","") + ?(обЗначениеНеЗаполнено(ВыбОрганизация), "", ",
			|	Документ.Организация " + ?(ПервыйЗапрос, " КАК Организация","")) + "
			|ИЗ
			|	Документ." + ТекСтрока.Объект + " КАК Документ
			|" + СтрЗаменить(СтрУсловий,"Дата","ДатаЗакрытия");
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	""" + ТекСтрока.Объект + """ "+?(ПервыйЗапрос,"КАК Вид","")+",
			|	Документ.Дата "+?(ПервыйЗапрос," КАК Дата","")+",
			|	Документ.Ссылка " +?(ПервыйЗапрос," КАК Ссылка","") + ?(обЗначениеНеЗаполнено(ВыбОрганизация), "", ",
			|	Документ.Организация " + ?(ПервыйЗапрос, " КАК Организация","")) + "
			|ИЗ
			|	Документ." + ТекСтрока.Объект + " КАК Документ
			|" + СтрУсловий;
		КонецЕсли;

	КонецЦикла;
	
	Если (ПустаяСтрока(ТекстЗапроса)) И (ОбъектыКВыгрузке.Количество() = 0) И (ПустаяСтрока(ТекстЗапросаСпец)) Тогда
		#Если Клиент Тогда
		Предупреждение("Не выбрано ни одного вида документов!");
		#КонецЕсли
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда

		// создаем запрос
		Запрос = Новый Запрос();
		
		// параметры для отбора по дате
		СтрУсловий = "";
		Если НЕ обЗначениеНеЗаполнено(НачалоПериода) Тогда
			Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
		КонецЕсли;	
		Если НЕ обЗначениеНеЗаполнено(КонецПериода) Тогда
			Запрос.УстановитьПараметр("КонецПериода",  КонецДня(КонецПериода));
		КонецЕсли;	
		Если НЕ обЗначениеНеЗаполнено(ВыбОрганизация) Тогда
			Запрос.УстановитьПараметр("Организация",  ВыбОрганизация);
		КонецЕсли;
		Если ЕстьОтборПоКонтрагенту Тогда
			Запрос.УстановитьПараметр("спКонтрагент", спКонтрагенты);
		КонецЕсли;	
		Если ЕстьОтборПоПодразделению Тогда
			Запрос.УстановитьПараметр("спПодразделение", спПодразделения);
		КонецЕсли;	

		
		// упорядочим запрос по видам документов, датам и применим фильтр
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Документы.Вид КАК Вид,
		|	Документы.Дата КАК Дата,
		|	Документы.Ссылка КАК Ссылка"+?(обЗначениеНеЗаполнено(ВыбОрганизация),"",",
		|	Документы.Организация КАК Организация")+"
		|
		|ИЗ
		|(" + ТекстЗапроса + ") КАК Документы";	
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Документы.Вид,
		|	Документы.Дата,
		|	Документы.Ссылка";
		Запрос.Текст = ТекстЗапроса;
		
		РезЗапроса = Запрос.Выполнить();
		Если (РезЗапроса.Пустой()) И (ПустаяСтрока(ТекстЗапросаСпец)) Тогда
			#Если Клиент Тогда
			Предупреждение("Не обнаружено документов, подлежащих выгрузке!");
			Состояние("");
			#КонецЕсли
			Возврат;			   
		КонецЕсли;
		
		Выборка = РезЗапроса.Выбрать();
	КонецЕсли;	
		
	// Объекты, подлежащие выгрузке, накапливаем отдельно для справочников и документов
	тзЭлементов = Новый ТаблицаЗначений;
	тзЭлементов.Колонки.Добавить("Тип");
	тзЭлементов.Колонки.Добавить("Уровень");
	тзЭлементов.Колонки.Добавить("Ссылка");
	
	тзДоков = Новый ТаблицаЗначений;
	тзДоков.Колонки.Добавить("Тип");
	тзДоков.Колонки.Добавить("Уровень");
	тзДоков.Колонки.Добавить("Ссылка");
	
	Если ПоказыватьДеревоСсылок Тогда
		ДеревоСсылок = Новый ДеревоЗначений;
		ДеревоСсылок.Колонки.Добавить("Ссылка");
		ДеревоСсылок.Колонки.Добавить("ТипЗначения",,"Тип ссылки");
	КонецЕсли;

	//Справочники, для которых надо выгружать Контактную информацию
	спСправочникиКонтИнф = Новый СписокЗначений();
	спСправочникиКонтИнф.Добавить(Тип("СправочникСсылка.Контрагенты"));
	Если кАмбулатория Тогда
		спСправочникиКонтИнф.Добавить(Тип("СправочникСсылка.Организации"));
		спСправочникиКонтИнф.Добавить(Тип("СправочникСсылка.Пациенты"));
	КонецЕсли;
	
	//Список документов, справочников, которые не нужно выгружать
	спНеВыгружать = Новый Массив();
	Если кУР Тогда
		спНеВыгружать.Добавить(Тип("ДокументСсылка.Рецептура"));
		спНеВыгружать.Добавить(Тип("ДокументСсылка.ПланМеню"));
	КонецЕсли;	
	
	//Справочники, для которых необходимо выгрузить дополнительные реквизиты
	МассивТипов = Новый массив();
	МассивТипов.Добавить(Тип("Соответствие"));
	спСправочникиСпецРеквизиты = Новый ТаблицаЗначений();
	спСправочникиСпецРеквизиты.Колонки.Добавить("Тип");
	спСправочникиСпецРеквизиты.Колонки.Добавить("Реквизит");
	спСправочникиСпецРеквизиты.Колонки.Добавить("ТекстЗапроса");
	спСправочникиСпецРеквизиты.Колонки.Добавить("ПарметрыЗапроса");
	спСправочникиСпецРеквизиты.Колонки.Добавить("Значение");
	спСправочникиСпецРеквизиты.Колонки.Добавить("Объекты", Новый ОписаниеТипов(МассивТипов));

	
	СтрокаспСправочникиСпецРеквизиты 	 			= спСправочникиСпецРеквизиты.Добавить();
	СтрокаспСправочникиСпецРеквизиты.Тип 			= Тип("СправочникСсылка.Организации");
    СтрокаспСправочникиСпецРеквизиты.Реквизит 		= "ФизЛицо";
	СтрокаспСправочникиСпецРеквизиты.ТекстЗапроса	= "ВЫБРАТЬ
	                                             	  |	СведенияКомпанииСрезПоследних.Значение
	                                             	  |ИЗ
	                                             	  |	РегистрСведений.СведенияКомпании.СрезПоследних КАК СведенияКомпанииСрезПоследних
	                                             	  |ГДЕ
	                                             	  |	СведенияКомпанииСрезПоследних.Организация = &Организация
	                                             	  |	И СведенияКомпанииСрезПоследних.Объект = &Объект";
	СтрокаспСправочникиСпецРеквизиты.ПарметрыЗапроса = "ЗапросСпецРеквизиты.УстановитьПараметр(""Организация"", ЗнРекв);
													   |ЗапросСпецРеквизиты.УстановитьПараметр(""Объект"", Перечисления.ВидыОбъектовСведений.Руководитель)";
													   
	Свойство	= ПланыВидовХарактеристик.НазначенияСвойствОбъектов.НайтиПоНаименованию("Счет кассы");												   
	//Для справочника КассыККМ выгружаем свойство счет кассы
	СтрокаспСправочникиСпецРеквизиты 	 			= спСправочникиСпецРеквизиты.Добавить();
	СтрокаспСправочникиСпецРеквизиты.Тип 			= Тип("СправочникСсылка.КассыККМ");
    СтрокаспСправочникиСпецРеквизиты.Реквизит 		= "КоррСчет";
	СтрокаспСправочникиСпецРеквизиты.ТекстЗапроса	= "ВЫБРАТЬ
	                                             	  |	ЗначенияСвойствОбъектов.Значение КАК Значение
	                                             	  |ИЗ
	                                             	  |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                                             	  |ГДЕ
	                                             	  |	ЗначенияСвойствОбъектов.Объект = &Объект
	                                             	  |	И ЗначенияСвойствОбъектов.Свойство = &Свойство";
	СтрокаспСправочникиСпецРеквизиты.ПарметрыЗапроса = "ЗапросСпецРеквизиты.УстановитьПараметр(""Объект"", ЗнРекв);
														|ЗапросСпецРеквизиты.УстановитьПараметр(""Свойство"", ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию(""Корр. счет""));";	
														
	//Для справочника КассыКомпании выгружаем свойство счет кассы
	СтрокаспСправочникиСпецРеквизиты 	 			= спСправочникиСпецРеквизиты.Добавить();
	СтрокаспСправочникиСпецРеквизиты.Тип 			= Тип("СправочникСсылка.КассыКомпании");
    СтрокаспСправочникиСпецРеквизиты.Реквизит 		= "КоррСчет";
	СтрокаспСправочникиСпецРеквизиты.ТекстЗапроса	= "ВЫБРАТЬ
	                                             	  |	ЗначенияСвойствОбъектов.Значение КАК Значение
	                                             	  |ИЗ
	                                             	  |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                                             	  |ГДЕ
	                                             	  |	ЗначенияСвойствОбъектов.Объект = &Объект
	                                             	  |	И ЗначенияСвойствОбъектов.Свойство = &Свойство";
	СтрокаспСправочникиСпецРеквизиты.ПарметрыЗапроса = "ЗапросСпецРеквизиты.УстановитьПараметр(""Объект"", ЗнРекв);
														|ЗапросСпецРеквизиты.УстановитьПараметр(""Свойство"", ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию(""Корр. счет""));";
														
	//Для справочника ВидыРемонта выгружаем свойство КомлектацияАвтомобиля
	//Признак того, что выгружаемый вид ремонта является комплектацией автомобиля
	ЗапросВидовРемонта	= Новый Запрос();
	ЗапросВидовРемонта.Текст	= "ВЫБРАТЬ
	                        	  |	ВидыРемонта.Ссылка КАК Ссылка,
	                        	  |	ВидыРемонта.Наименование КАК наименование
	                        	  |ИЗ
	                        	  |	Справочник.ВидыРемонта КАК ВидыРемонта
	                        	  |ГДЕ
	                        	  |	ВидыРемонта.Предопределенный
	                        	  |	И ВидыРемонта.Наименование ПОДОБНО &Наименование";
	ЗапросВидовРемонта.УстановитьПараметр("Наименование", "%_омплектация%");
	РезульатаЗапросаПоВидамРемонта	= ЗапросВидовРемонта.Выполнить();
	
	Если НЕ РезульатаЗапросаПоВидамРемонта.Пустой() Тогда
		СтрокаспСправочникиСпецРеквизиты 	 			= спСправочникиСпецРеквизиты.Добавить();
		СтрокаспСправочникиСпецРеквизиты.Тип 			= Тип("СправочникСсылка.ВидыРемонта");
	    СтрокаспСправочникиСпецРеквизиты.Реквизит 		= "КомплектацияАвтомобиля";
		СтрокаспСправочникиСпецРеквизиты.Значение 		= Ложь;
		СтрокаспСправочникиСпецРеквизиты.ТекстЗапроса	= "ВЫБРАТЬ
		                                             	  |	ВЫБОР
		                                             	  |		КОГДА ВидыРемонта.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыРемонта.КомплектацияАвтомобиля)
		                                             	  |			ТОГДА ИСТИНА
		                                             	  |		ИНАЧЕ ЛОЖЬ
		                                             	  |	КОНЕЦ КАК Значение
		                                             	  |ИЗ
		                                             	  |	Справочник.ВидыРемонта КАК ВидыРемонта
		                                             	  |ГДЕ
		                                             	  |	ВидыРемонта.Ссылка = &Объект";
		СтрокаспСправочникиСпецРеквизиты.ПарметрыЗапроса = "ЗапросСпецРеквизиты.УстановитьПараметр(""Объект"", ЗнРекв);";
	КонецЕсли;
														
	// Сформируем список табличных частей документов для выгрузки                                      
	// Если при выгрузке документа идентификатор табличной части не входит в данный список,
	// то выгружаются все табличные части документа.
	// Если же в списке найден идентификатор документа, то выгружаются строки ТОЛЬКО указанной табличной части.
	// Примечание: таблица партионных движений выгружается всегда при наличии движений
	спТаблицВыгрузки = Новый СписокЗначений;
	спТаблицВыгрузки.Добавить("ВыпускПродукции",        "ПартииТоваров");
	спТаблицВыгрузки.Добавить("ЗакрытиеСмены",          "ПартииТоваров");
	спТаблицВыгрузки.Добавить("Комплектация",           "ПартииТоваров");
	спТаблицВыгрузки.Добавить("ПеремещениеТоваров",     "ПартииТоваров");
	спТаблицВыгрузки.Добавить("ПоступлениеДопРасходов", "ПартииТоваров");
	спТаблицВыгрузки.Добавить("Разделка",               "ПартииТоваров");
	спТаблицВыгрузки.Добавить("Разукомплектация",       "ПартииТоваров");
	спТаблицВыгрузки.Добавить("СписаниеТоваров",        "ПартииТоваров");
	спТаблицВыгрузки.Добавить("СписаниеСпеций",         "ПартииТоваров");
	спТаблицВыгрузки.Добавить("СписаниеАвтомобилей",    "ПартииТоваров");
	спТаблицВыгрузки.Добавить("ЗаказНаряд",         	"ТоварыВПроизводстве");
	
	//Сформируем список документов, у которых к реквизитам табличной части при выгрузке нужно добавить ГТД
	//Это делается путем ввода нового или открытий существующего счета-фактуры. И тем самым выгружается копия 
	//табличной части только уже с ГТД. Не все так гладко, у некоторых документов не совпадают реквизиты ТЧ и СФ.
	//Приходится выкручиваться по ходу ...
	спТаблицВыгрузкиГТД = Новый СписокЗначений();
	Если УчетГТД Тогда
		спТаблицВыгрузкиГТД.Добавить("РеализацияТоваров");
		спТаблицВыгрузкиГТД.Добавить("ОтчетКомиссионера");
		спТаблицВыгрузкиГТД.Добавить("ВозвратПоставщику");
		спТаблицВыгрузкиГТД.Добавить("ЗаказНаряд");	
		спТаблицВыгрузкиГТД.Добавить("РеализацияАвтомобилей");
		спТаблицВыгрузкиГТД.Добавить("ОтчетКомиссионераЗаАвтомобили");
		спТаблицВыгрузкиГТД.Добавить("ВозвратПоставщикуАвтомобилей");
	КонецЕсли;	
	
	//Список документов, для которых нельзя ввести счет-фактуру
	//но можно выгрузить номера ГТД, по которым будет проходить списание товаров в Бухгалтерии
	спТаблицВыгрузкиГТДДоп = Новый СписокЗначений();
	Если УчетГТД Тогда
		спТаблицВыгрузкиГТДДоп.Добавить("СписаниеТоваров");
		спТаблицВыгрузкиГТДДоп.Добавить("Инвентаризация");
		спТаблицВыгрузкиГТДДоп.Добавить("ПересортицаТоваров");
	КонецЕсли;	

	
	
	//Создадим и заполним таблицу регистров, которые нужно выгружать в добавок.
	//То есть если появляется необходимость в выгрузке еще какого-нибудь регистра, нужно добавить строчку к этой таблице.
	//Все должно само заработать.
	тзДвиженияКВыгрузке = Новый ТаблицаЗначений();
	тзДвиженияКВыгрузке.Колонки.Добавить("Наименование"); 		//Имя регистра 
	тзДвиженияКВыгрузке.Колонки.Добавить("ДобавлятьПоСсылкам"); //Нужно ли добавлять объекты по движениям регистра	
	тзДвиженияКВыгрузке.Колонки.Добавить("Конфигурация"); 		//Название конфигурации где это нужно делать.
	тзДвиженияКВыгрузке.Колонки.Добавить("НеВыгружать"); 		//Внутренняя переменная
	тзДвиженияКВыгрузке.Колонки.Добавить("РезультатЗапроса"); 	//Внутренняя переменная
	тзДвиженияКВыгрузке.Колонки.Добавить("ТекстЗапроса"); 		//Текст запроса к регистру
	тзДвиженияКВыгрузке.Колонки.Добавить("Документы"); 			//Список документов, для которых необходимо выгружать эти движения. Если список пуст, то для всех.
	
	Если кАвтосервис Тогда
		НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
		НоваяСтрока.Наименование = "Продажи";
		сзДокументы = Новый СписокЗначений;
		сзДокументы.Добавить("АктРазногласий");
		НоваяСтрока.Документы = сзДокументы;
		НоваяСтрока.ДобавлятьПоСсылкам = Ложь;	
		НоваяСтрока.Конфигурация = "Авто";
		НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
		                           |	Продажи.ПодразделениеКомпании,
		                           |	Продажи.Номенклатура,
		                           |	Продажи.Поставщик,
		                           |	Продажи.Покупатель,
		                           |	Продажи.СтатусПартии,
		                           |	Продажи.ХозОперация,
		                           |	Продажи.ДоговорВзаиморасчетов,
		                           |	Продажи.ХарактеристикаНоменклатуры,
		                           |	Продажи.СкладКомпании,
		                           |	Продажи.СтавкаНДС,
		                           |	Продажи.Партия,
		                           |	Продажи.Авторабота,
		                           |	Продажи.Проект,
		                           |	Продажи.Количество,
		                           |	Продажи.Сумма,
		                           |	Продажи.СуммаНДС,
		                           |	Продажи.СуммаСкидки,
		                           |	Продажи.СебестоимостьУпр,
		                           |	Продажи.Себестоимость,
		                           |	Продажи.СуммаНДСВходящий
		                           |ИЗ
		                           |	РегистрНакопления.Продажи КАК Продажи
		                           |ГДЕ
		                           |	Продажи.Регистратор = &Регистратор";
	Иначе
		НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
		НоваяСтрока.Наименование = "Продажи";
		НоваяСтрока.Документы = ПолучитьДокументыАмбулатории();
		НоваяСтрока.ДобавлятьПоСсылкам = Ложь;	
		НоваяСтрока.Конфигурация = "Амбулатория";
		НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
		                           |	Продажи.ПодразделениеКомпании,
		                           |	Продажи.Номенклатура,
		                           |	Продажи.Поставщик,
		                           |	Продажи.Покупатель,
		                           |	Продажи.СтатусПартии,
		                           |	Продажи.ХозОперация,
		                           |	Продажи.ДоговорВзаиморасчетов,
		                           |	Продажи.ХарактеристикаНоменклатуры,
		                           |	Продажи.СкладКомпании,
		                           |	Продажи.СтавкаНДС,
		                           |	Продажи.Партия,
		                           |	Продажи.Специалист,
		                           |	Продажи.Пациент,
		                           |	Продажи.Количество,
		                           |	Продажи.Сумма,
		                           |	Продажи.СуммаНДС,
		                           |	Продажи.СуммаСкидки,
		                           |	Продажи.СуммаУпр,
		                           |	Продажи.СебестоимостьУпр,
		                           |	Продажи.Себестоимость,
		                           |	Продажи.СуммаНДСВходящий	                           
		                           |ИЗ
		                           |	РегистрНакопления.Продажи КАК Продажи
		                           |ГДЕ
		                           |	Продажи.Регистратор = &Регистратор";
	КонецЕсли;
	
	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "ВзаиморасчетыКомпании";
	сзДокументы = Новый СписокЗначений;
	сзДокументы.Добавить("АктРазногласий");
	НоваяСтрока.Документы = сзДокументы;
	НоваяСтрока.ДобавлятьПоСсылкам = Ложь;	
	НоваяСтрока.Конфигурация = "Авто";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
	                           |	ВзаиморасчетыКомпании.Контрагент,
	                           |	ВзаиморасчетыКомпании.ДоговорВзаиморасчетов,
	                           |	ВзаиморасчетыКомпании.Сделка,
	                           |	ВзаиморасчетыКомпании.Сумма,
	                           |	ВзаиморасчетыКомпании.СуммаУпр,
	                           |	ВзаиморасчетыКомпании.СуммаБаз,
	                           |	ВзаиморасчетыКомпании.ХозОперация,
	                           |	ВзаиморасчетыКомпании.ВидОперации
	                           |ИЗ
	                           |	РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
	                           |ГДЕ
	                           |	ВзаиморасчетыКомпании.Регистратор = &Регистратор";

	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "ТоварыВПроизводстве";
	сзДокументы = Новый СписокЗначений;
	сзДокументы.Добавить("ЗаказНаряд");
	НоваяСтрока.Документы = сзДокументы;
	НоваяСтрока.ДобавлятьПоСсылкам = Ложь;	
	НоваяСтрока.Конфигурация = "Авто";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.Количество,
	|	ТоварыВПроизводстве.Партия,
	|	ТоварыВПроизводстве.СтатусПартии,
	|	ТоварыВПроизводстве.Цех,		
	|	ТоварыВПроизводстве.Сумма,
	|	ТоварыВПроизводстве.СуммаНДС,
	|	ТоварыВПроизводстве.СтавкаНДС
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор = &Регистратор";
	
	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "КомплектацияАвтомобилей";
	НоваяСтрока.ДобавлятьПоСсылкам = Истина;
	НоваяСтрока.Конфигурация = "Автосалон";
	НоваяСтрока.ТекстЗапроса ="ВЫБРАТЬ
	                          |	КомплектацияАвтомобилей.Номенклатура,
	                          |	КомплектацияАвтомобилей.Количество,
	                          |	КомплектацияАвтомобилей.СкладКомпании,
	                          |	КомплектацияАвтомобилей.Сумма,
	                          |	КомплектацияАвтомобилей.СуммаНДС,
	                          |	КомплектацияАвтомобилей.СтавкаНДС,
	                          |	КомплектацияАвтомобилей.ХозОперация,
							  |	КомплектацияАвтомобилей.Автомобиль
	                          |ИЗ
	                          |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	                          |ГДЕ
	                          |	КомплектацияАвтомобилей.Регистратор = &Регистратор";
							  
	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "ОстаткиАвтомобилей";
	НоваяСтрока.ДобавлятьПоСсылкам = Ложь;
	НоваяСтрока.Конфигурация = "Автосалон";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
	                           |	ОстаткиАвтомобилей.Автомобиль,
	                           |	ОстаткиАвтомобилей.Количество,
	                           |	ОстаткиАвтомобилей.СкладКомпании,
	                           |	ОстаткиАвтомобилей.СтатусПартии,
	                           |	ОстаткиАвтомобилей.Сумма,
	                           |	ОстаткиАвтомобилей.СуммаНДС,
	                           |	ОстаткиАвтомобилей.Партия
	                           |ИЗ
	                           |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	                           |ГДЕ
	                           |	ОстаткиАвтомобилей.Регистратор = &Регистратор";
							   
	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "РеализованныеТовары";
	сзДокументы = Новый СписокЗначений;
	сзДокументы.Добавить("ОтчетКомиссионера");
	сзДокументы.Добавить("РеализацияТоваров");
	сзДокументы.Добавить("ОтчетКомитенту");
	НоваяСтрока.Документы = сзДокументы;
	НоваяСтрока.ДобавлятьПоСсылкам = Истина;
	НоваяСтрока.Конфигурация = "";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
	                           |	РеализованныеТовары.Номенклатура,
	                           |	РеализованныеТовары.Количество,
	                           |	РеализованныеТовары.СуммаУпр,
	                           |	РеализованныеТовары.ДокументПередачи
	                           |ИЗ
	                           |	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
	                           |ГДЕ
	                           |	РеализованныеТовары.Регистратор = &Регистратор";
							   
	
	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "РеализованныеАвтомобили";
	НоваяСтрока.ДобавлятьПоСсылкам = Истина;
	НоваяСтрока.Конфигурация = "Автосалон";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
	                           |	РеализованныеАвтомобили.Автомобиль,
	                           |	РеализованныеАвтомобили.Количество,
	                           |	РеализованныеАвтомобили.СуммаУпр,
	                           |	РеализованныеАвтомобили.ДокументПередачи
	                           |ИЗ
	                           |	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
	                           |ГДЕ
	                           |	РеализованныеАвтомобили.Регистратор = &Регистратор";
							   
							   
							   
	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "Цены";
	сзДокументы = Новый СписокЗначений;
	сзДокументы.Добавить("ПоступлениеТоваров");
	сзДокументы.Добавить("Инвентаризация");
	сзДокументы.Добавить("ПеремещениеТоваров");
	сзДокументы.Добавить("ВводОстатковТоваров");
	сзДокументы.Добавить("Комплектация");
	сзДокументы.Добавить("Разукомплектация");
	сзДокументы.Добавить("ИзвлечениеТоваровИзПроизводства");
	сзДокументы.Добавить("ПересортицаТоваров");
    НоваяСтрока.Документы = сзДокументы;
	НоваяСтрока.ДобавлятьПоСсылкам = Истина;
	НоваяСтрока.Конфигурация = "";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ
	                           |	ЦеныСрезПоследних.ТипЦен,
	                           |	ЦеныСрезПоследних.Номенклатура,
	                           |	ЦеныСрезПоследних.Цена
	                           |ИЗ
	                           |	РегистрСведений.Цены.СрезПоследних(, Регистратор = &Регистратор) КАК ЦеныСрезПоследних";
							   

	НоваяСтрока = тзДвиженияКВыгрузке.Добавить();
	НоваяСтрока.Наименование = "ПрочиеАктивыВЭксплуатации";
	сзДокументы = Новый СписокЗначений;
	сзДокументы.Добавить("СписаниеАктивов");
    НоваяСтрока.Документы = сзДокументы;
	НоваяСтрока.ДобавлятьПоСсылкам = Истина;
	НоваяСтрока.Конфигурация = "";
	НоваяСтрока.ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	                           |	СписаниеАктивовАктивы.ПрочийАктив,
	                           |	ПрочиеАктивыВЭксплуатации.Регистратор КАК Партия,
	                           |	ПрочиеАктивыВЭксплуатации.МОЛ
	                           |ИЗ
	                           |	Документ.СписаниеАктивов.Активы КАК СписаниеАктивовАктивы
	                           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	                           |		ПО СписаниеАктивовАктивы.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив
	                           |ГДЕ
	                           |	СписаниеАктивовАктивы.Ссылка = &Регистратор
	                           |	И ПрочиеАктивыВЭксплуатации.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВводВЭксплуатацию)
	                           |	И ПрочиеАктивыВЭксплуатации.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)";
							   
							   
							   
	// Пройдемся по документам и накопим выгружаемые объекты
	ЗначениеИндикатора = 1;
	
	// Для того, что бы валюте регламентированного учета дать максимальный уровень 1,
	// добавим ссылку на него сейчас, это нбх для установки соответствия 810-му - 643-ей валюты
	Если НЕ Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить().Пустая() Тогда
		ДобавитьПоСсылкам(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),,Неопределено);
	КонецЕсли;	
	
	//Перед выгрузкой основной массы документов необходимо выгрузить документы партий товаров отрицательных остатков
	//Ссылки на них есть в шапке файла-выгрузки
	//Перед этим необходимо проверить, заполнены ли соответствующие константы
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		Пока Выборка.Следующий() Цикл
			#Если Клиент Тогда
			Состояние("Ждите. Идет поиск ссылок ... " + ЗначениеИндикатора + "/" + Выборка.Количество());
			#КонецЕсли
		
			ДобавитьПоСсылкам(Выборка.Ссылка,,Выборка.Ссылка);
			
			ЗначениеИндикатора = ЗначениеИндикатора + 1;
		КонецЦикла;	
	КонецЕсли;	
	
	//Выгрузка специальных документов (без признака регламентированного учета, не совершающих движения)
	Если НЕ ПустаяСтрока(ТекстЗапросаСпец)  Тогда
		Запрос = Новый Запрос();

		СтрУсловий = "";
		Если НЕ обЗначениеНеЗаполнено(НачалоПериода) Тогда
			Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
		КонецЕсли;	
		Если НЕ обЗначениеНеЗаполнено(КонецПериода) Тогда
			Запрос.УстановитьПараметр("КонецПериода",  КонецДня(КонецПериода));
		КонецЕсли;	
		Если НЕ обЗначениеНеЗаполнено(ВыбОрганизация) Тогда
			Запрос.УстановитьПараметр("Организация",  ВыбОрганизация);
		КонецЕсли;
		Если ЕстьОтборПоКонтрагенту Тогда
			Запрос.УстановитьПараметр("спКонтрагент", спКонтрагенты);
		КонецЕсли;
		Если ЕстьОтборПоПодразделению Тогда
			Запрос.УстановитьПараметр("спПодразделение", спПодразделения);
		КонецЕсли;

		// упорядочим запрос по видам документов, датам и применим фильтр
		ТекстЗапросаСпец = "
		|ВЫБРАТЬ 
		|	Документы.Вид КАК Вид,
		|	Документы.Дата КАК Дата,
		|	Документы.Ссылка КАК Ссылка
		|
		|ИЗ
		|(" + ТекстЗапросаСпец + ") КАК Документы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Документы.Вид,
		|	Документы.Дата,
		|	Документы.Ссылка";
		
		Запрос.Текст = ТекстЗапросаСпец;

		РезЗапроса = Запрос.Выполнить();
		Если НЕ РезЗапроса.Пустой() Тогда
			Выборка = РезЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				#Если Клиент Тогда
				Состояние("Ждите. Идет поиск ссылок ... " + ЗначениеИндикатора + "/" + Выборка.Количество());
				#КонецЕсли

				ДобавитьПоСсылкам(Выборка.Ссылка,,Выборка.Ссылка);
	
				ЗначениеИндикатора = ЗначениеИндикатора + 1;
			КонецЦикла;	

		КонецЕсли;
				
	КонецЕсли;	
	
	ЕстьВидыСостояний = (Метаданные.Справочники.Найти("ВидыСостоянийЗаказНарядов")<>Неопределено);

	ОбъектыКВыгрузке.Свернуть("Объект");
    Для Каждого Строка Из ОбъектыКВыгрузке Цикл
		#Если Клиент Тогда
		Состояние("Ждите. Идет поиск ссылок ... " + ЗначениеИндикатора + "/" + (ЗначениеИндикатора+ОбъектыКВыгрузке.Количество()));
		#КонецЕсли
		Если ТипЗнч(Строка.Объект) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			Если ЕстьВидыСостояний Тогда
				Если Строка.Объект.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
					ДобавитьПоСсылкам(Строка.Объект,,Строка.Объект);
				Иначе
					Сообщить(""+Строка.Объект+" не закрыт и не будет выгружен, за исключением случаев по ссылке как основание.");
				КонецЕсли;
			Иначе
				Если Строка.Объект.Состояние= Перечисления["СостояниеЗаказНаряда"].Закрыт Тогда
					ДобавитьПоСсылкам(Строка.Объект,,Строка.Объект);
				Иначе
					Сообщить(""+Строка.Объект+" не закрыт и не будет выгружен, за исключением случаев по ссылке как основание.");
				КонецЕсли;
			КонецЕсли;
		Иначе
			ДобавитьПоСсылкам(Строка.Объект,,Неопределено);
		КонецЕсли;
		
		ЗначениеИндикатора = ЗначениеИндикатора + 1;
    КонецЦикла;
	
	ЗначениеСчетчика     = 1;
	МаксЗначениеСчетчика = тзЭлементов.Количество() + тзДоков.Количество();
	
	// Формируем шапку файла-выгрузки
	ИмяФайла = "unload_" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd_HHmmss") + ?(XMLФорматВыгрузки, ".xml", ".txt");
	
	//Подготовим данные для выгрузки в первую строку(шапку) файла выгрузки
	ДатаВыгрузки = СокрЛП(ТекущаяДата());
	ПарамНачалоПериода = ?(НачалоПериода = Дата("00010101"), "", СокрЛП(Формат(НачалоПериода, "ДФ=dd.MM.yyyy")));
	ПарамКонецПериода = ?(КонецПериода = Дата("00010101"), "", СокрЛП(Формат(КонецПериода, "ДФ=dd.MM.yyyy")));
	ФлагРегламентированныйУчет = Строка(РегламентированныйУчет);
	Версия = "v8";
	СтратегияСписания = Строка(Константы.СтратегияСписанияПартийТоваровПоДатам.Получить());
	Если Константы.ПартияТоваровОтрицательныхОстатков.Получить() <> Документы.ПоступлениеТоваров.ПустаяСсылка() Тогда
		уиПартииОтрицательныхОстатков1 = Строка(Константы.ПартияТоваровОтрицательныхОстатков.Получить().УникальныйИдентификатор());
	Иначе
		уиПартииОтрицательныхОстатков1 = "";
	КонецЕсли;
	
	//В конфигурации "Управление Рестораном" есть дополнительная константа
	//"ПартияТоваровОтрицательныхОстатковДругихСкладов", её необходимо выгрузить.
	//В Альфа-Авто такой константы нет, и чтобы не нарушать порядок следования параметров будем писать пустую строку.
	Если кУР Тогда
		Если Константы.ПартияТоваровОтрицательныхОстатковДругихСкладов.Получить() <> Документы.ПоступлениеТоваров.ПустаяСсылка() Тогда
			уиПартииОтрицательныхОстатков2 = Строка(Константы.ПартияТоваровОтрицательныхОстатковДругихСкладов.Получить().УникальныйИдентификатор());
		Иначе
			уиПартииОтрицательныхОстатков2 = "";
		КонецЕсли;
	Иначе
		уиПартииОтрицательныхОстатков2 = "";
	КонецЕсли;
	
	Если XMLФорматВыгрузки Тогда
		ДокXML = Новый ЗаписьXML();
		ДокXML.ОткрытьФайл(ВыбКаталог + "/" + ИмяФайла, "UTF-8");
		ДокXML.ЗаписатьОбъявлениеXML();
		
		ДокXML.ЗаписатьНачалоЭлемента("БД");
		ДокXML.ЗаписатьАтрибут("Конфигурация",  Метаданные.Синоним);
		ДокXML.ЗаписатьАтрибут("ДатаВыгрузки",  ДатаВыгрузки);
		ДокXML.ЗаписатьАтрибут("НачалоПериода", ПарамНачалоПериода);
		ДокXML.ЗаписатьАтрибут("КонецПериода",  ПарамКонецПериода);
		ДокXML.ЗаписатьАтрибут("урРегламентированныйУчет", ФлагРегламентированныйУчет);
		ДокXML.ЗаписатьАтрибут("СтратегияСписанияПартийПоДатам", СтратегияСписания);
		ДокXML.ЗаписатьАтрибут("уиПартииОтрицательныхОстатков1", уиПартииОтрицательныхОстатков1);
		ДокXML.ЗаписатьАтрибут("уиПартииОтрицательныхОстатков2", уиПартииОтрицательныхОстатков2);
		ДокXML.ЗаписатьАтрибут("Версия",        Версия);
		ДокXML.ЗаписатьАтрибут("РежимВыгрузки", "Обычный");
	Иначе	
		Текст = Новый ЗаписьТекста(ВыбКаталог + "/" + ИмяФайла, КодировкаТекста.ANSI);
		
		СинонимКонфигурации = ПредставлениеРеквизита(СокрЛП(Метаданные.Синоним));
		
		// Шапка файла с идентификатором конфигурации
		ИдентификаторКонфигурации = "{""" + СинонимКонфигурации + """,""" + ДатаВыгрузки + """,""" + ПарамНачалоПериода + """,""" + ПарамКонецПериода + """,""" + ФлагРегламентированныйУчет + """,""" + СтратегияСписания + """,""" + уиПартииОтрицательныхОстатков1 + """,""" + уиПартииОтрицательныхОстатков2 + """,""" + "Обычный"  + ""","""  + Версия+ """}";
		
		// запишем строку
		Текст.ЗаписатьСтроку(ИдентификаторКонфигурации);
	КонецЕсли;
	
	// Инициализируем дерево выгруженных объектов
	Если ПоказатьВыгруженные Тогда
		ВыгруженныеОбъекты.Колонки.Очистить();
		ВыгруженныеОбъекты.Строки.Очистить();
		
		ВыгруженныеОбъекты.Колонки.Добавить("Объект");
		ВыгруженныеОбъекты.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		
		КореньСправочники = ВыгруженныеОбъекты.Строки.Добавить();
		КореньСправочники.Объект = "Справочники";
			
		КореньДокументы = ВыгруженныеОбъекты.Строки.Добавить();
		КореньДокументы.Объект = "Документы";
	КонецЕсли;
	
	//Выгрузим все цеха если есть документ Заказ няряд
	//Потому что для движений документов ссылки не выгружаются кроме партияобразующего документа
	Если тзДоков.Найти(Тип("ДокументСсылка.ЗаказНаряд"),"Тип") <> Неопределено Тогда
		Запрос= Новый  Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Цеха.Ссылка
		               |ИЗ
		               |	Справочник.Цеха КАК Цеха";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если тзЭлементов.Найти(Выборка.Ссылка,"Ссылка") = Неопределено Тогда
				ДобавитьПоСсылкам(Выборка.Ссылка,,Выборка.Ссылка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	// Сначала выгрузим справочники
	ПредТип = "";
	тзЭлементов.Сортировать("Тип,Уровень,Ссылка");
	
	ПервыйСправочник = Истина;
	ПервыйДокумент = Истина;
	
	Для Каждого ТекСтрока Из тзЭлементов Цикл
		#Если Клиент Тогда
		Состояние("Ждите. Идет формирование файла выгрузки ... " + ЗначениеСчетчика + "/" + МаксЗначениеСчетчика);
		#КонецЕсли
	
		Если ТекСтрока.Тип <> ПредТип Тогда
			ПервыйОбъект = Истина;
			
			Если XMLФорматВыгрузки Тогда
				Если ПервыйСправочник Тогда ДокXML.ЗаписатьНачалоЭлемента("Ссылки"); КонецЕсли;
			
			
				Если ПредТип <> "" Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
				
				ДокXML.ЗаписатьНачалоЭлемента(ТекСтрока.Ссылка.Метаданные().Имя);
			КонецЕсли;
		КонецЕсли;
		
		ВыгрузитьОбъект(ТекСтрока);
		
		Если ПервыйОбъект Тогда ПервыйОбъект = Ложь; КонецЕсли;
		Если ПервыйСправочник Тогда ПервыйСправочник = Ложь; КонецЕсли;
		
		ПредТип = ТекСтрока.Тип;
		
		ЗначениеСчетчика = ЗначениеСчетчика + 1;
	КонецЦикла;	
	
	Если XMLФорматВыгрузки Тогда
		Если ПредТип <> "" Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		
		ДокXML.ЗаписатьКонецЭлемента();
	КонецЕсли;	
	
	
	// Теперь можно выгрузить документы
	ПредТип = "";
	СтавитьЗакрывающиеТэги = Ложь;
	
	Если Константы.ПартияТоваровОтрицательныхОстатков.Получить() <> Документы.ПоступлениеТоваров.ПустаяСсылка() Тогда
   		СтрокаПОО = тзДоков.Найти(Константы.ПартияТоваровОтрицательныхОстатков.Получить(), "Ссылка");
		Если СтрокаПОО <> Неопределено Тогда
			СтрокаПОО.Уровень = 1;
		КонецЕсли;	
	КонецЕсли;	
	Если кУР Тогда
		Если Константы.ПартияТоваровОтрицательныхОстатковДругихСкладов.Получить() <> Документы.ПоступлениеТоваров.ПустаяСсылка() Тогда
			СтрокаПООДС = тзДоков.Найти(Константы.ПартияТоваровОтрицательныхОстатковДругихСкладов.Получить(), "Ссылка");
			Если СтрокаПООДС <> Неопределено Тогда
				СтрокаПООДС.Уровень = 1;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
		
	тзДоков.Сортировать("Уровень,Тип,Ссылка");
	
	Если XMLФорматВыгрузки Тогда				
		ДокXML.ЗаписатьНачалоЭлемента("Документы"); 
	КонецЕсли;	
	Для Каждого ТекСтрока Из тзДоков Цикл
		
		#Если Клиент Тогда
		Состояние("Ждите. Идет формирование файла выгрузки ... " + ЗначениеСчетчика + "/" + МаксЗначениеСчетчика);
		#КонецЕсли
	
		Если ТекСтрока.Тип <> ПредТип Тогда ПервыйОбъект = Истина;
			Если XMLФорматВыгрузки Тогда				
			
				Если СтавитьЗакрывающиеТэги Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
				СтавитьЗакрывающиеТэги = Ложь;
				
				//Так как дальше у нас стоит выгрузка при условии, то и открывающие тэги мы ставим при условии.
				Если кУР Тогда
					ДокXML.ЗаписатьНачалоЭлемента(ТекСтрока.Ссылка.Метаданные().Имя);					
					СтавитьЗакрывающиеТэги = Истина;
				Иначе
					Если ТекСтрока.Ссылка.Метаданные().Реквизиты.Найти("РегламентированныйУчет")<>Неопределено ИЛИ
						 спДокументовБезРегл.НайтиПоЗначению(ТекСтрока.Ссылка.Метаданные().Имя) <> Неопределено ИЛИ
						 спДокументовСпец.НайтиПоЗначению(ТекСтрока.Ссылка.Метаданные().Имя) <> Неопределено Тогда
							ДокXML.ЗаписатьНачалоЭлемента(ТекСтрока.Ссылка.Метаданные().Имя);				
							СтавитьЗакрывающиеТэги = Истина;
					КонецЕсли; 			
				КонецЕсли;									
			КонецЕсли;
		КонецЕсли;
		
		//Если УР, все выгружаем, если нет, то задумаемся
		Если кУР Тогда
			ВыгрузитьОбъект(ТекСтрока);
			Если ПервыйОбъект Тогда ПервыйОбъект = Ложь; КонецЕсли;
			Если ПервыйДокумент Тогда ПервыйДокумент = Ложь; КонецЕсли;
		
			ПредТип = ТекСтрока.Тип;
		
			ЗначениеСчетчика = ЗначениеСчетчика + 1;
		Иначе
			Если ТекСтрока.Ссылка.Метаданные().Реквизиты.Найти("РегламентированныйУчет")<>Неопределено ИЛИ 
				 спДокументовБезРегл.НайтиПоЗначению(ТекСтрока.Ссылка.Метаданные().Имя) <> Неопределено ИЛИ
				 спДокументовСпец.НайтиПоЗначению(ТекСтрока.Ссылка.Метаданные().Имя) <> Неопределено	Тогда
           
				ВыгрузитьОбъект(ТекСтрока);
				Если ПервыйОбъект Тогда ПервыйОбъект = Ложь; КонецЕсли;
				Если ПервыйДокумент Тогда ПервыйДокумент = Ложь; КонецЕсли;
				
				ПредТип = ТекСтрока.Тип;
				
				ЗначениеСчетчика = ЗначениеСчетчика + 1; 
			КонецЕсли; 			
		КонецЕсли;		
	КонецЦикла;	
	
	Если XMLФорматВыгрузки Тогда
		Если ПредТип <> "" Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		ДокXML.ЗаписатьКонецЭлемента();
		
		// Завершим работу с файлом-выгрузки
		ДокXML.ЗаписатьКонецЭлемента();
		ДокXML.Закрыть();
		
	Иначе
		Текст.Закрыть();
	КонецЕсли;
	
	#Если Клиент Тогда
		
	Состояние("Формирование файла выгрузки завершено!");
	Сообщить("ВЫГРУЗКА ЗАВЕРШЕНА! Имя файла: " + ВыбКаталог + "\" + ИмяФайла);
	Сообщить("	Начало формирования файла выгрузки: " + НачалоРаботы + ", окончание: " + ТекущаяДата());
	
	#КонецЕсли

КонецПроцедуры	//	Выгрузить()

// Процедура инициации/выполнения запроса выгружаемых документов
Процедура ВыгрузитьСправочники() Экспорт
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	Состояние("Ждите. Идет поиск ссылок в справочниках ...");
	#КонецЕсли

	НачалоРаботы = ТекущаяДата();	
	
	ВыбраныБанки = Ложь;
	ВыбраныБанковскиеСчета = Ложь;
	ВыбранаВалюта = Ложь;
	ВыбраныКонтрагенты = Ложь;
	ВыбраныДоговоры = Ложь;
	
	ТекстЗапроса = "";
	Для Каждого ТекСтрока Из ВидыСправочников Цикл
		Если ТекСтрока.Пометка = Неопределено Тогда ТекСтрока.Пометка = Ложь КонецЕсли;
		Если НЕ ТекСтрока.Пометка Тогда Продолжить; КонецЕсли;
		
		Если ТекСтрока.Объект = "Банки" Тогда 
			ВыбраныБанки = истина;
		ИначеЕсли ТекСтрока.Объект = "БанковскиеСчета" Тогда
			ВыбраныБанковскиеСчета = Истина;
		ИначеЕсли ТекСтрока.Объект = "Валюты" Тогда
			ВыбранаВалюта = Истина;
		ИначеЕсли ТекСтрока.Объект = "Контрагенты" Тогда
			ВыбраныКонтрагенты = Истина;
		ИначеЕсли ТекСтрока.Объект = "ДоговорыВзаиморасчетов" ИЛИ ТекСтрока.Объект = "Сотрудники" Тогда
			ВыбраныДоговоры = Истина;
			Если ОрганизацияСпр.Пустая() Тогда
				#Если Клиент Тогда
				Предупреждение("Не выбрана Организация для " + ?(ТекСтрока.Объект = "ДоговорыВзаиморасчетов", "договоров взаиморасчетов","сотрудников") + "!");
				#КонецЕсли
	            Возврат;
			КонецЕсли;	
		КонецЕсли;		
		
		// объединим таблицы 
		Если Не(ПустаяСтрока(ТекстЗапроса)) Тогда 
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ "+ ТекСтрока.Объект + ".Ссылка 
		|ИЗ Справочник." + ТекСтрока.Объект + " КАК "+ТекСтрока.Объект;
		
		Если ТекСтрока.Объект = "ДоговорыВзаиморасчетов" ИЛИ ТекСтрока.Объект = "Сотрудники" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ "+ ТекСтрока.Объект + ".Организация = &Организация ";
		КонецЕсли;	
		
	КонецЦикла;
	
	Если ТекстЗапроса = "" и ОрганизацияСпр.Пустая() Тогда
		#Если Клиент Тогда
		Предупреждение("Не выбрано ни одного вида справочников!");
		#КонецЕсли
	
		Возврат;
	КонецЕсли;
	
	Если НЕ ВыгружатьСправочникиПоСсылкам и ВыбраныБанковскиеСчета и (НЕ ВыбранаВалюта или НЕ ВыбраныБанки) Тогда
		Сообщить("Банковские счета можно выгружать только вместе с банками и валютой!");
		Возврат;
	КонецЕсли;
	
	Если НЕ ВыгружатьСправочникиПоСсылкам и ВыбраныДоговоры и (НЕ ВыбранаВалюта или НЕ ВыбраныКонтрагенты) Тогда
		Сообщить("Договоры взаиморасчетов можно выгружать только вместе с контрагентами и валютой!");
		Возврат;
	КонецЕсли;
    		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Если ТекстЗапроса <> "" Тогда
		Запрос.УстановитьПараметр("Организация", ОрганизацияСпр);
		РезЗапроса = Запрос.Выполнить();
		Если РезЗапроса.Пустой() Тогда
			#Если Клиент Тогда
			Предупреждение("Не справочников подлежащих выгрузке!");
			Состояние("");
		    #КонецЕсли
			Возврат;			   
		КонецЕсли;
		Выборка = РезЗапроса.Выбрать();
	Конецесли;	
	
	//Справочники, для которых надо выгружать Контактную информацию
	спСправочникиКонтИнф = Новый СписокЗначений();
	спСправочникиКонтИнф.Добавить(Тип("СправочникСсылка.Контрагенты"));
	Если кАмбулатория Тогда
		спСправочникиКонтИнф.Добавить(Тип("СправочникСсылка.Организации"));
    КонецЕсли;
	
	// Объекты, подлежащие выгрузке, накапливаем отдельно для справочников и документов
	тзЭлементов = Новый ТаблицаЗначений;
	тзЭлементов.Колонки.Добавить("Тип");
	тзЭлементов.Колонки.Добавить("Уровень");
	тзЭлементов.Колонки.Добавить("Ссылка");
	ЗначениеИндикатора = 1;	

	спНеВыгружать = Новый Массив();
	Если кУР Тогда
		спНеВыгружать.Добавить(Тип("ДокументСсылка.Рецептура"));
		спНеВыгружать.Добавить(Тип("ДокументСсылка.ПланМеню"));
	КонецЕсли;
	
	//Справочники, для которых необходимо выгрузить дополнительные реквизиты
	МассивТипов = Новый массив();
	МассивТипов.Добавить(Тип("Соответствие"));
	спСправочникиСпецРеквизиты = Новый ТаблицаЗначений();
	спСправочникиСпецРеквизиты.Колонки.Добавить("Тип");
	спСправочникиСпецРеквизиты.Колонки.Добавить("Реквизит");
	спСправочникиСпецРеквизиты.Колонки.Добавить("ТекстЗапроса");
	спСправочникиСпецРеквизиты.Колонки.Добавить("ПарметрыЗапроса");
	спСправочникиСпецРеквизиты.Колонки.Добавить("Значение");
	спСправочникиСпецРеквизиты.Колонки.Добавить("Объекты", Новый ОписаниеТипов(МассивТипов));
   	
	Если НЕ ВыгружатьСправочникиПоСсылкам Тогда
		Если НЕ ОрганизацияСпр.Пустая() Тогда
			ТекСтрока = тзЭлементов.Добавить();
			ТекСтрока.Тип = ТипЗнч(ОрганизацияСпр);
			ТекСтрока.Уровень = 1;
			ТекСтрока.Ссылка  = ОрганизацияСпр.Ссылка;		
		КонецЕсли;	
			
		Пока ТекстЗапроса <> "" и Выборка.Следующий() Цикл
			ТекСтрока = тзЭлементов.Добавить();		
			ТекСтрока.Тип     = ТипЗнч(Выборка.Ссылка);
			ТекСтрока.Уровень = 1;
			ТекСтрока.Ссылка  = Выборка.Ссылка;		
		КонецЦикла;
	Иначе
		Если НЕ ОрганизацияСпр.Пустая() Тогда 
			ДобавитьПоСсылкам(ОрганизацияСпр,,Неопределено);
		КонецЕсли;	
		
		Пока ТекстЗапроса <> "" и Выборка.Следующий() Цикл
			#Если Клиент Тогда
			Состояние("Ждите. Идет поиск ссылок ... " + ЗначениеИндикатора + "/" + Выборка.Количество());
			#КонецЕсли
		
			ДобавитьПоСсылкам(Выборка.Ссылка,,Неопределено);
			
			ЗначениеИндикатора = ЗначениеИндикатора + 1;
		КонецЦикла;			
	КонецЕсли;	
	
	//Бесполезные приготовления для выгрузки документов нужны, чтобы механизм выгрузки чувствовал себя как надо	
	тзДоков = Новый ТаблицаЗначений;
	тзДоков.Колонки.Добавить("Тип");
	тзДоков.Колонки.Добавить("Уровень");
	тзДоков.Колонки.Добавить("Ссылка");	
	
	спТаблицВыгрузки = Новый СписокЗначений;
	
	тзДвиженияКВыгрузке = Новый ТаблицаЗначений();
	тзДвиженияКВыгрузке.Колонки.Добавить("Наименование"); //Имя регистра 
	тзДвиженияКВыгрузке.Колонки.Добавить("ДобавлятьПоСсылкам"); //Нужно ли добавлять объекты по движениям регистра	
	тзДвиженияКВыгрузке.Колонки.Добавить("Конфигурация"); //Название конфигурации где это нужно делать.
	тзДвиженияКВыгрузке.Колонки.Добавить("НеВыгружать"); //Внутренняя переменная
	тзДвиженияКВыгрузке.Колонки.Добавить("РезультатЗапроса"); //Внутренняя переменная
	тзДвиженияКВыгрузке.Колонки.Добавить("ТекстЗапроса"); //Текст запроса к регистру
	тзДвиженияКВыгрузке.Колонки.Добавить("Документы"); //Список документов, для которых необходимо выгружать эти движения.
													   //Если список пуст, то для всех.
	
	
	ЗначениеСчетчика     = 1;
	МаксЗначениеСчетчика = тзЭлементов.Количество() + тзДоков.Количество();
	
	// Формируем шапку файла-выгрузки
	ИмяФайла = "unload_" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd_HHmmss") + ?(XMLФорматВыгрузки, ".xml", ".txt");
	
	//Подготовим данные для выгрузки в первую строку(шапку) файла выгрузки
	ДатаВыгрузки = СокрЛП(ТекущаяДата());
	ПарамНачалоПериода = ?(НачалоПериода = Дата("00010101"), "", СокрЛП(Формат(НачалоПериода, "ДФ=dd.MM.yyyy")));
	ПарамКонецПериода = ?(КонецПериода = Дата("00010101"), "", СокрЛП(Формат(КонецПериода, "ДФ=dd.MM.yyyy")));
	ФлагРегламентированныйУчет = Строка(РегламентированныйУчет);
	Версия = "v8";
	СтратегияСписания = Строка(Константы.СтратегияСписанияПартийТоваровПоДатам.Получить());
	Если Константы.ПартияТоваровОтрицательныхОстатков.Получить() <> Документы.ПоступлениеТоваров.ПустаяСсылка() Тогда
		уиПартииОтрицательныхОстатков1 = Строка(Константы.ПартияТоваровОтрицательныхОстатков.Получить().УникальныйИдентификатор());
	Иначе
		уиПартииОтрицательныхОстатков1 = "";
	КонецЕсли;
	
	//В конфигурации "Управление Рестораном" есть дополнительная константа "ПартияТоваровОтрицательныхОстатковДругихСкладов", её необходимо выгрузить.
	//В Альфа-Авто такой константы нет, и чтобы не нарушать порядок следования параметров будем писать пустую строку.
	Если кУР Тогда
		Если Константы.ПартияТоваровОтрицательныхОстатковДругихСкладов.Получить() <> Документы.ПоступлениеТоваров.ПустаяСсылка() Тогда
			уиПартииОтрицательныхОстатков2 = Строка(Константы.ПартияТоваровОтрицательныхОстатковДругихСкладов.Получить().УникальныйИдентификатор());
		Иначе
			уиПартииОтрицательныхОстатков2 = "";
		КонецЕсли;
	Иначе
		уиПартииОтрицательныхОстатков2 = "";
	КонецЕсли;
	
	Если ВыгружатьСправочникиДляНахожденияСоответсвтий Тогда
		РежимВыгрузки = "Справочники_длясоответствия";
	Иначе
		РежимВыгрузки = "Справочники";
	КонецЕсли;	
		
	Если XMLФорматВыгрузки Тогда
		ДокXML = Новый ЗаписьXML();
		ДокXML.ОткрытьФайл(ВыбКаталог + "/" + ИмяФайла, "UTF-8");
		ДокXML.ЗаписатьОбъявлениеXML();
		
		ДокXML.ЗаписатьНачалоЭлемента("БД");
		ДокXML.ЗаписатьАтрибут("Конфигурация",  Метаданные.Синоним);
		ДокXML.ЗаписатьАтрибут("ДатаВыгрузки",  ДатаВыгрузки);
		ДокXML.ЗаписатьАтрибут("НачалоПериода", ПарамНачалоПериода);
		ДокXML.ЗаписатьАтрибут("КонецПериода",  ПарамКонецПериода);
		ДокXML.ЗаписатьАтрибут("урРегламентированныйУчет", ФлагРегламентированныйУчет);
		ДокXML.ЗаписатьАтрибут("СтратегияСписанияПартийПоДатам", СтратегияСписания);
		ДокXML.ЗаписатьАтрибут("уиПартииОтрицательныхОстатков1", уиПартииОтрицательныхОстатков1);
		ДокXML.ЗаписатьАтрибут("уиПартииОтрицательныхОстатков2", уиПартииОтрицательныхОстатков2);
		ДокXML.ЗаписатьАтрибут("РежимВыгрузки",  РежимВыгрузки);
		ДокXML.ЗаписатьАтрибут("Версия",        Версия);		
	Иначе	
		Текст = Новый ЗаписьТекста(ВыбКаталог + "/" + ИмяФайла, КодировкаТекста.ANSI);
		
		СинонимКонфигурации = ПредставлениеРеквизита(СокрЛП(Метаданные.Синоним));
		
		// Шапка файла с идентификатором конфигурации
		ИдентификаторКонфигурации = "{""" + СинонимКонфигурации + """,""" + ДатаВыгрузки + """,""" + ПарамНачалоПериода + """,""" + ПарамКонецПериода + """,""" + ФлагРегламентированныйУчет + """,""" + СтратегияСписания + """,""" + уиПартииОтрицательныхОстатков1 + """,""" + уиПартииОтрицательныхОстатков2 + """,""" + РежимВыгрузки + """,""" + Версия + """}";
		
		// запишем строку
		Текст.ЗаписатьСтроку(ИдентификаторКонфигурации);
	КонецЕсли;
	
	// Инициализируем дерево выгруженных объектов
	Если ПоказатьВыгруженные Тогда
		ВыгруженныеОбъекты.Колонки.Очистить();
		ВыгруженныеОбъекты.Строки.Очистить();
		
		ВыгруженныеОбъекты.Колонки.Добавить("Объект");
		ВыгруженныеОбъекты.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		
		КореньСправочники = ВыгруженныеОбъекты.Строки.Добавить();
		КореньСправочники.Объект = "Справочники";			
	КонецЕсли;
	
	
	// Сначала выгрузим справочники
	ПредТип = "";
	тзЭлементов.Сортировать("Тип,Уровень,Ссылка");
	
	ПервыйСправочник = Истина;
	ПервыйДокумент = Истина;
	
	Для Каждого ТекСтрока Из тзЭлементов Цикл
		#Если Клиент Тогда
		Состояние("Ждите. Идет формирование файла выгрузки ... " + ЗначениеСчетчика + "/" + МаксЗначениеСчетчика);
		#КонецЕсли
	
		Если ТекСтрока.Тип <> ПредТип Тогда
			ПервыйОбъект = Истина;
			
			Если XMLФорматВыгрузки Тогда
				Если ПервыйСправочник Тогда ДокXML.ЗаписатьНачалоЭлемента("Ссылки"); КонецЕсли;
			
			
				Если ПредТип <> "" Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
				
				ДокXML.ЗаписатьНачалоЭлемента(ТекСтрока.Ссылка.Метаданные().Имя);
			КонецЕсли;
		КонецЕсли;
		
		ВыгрузитьОбъект(ТекСтрока);
		
		Если ПервыйОбъект Тогда ПервыйОбъект = Ложь; КонецЕсли;
		Если ПервыйСправочник Тогда ПервыйСправочник = Ложь; КонецЕсли;
		
		ПредТип = ТекСтрока.Тип;
		
		ЗначениеСчетчика = ЗначениеСчетчика + 1;
	КонецЦикла;	
	
	Если XMLФорматВыгрузки Тогда
		Если ПредТип <> "" Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		
		ДокXML.ЗаписатьКонецЭлемента();
	КонецЕсли;			
	
	Если XMLФорматВыгрузки Тогда
		//Если ПредТип <> "" Тогда ДокXML.ЗаписатьКонецЭлемента(); КонецЕсли;
		//ДокXML.ЗаписатьКонецЭлемента();
		
		// Завершим работу с файлом-выгрузки
		ДокXML.ЗаписатьКонецЭлемента();
		ДокXML.Закрыть();
		
	Иначе
		Текст.Закрыть();
	КонецЕсли;
	
	#Если Клиент Тогда
		
	Состояние("Формирование файла выгрузки завершено!");
	Сообщить("ВЫГРУЗКА ЗАВЕРШЕНА! Имя файла: " + ВыбКаталог + "\" + ИмяФайла);
	Сообщить("	Начало формирования файла выгрузки: " + НачалоРаботы + ", окончание: " + ТекущаяДата());
	
	#КонецЕсли

КонецПроцедуры	//	Выгрузить()

//Функция возвращает таблицу стоимостей списания автомобилей
Функция ПолучитьТаблицуСписанийАвтомобилей(РеализацияАвтомобилей)
		
	Если РеализацияАвтомобилей=Неопределено Тогда
		ТаблицаСписаний	= Новый ТаблицаЗначений();
		Возврат ТаблицаСписаний;	
	КонецЕсли;
	
	Запрос			= Новый Запрос();
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	РеализацияАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	            	  |	ЕСТЬNULL(ПродажиАвтомобилей.Себестоимость, 0) - ЕСТЬNULL(ПродажиАвтомобилей.СуммаНДСВходящий, 0) КАК Себестоимость,
	            	  |	ЕСТЬNULL(ПродажиАвтомобилей.СуммаНДС, 0) КАК СуммаНДС,
	            	  |	ЕСТЬNULL(ПродажиАвтомобилей.СуммаНДСВходящий, 0) КАК СуммаНДСВходящий
	            	  |ПОМЕСТИТЬ ПродажиАвтомобилей
	            	  |ИЗ
	            	  |	Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
	            	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
	            	  |		ПО РеализацияАвтомобилейАвтомобили.Ссылка = ПродажиАвтомобилей.Регистратор
	            	  |			И РеализацияАвтомобилейАвтомобили.Автомобиль = ПродажиАвтомобилей.Автомобиль
	            	  |ГДЕ
	            	  |	РеализацияАвтомобилейАвтомобили.Ссылка = &Ссылка
	            	  |;
	            	  |
	            	  |////////////////////////////////////////////////////////////////////////////////
	            	  |ВЫБРАТЬ
	            	  |	РеализацияАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	            	  |	СУММА(ЕСТЬNULL(Продажи.Себестоимость, 0) - ЕСТЬNULL(Продажи.СуммаНДСВходящий, 0)) КАК Себестоимость,
	            	  |	СУММА(ЕСТЬNULL(Продажи.СуммаНДС, 0)) КАК СуммаНДС,
	            	  |	СУММА(ЕСТЬNULL(Продажи.СуммаНДСВходящий, 0)) КАК СуммаНДСВходящий
	            	  |ПОМЕСТИТЬ ПродажиКомплектацииАвтомобилей
	            	  |ИЗ
	            	  |	Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
	            	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи КАК Продажи
	            	  |		ПО РеализацияАвтомобилейАвтомобили.Автомобиль = Продажи.Автомобиль
	            	  |			И РеализацияАвтомобилейАвтомобили.Ссылка = Продажи.Регистратор
	            	  |ГДЕ
	            	  |	РеализацияАвтомобилейАвтомобили.Ссылка = &Ссылка
	            	  |
	            	  |СГРУППИРОВАТЬ ПО
	            	  |	РеализацияАвтомобилейАвтомобили.Автомобиль
	            	  |;
	            	  |
	            	  |////////////////////////////////////////////////////////////////////////////////
	            	  |ВЫБРАТЬ
	            	  |	ПродажиАвтомобилей.Автомобиль КАК Автомобиль,
	            	  |	ПродажиАвтомобилей.Себестоимость + ЕСТЬNULL(ПродажиКомплектацииАвтомобилей.Себестоимость, 0) КАК Себестоимость,
	            	  |	ПродажиАвтомобилей.СуммаНДС + ЕСТЬNULL(ПродажиКомплектацииАвтомобилей.СуммаНДС, 0) КАК СуммаНДС,
	            	  |	ПродажиАвтомобилей.СуммаНДСВходящий + ЕСТЬNULL(ПродажиКомплектацииАвтомобилей.СуммаНДСВходящий, 0) КАК СуммаНДСВходящий
	            	  |ИЗ
	            	  |	ПродажиАвтомобилей КАК ПродажиАвтомобилей
	            	  |		ЛЕВОЕ СОЕДИНЕНИЕ ПродажиКомплектацииАвтомобилей КАК ПродажиКомплектацииАвтомобилей
	            	  |		ПО ПродажиАвтомобилей.Автомобиль = ПродажиКомплектацииАвтомобилей.Автомобиль";
	Запрос.УстановитьПараметр("Ссылка", РеализацияАвтомобилей);
	
	Попытка
		ТаблицаСписаний	= Запрос.Выполнить().Выгрузить();
	Исключение
		ТаблицаСписаний	= Новый ТаблицаЗначений();
	КонецПопытки;

	Возврат ТаблицаСписаний;
КонецФункции

//Процедура осуществляет инициализацию таблицы видов справочников
Процедура ИнициализироватьВидыСправочников(ЭтаФорма=Неопределено) Экспорт
	
	Если ТипЗнч(ВидыСправочников)=Тип("ТаблицаЗначений") И ВидыСправочников.Колонки.Количество()>0 Тогда
		Возврат;
	КонецЕсли;
	
	 //Инициализируем таблицу значений видов справочников
	ВидыСправочников.Колонки.Добавить("Пометка");
	ВидыСправочников.Колонки.Добавить("Представление");
	ВидыСправочников.Колонки.Добавить("Объект");
	
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "Контрагенты";
	ТекСтрока.Представление = "Контрагенты";
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "ДоговорыВзаиморасчетов";
	ТекСтрока.Представление = "Договоры взаиморасчетов";
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "Банки";
	ТекСтрока.Представление = "Банки";
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "БанковскиеСчета";
	ТекСтрока.Представление = "Банковские счета";
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "Валюты";
	ТекСтрока.Представление = "Валюты";
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "СтатьиДДС";
	ТекСтрока.Представление = "Статьи ДДС";	
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "Номенклатура";
	ТекСтрока.Представление = "Номенклатура";
	ТекСтрока = ВидыСправочников.Добавить();
	ТекСтрока.Пометка       = Истина;
	ТекСтрока.Объект        = "Сотрудники";
	ТекСтрока.Представление = "Сотрудники";	
	
КонецПроцедуры	//ИнициализироватьВидыСправочников

//Процедура осуществляет инициализацию таблицы видов документов
Процедура ИнициализироватьВидыДокументов(ЭтаФорма=Неопределено) Экспорт
	
	Если ТипЗнч(ВидыДокументов)=Тип("ТаблицаЗначений") И ВидыДокументов.Колонки.Количество()>0 Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализируем таблицу значений видов документов
	ВидыДокументов.Колонки.Добавить("Пометка");
	ВидыДокументов.Колонки.Добавить("Представление");
	ВидыДокументов.Колонки.Добавить("Объект");
	ВидыДокументов.Колонки.Добавить("ЕстьКонтрагент");
	
	// Инициализируем таблицу объектов к выгрузке
	Общие					= Новый ОписаниеТипов(ОбщиеТипы);
	ТипыОбъектовКВыгрузке	= Новый ОписаниеТипов(Общие, ТипыАльфа);
	ОбъектыКВыгрузке.Колонки.Добавить("Объект",ТипыОбъектовКВыгрузке);
	Если ЭтаФорма<>Неопределено Тогда
		ЭтаФорма.ЭлементыФормы.ОбъектыКВыгрузке.СоздатьКолонки();
	КонецЕсли;
	
	// Заполним виды выгружаемых документов
	Если кУР Тогда
		Для Каждого ТекОбъект Из Метаданные["Документы"] Цикл
			Если ПланыВидовХарактеристик.ТипыСмешанныхДокументов.СоздатьЭлемент().ТипЗначения.СодержитТип(ТипЗнч(Документы[ТекОбъект.Имя].ПустаяСсылка())) Тогда
				Продолжить;
			ИначеЕсли ПланыВидовХарактеристик.ТипыУправленческихДокументов.СоздатьЭлемент().ТипЗначения.СодержитТип(ТипЗнч(Документы[ТекОбъект.Имя].ПустаяСсылка())) Тогда
				Продолжить;
			ИначеЕсли ТекОбъект.Имя = "Рецептура" Тогда
				Продолжить;
			КонецЕсли;
		
			ТекСтрока				= ВидыДокументов.Добавить();
			ТекСтрока.Пометка       = Истина;
			ТекСтрока.Объект        = ТекОбъект.Имя;
			ТекСтрока.Представление = ТекОбъект.Представление();
			ТекСтрока.ЕстьКонтрагент = ?(ТекОбъект.Реквизиты.Найти("Контрагент") = Неопределено, Ложь, Истина);
		КонецЦикла;
	Иначе
		Для Каждого ТекОбъект Из Метаданные["Документы"] Цикл
			Если (ТекОбъект.Реквизиты.Найти("РегламентированныйУчет") = Неопределено) И (спДокументовБезРегл.НайтиПоЗначению(ТекОбъект.Имя) = Неопределено) И (спДокументовСпец.НайтиПоЗначению(ТекОбъект.Имя) = Неопределено) Тогда Продолжить; КонецЕсли;
			
			ТекСтрока				= ВидыДокументов.Добавить();
			ТекСтрока.Пометка       = Истина;
			ТекСтрока.Объект        = ТекОбъект.Имя;
			ТекСтрока.Представление = ТекОбъект.Представление();
			ТекСтрока.ЕстьКонтрагент = ?(ТекОбъект.Реквизиты.Найти("Контрагент") = Неопределено, Ложь, Истина);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры	//ИнициализироватьВидыДокументов

Функция ПолучитьНомерИсправления(Ссылка) 
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураПолученный.НомерИсправления КАК НомерИсправления
		|ИЗ
		|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|ГДЕ
		|	СчетФактураПолученный.ДокументОснование = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.НомерИсправления;
		
	Иначе			
		СчетФактура = Документы.СчетФактураПолученный.СоздатьДокумент();
		СчетФактура.Заполнить(Ссылка);
		Возврат СчетФактура.НомерИсправления;   			
	КонецЕсли;	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
кАвтосервис = Ложь;
кУР = Ложь;
кАмбулатория = Ложь;
Если Найти(Метаданные.Имя,"УправлениеРестораном") > 0 Тогда
	кУР = Истина;
ИначеЕсли Найти(Метаданные.Имя,"Амбулатория") > 0 Тогда
	кАмбулатория = Истина;
КонецЕсли;
//Делаем отвязку от имени конфигурации так как часто её переименовывают что ломает выгрузку
кАвтосервис =	(Метаданные.Документы.Найти("АктПриемаПередачиЦенныхБумаг")<>Неопределено И
				Метаданные.Документы.Найти("АктРазногласий")<>Неопределено И
				Метаданные.Документы.Найти("ВводОстатковТоваровВПроизводстве")<>Неопределено И
				Метаданные.Документы.Найти("ЗаказНаряд")<>Неопределено И
				Метаданные.Документы.Найти("ИзвлечениеТоваровИзПроизводства")<>Неопределено И
				Метаданные.Документы.Найти("ПеремещениеТоваровВПроизводство")<>Неопределено И
				Метаданные.Документы.Найти("НачислениеЗарплаты")<>Неопределено И
				Метаданные.Документы.Найти("ВыплатаЗарплаты")<>Неопределено И
				Метаданные.Документы.Найти("КорректировкаПоступления")<>Неопределено И
				Метаданные.Документы.Найти("КорректировкаПоступленияАвтомобилей")<>Неопределено И
				Метаданные.Документы.Найти("КорректировкаРеализации")<>Неопределено И
				Метаданные.Документы.Найти("КорректировкаРеализацииАвтомобилей")<>Неопределено);
спДокументовБезРегл = Новый СписокЗначений();
спДокументовБезРегл.Добавить("ИзменениеЦен");
спДокументовБезРегл.Добавить("Инкассация");
спДокументовБезРегл.Добавить("ПересортицаТоваров");

//Список специальных документов (без признака регламентированного учета, не совершающих движения)
спДокументовСпец = Новый СписокЗначений();
спДокументовСпец.Добавить("СчетНаОплату");
спДокументовСпец.Добавить("СчетНаОплатуЗаАвтомобили");
спДокументовСпец.Добавить("СчетОтПоставщика");
спДокументовСпец.Добавить("СчетОтПоставщикаЗаАвтомобили");
спДокументовСпец.Добавить("ПеремещениеДенежныхСредств");


//Заполним список документов, у которых обязательно выгружать документ-основание
спДокументыСОснованием = Новый СписокЗначений();
спДокументыСОснованием.Добавить("ВозвратОтПокупателя");
спДокументыСОснованием.Добавить("ВозвратПоставщику");
спДокументыСОснованием.Добавить("ПоступлениеДопРасходов");
спДокументыСОснованием.Добавить("ВозвратОтПокупателяАвтомобилей");
спДокументыСОснованием.Добавить("ВозвратПоставщикуАвтомобилей");
спДокументыСОснованием.Добавить("СчетФактураВыданный");
спДокументыСОснованием.Добавить("СчетФактураПолученный");

ИмяКонфигурации = Метаданные.Имя;

//Описание общих типов для выгрузки объектов:
//делаем так, чтобы выгружать можно было только те объекты, для которых существует перегрузка
//Документы
ОбщиеТипы = Новый Массив();
ОбщиеТипы.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Взаимозачет"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ВводОстатковТоваров"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ВозвратОтПокупателя"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ВозвратПоставщику"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Выписка"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ЗакрытиеСмены"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ИзменениеЦен"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Инкассация"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Инвентаризация"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Комплектация"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.КорректировкаДолга"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Переоценка"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ПоступлениеДопРасходов"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ПоступлениеТоваров"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Разукомплектация"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.РеализацияТоваров"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.СписаниеТоваров"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.СчетФактураВыданный"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.СчетФактураПолученный"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ЧекНаОплату"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.Чек"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ВводОстатковПрочихАктивов"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ВводВЭксплуатацию"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ПеремещениеАктивов"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.СписаниеАктивов"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.РеализацияАктивов"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт"));

//Справочники
ОбщиеТипы.Добавить(Тип("СправочникСсылка.ТипыНоменклатуры"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Номенклатура"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.КлассификаторЕдиницИзмерения"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.КлассификаторСтранМира"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.ГТД"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Контрагенты"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.ДоговорыВзаиморасчетов"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Валюты"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.СкладыКомпании"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Пользователи"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Сотрудники"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.ТипыЦен"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.БанковскиеСчета"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Банки"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.СтатьиДДС"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.СтатьиДоходовИРасходов"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Цеха"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.Организации"));
ОбщиеТипы.Добавить(Тип("СправочникСсылка.ПрочиеАктивы"));
ОбщиеТипы.Добавить(Тип("ДокументСсылка.ПересортицаТоваров"));

ТипыАльфа	= Новый Массив();

Если кАвтосервис Тогда
	ТипыАльфа.Добавить(Тип("ДокументСсылка.АктПриемаПередачиЦенныхБумаг"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.АктРазногласий"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ВводОстатковТоваровВПроизводстве"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ЗаказНаряд"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ПеремещениеДенежныхСредств"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ИзвлечениеТоваровИзПроизводства"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ПеремещениеТоваровВПроизводство"));
	ТипыАльфа.Добавить(Тип("СправочникСсылка.ЦенныеБумаги"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.НачислениеЗарплаты"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ВыплатаЗарплаты"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.КорректировкаРеализацииАвтомобилей"));
	Для К = 0 По Метаданные.Документы.Количество() - 1 Цикл
		Документ = Метаданные.Документы.Получить(К);
		Если Документ.Имя = "ПеремещениеНезавершенногоПроизводства" Тогда
			ТипыАльфа.Добавить(Тип("ДокументСсылка.ПеремещениеНезавершенногоПроизводства"));
		КонецЕсли;
	КонецЦикла;
	
	Если Найти(Метаданные.Имя,"Автосалон") > 0 Тогда
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ВводВЭксплуатациюАвтомобилей"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ВводОстатковАвтомобилей"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей"));
    	ТипыАльфа.Добавить(Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей"));
	    ТипыАльфа.Добавить(Тип("ДокументСсылка.ИнвентаризацияАвтомобилей"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераЗаАвтомобили"));
    	ТипыАльфа.Добавить(Тип("ДокументСсылка.ОтчетКомитентуЗаАвтомобили"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ПеремещениеАвтомобилей"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ПереоценкаАвтомобилей"));		
		ТипыАльфа.Добавить(Тип("ДокументСсылка.ПоступлениеАвтомобилей"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.РеализацияАвтомобилей"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.СписаниеАвтомобилей"));		
		ТипыАльфа.Добавить(Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили"));
		ТипыАльфа.Добавить(Тип("ДокументСсылка.РазукомплектацияАвтомобилей"));
		ТипыАльфа.Добавить(Тип("СправочникСсылка.Модели"));
	КонецЕсли;	
ИначеЕсли кАмбулатория Тогда
	ТипыАльфа.Добавить(Тип("СправочникСсылка.Пациенты"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ТалонАмбулаторногоПациента"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ВыбытиеИзСтационара"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.Вакцинация"));
    ТипыАльфа.Добавить(Тип("ДокументСсылка.ДиагностическиеИсследования"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ПроцедурноОперационнаяКарта"));
    ТипыАльфа.Добавить(Тип("ДокументСсылка.ПриемСпециалиста"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ЗачетОказанныхУслуг"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.МедицинскийОсмотр"));
	ТипыАльфа.Добавить(Тип("ДокументСсылка.ОбращениеГоспитализация"));
Иначе
	ТипыАльфа.Добавить(Тип("ДокументСсылка.АктПриемаПередачиЦенныхБумаг"));
	ТипыАльфа.Добавить(Тип("СправочникСсылка.ЦенныеБумаги"));
КонецЕсли;

ВыгружатьСправочникиПоСсылкам = Истина;
XMLФорматВыгрузки = Истина;
ЕстьТаблицаСписанийАвтомобилей = Ложь;

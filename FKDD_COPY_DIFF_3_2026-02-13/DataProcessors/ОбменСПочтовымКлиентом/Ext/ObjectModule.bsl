// Модуль обработки
#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем МассивПочтовыеСообщения;		// Массив импортируемых писем.
Перем Почта;                        // Идентификатор почтового клиента
Перем ПодключеноУспешно;            // Наличие подключения к почтовому клиенту
///////////////////////////////////////////////////////////////////////	
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Возвращает исправленную строку
Функция ИсправитьСтроку(ИсходнаяСтрока)    
    Если ИсходнаяСтрока = "" Тогда
        Возврат "";
	КонецЕсли;
	Результат = ИсходнаяСтрока;
    Пока Истина Цикл
        Позиция = НайтиНедопустимыеСимволыXML(Результат);        
        Если Позиция = 0 Тогда
            Прервать;
        КонецЕсли;
        Результат = Сред(Результат,1, Позиция-1)+Сред(Результат, Позиция+1); 
    КонецЦикла;
    
    Результат = СтрЗаменить(Результат, Символ(13) + Символ(10), Символы.ПС);
    Результат = СтрЗаменить(Результат, Символ(182), Символы.ПС);
    Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Подключение к почтовому серверу
Функция ПочтаПодключиться() Экспорт
	ПодключеноУспешно = Ложь;
	Если Почта = Неопределено Тогда
		Почта = Новый Почта;
	КонецЕсли;
	Попытка
		Почта.Подключиться();
		ПодключеноУспешно = Истина;
	Исключение
	КонецПопытки;
	Возврат ПодключеноУспешно;
КонецФункции

// Разъединение с почтовым сервером
Процедура ПочтаОтключиться() Экспорт
	Если ПодключеноУспешно Тогда
		Почта.Отключиться();
		ПодключеноУспешно = Ложь;
	КонецЕсли;
КонецПроцедуры
                                                          
// Заполнение таблицы письмами почтового клиента
Функция ЗаполнениеТаблицаОбработки(Таблица, ТолькоНепрочитанные) Экспорт
	Таблица.Очистить();
	Если НЕ ПодключеноУспешно Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		МассивПочтовыеСообщения = Почта.Выбрать(ТолькоНепрочитанные, Ложь);
	Исключение		
		Возврат Ложь;
	КонецПопытки;
	Для Каждого Элемент Из МассивПочтовыеСообщения Цикл
		ТекСтрока = Таблица.Добавить();
		ТекСтрока.Тема = Элемент.Тема;
		ТекСтрока.ТекстСообщения = ИсправитьСтроку(Элемент.Текст);		
		ТекСтрока.ДатаПолучения = Элемент.ДатаПолучения;
		Если ТипЗнч(Элемент.Отправитель) = Тип("Строка") Тогда
			ТекСтрока.Отправитель = Сред(Элемент.Отправитель, Найти(Элемент.Отправитель, ":")+1);
		Иначе       // Тип("ПочтовыйАдрес")
			ТекСтрока.Отправитель = Сред(Элемент.Отправитель.Адрес, Найти(Элемент.Отправитель.Адрес, ":")+1);
		КонецЕсли;
		Если Элемент.Вложения.Количество() > 1 Тогда
			ТекСтрока.ЕстьВложения = Истина;
		Иначе
			ТекСтрока.ЕстьВложения = Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

// Сохранение указанных писем
Процедура ОбработкаИмпортВыполнить(Таблица)		Экспорт 
	Если обЗначениеНеЗаполнено(УчетнаяЗапись) Тогда
		Сообщить("Реквизит <Учетная запись> не заполнен !" + Символы.ПС + "Действие отменено!", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	ГруппаПисемЭлектроннойПочты = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Входящие",Истина,,УчетнаяЗапись);
	Для Каждого ТекСтрока Из Таблица Цикл
		Если НЕ ТекСтрока.Пометка Тогда
			Продолжить;
		КонецЕсли;
		Индекс = Таблица.Индекс(ТекСтрока);
		ПочтовоеСообщение = МассивПочтовыеСообщения[Индекс];
		ПолучательАдрес = Сред(ПочтовоеСообщение.Получатели[0].Адрес, Найти(ПочтовоеСообщение.Получатели[0].Адрес, ":")+1);
		Для Индекс=1 По (ПочтовоеСообщение.Получатели.Количество()-1) Цикл
			ПолучательАдрес = ПолучательАдрес +", "+ Сред(ПочтовоеСообщение.Получатели[Индекс].Адрес, Найти(ПочтовоеСообщение.Получатели[Индекс].Адрес, ":")+1);
		КонецЦикла;
		
		ЭлектронноеПисьмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
		ЭлектронноеПисьмо.ХозОперация = Справочники.ХозОперации.ЭлектронноеПисьмо;
		ЭлектронноеПисьмо.НеРассмотрено = Истина;
		ЭлектронноеПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.Входящее;
		ЭлектронноеПисьмо.ГруппаПисем = ГруппаПисемЭлектроннойПочты;
		ЭлектронноеПисьмо.Тема = ТекСтрока.Тема;
		ЭлектронноеПисьмо.УчетнаяЗапись = УчетнаяЗапись;
		ЭлектронноеПисьмо.ТекстПисьма = ТекСтрока.ТекстСообщения;
		ЭлектронноеПисьмо.ОтправительПредставление = ТекСтрока.Отправитель;
		ЭлектронноеПисьмо.ДатаПолучения = ТекСтрока.ДатаПолучения;
		ЭлектронноеПисьмо.ПолучателиПредставление = ПолучательАдрес;
		ЭлектронноеПисьмо.ЕстьВложения = ТекСтрока.ЕстьВложения;
		Если ПочтовоеСообщение.Вложения.Количество() > 1 Тогда
			МассивИменФайлов = Новый Массив;
			Для Индекс = 1 По (ПочтовоеСообщение.Вложения.Количество() - 1) Цикл
				Прикрепление = ПочтовоеСообщение.Вложения[Индекс];
				ИмяФайла = КаталогВременныхФайлов() + Прикрепление.Наименование;
				Прикрепление.Данные.Записать(ИмяФайла);
				МассивИменФайлов.Добавить(ИмяФайла);
			КонецЦикла;
			эпДобавитьВложенияВПисьмо(ЭлектронноеПисьмо.ПрикрепленныеФайлы, МассивИменФайлов, Истина, Ложь);
		КонецЕсли;		
		
		дкОбработкаЗаполнения(ЭлектронноеПисьмо);
		ЭлектронноеПисьмо.Записать();
		ПочтовоеСообщение.Непрочтено = Ложь;
	КонецЦикла;	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ПодключеноУспешно = Ложь;

#КонецЕсли

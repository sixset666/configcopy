#Если Клиент Тогда
Перем ДокументОбъект Экспорт; // Документ объект

//Заполнение документа план операция: планирование
Процедура ЗаполнитьПлан() Экспорт
	Перем Заказ;
	
	ТаблицаЗначений=ДокументОбъект.Показатели.Выгрузить();

	ЧислоСтрок=ТаблицаЗначений.Количество()-1;
	Если ЧислоСтрок=-1 Тогда
		Сообщить("Нет данных, на основании которых должно производиться заполнение заказов. Заполните план и затем попробуйте снова!");
		Возврат;
	КонецЕсли;
		
	ТаблицаЗначений.Сортировать("ВидАналитикиПланирования2");	
	ТекущийКонтрагент=Неопределено;
	ЭтоПервыйДокумент=Истина;
	Сообщить("Начало автоматического заполнения заказов поставщикам...", СтатусСообщения.Внимание);
	
	Для Счётчик=0 По ЧислоСтрок Цикл
		ТекущаяСтрока=ТаблицаЗначений.Получить(Счётчик);
		Если ТекущаяСтрока.ВидАналитикиПланирования2=Неопределено Тогда
			ТекущаяСтрока.ВидАналитикиПланирования2=Справочники.Контрагенты.ОсновнойПоставщик;
			Если ЭтоПервыйДокумент Тогда
				Сообщить("В следующий документ объединяются товары, поставщик которых неизвестен (подставляется «Основной поставщик»)", СтатусСообщения.Информация);
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущаяСтрока.ВидАналитикиПланирования2<>ТекущийКонтрагент Тогда
			Если НЕ ЭтоПервыйДокумент Тогда
				Заказ.Записать();
				Сообщить("Документ создан."); 							
			КонецЕсли;
			
			ТекущийКонтрагент=ТекущаяСтрока.ВидАналитикиПланирования2;
			
			// Заполнение шапки документа ЗаказПоставщику
			Заказ=Документы.ЗаказПоставщику.СоздатьДокумент();
			Заказ.Дата=ТекущаяДата();
			Заказ.УстановитьВремя();
			Заказ.УстановитьНовыйНомер();
			ЭтоПервыйДокумент=Ложь;			
			Сообщить("Создание документа «"+Заказ+"» (поставщик: «"+ТекущийКонтрагент+"»)..."); 			
			
			Заказ.Заполнить(Неопределено);
			Заказ.ХозОперация=Справочники.ХозОперации.ЗаказПоставщику;
			Заказ.ОбработкаРеквизита("ХозОперация");
			
			Заказ.ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			Заказ.ОбработкаРеквизита("ВалютаДокумента");
			
			Заказ.Контрагент=ТекущийКонтрагент;			
			Заказ.ОбработкаРеквизита("Контрагент");
			
			Заказ.Организация=ДокументОбъект.Организация;
			Заказ.ПодразделениеКомпании=ДокументОбъект.ПодразделениеКомпании;
			Заказ.СрокПоставки=ДокументОбъект.ДатаКонца;
		КонецЕсли;
		
		// Заполнение ТЧ Товары документа ЗаказПоставщику
		НоваяСтрокаЗаказа=Заказ.Товары.Добавить();
		
		НоваяСтрокаЗаказа.Номенклатура=ТекущаяСтрока.ВидАналитикиПланирования1;
		Заказ.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрокаЗаказа);
		
		НоваяСтрокаЗаказа.Количество=ТекущаяСтрока.ПоказательПлана1;
		Заказ.ОбработкаРеквизита("Товары.Количество", НоваяСтрокаЗаказа);
		
		НоваяСтрокаЗаказа.Цена=ТекущаяСтрока.ПоказательПлана2;
		Заказ.ОбработкаРеквизита("Товары.Цена", НоваяСтрокаЗаказа);
		
	КонецЦикла;
	
	Заказ.Записать();
	Сообщить("Документ создан."); 							
	
	Сообщить("Окончание автоматического заполнения заказов поставщикам.", СтатусСообщения.Внимание);
КонецПроцедуры

#КонецЕсли
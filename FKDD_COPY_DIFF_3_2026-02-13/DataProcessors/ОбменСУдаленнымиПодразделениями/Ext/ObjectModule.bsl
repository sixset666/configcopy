
Перем Права 							Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем УдаленныеПодразделения_ЭтотУзел 	Экспорт;    // Признак того что это узел удаленного подразделения
Перем НастройкаТекущегоУзла				Экспорт;    // хранится настройка текущего узла
Перем ЛогПланОбмена 					Экспорт;    // файл лога обмена
Перем ОбработкаДоставкаСообщений;
Перем ПрефиксЛога;
Перем ДатаНачалаОбмена					Экспорт;    // дата начала обмена
Перем ДатаОкончанияОбмена				Экспорт;    // дата окончания обмена
Перем счОбъектов						Экспорт;    // счетчик объектов
Перем счДокументов						Экспорт;    // счетчик документов
Перем счСправочников					Экспорт;    // счетчик справочников

// Выполняет загрузку сообщения обмена для указанного узла
//  по определенной настройке доставке (лок. сеть, фтп, почта)
//
// Параметры:
//	УзелОбменаДоп 			- ПланыОбменыСсылка.УдаленныеПодразделения. Ссылка на узел плана обмена
//	НастройкаДоставкиДоп 	- СправочникСсылка.НастройкиДоставкиСообщений. Настройка доставки сообщения
//
// Возвращаемое значение:
//   Булево. Флаг успешного завершения загрузки сообщения
//
Функция ВыполнитьЗагрузку(Знач УзелОбменаДоп = Неопределено, Знач НастройкаДоставкиДоп = Неопределено, 
		СтрОшибки = "", ИмяФайлаСообщения = "",ДочитываниеФайлаОбмена = Ложь, ПрерватьЗадание = Ложь) Экспорт
	
	Рез = Ложь; // все плохо
	
	// НАСТРОЙКА
	Если НастройкаДоставкиДоп = Неопределено Тогда
		Если УзелОбменаДоп = Неопределено Тогда
			НастройкаДоставкиДоп = НастройкаОбмена;
		Иначе
			НастройкаДоставкиДоп = УзелОбменаДоп.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
		КонецЕсли;	
	КонецЕсли;
	Если УзелОбменаДоп = Неопределено Тогда
		УзелОбменаДоп = УзелОбмена;
	КонецЕсли;
	
	// счетчики
	ПараметрыСеанса.СчетчикОбъектов 	= 0;
	ПараметрыСеанса.СчетчикСправочников = 0;
	ПараметрыСеанса.СчетчикДокументов 	= 0;
	
		//настройки обмена кидаем в структуру, некоторые значения будут установлены в этом объекте
		//(Комментировать ход обмена, комментировать пообъектно)
	НастройкиПараметраСеанса = Новый Структура();
	НастройкиПараметраСеанса.Вставить("ВыполнятьОбменНаКлиенте",ВыполнятьОбменНаКлиенте);
	НастройкиПараметраСеанса.Вставить("ПрефиксЛога",ПрефиксЛога);
	одОбменДанными.УстановитьПараметрыСеансаОбмена(НастройкиПараметраСеанса);

	
	Если НастройкаДоставкиДоп = неопределено Тогда
		СтрСообщения = "Для узла """ + СокрЛП(УзелОбменаДоп.Наименование) + """ не определена настройка доставки.";
		#Если Клиент Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Внимание);
		#КонецЕсли
		ЗаписьЖурналаРегистрации(ПрефиксЛога + "Получение", УровеньЖурналаРегистрации.Ошибка, , , СтрСообщения);
	    Возврат Ложь;
	КонецЕсли; 
	
	// если передали полное имя файла сообщения - проверим его
	// проверим существование файла
	Если НЕ ПустаяСтрока(ИмяФайлаСообщения) Тогда
		ФайлСообщения = Новый Файл(ИмяФайлаСообщения);
		Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
			Возврат Ложь;
		Иначе
			Рез = истина;
		КонецЕсли;
	КонецЕсли;
	
	ДатаНачалаОбмена = сфТекущаяДатаСервера();
	
	// ДОСТАВКА
	Параметры=Новый Структура;
	Параметры.Вставить("УзелОбмена",		УзелОбменаДоп);
	Параметры.Вставить("НастройкаДоставки",	НастройкаДоставкиДоп);
	Параметры.Вставить("Прием",				Истина);
	Параметры.Вставить("Отправка",			Ложь);
	Параметры.Вставить("КомментироватьХодОбмена",Комментировать);
	Параметры.Вставить("ВестиЖурнал",		ВестиЖурналРегистрации);
	Параметры.Вставить("ВестиЛоги",			ВестиЛогФайл);
	Параметры.Вставить("ЛогФайлОбъект",		Неопределено);
	Параметры.Вставить("ПолноеИмяТекущегоЛога",	"");
	ОбработкаДоставкаСообщений.ИнициализацияПараметров(Параметры); 
	
	// уже есть готовый файл - не нужно его доставлять
	Если  ПустаяСтрока(ИмяФайлаСообщения) Тогда
		Рез = ОбработкаДоставкаСообщений.Доставка_ЗагрузкаСообщенияОбмена(ИмяФайлаСообщения,СтрОшибки);
	Иначе
		ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога, "Продолжается обработка файла сообщения """ + ИмяФайлаСообщения + """",
		,Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
	КонецЕсли;
		
	Если НЕ Рез ИЛИ ПустаяСтрока(ИмяФайлаСообщения) Тогда
		Если ПустаяСтрока(СтрОшибки) Тогда
			СтрОшибки = "Не было получено сообщений из узла """ + СокрЛП(УзелОбменаДоп.Наименование) + """";
		КонецЕсли;
		ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Получение", 
											  СтрОшибки ,
														,
												?(Рез,Перечисления.СтатусыСобытийСистемногоПротокола.Информация,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка));
												
										
		//Возврат Рез; 
	Иначе // Есть сообщение - будем его читать
		#Если Клиент Тогда
		// если это не Робот дадим возможность пользователю указать конкретный файл
		// (по умолчанию предложив найденный по настройке доставки)
		ПредложенноеИмяФайлаСообщения = ИмяФайлаСообщения;
		Если ДочитываниеФайлаОбмена Тогда
			Если НЕ ПараметрыСеанса.Пользователь = Справочники.Пользователи.Робот Тогда
				ДиалогВыбораФайла=Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
				ДиалогВыбораФайла.Заголовок="Выберите файл обмена из узла" + СокрЛП(УзелОбменаДоп.Наименование) + " для дочитывания";
				ДиалогВыбораФайла.ПредварительныйПросмотр=Ложь;
				ДиалогВыбораФайла.ИндексФильтра=?(ВРег(Прав(ИмяФайлаСообщения,3))="XML",0,1);
				ДиалогВыбораФайла.ПолноеИмяФайла = ИмяФайлаСообщения;
				ДиалогВыбораФайла.Фильтр="Файл сообщения обмена (*.xml)|*.xml|Файл сообщения обмена (архив)(*.zip)|*.zip";
				ДиалогВыбораФайла.ПроверятьСуществованиеФайла=Истина;
				Если ДиалогВыбораФайла.Выбрать() Тогда
					ИмяФайлаСообщения=ДиалогВыбораФайла.ПолноеИмяФайла;
					ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Чтение", 
						"Для дочитывания предложен файл обмена <" + ПредложенноеИмяФайлаСообщения + ">. Пользователь выбрал файл обмена <" + ИмяФайлаСообщения + "> из узла " + СокрЛП(УзелОбменаДоп.Наименование) + ".",,Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
				Иначе
					ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Чтение", 
						"Для дочитывания предложен файл обмена <" + ПредложенноеИмяФайлаСообщения + ">. Пользователь отказался от выбора файла обмена из узла " + СокрЛП(УзелОбменаДоп.Наименование) + ".",,Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		#КонецЕсли
		
		// Заполним параметры сеанса
		НастройкиПараметраСеанса = Новый Структура("ВестиЖурналРегистрации, 
			|ВестиЛогФайл, КаталогЛогФайла, 
			|АрхивироватьСообщенияОбмена, ПарольАрхивации, КомментироватьХодОбмена");
		ЗаполнитьЗначенияСвойств(НастройкиПараметраСеанса, НастройкаДоставкиДоп);
		НастройкиПараметраСеанса.КомментироватьХодОбмена = Комментировать;
		НастройкиПараметраСеанса.Вставить("ФиксироватьПриемОтправкуОбъектов",КомментироватьОбъекты);
		НастройкиПараметраСеанса.Вставить("ВестиЖурналРегистрации",ВестиЖурналРегистрации);
		НастройкиПараметраСеанса.Вставить("ВестиЛогФайл",ВестиЛогФайл);
		Если ВестиЛогФайл Тогда
			НастройкиПараметраСеанса.Вставить("КаталогЛогФайла",КаталогЛогов);
		Иначе
			НастройкиПараметраСеанса.Вставить("КаталогЛогФайла","");
		КонецЕсли;
		НастройкиПараметраСеанса.Вставить("ВыполнятьОбменНаКлиенте",ВыполнятьОбменНаКлиенте);
		НастройкиПараметраСеанса.Вставить("ПрефиксЛога",ПрефиксЛога);
		одОбменДанными.УстановитьПараметрыСеансаОбмена(НастройкиПараметраСеанса);
		
		// Получаем объект узла обмена и инициализация динам. кэша прав по подразделениям
		УзелОбменаОбъект=УзелОбменаДоп.ПолучитьОбъект();
		Если НЕ ПустаяСтрока(ОбработкаДоставкаСообщений.ПолноеИмяТекущегоЛога) Тогда
			УзелОбменаОбъект.ПолноеИмяТекущегоЛога=ОбработкаДоставкаСообщений.ПолноеИмяТекущегоЛога;
			УзелОбменаОбъект.ЛогФайлОбъект=ОбработкаДоставкаСообщений.ЛогФайлОбъект;
		КонецЕсли;
		
		// ЧТЕНИЕ СООБЩЕНИЯ ОБМЕНА
		ЗавершитьРаботу = Ложь; // флаг завершения работы в случае получения изменений конфигурации в клиентской сессии
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("НастройкаОбмена", НастройкаДоставкиДоп);
		Рез = одОбменДанными.ПрочитатьСообщениеСИзменениями(ИмяФайлаСообщения,УзелОбменаОбъект,
			КоличествоЭлементовВТранзакции,СтрОшибки, ДопПараметры);
       		
		одОбменДанными.ЗакрытьЛогФайл(ОбработкаДоставкаСообщений.ЛогФайлОбъект);

		// Анализ результата
		// SUIG - вот теперь, если это первая загрузка от главного узла после обновления,
		// в базе появилась главная ИБ, соот-но нужно закрепить за ней созданные ранее (сразу при обновлении)
		// дочерние ИБ для выстраивания иерархии
		Если НЕ Рез Тогда
			Если ПустаяСтрока(СтрОшибки) Тогда
				СтрОшибки = "Чтение сообщения <" + ИмяФайлаСообщения + "> закончилось с ошибками "
					+ ?(ПустаяСтрока(СтрОшибки),"",": " + Символы.ПС + СтрОшибки);
			КонецЕсли;
			ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Чтение", СтрОшибки,,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
			// интерактивно загрузили - должен быть администратор
			// через фоновое - спец.роль
		ИначеЕсли ПравоДоступа("Администрирование", Метаданные) ИЛИ ПравоДоступа("ОбновлениеКонфигурацииБазыДанных", Метаданные) Тогда
			// в принципе необходимо проверять иерархию только для не центральных узлов,
			// но для подстраховки проверим в любом случае
			РезПроверки = одОбменДанными.ПроверитьИерархиюИБ();
		КонецЕсли;
		
		// ЗАКЛЮЧИТЕЛЬНЫЕ ДЕЙСТВИЯ
		
		// по завершении обмена выполним удаление временных файлов (оставшихся от всяких проверок и не коррект. обменов)
		УдалитьВременныеФайлы();
		
		//++OkoE
		Попытка
			одОбменДанными.ЗаписатьПараметрыОбменаДанными(УзелОбменаДоп.ИнформационнаяБаза, НастройкаДоставкиДоп, Истина, Рез, ИмяФайлаСообщения, СтрОшибки);
		Исключение
		КонецПопытки;
		//--OkoE
		
		Если Рез Тогда
			Если НастройкаДоставкиДоп.СохранятьПолученные Тогда
				// сохраним копию обработанного сообщения		
				ОбработкаДоставкаСообщений.СохранитьОбработанноеСообщение(ИмяФайлаСообщения);
			КонецЕсли;
			ИсключатьТекущее = Ложь;
			одОбменДанными.УдалитьФайлыПоМаске(УзелОбменаДоп,НастройкаДоставкиДоп,Ложь,,,ИсключатьТекущее,);
		КонецЕсли;
		
		// Чистим параметры сеанса
		одОбменДанными.ОчиститьПараметрыСеансаОбмена();
		
		// допроводим полученные документы по партиям (предположительно через Робота)
		Если Рез И ДопроводитьПоПартиям Тогда
			дкДопровестиПоПартиям();
		КонецЕсли; 
		
		#Если Клиент Тогда
		//обновим кэш прав, так как в сообщении могли прийти изменения по правам миграции документов
		глПрава = Неопределено;
		// Обновим текущий набор прав
		глПрава = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Сообщить(ТекДата + "Права текущего пользователя обновлены !",СтатусСообщения.Информация);
		// Поскольку на экране могут быть открыты формы объектов, скажем им, что надо бы обновить кэш прав..
		Оповестить("ОбновитьПрава",ПараметрыСеанса.Пользователь);
		#КонецЕсли

	КонецЕсли; // Есть новое сообщение обмена
	
	ДатаОкончанияОбмена = сфТекущаяДатаСервера();
	Если Рез Тогда
		СтрСообщения = "Загрузка сообщения обмена из узла """ + СокрЛП(УзелОбменаДоп.Наименование) + """ успешно завершена.";
		ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,
			,Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
	Иначе
		Если НЕ ПустаяСтрока(СтрОшибки) Тогда
			ПрерватьЗадание = Истина;
		КонецЕсли;
	КонецЕсли;
	СтрСообщения = "Общее время загрузки: " + ПолучитьВремяОбмена(ДатаОкончанияОбмена - ДатаНачалаОбмена)
		+ Символы.ВК + "Загружено объектов: " + Строка(ПараметрыСеанса.СчетчикОбъектов)
		+ Символы.ВК + "Из них справочников: " + Строка(ПараметрыСеанса.СчетчикСправочников)
		+ Символы.ВК + "Из них документов: " + Строка(ПараметрыСеанса.СчетчикДокументов);
 	ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,
			,Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
	Если НЕ Рез Тогда
		Возврат Ложь;
	КонецЕсли;
	
	#Если Клиент Тогда
		// обновим форму обработки Обмен с удаленными подразделениями
		Оповестить("ИзмененаСтруктураИБ",,);
	#КонецЕсли
	
	Возврат Рез;   
	
КонецФункции // ВыполнитьЗагрузку()

// Выполняет выгрузку сообщения обмена для указанного узла
//  по определенной настройке доставке (лок. сеть, фтп, почта)
//
// Параметры:
//	УзелОбменаДоп 			- ПланыОбменыСсылка.УдаленныеПодразделения. Ссылка на узел плана обмена
//	НастройкаДоставкиДоп 	- СправочникСсылка.НастройкиДоставкиСообщений. Настройка доставки сообщения
//	ПрерватьЗадание         - Флаг разрыва соединения
//
// Возвращаемое значение:
//   Булево. Флаг успешного завершения загрузки сообщения
//
Функция ВыполнитьВыгрузку(Знач УзелОбменаДоп = Неопределено, Знач НастройкаДоставкиДоп = Неопределено, ПрерватьЗадание = Ложь) Экспорт
	
	Рез = Ложь; // все плохо
	
	// НАСТРОЙКА
	Если НастройкаДоставкиДоп = Неопределено Тогда
		Если УзелОбменаДоп = Неопределено Тогда
			НастройкаДоставкиДоп = НастройкаОбмена;
		Иначе
			НастройкаДоставкиДоп = УзелОбменаДоп.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
		КонецЕсли;	
	КонецЕсли;
	Если УзелОбменаДоп = Неопределено Тогда
		УзелОбменаДоп = УзелОбмена;
	КонецЕсли;
	
	ПараметрыСеанса.СчетчикОбъектов 	= 0;
	ПараметрыСеанса.СчетчикСправочников = 0;
	ПараметрыСеанса.СчетчикДокументов 	= 0;
	
	Если НастройкаДоставкиДоп = неопределено Тогда
		СтрСообщения = "Для узла """ + СокрЛП(УзелОбменаДоп.Наименование) + """ не определена настройка доставки.";
		#Если Клиент Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + СтрСообщения,СтатусСообщения.Внимание);
		#КонецЕсли
		ЗаписьЖурналаРегистрации(ПрефиксЛога + "Отправка", УровеньЖурналаРегистрации.Предупреждение, , , СтрСообщения);
	    Возврат Ложь;
	КонецЕсли;
	
	ДатаНачалаОбмена = сфТекущаяДатаСервера();

	// ЗАПИСЬ СООБЩЕНИЯ ОБМЕНА
	//настройки обмена кидаем в структуру, некоторые значения будут установлены в этом объекте
	//(Комментировать ход обмена, комментировать пообъектно)
	НастройкиПараметраСеанса = Новый Структура();
	НастройкиПараметраСеанса.Вставить("ВестиЖурналРегистрации");
	НастройкиПараметраСеанса.Вставить("КомментироватьХодОбмена");
	НастройкиПараметраСеанса.Вставить("ФиксироватьПриемОтправкуОбъектов");
	НастройкиПараметраСеанса.Вставить("ВестиЛогФайл");
	НастройкиПараметраСеанса.Вставить("АрхивироватьСообщенияОбмена");
	НастройкиПараметраСеанса.Вставить("ПарольАрхивации");
	ЗаполнитьЗначенияСвойств(НастройкиПараметраСеанса, НастройкаДоставкиДоп);
	НастройкиПараметраСеанса.КомментироватьХодОбмена = Комментировать;
	НастройкиПараметраСеанса.ФиксироватьПриемОтправкуОбъектов = КомментироватьОбъекты;
	НастройкиПараметраСеанса.Вставить("ВестиЖурналРегистрации",ВестиЖурналРегистрации);
	НастройкиПараметраСеанса.Вставить("ВестиЛогФайл",ВестиЛогФайл);
	Если ВестиЛогФайл Тогда
		НастройкиПараметраСеанса.Вставить("КаталогЛогФайла",КаталогЛогов);
	Иначе
		НастройкиПараметраСеанса.Вставить("КаталогЛогФайла","");
	КонецЕсли;
	НастройкиПараметраСеанса.Вставить("ВыполнятьОбменНаКлиенте",ВыполнятьОбменНаКлиенте);
	НастройкиПараметраСеанса.Вставить("ПрефиксЛога",ПрефиксЛога);
	одОбменДанными.УстановитьПараметрыСеансаОбмена(НастройкиПараметраСеанса);
	
	// Получаем объект узла обмена
	УзелОбменаОбъект=УзелОбменаДоп.ПолучитьОбъект();
	
	// Проверим возможность отправки сообщения обмена после записи
	// нет возможности - незачем и записывать
	СтрОшибок = ""; FTP = НЕОПРЕДЕЛЕНО; КаталогОбмена = "";
	// инициализируем все параметры доставки
	Параметры=Новый Структура;
	Параметры.Вставить("УзелОбмена",		УзелОбменаДоп);
	Параметры.Вставить("НастройкаДоставки",	НастройкаДоставкиДоп);
	Параметры.Вставить("Прием",				Ложь);
	Параметры.Вставить("Отправка",			Истина);
	Параметры.Вставить("КомментироватьХодОбмена",Комментировать);
	Параметры.Вставить("ВестиЖурнал",		ВестиЖурналРегистрации);
	Параметры.Вставить("ВестиЛоги",			ВестиЛогФайл);
	Параметры.Вставить("ЛогФайлОбъект",		УзелОбменаОбъект.ЛогФайлОбъект);
	Параметры.Вставить("ПолноеИмяТекущегоЛога",	УзелОбменаОбъект.ПолноеИмяТекущегоЛога);

	ОбработкаДоставкаСообщений.ИнициализацияПараметров(Параметры); 
	//DooY - это лишнее сообщение в журнал	
	ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки сообщения обмена для узла """ + СокрЛП(УзелОбменаДоп.Наименование) + """",
		,Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
		
	// так как обработка доставки уже активизировано - все что нам необходимо (каталоги), там уже есть
	Если НЕ ОбработкаДоставкаСообщений.ПроверитьВозможностьОтправки(FTP,СтрОшибок) Тогда
		СтрСообщения = "Для узла """ + УзелОбменаДоп.Наименование + """ при проверке возможности отправки сообщения обмена обнаружены следующие ошибки: " + СтрОшибок;
		ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения,УзелОбменаДоп, Истина);
		Рез = Ложь;
	Иначе
		Рез = Истина;
	КонецЕсли;
	
	одОбменДанными.ЗакрытьЛогФайл(ОбработкаДоставкаСообщений.ЛогФайлОбъект);
         	
	// Записываем новое сообщение обмена
	Если Рез Тогда
	    ИмяФайлаСообщения = "";
		Рез = одОбменДанными.ЗаписатьСообщениеСИзменениями(ИмяФайлаСообщения,УзелОбменаОбъект,
			КоличествоЭлементовВТранзакции,ОбработкаДоставкаСообщений.КаталогТемпФайлов); // куда - что
	КонецЕсли; 
	
	// Удаляем старые файлы в любом случае
	Если ВыполнятьОбменНаКлиенте Тогда
		одОбменДанными.УдалитьФайлыПоМаске(УзелОбменаДоп,НастройкаДоставкиДоп, Истина);
	Иначе
		пвПривилегированныйМодуль.УдалитьФайлыПоМаске(УзелОбменаДоп,НастройкаДоставкиДоп, Истина);
	КонецЕсли; 
		
	// ДОСТАВКА
	Если Рез Тогда 
 		Рез = ОбработкаДоставкаСообщений.Доставка_ВыгрузкаСообщенияОбмена(ИмяФайлаСообщения);
	КонецЕсли;
	
	одОбменДанными.ЗакрытьЛогФайл(ОбработкаДоставкаСообщений.ЛогФайлОбъект);
	// по завершении обмена выполним удаление временных файлов (оставшихся от всяких проверок и не коррект. обменов)
	Если ВыполнятьОбменНаКлиенте Тогда
		УдалитьВременныеФайлы();
	Иначе
		пвПривилегированныйМодуль.УдалитьВременныеФайлы(УзелОбмена, НастройкаОбмена);
	КонецЕсли;

	Попытка
		одОбменДанными.ЗаписатьПараметрыОбменаДанными(УзелОбменаДоп.ИнформационнаяБаза, НастройкаДоставкиДоп, Ложь, Рез, ИмяФайлаСообщения, СтрОшибок);
	Исключение КонецПопытки;
	
	// чистим уже после доставки (в ее процессе параметры еще нужны) - в любом случае
	одОбменДанными.ОчиститьПараметрыСеансаОбмена();
	
	ДатаОкончанияОбмена = сфТекущаяДатаСервера();
    СтрСообщения = "Общее время выгрузки: " + ПолучитьВремяОбмена(ДатаОкончанияОбмена - ДатаНачалаОбмена)
		+ Символы.ВК + "Выгружено объектов: " + Строка(ПараметрыСеанса.СчетчикОбъектов)
		+ Символы.ВК + "Из них справочников: " + Строка(ПараметрыСеанса.СчетчикСправочников)
		+ Символы.ВК + "Из них документов: " + Строка(ПараметрыСеанса.СчетчикДокументов);
    ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения,
		,Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
	Если НЕ Рез И НЕ ПустаяСтрока(СтрСообщения) Тогда
		ПрерватьЗадание = Истина;
	КонецЕсли;	
	
	Возврат Рез;
	
КонецФункции
 
 // Выполняет последовательно загрузку/выгрузку сообщения обмена для указанного узла
//  по определенной настройке доставке (лок. сеть, фтп, почта)
//
// Параметры:
//	УзелОбменаДоп 			- ПланыОбменыСсылка.УдаленныеПодразделения. Ссылка на узел плана обмена
//	НастройкаДоставкиДоп 	- СправочникСсылка.НастройкиДоставкиСообщений. Настройка доставки сообщения
//
// Возвращаемое значение:
//   Булево. Флаг успешного завершения обмена сообщениями
//
Процедура ВыполнитьОбмен(Знач УзелОбменаДоп = Неопределено, Знач НастройкаДоставкиДоп = Неопределено,Рез = ложь) Экспорт
	
	КонфигурацияИзменена_ = КонфигурацияИзменена();
	Рез = ВыполнитьЗагрузку(УзелОбменаДоп, НастройкаДоставкиДоп);
	Если НЕ ((НЕ КонфигурацияИзменена_) И КонфигурацияИзменена()) Тогда
		Рез = ВыполнитьВыгрузку(УзелОбменаДоп, НастройкаДоставкиДоп);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбмен()

// Выполняет последовательно загрузку/выгрузку сообщения обмена для указанного узла
//  по определенной настройке доставке (лок. сеть, фтп, почта)
//
// Параметры:
//	Нет. 
//	
Процедура ВыполнитьОбменСоВсеми(Рез = Ложь,СтрОшибки = "") Экспорт
	
	//загрузка из всех узлов
	ЗАпрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдаленныеПодразделения.Ссылка КАК Узел,
	|	УдаленныеПодразделения.ИнформационнаяБаза КАК  ИнформационнаяБаза,
	|	УдаленныеПодразделения.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию КАК НастройкаДоставки,
	|   ИСТИНА КАК Прием,
	|   ИСТИНА КАК Отправка,
	|   &КоличествоЭлементовВТранзакции КАК КоличествоЭлементовВТранзакции,
	|   ИСТИНА КАК ДопроводитьПоПартиям
	|ИЗ
	|	ПланОбмена.УдаленныеПодразделения КАК УдаленныеПодразделения
	|ГДЕ
	|	УдаленныеПодразделения.Ссылка <> &Ссылка
	|	И УдаленныеПодразделения.ИнформационнаяБаза <> &ПустаяИнформационнаяБаза
	|	И УдаленныеПодразделения.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию <> &ПустаяНастройкаДоставкиПоУмолчанию";
	Запрос.УстановитьПараметр("Ссылка",УдаленныеПодразделения_ЭтотУзел);  
	Запрос.УстановитьПараметр("КоличествоЭлементовВТранзакции",КоличествоЭлементовВТранзакции);
	Запрос.УстановитьПараметр("ПустаяИнформационнаяБаза",Справочники.СтруктураИнформационныхБаз.ПустаяСсылка());
    Запрос.УстановитьПараметр("ПустаяНастройкаДоставкиПоУмолчанию",Справочники.НастройкиДоставкиСообщений.ПустаяСсылка());	
	ТаблицаУзлов = Запрос.Выполнить().Выгрузить();
	Если ТаблицаУзлов.Количество() = 0 Тогда
	   Рез = Ложь;
	   СтрОшибки = "Обмен невозможен. Необходимо проверить настройки доставки по умолчанию.";
	   Возврат;
    КонецЕсли; 
   
	Попытка
		фзФоновыеЗадания.ВыполнитьОбмен(ТаблицаУзлов,ВестиЖурналРегистрации,
			ВестиЛогФайл,КаталогЛогов,КомментироватьОбъекты, ВыполнятьОбменНаКлиенте, Рез);
	Исключение 
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		ТекстОшибки = "Ошибка выполнения обмена со всеми: " + ТекстОшибки + Символы.ВК + "(Имя модуля: " + ИнформацияОбОшибке().ИмяМодуля + ", " 
			+ Символы.ВК + "номер строки: " + ИнформацияОбОшибке().НомерСтроки + ")"; 
		#Если Клиент Тогда
			Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		#КонецЕсли 
		ЗаписьЖурналаРегистрации("Обмен", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Возврат;
	КонецПопытки;

	
КонецПроцедуры // ВыполнитьОбменСоВсеми()

// Выполняет проверку запуска регламентного задания обмена
//
// Параметры
//  Нет
//
// Возвращаемое значение:
//   Булево   – флаг запущенности задания обмена
//
Функция ПроверитьЗапускЗаданияОбмена(ПроверятьВсеФоновые = Ложь,ПредставлениеФоновыхЗаданий = "") Экспорт

	// 1. Сначала проверим не запущена ли другая обработка обмена
	// ??? дописать
	// 2. Проверим запуск фонового задания обмена
	СостояниеЗадания  = СостояниеФоновогоЗадания.Активно;
	СтруктураОтбора = Новый Структура("ИмяМетода,Состояние","фзФоновыеЗадания.ОбменСУдаленнымиПодразделениями",СостояниеЗадания);
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(СтруктураОтбора);
	Если ПроверятьВсеФоновые Тогда
		КолвоЗаданий = МассивФоновыхЗаданий.Количество();
	Иначе
		КолвоЗаданий = МассивФоновыхЗаданий.Количество()- 1; // одно из них - текущее
	КонецЕсли;
		
	Если КолвоЗаданий > 0 Тогда
		Для Инд = 0 По МассивФоновыхЗаданий.ВГраница() Цикл
			ТекПредставление = фзФоновыеЗадания.ПредставлениеФоновогоЗадания(МассивФоновыхЗаданий[Инд]);
			ПредставлениеФоновыхЗаданий = ПредставлениеФоновыхЗаданий + Символы.ПС + ТекПредставление;
		КонецЦикла;
	КонецЕсли;
	ПредставлениеФоновыхЗаданий = СокрЛП(ПредставлениеФоновыхЗаданий);
	
	Возврат  (КолвоЗаданий > 0);
		
КонецФункции // ПроверитьЗапускЗаданияОбмена()

// Удаляет временные файлы (не сообщений) из темп-каталога обменов,
// оставшиеся в результате сбоев обмена
//
Процедура УдалитьВременныеФайлы()
	
	// определимся с местонахождением временных файлов
	КорневойКаталог = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КорневойКаталог) Тогда
		Если НастройкаОбмена.Пустая() Тогда
			НастройкаДоставкиДоп = УзелОбмена.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
		Иначе
			НастройкаДоставкиДоп = НастройкаОбмена;
		КонецЕсли;
		КорневойКаталог = НастройкаДоставкиДоп.КаталогОбменов;
	КонецЕсли;	
	
	Если НастройкаОбмена.ИспользоватьВременныйКаталог Тогда 
		КаталогТемпФайлов = КорневойКаталог + "TEMP\";
	Иначе
		КаталогТемпФайлов = КаталогВременныхФайлов();
	КонецЕсли;
	
	// мусор мог остаться и в самом корневом каталоге
	МассивКаталоговВремФайлов = Новый Массив;
	МассивКаталоговВремФайлов.Добавить(КаталогТемпФайлов);
	МассивКаталоговВремФайлов.Добавить(КорневойКаталог);
	
	Для Сч = 0 По МассивКаталоговВремФайлов.ВГраница() Цикл
		КаталогТемпФайлов = МассивКаталоговВремФайлов[Сч];
		ТекстОшибки = "";
		Если НЕ одОбменДанными.ЕстьКаталог(КаталогТемпФайлов,ТекстОшибки) Тогда
			СтрОшибки = "Ошибка при удалении файлов из временного каталога <" + КаталогТемпФайлов + ">: " + ТекстОшибки;
			ОбработкаДоставкаСообщений.ЗаписьЛога(ПрефиксЛога + "УдалениеВремФайлов", СтрОшибки,
			,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
			Продолжить;
		КонецЕсли;
		
		МассивМасокФайлов = Новый Массив;
		МассивМасокФайлов.Добавить("_TstWRAcc_*.*");
		МассивМасокФайлов.Добавить("test_exch_*.*");
		Для Сч2 = 0 По МассивМасокФайлов.ВГраница() Цикл
			МаскаФайлов = МассивМасокФайлов[Сч2];
			УдалятьВремФайлы = Ложь;
			мсвФайлов = НайтиФайлы(КаталогТемпФайлов,МаскаФайлов);
			Если мсвФайлов.Количество()<>0 Тогда
				УдалятьВремФайлы = Истина;
			КонецЕсли;
			
			Если УдалятьВремФайлы Тогда
				ДатаУдаления = ТекущаяДата() - 3600*2; // создали 2 часа назад
				Для Каждого ТекФайл Из мсвФайлов Цикл
					Попытка
						Если ТекФайл.ПолучитьВремяИзменения() > ДатаУдаления Тогда
							Продолжить;
						КонецЕсли;
						УдалитьФайлы(ТекФайл.ПолноеИмя);
					Исключение
					КонецПопытки;
				КонецЦикла;
			КонецЕсли; 
		КонецЦикла;
		
	КонецЦикла;	// 	Для Сч = 0
	
КонецПроцедуры

Функция ПолучитьВремяОбмена(КоличествоСекунд)
	
	Часов = Цел(КоличествоСекунд/3600);
	Минут = Цел((КоличествоСекунд-Часов*3600)/60);
	Секунд = КоличествоСекунд - Часов*3600 - Минут*60;
	СтрокаЧасов = ?(Часов<10,"0"+Строка(Часов),Строка(Часов));
	СтрокаМинут = ?(Минут<10,"0"+Строка(Минут),Строка(Минут));
	СтрокаСекунд = ?(Секунд<10,"0"+Строка(Секунд),Строка(Секунд));
	Возврат СтрокаЧасов + ":" + СтрокаМинут + ":" + СтрокаСекунд;
	
КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

НастройкаТекущегоУзла = Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть;
УдаленныеПодразделения_ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
Если ЗначениеЗаполнено(УдаленныеПодразделения_ЭтотУзел) Тогда
	Если НЕ УдаленныеПодразделения_ЭтотУзел.ИнформационнаяБаза.Пустая() Тогда
		Если НЕ УдаленныеПодразделения_ЭтотУзел.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию.Пустая() Тогда
			НастройкаТекущегоУзла=УдаленныеПодразделения_ЭтотУзел.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
КонецЕсли;
ОбработкаДоставкаСообщений = Обработки.ДоставкаСообщений.Создать();
ПрефиксЛога = "Обмен.";
Если ЗначениеЗаполнено(УзелОбмена.Ссылка) Тогда
	ПрефиксЛога = ПрефиксЛога + "Узел_" + СокрЛП(УзелОбмена.Ссылка.Код) + ".";
КонецЕсли;
счОбъектов = 0;
счСправочников = 0;
счДокументов = 0;
// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
Перем Адрес Экспорт;
Перем Заголовки Экспорт;
Перем Соединение Экспорт;
Перем КонкретныйЗН Экспорт;
Перем КонкретныйЗнР Экспорт;
Перем ПрофильПодключения Экспорт;

Перем ОбновитьАвтомобиль Экспорт;
Перем ОбновитьКонтрагента Экспорт;
Перем ОбновитьТоварыИУслуги Экспорт;
Перем ВыгрузитьКакОткрытый Экспорт;

// Пример процедуры вызываемой регламентом
// 
//Процедура Инфообмен() Экспорт
//	Обработка = Обработки.БТ_Инфообмен.Создать(); 
//	Обработка.ВыгрузитьВсеЗаказНаряды();
//	Обработка.ВыгрузитьВсеЗаявкиНаРемонт();
//КонецПроцедуры


// TODO::
// 1. Согласия должны записываться с учетной записью 
// ^^^^^^^^

#Область Константы
Функция СписокКонстантныхЗначенийОбработки()
	
	КонсЗнач = Новый Структура;
	
	// Если указано Неопределено то профиль подбирается по сопоставлению Профиль/Подразделения
	// ТАКЖЕ при записи ЗН или ЗнР в сопоставление игнорируется проверка по подразделению из сопоставления Профиль/Подразделения
	
	// Если указана Конкретная учетная запись то в данном инфообмене будет использоваться именно она
	КонсЗнач.Вставить("ТолькоПрофильПодразделения", Неопределено);
	
	КонсЗнач.Вставить("РегистрСопоставления",РегистрыСведений.БТ_СопоставлениеДанныхJETOUR);
	КонсЗнач.Вставить("РегистрСопоставленияВидовРемонта",РегистрыСведений.БТ_СопоставлениеВидовРемонтаJETOUR);
	КонсЗнач.Вставить("РегистрСопоставленияПодразделений",РегистрыСведений.БТ_СопоставлениеПодразделенийJETOUR);

	КонсЗнач.Вставить("КонтрагентПоУмолчанию", Справочники.Контрагенты.НайтиПоКоду("ЦБ000440"));
	КонсЗнач.Вставить("IDЦехКузовногоРемонта", 2);
	КонсЗнач.Вставить("IDЦехНекузовногоРемонта", 1);

	КонсЗнач.Вставить("РазбивкаВидовРемонтаПоТО", Ложь);
	КонсЗнач.Вставить("ОбрабатыватьСогласияПриВыгрузкеЗН", Ложь);
	
	//// Механизм журнала записи на ремонт ////
	КонсЗнач.Вставить("ВыгружатьЛиЗаписиНаРемонт", Истина);
	
	//Массив реквизитов по которым будет проверяться необходимость обновления заявки на ремонт
	МассивРеквизитовДляПроверкиОбновленияЗаявкиНаРемонт = Новый Массив;
	МассивРеквизитовДляПроверкиОбновленияЗаявкиНаРемонт.Добавить("Состояние");
	МассивРеквизитовДляПроверкиОбновленияЗаявкиНаРемонт.Добавить("ПометкаУдаления");
	МассивРеквизитовДляПроверкиОбновленияЗаявкиНаРемонт.Добавить("Планирование");
	КонсЗнач.Вставить("МассивРеквизитовДляПроверкиЗнР", МассивРеквизитовДляПроверкиОбновленияЗаявкиНаРемонт);
	//
	
	// Автоработы которые подразумевают прием и выдачу автомобиля в Плане ремонта
	КонсЗнач.Вставить("АвтоработаПриемаАвтомобиля", Справочники.Автоработы.Приемка);
	КонсЗнач.Вставить("АвтоработаВыдачиАвтомобиля", Справочники.Автоработы.Выдача);
	//
	
	// Автоматическое добавление выдачи автомобиля в случае если ее нет в записи на ремонт
	// Выдача происходит точно так же как и при приемке
	// Выдача создается при записи заявки на ремонт
	КонсЗнач.Вставить("АвтоматическиПланироватьВыдачуАвтомобиля", Истина);
	КонсЗнач.Вставить("ВремяПланируемойВыдачиАвтомобиля", 600); // указывать в секундах
	//
	
	//// Механизм журнала записи на ремонт ////
	
	Возврат КонсЗнач; 
	
КонецФункции
Функция ПолучитьКонстантноеЗначение(ИмяКонстанты) Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство(ИмяКонстанты,ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьРегистрСопоставления() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("РегистрСопоставления",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьРегистрСопоставленияВидовРемонта() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("РегистрСопоставленияВидовРемонта",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьРегистрСопоставленияПодразделений() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("РегистрСопоставленияПодразделений",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьИмяРегистра(РегистрСведенийМенеджер) Экспорт
	Возврат РегистрСведенийМенеджер.СоздатьНаборЗаписей().Метаданные().Имя;
КонецФункции
Функция ПолучитьКонтрагентаПоУмолчанию() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("КонтрагентПоУмолчанию",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьЕстьЛиРазбивкаВидовРемонтаПоТО() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("РазбивкаВидовРемонтаПоТО",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьЕстьЛиОбрабатыватьСогласияПриВыгрузкеЗН() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("ОбрабатыватьСогласияПриВыгрузкеЗН",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
Функция ПолучитьВыгружатьЛиЗаписиНаРемонт() Экспорт 
	ЗначКонс = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("ВыгружатьЛиЗаписиНаРемонт",ЗначКонс);
	
	Возврат ЗначКонс;
КонецФункции
#КонецОбласти

#Область КонстантыМеханизмаСогласий
Функция СписокКонстантныхЗначенийМеханизмаСогласий()
	
	СписокПодразделенийДляОбработкиСогласия = Новый Массив;
	СписокПодразделенийДляОбработкиСогласия.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("ЦБ000280"));
	СписокПодразделенийДляОбработкиСогласия.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("ЦБ000436"));
	
	СписокВидовРемонтаДляОбработкиСогласия = Новый Массив;
	СписокВидовРемонтаДляОбработкиСогласия.Добавить(Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000037"));
	СписокВидовРемонтаДляОбработкиСогласия.Добавить(Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000033"));
	
	КонсЗнач = Новый Структура;
	КонсЗнач.Вставить("СписокПодразделенийДляОбработкиСогласия",СписокПодразделенийДляОбработкиСогласия);
	КонсЗнач.Вставить("СписокВидовРемонтаДляОбработкиСогласия",СписокВидовРемонтаДляОбработкиСогласия);
	Возврат КонсЗнач; 
	
КонецФункции
#КонецОбласти

#Область НастройкиОбработки
Функция ЗаполнитьСтруктуруНастроек()
	СтруктураНастроек = Новый Структура;
	
	Для Каждого РеквизитОбработки из ЭтотОбъект.Метаданные().Реквизиты Цикл
		СтруктураНастроек.Вставить(РеквизитОбработки.Имя, ?(ЗначениеЗаполнено(ЭтотОбъект[РеквизитОбработки.Имя]),ЭтотОбъект[РеквизитОбработки.Имя],Неопределено)); 
	КонецЦикла;
	
	СтруктураНастроек.Вставить("ПодменяемыеДанные", ЭтотОбъект.ПодменяемыеДанные.Выгрузить()); 
	
	Возврат СтруктураНастроек;
КонецФункции
Процедура СохранитьНастройки() Экспорт
	
	СтруктураНастроек = ЗаполнитьСтруктуруНастроек();
	ИмяОбработки = ЭтотОбъект.Метаданные().ПолноеИмя();
	ХранилищеОбщихНастроек.Сохранить(ИмяОбработки+"/Общие",, СтруктураНастроек,,ИмяОбработки+"/Общие");
	
КонецПроцедуры
Процедура ВосстановитьНастройки() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().ПолноеИмя();
	СохраненныеНастройки = ХранилищеОбщихНастроек.Загрузить(ИмяОбработки+"/Общие",,,ИмяОбработки+"/Общие");
	Если СохраненныеНастройки = Неопределено ИЛИ ТипЗнч(СохраненныеНастройки) <> Тип("Структура") Тогда
		СохраненныеНастройки = новый Структура;
	КонецЕсли;
	
	Для Каждого ТекНастройка Из СохраненныеНастройки Цикл
		Если СохраненныеНастройки.Свойство(ТекНастройка.Ключ) Тогда
			СохраненнаяНастройка = СохраненныеНастройки[ТекНастройка.Ключ];
		КонецЕсли;
		Попытка
			Если ТипЗнч(СохраненнаяНастройка) = Тип("ТаблицаЗначений") Тогда
				ЭтотОбъект[ТекНастройка.Ключ].Загрузить(СохраненнаяНастройка);
			Иначе
				ЭтотОбъект[ТекНастройка.Ключ] = ?(ЗначениеЗаполнено(СохраненнаяНастройка),СохраненнаяНастройка,Неопределено);
			КонецЕсли;
		Исключение	
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры
Функция ВсеЛиОбязательныеРеквизитыЗаполнены() Экспорт
	
	Для Каждого РеквизитОбработки из ЭтотОбъект.Метаданные().Реквизиты Цикл
		Если РеквизитОбработки.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку Тогда
			Если НЕ ЗначениеЗаполнено(ЭтотОбъект[РеквизитОбработки.Имя]) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
	
КонецФункции
#КонецОбласти

#Область HTTPЗапросы
Функция ПолучитьПрофильПодразделения(ПодразделениеЗН) Экспорт
	
	// Если указана принудительно на весь инфообмен учетка то будем использовать ее
	ТолькоПрофильПодразделения = ПолучитьКонстантноеЗначение("ТолькоПрофильПодразделения");
	Если ЗначениеЗаполнено(ТолькоПрофильПодразделения) Тогда
		Возврат ТолькоПрофильПодразделения;
	КонецЕсли;
	
	// Иначе проверим по сопоставлению
	НаименованиеРегистраСопоставленийПодразделений = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияПодразделений());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СопоставлениеПодразделений.ПрофильПодключения КАК ПрофильПодключения
		|ИЗ
		|	РегистрСведений."+НаименованиеРегистраСопоставленийПодразделений+" КАК СопоставлениеПодразделений
		|ГДЕ
		|	СопоставлениеПодразделений.Подразделение = &Подразделение";
	
	СтрокаДляПодмены = ПодменяемыеДанные.Найти(ПодразделениеЗН, "ПодменяемоеЗначение");
	
	Если СтрокаДляПодмены <> Неопределено Тогда
		Запрос.УстановитьПараметр("Подразделение", СтрокаДляПодмены.ЗначениеПодмены);			
	Иначе
		Запрос.УстановитьПараметр("Подразделение", ПодразделениеЗН);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Возврат ВыборкаДетальныеЗаписи.ПрофильПодключения;
		
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции
Функция УстановитьСоединение() Экспорт
	
	Если Не ЗначениеЗаполнено(ПрофильПодключения) Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	URL = ПрофильПодключения.URL;
	ИмяДоступа = ПрофильПодключения.ИмяПользователя;
	ПарольДоступа = ПрофильПодключения.ПарольПользователя;
	
	Соединение = Новый HTTPСоединение(URL, ,
                ИмяДоступа,
                ПарольДоступа,,
                600,
                ?(ПрофильПодключения.HTTPS, Новый ЗащищенноеСоединениеOpenSSL, Неопределено));
    
    Заголовки = Новый Соответствие;
	
	СтрокаАвторизации = ПолучитьBase64СтрокуИзДвоичныхДанных(
		ПолучитьДвоичныеДанныеИзСтроки(
			СокрЛП(ИмяДоступа) + ":" + ПарольДоступа, КодировкаТекста.UTF8, Ложь));
	
	
	Заголовки.Вставить("Content-Type", "application/json;charset=utf-8");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Authorization", ПрофильПодключения.Authorization);
	
КонецФункции
Функция ОтправитьЗапрос(Метод, Структура, Ошибка, ВызываемыйМетод = "POST")
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
			
	СтрокаДанных = ЗаписьJSON.Закрыть();
	
	Запрос = Новый HTTPЗапрос(Метод, Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаДанных, КодировкаТекста.UTF8);
	
    Ответ = Соединение.ВызватьHTTPМетод(ВызываемыйМетод, Запрос); 

	Если Ответ.КодСостояния >= 200 и Ответ.КодСостояния < 400 Тогда 
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		
		МассивДанных = ПрочитатьJSON(Чтение);
		
		Если ВызываемыйМетод = "GET" Тогда
			Возврат МассивДанных;
		Иначе
			Возврат МассивДанных.id;
		КонецЕсли;

	Иначе	
		Ошибка = "Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат 0;
	КонецЕсли;	
	
КонецФункции
Функция УстановитьСоединениеПоПрофилюЗаказНаряда(ПодразделениеЗН, Ошибка) Экспорт

	//Проверим, что подразделение не пустое
	Если ПодразделениеЗН = Неопределено Тогда
		Ошибка = "Не определено подразделение";
		Возврат Ложь;
	КонецЕсли;

	//Определим профиль подключения для выгрузки заказ-наряда
	ПрофильПодключения = ПолучитьПрофильПодразделения(ПодразделениеЗН);
	Если НЕ ЗначениеЗаполнено(ПрофильПодключения) Тогда
		Ошибка = "Не сопоставлен профиль подключения";
		Возврат Ложь;
	КонецЕсли;

	//Установим соединение
	УстановитьСоединение();

	Возврат Истина;

КонецФункции
#КонецОбласти

#Область ЗагрузкаДанных
Функция ЗагрузитьДанные(СтруктураОчередиЗагрузки) Экспорт
	
	//Установим учетную запись
	Если СтруктураОчередиЗагрузки.Свойство("УчетнаяЗапись") И ЗначениеЗаполнено(СтруктураОчередиЗагрузки.УчетнаяЗапись) Тогда
		ПрофильПодключения = СтруктураОчередиЗагрузки.УчетнаяЗапись;
	КонецЕсли;
	
	УстановитьСоединение();	

	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьБренды") И СтруктураОчередиЗагрузки.ПолучитьБренды = Истина Тогда
		ПолучитьДанные("/api/brand", "Бренд", Справочники.Модели.ПустаяСсылка(), "");
	КонецЕсли;
	
	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьМодели") И СтруктураОчередиЗагрузки.ПолучитьМодели = Истина Тогда
		ПолучитьДанные("/api/model", "Модель", Справочники.Модели.ПустаяСсылка(), "brand_id,is_recent");
	КонецЕсли;	                     
	
	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьЦеха") И СтруктураОчередиЗагрузки.ПолучитьЦеха = Истина Тогда
		ПолучитьДанные("/api/purchase-type", "Цех", Справочники.Цеха.ПустаяСсылка(), "");
	КонецЕсли;

	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьТипыОплат") И СтруктураОчередиЗагрузки.ПолучитьТипыОплат = Истина Тогда
		ПолучитьДанные("/api/purchase-group", "ТипОплаты", Справочники.ТипыОплат.ПустаяСсылка(), "status");
	КонецЕсли;
	
	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьВидыРемонта") И СтруктураОчередиЗагрузки.ПолучитьВидыРемонта = Истина Тогда
		ПолучитьДанные("/api/purchase-tag", "ВидРемонта", Справочники.ВидыРемонта.ПустаяСсылка(), "status");
	КонецЕсли;
	
	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьДолжности") И СтруктураОчередиЗагрузки.ПолучитьДолжности = Истина Тогда
		ПолучитьДанные("/api/position", "Должность", Справочники.Должности.ПустаяСсылка(), "code");
	КонецЕсли;	
	
	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьСотрудников") И СтруктураОчередиЗагрузки.ПолучитьСотрудников = Истина Тогда
		ПолучитьДанные("/api/manager", "Сотрудник", Справочники.Сотрудники.ПустаяСсылка(), 
			"code,first_name,middle_name,last_name, email,birthday,employment,position_list,status");
	КонецЕсли;
	
	// TODO - проверить
	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьПосты") И СтруктураОчередиЗагрузки.ПолучитьПосты = Истина Тогда
		ПолучитьДанные("/api/service/post-service", "Пост", Справочники.Цеха.ПустаяСсылка());
	КонецЕсли;

	Если СтруктураОчередиЗагрузки.Свойство("ПолучитьГрафикиСотрудников") И СтруктураОчередиЗагрузки.ПолучитьГрафикиСотрудников = Истина Тогда
		ПолучитьДанные("/api/staff/schedule", "ГрафикРаботы", Справочники.Сотрудники.ПустаяСсылка(), "user_id,date");
	КонецЕсли;
	//

КонецФункции
Процедура ПолучитьДанные(Адрес, Тип = "", Данные1С = "", СписокПолей = "")
	
	Если ПустаяСтрока(Тип) Тогда
		Возврат;
	КонецЕсли;
	
	Страница = 1;
	КоличествоСтраниц = 1;
	
	Пока Страница <= КоличествоСтраниц Цикл

		Запрос = Новый HTTPЗапрос(Адрес + "?per-page=50&page="+Страница, Заголовки);	
		Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос);
		
		Если Ответ.КодСостояния <> 200 Тогда
			Сообщить("Ошибка получения типа данных: " + Тип);
			Возврат;
		КонецЕсли;	
		
		// Получаем количество страниц
		КолСтрСтр = Ответ.Заголовки.Получить("X-Pagination-Page-Count");
		Если КолСтрСтр <> Неопределено Тогда
			КоличествоСтраниц = Число(КолСтрСтр);
		КонецЕсли;
		
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		
		МассивДанных = ПрочитатьJSON(Чтение);
		
		Если НЕ МассивДанных.количество() Тогда
			Сообщить("Получили пустой массив типа данных: " + Тип);
			Возврат;
		КонецЕсли;
		
		Для Каждого ЭлементДанных Из МассивДанных Цикл	
			ID = ЭлементДанных.ID;
			Если Тип <> "Сотрудник" И Тип <> "ГрафикРаботы" Тогда
				Name = ЭлементДанных.name;
			ИначеЕсли Тип = "ГрафикРаботы" Тогда
				Name = "";
			Иначе     
				Name = ЭлементДанных.last_name + " " + ЭлементДанных.first_name + " " + ЭлементДанных.middle_name;
			КонецЕсли;
			
			// Если Загружаем график сотрудников то запишем данные в регистр с карточкой сотрудника
			Если Тип = "ГрафикРаботы" Тогда
				IDСотрудника = ЭлементДанных.user_id;
				// Попытаемся найти такого пользователя
				НаборСотрудника = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
				НаборСотрудника.Отбор.ID.Установить(IDСотрудника);
				НаборСотрудника.Отбор.Тип.Установить("Сотрудник");
				НаборСотрудника.Прочитать();
				Если НаборСотрудника.Количество() Тогда
					// Сотрудник найден, получим ссылку на него и используем при загрузке графика
					Если ЗначениеЗаполнено(НаборСотрудника[0].Данные1С) Тогда
						Данные1С = НаборСотрудника[0].Данные1С;
					Иначе
						// Нет смысла записывать график без самого сотрудника
						Сообщить("["+ID+"] График не загружен так как не сопоставлен сотрудник 1с в регистре сопоставления!");
						Продолжить;
					КонецЕсли;
				Иначе
					// Нет смысла записывать график без самого сотрудника
					Сообщить("["+ID+"] График не загружен так как не найден сотрудник в регистре сопоставления!");
					Продолжить;
				КонецЕсли;	
			КонецЕсли;
			// 
			
			Набор = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
			Набор.Отбор.ID.Установить(ID);
			Набор.Отбор.Тип.Установить(Тип);
			
			Набор.Прочитать();
			Если Набор.Количество() Тогда
				Запись = Набор[0];
			Иначе
				Запись = Набор.Добавить();
				
				Запись.Период = ТекущаяДатаСеанса();
				Запись.ID = ID;
				Запись.Тип = Тип;
				
			КонецЕсли;
			
			// Посты загружаются на определенную учетную запись
			Если Тип = "Пост" Тогда
				Запись.ДопДанные1С = ПрофильПодключения;	
			КонецЕсли;
			
			Запись.Name = Name;
			
			Если Не ЗначениеЗаполнено(Запись.Данные1С) Тогда
				Запись.Данные1С = Данные1С;
			КонецЕсли;	
			
			Структура = Новый Структура;
			
			Массив = СтрРазделить(СписокПолей, ",");
			Для сч = 0 По Массив.Количество()-1 Цикл
				
				ИмяПоля = СокрЛП(Массив[сч]);
				
				Если Не ПустаяСтрока(ИмяПоля) Тогда
					Структура.Вставить(ИмяПоля, ЭлементДанных[ИмяПоля]);
				КонецЕсли;	
				
			КонецЦикла;	
			
			Если Структура.Количество() > 0 Тогда
				ЗаписьJSON = Новый ЗаписьJSON;
				ЗаписьJSON.УстановитьСтроку();
				ЗаписатьJSON(ЗаписьJSON, Структура);
				
				Запись.ДопИнфо = ЗаписьJSON.Закрыть();
			Иначе
				Запись.ДопИнфо = "";
			КонецЕсли;
			
			// При загрузке графиков также записать дату графика в ДопИнфо
			Если Тип = "ГрафикРаботы" Тогда
				Запись.ДопИнфо = ЭлементДанных.date;		
			КонецЕсли;
			//
			
			
			Набор.Записать();
		КонецЦикла;
		Страница = Страница + 1;
	КонецЦикла
	
КонецПроцедуры		
#КонецОбласти

#Область ВыгрузкаДанных
// Выборки данных
Функция ПолучитьВыборкуВсехЗаказНарядов()
	
	НаимРегистраСоответствий = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СопоставлениеДанных.Данные1С КАК Данные1С,
	                      |	СопоставлениеДанных.Период КАК Период,
	                      |	СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСоответствий+" КАК СопоставлениеДанных
	                      |ГДЕ
	                      |	СопоставлениеДанных.Тип = ""ЗаказНаряд""
	                      |	И НЕ СопоставлениеДанных.Данные1С.НеВыгружатьИмпортеру");
		
	Выборка = Запрос.Выполнить().Выбрать();	
	Возврат Выборка;
	
КонецФункции
Функция ПолучитьВыборкуВсехНеВыгрЗаказНарядов()
	
	НаимРегистраСоответствий = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СопоставлениеДанных.Данные1С КАК Данные1С,
	                      |	СопоставлениеДанных.Период КАК Период,
	                      |	СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСоответствий+" КАК СопоставлениеДанных
	                      |ГДЕ
	                      |	СопоставлениеДанных.ID = 0
	                      |	И СопоставлениеДанных.Тип = ""ЗаказНаряд""
	                      |	И НЕ СопоставлениеДанных.Данные1С.НеВыгружатьИмпортеру");
		
	Выборка = Запрос.Выполнить().Выбрать();	
	Возврат Выборка;
	
КонецФункции
Функция ПолучитьВыборкуВсехНеВыгрРаботЗаказНарядов()
	
	НаимРегистраСоответствий = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.Данные1С КАК Данные1С,
	                      |	БТ_СопоставлениеДанных.ДопДанные1С КАК ДопДанные1С,
	                      |	БТ_ДанныеПоЗН.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСоответствий+" КАК БТ_СопоставлениеДанных
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений."+НаимРегистраСоответствий+" КАК БТ_ДанныеПоЗН
	                      |		ПО БТ_СопоставлениеДанных.Данные1С = БТ_ДанныеПоЗН.Данные1С
	                      |			И (БТ_ДанныеПоЗН.Тип = ""ЗаказНаряд"")
	                      |			И (БТ_ДанныеПоЗН.ID <> 0)
	                      |ГДЕ
	                      |	БТ_СопоставлениеДанных.ID = 0
	                      |	И БТ_СопоставлениеДанных.Тип = ""ЗаказНарядРаботы""");
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Возврат Выборка;
	
КонецФункции
Функция ПолучитьВыборкуВсехНеВыгрТоваровЗаказНарядов()
	
	НаимРегистраСоответствий = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.Данные1С КАК Данные1С,
	                      |	БТ_СопоставлениеДанных.ДопДанные1С КАК ДопДанные1С,
	                      |	БТ_ДанныеПоЗН.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСоответствий+" КАК БТ_СопоставлениеДанных
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений."+НаимРегистраСоответствий+" КАК БТ_ДанныеПоЗН
	                      |		ПО БТ_СопоставлениеДанных.Данные1С = БТ_ДанныеПоЗН.Данные1С
	                      |			И (БТ_ДанныеПоЗН.Тип = ""ЗаказНаряд"")
	                      |			И (БТ_ДанныеПоЗН.ID <> 0)
	                      |ГДЕ
	                      |	БТ_СопоставлениеДанных.ID = 0
	                      |	И БТ_СопоставлениеДанных.Тип = ""ЗаказНарядТовары""");
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Возврат Выборка;
	
КонецФункции
//
Функция ПолучитьВыборкуВсехНеВыгрЗаявокНаРемонт()
	// Получаем все заявки на ремонт, которые нужно выгрузить (ID = 0 или требуется обновление)
	
	НаимРегистраСопоставления = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.Данные1С КАК Данные1С,
	                      |	БТ_СопоставлениеДанных.Период КАК Период,
	                      |	БТ_СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений." + НаимРегистраСопоставления + " КАК БТ_СопоставлениеДанных
	                      |ГДЕ
	                      |	(БТ_СопоставлениеДанных.ID = 0
	                      |			ИЛИ БТ_СопоставлениеДанных.ТребуетсяОбновление)
	                      |	И БТ_СопоставлениеДанных.Тип = ""ПланРемонта""");
	
	
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции
Функция ПолучитьВыборкуНеВыгрЗаявокНаРемонтПоОдной(ОднаЗаявка)
	// Получаем конкретную заявку на ремонт для выгрузки
	
	НаимРегистраСопоставления = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.Данные1С КАК Данные1С,
	                      |	БТ_СопоставлениеДанных.Период КАК Период,
	                      |	БТ_СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений." + НаимРегистраСопоставления + " КАК БТ_СопоставлениеДанных
	                      |ГДЕ
	                      |	БТ_СопоставлениеДанных.Данные1С = &ОднаЗаявка
	                      |	И БТ_СопоставлениеДанных.Тип = ""ПланРемонта""
	                      |	И (БТ_СопоставлениеДанных.ID = 0
	                      |			ИЛИ БТ_СопоставлениеДанных.ТребуетсяОбновление)");
	
	Запрос.УстановитьПараметр("ОднаЗаявка", ОднаЗаявка);
	
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции
//

// Выгрузка заказ-наряд
Процедура ВыгрузитьВсеЗаказНаряды() Экспорт

	// Выгрузка заказ-нарядов
	Выборка = ПолучитьВыборкуВсехНеВыгрЗаказНарядов();
	Пока Выборка.Следующий() Цикл
		Ошибка = "";
		//@skip-check query-in-loop
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			//@skip-check query-in-loop
			идЗН = ВыгрузитьЗН(Выборка.Данные1С, Ошибка, Выборка.ID);
		КонецЕсли;
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНаряд", идЗН, Ошибка);
	КонецЦикла;

	// Выгрузка работ по заказ-нарядам
	Выборка = ПолучитьВыборкуВсехНеВыгрРаботЗаказНарядов();
	Пока Выборка.Следующий() Цикл
		Ошибка = "";
		//@skip-check query-in-loop
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			Работа = Выборка.ДопДанные1С;
			идРаботЗН = ВыгрузитьРаботыЗН(Выборка.Данные1С, Выборка.ID, Работа, Ошибка);
		КонецЕсли;
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНарядРаботы", идРаботЗН, Ошибка, Работа);
	КонецЦикла;

	// Выгрузка товаров по заказ-нарядам
	Выборка = ПолучитьВыборкуВсехНеВыгрТоваровЗаказНарядов();
	Пока Выборка.Следующий() Цикл
		Ошибка = "";
		//@skip-check query-in-loop
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			Товар = Выборка.ДопДанные1С;
			идТовараЗН = ВыгрузитьТоварыЗН(Выборка.Данные1С, Выборка.ID, Товар, Ошибка);
		КонецЕсли;
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНарядТовары", идТовараЗН, Ошибка, Товар);
	КонецЦикла;

КонецПроцедуры
Процедура ВыгрузитьОдинЗаказНаряд(ОдинЗН) Экспорт

	ЗННайден = Ложь;
	// Выгрузка заказ-наряда
	Выборка = ПолучитьВыборкуВсехЗаказНарядов();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Данные1С <> ОдинЗН Тогда
			Продолжить;
		КонецЕсли;
		ЗННайден = Истина;
		Ошибка = "";
		//@skip-check query-in-loop
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			идЗН = ВыгрузитьЗН(Выборка.Данные1С, Ошибка, Выборка.ID);
		КонецЕсли;
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНаряд", идЗН, Ошибка);
		Прервать;
	КонецЦикла;

	Если ЗННайден = Ложь Тогда
		Сообщить("Заказ-наряд не найден в регистре");
		Возврат;
	КонецЕсли;
	
	// Выгрузка работ по заказ-наряду
	Выборка = ПолучитьВыборкуВсехНеВыгрРаботЗаказНарядов();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Данные1С <> ОдинЗН Тогда
			Продолжить;
		КонецЕсли;
		Ошибка = "";
		//@skip-check query-in-loop
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			Работа = Выборка.ДопДанные1С;
			идРаботЗН = ВыгрузитьРаботыЗН(Выборка.Данные1С, Выборка.ID, Работа, Ошибка);
		КонецЕсли;
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНарядРаботы", идРаботЗН, Ошибка, Работа);
		Прервать;
	КонецЦикла;

	// Выгрузка товаров по заказ-наряду
	Выборка = ПолучитьВыборкуВсехНеВыгрТоваровЗаказНарядов();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Данные1С <> ОдинЗН Тогда
			Продолжить;
		КонецЕсли;
		Ошибка = "";
		//@skip-check query-in-loop
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			Товар = Выборка.ДопДанные1С;
			идТовараЗН = ВыгрузитьТоварыЗН(Выборка.Данные1С, Выборка.ID, Товар, Ошибка);
		КонецЕсли;
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНарядТовары", идТовараЗН, Ошибка, Товар);
		Прервать;
	КонецЦикла;

КонецПроцедуры
Функция ВыгрузитьЗН(ЗН, Ошибка, идОбновления = 0)
	
	//Для удобства
	Если 1 = 2 Тогда
		ЗН = Документы.ЗаказНаряд.ПустаяСсылка();
	КонецЕсли;
	
	//ИдОбновления в строку
	Если ЗначениеЗаполнено(ИдОбновления) Тогда
		ИдОбновленияСтрока = СтрЗаменить(идОбновления, Символы.НПП, "");
	КонецЕсли;
	
	СтрокаДляПодмены = ПодменяемыеДанные.Найти(ЗН.ПодразделениеКомпании, "ПодменяемоеЗначение");
	
	Если СтрокаДляПодмены <> Неопределено Тогда
		ПодразделениеЗН = СтрокаДляПодмены.ЗначениеПодмены;			
	Иначе
		ПодразделениеЗН = ЗН.ПодразделениеКомпании;
	КонецЕсли; 
	
	// Поиск вида ремонта
	ВидРемонта = Неопределено;
	//Вид ремонта + ТО = вид ремонта
	Если ПолучитьЕстьЛиРазбивкаВидовРемонтаПоТО() = Истина Тогда
		Если ЗН.Метаданные().Реквизиты.Найти("БТ_НомерТО") <> Неопределено И ЗначениеЗаполнено(ЗН.БТ_НомерТО) Тогда
			//Вид ремонта = вид ремонта
			ВидРемонта = ПолучитьВидРемонтаПоТО(ЗН.ВидРемонта, ЗН.БТ_НомерТО);
		Иначе
			Ошибка = "Не указан номер ТО";
			Возврат 0;
		КонецЕсли;    
	Иначе
		//Вид ремонта = вид ремонта
		ВидРемонта = ПолучитьВидРемонта(ЗН.ВидРемонта);	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВидРемонта) тогда
		Ошибка = "Не сопоставлен вид ремонта";
		Возврат 0;
	КонецЕсли;

	//Сопоставление типа оплаты
	ТипОплаты = ПолучитьТипОплаты(ЗН.ВидРемонта);
	Если не ЗначениеЗаполнено(ТипОплаты) тогда
		Ошибка = "Не сопоставлен тип оплаты";
		Возврат 0;
	КонецЕсли;	

	//Если Контрагента нет то возьмем контрагента по умолчанию (если он заполнен)
	_Контрагент = ЗН.Контрагент;
	Если Не ЗначениеЗаполнено(_Контрагент) И ЗначениеЗаполнено(ПолучитьКонтрагентаПоУмолчанию()) Тогда
		_Контрагент = ПолучитьКонтрагентаПоУмолчанию();
	КонецЕсли;
	
	//Выгрузка контрагента
	ИдКлиента = 0;
	Если ЗначениеЗаполнено(_Контрагент) Тогда
		ИдКлиента = НайтиВыгруженныйОбъект(_Контрагент, ПрофильПодключения);
		Если Не ЗначениеЗаполнено(ИдКлиента) Тогда
			Ошибка = "";
			ИдКлиента = ВыгрузитьКонтрагента(_Контрагент, ЗН, Ошибка);
			ДобавитьВыгруженныеДанные(_Контрагент, "Контрагент", ИдКлиента, Ошибка, ПрофильПодключения);
			Если Не ЗначениеЗаполнено(ИдКлиента) Тогда
				Ошибка = "Не сопоставлен контрагент";
				Возврат 0;
			КонецЕсли;	
		Иначе
			Если ОбновитьКонтрагента = Истина Тогда
				ВыгрузитьКонтрагента(_Контрагент, ЗН, Ошибка, ИдКлиента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	//Выгрузка автомобиля
	ИдАвто = НайтиВыгруженныйОбъект(ЗН.Автомобиль);
	Если Не ЗначениеЗаполнено(ИдАвто) Тогда
		Ошибка = "";
		ИдАвто = ВыгрузитьАвтомобиль(ЗН.Автомобиль, Ошибка);
		
		Если ЗначениеЗаполнено(ИдАвто) или не ПустаяСтрока(Ошибка) Тогда
			ДобавитьВыгруженныеДанные(ЗН.Автомобиль, "Автомобиль", ИдАвто, Ошибка);
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(ИдАВто) Тогда 
			Ошибка = "Не сопоставлен автомобиль";
			Возврат 0;
		КонецЕсли;	
	Иначе
		Если Не ОбновитьАвтомобиль = Истина Тогда
			ОбновитьАвтомобиль(ЗН.Автомобиль, Ошибка, ИдАвто);
		Иначе
			ВыгрузитьАвтомобиль(ЗН.Автомобиль, Ошибка, ИдАвто)		
		КонецЕсли;	
	КонецЕсли;	
	
	//Выгрузка заказа-наряда
	Ошибка = "";
	
	Структура = Новый Структура;
	Если ЗначениеЗаполнено(идОбновления) Тогда
		Структура.Вставить("ID", идОбновления);
	КонецЕсли;	
	
	Структура.Вставить("code", СокрЛП(ЗН.Номер));
	Структура.Вставить("dateOpen", Формат(ЗН.ДатаСоздания, "ДФ=dd.MM.yyyy"));
	Структура.Вставить("dateClose", Формат(?(ЗначениеЗаполнено(ЗН.ДатаЗакрытия), ЗН.ДатаЗакрытия, ТекущаяДатаСеанса()), "ДФ=dd.MM.yyyy"));
	
	//Выгрузка мастера-приемщика
	Если ЗначениеЗаполнено(ЗН.Мастер) Тогда
		ИдМастера = НайтиВыгруженныйОбъект(ЗН.Мастер);
		Если ЗначениеЗаполнено(ИдМастера) Тогда
			Структура.Вставить("acceptor_id", ИдМастера);
		Иначе
			Ошибка = "";
			ИдМастера = ВыгрузитьСотрудника(ЗН.Мастер, Ошибка);
			
			Если Не ЗначениеЗаполнено(ИдМастера) Тогда 
				Ошибка = "Не сопоставлен мастер-приемщик";
				Возврат 0;
			КонецЕсли;	
			
			Структура.Вставить("acceptor_id", ИдМастера);
		КонецЕсли;	
	КонецЕсли;	
	
	Структура.Вставить("client_id", ИдКлиента); 

	//Выгрузка контактного лица
	КонтактноеЛицо = ПолучитьКонтактноеЛицо(_Контрагент);
	Если ЗначениеЗаполнено(КонтактноеЛицо) И _Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		
		ИдКонтакта = НайтиВыгруженныйОбъект(КонтактноеЛицо, ПрофильПодключения);
		
		Если Не ЗначениеЗаполнено(ИдКонтакта) Тогда
			ИдКонтакта = ВыгрузитьКонтрагента(КонтактноеЛицо, Неопределено, Ошибка);
			
			Если ЗначениеЗаполнено(ИдКонтакта) Тогда
				ДобавитьВыгруженныеДанные(КонтактноеЛицо, "Контрагент", ИдКонтакта, Ошибка, ПрофильПодключения);
            КонецЕсли;
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(ИдКонтакта) Тогда
			Структура.Вставить("contact_person_id", ИдКонтакта);
		Иначе
			Структура.Вставить("contact_person_id", "");
		КонецЕсли;	
	Иначе
	    Структура.Вставить("contact_person_id", "");
    КонецЕсли;


	Структура.Вставить("car_id", ИдАвто);
	
	//Сопоставление цеха
	IDЦехКузовногоРемонта = Неопределено;
	IDЦехНекузовногоРемонта = Неопределено;
	СписокКонстантныхЗначенийОбработки().Свойство("IDЦехКузовногоРемонта", IDЦехКузовногоРемонта);
	СписокКонстантныхЗначенийОбработки().Свойство("IDЦехНекузовногоРемонта", IDЦехНекузовногоРемонта);
	Если ЗначениеЗаполнено(IDЦехКузовногоРемонта) и ЗначениеЗаполнено(IDЦехНекузовногоРемонта) Тогда
		Структура.Вставить("purchase_type_id", ?(найти(нрег(Зн.Цех), "кузов") > 0, IDЦехКузовногоРемонта, IDЦехНекузовногоРемонта));
	Иначе
		Ошибка = "Не сопоставлены цеха в константах обработки. Функция СписокКонстантныхЗначенийОбработки()";
		Возврат 0;
	КонецЕсли;

	
	Структура.Вставить("purchase_group_id", ТипОплаты);
	Если ВыгрузитьКакОткрытый = Истина Тогда
		Структура.Вставить("status", "opened");
	Иначе
		Структура.Вставить("status", "closed");
	КонецЕсли;
	Структура.Вставить("verify", "draft");
	Структура.Вставить("request_reason", ?(Не пустаяСтрока(ЗН.ПричинаОбращения), СокрЛП(ЗН.ПричинаОбращения), СокрЛП(ЗН.ВидРемонта)));
	
	пробег = ПолучитьПробегАвтомобиля(зн.автомобиль);
	
	Если ЗначениеЗаполнено(пробег) Тогда
		Структура.Вставить("mileage",  пробег);
	КонецЕсли;	
	 
	Структура.Вставить("tags", ВидРемонта);  
	Структура.Вставить("recommendations", СокрЛП(ЗН.Рекомендации));  
	
	Ошибка = "";
	Если Не ЗначениеЗаполнено(идОбновления) Тогда
		идЗН = ОтправитьЗапрос("/api/purchase", Структура, Ошибка);
	Иначе     
		идЗН = ОтправитьЗапрос("api/purchase/" + ИдОбновленияСтрока, Структура, Ошибка, "PUT");
	КонецЕсли;
	
	// Выгрузим товары и работы
	ОшибкаПоРаботам = "";
	ОшибкаПоТоварам = "";

	//Если ЗН выгружен
	Если ЗначениеЗаполнено(идЗН) Тогда
		//Если ЗН обновляется и нужно обновить товары и услуги
		Если ЗначениеЗаполнено(идОбновления) И ОбновитьТоварыИУслуги = Истина Тогда
			//Удалим все работы и запчасти из регистра сопоставления
			Наб = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
			Наб.Отбор.Данные1С.Установить(ЗН);
			Наб.Отбор.Тип.Установить("ЗаказНарядРаботы");
			Наб.Записать();
			
			Наб = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
			Наб.Отбор.Данные1С.Установить(ЗН);
			Наб.Отбор.Тип.Установить("ЗаказНарядТовары");
			Наб.Записать();
			
			//Получим с портала все работы по заказ-наряду
			//Нужно учитывать пагинацию
			Структура = Новый Структура;
			МасВсехРабот = Новый Массив(); 
			Страница = 1;
			МаксСтраниц = 30;
			Пока Страница <= МаксСтраниц Цикл
				МасРабот = ОтправитьЗапрос("/api/purchase-work?per-page=50&page=" + Страница + "&purchase_id=" + ИдОбновленияСтрока,,Ошибка, "GET");;
				Если ЗначениеЗаполнено(МасРабот) Тогда
					Для Каждого Элемент Из МасРабот Цикл
						МасВсехРабот.Добавить(Элемент);
					КонецЦикла;
					Если МасРабот.Количество() < 50 Тогда
						Прервать;
					КонецЕсли;	
				КонецЕсли;	
				Страница = Страница + 1;
			КонецЦикла;	

			//Если есть работы то удалим их
			Если ЗначениеЗаполнено(МасВсехРабот) Тогда
				Для Каждого Элемент Из МасВсехРабот Цикл
					Если Элемент.Status = 0 Тогда
						УдалитьРаботу(Элемент.id);
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
			
			//Получим с портала все запчасти по заказ-наряду
			//Нужно учитывать пагинацию
			Структура = Новый Структура;
			МасВсехЗЧ = Новый Массив(); 
			Страница = 1;
			МаксСтраниц = 30;
			Пока Страница <= МаксСтраниц Цикл
				МасЗЧ = ОтправитьЗапрос("/api/purchase-spare?per-page=50&page=" + Страница + "&purchase_id=" + ИдОбновленияСтрока,,Ошибка, "GET");;
				Если ЗначениеЗаполнено(МасЗЧ) Тогда
					Для Каждого Элемент Из МасЗЧ Цикл
						МасВсехЗЧ.Добавить(Элемент);
					КонецЦикла;
					Если МасЗЧ.Количество() < 50 Тогда
						Прервать;
					КонецЕсли;	
				КонецЕсли;	
				Страница = Страница + 1;
			КонецЦикла;
			
			//Если есть запчасти то удалим их
			Если ЗначениеЗаполнено(МасВсехЗЧ) Тогда
				Для Каждого Элемент Из МасВсехЗЧ Цикл
					Если Элемент.Status = 0 Тогда
						УдалитьЗапчасть(Элемент.id);
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;
		КонецЕсли;	
		
		//Если заказ-наряд впервые выгружается или нужно обновить товары и услуги
		Если ОбновитьТоварыИУслуги = Истина ИЛИ Не ЗначениеЗаполнено(идОбновления) Тогда
			Для Каждого СтрокаРабот Из ЗН.Работы Цикл
				НоваяЗапись = ПолучитьРегистрСопоставления().СоздатьМенеджерЗаписи();
				НоваяЗапись.Период = ТекущаяДатаСеанса();
				НоваяЗапись.Тип = "ЗаказНарядРаботы";
				НоваяЗапись.Данные1С = ЗН;
				НоваяЗапись.ДопДанные1С = СтрокаРАбот.Работа;
				НоваяЗапись.Записать();  
				
				Ошибка = "";
				ИдРаботЗН = ВыгрузитьРаботыЗН(ЗН, идЗН, СтрокаРабот.Работа, Ошибка);
				
				ДобавитьВыгруженныеДанные(ЗН, "ЗаказНарядРаботы", идРаботЗН, Ошибка, СтрокаРабот.Работа);
				ОшибкаПоРаботам = Ошибка;
			КонецЦикла;		
			
			
			Для Каждого СтрокаТоваров Из ЗН.Товары Цикл
				НоваяЗапись = ПолучитьРегистрСопоставления().СоздатьМенеджерЗаписи();
				НоваяЗапись.Период = ТекущаяДатаСеанса();
				НоваяЗапись.Тип = "ЗаказНарядТовары";
				НоваяЗапись.Данные1С = ЗН;
				НоваяЗапись.ДопДанные1С = СтрокаТоваров.Номенклатура;
				НоваяЗапись.Записать();
					
				Ошибка = "";
				ИдТоваровЗН = ВыгрузитьТоварыЗН(ЗН, идЗН, СтрокаТоваров.Номенклатура, Ошибка);
					
				ДобавитьВыгруженныеДанные(ЗН, "ЗаказНарядТовары", идТоваровЗН, Ошибка, СтрокаТоваров.Номенклатура);
				ОшибкаПоТоварам = Ошибка;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	
	// Если нет ошибок то выгрузим ЗН как verified
	Если ВыгрузитьКакОткрытый <> Истина И ЗначениеЗаполнено(идЗН) И Не ЗначениеЗаполнено(ОшибкаПоРаботам) И Не ЗначениеЗаполнено(ОшибкаПоТоварам) Тогда
		Структура = Новый Структура; 
		Структура.Вставить("verify", "approved");
		ОтправитьЗапрос("api/purchase/"+СтрЗаменить(идЗН, Символы.НПП, ""), Структура, Ошибка, "PUT");
	КонецЕсли;
	
	// Обработка согласий
	Если ПолучитьЕстьЛиОбрабатыватьСогласияПриВыгрузкеЗН() И ВыгрузитьКакОткрытый <> Истина И Не ЗначениеЗаполнено(ОшибкаПоРаботам) И Не ЗначениеЗаполнено(ОшибкаПоТоварам)  Тогда
		ЗНОшибка = Ошибка;
		Ошибка = "";
		ОбработатьСогласия(_Контрагент,ЗН,Ошибка);
		Ошибка = ЗНОшибка;	
	КонецЕсли;
			
	Возврат идЗН;
КонецФункции
Функция ВыгрузитьРаботыЗН(ЗН, идЗН, Знач Работа, Ошибка) 

	Если 1 = 2 Тогда
		ЗН = Документы.ЗаказНаряд.ПустаяСсылка();
	КонецЕсли;	
	
	// Поиск работы в заказ-наряде
	СтрокаРабот = Зн.Работы.Найти(Работа);
	Если СтрокаРабот = Неопределено Тогда
		Ошибка = "Работа не найдена в ЗН";
		Возврат Неопределено;
	КонецЕсли;
	
	// Формирование данных для выгрузки
	Структура = Новый Структура;
	Структура.Вставить("purchase_id", идЗН);
	
	// Определение кода работы
	Если ЗначениеЗаполнено(СтрокаРабот.Работа.Артикул) Тогда
		Структура.Вставить("code", СтрокаРабот.Работа.Артикул);
	Иначе
		Структура.Вставить("code", СтрокаРабот.Работа.Код);
	КонецЕсли;
	
	// Заполнение основных данных
	Структура.Вставить("name", СтрокаРабот.Работа.Наименование);
	Структура.Вставить("price", КорректноеЧисло(СтрокаРабот.Цена));
	Структура.Вставить("quantity", КорректноеЧисло(СтрокаРабот.Количество * СтрокаРабот.Коэффициент));
	Структура.Вставить("discount", КорректноеЧисло(СтрокаРабот.СуммаСкидки + СтрокаРабот.СуммаСкидкиСтроки));
	Структура.Вставить("sum", КорректноеЧисло(СтрокаРабот.СуммаВсего));
	Структура.Вставить("status", 0);
	
	// Отправка данных
	Ошибка = "";
	идРабот = ОтправитьЗапрос("/api/purchase-work", Структура, Ошибка);
	
	Возврат идРабот;
КонецФункции
Функция ВыгрузитьТоварыЗН(ЗН, идЗН, Товар, Ошибка) 
	// Проверка параметров
	Если 1 = 2 Тогда
		ЗН = Документы.ЗаказНаряд.ПустаяСсылка();
	КонецЕсли;	
	
	// Поиск товара в заказ-наряде
	СтрокаТоваров = Зн.Товары.Найти(Товар);
	Если СтрокаТоваров = Неопределено Тогда
		Ошибка = "Товар не найден в ЗН";
		Возврат Неопределено;
	КонецЕсли;
	
	// Формирование данных для выгрузки
	Структура = Новый Структура;
	
	// Заполнение основных данных
	Структура.Вставить("purchase_id", идЗН);
	Структура.Вставить("code", СтрокаТоваров.Номенклатура.Артикул);
	Структура.Вставить("name", СтрокаТоваров.Номенклатура.Наименование);
	
	// Определение типа товара
	ТипТовара = ?(Найти(нрег(ЗН.ВидРемонта), "гарант") > 0, 0, 1);
	Структура.Вставить("type", ТипТовара);
	
	// Заполнение ценовых показателей
	Структура.Вставить("price", КорректноеЧисло(СтрокаТоваров.Цена));
	Структура.Вставить("quantity", КорректноеЧисло(СтрокаТоваров.Количество));
	Структура.Вставить("discount", КорректноеЧисло(СтрокаТоваров.СуммаСкидки +  + СтрокаТоваров.СуммаСкидкиСтроки));
	Структура.Вставить("sum", КорректноеЧисло(СтрокаТоваров.СуммаВсего));
	Структура.Вставить("status", 0);
	
	// Отправка данных
	Ошибка = "";
	идТовара = ОтправитьЗапрос("/api/purchase-spare", Структура, Ошибка);
	
	Возврат идТовара;
КонецФункции
Процедура УдалитьРаботу(ид)
	Ошибка = "";
	Структура = Новый Структура; 
	Структура.Вставить("status", 1);
	
	ОтправитьЗапрос("/api/purchase-work/"+СтрЗаменить(ид, Символы.НПП, ""), Структура, Ошибка, "PUT");
КонецПроцедуры	
Процедура УдалитьЗапчасть(ид)
	Ошибка = "";
	Структура = Новый Структура; 
	Структура.Вставить("status", 1);
	
	ОтправитьЗапрос("/api/purchase-spare/"+СтрЗаменить(ид, Символы.НПП, ""), Структура, Ошибка, "PUT");
КонецПроцедуры
//

// Выгрузка заявки на ремонт
Процедура ВыгрузитьВсеЗаявкиНаРемонт() Экспорт
	// Основная процедура выгрузки всех заявок на ремонт
	
	// Проверяем, что выгрузка заявок на ремонт включена
	Если НЕ ПолучитьВыгружатьЛиЗаписиНаРемонт() Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем все невыгруженные заявки на ремонт
	Выборка = ПолучитьВыборкуВсехНеВыгрЗаявокНаРемонт();
	
	Пока Выборка.Следующий() Цикл
		Ошибка = "";
		
		// Устанавливаем соединение по профилю заявки на ремонт
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			// Выгружаем заявку на ремонт
			ИдЗаявки = ВыгрузитьЗаявкуНаРемонт(Выборка.Данные1С, Ошибка, Выборка.ID);
		КонецЕсли;
		
		// Записываем результат выгрузки в регистр сопоставления
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ПланРемонта", ИдЗаявки, Ошибка);
	КонецЦикла;
КонецПроцедуры
Процедура ВыгрузитьОднуЗаявкуНаРемонт(ОднаЗаявка) Экспорт
	// Процедура выгрузки одной конкретной заявки на ремонт
	
	// Проверяем, что выгрузка заявок на ремонт включена
	Если НЕ ПолучитьВыгружатьЛиЗаписиНаРемонт() Тогда
		Сообщить("Выгрузка заявок на ремонт отключена");
		Возврат;
	КонецЕсли;
		
	// Получаем заявку для выгрузки
	Выборка = ПолучитьВыборкуНеВыгрЗаявокНаРемонтПоОдной(ОднаЗаявка);
	
	Если Выборка.Количество() = 0 Тогда
		Сообщить("Заявка на ремонт " + ОднаЗаявка + " не найдена в регистре сопоставления, или не требует обновления");
		Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		Ошибка = "";
		
		// Устанавливаем соединение по профилю заявки на ремонт
		Если УстановитьСоединениеПоПрофилюЗаказНаряда(Выборка.Данные1С.ПодразделениеКомпании, Ошибка) Тогда
			// Выгружаем заявку на ремонт
			ИдЗаявки = ВыгрузитьЗаявкуНаРемонт(Выборка.Данные1С, Ошибка, Выборка.ID);
		КонецЕсли;
		
		// Записываем результат выгрузки в регистр сопоставления
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ПланРемонта", ИдЗаявки, Ошибка);
		Прервать; // Выгружаем только одну заявку
	КонецЦикла;
КонецПроцедуры
Функция ВыгрузитьЗаявкуНаРемонт(Заявка, Ошибка, ИдОбновления = 0) Экспорт
	// Основная функция выгрузки документа "Заявка на ремонт" и ее графика на портал
	Если 1 = 2 Тогда
		Заявка = Документы.ЗаявкаНаРемонт.ПустаяСсылка();
	КонецЕсли;

	ФорматДатаВремя = "ДФ='dd.MM.yyyy HH:mm'";
	
	// Преобразуем ИД обновления к строке для использования в URL
	Если ЗначениеЗаполнено(ИдОбновления) Тогда
		ИдОбновленияСтрока = СтрЗаменить(ИдОбновления, Символы.НПП, "");
	КонецЕсли;

	// Определяем клиента (контрагента) и при необходимости выгружаем его
	ИдКлиента = Неопределено;
	Если ЗначениеЗаполнено(Заявка.Заказчик) Тогда
		ИдКлиента = НайтиВыгруженныйОбъект(Заявка.Заказчик, ПрофильПодключения);
		Если Не ЗначениеЗаполнено(ИдКлиента) Тогда
			Ошибка = "";
			ИдКлиента = ВыгрузитьКонтрагента(Заявка.Заказчик, Неопределено, Ошибка);
			ДобавитьВыгруженныеДанные(Заявка.Заказчик, "Контрагент", ИдКлиента, Ошибка, ПрофильПодключения);
			Если Не ЗначениеЗаполнено(ИдКлиента) Тогда
				Ошибка = "Не сопоставлен контрагент";
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	// Определяем автомобиль и при необходимости выгружаем его
	ИдАвто = НайтиВыгруженныйОбъект(Заявка.Автомобиль);
	Если Не ЗначениеЗаполнено(ИдАвто) Тогда
		Ошибка = "";
		ИдАвто = ВыгрузитьАвтомобиль(Заявка.Автомобиль, Ошибка);
		Если ЗначениеЗаполнено(ИдАвто) ИЛИ НЕ ПустаяСтрока(Ошибка) Тогда
			ДобавитьВыгруженныеДанные(Заявка.Автомобиль, "Автомобиль", ИдАвто, Ошибка);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ИдАвто) Тогда
			Ошибка = "Не сопоставлен автомобиль";
			Возврат 0;
		КонецЕсли;
	Иначе	
		// При необходимости обновим пробег автомобиля
		Если ОбновитьАвтомобиль = Истина Тогда
			ОбновитьАвтомобиль(Заявка.Автомобиль, Ошибка, ИдАвто);
		КонецЕсли;
	КонецЕсли;

	// Контактное лицо
	КонтактноеЛицо = ПолучитьКонтактноеЛицо(Заявка.Заказчик);
	ИдКонтакта = "";
	Если ЗначениеЗаполнено(КонтактноеЛицо) И КонтактноеЛицо.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		ИдКонтакта = НайтиВыгруженныйОбъект(КонтактноеЛицо, ПрофильПодключения);
		Если Не ЗначениеЗаполнено(ИдКонтакта) Тогда
			ИдКонтакта = ВыгрузитьКонтрагента(КонтактноеЛицо, Неопределено, Ошибка);
			Если ЗначениеЗаполнено(ИдКонтакта) Тогда
				ДобавитьВыгруженныеДанные(КонтактноеЛицо, "Контрагент", ИдКонтакта, Ошибка, ПрофильПодключения);
			Иначе
				ИдКонтакта = "";
			КонецЕсли;
		КонецЕсли;
	Иначе
		ИдКонтакта = "";
	КонецЕсли;

	// Формируем базовую структуру заявки на ремонт
	Структура = Новый Структура;
	Структура.Вставить("code", СокрЛП(Заявка.Номер));
	Структура.Вставить("status", ?(Заявка.Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено ИЛИ Заявка.ПометкаУдаления, "2", "1"));
	Структура.Вставить("occupying_type", "1"); // Всегда запись
	Если ЗначениеЗаполнено(ИдКлиента) Тогда
		Структура.Вставить("client_id", ИдКлиента);
	КонецЕсли;
	Структура.Вставить("car_id", ИдАвто);
	Структура.Вставить("contact_person_id", ИдКонтакта);

	// Разберем планирование, чтобы определить приемку/работы/выдачу и собрать станции
	МассивСтанций = Новый Массив;
	
	// Данные приемки
	МастерДляПриемки = Неопределено;
	ИДМастераПриемки = Неопределено;
	НачалоПриемки = Неопределено;
	КонецПриемки = Неопределено;
	
	// Данные Выдачи
	МастерДляВыдачи = Неопределено;
	ИДМастераВыдачи = Неопределено;
	НачалоВыдачи = Неопределено;
	КонецВыдачи = Неопределено;
	
	// Определим автоработы приемки и выдачи
	АвтоработаПриемки = ПолучитьКонстантноеЗначение("АвтоработаПриемаАвтомобиля");
	АвтоработаВыдачи = ПолучитьКонстантноеЗначение("АвтоработаВыдачиАвтомобиля"); 
	
	Если АвтоработаПриемки = Справочники.Автоработы.ПустаяСсылка() ИЛИ АвтоработаВыдачи = Справочники.Автоработы.ПустаяСсылка() Тогда
		Ошибка = "Не указана авторабота приемки или выдачи. Проверьте константы инфообмена.";
		Возврат 0;
	КонецЕсли;

	// Перед тем как выгружать проверим что есть приемка и есть выдача
	МассивСтрокПриемки = Заявка.Планирование.НайтиСтроки(Новый Структура("Авторабота", АвтоработаПриемки));
	МассивСтрокВыдачи = Заявка.Планирование.НайтиСтроки(Новый Структура("Авторабота", АвтоработаВыдачи));
	Если МассивСтрокПриемки.Количество() = 0 ИЛИ МассивСтрокВыдачи.Количество() = 0 Тогда
		Ошибка = "Не указана приемка или выдача. Проверьте планирование заявки.";
		Возврат 0;
	КонецЕсли;
	
	// Получим начальное время и конечное время приемки и выдачи
	ТаблицаВремениПриемки = Заявка.Планирование.Выгрузить(МассивСтрокПриемки, "НачалоВыполнения,ОкончаниеВыполнения");
	ТаблицаВремениВыдачи = Заявка.Планирование.Выгрузить(МассивСтрокВыдачи, "НачалоВыполнения,ОкончаниеВыполнения");
	// Сортируем чтобы минимальное начальное время было первой строкой
	ТаблицаВремениПриемки.Сортировать("НачалоВыполнения Возр");
	НачалоПриемки = ТаблицаВремениПриемки.Получить(0).НачалоВыполнения;
	// Сортируем чтобы максимальное конечное время было первой строкой
	ТаблицаВремениПриемки.Сортировать("ОкончаниеВыполнения Убыв");
	КонецПриемки = ТаблицаВремениПриемки.Получить(0).ОкончаниеВыполнения;

	// Теперь тоже самое для выдачи
	ТаблицаВремениВыдачи.Сортировать("НачалоВыполнения Возр");
	НачалоВыдачи = ТаблицаВремениВыдачи.Получить(0).НачалоВыполнения;
	ТаблицаВремениВыдачи.Сортировать("ОкончаниеВыполнения Убыв");
	КонецВыдачи = ТаблицаВремениВыдачи.Получить(0).ОкончаниеВыполнения;

	// Определим статус заявки
	ЗаявкаОтменена = (Заявка.Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено ИЛИ Заявка.ПометкаУдаления);
	
	Для Каждого СтрокаПлан Из Заявка.Планирование Цикл
		// Пока не вижу смысла по виду использования цеха определять приемка ли это или просто запланированная работа
		// Поэтому оставлю данную строку для будущих улучшений механизма
		ВидИспользования = обПолучитьЗначениеСвойства(СтрокаПлан.РабочееМесто, ПланыВидовХарактеристик.СвойстваОбъектов.ВидИспользованияРабочегоМеста, Перечисления.ВидыИспользованияРабочихМест.НеУчаствуетВПланировании);

		// Цех нужен только для работ. Для приемки и выдачи он не нужен.
		Если НЕ СтрокаПлан.Авторабота = АвтоработаПриемки И НЕ СтрокаПлан.Авторабота = АвтоработаВыдачи Тогда
			// Определим цех по цеху
			ИДЦех = НайтиВыгруженныйОбъект(СтрокаПлан.РабочееМесто, ПрофильПодключения, "Пост");
			Если Не ЗначениеЗаполнено(ИДЦех) Тогда
				Ошибка = "Не сопоставлен цех";
				Возврат 0;
			КонецЕсли;
		КонецЕсли;

		// Обработка по условиям нужна для гибкости механизма выгрузки
		Если СтрокаПлан.Авторабота = АвтоработаПриемки Тогда
			// Обрабатываем приемку:

			// Определим мастера-приемщика по следующим правилам:
			// 1. Мастер заполнен как исполнитель работы ИНАЧЕ
			// 2. Мастер заполнен как мастер цеха ИНАЧЕ
			// 3. Мастер заполнен как диспетчер
			Если ЗначениеЗаполнено(СтрокаПлан.Исполнитель) Тогда
				МастерДляПриемки = СтрокаПлан.Исполнитель;
			ИначеЕсли ЗначениеЗаполнено(СтрокаПлан.РабочееМесто.Мастер) Тогда
				МастерДляПриемки = СтрокаПлан.РабочееМесто.Мастер;
			ИначеЕсли ЗначениеЗаполнено(Заявка.Диспетчер) Тогда
				МастерДляПриемки = Заявка.Диспетчер;
			Иначе
				Ошибка = "Не определен мастер-приемщик";
				Возврат 0;
			КонецЕсли;

			// Выполним поиск мастера в регистре выгруженных данных
			ИДМастераПриемки = НайтиВыгруженныйОбъект(МастерДляПриемки);
			Если Не ЗначениеЗаполнено(ИДМастераПриемки) Тогда
				Ошибка = "";
				ИДМастераПриемки = ВыгрузитьСотрудника(МастерДляПриемки, Ошибка, Истина);
				ДобавитьВыгруженныеДанные(МастерДляПриемки, "Сотрудник", ИДМастераПриемки, Ошибка);
				Если Не ЗначениеЗаполнено(ИДМастераПриемки) Тогда
					Ошибка = "Не сопоставлен мастер-приемщик. Ошибка выгрузки сотрудника.";
					Возврат 0;
				КонецЕсли;
			КонецЕсли;

			// Проверим график мастера на день приемки
			ИДГрафикаПриемки = НайтиВыгруженныйОбъект(МастерДляПриемки,, "ГрафикРаботы", Формат(СтрокаПлан.НачалоВыполнения, "ДФ=dd.MM.yyyy"));
			Если Не ЗначениеЗаполнено(ИДГрафикаПриемки) Тогда
				ОшибкаГрафика = "";
				ИДГрафикаПриемки = ВыгрузитьГрафикРаботы(ИДМастераПриемки, СтрокаПлан.НачалоВыполнения, ОшибкаГрафика);
				Если НЕ ПустаяСтрока(ОшибкаГрафика) Тогда 
					Ошибка = "Не выгружен график работы " + МастерДляПриемки + " за день " + Формат(СтрокаПлан.НачалоВыполнения, "ДФ=dd.MM.yyyy") + ": " + ОшибкаГрафика;
					Возврат 0;
				КонецЕсли;
				// Записываем сопоставление графика в регистр
				ДобавитьВыгруженныеДанные(МастерДляПриемки, "ГрафикРаботы", ИДГрафикаПриемки, ОшибкаГрафика,, Формат(СтрокаПлан.НачалоВыполнения, "ДФ=dd.MM.yyyy"));
			КонецЕсли;

		ИначеЕсли СтрокаПлан.Авторабота = АвтоработаВыдачи Тогда
			// Обрабатываем выдачу:

			// Определим мастера-выдачи по следующим правилам:
			// 1. Мастер заполнен как исполнитель работы ИНАЧЕ
			// 2. Мастер заполнен как мастер цеха ИНАЧЕ
			// 3. Мастер заполнен как диспетчер
			Если ЗначениеЗаполнено(СтрокаПлан.Исполнитель) Тогда
				МастерДляВыдачи = СтрокаПлан.Исполнитель;
			ИначеЕсли ЗначениеЗаполнено(СтрокаПлан.РабочееМесто.Мастер) Тогда
				МастерДляВыдачи = СтрокаПлан.РабочееМесто.Мастер;
			ИначеЕсли ЗначениеЗаполнено(Заявка.Диспетчер) Тогда
				МастерДляВыдачи = Заявка.Диспетчер;
			Иначе
				Ошибка = "Не определен мастер для выдачи автомобиля";
				Возврат 0;
			КонецЕсли;

			// Выполним поиск мастера в регистре выгруженных данных
			ИДМастераВыдачи = НайтиВыгруженныйОбъект(МастерДляВыдачи);
			Если Не ЗначениеЗаполнено(ИДМастераПриемки) Тогда
				Ошибка = "";
				ИДМастераВыдачи = ВыгрузитьСотрудника(МастерДляВыдачи, Ошибка, Истина);
				ДобавитьВыгруженныеДанные(МастерДляВыдачи, "Сотрудник", ИДМастераВыдачи, Ошибка);
				Если Не ЗначениеЗаполнено(ИДМастераВыдачи) Тогда
					Ошибка = "Не сопоставлен мастер для выдачи автомобиля. Ошибка выгрузки сотрудника.";
					Возврат 0;
				КонецЕсли;
			КонецЕсли;

			// Проверим график мастера на день выдачи
			ИДГрафикаВыдачи = НайтиВыгруженныйОбъект(МастерДляВыдачи,, "ГрафикРаботы", Формат(СтрокаПлан.НачалоВыполнения, "ДФ=dd.MM.yyyy"));
			Если Не ЗначениеЗаполнено(ИДГрафикаВыдачи) Тогда
				ОшибкаГрафика = "";
				ИДГрафикаВыдачи = ВыгрузитьГрафикРаботы(ИДМастераВыдачи, СтрокаПлан.НачалоВыполнения, ОшибкаГрафика);
				Если НЕ ПустаяСтрока(ОшибкаГрафика) Тогда
					Ошибка = "Не выгружен график работы " + МастерДляВыдачи + " за день " + Формат(СтрокаПлан.НачалоВыполнения, "ДФ=dd.MM.yyyy") + ": " + ОшибкаГрафика;
					Возврат 0;
				КонецЕсли;
				// Записываем сопоставление графика в регистр
				ДобавитьВыгруженныеДанные(МастерДляВыдачи, "ГрафикРаботы", ИДГрафикаВыдачи, ОшибкаГрафика,, Формат(СтрокаПлан.НачалоВыполнения, "ДФ=dd.MM.yyyy"));
			КонецЕсли;

		Иначе
			// Обрабатываем производственные работы:

			// Мастер и его график не требуется для производственных работ


			СтруктураСтанции = Новый Структура;
			СтруктураСтанции.Вставить("station_id", ИДЦех);
			СтруктураСтанции.Вставить("service_from", Формат(СтрокаПлан.НачалоВыполнения, ФорматДатаВремя));
			СтруктураСтанции.Вставить("service_to", Формат(СтрокаПлан.ОкончаниеВыполнения, ФорматДатаВремя));
			СтруктураСтанции.Вставить("request_reason", Строка(СтрокаПлан.Авторабота));
			СтруктураСтанции.Вставить("status", ?(ЗаявкаОтменена, "2", "1"));
			МассивСтанций.Добавить(СтруктураСтанции);
			
		КонецЕсли;
	КонецЦикла;

	// Укажем даты приема и выдачи автомобиля и их мастеров
	Структура.Вставить("acceptor_id", ИДМастераПриемки);
	Структура.Вставить("accept_from", Формат(НачалоПриемки, ФорматДатаВремя));
	Структура.Вставить("accept_to", Формат(КонецПриемки, ФорматДатаВремя));

	Структура.Вставить("issued_by_id", ИДМастераВыдачи);
	Структура.Вставить("issue_from", Формат(НачалоВыдачи, ФорматДатаВремя));
	Структура.Вставить("issue_to", Формат(КонецВыдачи, ФорматДатаВремя));

	// Добавим собранные станции в структуру
	Структура.Вставить("stations", МассивСтанций);

	// Отправим заявку: POST для новых, PUT для обновления
	Ошибка = "";
	Если ЗначениеЗаполнено(ИдОбновленияСтрока) Тогда
		ИД = ОтправитьЗапрос("api/service/maintenance-schedule/station-reschedule/" + ИдОбновленияСтрока, Структура, Ошибка, "PUT");
	Иначе
		ИД = ОтправитьЗапрос("api/service/maintenance-schedule", Структура, Ошибка);
	КонецЕсли;

	Возврат ИД;
КонецФункции
//

// Выгрузка составляющих
Функция ВыгрузитьСотрудника(Сотрудник, Ошибка, ДляЖурналаЗаписи = Ложь)
	// Проверка параметров
	Если 1 = 2 Тогда
		Сотрудник = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;	
	
	// Формирование данных для выгрузки
	Структура = Новый Структура;
	
	// Заполнение основных данных
	Структура.Вставить("code", Сотрудник.Код);
	
	// Обработка ФИО
	ФИО = РазобратьФИО(Сотрудник);
	Если ЗначениеЗаполнено(ФИО.Имя) Тогда
		Структура.Вставить("first_name", ФИО.Имя);
	КонецЕсли;	
	Если ЗначениеЗаполнено(ФИО.Фамилия) Тогда
		Структура.Вставить("last_name", ФИО.Фамилия);
	КонецЕсли;		
	Если ЗначениеЗаполнено(ФИО.Отчество) Тогда
		Структура.Вставить("middle_name", ФИО.Отчество);
	КонецЕсли;	
	
	// Обработка даты рождения

	ДеньРождения = Сотрудник.ДатаРождения;
	Если ЗначениеЗаполнено(ДеньРождения) Тогда
		Структура.Вставить("birthday", Формат(ДеньРождения, "ДФ=dd.MM.yyyy"));
	Иначе
		Структура.Вставить("birthday", "");
	КонецЕсли;	
	
	// Обработка email
	ЕмейлСотрудника = Емейл(Сотрудник);
	Если ЗначениеЗаполнено(ЕмейлСотрудника) Тогда
		Структура.Вставить("email", ЕмейлСотрудника);
	Иначе
		Структура.Вставить("email", "no" + СокрЛП(Сред(Сотрудник.Код, 3)) + "@mail.ru");
	КонецЕсли;	
	
	// Обработка даты приема
	Если ЗначениеЗаполнено(Сотрудник.ДатаПриема) Тогда
		Структура.Вставить("employment", Формат(Сотрудник.ДатаПриема, "ДФ=dd.MM.yyyy"));
	КонецЕсли;	
	
	// Обработка должности
	ИдДолжности = НайтиВыгруженныйОбъект(Сотрудник.Должность);
	Если ЗначениеЗаполнено(ИдДолжности) Тогда
		МассивДолжностей = Новый Массив;
		МассивДолжностей.Вставить(ИдДолжности);
		Структура.Вставить("position_list", МассивДолжностей);
	КонецЕсли;	
	
	// Установка статуса
	Структура.Вставить("status", "active");

	// Если сотрудник выгружается для использования в журнале записи на ремонт то установим требуемый параметр
	Если ДляЖурналаЗаписи = Истина Тогда
		Структура.Вставить("show_in_maintenance_schedule", 1); 
		//Структура.Вставить("role", "dealer"); // не уверен что обязательно требуется устанавливать роль
	КонецЕсли;
	
	// Отправка данных
	Ошибка = "";
	ИдСотрудника = ОтправитьЗапрос("/api/manager", Структура, Ошибка);
	
	// Сохранение результатов выгрузки
	ДобавитьВыгруженныеДанные(Сотрудник, "Сотрудник", ИдСотрудника, Ошибка);
	
	Возврат ИдСотрудника;
КонецФункции
Функция ВыгрузитьАвтомобиль(Автомобиль, Ошибка, ИдОбновления = 0)
	
	Если 1 = 2 Тогда
		Автомобиль = Справочники.Автомобили.ПустаяСсылка();
	КонецЕсли;
	
	ИдБренда = НайтиВыгруженныйОбъект(Автомобиль.Модель.Родитель);
	
	Если Не ЗначениеЗаполнено(ИдБренда) Тогда
		ДобавитьВыгруженныеДанные(Автомобиль, "Автомобиль", 0, "Не найден ID бренда " + Автомобиль.Модель.Родитель);
		Возврат 0;
	КонецЕсли;	
	
	ИдМодели = НайтиВыгруженныйОбъект(Автомобиль.Модель);
	
	Если Не ЗначениеЗаполнено(ИдМодели) Тогда
		ДобавитьВыгруженныеДанные(Автомобиль, "Автомобиль", 0, "Не найден ID модели " + Автомобиль.Модель);
		Возврат 0;
	КонецЕсли;	
	
	пробег = ПолучитьПробегАвтомобиля(автомобиль);
	
	Структура = Новый Структура; 	
	Структура.Вставить("brand_id", ИдБренда);
	Структура.Вставить("model_id", ИдМодели);
	Структура.Вставить("code", Автомобиль.Код);
	Структура.Вставить("version", СокрЛП(Автомобиль.ВариантКомплектации));
	Структура.Вставить("color", СокрЛП(Автомобиль.Цвет));
	Структура.Вставить("vin", Автомобиль.VIN); 
	Структура.Вставить("mileage",  пробег);
	Структура.Вставить("is_test_drive", 0);  
	
	ГосНомер = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер));
	Если Не ПустаяСтрока(ГосНомер) Тогда
		Структура.Вставить("reg_number", ГосНомер);  
	КонецЕсли;	

	
	идАвто = ОтправитьЗапрос("/api/vehicle", Структура, Ошибка);
		
	Возврат идАвто;
	
КонецФункции
Функция ОбновитьАвтомобиль(Автомобиль, Ошибка, идАвто = 0)
	Если 1 = 2 Тогда
		Автомобиль = Справочники.Автомобили.ПустаяСсылка();
	КонецЕсли;
	
	//ИдБренда = НайтиВыгруженныйОбъект(Автомобиль.Модель.Родитель);
	//
	//Если Не ЗначениеЗаполнено(ИдБренда) Тогда
	//	Возврат 0;
	//КонецЕсли;	
	//
	//ИдМодели = НайтиВыгруженныйОбъект(Автомобиль.Модель);
	//
	//Если Не ЗначениеЗаполнено(ИдМодели) Тогда
	//	Возврат 0;
	//КонецЕсли;	
	//
	//пробег = Пробег(автомобиль);
	////ЗаписьЖурналаРегистрации("ИнфообменChangan_пробег", УровеньЖурналаРегистрации.Информация,,Автомобиль, пробег);
	
	пробег = ПолучитьПробегАвтомобиля(автомобиль);
	
	Если Не ЗначениеЗаполнено(пробег) Тогда
		Возврат Ложь;
	КонецЕсли;	
		
	Структура = Новый Структура; 
	//Если ЗначениеЗаполнено(ИдОбновления) Тогда
	//	Структура.Вставить("ID", ИдОбновления);
	//КонецЕсли;	
	Структура.Вставить("mileage",  пробег);
	//Структура.Вставить("brand_id", ИдБренда);
	//Структура.Вставить("model_id", ИдМодели);
	//Структура.Вставить("code", Автомобиль.Код);
	//
	ОтправитьЗапрос("api/vehicle/"+СтрЗаменить(идавто, Символы.НПП, ""), Структура, Ошибка, "PUT");
	
	//Если ЗначениеЗаполнено(идАвто) Тогда
	//	Наб = РегистрыСведений.БТ_СопоставлениеДанныхChangan.СоздатьНаборЗаписей();
	//	Наб.Отбор.Данные1С.Установить(Автомобиль);
	//	Наб.Записать();

	//КонецЕсли;	
	
	Возврат идАвто;
	
КонецФункции
Функция ВыгрузитьКонтрагента(Контрагент, ЗН = Неопределено, Ошибка, ИдОбновления = 0)
	// Для удобства
	Если 1 = 2 Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	//ИдОбновления в строку
	Если ЗначениеЗаполнено(ИдОбновления) Тогда
		ИдОбновленияСтрока = СтрЗаменить(идОбновления, Символы.НПП, "");
	КонецЕсли;

	// Формирование структуры для выгрузки
	Структура = Новый Структура;
	Структура.Вставить("code", Контрагент.Код);
	Структура.Вставить("type", ?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо, 1, 0));

	// Переменные для ФИО, дня рождения, согласия на обработку ПД и прочего
	ФИО = РазобратьФИО(Контрагент);
	ДеньРождения = Формат(Контрагент.ДатаРождения, "ДФ=dd.MM.yyyy");
	Имя = ?(ЗначениеЗаполнено(Контрагент.Имя), Контрагент.Имя, ФИО.Имя);
	ДаноСОПД = (Контрагент.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да);
	ЛюбойТелефон = "";
	Телефоны = Телефоны(Контрагент, ЛюбойТелефон);
	Емейл = Емейл(Контрагент);
	Адрес = Адрес(Контрагент);

	// Поиск контактного лица
	КонтактноеЛицо = Неопределено;
	// Если есть реквизит ДовЛицо, то берем его, иначе берем контактное лицо из контрагента
	Если ЗН <> Неопределено И ЗН.Метаданные().Реквизиты.Найти("ДовЛицо") <> Неопределено И ЗначениеЗаполнено(ЗН.ДовЛицо) Тогда
		КонтактноеЛицо = ЗН.ДовЛицо;
	Иначе
		КонтактноеЛицо = ПолучитьКонтактноеЛицо(Контрагент);
	КонецЕсли;
	
	//Данные контактного лица
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		ФИОКонтактногоЛица = РазобратьФИО(КонтактноеЛицо);
		ДеньРожденияКонтактногоЛица = Формат(КонтактноеЛицо.ДатаРождения, "ДФ=dd.MM.yyyy");
		ИмяКонтактногоЛица = ?(ЗначениеЗаполнено(КонтактноеЛицо.Имя), КонтактноеЛицо.Имя, ФИОКонтактногоЛица.Имя);
		ДаноСОПДКонтактногоЛица = (КонтактноеЛицо.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да);
		ДаноСогласиеНаРекламнуюКоммуникацию = (КонтактноеЛицо.СогласиеНаПолучениеSMS = Истина);
		ЛюбойТелефонКонтактногоЛица = "";
		ТелефоныКонтактногоЛица = Телефоны(КонтактноеЛицо, ЛюбойТелефонКонтактногоЛица);
		ЕмейлКонтактногоЛица = Емейл(КонтактноеЛицо);
		АдресКонтактногоЛица = Адрес(КонтактноеЛицо);
	КонецЕсли;
	

	// Если форма собственности юридическое лицо, то выгружаем юридическое лицо, иначе выгружаем частное лицо
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			Структура.Вставить("name", ИмяКонтактногоЛица);
			Если ДаноСОПДКонтактногоЛица Тогда
				Структура.Вставить("last_name", ФИОКонтактногоЛица.Фамилия);
				Структура.Вставить("middle_name", ФИОКонтактногоЛица.Отчество);
				Структура.Вставить("client_confirm_communication", ?(КонтактноеЛицо.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да, 1, 0));
				Структура.Вставить("may_process_personal_data", Структура.client_confirm_communication);
			Иначе
				Структура.Вставить("last_name", "");
				Структура.Вставить("middle_name", "");
			КонецЕсли;
			//Заполнение контактных данных, если они есть у контактного лица
			Если ЗначениеЗаполнено(ТелефоныКонтактногоЛица) Тогда
				Структура.Вставить("phones", ТелефоныКонтактногоЛица);
			КонецЕсли;
			Если ЗначениеЗаполнено(ЕмейлКонтактногоЛица) Тогда
				Структура.Вставить("email", ЕмейлКонтактногоЛица);
			Иначе
				Структура.Вставить("email", "");
			КонецЕсли;
			Если ЗначениеЗаполнено(АдресКонтактногоЛица) Тогда
				Структура.Вставить("address", АдресКонтактногоЛица);
			Иначе
				Структура.Вставить("address", "");
			КонецЕсли;
			Если ЗначениеЗаполнено(ДеньРожденияКонтактногоЛица) Тогда
				Структура.Вставить("birthday", Формат(ДеньРожденияКонтактногоЛица, "ДФ=dd.MM.yyyy"));
			Иначе
				Структура.Вставить("birthday", "");
			КонецЕсли;
		Иначе
			//Если нет контактного лица
			Структура.Вставить("name", Неопределено);
			//Заполнение контактных данных, если они есть у контрагента
			Если ЗначениеЗаполнено(Телефоны) Тогда
				Структура.Вставить("phones", Телефоны);
			КонецЕсли;
			Если ЗначениеЗаполнено(Емейл) Тогда
				Структура.Вставить("email", Емейл);
			Иначе
				Структура.Вставить("email", "");
			КонецЕсли;
			Если ЗначениеЗаполнено(Адрес) Тогда
				Структура.Вставить("address", Адрес);
			Иначе
				Структура.Вставить("address", "");
			КонецЕсли;
			Если ЗначениеЗаполнено(ДеньРождения) Тогда
				Структура.Вставить("birthday", Формат(ДеньРождения, "ДФ=dd.MM.yyyy"));
			Иначе
				Структура.Вставить("birthday", "");
			КонецЕсли;
		КонецЕсли;
		Структура.Вставить("company_legal_form", ФормаСобственности(Контрагент.Наименование));
		Структура.Вставить("company_name", СокрЛП(Контрагент.НаименованиеПолное));
		Структура.Вставить("company_address", Адрес(Контрагент, Справочники.ВидыКонтактнойИнформации.АдресЮридический));
		Структура.Вставить("company_inn", Контрагент.ИНН);
		Структура.Вставить("company_kpp", Контрагент.КПП);
		Структура.Вставить("company_okpo", Контрагент.КодПоОКПО);
		Структура.Вставить("company_email", Емейл(Контрагент));

	Иначе
		//Если лицо частное
		Структура.Вставить("name", Имя);
		Если ДаноСОПД Тогда
			Структура.Вставить("last_name", ФИО.Фамилия);
			Структура.Вставить("middle_name", ФИО.Отчество);
			Если ЗначениеЗаполнено(Телефоны) Тогда
				Структура.Вставить("phones", Телефоны);
			КонецЕсли;
			Если ЗначениеЗаполнено(Емейл) Тогда
				Структура.Вставить("email", Емейл);
			Иначе
				Структура.Вставить("email", "");
			КонецЕсли;
			Если ЗначениеЗаполнено(Адрес) Тогда
				Структура.Вставить("address", Адрес);
			Иначе
				Структура.Вставить("address", "");
			КонецЕсли;
			Если ЗначениеЗаполнено(ДеньРождения) Тогда
				Структура.Вставить("birthday", Формат(ДеньРождения, "ДФ=dd.MM.yyyy"));
			Иначе
				Структура.Вставить("birthday", "");
			КонецЕсли;
		Иначе
			Структура.Вставить("last_name", "");
			Структура.Вставить("middle_name", "");
			Структура.Вставить("birthday", "");
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(ЛюбойТелефон) Тогда
			Структура.Вставить("phone_hash", МД5(ЛюбойТелефон));
		Иначе
			Структура.Вставить("phone_hash", "");	
		КонецЕсли;

		Если ЗначениеЗаполнено(Емейл) Тогда
			Структура.Вставить("email_hash", МД5(Емейл));
		Иначе
			Структура.Вставить("email_hash", "");
		КонецЕсли;

		Структура.Вставить("client_confirm_communication", ?(Контрагент.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да, 1, 0));
		Структура.Вставить("may_process_personal_data", Структура.client_confirm_communication);
	КонецЕсли;

	// Отправка данных
	Если Не ЗначениеЗаполнено(идОбновления) Тогда
		Возврат ОтправитьЗапрос("/api/client", Структура, Ошибка);
	Иначе
		Возврат ОтправитьЗапрос("/api/client/" + ИдОбновленияСтрока, Структура, Ошибка, "PUT");
	КонецЕсли;
	
КонецФункции
Функция ВыгрузитьГрафикРаботы(ИДМастера, Дата, Ошибка)
	// Формируем график работы мастера на день, если он не выгружен
	Структ = Новый Структура;
	Структ.Вставить("user_id", ИДМастера);
	Структ.Вставить("date", Формат(Дата, "ДФ=yyyy-MM-dd"));
	Структ.Вставить("time_from", "07:00");
	Структ.Вставить("time_to", "23:00");
	Структ.Вставить("status", 1);

	// Отправляем график и возвращаем ID
	ИД = ОтправитьЗапрос("/api/staff/schedule", Структ, Ошибка);
	Возврат ИД;
КонецФункции
//

// Доп функции
Функция ОбновитьПробегВЗН(идОбновления, Автомобиль, Ошибка)
	
	пробег = ПолучитьПробегАвтомобиля(автомобиль);
	
	Если Не ЗначениеЗаполнено(пробег) Тогда
		Возврат Ложь;
	КонецЕсли;	
		
	Структура = Новый Структура; 
	//Если ЗначениеЗаполнено(ИдОбновления) Тогда
	//	Структура.Вставить("ID", ИдОбновления);
	//КонецЕсли;	
	Структура.Вставить("mileage",  пробег);
	//Структура.Вставить("brand_id", ИдБренда);
	//Структура.Вставить("model_id", ИдМодели);
	//Структура.Вставить("code", Автомобиль.Код);
	//
	ОтправитьЗапрос("api/purchase/"+СтрЗаменить(идОбновления, Символы.НПП, ""), Структура, Ошибка, "PUT");

КонецФункции
//
#КонецОбласти

#Область ВспомогательныеФункции
Функция КорректноеЧисло(ч)
	Возврат СтрЗаменить(формат(ч, "ЧДЦ=4; ЧРД=.; ЧН="), Символы.НПП, "");
КонецФункции
Функция РазобратьФИО(Контрагент)
	_Наименование = СокрЛП(СтрЗаменить(Контрагент, "  ", " "));
	
	МасКомп = СтрРазделить(_Наименование, " ");
	СтруктураФИО = Новый Структура("Имя,Фамилия,Отчество", "", "", "");
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		Если ЗначениеЗаполнено(Контрагент.Имя) И ЗначениеЗаполнено(Контрагент.Фамилия) И ЗначениеЗаполнено(Контрагент.Отчество) Тогда
			СтруктураФИО.Имя = Контрагент.Имя;
			СтруктураФИО.Фамилия = Контрагент.Фамилия;
			СтруктураФИО.Отчество = Контрагент.Отчество;
			Возврат СтруктураФИО; 
		КонецЕсли;
	КонецЕсли;
	
	Если МасКомп.Количество() = 0 тогда
		// Пустая строка - возвращаем пустую структуру
	ИначеЕсли МасКомп.Количество() = 1 Тогда
		СтруктураФИО.Имя = МасКомп[0];
	Иначе
		СтруктураФИО.Имя = МасКомп[1];
		СтруктураФИО.Фамилия = МасКомп[0];
		
		Если МасКомп.Количество() > 2 Тогда
			СтруктураФИО.Отчество = МасКомп[2];
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат СтруктураФИО;
КонецФункции
Функция КорректныйТелефон(Телефон)
	//Непонятно для чего данная функция требуется
	поз = найти(Телефон, "<");
	Если поз = 0 Тогда
		Возврат Телефон;
	КонецЕсли;	        
	
	_Телефон = Сред(Телефон, поз + 1);
	Поз2 = Найти(_Телефон, ">");
	
	Если Поз2 = 0 Тогда
		Возврат _Телефон;
	Иначе
		Возврат Лев(_Телефон, поз2 - 1);
	КонецЕсли;	
КонецФункции
Функция ФормаСобственности(Контрагент)
	Если Найти(Контрагент, "ООО") > 0 Тогда
		Возврат "ООО";
	ИначеЕсли Найти(Контрагент, "ИП ") > 0 или Найти(Контрагент, " ИП") > 0 Тогда
		Возврат "ИП";	
	ИначеЕсли Найти(Контрагент, "АО ") > 0 или Найти(Контрагент, " АО") > 0 Тогда
		Возврат "АО";		
	ИначеЕсли Найти(Контрагент, "ОАО ") > 0 или Найти(Контрагент, " ОАО") > 0 Тогда
		Возврат "ОАО";			
	ИначеЕсли Найти(Контрагент, "ТОО ") > 0 или Найти(Контрагент, " ТОО") > 0 Тогда
		Возврат "ТОО";				
	ИначеЕсли Найти(Контрагент, "СООО ") > 0 или Найти(Контрагент, " СООО") > 0 Тогда
		Возврат "СООО";					
	ИначеЕсли Найти(Контрагент, "ЗАО ") > 0 или Найти(Контрагент, " ЗАО") > 0 Тогда
		Возврат "ЗАО";	
	Иначе
		Возврат "ООО";
	КонецЕсли;	
Конецфункции
Функция МД5(ЛюбойТелефон) Экспорт
	
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
 
	Хеш.Добавить(ЛюбойТелефон);
	 
	Возврат нрег(ПолучитьHexСтрокуИзДвоичныхДанных(Хеш.ХешСумма));
	
КонецФункции
Функция ПолучитьВсеСвязанныеДанныеЗаказНаряда(ЗаказНаряд) Экспорт 
	МассивСвязанныхЗначений = Новый СписокЗначений;
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.Контрагент);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.Автомобиль);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.Автомобиль.Модель);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.Автомобиль.Модель.Родитель);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.ВидРемонта);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.Мастер);
	МассивСвязанныхЗначений.Добавить(ЗаказНаряд.Мастер.Должность);
	КонтЛ = ПолучитьКонтактноеЛицо(ЗаказНаряд.Контрагент);
	Если ЗначениеЗаполнено(КонтЛ) Тогда
		МассивСвязанныхЗначений.Добавить(КонтЛ);
	КонецЕсли;
	Возврат МассивСвязанныхЗначений;
КонецФункции
Функция ПолучитьНаименованиеИнфообмена() Экспорт
	// Возможно в будущем понадобится для дашбоарда
	
	//Для Каждого Обр из Обработки Цикл
	//	Попытка
	//		Инфо = Обр.Создать().ПолучитьИнформациюИнфообмена();
	//		Сообщить(Инфо);
	//	Исключение
	//	Конецпопытки;
	//КонецЦикла;
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя;
	Возврат ИмяОбработки;
КонецФункции
Функция УказатьСогласиеКонтрагентаСОПД(Контрагент) Экспорт
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо И 
		Контрагент.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Спрашивать Тогда
		КонтрагентОбъект = Контрагент.ПолучитьОбъект();  
		КонтрагентОбъект.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да;
		КонтрагентОбъект.СогласиеНаОбработкуПерсональныхДанныхДата = ТекущаяДата();
		КонтрагентОбъект.Записать();
	КонецЕсли;
КонецФункции
// Функция выполняет сравнение двух коллекций строк, для которых доступен обход 
// посредством оператора Для каждого … Из … Цикл.
// Сравниваемые коллекции должны отвечать следующим требованиям:
//	- доступен обход посредством оператора Для каждого … Из … Цикл,
//	- наличие в обеих коллекциях всех колонок, перечисленных в параметре ИменаКолонок 
//  (если ИменаКолонок не заполнен - всех колонок)
//
// Параметры:
//	КоллекцияСтрок1 - коллекция, отвечающая вышеописанным требованиям
//	КоллекцияСтрок2 - коллекция, отвечающая вышеописанным требованиям
//	ИменаКолонок 	- строка, разделенная запятыми - имена колонок, 
//						по которым производится сравнение. 
//						Не является обязательным для коллекций, 
//						состав колонок которых можно выявить: 
//						ТаблицаЗначений, СписокЗначений, Соответствие, Структура,
//						если не указан - сравнение производится по всем колонкам.
//						Для коллекций других типов является обязательным
//	ИсключаяКолонки	- имена колонок, которые игнорируются при сравнении, необязательный
//	УчитыватьПоследовательностьСтрок - булево, если Истина, то коллекции признаются 
//						идентичными только если одинаковые строки размещены 
//						в коллекциях на одинаковых местах
//
Функция КоллекцииИдентичны_Изолированн(КоллекцияСтрок1, КоллекцияСтрок2, ИменаКолонок = "", ИсключаяКолонки = "", УчитыватьПоследовательностьСтрок = Ложь) Экспорт
	
	// Типы коллекций, для которых доступен состав колонок, 
	// и можно выявить, если он не задан
	ТипыОсобыхКоллекций = Новый Массив;
	ТипыОсобыхКоллекций.Добавить(Тип("ТаблицаЗначений"));
	ТипыОсобыхКоллекций.Добавить(Тип("СписокЗначений"));
	
	ТипыКоллекцийКлючИЗначение = Новый Массив;
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("Соответствие"));
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("Структура"));
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("ФиксированноеСоответствие"));
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("ФиксированнаяСтруктура"));
	
	Если ПустаяСтрока(ИменаКолонок) Тогда
		Если ТипыОсобыхКоллекций.Найти(ТипЗнч(КоллекцияСтрок1)) <> Неопределено 
			Или ТипыКоллекцийКлючИЗначение.Найти(ТипЗнч(КоллекцияСтрок1)) <> Неопределено Тогда
			СравниваемыеКолонки = Новый Массив;
			Если ТипЗнч(КоллекцияСтрок1) = Тип("ТаблицаЗначений") Тогда
				Для Каждого Колонка Из КоллекцияСтрок1.Колонки Цикл
					СравниваемыеКолонки.Добавить(Колонка.Имя);
				КонецЦикла;
			ИначеЕсли ТипЗнч(КоллекцияСтрок1) = Тип("СписокЗначений") Тогда
				СравниваемыеКолонки.Добавить("Значение");
				СравниваемыеКолонки.Добавить("Картинка");
				СравниваемыеКолонки.Добавить("Пометка");
				СравниваемыеКолонки.Добавить("Представление");
			ИначеЕсли ТипыКоллекцийКлючИЗначение.Найти(ТипЗнч(КоллекцияСтрок1)) <> Неопределено Тогда
				СравниваемыеКолонки.Добавить("Ключ");
				СравниваемыеКолонки.Добавить("Значение");
			КонецЕсли;
		Иначе
			ТекстИсключения = НСтр("ru = 'Для коллекции типа "+Строка(ТипЗнч(КоллекцияСтрок1))+" необходимо указать имена полей, по которым производится сравнение'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	Иначе
		СравниваемыеКолонки = РазложитьСтрокуВМассивПодстрок_Изолированн(ИменаКолонок);
	КонецЕсли;

	// Вычитаем исключаемые поля
	СравниваемыеКолонки = СократитьМассив_Изолированн(СравниваемыеКолонки, 
						РазложитьСтрокуВМассивПодстрок_Изолированн(ИсключаяКолонки));
						
	Если УчитыватьПоследовательностьСтрок Тогда
		
		// Параллельный обход обеих коллекций
		НомерСтрокиКоллекции1 = 0;
		Для Каждого СтрокаКоллекции1 Из КоллекцияСтрок1 Цикл
			// Спозиционируемся на аналогичную строку второй коллекции
			НомерСтрокиКоллекции2 = 0;
			ЕстьСтрокиКоллекции2 = Ложь;
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				ЕстьСтрокиКоллекции2 = Истина;
				Если НомерСтрокиКоллекции2 = НомерСтрокиКоллекции1 Тогда
					Прервать;
				КонецЕсли;
				НомерСтрокиКоллекции2 = НомерСтрокиКоллекции2 + 1;
			КонецЦикла;
			Если Не ЕстьСтрокиКоллекции2 Тогда
				// Во второй коллекции вообще нет строк
				Возврат Ложь;
			КонецЕсли;
			// Сравниваем значения полей двух строк
			Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
				Если СтрокаКоллекции1[ИмяКолонки] <> СтрокаКоллекции2[ИмяКолонки] Тогда
					Возврат Ложь;
				КонецЕсли;
			КонецЦикла;
			НомерСтрокиКоллекции1 = НомерСтрокиКоллекции1 + 1;
		КонецЦикла;
		
		КоличествоСтрокКоллекции1 = НомерСтрокиКоллекции1;
		
		// Отдельно подсчитаем количество строк второй коллекции
		КоличествоСтрокКоллекции2 = 0;
		Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
			КоличествоСтрокКоллекции2 = КоличествоСтрокКоллекции2 + 1;
		КонецЦикла;
		
		// Если в первой коллекции не оказалось строк, 
		// то их не должно быть и во второй
		Если КоличествоСтрокКоллекции1 = 0 Тогда
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				Возврат Ложь;
			КонецЦикла;
			КоличествоСтрокКоллекции2 = 0;
		КонецЕсли;
		
		// Количество строк не должно отличаться
		Если КоличествоСтрокКоллекции1 <> КоличествоСтрокКоллекции2 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
	
		// Проверяем идентичность состава одинаковых строк без учета их последовательности
		
		// Строки отбора накапливаем по первой коллекции для того, чтобы:
		//  - повторно не искать одинаковые строки,
		//  - убедиться, что во второй коллекции ни одной такой строки, которой нет в накопленных
		
		СтрокиОтбора = Новый ТаблицаЗначений;
		ПараметрыОтбора = Новый Структура;
		Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
			СтрокиОтбора.Колонки.Добавить(ИмяКолонки);
			ПараметрыОтбора.Вставить(ИмяКолонки);
		КонецЦикла;
		
		ЕстьСтрокиКоллекции1 = Ложь;
		Для Каждого СтрокаОтбора Из КоллекцияСтрок1 Цикл
			
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаОтбора);
			Если СтрокиОтбора.НайтиСтроки(ПараметрыОтбора).Количество() > 0 Тогда
				// строку с такими полями уже искали
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокиОтбора.Добавить(), СтрокаОтбора);
			
			// Подсчитаем количество таких строк в первой коллекции
			НайденоСтрокКоллекции1 = 0;
			Для Каждого СтрокаКоллекции1 Из КоллекцияСтрок1 Цикл
				СтрокаПодходит = Истина;
				Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
					Если СтрокаКоллекции1[ИмяКолонки] <> СтрокаОтбора[ИмяКолонки] Тогда
						СтрокаПодходит = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если СтрокаПодходит Тогда
					НайденоСтрокКоллекции1 = НайденоСтрокКоллекции1 + 1;
				КонецЕсли;
			КонецЦикла;
			
			// Подсчитаем количество таких строк во второй коллекции
			НайденоСтрокКоллекции2 = 0;
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				СтрокаПодходит = Истина;
				Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
					Если СтрокаКоллекции2[ИмяКолонки] <> СтрокаОтбора[ИмяКолонки] Тогда
						СтрокаПодходит = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если СтрокаПодходит Тогда
					НайденоСтрокКоллекции2 = НайденоСтрокКоллекции2 + 1;
					// Если количество таких строк во второй коллекции превысило количество в первой, 
					// то уже можно сделать вывод, что коллекции не идентичны
					Если НайденоСтрокКоллекции2 > НайденоСтрокКоллекции1 Тогда
						Возврат Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			// Количество таких строк не должно отличаться
			Если НайденоСтрокКоллекции1 <> НайденоСтрокКоллекции2 Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ЕстьСтрокиКоллекции1 = Истина;
			
		КонецЦикла;
		
		// Если в первой коллекции не оказалось строк, 
		// то их не должно быть и во второй
		Если Не ЕстьСтрокиКоллекции1 Тогда
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				Возврат Ложь;
			КонецЦикла;
		КонецЕсли;
		
		// Проверим, что во второй коллекции нет ни одной такой строки, которой нет в накопленных
		Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаКоллекции2);
			Если СтрокиОтбора.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции
// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//     - для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//     - если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//
//
// Возвращаемое значение:
//  Массив - массив строк.
//
// Примеры:
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",") - возвратит массив из 5 элементов, три из которых  - пустые строки;
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",", Истина) - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок(" один   два  ", " ") - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок("") - возвратит пустой массив;
//  РазложитьСтрокуВМассивПодстрок("",,Ложь) - возвратит массив с одним элементом "" (пустой строкой);
//  РазложитьСтрокуВМассивПодстрок("", " ") - возвратит массив с одним элементом "" (пустой строкой);
//
Функция РазложитьСтрокуВМассивПодстрок_Изолированн(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено) 
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
// Вычитает один массив элементов из другого массива. Возвращает результат вычитания.
//
// Параметры:
//  Массив - массив элементов, из которого необходимо выполнить вычитание;
//  МассивВычитания - массив элементов, который будет вычитаться.
// 
// Возвращаемое значение:
//  Массив – результат вычитания двух массивов.
//
Функция СократитьМассив_Изолированн(Массив, МассивВычитания) Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого Элемент Из Массив Цикл
		
		Если МассивВычитания.Найти(Элемент) = Неопределено Тогда
			
			Результат.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти

#Область РаботаСРегистрамиСопоставления
Процедура ДобавитьВыгруженныеДанные(Ссылка, Тип, Ид, Комментарий = "", ДопДанные = Неопределено, Name = Неопределено)
	Наб = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	Наб.Отбор.Данные1С.Установить(Ссылка);
	Наб.Отбор.Тип.Установить(Тип);
	
	Если ЗначениеЗаполнено(Name) Тогда
		Наб.Отбор.Name.Установить(Name);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДопДанные) Тогда
		Наб.Отбор.ДопДанные1С.Установить(ДопДанные);
	Иначе
		Если Тип = "ЗаказНаряд" Тогда
			Наб.Отбор.ДопДанные1С.Установить(Неопределено);
		КонецЕсли;	
	КонецЕсли;	                                  
	
	Наб.Прочитать();
	
	Если Наб.Количество() = 0 тогда
		Мен = Наб.Добавить();
		Мен.Период = ТекущаяДатаСеанса();
		Мен.Тип = Тип;
		Мен.Данные1С = Ссылка;
	Иначе
		Мен = Наб[0];
	КонецЕсли;	
	
	мен.Комментарий = Комментарий;
	Мен.ID = Ид;
	Мен.ДатаВыгрузки = ТекущаяДатаСеанса();
	Мен.ТребуетсяОбновление = Ложь;
	Если ЗначениеЗаполнено(ДопДанные) и Тип = "Контрагент" Тогда
		мен.ДопДанные1С = ДопДанные;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Name) Тогда
		мен.Name = Name;
	КонецЕсли;
	
	Наб.Записать();

КонецПроцедуры	
Функция НайтиВыгруженныйОбъект(Ссылка = Неопределено, УчетнаяЗапись = Неопределено, Тип = Неопределено, Name = Неопределено)
	
	Набор = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Набор.Отбор.Данные1С.Установить(Ссылка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Набор.Отбор.ДопДанные1С.Установить(УчетнаяЗапись);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Тип) Тогда
		Набор.Отбор.Тип.Установить(Тип);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Name) Тогда
		Набор.Отбор.Name.Установить(Name);
	КонецЕсли;
	
    Набор.Прочитать();
	
	Если Набор.Количество() Тогда
		Возврат Набор[0].ID;
	КонецЕсли;	
	
КонецФункции
//
Процедура ПроверитьИЗаписатьДляВыгрузки(ЗаказНарядИлиЗаписьНаРемонт, ДопСвойства=Неопределено) Экспорт
	
	Если ТипЗнч(ЗаказНарядИлиЗаписьНаРемонт) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ЗаказНаряд = ЗаказНарядИлиЗаписьНаРемонт;
		Если ДоступенЛиДляВыгрузки(ЗаказНаряд) Тогда
			ЗаписатьЗаказНарядДляВыгрузки(ЗаказНаряд);
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЗаказНарядИлиЗаписьНаРемонт) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		ЗаписьНаРемонт = ЗаказНарядИлиЗаписьНаРемонт;
		Если ПолучитьВыгружатьЛиЗаписиНаРемонт() Тогда
			Если ДоступенЛиДляВыгрузкиЗнР(ЗаписьНаРемонт) Тогда
				ЗаписатьЗнРДляВыгрузки(ЗаписьНаРемонт, ДопСвойства);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;

КонецПроцедуры
// Заказ-наряд
Процедура ЗаписатьЗаказНарядДляВыгрузки(ЗаказНаряд) Экспорт
	
	НаборЗаписей = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Тип.Установить("ЗаказНаряд");
	НаборЗаписей.Отбор.Данные1С.Установить(ЗаказНаряд);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Мен = ПолучитьРегистрСопоставления().СоздатьМенеджерЗаписи();
		Мен.Период = ТекущаяДатаСеанса();
		Мен.Тип = "ЗаказНаряд";
		Мен.Данные1С = ЗаказНаряд;
		Мен.Записать();
	КонецЕсли;

КонецПроцедуры
Функция ДоступенЛиДляВыгрузки(ЗаказНаряд) Экспорт
	
	Если 1=2 Тогда
		ЗаказНаряд = Документы.ЗаказНаряд.СоздатьДокумент();
	КонецЕсли;
	
	Если ЗаказНаряд.НеВыгружатьИмпортеру Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НаимРегистраСоответствий = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	НаимРегистраСоответствийВидовРемонта = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияВидовРемонта());
	НаимРегистраСоответствийПодразделений = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияПодразделений());

	Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	БТ_СопоставлениеПодразделений.Подразделение КАК Подразделение
		                      |ИЗ
		                      |	РегистрСведений."+НаимРегистраСоответствийПодразделений+" КАК БТ_СопоставлениеПодразделений
		                      |ГДЕ
		                      |	БТ_СопоставлениеПодразделений.Подразделение = &Подразделение
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	БТ_СопоставлениеДанных.Период КАК Период
		                      |ИЗ
		                      |	РегистрСведений."+НаимРегистраСоответствий+" КАК БТ_СопоставлениеДанных
		                      |ГДЕ
		                      |	БТ_СопоставлениеДанных.Тип = &Тип
		                      |	И БТ_СопоставлениеДанных.Данные1С = &Данные1С
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта КАК ВидРемонта,
		                      |	БТ_СопоставлениеВидовРемонта.ДанныеИнфообмена КАК ДанныеИнфообмена,
		                      |	БТ_СопоставлениеВидовРемонта.ТипОплатыИнфообмен КАК ТипОплатыИнфообмен
		                      |ИЗ
		                      |	РегистрСведений."+НаимРегистраСоответствийВидовРемонта+" КАК БТ_СопоставлениеВидовРемонта
		                      |ГДЕ
		                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта = &ВидРемонта");
	Запрос.УстановитьПараметр("Тип", "Бренд");
	Запрос.УстановитьПараметр("Данные1С", ЗаказНаряд.Автомобиль.Модель.Родитель);
	СтрокаДляПодмены = ПодменяемыеДанные.Найти(ЗаказНаряд.ПодразделениеКомпании, "ПодменяемоеЗначение");
	
	Если СтрокаДляПодмены <> Неопределено Тогда
		Запрос.УстановитьПараметр("Подразделение", СтрокаДляПодмены.ЗначениеПодмены);			
	Иначе
		Запрос.УстановитьПараметр("Подразделение", ЗаказНаряд.ПодразделениеКомпании);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидРемонта", ЗаказНаряд.ВидРемонта);

	Пакет = Запрос.ВыполнитьПакет();
	
	ПроверкаПоСопоставлениюПодразделения = Не Пакет[0].Пустой();
	
	// При определении конкретной учетной записи для данного инфообмена пропускаем проверку по сопоставлению Профиль/Подразделение
	Если ЗначениеЗаполнено(ПолучитьКонстантноеЗначение("ТолькоПрофильПодразделения")) Тогда
		ПроверкаПоСопоставлениюПодразделения = Истина;
	КонецЕсли;
	
	Если ПроверкаПоСопоставлениюПодразделения и Не пакет[1].Пустой() И НЕ Пакет[2].пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции
// Заявка на ремонт
Функция ДоступенЛиДляВыгрузкиЗнР(ЗнР_Ссылка)
	
	Если 1=2 Тогда
		ЗнР_Ссылка = Документы.ЗаявкаНаРемонт.СоздатьДокумент();
	КонецЕсли;

	//Проверим что автомобиль указан из справочника
	Если ЗнР_Ссылка.Автомобиль = Неопределено Или ТипЗнч(ЗнР_Ссылка.Автомобиль) <> Тип("СправочникСсылка.Автомобили") Тогда
		Возврат Ложь;
	КонецЕсли;

	//Проверим хозоперацию
	Если ЗнР_Ссылка.ХозОперация.Наименование <> "План ремонта" Тогда
		Возврат Ложь;
	КонецЕсли;
	//

	//Проверим что таблица планирования заполнена
	Если ЗнР_Ссылка.Планирование.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	//
	
	//Проверим что все записи планирования для будущего
	Для каждого СтрокаПланирования Из ЗнР_Ссылка.Планирование цикл  
		Если СтрокаПланирования.НачалоВыполнения  <= ТекущаяДатаСеанса() Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	//
	
	НаимРегистраСоответствий = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	НаимРегистраСоответствийВидовРемонта = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияВидовРемонта());
	НаимРегистраСоответствийПодразделений = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияПодразделений());

	Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	БТ_СопоставлениеПодразделений.Подразделение КАК Подразделение
		                      |ИЗ
		                      |	РегистрСведений."+НаимРегистраСоответствийПодразделений+" КАК БТ_СопоставлениеПодразделений
		                      |ГДЕ
		                      |	БТ_СопоставлениеПодразделений.Подразделение = &Подразделение
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	БТ_СопоставлениеДанных.Период КАК Период
		                      |ИЗ
		                      |	РегистрСведений."+НаимРегистраСоответствий+" КАК БТ_СопоставлениеДанных
		                      |ГДЕ
		                      |	БТ_СопоставлениеДанных.Тип = &Тип
		                      |	И БТ_СопоставлениеДанных.Данные1С = &Данные1С
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта КАК ВидРемонта,
		                      |	БТ_СопоставлениеВидовРемонта.ДанныеИнфообмена КАК ДанныеИнфообмена,
		                      |	БТ_СопоставлениеВидовРемонта.ТипОплатыИнфообмен КАК ТипОплатыИнфообмен
		                      |ИЗ
		                      |	РегистрСведений."+НаимРегистраСоответствийВидовРемонта+" КАК БТ_СопоставлениеВидовРемонта
		                      |ГДЕ
		                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта = &ВидРемонта
							  |	И БТ_СопоставлениеВидовРемонта.ВыгружатьДляЖурналаЗаписи = ИСТИНА");
	Запрос.УстановитьПараметр("Тип", "Модель");
	Запрос.УстановитьПараметр("Данные1С", ЗнР_Ссылка.Автомобиль.Модель);
	Запрос.УстановитьПараметр("Подразделение", ЗнР_Ссылка.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ВидРемонта", ЗнР_Ссылка.ВидРемонта);

	Пакет = Запрос.ВыполнитьПакет();
	
	ПроверкаПоСопоставлениюПодразделения = Не Пакет[0].Пустой();
	
	// При определении конкретной учетной записи для данного инфообмена пропускаем проверку по сопоставлению Профиль/Подразделение
	Если ЗначениеЗаполнено(ПолучитьКонстантноеЗначение("ТолькоПрофильПодразделения")) Тогда
		ПроверкаПоСопоставлениюПодразделения = Истина;
	КонецЕсли;
	
	Если ПроверкаПоСопоставлениюПодразделения и Не пакет[1].Пустой() И НЕ Пакет[2].пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
Процедура ЗаписатьЗнРДляВыгрузки(ЗнР_Ссылка, ДопСвойства=Неопределено) Экспорт

	НаборЗаписей = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Тип.Установить("ПланРемонта");
	НаборЗаписей.Отбор.Данные1С.Установить(ЗнР_Ссылка);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		// Значит ЗнР еще нет в регистре
		// Проверим что Заявка не отменена
		ЗаявкаОтменена = (ЗнР_Ссылка.Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено ИЛИ ЗнР_Ссылка.ПометкаУдаления);
		Если ЗаявкаОтменена Тогда
			Возврат;
		Иначе
			Мен = ПолучитьРегистрСопоставления().СоздатьМенеджерЗаписи();
			Мен.Период = ТекущаяДатаСеанса();
			Мен.Тип = "ПланРемонта";
			Мен.Данные1С = ЗнР_Ссылка;
			Мен.Записать();	
		КонецЕсли;
	Иначе
		// Значит ЗнР уже существует в регистре
		ТекущаяЗапись = НаборЗаписей[0];
		// Проверим выгружен ли он уже
		Если НЕ ЗначениеЗаполнено(ТекущаяЗапись.ID) Тогда
			// ЗнР еще не выгружен поэтому не будем трогать запись
			Возврат;
		ИначеЕсли ТекущаяЗапись.ТребуетсяОбновление = Истина Тогда
			// Для ЗнР уже стоит признак ТребуетсяОбновление, значит он уже в очереди на выгрузку
			Возврат;
		Иначе
			// ЗнР уже выгружен, проверим что есть ли доп свойства для проверки изменения
			ДанныеДоЗаписиОбъекта = Неопределено;
			Если ДопСвойства <> Неопределено И ДопСвойства.Свойство("ДанныеДляПроверкиЗнР" + ПолучитьНаименованиеИнфообмена(), ДанныеДоЗаписиОбъекта) Тогда
				// Проверим изменились ли данные
				ИзмененыДанные = Ложь;

				// Получим массив реквизитов для проверки
				МассивРеквизитов = ПолучитьКонстантноеЗначение("МассивРеквизитовДляПроверкиЗнР");

				// Проверим каждый реквизит на изменение
				Для Каждого Реквизит Из МассивРеквизитов Цикл
					// проверим что такой реквизит существует в структуре до записи
					Если ДанныеДоЗаписиОбъекта.Свойство(Реквизит) Тогда
						// определим тип реквизита
						Если ТипЗнч(ДанныеДоЗаписиОбъекта[Реквизит]) = Тип("ТаблицаЗначений") Тогда
							// Табличная часть
							Если НЕ КоллекцииИдентичны_Изолированн(ДанныеДоЗаписиОбъекта[Реквизит], ЗнР_Ссылка[Реквизит].Выгрузить()) Тогда
								ИзмененыДанные = Истина;
								Прервать;
							КонецЕсли;
						Иначе
							// Реквизит
							Если ДанныеДоЗаписиОбъекта[Реквизит] <> ЗнР_Ссылка[Реквизит] Тогда
								ИзмененыДанные = Истина;
								Прервать;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				// Если данные изменены, то установим признак ТребуетсяОбновление
				Если ИзмененыДанные Тогда
					ТекущаяЗапись.ТребуетсяОбновление = Истина;
					НаборЗаписей.Записать();
				КонецЕсли;
			Иначе
				// Если доп свойств нет, то сообщим что логика проверки изменений в документе нарушена но делать ничего не будем
				Сообщить("Попытка записи ЗнР в регистр сопоставления без данных для проверки изменений. Логика нарушена.");
				Возврат;
			КонецЕсли;
				
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
//
#КонецОбласти

#Область ПолучитьДанные
Функция ПолучитьВидРемонта(ВидРемонта)

	НаимРегистраСопоставления = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	НаимРегистраСопоставленияВидовРемонта = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияВидовРемонта());

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСопоставленияВидовРемонта+" КАК БТ_СопоставлениеВидовРемонта
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений."+НаимРегистраСопоставления+" КАК БТ_СопоставлениеДанных
	                      |		ПО БТ_СопоставлениеВидовРемонта.ДанныеИнфообмена = БТ_СопоставлениеДанных.Name
	                      |			И (БТ_СопоставлениеДанных.Тип = ""ВидРемонта"")
	                      |ГДЕ
	                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта = &ВидРемонта");
	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ID;
	КонецЕсли;	
	
	Возврат Неопределено;
КонецФункции
Функция ПолучитьВидРемонтаПоТО(ВидРемонта, Номер_ТО)

	НаимРегистраСопоставления = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	НаимРегистраСопоставленияВидовРемонта = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияВидовРемонта());

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСопоставленияВидовРемонта+" КАК БТ_СопоставлениеВидовРемонта
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений."+НаимРегистраСопоставления+" КАК БТ_СопоставлениеДанных
	                      |		ПО БТ_СопоставлениеВидовРемонта.ДанныеИнфообмена = БТ_СопоставлениеДанных.Name
	                      |			И (БТ_СопоставлениеДанных.Тип = ""ВидРемонта"")
	                      |ГДЕ
	                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта = &ВидРемонта
						  | И БТ_СопоставлениеВидовРемонта.НомерТО = &НомерТО");
	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	Запрос.УстановитьПараметр("НомерТО", Номер_ТО);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ID;
	КонецЕсли;	
	
	Возврат Неопределено;
КонецФункции
Функция ПолучитьТипОплаты(ВидРемонта)

	НаимРегистраСопоставления = ПолучитьИмяРегистра(ПолучитьРегистрСопоставления());
	НаимРегистраСопоставленияВидовРемонта = ПолучитьИмяРегистра(ПолучитьРегистрСопоставленияВидовРемонта());

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанных.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений."+НаимРегистраСопоставленияВидовРемонта+" КАК БТ_СопоставлениеВидовРемонта
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений."+НаимРегистраСопоставления+" КАК БТ_СопоставлениеДанных
	                      |		ПО БТ_СопоставлениеВидовРемонта.ТипОплатыИнфообмен = БТ_СопоставлениеДанных.Name
	                      |			И (БТ_СопоставлениеДанных.Тип = ""ТипОплаты"")
	                      |ГДЕ
	                      |	БТ_СопоставлениеВидовРемонта.ВидРемонта = &ВидРемонта");
	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ID;
	КонецЕсли;	
	
	Возврат Неопределено;
КонецФункции
Функция ПолучитьПробегАвтомобиля(авто)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	АвтомобилиСрезПоследних.Значение КАК Значение
	                      |ИЗ
	                      |	РегистрСведений.Автомобили.СрезПоследних(
	                      |			,
	                      |			Автомобиль = &Авто
	                      |				И ВидЗначения = &Видзначения) КАК АвтомобилиСрезПоследних");  
	Запрос.УстановитьПараметр("Авто", Авто);
	Запрос.УстановитьПараметр("ВидЗначения", Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;
	КонецЕсли;	                 

	Возврат 0;
	
КонецФункции
Функция ПолучитьКонтактноеЛицо(Контрагент) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КонтактныеЛицаКонтрагентов.Ведомый КАК Ссылка
	                      |ИЗ
	                      |	РегистрСведений.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	                      |ГДЕ
	                      |	КонтактныеЛицаКонтрагентов.Ведущий = &Ведущий
	                      |	И КонтактныеЛицаКонтрагентов.Использование
	                      |	И КонтактныеЛицаКонтрагентов.Основной"); 
	Запрос.УстановитьПараметр("Ведущий", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Ссылка;
		
	КонецЕсли;	
	
КонецФункции
Функция Телефоны(Контрагент, ЛюбойТелефон = "")
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
	                      |	КонтактнаяИнформация.Представление КАК Представление,
	                      |	ВЫБОР
	                      |		КОГДА КонтактнаяИнформация.Вид = &Мобильный
	                      |			ТОГДА 1
	                      |		ИНАЧЕ 2
	                      |	КОНЕЦ КАК Тип
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект");
	Запрос.УстановитьПараметр("Мобильный", Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	МассивДанных = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	спУже = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		Если СпУже.НайтиПоЗначению(Выборка.Представление) <> Неопределено Тогда
			ПродолжитЬ;
		КонецЕсли;	   
		СпУже.Добавить(Выборка.Представление);
		
		ЛюбойТелефон = Выборка.Представление;
		
		Структура = Новый структура("number,type", Выборка.Представление,выборка.ТИп);
		МассивДанных.Добавить(Структура);
	КонецЦикла;
	
	Возврат МассивДанных;
КонецФункции
Функция Адрес(Контрагент, ВидКонтактнойИнфо = Неопределено)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект
	                      |	И (КонтактнаяИнформация.Вид = &Вид
	                      |			ИЛИ &все)");
	Запрос.УстановитьПараметр("Вид", ВидКонтактнойИнфо);
	Запрос.УстановитьПараметр("Все", ВидКонтактнойИнфо = Неопределено);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	МассивДанных = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Представление;
	КонецЕсли;
	
КонецФункции 
Функция Емейл(Контрагент)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект");
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	МассивДанных = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат КорректныйТелефон(Выборка.Представление);
	КонецЕсли;

КонецФункции
#КонецОбласти


//Создание "Согласий" на портале
#Область ФункционалСогласийНОВЫЙ

#Область НастройкаСогласийВКонфигурации

// В форме заказ-наряда сделать кнопку и реквизит СостояниеСогласияДляHaval (число)

// В модуле Формы документа заказ-наряд добавить следующее в процедуру ПриОткрытии():

//Если Обработки.БТ_ИнфообменНовый.Создать().ОбновитьСостояниеКнопкиСогласия(ЭтотОбъект) Тогда
//	ЭлементыФормы.ДействияФормы.Кнопки.СостояниеСогласияДляHaval.Доступность = Истина;
//	ЭтаФорма.СостояниеСогласияДляHaval = 1; // Согласие надо проверить
//Иначе
//	ЭлементыФормы.ДействияФормы.Кнопки.СостояниеСогласияДляHaval.Доступность = Ложь;
//	ЭтаФорма.СостояниеСогласияДляHaval = 0; // Согласие не требуется
//КонецЕсли;

#КонецОбласти

Функция ОбновитьСостояниеКнопкиСогласия(ДокументЗН) Экспорт
	
	СписокВидовРемонта = Неопределено;
	СписокПодразделений = Неопределено;
	
	СписокКонстантныхЗначенийМеханизмаСогласий().Свойство("СписокПодразделенийДляОбработкиСогласия",СписокПодразделений);
	СписокКонстантныхЗначенийМеханизмаСогласий().Свойство("СписокВидовРемонтаДляОбработкиСогласия",СписокВидовРемонта);
		
	//Проверим что объект это документ ЗаказНаряд
	Если ТипЗнч(ДокументЗН) <> Тип("ДокументОбъект.ЗаказНаряд") Тогда
		Возврат Ложь;
	КонецЕсли;
	//
	
	//Проверим что объект не новый документ
	Если ДокументЗН.ЭтоНовый() Тогда
		Возврат Ложь;
	КонецЕсли;
	//
	
	//Если текущее состояние заказ-наряда не соответствует начальному статусу
	//Если ДокументЗН.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
	//	Возврат Ложь;
	//КонецЕсли;
	//
	
	//Если заполнен список подразделений то проверим что заказ-наряд относится к одному из подразделений
	Если СписокПодразделений <> Неопределено Тогда
		Если СписокПодразделений.Найти(ДокументЗН.ПодразделениеКомпании) = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	//

	//Если заполнен список видов ремонта то проверим что заказ-наряд относится к одному из видов ремонта
	Если СписокВидовРемонта <> Неопределено Тогда
		Если СписокВидовРемонта.Найти(ДокументЗН.ВидРемонта) = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	//

	//Если заказчик не частное лицо то отказ
	Если ДокументЗН.Заказчик.ФормаСобственности <> Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		Возврат Ложь;
	КонецЕсли;
	//

	Возврат Истина;
	
КонецФункции


//Функции с согласиями для кнопки "Проверить согласие" в ЗН
Функция ПроверитьЕстьЛиСогласие(Контрагент, СтруктураСогласия=Неопределено) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
	
	//Определение контрагента для поиска
	КонтрагентДляПоиска = Неопределено;
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		КонтактноеЛицо = ПолучитьКонтактноеЛицо(Контрагент);
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			КонтрагентДляПоиска = КонтактноеЛицо;
		КонецЕсли;
	Иначе
		КонтрагентДляПоиска = Контрагент;
	КонецЕсли;
	//Если не заполнено контактное лицо
	Если НЕ ЗначениеЗаполнено(КонтрагентДляПоиска) Тогда
		Сообщить("Не найдено контактное лицо для поиска Согласий");
		Возврат Неопределено;
	КонецЕсли;
	//Если уже создано согласие
	// Если СтруктураСогласия передана в функцию - значит данные нужно брать с портала а не с регистра
	Если НЕ ТипЗнч(СтруктураСогласия) = Тип("Структура") Тогда
		Если ЗначениеЗаполнено(НайтиВыгруженноеСогласиеВРегистре(КонтрагентДляПоиска)) Тогда
			Возврат НайтиВыгруженноеСогласиеВРегистре(КонтрагентДляПоиска);
		КонецЕсли;
	КонецЕсли;
	//Обработка телефона контрагента
	ТелефонКонтрагента = ПолучитьОсновнойТелефонИЕгоТип(КонтрагентДляПоиска);
	Если ТелефонКонтрагента <> Неопределено Тогда
		ТелефонКонтрагента = ТелефонКонтрагента.Представление;
	Иначе
		Сообщить("Не заполнен основной номер телефона у контрагента");
		Возврат Неопределено;
	КонецЕсли;
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"(","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,")","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента," ","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"+","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"-","");
	ТелефонКонтрагента = СокрЛП(ТелефонКонтрагента);
	//ФОрмирование структуры для поиска
	СтруктураПоискаСогласий = новый Структура;
	СтруктураПоискаСогласий.Вставить("firstName",	СокрЛП(КонтрагентДляПоиска.Имя));
	СтруктураПоискаСогласий.Вставить("lastName",	СокрЛП(КонтрагентДляПоиска.Фамилия));
	СтруктураПоискаСогласий.Вставить("middleName",	СокрЛП(КонтрагентДляПоиска.Отчество));
	СтруктураПоискаСогласий.Вставить("phone",		СокрЛП(ТелефонКонтрагента));
	//Поиск существующего согласия
	Ошибка = "";
	НайденныеСогласия = НайтиСогласияПоРеквизитам(СтруктураПоискаСогласий,Ошибка);
	Если НайденныеСогласия = Ложь тогда
		Сообщить("Ошибка получения согласий. " + Ошибка);
		Возврат Неопределено;
	КонецЕсли;

	Если ТипЗнч(НайденныеСогласия) = Тип("Структура") И НайденныеСогласия.Свойство("id") Тогда
		СтруктураСогласия = НайденныеСогласия;
		Возврат НайденныеСогласия.id;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции
Функция НайтиВыгруженноеСогласиеВРегистре(СсылкаКонтрагент) Экспорт
	
	Набор = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	Набор.Отбор.Тип.Установить("Согласие");
	Набор.Отбор.Данные1С.Установить(СсылкаКонтрагент);
    Набор.Прочитать();
	
	Если Набор.Количество() Тогда
		
		Возврат Набор[0].ID;
		
	КонецЕсли;
	
КонецФункции
Функция ПолучитьОсновнойТелефонИЕгоТип(Контрагент) Экспорт 
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление,
	                      |	ВЫБОР
	                      |		КОГДА КонтактнаяИнформация.Вид = &Мобильный
	                      |			ТОГДА 1
	                      |		ИНАЧЕ 2
	                      |	КОНЕЦ КАК Тип
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект
	                      |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = ИСТИНА");
	Запрос.УстановитьПараметр("Мобильный", Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Объект", Контрагент);

	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() Тогда
		Возврат Таблица[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
Функция НайтиСогласияПоРеквизитам(СтруктураПоиска,Ошибка)
    
    Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
    
	СтрокаЗапросаПоиска = "?";
	Для каждого РеквизитПоиска из СтруктураПоиска Цикл
		Если ЗначениеЗаполнено(РеквизитПоиска.Значение) Тогда
			СтрокаЗапросаПоиска = СтрокаЗапросаПоиска + РеквизитПоиска.Ключ + "=" + РеквизитПоиска.Значение + "&";
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Запрос = Новый HTTPЗапрос("/api/personal-data-agreement/event"+СтрокаЗапросаПоиска, Заголовки);
    	Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос); 
	Исключение
		Ошибка = "Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат Ложь;
	КонецПопытки;

	Если Ответ.КодСостояния = 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Если ТелоОтвета = "null" тогда
			Возврат Новый Массив();
		КонецЕсли;
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(ТелоОтвета);
		МассивДанных = ПрочитатьJSON(Чтение);
		Возврат МассивДанных;
	Иначе	
		Ошибка = "Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

//Функции для формы обработки согласия
Функция ПолучитьФормуОбработкиСогласий(ПараметрыФормы=Неопределено) Экспорт
	ИмяИнфообмена = ЭтотОбъект.Метаданные().Имя;
	Параметры = Новый Структура("ПереданныеПараметры", ПараметрыФормы);
	Форма = ПолучитьФорму("Обработка."+ИмяИнфообмена+".Форма.ФормаОбработкиСогласия",Параметры);
	Возврат Форма;
КонецФункции

//Функции отображения прогресса
Функция ПолучитьМакетПрогресса() Экспорт
	Возврат ПолучитьМакет("ПрогрессСогласий");
КонецФункции


Функция ОбработатьСогласия(Контрагент,ЗаказНаряд,Ошибка) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
	
	//Определение контрагента для поиска
	КонтрагентДляПоиска = Неопределено;
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		КонтактноеЛицо = ПолучитьКонтактноеЛицо(Контрагент);
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			КонтрагентДляПоиска = КонтактноеЛицо;
		КонецЕсли;
	Иначе
		КонтрагентДляПоиска = Контрагент;
	КонецЕсли;
	//Если не заполнено контактное лицо
	Если НЕ ЗначениеЗаполнено(КонтрагентДляПоиска) Тогда
		Ошибка = "Не найдено контактное лицо для поиска Согласий";
		Возврат Ложь;
	КонецЕсли;
	//Если нет СОПД
	Если КонтрагентДляПоиска.СогласиеНаОбработкуПерсональныхДанных <> Перечисления.ВариантыОтветов.Да Тогда
		Возврат Истина;
	КонецЕсли;
	//Если уже создано согласие
	Если ЗначениеЗаполнено(НайтиВыгруженноеСогласиеВРегистре(КонтрагентДляПоиска)) Тогда
		Возврат Истина;
	КонецЕсли;
	//Обработка телефона контрагента
	ТелефонКонтрагента = ПолучитьОсновнойТелефонИЕгоТип(Контрагент);
	Если ТелефонКонтрагента <> Неопределено Тогда
		ТелефонКонтрагента = ТелефонКонтрагента.Представление;
	Иначе
		Ошибка = "Не заполнен основной номер телефона у контрагента";
		Возврат Ложь;
	КонецЕсли;
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"(","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,")","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента," ","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"+","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"-","");
	ТелефонКонтрагента = СокрЛП(ТелефонКонтрагента);
	//ФОрмирование структуры для поиска
	СтруктураПоискаСогласий = новый Структура;
	СтруктураПоискаСогласий.Вставить("firstName",	СокрЛП(КонтрагентДляПоиска.Имя));
	СтруктураПоискаСогласий.Вставить("lastName",	СокрЛП(КонтрагентДляПоиска.Фамилия));
	СтруктураПоискаСогласий.Вставить("middleName",	СокрЛП(КонтрагентДляПоиска.Отчество));
	СтруктураПоискаСогласий.Вставить("phone",		СокрЛП(ТелефонКонтрагента));
	//Поиск существующего согласия
	Ошибка = "";
	НайденныеСогласия = НайтиСогласияПоРеквизитам(СтруктураПоискаСогласий,Ошибка);
	Если НайденныеСогласия = Ложь тогда
		Возврат Ложь;
	КонецЕсли;
	//Если согласие не нашли
	Если НЕ НайденныеСогласия.Количество() Тогда
		
		Набор = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
		Набор.Отбор.Данные1С.Установить(КонтрагентДляПоиска);
		Набор.Отбор.Тип.Установить("Согласие");
		Набор.Прочитать();
		
		Если Набор.Количество() = 0 тогда
			ТекущаяЗапись = Набор.Добавить();
			ТекущаяЗапись.Тип = "Согласие";
			ТекущаяЗапись.Период = ТекущаяДатаСеанса();
			ТекущаяЗапись.Данные1С = КонтрагентДляПоиска;
		Иначе
			ТекущаяЗапись = Набор[0];
		КонецЕсли;
		
		Ошибка = "";
		IDСогласия = СоздатьНовоеСогласие(КонтрагентДляПоиска,ЗаказНаряд,Ошибка);
		//Если ошибка создания
		Если IDСогласия = Ложь Тогда
			ТекущаяЗапись.Комментарий = Ошибка;
			Набор.Записать();
		Иначе
			ТекущаяЗапись.Комментарий = "";
			ТекущаяЗапись.ID = IDСогласия;
			ТекущаяЗапись.ДатаВыгрузки = ТекущаяДатаСеанса();
			Набор.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции
Функция СоздатьНовоеСогласие(Контрагент,ЗаказНаряд,Ошибка) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
		
	//Пол контрагента
	ПолКонтрагента = Неопределено;
	Если Контрагент.Пол = Перечисления.ПолФизическихЛиц.Мужской Тогда
		ПолКонтрагента = 1;
	ИначеЕсли Контрагент.Пол = Перечисления.ПолФизическихЛиц.Женский Тогда
		ПолКонтрагента = 2;
	Иначе
		Ошибка = "Не указан пол контрагента";
		Возврат Ложь;
	КонецЕсли;
	
	//Обработка телефона контрагента
	СтруктураТелефона = ПолучитьОсновнойТелефонИЕгоТип(Контрагент);
	Если СтруктураТелефона <> Неопределено Тогда
		ТелефонКонтрагента = СтруктураТелефона.Представление;
		ТипТелефонаКонтрагента = СтруктураТелефона.Тип;
	Иначе
		Ошибка = "Не заполнен основной номер телефона у контрагента";
		Возврат Ложь;
	КонецЕсли;
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"(","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,")","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента," ","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"+","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"-","");
	ТелефонКонтрагента = СокрЛП(ТелефонКонтрагента);
	
	//Почта контрагента
	Если ЗначениеЗаполнено(ПолучитьОсновнуюПочту(Контрагент)) Тогда
		ПочтаКонтрагента = ПолучитьОсновнуюПочту(Контрагент);
	Иначе
		ПочтаКонтрагента = "";
	КонецЕсли;
	
	//Дата Рождения
	Если ЗначениеЗаполнено(Контрагент.ДатаРождения) Тогда
		ДатаРожденияСтрокой = Формат(Контрагент.ДатаРождения,"ДФ=dd.MM.yyyy");
	Иначе
		Ошибка = "Не заполнена дата рождения контрагента";
		Возврат Ложь;	
	КонецЕсли;
	
	//Паспорт
	СтруктураПаспорта = ПолучитьПаспорт(Контрагент);
	Если СтруктураПаспорта = Неопределено Тогда
		Ошибка = "Не заполнен паспорт контрагента";
		Возврат Ложь;
	КонецЕсли;
	
	//Адрес
	КонтрагентАдрес = "";
	КонтрагентАдресФактический 	= киПолучитьПредставлениеКИВыделен(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресФактический);
	КонтрагентАдресПочтовый 	= киПолучитьПредставлениеКИВыделен(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
	КонтрагентАдресЮридический 	= киПолучитьПредставлениеКИВыделен(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	Если ЗначениеЗаполнено(КонтрагентАдресФактический) Тогда
		КонтрагентАдрес = КонтрагентАдресФактический;	
	ИначеЕсли ЗначениеЗаполнено(КонтрагентАдресПочтовый) Тогда
		КонтрагентАдрес = КонтрагентАдресПочтовый;
	ИначеЕсли ЗначениеЗаполнено(КонтрагентАдресЮридический) Тогда
		КонтрагентАдрес = КонтрагентАдресЮридический;
	Иначе
		Ошибка = "Не заполнен адрес контрагента";
		Возврат Ложь;
	КонецЕсли;
	
	VINАвтомобиля = ЗаказНаряд.Автомобиль.VIN;
	Если НЕ ЗначениеЗаполнено(ЗаказНаряд.ДатаМашинозаезда) Тогда
		Ошибка = "Не заполнена Дата машинозаезда в ЗН";
		Возврат Ложь;
	Иначе
		ДатаПосещения = Формат(ЗаказНаряд.ДатаМашинозаезда,"ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаказНаряд.Автомобиль.Модель) Тогда
		ИдМодели = НайтиВыгруженныйОбъект(ЗаказНаряд.Автомобиль.Модель);
		Если Не ЗначениеЗаполнено(ИдМодели) Тогда
			Ошибка = "Не найден ID модели " + ЗаказНаряд.Автомобиль.Модель;
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Ошибка = "Не заполнена модель автомобиля" + ЗаказНаряд.Автомобиль;
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаказНаряд.Автомобиль.Модель.Родитель) Тогда
		ИдБренда = НайтиВыгруженныйОбъект(ЗаказНаряд.Автомобиль.Модель.Родитель);
		Если Не ЗначениеЗаполнено(ИдБренда) Тогда
			Ошибка = "Не найден ID бренда " + ЗаказНаряд.Автомобиль.Модель.Родитель;
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Ошибка = "Не заполнен бренд (родитель модели) автомобиля" + ЗаказНаряд.Автомобиль;
		Возврат Ложь;
	КонецЕсли;
		
	//Структура согласия
	СтруктураСогласия = новый Структура;
	СтруктураСогласия.Вставить("first_name",			СокрЛП(Контрагент.Имя));
	СтруктураСогласия.Вставить("last_name",			СокрЛП(Контрагент.Фамилия));
	СтруктураСогласия.Вставить("middle_name",		СокрЛП(Контрагент.Отчество));
	СтруктураСогласия.Вставить("gender",			ПолКонтрагента);
	СтруктураСогласия.Вставить("phone",				ТелефонКонтрагента);
	СтруктураСогласия.Вставить("phoneType",			ТипТелефонаКонтрагента);
	СтруктураСогласия.Вставить("email",				ПочтаКонтрагента);
	СтруктураСогласия.Вставить("birthdayAt",		ДатаРожденияСтрокой);
	СтруктураСогласия.Вставить("passportType",		1);
	СтруктураСогласия.Вставить("passportSeries",	СтрЗаменить(СтруктураПаспорта.Серия," ",""));
	СтруктураСогласия.Вставить("passportNumber",	СтруктураПаспорта.Номер);
	СтруктураСогласия.Вставить("passportIssueDate",	Формат(Дата(СтруктураПаспорта.ДатаВыдачи),"ДФ=dd.MM.yyyy"));
	СтруктураСогласия.Вставить("passportAuthority",	СтруктураПаспорта.КемВыдан);
	//СтруктураСогласия.Вставить("postal_code",		СокрЛП(ТелефонКонтрагента));
	СтруктураСогласия.Вставить("address",			КонтрагентАдрес);
	СтруктураСогласия.Вставить("role",				1);
	СтруктураСогласия.Вставить("type",				2);
	СтруктураСогласия.Вставить("vin",				VINАвтомобиля);
	СтруктураСогласия.Вставить("date",				ДатаПосещения);
	СтруктураСогласия.Вставить("brand_id", ИдБренда);
	СтруктураСогласия.Вставить("model_id", ИдМодели);
	
	Данные = ОтправитьСогласие(СтруктураСогласия);
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		Возврат Данные.id;
	Иначе
		Возврат Данные;
	КонецЕсли;
	
Конецфункции
Функция ОтправитьСогласие(СтруктураСогласия, Ошибка=Неопределено, ТелоОтвета=Неопределено) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
			
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураСогласия);
	СтрокаДанных = ЗаписьJSON.Закрыть();
	
	Запрос = Новый HTTPЗапрос("/api/personal-data-agreement/event", Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаДанных, КодировкаТекста.UTF8);
	
    Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос); 

	Если Ответ.КодСостояния >= 200 и Ответ.КодСостояния < 400 Тогда 
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		МассивДанных = ПрочитатьJSON(Чтение);
		Возврат МассивДанных;
	Иначе	
		Ошибка = "Код ответа при отправке согласия " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Возврат Ложь;
	КонецЕсли;
	
Конецфункции

Функция ПолучитьОсновнуюПочту(Контрагент) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект
	                      |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = ИСТИНА");
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Объект", Контрагент);

	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() Тогда
		Возврат Таблица[0].Представление;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции
Функция ПолучитьПаспорт(Контрагент) Экспорт
	Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПодтверждающиеДокументы.Ссылка КАК Документ,
		|	ПодтверждающиеДокументы.КемВыдан КАК ДокументКемВыдан,
		|	ПодтверждающиеДокументы.ДатаВыдачи КАК ДокументДатаВыдачи,
		|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента КАК ВидДокумента
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Владелец
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &ВидПодтверждающегоДокумента";
		Запрос.УстановитьПараметр("Владелец", Контрагент);		
		Запрос.УстановитьПараметр("ВидПодтверждающегоДокумента", Перечисления.ВидыДокументов.Паспорт);		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		Если ВыборкаДокументов.Количество()=0 Тогда
			Возврат Неопределено;
		Иначе 
			ВыборкаДокументов.Следующий();
			СтруктураПаспорта = Новый Структура();
			СтруктураПаспорта.Вставить("Серия",				СокрЛП(ВыборкаДокументов.Документ.Серия));
			СтруктураПаспорта.Вставить("Номер",				СокрЛП(ВыборкаДокументов.Документ.Номер));
			СтруктураПаспорта.Вставить("КодПодразделения",	СокрЛП(ВыборкаДокументов.Документ.НомерБланка));
			СтруктураПаспорта.Вставить("КемВыдан",			СокрЛП(ВыборкаДокументов.Документ.КемВыдан));
			СтруктураПаспорта.Вставить("ДатаВыдачи",		СокрЛП(ВыборкаДокументов.Документ.ДатаВыдачи)); 
			Возврат СтруктураПаспорта;
		КонецЕсли;
		
КонецФункции
Функция киПолучитьПредставлениеКИВыделен(Объект, ЗначениеКИ, ДобавлятьКомментарий = Ложь) Экспорт

	Представление = "";
	
	НаборЗаписейКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	НаборЗаписейКИ.Отбор.Объект.Значение      = Объект;
	НаборЗаписейКИ.Отбор.Объект.Использование = Истина;
	Если ТипЗнч(ЗначениеКИ) = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
		НаборЗаписейКИ.Отбор.Вид.Значение      = ЗначениеКИ;
		НаборЗаписейКИ.Отбор.Вид.Использование = Истина;
		НаборЗаписейКИ.Прочитать();
		Если НаборЗаписейКИ.Количество() > 0  Тогда
			Представление = НаборЗаписейКИ[0].Представление;
			Если ДобавлятьКомментарий И Не ПустаяСтрока(НаборЗаписейКИ[0].Комментарий) Тогда
				Если НаборЗаписейКИ[0].Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
					Представление = Представление + Символы.ПС + "Комментарий: " + НаборЗаписейКИ[0].Комментарий;
				Иначе
					Представление = Представление + " (" + НаборЗаписейКИ[0].Комментарий + ")";
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЗначениеКИ) = Тип("ПеречислениеСсылка.ТипыКонтактнойИнформации") Тогда
		НаборЗаписейКИ.Отбор.Тип.Значение      = ЗначениеКИ;
		НаборЗаписейКИ.Отбор.Тип.Использование = Истина;
		НаборЗаписейКИ.Прочитать();
		Представление = "";
		Для каждого Запись Из НаборЗаписейКИ Цикл
			Если Запись.ЗначениеПоУмолчанию Тогда
				Представление = Запись.Представление;
				Если ДобавлятьКомментарий И Не ПустаяСтрока(Запись.Комментарий) Тогда
					Если Запись.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
						Представление = Представление + Символы.ПС + "Комментарий: " + Запись.Комментарий;
					Иначе
						Представление = Представление + " (" + Запись.Комментарий + ")";
					КонецЕсли;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат Представление;
	
КонецФункции // киПолучитьПредставлениеКИ()
Функция ПолучитьPDFСогласия(IDСогласия, ВВидеДвоичныхДанных = Ложь) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(IDСогласия) Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	IDСогласияСтрокой = Формат(IDСогласия,"ЧДЦ=0; ЧГ=0");
	
	Попытка
		Запрос = Новый HTTPЗапрос("/api/personal-data-agreement/event/print-form/"+IDСогласияСтрокой, Заголовки);
		Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос); 
	Исключение
		Сообщить("Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку());
	КонецПопытки;

	Если Ответ.КодСостояния = 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(ТелоОтвета);
		СтруктураОтвета = ПрочитатьJSON(Чтение);
		Если СтруктураОтвета.Свойство("base64_content") Тогда
			ДвоичныеДанные = Base64Значение(СтрЗаменить(СтруктураОтвета.base64_content,"data:application/pdf;base64,",""));
			Поток  = Новый ПотокВПамяти;
			ЗаписьПотока = Новый ЗаписьДанных(Поток); 
			ЗаписьПотока.Записать(ДвоичныеДанные);
			ЗаписьПотока.Закрыть();
			Поток.Перейти(0, ПозицияВПотоке.Начало);
			PDF = Новый ДокументPDF;
			PDF.Прочитать(Поток);
			Если ВВидеДвоичныхДанных = Истина Тогда
				Возврат ДвоичныеДанные;
			Иначе
				Возврат PDF;
			КонецЕсли;
		Иначе
			Сообщить("Сервер не предоставил PDF: " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку());
			Возврат Ложь;
		КонецЕсли;
	Иначе	
		Сообщить("Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку());
		Возврат Ложь;
	КонецЕсли;
КонецФункции
Функция ЗагрузитьСКАНСогласия(IDСогласия, СканBase64, ИмяФайла) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(IDСогласия) Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	IDСогласияСтрокой = Формат(IDСогласия,"ЧДЦ=0; ЧГ=0");
	
	СтруктураДанных = новый Структура;
	СтруктураДанных.Вставить("file",СканBase64);
	СтруктураДанных.Вставить("file_name",ИмяФайла);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураДанных);
			
	СтрокаДанных = ЗаписьJSON.Закрыть();
	
	Попытка
		Запрос = Новый HTTPЗапрос("/api/personal-data-agreement/document/upload", Заголовки);
		Запрос.УстановитьТелоИзСтроки(СтрокаДанных, КодировкаТекста.UTF8);
		Ответ = Соединение.ВызватьHTTPМетод("PUT", Запрос); 
	Исключение
		Сообщить("Ошибка запроса");
	КонецПопытки;

	Если Ответ.КодСостояния = 201 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(ТелоОтвета);
		СтруктураОтвета = ПрочитатьJSON(Чтение);
		Возврат СтруктураОтвета;
	Иначе	
		Сообщить("Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку());
		Возврат Ложь;
	КонецЕсли;
КонецФункции
Функция СоздатьСогласиеЧерезКнопку(Контрагент,ЗаказНаряд) Экспорт
	
	Если Соединение = Неопределено Тогда
		Сообщить("Не определен профиль подключения");
		Возврат Ложь;
	КонецЕсли;	
	
	КонтрагентДляПоиска = Неопределено;
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		КонтактноеЛицо = ПолучитьКонтактноеЛицо(Контрагент);
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			КонтрагентДляПоиска = КонтактноеЛицо;
		КонецЕсли;
	Иначе
		КонтрагентДляПоиска = Контрагент;
	КонецЕсли;
	//Если не заполнено контактное лицо
	Если НЕ ЗначениеЗаполнено(КонтрагентДляПоиска) Тогда
		Ошибка = "Не найдено контактное лицо для создания Согласия";
		Возврат Ложь;
	КонецЕсли;
	//Если уже создано согласие
	Если ЗначениеЗаполнено(НайтиВыгруженноеСогласиеВРегистре(КонтрагентДляПоиска)) Тогда
		Возврат НайтиВыгруженноеСогласиеВРегистре(КонтрагентДляПоиска);
	КонецЕсли;
	Набор = ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	Набор.Отбор.Данные1С.Установить(КонтрагентДляПоиска);
	Набор.Отбор.Тип.Установить("Согласие");
	Набор.Прочитать();
	Если Набор.Количество() = 0 тогда
		ТекущаяЗапись = Набор.Добавить();
		ТекущаяЗапись.Тип = "Согласие";
		ТекущаяЗапись.Период = ТекущаяДата();
		ТекущаяЗапись.Данные1С = КонтрагентДляПоиска;
	Иначе
		ТекущаяЗапись = Набор[0];
	КонецЕсли;
	
	IDСогласия = СоздатьНовоеСогласие(КонтрагентДляПоиска,ЗаказНаряд,Ошибка);
	//Если ошибка создания
	Если IDСогласия = Ложь Тогда
		ТекущаяЗапись.Комментарий = Ошибка;
		Набор.Записать();
		Сообщить(Ошибка);
		Возврат Ложь;
	Иначе
		ТекущаяЗапись.Комментарий = "";
		ТекущаяЗапись.ID = IDСогласия;
		ТекущаяЗапись.ДатаВыгрузки = ТекущаяДатаСеанса();
		Набор.Записать();
		Возврат IDСогласия;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область ЖурналЗаписиНаСервис
Процедура ЗаполнитьДопСвойстваДляПроверкиЗнР(Ссылка, ДопСвойства, ТабличнаяЧастьПланированиеОбъекта = Неопределено) Экспорт
	
	// Проверим что мы вообще выгружаем заявки на ремонт через данный инфообмен
	Если НЕ ПолучитьВыгружатьЛиЗаписиНаРемонт() Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим что ссылка это заявка на ремонт
	Если ТипЗнч(Ссылка) <> Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		Возврат;
	КонецЕсли;
	//

	//Проверим хозоперацию
	Если Ссылка.ХозОперация.Наименование <> "План ремонта" Тогда
		Возврат;
	КонецЕсли;
	//

	//Проверим что таблица планирования заполнена
	Если Ссылка.Планирование.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	//
	
	//Проверим что все записи планирования для будущего
	//Для каждого СтрокаПланирования Из Ссылка.Планирование цикл  
	//	Если СтрокаПланирования.НачалоВыполнения  <= ТекущаяДатаСеанса() Тогда 
	//		Возврат;
	//	КонецЕсли;
	//КонецЦикла;
	//
	
	//Создадим структуру для хранения данных
	СтруктураДанныхПередЗаписью = Новый Структура;

	//Получим массив реквизитов для проверки
	МассивРеквизитов = ПолучитьКонстантноеЗначение("МассивРеквизитовДляПроверкиЗнР");

	//Заполним структуру данными из заявки на ремонт
	Для Каждого Реквизит Из МассивРеквизитов Цикл
		// проверим что такой реквизит существует
		Попытка
			МетаданныеСтандартногоРеквизита = Ссылка.Метаданные().СтандартныеРеквизиты[Реквизит];
		Исключение
			МетаданныеСтандартногоРеквизита = Неопределено;
		КонецПопытки;
		МетаданныеРеквизита = Ссылка.Метаданные().Реквизиты.Найти(Реквизит);
		МетаданныеТЧ = Ссылка.Метаданные().ТабличныеЧасти.Найти(Реквизит);
		
		Если МетаданныеРеквизита <> Неопределено ИЛИ МетаданныеСтандартногоРеквизита <> Неопределено Тогда
			// Заполним значение в структуру если это реквизит
			СтруктураДанныхПередЗаписью.Вставить(Реквизит, Ссылка[Реквизит]);
		ИначеЕсли МетаданныеТЧ <> Неопределено Тогда
			// Иначе если это табличная часть, то выгрузим ее
			СтруктураДанныхПередЗаписью.Вставить(Реквизит, Ссылка[Реквизит].Выгрузить());
		Иначе
			// Если не существует, то пропустим
			Продолжить;
		КонецЕсли;

	КонецЦикла;

	//Если реквизиты заполнили, то сохраним их в доп свойства
	Если СтруктураДанныхПередЗаписью.Количество() > 0 Тогда
		ДопСвойства.Вставить("ДанныеДляПроверкиЗнР" + ПолучитьНаименованиеИнфообмена(), СтруктураДанныхПередЗаписью);
	КонецЕсли;
	
	//Если указана опция для создания автоматической выдачи и передана табличная часть планирования то создадим выдачу если ее нет
	Если ПолучитьКонстантноеЗначение("АвтоматическиПланироватьВыдачуАвтомобиля") = Истина
		И Тип(ТабличнаяЧастьПланированиеОбъекта) = тип("ДокументТабличнаяЧасть.ЗаявкаНаРемонт.Планирование") Тогда
		ВремяВыдачиАвтомобиля = ПолучитьКонстантноеЗначение("ВремяПланируемойВыдачиАвтомобиля");
		// Если константное значение не заполнено то укажем по умолчанию 10 мин.
		Если НЕ ЗначениеЗаполнено(ВремяВыдачиАвтомобиля) Тогда
			ВремяВыдачиАвтомобиля = 600; // Значение по умолчанию
		КонецЕсли;
		// Проверим есть ли выдача
		// Определим автоработы приемки и выдачи
		АвтоработаПриемки = ПолучитьКонстантноеЗначение("АвтоработаПриемаАвтомобиля");
		АвтоработаВыдачи = ПолучитьКонстантноеЗначение("АвтоработаВыдачиАвтомобиля"); 
		Если АвтоработаПриемки = Справочники.Автоработы.ПустаяСсылка() ИЛИ АвтоработаВыдачи = Справочники.Автоработы.ПустаяСсылка() Тогда
			Ошибка = "Не указана авторабота приемки или выдачи. Проверьте константы инфообмена.";
			Возврат;
		КонецЕсли;
		// Найдем приемку и выдачу
		МассивСтрокПриемки = Ссылка.Планирование.НайтиСтроки(Новый Структура("Авторабота", АвтоработаПриемки));
		МассивСтрокВыдачи = Ссылка.Планирование.НайтиСтроки(Новый Структура("Авторабота", АвтоработаВыдачи));
		// Если есть приемка и нет выдачи то добавим ее
		Если МассивСтрокПриемки.Количество() И МассивСтрокВыдачи.Количество() = 0 Тогда
			// Выясним время окончания всех работ
			ТаблВремениОкончания = Ссылка.Планирование.Выгрузить(,"ОкончаниеВыполнения");
			ТаблВремениОкончания.Сортировать("ОкончаниеВыполнения Убыв");
			ВремяОкончанияВсехРабот = ТаблВремениОкончания.Получить(0).ОкончаниеВыполнения;
			// Выясним рабочее место приемки и исполнителя. Исполнитель не обязателен.
			РабочееМестоПриемки = Неопределено;
			ИсполнительПриемки = Неопределено;
			Для Каждого СтрокаПриемки из МассивСтрокПриемки Цикл
				Если ЗначениеЗаполнено(СтрокаПриемки.РабочееМесто) Тогда
					РабочееМестоПриемки = СтрокаПриемки.РабочееМесто;
					ИсполнительПриемки = СтрокаПриемки.Исполнитель;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			// Добавим выдачу если нашли место приемки
			Если РабочееМестоПриемки <> Неопределено И РабочееМестоПриемки <> Справочники.Сотрудники.ПустаяСсылка() Тогда
				СтрокаВыдачи = ТабличнаяЧастьПланированиеОбъекта.Добавить();
				СтрокаВыдачи.Авторабота = АвтоработаВыдачи;	
				СтрокаВыдачи.РабочееМесто = РабочееМестоПриемки;
				СтрокаВыдачи.Исполнитель = ИсполнительПриемки;
				СтрокаВыдачи.НачалоВыполнения = ВремяОкончанияВсехРабот;
				СтрокаВыдачи.ОкончаниеВыполнения = СтрокаВыдачи.НачалоВыполнения + ВремяВыдачиАвтомобиля;
				СтрокаВыдачи.Продолжительность = Окр((СтрокаВыдачи.ОкончаниеВыполнения - СтрокаВыдачи.НачалоВыполнения)/3600,2, РежимОкругления.Окр15как20);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//

КонецПроцедуры
#КонецОбласти

ВосстановитьНастройки();
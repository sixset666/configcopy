Перем БД_ТС Экспорт;                                     // dbf - таблица соответствия
Перем ВремБДОбъектов Экспорт;                            // временная база объектов (dbf)
Перем ВремБДДоков Экспорт;                               // временная база Документов (dbf)
Перем ВремБДТаблиц Экспорт;                              // dbf таблицы (временные таблицы обрабатываемых справочников и документов)
Перем XMLФорматВыгрузки Экспорт;                         // флаг выбора выгрузки в формат XML
Перем ФайлВыгрузкиИзV8 Экспорт;                          // путь к файлу для выгрузки из 8 версии
Перем ФайлЗагрузки Экспорт;                              // файл загрузки
Перем ФайлЗагрузкиИсходный Экспорт;                      // файл загрузки
Перем тзМД Экспорт;                                      // таблица значений структуры метаданных выгруженных объектов
Перем тзОбъектов Экспорт;                                // таблица значений объектов
Перем ТекСтруктура Экспорт;                              // текущая структура
Перем ТекРеквизиты Экспорт;                              // списки значений структуры, реквизитов, табличной части текущего объекта (обрабатываемого в данный момент)
Перем ПредТипОбъекта Экспорт;                            // предыдущий тип объекта
Перем ВидОбъекта Экспорт;                                // предыдущий тип обработанного объекта, текущий вид обрабатываемого объекта
Перем ИмяОбработки Экспорт;                              // имя обработки
Перем ПредыдущееИмяФайла Экспорт;                        // предыдущее имя файла
Перем ЕстьПроцедураЗагрузки Экспорт;                     // флаг наличия процедуры загрузки
Перем ИмяМенеджераОбъекта Экспорт;                       // имя менеджера объекта
Перем ПереместитьЗапись Экспорт;                         // переместить запись
Перем ИдентификаторИБИсточника Экспорт;                  // идентификатор информационной базы источника
Перем ТаблицаОтчета Экспорт;                             // таблица отчета
Перем НачалоРаботы Экспорт;                              // начало работы
Перем СтрокаЗагрузки Экспорт;                            // строка загрузки
Перем кУР Экспорт;                                       // Признак того, что конфигурация Управление рестораном
Перем ЕстьПолеUpdate Экспорт;                            // флаг наличия поля update
Перем ЕстьПолеDirect Экспорт;                            // флаг наличия поля direct
Перем ИдентификаторПартииОтрицательныхОстатков Экспорт;           //Идентификатор партии отрицательных остатков конфигурации источника;
Перем ИдентификаторПартииОтрицательныхОстатковДрСкладов Экспорт;  //Идентификатор партии отрицательных остатков для других складов. (Используется в перемещении только в УР)
Перем СпособОценкиМПЗ Экспорт;                                    //стратегия списания партий конфигурации источника. Может принимать значение "Средняя", "ФИФО", "ЛИФО".
Перем БылоСозданиеНовогоПоля;                            // флаг создания нового поля
Перем ВыбОрганизацияИсточник Экспорт;                    // выбранная организация источник
Перем ВыбОрганизацияПриемник Экспорт;                    // выбранная организация приемник
Перем ВидУчета Экспорт;                                  // Содержит значение переключателя вида учета при выгрузке.
Перем СоответствиеМенеджеров;                            // соотвецтвие менеджеров
Перем СоответствиеМенеджеровСправочник;                  // Соответствие менеджеров справочников.
Перем флЗагрузкаИзБП2 Экспорт;                           //флаг загрузки данных из "Бухгалтерии КОРП" или "Бухгалтерии 2.0"
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура удаляет из строки <Стр> символы,
// встретившиеся в переменной <УдаляемыеСимволы>
Функция УбратьСимволы(Знач Стр, УдаляемыеСимволы) Экспорт
	
	Для к = 1 по СтрДлина(УдаляемыеСимволы) Цикл
		Стр = СтрЗаменить(Стр, Сред(УдаляемыеСимволы, к, 1), "");
	КонецЦикла;
	
	Возврат Стр;
	
КонецФункции // УбратьСимволы()

// Функция возвращает строку, в которой спецсимволы,
// заменены на используемые в ИБ-источнике символы
//
// ====================================+
//         Порядок замены              |
//   спецсимвол  |    символ замены    |
// ==============+=====================|
//       ¤       |   двойная кавычка   |
// --------------+---------------------|
//       †       |      запятая        |
// --------------+---------------------|
//       ‡       |  разделитель строк  |
// ====================================+
Функция ОчиститьСпецСимволы(Стр) Экспорт

	Если ТипЗнч(Стр) = Тип("Число") Тогда Возврат Стр; КонецЕсли;

	Возврат СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(Стр, "¤", """"), "†", ","), "‡", Символы.ПС),"±",Символы.ВК);
	
КонецФункции	//	ОчиститьСпецСимволы()

// Приводит значение к нужному типу
// Параметры:
//	ТекЗначение - переменная для приведения
//	НужныйТип - тип приведения
//Возвращает Истина или Ложь взависимости от успеха приведения
Функция ПривестиКТипу(ТекЗначение, НужныйТип) Экспорт
	
	ЗнВозврата = ТекЗначение;
	
	Если НужныйТип = "Число" Тогда
		Попытка ЗнВозврата = Число(ТекЗначение);
		Исключение ЗнВозврата = 0; КонецПопытки;
	
	ИначеЕсли НужныйТип = "Строка" Тогда
		ЗнВозврата = ОчиститьСпецСимволы(ТекЗначение);
	
	ИначеЕсли НужныйТип = "Дата" Тогда
		Если ПустаяСтрока(СтрЗаменить(СтрЗаменить(ТекЗначение, ".", ""), """", "")) Тогда
			ЗнВозврата = Дата('00000000');
		
		ИначеЕсли ФайлВыгрузкиИзV8 Тогда
			ЗнВозврата = Дата(ТекЗначение);
		
		Иначе
			ЗнВозврата = СтрЗаменить(ЗнВозврата, ".", "");
			ЗнВозврата = ?(Число(Прав(ЗнВозврата, 2)) < 6, "20", "19") + Прав(ЗнВозврата, 2) + Сред(ЗнВозврата, 3, 2) + Лев(ЗнВозврата, 2);
			ЗнВозврата = Дата(ЗнВозврата + "000000");
		КонецЕсли;
		
		ИначеЕсли НужныйТип = "Булево" Тогда
				Если НРег(ТекЗначение) = "истина" ИЛИ НРег(ТекЗначение) = "да" ИЛИ НРег(ТекЗначение) = "ложь" ИЛИ НРег(ТекЗначение) = "нет" Тогда
					ЗнВозврата = ?(НРег(ТекЗначение) = "истина" ИЛИ НРег(ТекЗначение) = "да", Истина, Ложь);
			
		Иначе
			Попытка ЗнВозврата = Число(ТекЗначение);
			Исключение ЗнВозврата = 0; КонецПопытки;
			
			Если ЗнВозврата = 1 Тогда ЗнВозврата = Истина;
			Иначе ЗнВозврата = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗнВозврата;
	
КонецФункции	//	ПривестиКТипу()

// Функция возвращает приведенное к требуемому типу <НужныйТип>,
// строковое значение <ТекЗначение>
Функция ПреобразоватьЗначение(ТекЗначение, НужныйТип, Периодический=Ложь)
	
	ЗнВозврата = ТекЗначение;
	
	Если Периодический Тогда
		Периодика = Новый Соответствие();
		
		Если XMLФорматВыгрузки Тогда
			Если ТипЗнч(ТекЗначение) = Тип("Соответствие") Тогда
				Для Каждого ТекЗн Из ТекЗначение Цикл
					Периодика.Вставить(ТекЗн.Ключ, ПривестиКТипу(ТекЗн.Значение, НужныйТип));
				КонецЦикла;
			КонецЕсли;
		
		Иначе	
			
			спПериодика = СписокИзСтрокиСРазделителями(СтрЗаменить(СтрЗаменить(ТекЗначение, "¤", """"), "_", ","));
			Для r = 0 по спПериодика.Количество() - 1 Цикл
				ЗнСписка = СтрЗаменить(спПериодика.Получить(r).Значение, """", "");
				
				Поз = Найти(ЗнСписка, "|");
				Если Поз > 0 Тогда
					Периодика.Вставить(ПривестиКТипу(Сред(ЗнСписка, Поз + 1), "Дата"), ПривестиКТипу(Лев(ЗнСписка, Поз - 1), НужныйТип));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ЗнВозврата = Периодика;
		
	Иначе
		ЗнВозврата = ПривестиКТипу(ТекЗначение, НужныйТип);
	КонецЕсли;

	Возврат ЗнВозврата;
	
КонецФункции	//	ПреобразоватьЗначение()

// Функция возвращает список значений, полученный из строки с разделителями
Функция СписокИзСтрокиСРазделителями(Знач Стр)
	
	ЗнВозврата = Новый СписокЗначений();
	Если СтрЧислоВхождений(Стр, ",") = 0 Тогда
		ЗнВозврата.Добавить(Стр);
	
	Иначе
		
		Стр = СтрЗаменить(Стр, "¤,¤", "¤_¤");
		
		Для к = 1 по СтрЧислоВхождений(Стр, ",") Цикл
			Поз = Найти(Стр, ",");
			ЗнВозврата.Добавить(СтрЗаменить(Лев(Стр, Поз - 1), """", ""));
			
			Стр = Сред(Стр, Поз + 1);
		КонецЦикла;	
		
		ЗнВозврата.Добавить(СтрЗаменить(Стр, """", ""));
	КонецЕсли;
	
	Возврат ЗнВозврата;
	
КонецФункции	//	СписокИзСтрокиСРазделителями()

// Убирает из строки разделители и нечитаемые символы
// Параметры:
//	Стр - строка для разбора
//	ЭтоОбъект - флаг того, что переменная не строкового типа, а объектного
//
// Возвращает разобранную строку
//
Функция РазобратьСтроку(Знач Стр, ЭтоОбъект=Ложь) Экспорт
	
	Стр = СокрЛП(Стр);
	
	Если ЭтоОбъект Тогда
		ЗнВозврата = СписокИзСтрокиСРазделителями(Стр);
		
	Иначе	
	    ЗнВозврата = Новый СписокЗначений();
		Для к = 1 по СтрЧислоВхождений(Стр, "{") Цикл
			Поз = Найти(Стр, "}");
			СтрОбработки = Лев(Стр, Поз - 1);
			
			ЗначенияСписка = СписокИзСтрокиСРазделителями(УбратьСимволы(СтрОбработки, "{}"""));
			ЗнВозврата.Добавить(ЗначенияСписка);
			
			Стр = Сред(Стр, Поз + 1);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЗнВозврата;
	
КонецФункции	//	РазобратьСтроку()

// Функция проверяет файл выгрузки на корректность формата и версии
// Возвращает: Ложь - Ошибка формата
//             Истина - Все ОК

#Если Клиент Тогда
	
// Проверяет файл на возможность чтения и сообщает об ощибках
// Параметры:
//	ИмяФайлаПроверки - имя файла проверки
//
// Возвращает Истина если все в порядке, иначе Ложь
//
Функция ПроверитьФайлЗагрузки(ИмяФайлаПроверки) Экспорт
	
	Состояние("Ждите. Идет проверка формата файла ...");
	Если ПустаяСтрока(ИмяФайлаПроверки) Тогда
		Сообщить("Не указано имя файла загрузки!", СтатусСообщения.ОченьВажное);
		
		Возврат Ложь;
	КонецЕсли;
	
	ИдентификаторИБИсточника   = "";
	СтрокаОписанияКонфигурации = "";
	XMLФорматВыгрузки = Ложь;
	ФайлВыгрузкиИзV8  = Истина;	
	ЗаголовокВыгрузки = "";
	ВидУчета = "";
	
	ФайлЗагрузки = Новый Файл(ИмяФайлаПроверки);
	ФайлЗагрузкиИсходный = Новый Файл(ИмяФайлаПроверки);
	Если НРег(ФайлЗагрузки.Расширение) = ".xml" Тогда
		XMLФорматВыгрузки = Истина;
		
		Попытка
			ФайлЗагрузки = Новый ЧтениеXML();
			ФайлЗагрузки.ОткрытьФайл(ИмяФайлаПроверки);
			
			ФайлЗагрузки.Прочитать();			
		
			НачалоПериодаВыгрузки = ФайлЗагрузки.ПолучитьАтрибут("НачалоПериода");
			КонецПериодаВыгрузки  = ФайлЗагрузки.ПолучитьАтрибут("КонецПериода");
			Если ПустаяСтрока(НачалоПериодаВыгрузки) и ПустаяСтрока(КонецПериодаВыгрузки) Тогда
				ТекстПериодаВыгрузки  = "за все существующие периоды";
				
			ИначеЕсли ПустаяСтрока(НачалоПериодаВыгрузки) Тогда
				ТекстПериодаВыгрузки  = "с начала ведения учета по " + КонецПериодаВыгрузки;
				
			ИначеЕсли ПустаяСтрока(КонецПериодаВыгрузки) Тогда
				ТекстПериодаВыгрузки  = "с " + НачалоПериодаВыгрузки;
				
			Иначе
				ТекстПериодаВыгрузки  = "с " + НачалоПериодаВыгрузки + " по " + КонецПериодаВыгрузки;
				
			КонецЕсли;
			
			ИдентификаторИБИсточника = ФайлЗагрузки.ПолучитьАтрибут("Конфигурация");			
			флЗагрузкаИзБП2				= ?(Найти(ВРег(ИдентификаторИБИсточника),"КОРП")>0,1,?(Найти(ВРег(ИдентификаторИБИсточника),"РЕДАКЦИЯ 2.0")>0 ИЛИ Найти(ВРег(ИдентификаторИБИсточника),"РЕДАКЦИЯ 3.0")>0,2,0));
			СтрокаОписанияКонфигурации = "Конфигурация: <" + ИдентификаторИБИсточника + ">, файл создан: " + ФайлЗагрузки.ПолучитьАтрибут("ДатаВыгрузки") + ?(ФайлВыгрузкиИзV8, "", " " + ФайлЗагрузки.ПолучитьАтрибут("ВремяВыгрузки")) + ". Период выгрузки: <" + ТекстПериодаВыгрузки + ">";
			
			Если флЗагрузкаИзБП2>0 Тогда
				СоответствиеМенеджеров.Удалить("ПлатежноеПоручениеВходящее");
				СоответствиеМенеджеров.Удалить("ПлатежноеПоручениеИсходящее");
				СоответствиеМенеджеров.Вставить("ПоступлениеНаРасчетныйСчет",	"Выписка");
				СоответствиеМенеджеров.Вставить("СписаниеСРасчетногоСчета",		"Выписка");
			Иначе
				СоответствиеМенеджеров.Удалить("ПоступлениеНаРасчетныйСчет");
				СоответствиеМенеджеров.Удалить("СписаниеСРасчетногоСчета");
				СоответствиеМенеджеров.Вставить("ПлатежноеПоручениеВходящее",	"Выписка");
				СоответствиеМенеджеров.Вставить("СписаниеСРасчетногоСчета",		"Выписка");
			КонецЕсли;			
			
			ВыбОрганизацияИсточник = ФайлЗагрузки.ПолучитьАтрибут("Организация");
			ВыбОрганизацияПриемник = ФайлЗагрузки.ПолучитьАтрибут("ОрганизацияПриемника");
			Если ВыбОрганизацияПриемник = Неопределено Тогда
				ВыбОрганизацияПриемник = "";
			КонецЕсли;
			ЗаголовокВыгрузки = ФайлЗагрузки.ПолучитьАтрибут("Заголовок");
			ВидУчета = ФайлЗагрузки.ПолучитьАтрибут("ВидУчета");
		
		Исключение
			Сообщить("Не удалось прочитать данные из файла:
			         |	" + ИмяФайлаПроверки + "
					 |	Ошибка: " + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
					 
			Возврат Ложь;
		КонецПопытки;
		
	ИначеЕсли НРег(ФайлЗагрузки.Расширение) = ".txt" Тогда
		Попытка
			ФайлЗагрузки = Новый ТекстовыйДокумент();
			ФайлЗагрузки.Прочитать(ИмяФайлаПроверки);
			
			ЗначенияСтроки = РазобратьСтроку(ФайлЗагрузки.ПолучитьСтроку(1));
			ЗаголовокФайла = ЗначенияСтроки.Получить(0).Значение;
			
			
			ФайлВыгрузкиИзV8 = Истина;			
	
			НачалоПериодаВыгрузки = ЗаголовокФайла.Получить(1).Значение;
			КонецПериодаВыгрузки  = ЗаголовокФайла.Получить(2).Значение;
			
			
			Если ПустаяСтрока(СтрЗаменить(НачалоПериодаВыгрузки, ".", "")) и ПустаяСтрока(СтрЗаменить(КонецПериодаВыгрузки, ".", "")) Тогда
				ТекстПериодаВыгрузки  = "за все существующие периоды";
				
			ИначеЕсли ПустаяСтрока(СтрЗаменить(НачалоПериодаВыгрузки, ".", "")) Тогда
				ТекстПериодаВыгрузки  = "с начала ведения учета по " + КонецПериодаВыгрузки;
				
			ИначеЕсли ПустаяСтрока(СтрЗаменить(КонецПериодаВыгрузки, ".", "")) Тогда
				ТекстПериодаВыгрузки  = "с " + НачалоПериодаВыгрузки;
				
			Иначе
				ТекстПериодаВыгрузки  = "с " + НачалоПериодаВыгрузки + " по " + КонецПериодаВыгрузки;
				
			КонецЕсли;
			
			ИдентификаторИБИсточника = ОчиститьСпецСимволы(ЗаголовокФайла.Получить(0).Значение);
			
			СтрокаОписанияКонфигурации = "Конфигурация: <" + ИдентификаторИБИсточника + ">, файл создан: " + ЗаголовокФайла.Получить(1).Значение + ". Период выгрузки: <" + ТекстПериодаВыгрузки + ">";
			
			ЗаголовокВыгрузки = ЗаголовокФайла.Получить(4).Значение;
			ВыбОрганизацияИсточник = ЗаголовокФайла.Получить(5).Значение;
			ВидУчета = ЗаголовокФайла.Получить(6).Значение;
			
		Исключение
			Сообщить("Не удалось прочитать данные из файла:
			         |	" + ИмяФайлаПроверки + "
					 |	Ошибка: " + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
					 
			Возврат Ложь;
		КонецПопытки;	
				
	Иначе
		Сообщить("Не известное расширение файла!", СтатусСообщения.ОченьВажное);
		
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗаголовокВыгрузки <> "ВыгрузкаИзБухгалтерии" Тогда
		Сообщить("Неверный файл выгрузки!", СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;		
	
	Если ВыбОрганизацияИсточник = "" или ВыбОрганизацияИсточник = Неопределено Тогда
		Сообщить("Не указана организация выгрузки!",СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;	

	Возврат Истина;
	
КонецФункции	//	ПроверитьФайлЗагрузки()
#КонецЕсли

//Добавляет к таблице соответсвий новые поля, путемя создания 
//новой дбф базы данных и копировании туда всей информации.

#Если Клиент Тогда

//Добавление к таблице соотвецтвий новое поле
Процедура ДобавитьКТаблицеСоответствийНовоеПоле(СтарыйФайлТС)
	
	Если БылоСозданиеНовогоПоля Тогда 
		Сообщить("Ошибка добавления нового поля!!!");
		Возврат;
	КонецЕсли;
	БылоСозданиеНовогоПоля = Истина;
	ФайлТС = Лев(СтарыйФайлТС,СтрДлина(СтарыйФайлТС)-4)+"_New.dbf";		
	
	УдалитьФайлы(ФайлТС);
	
	НоваяТС = Новый XBase;
	ФайлНовойТС = Новый Файл(ФайлТС);
	
	НоваяТС.поля.Добавить("ID_Source",  "S", 50);     //УИД Рарус
	НоваяТС.поля.Добавить("ID_Receive", "S", 50);     //УИД Буха
	НоваяТС.поля.Добавить("Manager",    "S", 100);    //Мэнеждер Буха
	НоваяТС.поля.Добавить("DateAct",    "D", 8);
	НоваяТС.поля.Добавить("File",       "S", 20);
	НоваяТС.поля.Добавить("Update",  	  "N", 2);		
	НоваяТС.поля.Добавить("Direct",  	  "N", 2);
	НоваяТС.поля.Добавить("ManagerRar",  	  "S", 100);  //Мэнеджер Рарус

	НоваяТС.СоздатьФайл(ФайлТС);
	
	БД_ТС.Первая();
	ТекЗапись = 1;
	КоличествоЗаписей = БД_ТС.КоличествоЗаписей();	
	
	Пока ТекЗапись <= КоличествоЗаписей Цикл
		
		БД_ТС.Перейти(ТекЗапись);
		ТекЗапись = ТекЗапись + 1;
		
		НоваяТС.Добавить();
		НоваяТС.ID_Source = БД_ТС.ID_Source;
		НоваяТС.ID_Receive = БД_ТС.ID_Receive;
		НоваяТС.Manager = БД_ТС.Manager;
		НоваяТС.DateAct = БД_ТС.DateAct;
		НоваяТС.File = БД_ТС.File;		
		Если ЕстьПолеUpdate Тогда
			НоваяТС.Update = БД_ТС.Update;
		Иначе			
			//Если поле Update есть, но значение не задано, задаем его равным 1 - по умолчанию.
			НоваяТС.Update = 1;;
		КонецЕсли;		
		Если ЕстьПолеDirect Тогда
			НоваяТС.Direct = БД_ТС.Direct;
			НоваяТС.ManagerRar = БД_ТС.ManagerRar;
		Иначе			
			//Если поле Update есть, но значение не задано, задаем его равным 1 - по умолчанию.
			НоваяТС.Direct = 1;        //1 - Рарус -> Бухгалтерия, 2 - Бухгалтерия -> Рарус
			НоваяТС.ManagerRar = "";
		КонецЕсли;		
		НоваяТС.Записать();
		
	    БД_ТС.Следующая();
	КонецЦикла;  	
	
	БД_ТС.ЗакрытьФайл();
	НоваяТС.ЗакрытьФайл();
	
	КопироватьФайл(СтарыйФайлТС,СтарыйФайлТС+"old");
	УдалитьФайлы(СтарыйФайлТС);
	
	ИмяФайлаИндексовТС = СтрЗаменить(СтарыйФайлТС, ".dbf", ".cdx");	
	Если ИмяФайлаИндексовТС = СтарыйФайлТС Тогда
		ИмяФайлаИндексовТС = СтрЗаменить(СтарыйФайлТС, ".DBF", ".CDX");	
	КонецЕсли;	
	КопироватьФайл(ИмяФайлаИндексовТС,ИмяФайлаИндексовТС+"old");
	УдалитьФайлы(ИмяФайлаИндексовТС);
	
	НоваяТС.ЗакрытьФайл();
	КопироватьФайл(ФайлТС,СтарыйФайлТС);
	УдалитьФайлы(ФайлТС);
	
	ПроверитьТаблицуСоответствия(СтарыйФайлТС);
	
КонецПроцедуры
#КонецЕсли

// Функция проверяет dbf файл таблицы соответствия на корректность индексов,
// полей и принадлежности к данной информационной базе
// Возвращет: Ложь - Таблица соответствия некорректна
//           Истина - Все ОК

#Если Клиент Тогда
	
// Проверяет таблицу соотвецтвия
Функция ПроверитьТаблицуСоответствия(ФайлТС) Экспорт
	
	Если НЕ ХранитьТСВРегистре Тогда
		
		ДобавитьПоле = Ложь;
		
		// Определим существование таблицы соответствия и при необходимости создадим ее	
		ИмяФайлаИндексовТС = СтрЗаменить(ФайлТС, ".dbf", ".cdx");
		Если ФайлТС = ИмяФайлаИндексовТС Тогда
			ИмяФайлаИндексовТС = СтрЗаменить(ФайлТС, ".DBF", ".CDX");
		КонецЕсли;
		
		Попытка
			Если БД_ТС.Открыта() Тогда
				БД_ТС.ЗакрытьФайл();
			КонецЕсли;
		Исключение КонецПопытки;
		
		БД_ТС = Новый XBase;
		ФайлБД_ТС = Новый Файл(ФайлТС);
		ФайлИндексовБД_ТС = Новый Файл(ИмяФайлаИндексовТС);
		Если ФайлБД_ТС.Существует() Тогда
			Если НЕ БД_ТС.Открыта() Тогда			
				
				БД_ТС.индексы.Добавить("IndSource", "TRIM(ID_Source)", Истина);
				БД_ТС.индексы.Добавить("IndReceive", "TRIM(ID_Receive)", Ложь);
				
				БД_ТС.ОткрытьФайл(ФайлТС,, Ложь);
				Если НЕ БД_ТС.СоздатьИндексныйФайл(ИмяФайлаИндексовТС) Тогда
					Сообщить("ВНИМАНИЕ! Не удачная попытка создания индексного файла таблицы соответствия:
					|	" + ИмяФайлаИндексовТС, СтатусСообщения.ОченьВажное);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;		
			
		Иначе
			БД_ТС.поля.Добавить("ID_Source",  "S", 50); //УИД Рарус
			БД_ТС.поля.Добавить("ID_Receive", "S", 50); //УИД Бухгалтерия
			БД_ТС.поля.Добавить("Manager",    "S", 100);//Менеджер Бухгалтерия
			БД_ТС.поля.Добавить("DateAct",    "D", 8);
			БД_ТС.поля.Добавить("File",       "S", 20);
			БД_ТС.поля.Добавить("Update",  	  "N", 2);
			БД_ТС.поля.Добавить("Direct",  	  "N", 2); //1 - Рарус -> Бухгалтерия, 2 - Бухгалтерия -> Рарус
			БД_ТС.поля.Добавить("ManagerRar",  	  "S", 100);  //Менеджер Рарус				
			
			Попытка БД_ТС.СоздатьФайл(ФайлТС, ИмяФайлаИндексовТС);
			Исключение
				Сообщить("ВНИМАНИЕ! Нет возможности открыть файл таблицы соответствия: " + ФайлТС + "
				|	" + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
				
				Возврат Ложь;
			КонецПопытки;	
			
			БД_ТС.индексы.Добавить("IndSource", "ID_Source", Истина);
			БД_ТС.индексы.Добавить("IndReceive", "ID_Receive", Ложь);					
			Попытка
				БД_ТС.СоздатьИндексныйФайл(ИмяФайлаИндексовТС);
			Исключение
				Сообщить("Внимание! Нет возможности создать индексный файл: " + ИмяФайлаИндексовТС + ".
				| Возможно нет доступа к текущей папке.", СтатусСообщения.ОченьВажное);
				Возврат Ложь;
			КонецПопытки;
			
			
		КонецЕсли;
		
		Если НЕ БД_ТС.Открыта() Тогда
			Сообщить("ВНИМАНИЕ! Нет возможности открыть файл таблицы соответствия:
			|	" + ФайлТС, СтатусСообщения.ОченьВажное);
			
			Возврат Ложь;
			
		Иначе
			// Проверим, подходит ли нам выбранный файл
			ТекПоле = БД_ТС.поля.Найти("ID_Source");
			Если ТекПоле = Неопределено Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|не обнаружено поле <ID_Source>");
				Возврат Ложь;
			ИначеЕсли ТекПоле.Тип <> "S" или ТекПоле.Длина <> 50 Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|тип или длина поля <ID_Source>, не соответствует требованиям!");
				Возврат Ложь;
			КонецЕсли;
			
			ТекПоле = БД_ТС.поля.Найти("ID_Receive");
			Если ТекПоле = Неопределено Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|не обнаружено поле <ID_Receive>");
				Возврат Ложь;
			ИначеЕсли ТекПоле.Тип <> "S" или ТекПоле.Длина <> 50 Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|тип или длина поля <ID_Receive>, не соответствует требованиям!");
				Возврат Ложь;
			КонецЕсли;
			
			ТекПоле = БД_ТС.поля.Найти("Manager");
			Если ТекПоле = Неопределено Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|не обнаружено поле <Manager>");
				Возврат Ложь;
			ИначеЕсли ТекПоле.Тип <> "S" или ТекПоле.Длина <> 100 Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|тип или длина поля <Manager>, не соответствует требованиям!");
				Возврат Ложь;
			КонецЕсли;
			
			ТекПоле = БД_ТС.поля.Найти("DateAct");
			Если ТекПоле = Неопределено Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|не обнаружено поле <DateAct>");
				Возврат Ложь;
			ИначеЕсли ТекПоле.Тип <> "D" Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|тип поля <DateAct>, не соответствует требованиям!");
				Возврат Ложь;
			КонецЕсли;
			
			ТекПоле = БД_ТС.поля.Найти("File");
			Если ТекПоле = Неопределено Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|не обнаружено поле <File>");
				Возврат Ложь;
			ИначеЕсли ТекПоле.Тип <> "S" или ТекПоле.Длина <> 20 Тогда
				Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
				|тип или длина поля <File>, не соответствует требованиям!");
				Возврат Ложь;
			КонецЕсли;                                
			
			ТекПоле = БД_ТС.Поля.Найти("Update");
			Если ТекПоле = Неопределено Тогда
				ДобавитьПоле = Истина;
			КонецЕсли;					
			ТекПоле = БД_ТС.Поля.Найти("Direct");
			Если ТекПоле = Неопределено Тогда
				ДобавитьПоле = Истина;
			КонецЕсли;			
			ТекПоле = БД_ТС.Поля.Найти("ManagerRar");
			Если ТекПоле = Неопределено Тогда
				ДобавитьПоле = Истина;
			КонецЕсли;						
			
			Если Не ДобавитьПоле Тогда			
				ТекИндекс = БД_ТС.индексы.Найти("IndSource");
				Если ТекИндекс = Неопределено Тогда
					Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
					|не обнаружен индекс <IndSource>");
					Возврат Ложь;
				ИначеЕсли (ТекИндекс.Выражение <> "TRIM(ID_Source)" И ТекИндекс.Выражение <> "ID_Source") ИЛИ НЕ ТекИндекс.Уникальность Тогда
					Сообщить("ВНИМАНИЕ! В таблице соответствия: " + ФайлТС + "
					|свойства индекса <IndSource>, не соответствуют требованиям!");
					Возврат Ложь;
				КонецЕсли;	
			КонецЕсли;		
		КонецЕсли;
		
		БД_ТС.Кодировка     = КодировкаXBase.ANSI;
		БД_ТС.ТекущийИндекс = БД_ТС.индексы.Найти("IndSource");
		
		// Проверяем на соответствие идентификатору конфигурации
		ИдентификаторКонфигурации = ЗначениеВСтрокуВнутр(Метаданные);
		ИдентификаторКонфигурации = СокрЛП(УбратьСимволы(ИдентификаторКонфигурации, "{}#"","));
		
		Если БД_ТС.Найти("GUID_ReceiveRar", "=") Тогда
			Если СокрЛП(БД_ТС.ID_Receive) <> ИдентификаторКонфигурации Тогда
				Сообщить("ВНИМАНИЕ! GUID текущей конфигурации не соответствует
				|GUID-у конфигурации, используемому в таблице соответствия
				|в качестве конфигурации приемника ТОР Раруса!", СтатусСообщения.ОченьВажное);
				
				Возврат Ложь;
			КонецЕсли;
			
		Иначе
			БД_ТС.Добавить();
			БД_ТС.ID_Source  = "GUID_ReceiveRar";
			БД_ТС.ID_Receive = ИдентификаторКонфигурации;
			БД_ТС.DateAct    = ТекущаяДата();
			БД_ТС.Записать();
		КонецЕсли;	
		
		БД_ТС.ТекущийИндекс = БД_ТС.индексы.Найти("IndReceive");
		ПолеОбновления = БД_ТС.Поля.Найти("Update");
		Если ПолеОбновления = Неопределено Тогда
			ЕстьПолеUpdate = Ложь;
		Иначе
			ЕстьПолеUpdate = Истина;
		КонецЕсли;
		ПолеНаправления = БД_ТС.поля.Найти("Direct");
		Если ПолеНаправления = Неопределено Тогда
			ЕстьПолеDirect = Ложь;
		Иначе
			ЕстьПолеDirect = Истина;
		КонецЕсли;
		
		Если ДобавитьПоле Тогда
			ДобавитьКТаблицеСоответствийНовоеПоле(ФайлТС);	
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции	//	ПроверитьТаблицуСоответствия();
#КонецЕсли


#Если Клиент Тогда
	
// Процедура загружает данные из файла во временные объекты,
// содержащие структуру базы источника и загружаемые объекты	
Процедура ОбновитьДанныеВыгрузки() Экспорт

	// ВремБДОбъектов - данные выгрузки (элементы справочников), индексированные
	ВремБДОбъектов = Новый XBase;
	ВремБДОбъектов.Кодировка = КодировкаXBase.ANSI;
	ВремБДОбъектов.Поля.Добавить("Level",    "S", 1,   0);
	ВремБДОбъектов.Поля.Добавить("Type",     "S", 150, 0);
	ВремБДОбъектов.Поля.Добавить("ID",       "S", 36,  0);
	ВремБДОбъектов.Поля.Добавить("IDReceiver", "S", 36,  0);
	ВремБДОбъектов.Поля.Добавить("LineNum",  "N", 10,  0);
	ВремБДОбъектов.Поля.Добавить("ByPassed", "N", 2,   0);
	ВремБДОбъектов.Индексы.Добавить("IndID",    "TRIM(ID)",   Ложь);
	ВремБДОбъектов.Индексы.Добавить("IndLevel", "Level+Type", Ложь);
		
	ИмяФайлаВремБДОбъектов = КаталогВременныхФайлов() + "TempRef";
	
	// ВремБДДоков - данные выгрузки (документы), индексированные
	ВремБДДоков = Новый XBase;
	ВремБДДоков.Кодировка = КодировкаXBase.ANSI;
	ВремБДДоков.Поля.Добавить("Status",   "S", 1,   0);
	ВремБДДоков.Поля.Добавить("Time",     "S", 14,  0);
	ВремБДДоков.Поля.Добавить("Type",     "S", 150, 0);
	ВремБДДоков.Поля.Добавить("ID",       "S", 36,  0);
	ВремБДДоков.Поля.Добавить("IDReceiver", "S", 36,  0);
	ВремБДДоков.Поля.Добавить("LineNum",  "N", 10,  0);
	ВремБДДоков.Поля.Добавить("ByPassed", "N", 1,   0);
	ВремБДДоков.Индексы.Добавить("IndID",   "TRIM(ID)",  Ложь);
	ВремБДДоков.Индексы.Добавить("IndTime", "Time+Type", Ложь);
		
	ИмяФайлаВремБДДоков = КаталогВременныхФайлов() + "TempDoc";
	
	// ВремБДТаблиц - номера строк файла выгрузки табличных частей объектов
	ВремБДТаблиц = Новый XBase;
	ВремБДТаблиц.Кодировка = КодировкаXBase.ANSI;
	ВремБДТаблиц.Поля.Добавить("ID",      "S", 36,  0);      
	ВремБДТаблиц.Поля.Добавить("TabName", "S", 150, 0);      
	ВремБДТаблиц.Поля.Добавить("LineNum", "N", 10,  0);
	ВремБДТаблиц.Индексы.Добавить("IndTabName", "TRIM(ID)+TRIM(TabName)", Ложь);
	
	ИмяФайлаВремБДТаблиц = КаталогВременныхФайлов() + "TempTab";
	
	// на всякий случай, прибъем временные файлы если они еще остались
	УдалитьФайлы(ИмяФайлаВремБДОбъектов + ".dbf");
	УдалитьФайлы(ИмяФайлаВремБДОбъектов + ".cdx");
	УдалитьФайлы(ИмяФайлаВремБДДоков    + ".dbf");
	УдалитьФайлы(ИмяФайлаВремБДДоков    + ".cdx");
	УдалитьФайлы(ИмяФайлаВремБДТаблиц   + ".dbf");
	УдалитьФайлы(ИмяФайлаВремБДТаблиц   + ".cdx");
	
	ВремБДОбъектов.СоздатьФайл(ИмяФайлаВремБДОбъектов, ИмяФайлаВремБДОбъектов); 
	ВремБДДоков.СоздатьФайл(ИмяФайлаВремБДДоков, ИмяФайлаВремБДДоков+".cdx"); 
	ВремБДТаблиц.СоздатьФайл(ИмяФайлаВремБДТаблиц, ИмяФайлаВремБДТаблиц); 
	
	// Проверим, вдруг не смогли открыть dbf-ы?
	Если НЕ ВремБДОбъектов.Открыта() или НЕ ВремБДДоков.Открыта() или НЕ ВремБДТаблиц.Открыта() Тогда
		Сообщить("Нет возможности открыть временные таблицы!", СтатусСообщения.ОченьВажное);
		
		Возврат;
	КонецЕсли;	
	
	// Установим текущие индексы
	ВремБДОбъектов.ТекущийИндекс = ВремБДОбъектов.индексы.Найти("IndLevel");
	ВремБДДоков.ТекущийИндекс = ВремБДДоков.индексы.INDTIME;//ВремБДДоков.индексы.Найти("IndTime");
	ВремБДТаблиц.ТекущийИндекс = ВремБДТаблиц.индексы.Найти("IndTabName");
	
	// тзМД - структура метаданных базы-источника
	тзМД = Новый ТаблицаЗначений;
	тзМД.Колонки.Добавить("Метаданные"); // идентификатор объекта
	тзМД.Колонки.Добавить("Основные");   // основные характеристики объекта
	тзМД.Колонки.Добавить("Реквизиты");  // описание реквизитов элементов или шапки документов
	тзМД.Колонки.Добавить("Таблица");    // описание реквизитов табличной части документа

	Если XMLФорматВыгрузки Тогда
		тзОбъектов = Новый ТаблицаЗначений();
		тзОбъектов.Колонки.Добавить("Идентификатор");
		тзОбъектов.Колонки.Добавить("ИдентификаторОбъекта");
		тзОбъектов.Колонки.Добавить("Реквизиты");
		тзОбъектов.Колонки.Добавить("Строки");
		
		ОбъектОткрыт        = Ложь;
		ЭтоРеквизитыТаблицы = Ложь;
		ЭтоСтруктураОбъекта = Ложь;
		НаименованиеТаблицы = "";
		
		Пока ФайлЗагрузки.Прочитать() Цикл
			Если Строка(ФайлЗагрузки.ТипУзла) = "Конец элемента" Тогда
				Если ФайлЗагрузки.Имя = "Объект" Тогда
					ОбъектОткрыт         = Ложь;
					ЭтоРеквизитыТаблицы  = Ложь;
					ИдентификаторОбъекта = "";
				
				ИначеЕсли ФайлЗагрузки.Имя = "Структура" Тогда
					ЭтоСтруктураОбъекта = Ложь;
				КонецЕсли;
				
				Продолжить;
			КонецЕсли;
			
			Если ФайлЗагрузки.Имя = "Справочники" или ФайлЗагрузки.Имя = "Ссылки" Тогда
				ТипОбъекта = "Справочник";
				
				Продолжить;
				
			ИначеЕсли ФайлЗагрузки.Имя = "Документы" Тогда
				ТипОбъекта = "Документ";
				
				Продолжить;
			КонецЕсли;
			
			Если ФайлЗагрузки.КоличествоАтрибутов() = 0 и НЕ ЭтоСтруктураОбъекта и НЕ ОбъектОткрыт Тогда
				ВидОбъекта          = ФайлЗагрузки.Имя;
				ЭтоРеквизитыТаблицы = Ложь;
				
				Продолжить;
			КонецЕсли;
			
			Если ФайлЗагрузки.Имя = "Таблица" и ФайлЗагрузки.КоличествоАтрибутов() = 1 Тогда
				НаименованиеТаблицы = "Таблица";
				Попытка
					НаименованиеТаблицы = ФайлЗагрузки.ПолучитьАтрибут("Наименование");
					ЭтоРеквизитыТаблицы = Истина;
				Исключение
					ЭтоРеквизитыТаблицы = Ложь;
				КонецПопытки;
				
				Продолжить;
			КонецЕсли;
			
			Если ФайлЗагрузки.Имя = "Структура" Тогда
				ЭтоСтруктураОбъекта = Истина;
				
				// зальем структуру метаданных
				СтрокаМД = тзМД.Добавить();
				СтрокаМД.Метаданные = ТипОбъекта + "." + ВидОбъекта;
				
				СистемныеПараметры = Новый Соответствие();
				СистемныеПараметры.Вставить("Идентификатор", СтрокаМД.Метаданные);
				Пока ФайлЗагрузки.ПрочитатьАтрибут() Цикл
					СистемныеПараметры.Вставить(ФайлЗагрузки.Имя, ФайлЗагрузки.Значение);
				КонецЦикла;
				
				СтрокаМД.Основные = СистемныеПараметры;
			
			ИначеЕсли ФайлЗагрузки.Имя = "Реквизит" Тогда
				// описание реквизитов объекта базы-источника
				СтрокаМД = тзМД.Найти(ТипОбъекта + "." + ВидОбъекта, "Метаданные");
				
				СтруктураРеквизита = Новый Соответствие();
				
				СтруктураРеквизитов = СтрокаМД[?(ЭтоРеквизитыТаблицы, "Таблица", "Реквизиты")];
				Если ТипЗнч(СтруктураРеквизитов) <> ТипЗнч(СтруктураРеквизита) Тогда
					СтруктураРеквизитов = Новый Соответствие();
				КонецЕсли;
				
				Пока ФайлЗагрузки.ПрочитатьАтрибут() Цикл
					СтруктураРеквизита.Вставить(?(ФайлЗагрузки.Имя = "Наименование", "Идентификатор", ФайлЗагрузки.Имя), ФайлЗагрузки.Значение);
				КонецЦикла;
				
				Если ЭтоРеквизитыТаблицы Тогда
					Если СтруктураРеквизитов.Получить(НаименованиеТаблицы) = Неопределено Тогда
						СтруктураРеквизитов.Вставить(НаименованиеТаблицы, Новый Соответствие());
					КонецЕсли;
					
					СтруктураРеквизитов[НаименованиеТаблицы].Вставить(ФайлЗагрузки.ПолучитьАтрибут("Наименование"), СтруктураРеквизита);
					СтрокаМД["Таблица"] = СтруктураРеквизитов;
					
				Иначе	
					СтруктураРеквизитов.Вставить(ФайлЗагрузки.ПолучитьАтрибут("Наименование"), СтруктураРеквизита);
					СтрокаМД["Реквизиты"] = СтруктураРеквизитов;
				КонецЕсли;
			
			ИначеЕсли ФайлЗагрузки.Имя = "Объект" Тогда
				ОбъектОткрыт = Истина;
				
				ЗначенияРеквизитов = Новый Соответствие();
				Пока ФайлЗагрузки.ПрочитатьАтрибут() Цикл
					ЗначенияРеквизитов.Вставить(ФайлЗагрузки.Имя, ФайлЗагрузки.Значение);
				КонецЦикла;
				
				СтрокаОбъекта = тзОбъектов.Добавить();
				СтрокаОбъекта.Идентификатор = ФайлЗагрузки.ПолучитьАтрибут("_1С_ИдентификаторБД");
				СтрокаОбъекта.ИдентификаторОбъекта = ФайлЗагрузки.ПолучитьАтрибут("_1С_ИдентификаторПриемника");
				СтрокаОбъекта.Реквизиты     = ЗначенияРеквизитов;
				
				// "зальем" новый элемент-документ
				Если ТипОбъекта = "Справочник" Тогда // блок для справочника
					// вводим новую строку в dbf файл
					ВремБДОбъектов.Добавить();
					ВремБДОбъектов.Level = ФайлЗагрузки.ПолучитьАтрибут("_1С_Уровень"); // Уровень
					ВремБДОбъектов.Type  = ТипОбъекта + "." + ВидОбъекта;               // Тип объекта
					ВремБДОбъектов.ID    = СтрокаОбъекта.Идентификатор;                 // ИД объекта
					ВремБДОбъектов.IDReceiver = СтрокаОбъекта.ИдентификаторОбъекта;     // ИД объекта ИБ
					ВремБДОбъектов.Записать();
				Иначе // блок для документа
					
					ДатаДокумента = ФайлЗагрузки.ПолучитьАтрибут("_1С_Дата");
					Если ПустаяСтрока(УбратьСимволы(ДатаДокумента, ".: ")) Тогда
						ДатаДокумента = Дата('00000000');
							
					ИначеЕсли НЕ ФайлВыгрузкиИзV8 Тогда
						ДатаДокумента = СтрЗаменить(ДатаДокумента, ".", "");
						ДатаДокумента = ?(Число(Прав(ДатаДокумента, 2)) < 6, "20", "19") + Прав(ДатаДокумента, 2) + Сред(ДатаДокумента, 3, 2) + Лев(ДатаДокумента, 2);
						ДатаДокумента = ДатаДокумента + СтрЗаменить(ФайлЗагрузки.ПолучитьАтрибут("_1С_Время"), ":", "");
						
					Иначе
						ДатаСтрокой = Лев(ДатаДокумента, Найти(ДатаДокумента, " ") - 1);
						Если СтрДлина(ДатаСтрокой) = 8 Тогда
							ГодСтрокой = Прав(ДатаСтрокой, 2);
							Если Число(ГодСтрокой) < 10 Тогда
								  ГодСтрокой = "20" + ГодСтрокой;
							Иначе ГодСтрокой = "19" + ГодСтрокой;
							КонецЕсли;
							ДатаДокумента = Лев(ДатаДокумента, 6) + ГодСтрокой + Сред(ДатаДокумента, 9);
						КонецЕсли;
					КонецЕсли;
                    
					ДатаДокумента = Формат(Дата(ДатаДокумента), "ДФ=yyyyMMddHHmmss");
					
					// вводим новую строку в dbf файл
					ВремБДДоков.Добавить();
					ВремБДДоков.Time = ДатаДокумента;                 // Дата и время документа
					ВремБДДоков.Type = ТипОбъекта + "." + ВидОбъекта; // Тип объекта
					ВремБДДоков.ID   = СтрокаОбъекта.Идентификатор;   // ИД объекта
					ВремБДДоков.IDReceiver = СтрокаОбъекта.ИдентификаторОбъекта; // ИД объекта ИБ
					Если ФайлЗагрузки.ПолучитьАтрибут("_1С_Уровень") = "1" Тогда  
						ВремБДДоков.Status = "1";
					Иначе
						ВремБДДоков.Status = "2";
					КонецЕсли;
					ВремБДДоков.Записать();
				КонецЕсли;
			
			ИначеЕсли ФайлЗагрузки.Имя = "Строка" Тогда
				ЗначенияРеквизитовСтроки = Новый Соответствие();
				Пока ФайлЗагрузки.ПрочитатьАтрибут() Цикл
					ЗначенияРеквизитовСтроки.Вставить(ФайлЗагрузки.Имя, ФайлЗагрузки.Значение);
				КонецЦикла;
				
				ЗначенияСтрок = СтрокаОбъекта.Строки;
				Если ТипЗнч(ЗначенияСтрок) <> Тип("Соответствие") Тогда
					ЗначенияСтрок = Новый Соответствие();
				КонецЕсли;
				Если ЗначенияСтрок.Получить(НаименованиеТаблицы) = Неопределено Тогда
					ЗначенияСтрок.Вставить(НаименованиеТаблицы, Новый СписокЗначений());
				КонецЕсли;
				
				ЗначенияСтрок[НаименованиеТаблицы].Добавить(ЗначенияРеквизитовСтроки);
				СтрокаОбъекта.Строки = ЗначенияСтрок;				
			КонецЕсли;
		КонецЦикла;
	
	Иначе	
				
		// выборку строк начинаем со второй строки, т.к. первую уже прочитали при выборе файла
		ВсегоСтрок = ФайлЗагрузки.КоличествоСтрок();
		Для к = 2 по ВсегоСтрок Цикл
			Состояние("Ждите. Идет просмотр файла ... " + к + "/" + ВсегоСтрок);
			
			// получаем строку
			Стр = ФайлЗагрузки.ПолучитьСтроку(к);
			
			// если строка пустая, то продолжим цикл
			Если ПустаяСтрока(Стр) = 1 Тогда Продолжить; КонецЕсли;
			
			Если НЕ ПустаяСтрока(Лев(Стр, 1)) Тогда // новый объект метаданных базы-источника
				ЗначенияСтроки = РазобратьСтроку(Стр).Получить(0).Значение;
				ТипВидОбъекта  = ЗначенияСтроки.Получить(0).Значение;
				
				// зальем структуру метаданных
				СтрокаМД = тзМД.Добавить();
				СтрокаМД.Метаданные = ТипВидОбъекта;
				
				СистемныеПараметры = Новый Соответствие();
				Для кк = 0 по ЗначенияСтроки.Количество() - 1 Цикл
					ЭлСписка = ЗначенияСтроки.Получить(кк);
					Если Найти(ТипВидОбъекта, "Документ.") = 0 Тогда // блок для справочника
						Если      кк = 0 Тогда Ключ = "Идентификатор";
						ИначеЕсли кк = 1 Тогда Ключ = "ДлинаКода";
						ИначеЕсли кк = 2 Тогда Ключ = "ДлинаНаименования";
						ИначеЕсли кк = 3 Тогда    Ключ = "Владельцы";
						Иначе Продолжить;	
						КонецЕсли;
						
					Иначе
						Если      кк = 0 Тогда Ключ = "Идентификатор";
						ИначеЕсли кк = 1 Тогда Ключ = "ДлинаКода";
						ИначеЕсли кк = 2 Тогда Ключ = "КонтрольУникальности";
						Иначе Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					СистемныеПараметры.Вставить(Ключ, ЭлСписка.Значение);
				КонецЦикла;
				
				СтрокаМД.Основные = СистемныеПараметры;
			
			ИначеЕсли НЕ ПустаяСтрока(Лев(Стр, 3)) Тогда // описание реквизитов объекта базы-источника
				СтрокаМД = тзМД.Найти(ТипВидОбъекта, "Метаданные");
				
				тзСтруктурыРеквизитов = Новый ТаблицаЗначений();
				тзСтруктурыРеквизитов.Колонки.Добавить("Идентификатор");
				тзСтруктурыРеквизитов.Колонки.Добавить("Значение");
				
				ЭтоРеквизитыТаблицы = ?(ПустаяСтрока(СтрокаМД.Реквизиты), Ложь, Истина);
				НаименованиеТаблицы = "Таблица";
				
				ЗначенияСтроки = РазобратьСтроку(Стр);
				Для кк = 0 по ЗначенияСтроки.Количество() - 1 Цикл
					ЭлСписка = ЗначенияСтроки.Получить(кк).Значение;
					
					Если Найти(ЭлСписка.Получить(0).Значение, "Таблица.") = 1 Тогда
						ЭтоРеквизитыТаблицы = Истина;
						НаименованиеТаблицы = Сред(ЭлСписка.Получить(0).Значение, 9);
													
						Продолжить;
					КонецЕсли;
					
					СтруктураРеквизита = Новый Соответствие();
					Для а = 0 по ЭлСписка.Количество() - 1 Цикл
						Если      а = 0 Тогда Ключ = "Идентификатор";
						ИначеЕсли а = 1 Тогда Ключ = "Тип";
						ИначеЕсли а = 2 Тогда Ключ = "Длина";
						ИначеЕсли а = 3 Тогда Ключ = "Точность";
						Иначе Продолжить;	
						КонецЕсли;
						
						СтруктураРеквизита.Вставить(Ключ, ЭлСписка.Получить(а).Значение);
					КонецЦикла;
					
					ТекСтрока = тзСтруктурыРеквизитов.Добавить();
					ТекСтрока.Идентификатор = СтруктураРеквизита["Идентификатор"];
					ТекСтрока.Значение      = СтруктураРеквизита;
				КонецЦикла;
				
				Если ЭтоРеквизитыТаблицы Тогда
					ТаблицыОбъекта = СтрокаМД["Таблица"];
					Если ТипЗнч(ТаблицыОбъекта) <> Тип("Соответствие") Тогда
						ТаблицыОбъекта = Новый Соответствие();
					КонецЕсли;
					
					ТаблицыОбъекта.Вставить(НаименованиеТаблицы, тзСтруктурыРеквизитов);
					СтрокаМД["Таблица"] = ТаблицыОбъекта;
					
				Иначе	
					СтрокаМД["Реквизиты"] = тзСтруктурыРеквизитов;
				КонецЕсли;
			
			Иначе
				
				Стр = Сред(СокрЛП(Стр), 2);
				Стр = Лев(Стр, СтрДлина(Стр) - 1);
				
				ЭтоОбъект = ?(Лев(Стр, 1) = """", Ложь, Истина);
				
				ЗначенияСтроки = РазобратьСтроку(Стр, Истина);
				
				Если ЭтоОбъект Тогда // начало нового элемента-документа
					ПредНаименованиеТаблицы = "";
					ПредИдентификаторОбъекта = "";
					
					// вот теперь пошли элементы-документы, получим уровень и ИД объекта
					УровеньВыгрузки = ЗначенияСтроки.Получить(0).Значение;
					ИдентификаторОбъекта = ЗначенияСтроки.Получить(1).Значение;
					
					// "зальем" новый элемент-документ
					Если Найти(ТипВидОбъекта, "Документ.") = 0 Тогда // блок для справочника
						// вводим новую строку в dbf файл
						ВремБДОбъектов.Добавить();
						ВремБДОбъектов.Level   = Число(УровеньВыгрузки); // Уровень
						ВремБДОбъектов.Type    = ТипВидОбъекта;          // Тип объекта
						ВремБДОбъектов.ID      = ИдентификаторОбъекта;   // ИД объекта
						//ВремБДОбъектов.IDReceiver = ИдентификаторОбъекта;     // ИД объекта ИБ
						ВремБДОбъектов.LineNum = к;                      // Номер строки в текстовом файле
						ВремБДОбъектов.Записать();
						
					Иначе // блок для документа
					    ДатаДокумента = ЗначенияСтроки.Получить(3).Значение;
						Если ПустаяСтрока(УбратьСимволы(ДатаДокумента, ".: ")) Тогда
							ДатаДокумента = Дата('00000000');
							
						ИначеЕсли НЕ ФайлВыгрузкиИзV8 Тогда
							ДатаДокумента = СтрЗаменить(ДатаДокумента, ".", "");
							ДатаДокумента = ?(Число(Прав(ДатаДокумента, 2)) < 6, "20", "19") + Прав(ДатаДокумента, 2) + Сред(ДатаДокумента, 3, 2) + Лев(ДатаДокумента, 2);
							ДатаДокумента = ДатаДокумента + СтрЗаменить(ЗначенияСтроки.Получить(4).Значение, ":", "");
						КонецЕсли;
						
						ДатаДокумента = Формат(Дата(ДатаДокумента), "ДФ=yyyyMMddHHmmss");
						
						// вводим новую строку в dbf файл
						ВремБДДоков.Добавить();
						ВремБДДоков.Time    = ДатаДокумента;        // Дата и время документа
						ВремБДДоков.Type    = ТипВидОбъекта;        // Тип объекта
						ВремБДДоков.ID      = ИдентификаторОбъекта; // ИД объекта
						ВремБДДоков.LineNum = к;                    // Номер строки в текстовом файле
						Если Число(УровеньВыгрузки) = 1 Тогда 
							ВремБДДоков.Status = "1";
						Иначе
							ВремБДДоков.Status = "2";
						КонецЕсли;							
						ВремБДДоков.Записать();
					КонецЕсли;
					
				Иначе // ну и табличные части
					СтрокаМД = тзМД.Найти(ТипВидОбъекта, "Метаданные");
					Стр = Сред(СокрЛП(Стр), 2);
					
					НаименованиеТаблицы = "Таблица";
					Если ФайлВыгрузкиИзV8 Тогда
						НаименованиеТаблицы = Сред(ЗначенияСтроки.Получить(0).Значение, 9);
						
					ИначеЕсли Найти(ТипВидОбъекта, "Документ.") = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Если ПредНаименованиеТаблицы = НаименованиеТаблицы и
						 ПредИдентификаторОбъекта = ИдентификаторОбъекта Тогда 
						Продолжить;
					КонецЕсли;
					
					ВремБДТаблиц.Добавить();
					ВремБДТаблиц.ID      = ИдентификаторОбъекта;
					ВремБДТаблиц.TabName = НаименованиеТаблицы;
					ВремБДТаблиц.LineNum = к;
					ВремБДТаблиц.Записать();
					
					ПредНаименованиеТаблицы = НаименованиеТаблицы;
					ПредИдентификаторОбъекта = ИдентификаторОбъекта;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры  // ОбновитьДанныеВыгрузки
#КонецЕсли

// Функция возвращает структуру реквизитов
// объекта метаданных базы источника
Функция ПолучитьСтруктуру(ТипОбъекта) Экспорт
	
	СтруктураОбъекта = Новый ТаблицаЗначений();
	СтруктураОбъекта.Колонки.Добавить("Идентификатор");
	СтруктураОбъекта.Колонки.Добавить("Значение");
	
	// найдем по идентификатору объекта строку с реквизитами
 	СтрокаМД = тзМД.Найти(ТипОбъекта, "Метаданные");
	Если СтрокаМД  = Неопределено Тогда Возврат СтруктураОбъекта; КонецЕсли;
	
	ТекСтрока = СтруктураОбъекта.Добавить();
	ТекСтрока.Идентификатор = "СистемныеПараметры";
	ТекСтрока.Значение      = СтрокаМД.Основные;
	
	Попытка
		// пройдемся по реквизитам
		Для Каждого ТекРекв Из СтрокаМД.Реквизиты Цикл
			ТекСтрока = СтруктураОбъекта.Добавить();
			Попытка ТекСтрока.Идентификатор = ТекРекв.Идентификатор;
			Исключение ТекСтрока.Идентификатор = ТекРекв.Ключ;
			КонецПопытки;
			
			ТекСтрока.Значение = ТекРекв.Значение;
		КонецЦикла;
	Исключение КонецПопытки;
	
	Попытка
		// пройдемся по реквизитам табличной части
		Для Каждого ТекРекв Из СтрокаМД.Таблица Цикл
			ТекСтрока = СтруктураОбъекта.Добавить();
			ТекСтрока.Идентификатор = ТекРекв.Ключ;
			ТекСтрока.Значение      = ТекРекв.Значение;
		КонецЦикла;
	Исключение КонецПопытки;
	
	Возврат СтруктураОбъекта;
	
КонецФункции  // ПолучитьСтруктуру()

// возвращает список реквизитов
//
Функция ПолучитьСписокРеквизитов(ТипИлиПозиция, СтруктураОбъекта=Неопределено, НаименованиеТаблицы="") Экспорт
	
	Если СтруктураОбъекта = Неопределено Тогда СтруктураОбъекта = ТекСтруктура; КонецЕсли;
	
	ТипВидОбъекта = СтруктураОбъекта[0].Значение["Идентификатор"];
	
	ЗначенияРеквизитов = Новый Соответствие();
	Если XMLФорматВыгрузки Тогда
		СтрокаОбъекта = тзОбъектов.Найти(ТипИлиПозиция, "Идентификатор");
		Если СтрокаОбъекта = Неопределено Тогда
			  ЗначенияРекв = Новый Соответствие();
		Иначе ЗначенияРекв = СтрокаОбъекта.Реквизиты;
		КонецЕсли;
		
		Для Каждого ТекРекв Из ЗначенияРекв Цикл
			Если ТекРекв.Ключ = "_1С_Уровень" Тогда
				ЗначенияРеквизитов.Вставить(ТекРекв.Ключ, ПривестиКТипу(ТекРекв.Значение, "Число"));
			
			ИначеЕсли ТекРекв.Ключ = "_1С_ЭтоГруппа" или
				      ТекРекв.Ключ = "_1С_Проведен" Тогда
				 
				 ЗначенияРеквизитов.Вставить(ТекРекв.Ключ, ПривестиКТипу(ТекРекв.Значение, "Булево"));
				 
			ИначеЕсли ТекРекв.Ключ = "_1С_ИдентификаторБД" или
			 	      ТекРекв.Ключ = "_1С_Владелец" или
			 		  ТекРекв.Ключ = "_1С_Родитель" или
			 		  ТекРекв.Ключ = "_1С_Код" или
			 		  ТекРекв.Ключ = "_1С_Номер" или
			 		  ТекРекв.Ключ = "_1С_Время" или
					  ТекРекв.Ключ = "_1С_НаименованиеПредопределённого" или
					  ТекРекв.Ключ = "_1С_ИдентификаторПриемника" Тогда
					  
				ЗначенияРеквизитов.Вставить(ТекРекв.Ключ, ТекРекв.Значение);
				
			ИначеЕсли ТекРекв.Ключ = "_1С_Наименование" Тогда
				ЗначенияРеквизитов.Вставить(ТекРекв.Ключ, ОчиститьСпецСимволы(ТекРекв.Значение));
				
			
			ИначеЕсли ТекРекв.Ключ = "_1С_Дата" Тогда
				ЗначенияРеквизитов.Вставить(ТекРекв.Ключ, ПривестиКТипу(ТекРекв.Значение, "Дата"));
				
			Иначе
				
				ОписаниеРекв = СтруктураОбъекта.Найти(ТекРекв.Ключ, "Идентификатор").Значение;
				
				// Проверим, является ли реквизит периодическим?
				Если Найти(ТипВидОбъекта, "Справочник.") > 0 и НЕ ФайлВыгрузкиИзV8 Тогда
					  Периодический = ПривестиКТипу(ОписаниеРекв["Периодический"], "Булево");
				Иначе Периодический = Ложь;
				КонецЕсли;
				
				ЗначенияРеквизитов.Вставить(ТекРекв.Ключ, ПреобразоватьЗначение(ТекРекв.Значение, ОписаниеРекв["Тип"], Периодический));
			КонецЕсли;
		КонецЦикла;
		
		Возврат ЗначенияРеквизитов;
		
	Иначе
		СтрРекв = СокрЛП(ФайлЗагрузки.ПолучитьСтроку(ТипИлиПозиция));
		ЗначенияРекв = РазобратьСтроку(УбратьСимволы(СтрРекв, "{}"), Истина);
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеТаблицы) Тогда
		СчРекв = 1;
		
		Для в = 0 по ЗначенияРекв.Количество() - 1 Цикл
			ЗначениеРекв = ЗначенияРекв.Получить(в);
			
			Если Найти(ТипВидОбъекта, "Справочник.") > 0 Тогда
				Если      в = 0 Тогда ЗначенияРеквизитов.Вставить("_1С_Уровень",         ПривестиКТипу(ЗначениеРекв.Значение, "Число"));
				ИначеЕсли в = 1 Тогда ЗначенияРеквизитов.Вставить("_1С_ИдентификаторБД", ЗначениеРекв.Значение);
				ИначеЕсли в = 2 Тогда ЗначенияРеквизитов.Вставить("_1С_Владелец",        ЗначениеРекв.Значение);
				ИначеЕсли в = 3 Тогда ЗначенияРеквизитов.Вставить("_1С_Родитель",        ЗначениеРекв.Значение);
				ИначеЕсли в = 4 Тогда ЗначенияРеквизитов.Вставить("_1С_ЭтоГруппа",       ПривестиКТипу(ЗначениеРекв.Значение, "Булево"));
				ИначеЕсли в = 5 Тогда ЗначенияРеквизитов.Вставить("_1С_Код",             ЗначениеРекв.Значение);
				ИначеЕсли в = 6 Тогда ЗначенияРеквизитов.Вставить("_1С_Наименование",    ОчиститьСпецСимволы(ЗначениеРекв.Значение));
				Иначе
					
					ТекРекв = СтруктураОбъекта[СчРекв].Значение;
					
					// Проверим, является ли реквизит периодическим?
					Периодический = ?(НЕ ФайлВыгрузкиИзV8, ПривестиКТипу(ТекРекв["Периодический"], "Булево"), Ложь);
					
					ЗначенияРеквизитов.Вставить(ТекРекв["Идентификатор"], ПреобразоватьЗначение(ЗначениеРекв.Значение, ТекРекв["Тип"], Периодический));
					
					СчРекв = СчРекв + 1;
				КонецЕсли;
				
			Иначе
				Если      в = 0 Тогда ЗначенияРеквизитов.Вставить("_1С_Уровень",         ПривестиКТипу(ЗначениеРекв.Значение, "Число"));
				ИначеЕсли в = 1 Тогда ЗначенияРеквизитов.Вставить("_1С_ИдентификаторБД", ЗначениеРекв.Значение);
				ИначеЕсли в = 2 Тогда ЗначенияРеквизитов.Вставить("_1С_Номер",           ЗначениеРекв.Значение);
				ИначеЕсли в = 3 Тогда ЗначенияРеквизитов.Вставить("_1С_Дата",            ПривестиКТипу(ЗначениеРекв.Значение, "Дата"));
				ИначеЕсли в = 4 и НЕ ФайлВыгрузкиИзV8 Тогда ЗначенияРеквизитов.Вставить("_1С_Время", ЗначениеРекв.Значение);
				ИначеЕсли (в = 5 и НЕ ФайлВыгрузкиИзV8) или (в = 4 и ФайлВыгрузкиИзV8) Тогда ЗначенияРеквизитов.Вставить("_1С_Проведен", ПривестиКТипу(ЗначениеРекв.Значение, "Булево"));
				Иначе
					
					ТекРекв = СтруктураОбъекта[СчРекв].Значение;
					
					ЗначенияРеквизитов.Вставить(ТекРекв["Идентификатор"], ПривестиКТипу(ЗначениеРекв.Значение, ТекРекв["Тип"]));
					
					СчРекв = СчРекв + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		ОписаниеТаблицы = СтруктураОбъекта.Найти(НаименованиеТаблицы);
		Если ОписаниеТаблицы <> Неопределено Тогда
			Для в = 1 по ЗначенияРекв.Количество() - 1 Цикл
				ЗначениеРекв = ЗначенияРекв.Получить(в);
				
				ТекРекв = ОписаниеТаблицы.Значение[в - 1].Значение;
				
				ЗначенияРеквизитов.Вставить(ТекРекв["Идентификатор"], ПривестиКТипу(ЗначениеРекв.Значение, ТекРекв["Тип"]));
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции  // ПолучитьСписокРеквизитов

// возвращает табличную часть
//
Функция ПолучитьТабличнуюЧасть(ИдентификаторОбъекта, НаименованиеТаблицы="", СтруктураОбъекта=Неопределено) Экспорт
	
	Если НаименованиеТаблицы = "" Тогда НаименованиеТаблицы = "Таблица"; КонецЕсли;
	Если СтруктураОбъекта = Неопределено Тогда СтруктураОбъекта = ТекСтруктура; КонецЕсли;
	
	ЗначенияТЧ = Новый СписокЗначений();
	
	Если XMLФорматВыгрузки Тогда
		СтрокаОбъекта = тзОбъектов.Найти(ИдентификаторОбъекта, "Идентификатор");
		Если СтрокаОбъекта = Неопределено Тогда
			  ЗначенияСтрок = Новый СписокЗначений();
		Иначе ЗначенияСтрок = СтрокаОбъекта.Строки;
		КонецЕсли;
		
		Если ТипЗнч(ЗначенияСтрок) <> Тип("Соответствие") Тогда
			ЗначенияСтрок = Новый Соответствие();
		КонецЕсли;	
		
		ТекТаблица = ЗначенияСтрок.Получить(НаименованиеТаблицы);
		Если ТекТаблица = Неопределено Тогда Возврат ЗначенияТЧ; КонецЕсли;
				
		Для ккк = 0 по ТекТаблица.Количество() - 1 Цикл
			РеквизитыСтроки = ТекТаблица.Получить(ккк).Значение;
			
			ТекСтрокаРекв = Новый Соответствие();
			Для Каждого ТекСтрока Из РеквизитыСтроки Цикл
			    ОписаниеРеквизитов = СтруктураОбъекта.Найти(НаименованиеТаблицы, "Идентификатор").Значение;
				
				ОписаниеРеквизита = ОписаниеРеквизитов.Получить(ТекСтрока.Ключ);
				Если ОписаниеРеквизита = Неопределено Тогда Продолжить; КонецЕсли;
				
				ТекСтрокаРекв.Вставить(ТекСтрока.Ключ, ПривестиКТипу(ТекСтрока.Значение, ОписаниеРеквизита["Тип"]));
			КонецЦикла;
			
			ЗначенияТЧ.Добавить(ТекСтрокаРекв);
		КонецЦикла;
		
	Иначе
		
		Если ВремБДТаблиц.Найти(ИдентификаторОбъекта + НаименованиеТаблицы, "=") Тогда
			НомерСтрокиФайла = ВремБДТаблиц.LineNum;
			Для ккк = НомерСтрокиФайла по ФайлЗагрузки.КоличествоСтрок() Цикл
				Стр = ФайлЗагрузки.ПолучитьСтроку(ккк);
				Если ПустаяСтрока(Стр) или НЕ ПустаяСтрока(Лев(Стр, 4)) Тогда Прервать; КонецЕсли;
				Если ФайлВыгрузкиИзV8 Тогда
					Если Найти(Стр, "{""Таблица." + НаименованиеТаблицы + """,""") = 0 Тогда Прервать; КонецЕсли;
				КонецЕсли;
				
				ЗначенияСтроки = ПолучитьСписокРеквизитов(ккк, СтруктураОбъекта, НаименованиеТаблицы);
				ЗначенияТЧ.Добавить(ЗначенияСтроки);
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	
		
	Возврат ЗначенияТЧ;
	
КонецФункции  // ПолучитьТабличнуюЧасть

//Функция возвращает текст сообщения найденного по коду из макета "СообщенияЗагрузки"
Функция ПолучитьСообщениеПоКоду(Код) Экспорт
	МакетСообщения = ПолучитьМакет("СообщенияЗагрузки");
	
	Сообщение = МакетСообщения.Область("Код_"+Код+"|Сообщение").Текст;
	Возврат Сообщение;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОЛУЧЕНИЯ/УСТАНОВКИ РЕКВИЗИТОВ

// Функция возвращает значение реквизита по имени
//
// Параметры:
//   ИмяРекв - имя получаемого реквизита
//   ТипОбъекта - тип + вид объекта, для которого необходимо получить значение реквизита
//   Объект - идентификатор объекта, по которому необходимо получить значение реквизита
Функция ПолучитьЗначениеРеквизита(ИмяРекв, ТипОбъекта="", Объект="") Экспорт
	
	ЗначенияРекв = ТекРеквизиты;
	Если НЕ ПустаяСтрока(ТипОбъекта) Тогда
		Если Объект = "" Тогда Возврат Неопределено; КонецЕсли;
		СтрокаФайла = Истина;
		//Если ПолучитьОбъект(Объект, ТипОбъекта, СтрокаФайла) = Неопределено Тогда Возврат Неопределено; КонецЕсли;
		ПолученныйОбъект = ПолучитьОбъект(Объект, ТипОбъекта, СтрокаФайла);
		
		Если НЕ ПустаяСтрока(СтрокаФайла) Тогда //и ПолученныйОбъект <> Неопределено Тогда
			Если XMLФорматВыгрузки Тогда
				ЗначенияРекв = ПолучитьСписокРеквизитов(Объект, ПолучитьСтруктуру(ТипОбъекта));
			Иначе	
				ЗначенияРекв = ПолучитьСписокРеквизитов(СтрокаФайла, ПолучитьСтруктуру(ТипОбъекта));
			КонецЕсли;
			
		Иначе Возврат Неопределено;
		КонецЕсли;	
	КонецЕсли;

	Возврат ЗначенияРекв[ИмяРекв];
	
КонецФункции  // ПолучитьЗначениеРеквизита

// Процедура устанавливает реквизит
Процедура УстановитьРеквизит(ТекОбъект, ИмяРекв, ЗначениеРекв) Экспорт
	
	Попытка
		ТекОбъект[ИмяРекв] = ЗначениеРекв;
	Исключение КонецПопытки;
	
КонецПроцедуры

// функция возвращает объект БД, если он был уже загружен,
// если такой объект еще не был обработан, вернет Неопределено
Функция ПолучитьОбъект(ИдентификаторОбъекта, ТипОбъекта="", СтрокаФайла=Ложь) Экспорт
	
	Если ТипЗнч(ИдентификаторОбъекта)<>Тип("Строка") Тогда
		// Это значит в этот параметр передали не идентификатор объекта, а сам объект
		// (такое случается при свёртке ТЧ)
		Возврат ИдентификаторОбъекта;
	КонецЕсли;
	
	Если ПустаяСтрока(ИдентификаторОбъекта) Тогда Возврат Неопределено; КонецЕсли;
	
	Если СтрДлина(ИдентификаторОбъекта) > 36 Тогда
		ТипОбъекта = Лев(ИдентификаторОбъекта, СтрДлина(ИдентификаторОбъекта) - 36);
		//ИдентификаторОбъекта = Прав(ИдентификаторОбъекта, 36);
	КонецЕсли;
	
	Если СтрокаФайла Тогда
		тзПоиска       = ?(Найти(ТипОбъекта, "Справочник") = 1, ВремБДОбъектов, ВремБДДоков);
		БылНомерЗаписи = тзПоиска.НомерЗаписи();
		
		тзПоиска.ТекущийИндекс = тзПоиска.индексы.Найти("IndID");
		Если тзПоиска.Найти(ИдентификаторОбъекта, "=") Тогда
			ТипОбъекта = СокрЛП(тзПоиска.Type);
			СтрокаФайла = ?(XMLФорматВыгрузки, ТипОбъекта, тзПоиска.LineNum);
		КонецЕсли;
		
		тзПоиска.ТекущийИндекс = тзПоиска.индексы.Найти(?(Найти(ТипОбъекта, "Справочник") = 1, "IndLevel", "IndTime"));
		Если БылНомерЗаписи > 0 Тогда тзПоиска.Перейти(БылНомерЗаписи); КонецЕсли;
	КонецЕсли;
	
	Если ХранитьТСВРегистре Тогда
		
		УИД = Новый УникальныйИдентификатор(ИдентификаторОбъекта);
		
		Если Найти(ТипОбъекта, "Справочник") = 1 Тогда
			ИмяСправочника = СтрЗаменить(ТипОбъекта, "Справочник.", "");
			ИмяМенеджера = СоответствиеМенеджеровСправочник[ИмяСправочника];
			Менеджер = Справочники[ИмяМенеджера];
		Иначе
			ИмяДокумента = СтрЗаменить(ТипОбъекта, "Документ.", "");
			ИмяМенеджера = СоответствиеМенеджеров[ИмяДокумента];
			Менеджер = Документы[ИмяМенеджера];
		КонецЕсли;
		
		ОбъектСсылка = Менеджер.ПолучитьСсылку(УИД);
		
		Если ОбъектСсылка = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Попытка
			Если ОбъектСсылка.Пустая() Тогда
				Возврат Неопределено;
			КонецЕсли;
		Исключение
			Возврат Неопределено;
		КонецПопытки;
		
		// Проверим на "битую" ссылку
		Попытка
			Если ОбъектСсылка.ПолучитьОбъект() = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
		Исключение
			Возврат Неопределено;
		КонецПопытки;
		
		Возврат ОбъектСсылка;
		
	ИначеЕсли БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда
		ИмяМенеджера = СокрЛП(БД_ТС.ManagerRar);
		Если ПустаяСтрока(ИмяМенеджера) Тогда
			ИмяМенеджера = СокрЛП(БД_ТС.Manager);
			Если НЕ ПустаяСтрока(ИмяМенеджера) Тогда
				ИмяМенеджера = СоответствиеМенеджеров[ИмяМенеджера];
				Если ИмяМенеджера = Неопределено Тогда ИмяМенеджера = ""; КонецЕсли;
			КонецЕсли;
		КонецЕсли;				
		Если НЕ ПустаяСтрока(ИмяМенеджера) Тогда
			Попытка УИД = Новый УникальныйИдентификатор(СокрЛП(БД_ТС.ID_Source));
			Исключение Возврат Неопределено; КонецПопытки;
			
			Если Найти(ТипОбъекта, "Справочник") = 1 Тогда
				  Менеджер = Справочники[ИмяМенеджера];
			Иначе Менеджер = Документы[ИмяМенеджера];
			КонецЕсли;
			
			ОбъектСсылка = Менеджер.ПолучитьСсылку(УИД);
			Если ОбъектСсылка = Неопределено Тогда Возврат Неопределено; КонецЕсли;
		
			Попытка Если ОбъектСсылка.Пустая() Тогда Возврат Неопределено; КонецЕсли;
			Исключение Возврат Неопределено; КонецПопытки;
			
			// Проверим на "битую" ссылку
			Попытка Если ОбъектСсылка.ПолучитьОбъект() = Неопределено Тогда
						Возврат Неопределено;
					КонецЕсли;
			Исключение Возврат Неопределено; КонецПопытки;
			
			Возврат ОбъектСсылка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции  // ПолучитьОбъект

//Функция вызывается в том случае, если соответсвие не получено, но объект найден поиском по реквизитам.
//Проверяется а не существует ли уже соответсвие с найденным объектом, но не с ссылкой загружаемого объекта.
Функция ВозможноУстановлениеСоответсвия(ОбъектБД) Экспорт
	Если ОбъектБД.Ссылка.Пустая() Тогда Возврат Ложь; КонецЕсли;
	УИД = ОбъектБД.Ссылка.УникальныйИдентификатор();
	Результат = ПроверитьНаУникальность(УИД,,,Истина);
	Возврат Результат;	
КонецФункции

//Функция проверяет на уникальность поле "ID_Source". Если допусить запись неуникального значения, то упадет индекс.
//В этом случае может возникнуть невозможность добавления новых строк в таблицу соответствия.
//Функция вызывается из процедур устанавливающих соответсвие
Функция ПроверитьНаУникальность(ID_Source,НомерТекЗаписи= "",НовыйНомерСтроки,Проверка=Ложь) Экспорт
	
	Если ХранитьТСВРегистре Тогда
		Возврат Истина;
	КонецЕсли;
	
	НовыйНомерСтроки = "";
	ТекущийИндекс = БД_ТС.ТекущийИндекс;
	БД_ТС.ТекущийИндекс = БД_ТС.индексы.Найти("IndSource");
	Если НомерТекЗаписи = "" Тогда
		Если БД_ТС.Найти(ID_Source,"=") Тогда
			Попытка 
				ПерезаписатьСоответствие = Ложь;
				ИмяМенеджера = СокрЛП(БД_ТС.ManagerRar);
				Если ПустаяСтрока(ИмяМенеджера) Тогда
					ИмяМенеджера = СокрЛП(БД_ТС.Manager);
					Если ИмяМенеджера = "ДоговорыКонтрагентов" Тогда
						ИмяМенеджера = "ДоговорыВзаиморасчетов"
					КонецЕсли;											
				КонецЕсли;	
				Если НЕ ПустаяСтрока(ИмяМенеджера) Тогда
					Попытка УИД = Новый УникальныйИдентификатор(СокрЛП(БД_ТС.ID_Source));
					Исключение ПерезаписатьСоответствие = Истина; КонецПопытки;
					
					Если Найти(ПредТипОбъекта, "Справочник") = 1 Тогда
						  Менеджер = Справочники[ИмяМенеджера];
					Иначе Менеджер = Документы[ИмяМенеджера];
					КонецЕсли;
					
					ОбъектСсылка = Менеджер.ПолучитьСсылку(УИД);
					Если ОбъектСсылка = Неопределено Тогда ПерезаписатьСоответствие = Истина; КонецЕсли;
				
					Попытка Если ОбъектСсылка.Пустая() Тогда ПерезаписатьСоответствие = Истина; КонецЕсли;
					Исключение ПерезаписатьСоответствие = Истина; КонецПопытки;
					
					// Проверим на "битую" ссылку
					Попытка 
						Если ОбъектСсылка.ПолучитьОбъект() = Неопределено Тогда
							ПерезаписатьСоответствие = Истина;
						КонецЕсли;
					Исключение ПерезаписатьСоответствие = Истина; КонецПопытки;								
				Иначе
					ПерезаписатьСоответствие = Истина;										
				КонецЕсли;
			Исключение 
				ПерезаписатьСоответствие = Истина;
			КонецПопытки;

			// Если в результате ПерезаписатьСоответствие = Истина, значит в таблицу соответствия записана
			// битая ссылка раруса, ее можно перезаписать.
			Если НЕ ПерезаписатьСоответствие Тогда
				Если НЕ Проверка  Тогда
					Сообщить("Попытка записи в таблицу соответствия неуникального элемента <" + ПолучитьЗначениеРеквизита("_1С_Наименование") + ">, запись соответствия непроизведена!");
				КонецЕсли;	
				Возврат Ложь;
			Иначе
				НовыйНомерСтроки = БД_ТС.НомерЗаписи();
				Возврат Истина;
			КонецЕсли;	
		Иначе
			Возврат Истина;
		КонецЕсли;	
	Иначе
		Если БД_ТС.Найти(ID_Source,"=") Тогда 
			Если БД_ТС.НомерЗаписи() <> НомерТекЗаписи Тогда 
				Сообщить("Попытка записи в таблицу соответствия неуникального элемента <" + ПолучитьЗначениеРеквизита("_1С_Наименование") + ">, запись соответствия непроизведена!");
				Возврат Ложь;
			Иначе
				БД_ТС.Перейти(НомерТекЗаписи);	
			КонецЕсли;
		Иначе
			БД_ТС.Перейти(НомерТекЗаписи);	
		КонецЕсли;	
	КонецЕсли;	
	Возврат Истина;
КонецФункции

//Функция проверяет на уникальность поле "ID_Source". 
//Взывается из процедур ищущих свободный номер
Функция УникальныйИдСорс(ID_Source) Экспорт
	
	Если ХранитьТСВРегистре Тогда
		Возврат Истина;
	КонецЕсли;
	
	БД_ТС.ТекущийИндекс = БД_ТС.индексы.Найти("IndSource");
	Если БД_ТС.Найти(ID_Source,"=") Тогда		
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;	
КонецФункции	

// производит поиск
Функция БД_ТСНайти(ИдентификаторОбъекта, РежимСравнения) Экспорт
	БД_ТС.ТекущийИндекс = БД_ТС.индексы.Найти("IndReceive");
	Возврат БД_ТС.Найти(ИдентификаторОбъекта, РежимСравнения);	
КонецФункции

Функция НайтиОбъектПриемника(ДокМенеджер, ИдентификаторОбъекта, ИдентификаторОбъектаПриемника="") Экспорт
	
	Если НЕ ПустаяСтрока(ИдентификаторОбъектаПриемника) Тогда
		ОбъектПриемник = ДокМенеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(СокрЛП(ИдентификаторОбъектаПриемника)));
		Если ОбъектПриемник <> Неопределено И СтрНайти(Строка(ОбъектПриемник), НСтр("ru = '<Объект не найден>'")) = 0 Тогда
			Возврат ОбъектПриемник;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИдентификаторОбъекта) Тогда
		ОбъектПриемник = ДокМенеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(СокрЛП(ИдентификаторОбъекта)));
		Если ОбъектПриемник <> Неопределено И СтрНайти(Строка(ОбъектПриемник), НСтр("ru = '<Объект не найден>'")) = 0 Тогда
			Возврат ОбъектПриемник;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// устанавливает соотвецтвие
Процедура УстановитьСоответствие(ИдентификаторОбъекта, ОбъектБД, Знач ТипПриемника="", ИдентификаторПриемник = "") Экспорт
// Процедура осуществляет запись объекта в таблицу соответствия
	Перем НовыйНомерСтроки;
    НоваяСтрока = Ложь;	
	Если ОбъектБД.Ссылка.Пустая() Тогда Возврат; КонецЕсли;
	УИД = ОбъектБД.Ссылка.УникальныйИдентификатор();
	
	Если ХранитьТСВРегистре Тогда
		// Нет смысла соответствие формировать
		Возврат;
	КонецЕсли;
	
	// Поищем в таблице	
	Если НЕ БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда
		// Если соответствия еще нет, то установим его
		Если НЕ ПроверитьНаУникальность(УИД,,НовыйНомерСтроки) Тогда 
			ОбъектБД = Неопределено;
			Возврат;
		КонецЕсли;	
		Если НовыйНомерСтроки = "" Тогда
			БД_ТС.Добавить();
			БД_ТС.ID_Receive = ИдентификаторОбъекта;
			НоваяСтрока = Истина;
		Иначе
			БД_ТС.Перейти(НовыйНомерСтроки);
			БД_ТС.ID_Receive = ИдентификаторОбъекта;
		КонецЕсли;		
	Иначе
		Если НЕ ПроверитьНаУникальность(БД_ТС.ID_Source,БД_ТС.НомерЗаписи(),НовыйНомерСтроки) Тогда 
			ОбъектБД = Неопределено;
			Возврат;
		КонецЕсли;					
		//Если у нас есть соответсвтие 
		Если НовыйНомерСтроки <> "" Тогда
		КонецЕсли;		
	КонецЕсли;
	
	// Представление объекта во внутреннем формате
	БД_ТС.ID_Source = УИД;
	БД_ТС.Manager		= ВидОбъекта;
	БД_ТС.ManagerRar    = ОбъектБД.Ссылка.Метаданные().Имя;
	БД_ТС.Direct = 2;
	
	//Если поле Update есть, но значение не задано, задаем его равным 1 - по умолчанию.
	Если ЕстьПолеUpdate Тогда
		Если НоваяСтрока или БД_ТС.Update = 0 Тогда
			БД_ТС.Update = 1;
		КонецЕсли;
	КонецЕсли;
		
	БД_ТС.Записать();
КонецПроцедуры  // УстановитьСоответствие

// Процедура осуществляет запись объекта в таблицу соответствия
Процедура УстановитьСоответствиеДляВыписки(ИдПП, ИдВып, ОбъектБД, ПредставлениеОбъекта="", ИдААА = "") Экспорт
	Перем НовыйНомерСтроки;

	Если ХранитьТСВРегистре Тогда
		// Установкой соответствия не нужно
		Возврат;
	КонецЕсли;
	
    Если ОбъектБД = неопределено или ОбъектБД.Ссылка.Пустая() Тогда Возврат; КонецЕсли;
    НоваяСтрока = Ложь;	
	Если НЕ ЗначениеЗаполнено(ИдВып) Тогда Возврат; КонецЕсли;
	ИдентификаторОбъекта = ИдПП;
	УИД = ИдВып;	
	// Поищем в таблице
	
	Если НЕ БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда
		// Если соответствия еще нет, то установим его
		Если НЕ ПроверитьНаУникальность(УИД,,НовыйНомерСтроки) Тогда 
			Возврат;
		КонецЕсли;	
		Если НовыйНомерСтроки = "" Тогда
			БД_ТС.Добавить();
			БД_ТС.ID_Receive = ИдентификаторОбъекта;
			НоваяСтрока = Истина;
		Иначе
			БД_ТС.Перейти(НовыйНомерСтроки);
			БД_ТС.ID_Receive = ИдентификаторОбъекта;
		КонецЕсли;		
	Иначе
		Если НЕ ПроверитьНаУникальность(БД_ТС.ID_Source,БД_ТС.НомерЗаписи(),НовыйНомерСтроки) Тогда 
			Возврат;
		КонецЕсли;					
		//Если у нас есть соответсвтие 
		Если НовыйНомерСтроки <> "" Тогда
		КонецЕсли;		
	КонецЕсли;
	
	// Представление объекта во внутреннем формате
	БД_ТС.ID_Source = УИД;
	БД_ТС.ManagerRar    = "Выписка";
	БД_ТС.Direct = 2;
	
	//Если поле Update есть, но значение не задано, задаем его равным 1 - по умолчанию.
	Если ЕстьПолеUpdate Тогда
		Если НоваяСтрока или БД_ТС.Update = 0 Тогда
			БД_ТС.Update = 1;
		КонецЕсли;
	КонецЕсли;
	
	БД_ТС.Записать();
	
КонецПроцедуры
	
// Функция возвращает объект-приемник по объекту-источнику
Функция ПолучитьСоответствие(Менеджер, ИдентификаторОбъекта, ОбъектПриемник, ТолькоСсылка=Ложь, ИдентификаторОбъектаПриемника="", ТипПриемника="") Экспорт
	
	Если ХранитьТСВРегистре Тогда
		
		Если НЕ ПустаяСтрока(ИдентификаторОбъектаПриемника) Тогда
			ОбъектПриемник = Менеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(СокрЛП(ИдентификаторОбъектаПриемника)));
			ОбъектПриемник = ОбъектПриемник.ПолучитьОбъект();
			
			Если ОбъектПриемник <> Неопределено Тогда
				Если ТолькоСсылка Тогда
					ОбъектПриемник = ОбъектПриемник.Ссылка;
				КонецЕсли;
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ИдентификаторОбъекта) Тогда
			ОбъектПриемник = Менеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(СокрЛП(ИдентификаторОбъекта)));
			ОбъектПриемник = ОбъектПриемник.ПолучитьОбъект();
			
			Если ОбъектПриемник <> Неопределено Тогда
				Если ТолькоСсылка Тогда
					ОбъектПриемник = ОбъектПриемник.Ссылка;
				КонецЕсли;
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
		
	Иначе
		
		Если НЕ БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда Возврат Ложь; КонецЕсли;
		Если ПустаяСтрока(БД_ТС.ID_Source) Тогда Возврат Ложь; КонецЕсли;
		
		Попытка УИД = Новый УникальныйИдентификатор(СокрЛП(БД_ТС.ID_Source));
		Исключение Возврат Ложь; КонецПопытки;	
		
		ОбъектПриемник = Менеджер.ПолучитьСсылку(УИД);
		Если ОбъектПриемник = Неопределено Тогда Возврат Ложь; КонецЕсли;
		
		Попытка Если ОбъектПриемник.Пустая() Тогда Возврат Ложь; КонецЕсли;
		Исключение Возврат Ложь; КонецПопытки;
		
	КонецЕсли;
	
	Если НЕ ТолькоСсылка Тогда
		
		ОбъектПриемник = ОбъектПриемник.ПолучитьОбъект();
	КонецЕсли;
	Возврат ?(ОбъектПриемник<>Неопределено,Истина,Ложь);	
КонецФункции  // ПолучитьСоответствие

// Функция возвращает соответствие для выписки
Функция ПолучитьСоответствиеДляВыписки(ДокМенеджер, ИдентификаторОбъекта, ИдентификаторПриемника, ОбъектПриемник, ИдентификаторВыписки=0, ИзмененныйИдентификатор=0) Экспорт
	
	Если ХранитьТСВРегистре Тогда
		
		НайденоВП = Найти(ИдентификаторПриемника,"-ВП");
		НайденоИП = Найти(ИдентификаторПриемника,"-ИП");
		
		Если НайденоВП > 0 Тогда
			Идентификатор = Лев(ИдентификаторПриемника, НайденоВП - 1);
		ИначеЕсли НайденоИП > 0 Тогда
			Идентификатор = Лев(ИдентификаторПриемника, НайденоИП - 1);
		Иначе
			//Такого быть не должно
			Идентификатор = ИдентификаторПриемника;
		КонецЕсли;
		ИдентификаторВыписки = Идентификатор;
		
		ДокументПриемник = НайтиОбъектПриемника(ДокМенеджер, ИдентификаторОбъекта, Идентификатор);
		Если ДокументПриемник = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ОбъектПриемник = ДокументПриемник.ПолучитьОбъект();
		
		Возврат ?(ОбъектПриемник<>Неопределено,Истина,Ложь);
		
	ИначеЕсли НЕ БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда
		Возврат Ложь;
	КонецЕсли;
	Если ПустаяСтрока(БД_ТС.ID_Source) Тогда Возврат Ложь; КонецЕсли;
	ИзмененныйИдентификатор = СокрЛП(БД_ТС.ID_Source);
	НайденоВП = Найти(ИзмененныйИдентификатор,"-ВП");
	НайденоИП = Найти(ИзмененныйИдентификатор,"-ИП");
	Если НайденоВП > 0 Тогда
		Идентификатор = Лев(ИзмененныйИдентификатор,НайденоВП-1);
	ИначеЕсли НайденоИП > 0 Тогда
		Идентификатор = Лев(ИзмененныйИдентификатор,НайденоИП-1);
	Иначе
		//Такого быть не должно
		Идентификатор = ИзмененныйИдентификатор;
	КонецЕсли;	
	ИдентификаторВыписки = Идентификатор;
	
	Попытка УИД = Новый УникальныйИдентификатор(СокрЛП(Идентификатор));
	Исключение Возврат Ложь; КонецПопытки;	
	
	Менеджер = Документы.Выписка;
	ОбъектПриемник = Менеджер.ПолучитьСсылку(УИД);
	Если ОбъектПриемник = Неопределено Тогда Возврат Ложь; КонецЕсли;
	
	Попытка Если ОбъектПриемник.Пустая() Тогда Возврат Ложь; КонецЕсли;
	Исключение Возврат Ложь; КонецПопытки;
	
	ОбъектПриемник = ОбъектПриемник.ПолучитьОбъект();
	Возврат ?(ОбъектПриемник<>Неопределено,Истина,Ложь);	
КонецФункции  // ПолучитьСоответствие

Функция ПолучитьИдентификаторДляВыписки(ИдентификаторОбъекта)
	Если НЕ БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда Возврат 0; КонецЕсли;
	
КонецФункции

//Процедура помечает на удаление объекты, которые есть в соответсвтии, но уже не нужны.
//Эти документы были созданы на основе строк табличной части документа-источника.
//Параметры:
//	ИдентификаторОбъекта - идентификатор документа-источника
//	КоличествоДокументов - количество документов созданных в этот раз.
Процедура ПометитьНаУдалениеДокументыСоответсвия(ИдентификаторОбъекта,КоличествоДокументов,ДокМенеджер) Экспорт
	Если КоличествоДокументов > 1 Тогда
		ИдентификаторСтроки = ИдентификаторОбъекта;
		
		НайденОбъект = (ХранитьТСВРегистре И НайтиОбъектПриемника(ДокМенеджер, ИдентификаторСтроки) <> Неопределено)
			ИЛИ (НЕ ХранитьТСВРегистре И БД_ТСНайти(ИдентификаторСтроки, "="));
		
		Если НайденОбъект Тогда 
			ТекОбъектДляУдаления = ДокМенеджер.ПустаяСсылка();	
			Если ПолучитьСоответствие(ДокМенеджер, ИдентификаторСтроки, ТекОбъектДляУдаления) Тогда				
				// Отменим проведение, если документ был проведен ранее
				Если ТекОбъектДляУдаления.Проведен Тогда
					ТекОбъектДляУдаления.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				КонецЕсли;					
				ТекОбъектДляУдаления.ПометкаУдаления = Истина;
				ТекОбъектДляУдаления.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;							
		КонецЕсли;	
	КонецЕсли;	
	Начало = ?(КоличествоДокументов = 1, 0, КоличествоДокументов);
	Для к = Начало По 50 Цикл
		ИдентификаторСтроки = ИдентификаторОбъекта + "-" + к;			
		Если НЕ ХранитьТСВРегистре И НЕ БД_ТСНайти(ИдентификаторСтроки, "=") Тогда Прервать; КонецЕсли;
		Если ХранитьТСВРегистре И НайтиОбъектПриемника(ДокМенеджер, ИдентификаторСтроки) = Неопределено Тогда
			Прервать;
		КонецЕсли;
		ТекОбъектДляУдаления = ДокМенеджер.ПустаяСсылка();	
		Если ПолучитьСоответствие(ДокМенеджер, ИдентификаторСтроки, ТекОбъектДляУдаления) Тогда				
			// Отменим проведение, если документ был проведен ранее
			Если ТекОбъектДляУдаления.Проведен Тогда
				ТекОбъектДляУдаления.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			КонецЕсли;					
			ТекОбъектДляУдаления.ПометкаУдаления = Истина;
			ТекОбъектДляУдаления.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;							
	КонецЦикла;	
КонецПроцедуры	


// функция возвращает способ обновления - значения дополнинетльного поля Update в таблицы соответствия.
// Если такого поля нет - возвращает 0. (1 - по умолчанию, 2 - не обновлять, 3 - обновлять)
Функция ПолучитьСпособОбновления(ИдентификаторОбъекта) Экспорт
	
	Если НЕ ЕстьПолеUpdate Тогда
		Возврат 0;
	КонецЕсли;	
	Если НЕ БД_ТСНайти(ИдентификаторОбъекта, "=") Тогда Возврат 0; КонецЕсли;
	СпособОбновления = БД_ТС.Update;	
	Возврат СпособОбновления;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

СтрокаОписанияКонфигурации = "";
ТаблицаОтчета = Новый ТаблицаЗначений;
ТаблицаОтчета.Колонки.Добавить("ОбъектИсточник");
ТаблицаОтчета.Колонки.Добавить("ОбъектПриемник");
ТаблицаОтчета.Колонки.Добавить("Код");
ТаблицаОтчета.Колонки.Добавить("Сообщение");
ТаблицаОтчета.Колонки.Добавить("ДопИнформация");
ТаблицаОтчета.Колонки.Добавить("Проведен");

БылоСозданиеНовогоПоля = Ложь;
ПервыйРазСоотв = Истина;
кУР = Найти(Метаданные.Имя,"УправлениеРестораном")>0;

СоответствиеМенеджеров = Новый Соответствие;
СоответствиеМенеджеров.Вставить("АвансовыйОтчет",				"АвансовыйОтчет");
СоответствиеМенеджеров.Вставить("ВводНачальныхОстатков",		"ВводОстатковТоваров");
СоответствиеМенеджеров.Вставить("ВозвратТоваровОтПокупателя",	"ВозвратОтПокупателя");
СоответствиеМенеджеров.Вставить("ВозвратТоваровПоставщику",		"ВозвратПоставщику");
СоответствиеМенеджеров.Вставить("ПеремещениеТоваров",			"ПеремещениеТоваров");
СоответствиеМенеджеров.Вставить("ПлатежноеПоручениеВходящее",	"Выписка");
СоответствиеМенеджеров.Вставить("ПлатежноеПоручениеИсходящее",	"Выписка");
СоответствиеМенеджеров.Вставить("ПоступлениеТоваровУслуг",		"ПоступлениеТоваров");
СоответствиеМенеджеров.Вставить("ПриходныйКассовыйОрдер",		"ПриходныйКассовыйОрдер");
СоответствиеМенеджеров.Вставить("РасходныйКассовыйОрдер",		"РасходныйКассовыйОрдер");
СоответствиеМенеджеров.Вставить("РеализацияТоваровУслуг",		"АвансовыйОтчет");
СоответствиеМенеджеров.Вставить("АвансовыйОтчет",				"РеализацияТоваров");
СоответствиеМенеджеров.Вставить("СписаниеТоваров",				"СписаниеТоваров");

СоответствиеМенеджеровСправочник = Новый Соответствие;
СоответствиеМенеджеровСправочник.Вставить("Валюты",							"Валюты");
СоответствиеМенеджеровСправочник.Вставить("Организации",					"Организации");
СоответствиеМенеджеровСправочник.Вставить("ПодразделенияКомпании",			"ПодразделенияКомпании");
СоответствиеМенеджеровСправочник.Вставить("Пользователи",					"Пользователи");
СоответствиеМенеджеровСправочник.Вставить("Контрагенты",					"Контрагенты");
СоответствиеМенеджеровСправочник.Вставить("ДоговорыКонтрагентов",			"ДоговорыВзаиморасчетов");
СоответствиеМенеджеровСправочник.Вставить("БанковскиеСчета",				"БанковскиеСчета");
СоответствиеМенеджеровСправочник.Вставить("ТипыЦенНоменклатуры",			"ТипыЦен");
СоответствиеМенеджеровСправочник.Вставить("СтатьиДДС",						"СтатьиДДС");
СоответствиеМенеджеровСправочник.Вставить("ТипыНоменклатуры",				"ТипыНоменклатуры");
СоответствиеМенеджеровСправочник.Вставить("КлассификаторЕдиницИзмерения",	"КлассификаторЕдиницИзмерения");
СоответствиеМенеджеровСправочник.Вставить("КлассификаторСтранМира",			"КлассификаторСтранМира");
СоответствиеМенеджеровСправочник.Вставить("НомераГТД",						"ГТД");
СоответствиеМенеджеровСправочник.Вставить("ДоговорКонтрагента",				"ДоговорыВзаиморасчетов");
СоответствиеМенеджеровСправочник.Вставить("СтатьиДвиженияДенежныхСредств",	"СтатьиДДС");
СоответствиеМенеджеровСправочник.Вставить("Номенклатура",					"Номенклатура");
СоответствиеМенеджеровСправочник.Вставить("Банки",							"Банки");

флЗагрузкаИзБП2	= 0;

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ

// Функция выполняет подключение внешней компоненты и ее первоначальную настройку.
// 
// Параметры: 
//  Нет 
// 
// Возвращаемое значение: 
//  Картинка - Картинка со сформированным штрихкодом или НЕОПРЕДЕЛЕНО
Функция ПодключитьВнешнююКомпонентуПечатиШтрихкода()
	
	// Подлючение компоненты
	Попытка
		Если ПодключитьВнешнююКомпоненту("ОбщийМакет.КомпонентаПечатиШтрихкодов", "Barcode") Тогда
			ВнешняяКомпонента = Новый("AddIn.Barcode.Barcode");
		Иначе
			Сообщить("Не удалось подключить компоненту для печати маркировки.");
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		Сообщить("Не удалось подключить компоненту для печати маркировки.");
		Возврат Неопределено;
	КонецПопытки;
	
	// Если нет возможности рисовать
	Если НЕ ВнешняяКомпонента.ГрафикаУстановлена Тогда
		// То картинку сформировать не сможем
		Возврат Неопределено;
	Иначе
		
		// Установим основные параметры компоненты
		
		// Если в системе установлен шрифт Tahoma
		Если ВнешняяКомпонента.НайтиШрифт("Tahoma") = Истина Тогда
			// Выбираем его как шрифт для формирования картинки
			ВнешняяКомпонента.Шрифт = "Tahoma";
		Иначе
			
			// Шрифт Tahoma в системе отсутствует
			// Обойдем все доступные компоненте шрифты
			Для Сч = 0 По ВнешняяКомпонента.КоличествоШрифтов -1 Цикл
				// Получим очередной шрифт, доступный компоненте
				ТекущийШрифт = ВнешняяКомпонента.ШрифтПоИндексу(Сч);
				// Если шрифт доступен
				Если ТекущийШрифт <> Неопределено Тогда
					// Они и будет шрифтом для формирования штри-кода
					ВнешняяКомпонента.Шрифт = ТекущийШрифт;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Утановим размер шрифта
		ВнешняяКомпонента.РазмерШрифта = 12;
		
		Возврат ВнешняяКомпонента;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКартинку(ВнешняяКомпонента, ПараметрыШК)
	
	// Зададим размер формируемой картинки.
	ВнешняяКомпонента.Ширина = Окр(ПараметрыШК.Ширина);
	ВнешняяКомпонента.Высота = Окр(ПараметрыШК.Высота);
	ВнешняяКомпонента.АвтоТип = Ложь;
	
	ШтрихкодВрем = Строка(ПараметрыШК.Штрихкод); // Преобразуем явно в строку.
	
	ВнешняяКомпонента.АвтоТип = Ложь;
	ВнешняяКомпонента.ТипКода = 24;
	ВнешняяКомпонента.ОтображатьТекст = Истина;
	// Формируем картинку штрихкода.
	ВнешняяКомпонента.ЗначениеКода = ШтрихкодВрем;
	// Угол поворота штрихкода.
	ВнешняяКомпонента.УголПоворота = 0;
	// Уровень коррекции QR кода (L=0, M=1, Q=2, H=3).
	ВнешняяКомпонента.УровеньКоррекцииQR = 1;
	ВнешняяКомпонента.ТипВходныхДанных = 1;
	
	// Сформируем картинку
	ДвоичныеДанныеКартинки = ВнешняяКомпонента.ПолучитьШтрихкод();
	
	// Если картинка сформировалась.
	Если ДвоичныеДанныеКартинки <> Неопределено Тогда
		// Формируем из двоичных данных.
		Возврат Новый Картинка(ДвоичныеДанныеКартинки);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ШтрихКодВBase64(СтрокаШК)
	
	ДвоичныеДанныеШК  = ПолучитьДвоичныеДанныеИзСтроки(СтрокаШК);
	
	Base64ШК = Base64Строка(ДвоичныеДанныеШК);
	Base64ШК = СтрЗаменить(Base64ШК, Символы.ПС, "");
	Base64ШК = СтрЗаменить(Base64ШК, Символы.ВК, "");
	
	Возврат Base64ШК;
	
КонецФункции

Функция ПечатьКодовМаркировок(Документ, КодыМаркировки) Экспорт
	
	ВнешняяКомпонента = ПодключитьВнешнююКомпонентуПечатиШтрихкода();
	
	Если ВнешняяКомпонента = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Эталон = ПолучитьМакет("Эталон");
	ЭтикеткаШин = ПолучитьМакет("ЭтикеткаШин");
	
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Область = ЭтикеткаШин.ПолучитьОбласть(ЭтикеткаШин.ОбластьПечати.Имя);
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	СимволГруппы = ОбъектШК.РазделительGS1();
	СимволЭкрана = ОбъектШК.ЭкранированныйСимволGS1();
	
	ПараметрыШК = Новый Структура("Ширина,Высота,Штрихкод,ТипКода,ОтображатьТекст");
	ПараметрыШК.ТипКода = 18;
	ПараметрыШК.ОтображатьТекст = Истина;
	
	Номер = 0;
	Для Каждого ТекущаяСтрока Из КодыМаркировки Цикл
		
		СтруктураМарировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Область.Параметры.Наименование = ТекущаяСтрока.Номенклатура.Наименование;
		Область.Параметры.Артикул      = ТекущаяСтрока.Номенклатура.Артикул;
		
		Область.Параметры.ПредставлениеШтрихкода = "01"+СтруктураМарировки.ДанныеШтрихкода["01"].Значение + "21" + СтруктураМарировки.ДанныеШтрихкода["21"].Значение;
		
		ТекущаяМаркировка = ШтрихКодВBase64(СтрЗаменить(ТекущаяСтрока.КодМаркировки, СимволЭкрана, СимволГруппы));
		
		Для Каждого Рисунок Из Область.Рисунки Цикл
			
			ПараметрыШК.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
			ПараметрыШК.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
			ПараметрыШК.Вставить("Штрихкод", ТекущаяМаркировка);
			
			Рисунок.Картинка = ПолучитьКартинку(ВнешняяКомпонента, ПараметрыШК);
			
		КонецЦикла;
		
		Если Не ТабличныйДокумент.ПроверитьВывод(Область) Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Если Номер = 0 Тогда
			ТабличныйДокумент.Вывести(Область);
		Иначе
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		
		Если Номер = 2 Тогда
			Номер = 0;
		Иначе
			Номер = Номер + 1;
		КонецЕсли;
		
		//ТабличныйДокумент.Вывести(Область);
		
	КонецЦикла;
	
	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",Права);
	ТабДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	ТабДокумент.ПолеСлева				= 9;
	ТабДокумент.ПолеСправа			= 6;
	//Выведем печатную форму	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабличныйДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ДополнительноКПредставлению	= "Печать кодов маркировки";
	ФормаПечати.Открыть();
	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
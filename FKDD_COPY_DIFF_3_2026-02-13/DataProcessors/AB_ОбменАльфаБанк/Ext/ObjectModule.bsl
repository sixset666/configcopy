Перем Эндпоинт;
Перем Сертификат;
Перем ПарольСертификат;
Перем КлиентИД;
Перем КлиентСекрет;
Перем Редирект;

//Вызываемая регламентом Функция
Функция ОбменДанными() Экспорт
	УчетныеЗаписи = ПолучитьУчетныеЗаписи();
	
	Для каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
		УчетнаяЗапись = УчетнаяЗапись.Ссылка;
		//обновим Токен	
		Если ОбновитьТокен(УчетнаяЗапись) = Неопределено Тогда
			Сообщить("Не удалось обновить токен доступа для учетной записи <"+ УчетнаяЗапись +">");
			Продолжить;
		КонецЕсли;
		//Получим транзакции
		Транзакции = ПолучитьТранзакции(УчетнаяЗапись,ТекущаяДатаСеанса());
		Если Транзакции = Неопределено Тогда
			Сообщить("Не удалось получить транзакции для учетной записи <"+ УчетнаяЗапись +">");
			Продолжить;
		Иначе
			ЗаписатьТранзакцииВРегистр(Транзакции,УчетнаяЗапись);
		КонецЕсли;
	КонецЦикла;
	Сообщить("Выполнено!");
КонецФункции

Функция ПеременныеПоУмолчанию() 
	
	Если НЕ ЗначениеЗаполнено(Редирект) 			Тогда Редирект = "http://localhost"; 						КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Эндпоинт) 			Тогда Эндпоинт = "baas.alfabank.ru"; 						КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Сертификат) 			Тогда Сертификат = "D:\Sert\baas_swagger_2025.p12"; 		КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПарольСертификат) 	Тогда ПарольСертификат = "alfabank"; 						КонецЕсли;
	Если НЕ ЗначениеЗаполнено(КлиентИД) 			Тогда КлиентИД = "f5608827-f39a-4f63-9d3f-5eb984ff0a56"; 	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(КлиентСекрет) 		Тогда КлиентСекрет = ""; 									КонецЕсли;
	
	
КонецФункции

#Область Логирование

#КонецОбласти

#Область HTTPЗапросыИДанные
// Разбирает строку URI на составные части и возвращает в виде структуры.
// На основе RFC 3986.
//
// Параметры:
//  СтрокаURI - Строка - ссылка на ресурс в формате:
//                       <схема>://<логин>:<пароль>@<хост>:<порт>/<путь>?<параметры>#<якорь>.
//
// Возвращаемое значение:
//  Структура - составные части URI согласно формату:
//   * Схема         - Строка - схема из URI.
//   * Логин         - Строка - логин из URI.
//   * Пароль        - Строка - пароль из URI.
//   * ИмяСервера    - Строка - часть <хост>:<порт> из URI.
//   * Хост          - Строка - хост из URI.
//   * Порт          - Строка - порт из URI.
//   * ПутьНаСервере - Строка - часть <путь>?<параметры>#<якорь> из URI.
//
Функция СтруктураURI(Знач СтрокаURI) Экспорт
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// Строка соединения и путь на сервере.
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// Информация пользователя и имя сервера.
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@", НаправлениеПоиска.СКонца);
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
		Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Порт) Тогда
			Порт = "";
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(ПустаяСтрока(Порт), Неопределено, Число(Порт)));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции
//HTTP запрос
Функция ВыполнитьHTTPЗапрос(URL, МетодЗапроса = "GET", ТелоЗапроса = "", Заголовки = Неопределено, Авторизация = Неопределено,SSL = Неопределено) Экспорт
	
	СтруктураURL = СтруктураURI(URL);
	
	Если SSL = Неопределено Тогда
		SSL = Новый ЗащищенноеСоединениеOpenSSL;	
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(СтруктураURL.Хост,,,,,,SSL);
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURL.ПутьНаСервере);
	
	//Заголовки
	Если ЗначениеЗаполнено(Заголовки) И ТипЗнч(Заголовки) = Тип("Соответствие") тогда
		HTTPЗапрос.Заголовки = Заголовки;		
	КонецЕсли;
	//Обяз хост
	Если HTTPЗапрос.Заголовки.Получить("Host") = Неопределено Тогда
		HTTPЗапрос.Заголовки.Вставить("Host",СтруктураURL.Хост);	
	КонецЕсли;
	//Обяз ассерт
	Если HTTPЗапрос.Заголовки.Получить("Accept") = Неопределено Тогда
		HTTPЗапрос.Заголовки.Вставить("Accept","application/json");	
	КонецЕсли;
	
	//Авторизация
	Если ЗначениеЗаполнено(Авторизация) Тогда
		Если ТипЗнч(Авторизация) = тип("ТокенДоступа") Тогда
			HTTPЗапрос.ДобавитьТокенДоступа(Авторизация);			
		ИначеЕсли ТипЗнч(Авторизация) = тип("Строка") Тогда
			 HTTPЗапрос.Заголовки.Вставить("Authorization",Авторизация);
		КонецЕсли;
	КонецЕсли;
	
	//Тело запроса
	Если ЗначениеЗаполнено(ТелоЗапроса) И ТипЗнч(ТелоЗапроса) = Тип("Строка") Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса,,ИспользованиеByteOrderMark.НеИспользовать); 
		HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded")
	ИначеЕсли ЗначениеЗаполнено(ТелоЗапроса) И ТипЗнч(ТелоЗапроса) = Тип("Структура") Тогда
		ЗаписьJSON = Новый ЗаписьJSON;			
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON,ТелоЗапроса);
		HTTPЗапрос.УстановитьТелоИзСтроки(ЗаписьJSON.Закрыть());
		HTTPЗапрос.Заголовки.Вставить("Content-Type","application/json");
	КонецЕсли;
	
	//Выполнение запроса 
	Попытка
		ОтветHTTP = HTTPСоединение.ВызватьHTTPМетод(МетодЗапроса, HTTPЗапрос); 
		
		КодСостояния = ОтветHTTP.КодСостояния;
		ТекстОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
		ДанныеСтруктура = Неопределено;
		
		//Попытаемся прочитать JSON
		ЧтениеJson = Новый ЧтениеJSON;
		ЧтениеJson.УстановитьСтроку(ТекстОтвета);
		ДанныеСтруктура = ПрочитатьJSON(ЧтениеJson);
		
		Ошибка = ОбработкаОшибокЗапросов(ДанныеСтруктура,КодСостояния);
		
		Если ЗначениеЗаполнено(ДанныеСтруктура) Тогда
			Возврат ДанныеСтруктура;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Сообщить(ТекстОшибки);
		
		ВторойТекстОшибки = "";
		ВторойТекстОшибки = ВторойТекстОшибки + ?(ЗначениеЗаполнено(КодСостояния),"КодСостояния - " + Строка(КодСостояния) + Символы.ПС,"");
		ВторойТекстОшибки = ВторойТекстОшибки + ?(ЗначениеЗаполнено(ТекстОтвета),"ТекстОтвета - " + Строка(ТекстОтвета) + Символы.ПС,"");
		Если ЗначениеЗаполнено(ВторойТекстОшибки) Тогда
			Сообщить(ВторойТекстОшибки);
		КонецЕсли;
	КонецПопытки;
КонецФункции
//Получает данные из структуры если ключ существует
Функция ПолучитьЗначениеЕслиСуществуетИЗаполнено(Данные,Наименование) Экспорт
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если Данные.Свойство(Наименование) И ЗначениеЗаполнено(Данные[Наименование]) Тогда
		Возврат Данные[Наименование];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
//Получает данные из структуры по пути "ключ1.ключ2" если ключи сущестуют
Функция ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Данные,Путь) Экспорт
	МассивПути = СтрРазделить(Путь,".",Ложь);
	ТекущиеДанные = Данные;
	Для Каждого ТекущийКлюч из МассивПути Цикл
		ТекущиеДанные = ПолучитьЗначениеЕслиСуществуетИЗаполнено(ТекущиеДанные,ТекущийКлюч); 		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат ТекущиеДанные;
КонецФункции
//Возвращает Basic Base64(Логин:Пароль) 
Функция ПолучитьАвторизациюКакBASIC(Логин,Пароль)
	Возврат "Basic "+Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(Логин + ":" + Пароль));	
КонецФункции 
//Возвращает Bearer
Функция ПолучитьАвторизациюКакBearer(Токен)
	Возврат "Bearer "+Токен;	
КонецФункции
//добавляет к url структуру в виде ?param1=1&param2=2
Функция ДобавитьBodyURLEncoded(URL, Params)
	URL = URL + "?";
	Для Каждого Параметр из Params Цикл
		URL = URL + Параметр.Ключ + "=" + Параметр.Значение + "&";
	КонецЦикла;
	Возврат URL;
КонецФункции
Функция ВернутьBodyURLEncoded(Params)
	URL = "";
	Для Каждого Параметр из Params Цикл
		URL = URL + Параметр.Ключ + "=" + Параметр.Значение + "&";
	КонецЦикла;
	Возврат URL;
КонецФункции
//Добавляет сертификат к запросу
Функция ПолучитьСоединениеПоСертификату(ПутьКСертификату, Пароль)
	Если ЗначениеЗаполнено(Пароль) Тогда
		SSL = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаФайл(ПутьКСертификату, Пароль));
	Иначе
		SSL = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаФайл(ПутьКСертификату));
	КонецЕсли;
	Возврат SSL;
КонецФункции
//Конвертирует структуру в строку в формате JSON
Функция СтруктуруВJSONТекст(JSON)
	Попытка
		JSONобъект = новый ЗаписьJSON;
		JSONобъект.УстановитьСтроку();
		ЗаписатьJSON(JSONобъект,JSON);
		JSONТекстом = JSONобъект.Закрыть(); 
		Возврат JSONТекстом;
	Исключение
		JSONТекстом = "Не удалось получить JSON конкретного объекта";
		Возврат JSONТекстом;
	КонецПопытки;
КонецФункции
// Нормализует дату в формате 2024-12-03T21:00:00Z в дату 1с
Функция JSONДатаНормализовать(ДатаJSON)
	ДатаJSON = СтрЗаменить(ДатаJSON,"Z","");
	ДатаJSON = XMLЗначение(Тип("Дата"),ДатаJSON);
	Возврат ДатаJSON;	
КонецФункции
#КонецОбласти   

#Область Авторизация
Функция ПолучитьСоединениеПоСертификатуПоКлиентID(КлиентИД)
	
	СоответствиеСертификатов = новый Соответствие;
	СоответствиеСертификатов.Вставить("80663a27-b58a-4ea7-9fd5-f29e19c4674f","D:\Sert\kuban.pfx");
	СоответствиеСертификатов.Вставить("7e3fa3f3-11c7-46bb-b78e-bfdecc42a083","D:\Sert\line.pfx");
	
	СоответствиеПаролейКСертификату = новый Соответствие;
	СоответствиеПаролейКСертификату.Вставить("80663a27-b58a-4ea7-9fd5-f29e19c4674f","8fj1ladllffA");
	СоответствиеПаролейКСертификату.Вставить("7e3fa3f3-11c7-46bb-b78e-bfdecc42a083","slDla92daa8jaad");
	
	//SANDBOX
	СоответствиеСертификатов.Вставить("f5608827-f39a-4f63-9d3f-5eb984ff0a56","D:\Sert\baas_swagger_2025.p12");
	СоответствиеПаролейКСертификату.Вставить("f5608827-f39a-4f63-9d3f-5eb984ff0a56","alfabank");
	
	Сертификат = СоответствиеСертификатов.Получить(КлиентИД);
	ПарольСертификат = СоответствиеПаролейКСертификату.Получить(КлиентИД); 
	SSL = ПолучитьСоединениеПоСертификату(Сертификат,ПарольСертификат);
	Возврат SSL;
	
КонецФункции
Функция ПолучитьСекрет(КлиентИД) Экспорт
	ПеременныеПоУмолчанию();
	
	URL = "https://" + Эндпоинт + "/api/clients/" + КлиентИД + "/client-secret";
	Метод = "POST";
	
	SSL = ПолучитьСоединениеПоСертификатуПоКлиентID(КлиентИД);
	
	Ответ = ВыполнитьHTTPЗапрос(URL,Метод,,,,SSL);
	
	Секрет = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Ответ,"clientSecret");
	Возврат Секрет;
КонецФункции

Функция ПолучитьТокен(КлиентИД,КлиентСекрет, ТипВыдачи = "authorization_code",КодАвторизации = "", ТокенОбновления = "") Экспорт
	ПеременныеПоУмолчанию();

	URL = "https://" + Эндпоинт + "/api/token";
	Метод = "POST";
	
	SSL = ПолучитьСоединениеПоСертификатуПоКлиентID(КлиентИД);
	
	params = новый Соответствие;
	params.Вставить("grant_type",		ТипВыдачи);
	params.Вставить("client_id",		КлиентИД);
	params.Вставить("client_secret",	КлиентСекрет);
	params.Вставить("redirect_uri",		Редирект);
	
	Если ТипВыдачи = "refresh_token" Тогда
		params.Вставить("refresh_token",	ТокенОбновления);
	ИначеЕсли ТипВыдачи = "authorization_code" Тогда
		params.Вставить("code",				КодАвторизации);
	КонецЕсли;
	
	Body = ВернутьBodyURLEncoded(Params);
	
	Ответ = ВыполнитьHTTPЗапрос(URL,Метод,Body,,,SSL);
	
	Если НЕ ЗначениеЗаполнено(Ответ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	access_token = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Ответ,"access_token");
	refresh_token = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Ответ,"refresh_token"); 
	expires_in = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Ответ,"expires_in");
	
	Если ЗначениеЗаполнено(access_token) И ЗначениеЗаполнено(refresh_token) И ЗначениеЗаполнено(expires_in) Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Токен", 			access_token);
		СтруктураВозврата.Вставить("ТокенОбновления", 	refresh_token);
		СтруктураВозврата.Вставить("СрокЖизни", 		ТекущаяДата() + Число(expires_in));
		
		Возврат СтруктураВозврата;
	Иначе
		Сообщить("Ошибка входа");
	КонецЕсли;
	
КонецФункции

Функция ОбновитьТокен(СсылкаНаУчетнуюЗапись) Экспорт
	
	УчетнаяЗапись = ПолучитьДанныеУчетнойЗаписи(СсылкаНаУчетнуюЗапись);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись.ClientID)
		И ЗначениеЗаполнено(УчетнаяЗапись.ClientSecret)
		И ЗначениеЗаполнено(УчетнаяЗапись.refresh_token)
		И ЗначениеЗаполнено(УчетнаяЗапись.expires_in) Тогда
		//Надо ли обновлять токен?
		Если УчетнаяЗапись.expires_in <= (ТекущаяДатаСеанса()+5*60) Тогда
			Токен = ПолучитьТокен(УчетнаяЗапись.ClientID,
								  УчетнаяЗапись.ClientSecret,
								  "refresh_token",
								  ,
								  УчетнаяЗапись.refresh_token);
		Иначе
			Возврат Истина;
		КонецЕсли;  
	Иначе
		Сообщить("Невозможно обновить токен! Нет данных о учетной записи!");						  
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Токен) Тогда
		Попытка 
			ЗаписатьДанныеУчетнойЗаписи(СсылкаНаУчетнуюЗапись,Токен);
			Возврат Истина;
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			Сообщить(ТекстОшибки); 
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
КонецФункции
#КонецОбласти 

#Область ОсновныеФункцииОбмена
Функция ОбработкаОшибокЗапросов(ДанныеСтруктура,КодСостояния = 200)
	Если ДанныеСтруктура.Свойство("error") ИЛИ КодСостояния <> 200 тогда
		//ЕстьОшибка
		Если ДанныеСтруктура.Свойство("error") Тогда
			//Есть текст ошибки
			Сообщить(ДанныеСтруктура["error"]);
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТранзакции(УчетнаяЗаписьСсылка,НаДату = Неопределено)
	ПеременныеПоУмолчанию();
	
	ДанныеУчетнойЗаписи = ПолучитьДанныеУчетнойЗаписи(УчетнаяЗаписьСсылка);
	Транзакции = Новый Массив;
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	НаДату = Формат(НаДату,"ДФ=yyyy-MM-dd");
	
	Если ЗначениеЗаполнено(ДанныеУчетнойЗаписи.РасчетныйСчет) И СтрПодобнаПоРегулярномуВыражению(ДанныеУчетнойЗаписи.РасчетныйСчет,"\d{20}") Тогда
		
		НомерСтраницы = 0; 
		ЕстьСледующаяСтраница = Истина;
		Пока ЕстьСледующаяСтраница Цикл
			ЕстьСледующаяСтраница = Ложь;
			НомерСтраницы = НомерСтраницы + 1;
			
			URL = "https://" + Эндпоинт + "/api/statement/transactions";
			Метод = "GET";
			
			SSL = ПолучитьСоединениеПоСертификатуПоКлиентID(ДанныеУчетнойЗаписи.ClientID);

			Авторизация = ПолучитьАвторизациюКакBearer(ДанныеУчетнойЗаписи.access_token);
			
			params = новый Соответствие;
			
			params.Вставить("page",					НомерСтраницы);
			params.Вставить("curFormat",			"curTransfer");
			params.Вставить("statementDate",		НаДату);
			params.Вставить("accountNumber",		ДанныеУчетнойЗаписи.РасчетныйСчет);
			
			URL = ДобавитьBodyURLEncoded(URL,Params);
			
			Ответ = ВыполнитьHTTPЗапрос(URL,Метод,,,Авторизация,SSL);		
			Если ответ.свойство("_links") И Ответ.свойство("transactions") Тогда
				//Проверим есть ли дополнительные страницы
				Если Ответ._links.Количество() Тогда
					Для Каждого Ссылка из Ответ._links Цикл
						Если Ссылка.rel = "next" тогда
							ЕстьСледующаяСтраница = Истина;	
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				//Проверим есть ли транзакции
				Если Ответ.transactions.Количество() Тогда
					Для Каждого Транзакция из Ответ.transactions Цикл
						//Проверим подходит ли нам транзакция
						ТипОперации = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"direction");
						КодОперации = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"operationCode");
						Если ТипОперации = "DEBIT" Тогда Продолжить; КонецЕсли; // Списания нам не нужны
						Если КодОперации <> "17" Тогда Продолжить; КонецЕсли; // Прочие операции нам не нужны
						
						НазначениеПлатежа = Транзакция.paymentPurpose;
						
						СтруктураТранзакции = Новый Структура;
						СтруктураТранзакции.Вставить("IDОперации",	ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"transactionId"));
						СтруктураТранзакции.Вставить("UUID",		ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"uuid"));
						
						//ДатаОперации
						ДатаОперации = ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"operationDate");
						ДатаОперации = JSONДатаНормализовать(ДатаОперации);
						СтруктураТранзакции.Вставить("ДатаОперации",ДатаОперации+3*60*60);
						
						//Сумма
						СтруктураТранзакции.Вставить("Сумма",ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"amountRub.amount"));
						
						//Назначение платежа
						СтруктураТранзакции.Вставить("НазначениеПлатежа",ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Транзакция,"paymentPurpose"));
						
						//JSON
						СтруктураТранзакции.Вставить("JSON",СтруктуруВJSONТекст(Транзакция));
						
						//Терминал
						Терминал = СтрНайтиВсеПоРегулярномуВыражению(НазначениеПлатежа,"ТУ ([0-9]{6})");
						Если Терминал.Количество() Тогда
							Терминал = Терминал[0].Значение;
							Терминал = СтрНайтиВсеПоРегулярномуВыражению(Терминал,"[0-9]{6}"); 
							Если Терминал.Количество() Тогда
								Терминал = Терминал[0].Значение;
								Запрос = Новый Запрос;
								Запрос.Текст = 
									"ВЫБРАТЬ
									|	AB_ТерминалыАльфабанк.Ссылка КАК Ссылка
									|ИЗ
									|	Справочник.AB_ТерминалыАльфабанк КАК AB_ТерминалыАльфабанк
									|ГДЕ
									|	AB_ТерминалыАльфабанк.ID = &ID
									|	И AB_ТерминалыАльфабанк.Организация = &Организация";
								
								Запрос.УстановитьПараметр("ID", Терминал);
								Запрос.УстановитьПараметр("Организация", ДанныеУчетнойЗаписи.Организация);
								РезультатЗапроса = Запрос.Выполнить();
								Если НЕ РезультатЗапроса.Пустой() Тогда
									ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
									ВыборкаДетальныеЗаписи.Следующий();
									Терминал = ВыборкаДетальныеЗаписи.Ссылка;
								КонецЕсли;
								//Терминал = Справочники.AB_ТерминалыАльфабанк.НайтиПоРеквизиту("ID",Терминал);
							КонецЕсли;
						КонецЕсли;
						Если типЗнч(терминал) = Тип("СправочникСсылка.AB_ТерминалыАльфабанк") Тогда
							СтруктураТранзакции.Вставить("Терминал",терминал);
						Иначе
							СтруктураТранзакции.Вставить("Терминал",Справочники.AB_ТерминалыАльфабанк.ПустаяСсылка());
						КонецЕсли;
						
						Транзакции.Добавить(СтруктураТранзакции);
						
					КонецЦикла;
				КонецЕсли;
	
			Иначе
				Возврат Неопределено;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Транзакции;
		
	Иначе
		
		Сообщить("В карточке учетной записи <"+ УчетнаяЗаписьСсылка +"> не заполнен расчетный счет или заполнен неверно!");
		Возврат Неопределено;
		
	КонецЕсли;
		
КонецФункции


#КонецОбласти

#Область ЭлементыКонфигурации
Функция ПолучитьДанныеУчетнойЗаписи(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_УчетныеЗаписиАльфабанк.Ссылка КАК Ссылка,
		|	AB_УчетныеЗаписиАльфабанк.ВерсияДанных КАК ВерсияДанных,
		|	AB_УчетныеЗаписиАльфабанк.ПометкаУдаления КАК ПометкаУдаления,
		|	AB_УчетныеЗаписиАльфабанк.Код КАК Код,
		|	AB_УчетныеЗаписиАльфабанк.Наименование КАК Наименование,
		|	AB_УчетныеЗаписиАльфабанк.Организация КАК Организация,
		|	AB_УчетныеЗаписиАльфабанк.ClientID КАК ClientID,
		|	AB_УчетныеЗаписиАльфабанк.ClientSecret КАК ClientSecret,
		|	AB_УчетныеЗаписиАльфабанк.access_token КАК access_token,
		|	AB_УчетныеЗаписиАльфабанк.refresh_token КАК refresh_token,
		|	AB_УчетныеЗаписиАльфабанк.expires_in КАК expires_in,
		|	AB_УчетныеЗаписиАльфабанк.РасчетныйСчет КАК РасчетныйСчет,
		|	AB_УчетныеЗаписиАльфабанк.Предопределенный КАК Предопределенный,
		|	AB_УчетныеЗаписиАльфабанк.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.AB_УчетныеЗаписиАльфабанк КАК AB_УчетныеЗаписиАльфабанк
		|ГДЕ
		|	AB_УчетныеЗаписиАльфабанк.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи;
	КонецЦикла;
КонецФункции
Функция ЗаписатьДанныеУчетнойЗаписи(Ссылка,СтруктураТокена) Экспорт
	
	ОбУчетки = Ссылка.ПолучитьОбъект();
	ОбУчетки.access_token = СтруктураТокена.Токен;
	ОбУчетки.refresh_token = СтруктураТокена.ТокенОбновления;
	ОбУчетки.expires_in = СтруктураТокена.СрокЖизни;
	ОбУчетки.Записать();
	
КонецФункции
Функция ПолучитьУчетныеЗаписи(Организация = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_УчетныеЗаписиАльфабанк.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.AB_УчетныеЗаписиАльфабанк КАК AB_УчетныеЗаписиАльфабанк
		|ГДЕ
		|	AB_УчетныеЗаписиАльфабанк.ПометкаУдаления = ЛОЖЬ
		|	И AB_УчетныеЗаписиАльфабанк.Организация = &Организация";
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И AB_УчетныеЗаписиАльфабанк.Организация = &Организация","");
	Иначе
		Запрос.УстановитьПараметр("Организация",Организация);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ЗаписатьТранзакцииВРегистр(Транзакции,УчетнаяЗапись)
	Для Каждого Транзакция из Транзакции Цикл
		Если ПолучитьТранзакциюВрегистре(Транзакция.UUID) = Неопределено Тогда
			НоваяЗапись = РегистрыСведений.AB_ТранзакцииАльфабанк.СоздатьМенеджерЗаписи();
			НоваяЗапись.Период = ТекущаяДатаСеанса();
			НоваяЗапись.УчетнаяЗапись = УчетнаяЗапись;
			НоваяЗапись.РасчетныйСчет = УчетнаяЗапись.РасчетныйСчет;
			НоваяЗапись.Терминал = Транзакция.Терминал;
			НоваяЗапись.IDОперации = Транзакция.IDОперации;
			НоваяЗапись.UUID = Транзакция.UUID;
			НоваяЗапись.ДатаОперации = Транзакция.ДатаОперации;
			НоваяЗапись.Сумма = Транзакция.Сумма;
			НоваяЗапись.JSON = Транзакция.JSON;
			НоваяЗапись.НазначениеПлатежа = Транзакция.НазначениеПлатежа; 
			НоваяЗапись.Используется = Ложь;
			НоваяЗапись.Записать();	
		КонецЕсли;
	КонецЦикла;
КонецФункции 
Функция ПолучитьТранзакциюВрегистре(UUID)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_ТранзакцииАльфабанк.УчетнаяЗапись КАК УчетнаяЗапись,
		|	AB_ТранзакцииАльфабанк.РасчетныйСчет КАК РасчетныйСчет,
		|	AB_ТранзакцииАльфабанк.Терминал КАК Терминал,
		|	AB_ТранзакцииАльфабанк.IDОперации КАК IDОперации,
		|	AB_ТранзакцииАльфабанк.UUID КАК UUID,
		|	AB_ТранзакцииАльфабанк.ДатаОперации КАК ДатаОперации,
		|	AB_ТранзакцииАльфабанк.Сумма КАК Сумма,
		|	AB_ТранзакцииАльфабанк.JSON КАК JSON
		|ИЗ
		|	РегистрСведений.AB_ТранзакцииАльфабанк КАК AB_ТранзакцииАльфабанк
		|ГДЕ
		|	AB_ТранзакцииАльфабанк.UUID = &UUID";
	
	Запрос.УстановитьПараметр("UUID", UUID);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	Если ВыборкаДетальныеЗаписи.Количество() Тогда
		Возврат ВыборкаДетальныеЗаписи;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
КонецФункции

#КонецОбласти
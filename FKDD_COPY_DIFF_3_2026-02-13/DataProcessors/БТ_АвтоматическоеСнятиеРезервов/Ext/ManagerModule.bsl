
// Возвращает таблицу заказов с истекшим сроком снятия резервов
//
Функция ПолучитьТаблицуЗаказов() Экспорт
	
	#Если Клиент Тогда
		РазрешениеСнятияРезервов = обПраво("РазрешениеСнятияРезервов", глПрава);
	#Иначе
		РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.Разрешено;	
	#КонецЕсли
	
	Если РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.РазрешеноРезервыИзСвободногоЗапаса Тогда
		Условие = "ЗаказыПокупателейОстатки.РезервСвободныйОстаток > 0";	
	Иначе 
		Условие = "ЗаказыПокупателейОстатки.РезервОстаток > 0";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыПокупателейОстатки.Заказ КАК Заказ,
	               |	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	МАКСИМУМ(ЗаказыПокупателейОстатки.РезервОстаток) КАК РезервОстаток,
	               |	МАКСИМУМ(ЗаказыПокупателейОстатки.РезервСвободныйОстаток) КАК РезервСвободныйОстаток,
	               |	ЗаказыПокупателейОстатки.Заказ.СрокСнятияРезерва КАК ЗаказСрокСнятияРезерва,
	               |	СУММА(ЗаказыПокупателей.СуммаУпр) КАК СуммаУпр,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказыПокупателей.Номенклатура) КАК НоменклатураГруппа,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказыПокупателей.ХарактеристикаНоменклатуры) КАК ХарактеристикаГруппа,
	               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании
	               |ПОМЕСТИТЬ ЗаказыОстаткиРезервов
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПокупателей.Остатки(, Заказ.СрокСнятияРезерва < &ДатаОкончания) КАК ЗаказыПокупателейОстатки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
	               |		ПО ЗаказыПокупателейОстатки.Заказ = ЗаказыПокупателей.Заказ
	               |			И (ЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	               |ГДЕ
	               |	"+Условие+"
	               |	И ЗаказыПокупателейОстатки.Заказ ССЫЛКА Документ.ЗаявкаНаРемонт
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказыПокупателейОстатки.Номенклатура,
	               |	ЗаказыПокупателейОстатки.Заказ,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.Заказ.СрокСнятияРезерва,
	               |	ЗаказыПокупателейОстатки.СкладКомпании
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры,
	               |	СкладКомпании
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыОстаткиРезервов.Заказ КАК Заказ,
	               |	ЗаказыОстаткиРезервов.Заказ.Номер КАК Номер,
	               |	ЗаказыОстаткиРезервов.Заказ.Дата КАК Дата,
	               |	ЗаказыОстаткиРезервов.Заказ.Автор КАК Автор,
	               |	ЗаказыОстаткиРезервов.Заказ.Организация КАК Организация,
	               |	ЗаказыОстаткиРезервов.Заказ.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказыОстаткиРезервов.Заказ.Контрагент КАК Контрагент,
	               |	ЗаказыОстаткиРезервов.Заказ.ХозОперация КАК ХозОперация,
	               |	ЗаказыОстаткиРезервов.Заказ.СкладКомпании КАК СкладКомпании,
	               |	ЗаказыОстаткиРезервов.Заказ.СрокСнятияРезерва КАК СрокСнятияРезерва,
	               |	ЗаказыОстаткиРезервов.Номенклатура КАК Номенклатура,
	               |	ЗаказыОстаткиРезервов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	МАКСИМУМ(ЗаказыОстаткиРезервов.РезервОстаток) КАК РезервОстаток,
	               |	МАКСИМУМ(ЗаказыОстаткиРезервов.РезервСвободныйОстаток) КАК РезервСвободныйОстаток,
	               |	НАЧАЛОПЕРИОДА(МАКСИМУМ(ЗаказыПокупателей.Период), ДЕНЬ) КАК ДатаРезервирования,
	               |	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(МАКСИМУМ(ЗаказыПокупателей.Период), ДЕНЬ), &ДатаОкончания, ДЕНЬ) + 1 КАК СрокРезервирования,
	               |	ИСТИНА КАК Использование,
	               |	ИСТИНА КАК Корректировка,
	               |	МАКСИМУМ(ЕСТЬNULL(-ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток, 0)) КАК Оплата,
	               |	МАКСИМУМ(ЗаказыОстаткиРезервов.СуммаУпр) КАК Сумма,
	               |	ЗаказыОстаткиРезервов.СкладКомпании КАК МестоРазмещения
	               |ИЗ
	               |	ЗаказыОстаткиРезервов КАК ЗаказыОстаткиРезервов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
	               |		ПО (ЗаказыПокупателей.Период <= &ДатаОкончания)
	               |			И (ЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	               |			И (ЗаказыПокупателей.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказов.Резервирование)
	               |				ИЛИ ЗаказыПокупателей.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказов.ЗаказСРезервированием))
	               |			И ЗаказыОстаткиРезервов.Заказ = ЗаказыПокупателей.Заказ
	               |			И ЗаказыОстаткиРезервов.Номенклатура = ЗаказыПокупателей.Номенклатура
	               |			И ЗаказыОстаткиРезервов.ХарактеристикаНоменклатуры = ЗаказыПокупателей.ХарактеристикаНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
	               |				,
	               |				Сделка ССЫЛКА Документ.ЗаказПокупателя
	               |					ИЛИ Сделка ССЫЛКА Документ.СчетНаОплату) КАК ВзаиморасчетыКомпанииОстатки
	               |		ПО (ВЫБОР
	               |				КОГДА ВзаиморасчетыКомпанииОстатки.Сделка ССЫЛКА Документ.ЗаказПокупателя
	               |					ТОГДА ЗаказыОстаткиРезервов.Заказ = ВзаиморасчетыКомпанииОстатки.Сделка
	               |				КОГДА ВзаиморасчетыКомпанииОстатки.Сделка ССЫЛКА Документ.СчетНаОплату
	               |					ТОГДА ЗаказыОстаткиРезервов.Заказ = ВзаиморасчетыКомпанииОстатки.Сделка.ДокументОснование
	               |				ИНАЧЕ ЛОЖЬ
	               |			КОНЕЦ)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказыОстаткиРезервов.Заказ,
	               |	ЗаказыОстаткиРезервов.Номенклатура,
	               |	ЗаказыОстаткиРезервов.ХарактеристикаНоменклатуры,
	               |	ЗаказыОстаткиРезервов.Заказ.Номер,
	               |	ЗаказыОстаткиРезервов.Заказ.Дата,
	               |	ЗаказыОстаткиРезервов.Заказ.Автор,
	               |	ЗаказыОстаткиРезервов.Заказ.Организация,
	               |	ЗаказыОстаткиРезервов.Заказ.ПодразделениеКомпании,
	               |	ЗаказыОстаткиРезервов.Заказ.Контрагент,
	               |	ЗаказыОстаткиРезервов.Заказ.ХозОперация,
	               |	ЗаказыОстаткиРезервов.Заказ.СкладКомпании,
	               |	ЗаказыОстаткиРезервов.Заказ.СрокСнятияРезерва,
	               |	ЗаказыОстаткиРезервов.СкладКомпании
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СрокСнятияРезерва";
	Запрос.УстановитьПараметр("ДатаОкончания",ТекущаяДата());
	Возврат Запрос.Выполнить().Выгрузить();				   
	
КонецФункции

// Создает запись напоминание автору(менеджеру) документа о списании резерва
//
Процедура СоздатьНапоминание(НаборЗаписей, ТаблицаЗаказов, СнятиеРезервов=Неопределено) Экспорт
	
	//Получим пользователей, кому требуется создать напоминание
	Запрос = Новый Запрос;
	
	// вариант запроса, который вернет всех пользователей, если для 1 сотрудника заведено несколько пользователей
	Запрос.Текст="ВЫБРАТЬ
	             |	ВЫРАЗИТЬ(ТаблицаЗаказов.Заказ КАК Документ.ЗаявкаНаРемонт) КАК Заказ
	             |ПОМЕСТИТЬ ТаблицаЗаказов
	             |ИЗ
	             |	&ТаблицаЗаказов КАК ТаблицаЗаказов
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ЗаказПокупателя.Заказ КАК Заказ,
	             |	ЕСТЬNULL(Пользователи.Ссылка, ЗаказПокупателя.Заказ.Автор) КАК Пользователь
	             |ИЗ
	             |	ТаблицаЗаказов КАК ЗаказПокупателя
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	             |		ПО ЗаказПокупателя.Заказ.Менеджер = Пользователи.Сотрудник
	             |			И (НЕ Пользователи.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка))
	             |			И (Пользователи.ПометкаУдаления = ЛОЖЬ)
	             |ГДЕ
	             |	ЕСТЬNULL(Пользователи.Ссылка, ЗаказПокупателя.Заказ.Автор) <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	             |ИТОГИ ПО
	             |	Пользователь";
	Запрос.УстановитьПараметр("ТаблицаЗаказов", ТаблицаЗаказов);
	ВыборкаПользователи = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ДатаОповещений  = ТекущаяДата();	
	
	Пока ВыборкаПользователи.Следующий() Цикл
		
		ВыборкаЗаказы = ВыборкаПользователи.Выбрать();
		
		//сформируем СтрЗаказы - список заказов по пользователю через запятую
		СтрЗаказы = "";
		Первый = Истина;
		ПервыйЗаказ = Неопределено;
		Пока ВыборкаЗаказы.Следующий() Цикл;
			Если Первый Тогда
				СтрЗаказы = "<" + Строка(ВыборкаЗаказы.Заказ) + ">";		
				ПервыйЗаказ = ВыборкаЗаказы.Заказ;
				Первый = Ложь;
			Иначе
				СтрЗаказы = СтрЗаказы + ", <" + Строка(ВыборкаЗаказы.Заказ) + ">";  	
			КонецЕсли;
		КонецЦикла;
		СтрЗаказы = ?(ВыборкаЗаказы.Количество()=1, "по заказу " + СтрЗаказы, "по заказам " + СтрЗаказы);
		
		Если СнятиеРезервов<>Неопределено Тогда
			Описание = "Сформирован документ снятия резервов <" + Строка(СнятиеРезервов) + "> " + СтрЗаказы;
		Иначе
			Описание = "Истек срок снятия резерва " + СтрЗаказы + ".  Необходимо выполнить снятие резерва.";
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Пользователь            = ВыборкаПользователи.Пользователь;
		НоваяЗапись.Автор                   = ПараметрыСеанса.Пользователь;
		НоваяЗапись.Завершено               = Ложь;
		НоваяЗапись.РеальнаяДатаОповещения  = ДатаОповещений;
		НоваяЗапись.ДатаНачала              = ДатаОповещений;
		НоваяЗапись.ДатаОповещения          = ДатаОповещений;
		НоваяЗапись.Редактирование          = Ложь;
		Если СнятиеРезервов<>Неопределено Тогда
			НоваяЗапись.Объект = СнятиеРезервов;
		Иначе
			НоваяЗапись.Объект = ПервыйЗаказ;
		КонецЕсли;
		НоваяЗапись.Описание = Описание;
		НоваяЗапись.Тема = "Автоматическое снятие резервов по заказам покупателей"; 
		НоваяЗапись.ТипПериода              = "00"; 
		НоваяЗапись.УдалитьПоИстеченииСрока = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует набор напоминаний по просроченным резервам и записывает их
//
Процедура СформироватьНапоминанияПроСнятиеРезервов() Экспорт
	
	ТаблицаЗаказов = ПолучитьТаблицуЗаказов();
	Если ТаблицаЗаказов=Неопределено ИЛИ ТаблицаЗаказов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписейНапоминания = РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
	НаборЗаписейНапоминания.Отбор.Автор.Установить(ПараметрыСеанса.Пользователь);
	НаборЗаписейНапоминания.Отбор.Завершено.Установить(Ложь);
	НаборЗаписейНапоминания.Прочитать();

	ТаблицаЗаказовИтоговая = Новый ТаблицаЗначений;
	ТаблицаЗаказовИтоговая.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРемонт"));	
	
	ТекДата = ТекущаяДата();
	Для Каждого ТекЗаказ Из ТаблицаЗаказов Цикл
		Если НЕ обЗначениеНеЗаполнено(ТекЗаказ.СрокСнятияРезерва) И ТекЗаказ.СрокСнятияРезерва < ТекДата Тогда
			СтрокаИтоговая = ТаблицаЗаказовИтоговая.Добавить();
			СтрокаИтоговая.Заказ = ТекЗаказ.Заказ;
		КонецЕсли;
	КонецЦикла;
	
	СоздатьНапоминание(НаборЗаписейНапоминания, ТаблицаЗаказовИтоговая);
	
	//записываем сформированные напоминания
	Попытка  
		НаборЗаписейНапоминания.Записать(Истина); 
	Исключение 	
	КонецПопытки;
	
КонецПроцедуры

// Формирует документы снятие резервов и возвращает результат работы
Функция ВыполнитьСнятиеРезервов(ТаблицаЗаказовДляСписания,КэшЗаказы,СоздаватьНапоминание=Ложь,ФоновыйРежим=Ложь) Экспорт
	
	КэшСнятиеРезервов = Новый ТаблицаЗначений;
	КэшСнятиеРезервов.Колонки.Добавить("Ссылка",Новый ОписаниеТипов("ДокументСсылка.СнятиеРезервовЗаказовПокупателя"));
	КэшСнятиеРезервов.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КэшСнятиеРезервов.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 0)));
	КэшСнятиеРезервов.Колонки.Добавить("ЕдиницаИзмерения",Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	КэшСнятиеРезервов.Колонки.Добавить("Коэффициент",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 0)));
	КэшСнятиеРезервов.Колонки.Добавить("МестоРазмещения",Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
	КэшСнятиеРезервов.Колонки.Добавить("ЗаказПокупателя",Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРемонт"));
	КэшСнятиеРезервов.Колонки.Добавить("ХарактеристикаНоменклатуры",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	#Если Клиент Тогда
		РазрешениеСнятияРезервов = обПраво("РазрешениеСнятияРезервов", глПрава);
	#Иначе
		РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.Разрешено;	
	#КонецЕсли
	
	Если РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.Запрещено Тогда
		Если НЕ ФоновыйРежим Тогда 
			#Если Клиент Тогда
				Предупреждение("Запрещено снимать резервы!");
			#КонецЕсли
		КонецЕсли;
		Возврат КэшСнятиеРезервов;		
	КонецЕсли;
	
	//если ничего не выбрано, прерываем обработку
	Если ТаблицаЗаказовДляСписания.Количество()=0 Тогда
		Если НЕ ФоновыйРежим Тогда 
			#Если Клиент Тогда
				Предупреждение("Не выбрано заявок для формирования снятия резервов");
			#КонецЕсли
		КонецЕсли;
		Возврат КэшСнятиеРезервов;
	КонецЕсли;
	
	ТаблицаЗаказовДляСписания.Сортировать("Контрагент,Корректировка");
	
	ТекущийДокумент = Документы.ЗаявкаНаРемонт.ПустаяСсылка();
	ТекущийКонтрагент = Справочники.Контрагенты.ПустаяСсылка();
	ТекФлагКорректировки = Ложь;
	
	//таблица содержит заказы, резервы по которым были сняты данным документом
	ТаблицаЗаказовИтоговая = Новый ТаблицаЗначений;
	ТаблицаЗаказовИтоговая.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРемонт"));	
	
	Если СоздаватьНапоминание Тогда
		НаборЗаписейНапоминания = РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
		НаборЗаписейНапоминания.Отбор.Автор.Установить(ПараметрыСеанса.Пользователь);
		НаборЗаписейНапоминания.Отбор.Завершено.Установить(Ложь);
		НаборЗаписейНапоминания.Прочитать();
	КонецЕсли;	
		
	Для Каждого ТекЗаказДляСписания Из ТаблицаЗаказовДляСписания Цикл
		
		Если ТекущийКонтрагент <> ТекЗаказДляСписания.Контрагент ИЛИ ТекФлагКорректировки <> ТекЗаказДляСписания.Корректировка Тогда
			
			//изменился контрагент
			ТекущийКонтрагент = ТекЗаказДляСписания.Контрагент;
			ТекФлагКорректировки = ТекЗаказДляСписания.Корректировка;
			
			//записываем документ
			Если НЕ обЗначениеНеЗаполнено(ТекущийДокумент) И ТекущийДокумент.Товары.Количество()>0 Тогда
				Попытка
					ТекущийДокумент.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);	
					Если НЕ ФоновыйРежим Тогда 
						Сообщить("Сформирован и проведен документ <" + Строка(ТекущийДокумент)+ ">");	
					КонецЕсли;
					//формируем данные для страницы "сформированные документы" 	
					Для Каждого СтрокаТоваров Из ТекущийДокумент.Ссылка.Товары Цикл
						СтрокаКэша = КэшСнятиеРезервов.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаКэша, СтрокаТоваров);
						СтрокаКэша.Ссылка = ТекущийДокумент.Ссылка;
					КонецЦикла;
					//создаем напоминание автору заказа покупателя
					Если СоздаватьНапоминание Тогда
						Обработки.АвтоматическоеСнятиеРезервов.СоздатьНапоминание(НаборЗаписейНапоминания, ТаблицаЗаказовИтоговая,ТекущийДокумент.Ссылка);
					КонецЕсли;
				Исключение
					Если НЕ ФоновыйРежим Тогда 
						Сообщить("Не удалось записать документ " + Строка(ТекущийДокумент));	
					КонецЕсли;
				КонецПопытки;
				
	 			ТаблицаЗаказовИтоговая.Очистить();
				
			КонецЕсли;
			
			// создаем новый документ и заполняем его реквизиты
			ТекущийДокумент = Документы.СнятиеРезервовЗаказовПокупателя.СоздатьДокумент();
			ТекущийДокумент.Контрагент = ТекЗаказДляСписания.Контрагент;	
			ТекущийДокумент.Заполнить(Неопределено);
			ТекущийДокумент.КорректировкаЗаказа = ТекЗаказДляСписания.Корректировка;
			ТекущийДокумент.Комментарий = "Автоматическое снятие резерва";
			
		КонецЕсли;
		
		//заполняем данными табличную часть "Товары" нового документа  
		ТекущиеТовары = КэшЗаказы.НайтиСтроки(Новый Структура("Заказ",ТекЗаказДляСписания.Заказ));
		Для Каждого ТекСтрокаТовара Из ТекущиеТовары Цикл
			НоваяСтрока = ТекущийДокумент.Товары.Добавить();
			НоваяСтрока.Номенклатура = ТекСтрокаТовара.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрокаТовара.ХарактеристикаНоменклатуры;
			НоваяСтрока.МестоРазмещения = ТекСтрокаТовара.МестоРазмещения;
			НоваяСтрока.ЗаказПокупателя = ТекСтрокаТовара.Заказ;
			Если РазрешениеСнятияРезервов = Перечисления.РезервыСпособыСписания.РазрешеноРезервыИзСвободногоЗапаса Тогда
				НоваяСтрока.Количество = ТекСтрокаТовара.РезервСвободныйОстаток;
			Иначе 
				НоваяСтрока.Количество = ТекСтрокаТовара.РезервОстаток;
			КонецЕсли;
			ТекущийДокумент.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
			Если (НЕ НоваяСтрока.Коэффициент = 0) И (НЕ НоваяСтрока.Коэффициент = 1) Тогда
				НоваяСтрока.Количество = Окр(НоваяСтрока.Количество/НоваяСтрока.Коэффициент, 3);
			КонецЕсли;
		КонецЦикла;
		
 		СтрокаИтоговая = ТаблицаЗаказовИтоговая.Добавить();
		СтрокаИтоговая.Заказ = ТекЗаказДляСписания.Заказ;		
		
	КонецЦикла;
	
	//записываем последний документ, если он не был записан в цикле
	Если НЕ обЗначениеНеЗаполнено(ТекущийДокумент) И ТекущийДокумент.Товары.Количество()>0 Тогда
		Попытка
			ТекущийДокумент.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);	
			Если НЕ ФоновыйРежим Тогда 
				Сообщить("Сформирован и проведен документ <" + Строка(ТекущийДокумент)+ ">");	
			КонецЕсли;
			//формируем данные для страницы "сформированные документы" 	
			Для Каждого СтрокаТоваров Из ТекущийДокумент.Ссылка.Товары Цикл
				СтрокаКэша = КэшСнятиеРезервов.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаКэша, СтрокаТоваров);
				СтрокаКэша.Ссылка = ТекущийДокумент.Ссылка;
			КонецЦикла;
			//создаем напоминание автору заказа покупателя
			Если СоздаватьНапоминание Тогда
				Обработки.АвтоматическоеСнятиеРезервов.СоздатьНапоминание(НаборЗаписейНапоминания,ТаблицаЗаказовИтоговая,ТекущийДокумент.Ссылка);
			КонецЕсли;
		Исключение
			Если НЕ ФоновыйРежим Тогда 
				Сообщить("Не удалось записать документ " + Строка(ТекущийДокумент));	
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	//записываем сформированные напоминания
	Если СоздаватьНапоминание Тогда
		Попытка  НаборЗаписейНапоминания.Записать(Истина); 
		Исключение 	
			Если НЕ ФоновыйРежим Тогда 
				Сообщить("Ошибка создания напоминаний о резервировании товаров: "+ОписаниеОшибки()); 
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Возврат КэшСнятиеРезервов;
	
КонецФункции

// Выполняет регламентное задание по автоматическому снятию резервов
//
Процедура ВыполнитьАвтоматическоеСнятиеРезервов(СтруктураПараметров) Экспорт
	
	// получим параметры
	КорректировкаЗаказа = Ложь;
	ФормированиеНапоминаний = Ложь;
	СписаниеРезервовСПредоплатой = Ложь;
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		СтруктураПараметров.Свойство("КорректировкаЗаказа",КорректировкаЗаказа);
		СтруктураПараметров.Свойство("ФормированиеНапоминаний",ФормированиеНапоминаний);
		СтруктураПараметров.Свойство("СписаниеРезервовСПредоплатой",СписаниеРезервовСПредоплатой);
	КонецЕсли;
	
	// инициализация таблиц с данными о заказах
	КэшЗаказы = ПолучитьТаблицуЗаказов();
	ТаблицаЗаказовДляСписания = Новый ТаблицаЗначений;
	ТаблицаЗаказовДляСписания.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРемонт"));
	ТаблицаЗаказовДляСписания.Колонки.Добавить("Контрагент",Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаЗаказовДляСписания.Колонки.Добавить("Корректировка",Новый ОписаниеТипов("Булево"));
	
	// заполнение таблиц данными о заказах в соответствии с параметрами
	ТекДата = ТекущаяДата();
	Для Каждого ТекЗаказ Из КэшЗаказы Цикл
		Если НЕ обЗначениеНеЗаполнено(ТекЗаказ.СрокСнятияРезерва) И ТекЗаказ.СрокСнятияРезерва < ТекДата Тогда
			Если ТекЗаказ.Оплата > 0 И НЕ СписаниеРезервовСПредоплатой Тогда
				Продолжить;
			КонецЕсли;
			Если ТаблицаЗаказовДляСписания.Найти(ТекЗаказ.Заказ, "Заказ") = Неопределено Тогда
				НоваяСтрокаТаблицы = ТаблицаЗаказовДляСписания.Добавить();
				НоваяСтрокаТаблицы.Заказ = ТекЗаказ.Заказ;
				НоваяСтрокаТаблицы.Контрагент = ТекЗаказ.Контрагент;
				НоваяСтрокаТаблицы.Корректировка = КорректировкаЗаказа;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// выполняем функцию, которая формирует документы снятия резервов 
	Результат = ВыполнитьСнятиеРезервов(ТаблицаЗаказовДляСписания,КэшЗаказы,ФормированиеНапоминаний,Истина);
	
КонецПроцедуры




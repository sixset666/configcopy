#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
	
Перем СписокПолейФильтров Экспорт; // список полей фильтров
Перем РеквизитыДляВыводаВРеестр Экспорт; // реквизиты для вывода в реестр
Перем РеквизитыДляВыводаВРеестрПоУмолчанию Экспорт; // реквизиты для вывода в реестр по умолчанию

Перем ВыполнитьВТранзакции Экспорт; // признак выполнения в транзакции
Перем ИндикацияОбработки Экспорт; // индикация обработки
Перем РасширенныйФильтрРеквизитов Экспорт; // расширенный фильтр реквизитов

Перем ДеревоДоступныхПолей Экспорт; // дерево доступных полей

Перем ЧислоДокументов Экспорт; // количество документов
Перем ПутьКДанным Экспорт; // путь к данным
Перем ИзменениеРеквизита Экспорт; // изменение реквизита

Перем СписокПомеченныхДокументов Экспорт; //для передачи извне
Перем СписокАктивныхФильтров Экспорт;     //для передачи извне

Перем СписокТЧ Экспорт; //список имен ТЧ, используемых для отбора
Перем СписокФильтровПоУмолчанию Экспорт; //список фильтров по организации, подразделению, автору и другим реквизитам, доступным для всех документов


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА
//Параметры отбора

	
// Процедура заполняет список видов документов всеми возможными 
Процедура ЗаполнитьСписокВидовДокументов(СписокВидыДокументов) Экспорт
	Для Каждого Объект Из Метаданные.Документы Цикл
		СписокВидыДокументов.Добавить(Объект, Объект.Представление());
	КонецЦикла; 
	СписокВидыДокументов.СортироватьПоПредставлению();
КонецПроцедуры
	
// Процедура заполняет список фильтров значениями
Процедура ЗаполнитьСписокФильтров(СписокФильтров) Экспорт
	//установим доступные поля по списку полей
	ПоляНастройки = СписокФильтров.ПолучитьДоступныеПоля();
	Для Каждого ТекФильтр Из СписокПолейФильтров Цикл
		НовоеПолеНастройки = ПоляНастройки.Добавить(ТекФильтр.Значение, ТекФильтр.Представление, ТекФильтр.ОписаниеТипов);
		НовоеПолеНастройки.Отбор = Истина;
	КонецЦикла;
	//покажем в таблице фильтров все доступные поля со снятой пометкой
	Для Каждого ТекПоле Из ПоляНастройки Цикл
		СписокФильтров.Добавить(ТекПоле.Имя);
	КонецЦикла;
КонецПроцедуры
	
// Процедура заполняет список документов по установленным настройкам/отборам
Процедура ЗаполнитьСписокДокументов(СписокВидыДокументов, ОтборФильтры, ТаблДокументы) Экспорт
	//массив видов документов
	ВидыДокументов = Новый Массив;
	Для Каждого ТекВид Из СписокВидыДокументов Цикл
		Если ТекВид.Пометка Тогда
			ВидыДокументов.Добавить(ТекВид.Значение);
		КонецЕсли;
	КонецЦикла;
	Если ВидыДокументов.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	//Формирование и выполнение запроса по документам
	ТекстЗапроса = "";
	СписокРеквизитов = Новый СписокЗначений;
	
	//сформируем список полей, принадлежащих нескольким ТЧ, для которых нужно делать объединение
	СписокПовтПолей = Новый СписокЗначений;
	Для Каждого Фильтр Из ОтборФильтры Цикл
		Если Найти(Фильтр.ПутьКДанным,"_ТЧ")<>0 Тогда
			СписокПовтПолей.Добавить(Фильтр.ПутьКДанным);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекВидДокумента Из ВидыДокументов Цикл
		// сформируем список реквизитов объекта
		СписокРеквизитов.Очистить();
		Для Каждого ТекРеквизит Из ТекВидДокумента.Реквизиты Цикл
			СписокРеквизитов.Добавить(ТекРеквизит.Имя);
		КонецЦикла;
		СписокРеквизитов.Добавить("Проведен");
		СписокРеквизитов.Добавить("ПометкаУдаления");          
		
		ТекстЗапросаПоДокументу = "  
		|ВЫБРАТЬ
		|	" + ТекВидДокумента.Имя + ".Ссылка КАК Ссылка,
		|	""" + ТекВидДокумента.Синоним + """ КАК Представление,
		|	" + ТекВидДокумента.Имя + ".Дата КАК Дата,
		|	" + ТекВидДокумента.Имя + ".Организация КАК Организация,
		|	" + ТекВидДокумента.Имя + ".ПодразделениеКомпании КАК Подразделение,
		|	" + ТекВидДокумента.Имя + ".Автор КАК Автор
		|ИЗ
		|	Документ." + ТекВидДокумента.Имя + " КАК " + ТекВидДокумента.Имя;
		
		Для Каждого ТекОтбор Из ОтборФильтры Цикл
			Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
			Если Найти(ТекОтбор.ПутьКДанным,"С_")<>0 Тогда
				ТекстЗапросаПоДокументу = ТекстЗапросаПоДокументу + "
				|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК Таблица_"+ТекОтбор.ПутьКДанным+"
				|		ПО " + ТекВидДокумента.Имя + ".Ссылка = Таблица_"+ТекОтбор.ПутьКДанным+".Объект
				|		И (Таблица_"+ТекОтбор.ПутьКДанным+".Свойство = &"+ТекОтбор.ПутьКДанным+")";
			КонецЕсли;
		КонецЦикла;
		
		
		ТекстЗапросаПоДокументу = ТекстЗапросаПоДокументу + Символы.ПС+"ГДЕ 
		|   (" + ТекВидДокумента.Имя + ".Дата МЕЖДУ &ДатаНач И &ДатаКон)"; 
		Для Каждого ТекОтбор Из ОтборФильтры Цикл
			// проверим на галочку
			Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
			
			//формируем строку условия для реквизитов ТЧ
			Если Найти(ТекОтбор.ПутьКДанным,"_Т_")<>0 Тогда
				//проверка - не стоит ли фильтр по нескольким разным ТЧ (*.отбор) - эти ситуации обрабатываются отдельно
				Если Найти(ТекОтбор.ПутьКДанным,"_ТЧ")=0 Тогда
					//обычная обработка
					СтрокаСравнения = отСформироватьСтрокуВидаСравнения(ТекОтбор.ВидСравнения);
					Если СтрокаСравнения = "" Тогда Продолжить; КонецЕсли;
					ПутьКДанным = ТекОтбор.ПутьКДанным; 
					ПутьКДанным = СтрЗаменить(ПутьКДанным,"_Т_",".");
					СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "#", ТекВидДокумента.Имя +"." + ПутьКДанным);
					ПутьКДанным = Прав(ТекОтбор.ПутьКДанным,СтрДлина(ТекОтбор.ПутьКДанным)-2);
					ПутьКДанным = СтрЗаменить(ПутьКДанным,"_Т_","");
					ПутьКДанным = СтрЗаменить(ПутьКДанным,".","");
					СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "&", "&" + ПутьКДанным);
					// добавим условия по текущему виду документа
					ТекстЗапросаПоДокументу = ТекстЗапросаПоДокументу + "
					|	И (" + СтрокаСравнения + ")";
				КонецЕсли;
			ИначеЕсли Найти(ТекОтбор.ПутьКДанным,"С_")=0 Тогда
				// сформируем строку сравнения по виду сравнения отбора
				СтрокаСравнения = отСформироватьСтрокуВидаСравнения(ТекОтбор.ВидСравнения);
				Если СтрокаСравнения = "" Тогда Продолжить; КонецЕсли;
				СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "#", ТекВидДокумента.Имя + "." + ТекОтбор.ПутьКДанным);
				ПутьКДанным = Прав(ТекОтбор.ПутьКДанным,СтрДлина(ТекОтбор.ПутьКДанным)-2);
				ПутьКДанным = СтрЗаменить(ПутьКДанным,".","");
				СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "&", "&" + ПутьКДанным);
				// добавим условия по текущему виду документа
				ТекстЗапросаПоДокументу = ТекстЗапросаПоДокументу + "
				|	И (" + СтрокаСравнения + ")";
			Иначе
				СтрокаСравнения = отСформироватьСтрокуВидаСравнения(ТекОтбор.ВидСравнения);
				Если СтрокаСравнения = "" Тогда Продолжить; КонецЕсли;
				СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "#","ЕстьNull(Таблица_"+ТекОтбор.ПутьКДанным+".Значение,0)");
				СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "&", "&" + ТекОтбор.ПутьКДанным+"Значение");
				ТекстЗапросаПоДокументу = ТекстЗапросаПоДокументу + "
				|	И (" + СтрокаСравнения + ")";
			КонецЕсли;
		КонецЦикла;
		
		//сохраним сформированный кусок текста запроса, чтобы добавить к нему условия
		Для Каждого ПовтПоле Из СписокПовтПолей Цикл
			//обработка для нескольких ТЧ
			Для Каждого ИмяТЧ Из СписокТЧ Цикл
				МетаданныеТЧ = ТекВидДокумента.ТабличныеЧасти;
				МетаданноеТЧ = МетаданныеТЧ.Найти(ИмяТЧ.Значение);
				Если МетаданноеТЧ <> Неопределено Тогда							
					СтрокаСравнения = отСформироватьСтрокуВидаСравнения(ТекОтбор.ВидСравнения);
					Если СтрокаСравнения = "" Тогда Продолжить; КонецЕсли;
					ПутьКДанным = ТекОтбор.ПутьКДанным; 
					ПутьКДанным = СтрЗаменить(ПутьКДанным,"_ТЧ",ИмяТЧ.Значение);
					ПутьКДанным = СтрЗаменить(ПутьКДанным,"_Т_",".");
					СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "#", ТекВидДокумента.Имя +"." + ПутьКДанным);
					ПутьКДанным = Прав(ТекОтбор.ПутьКДанным,СтрДлина(ТекОтбор.ПутьКДанным)-2);
					ПутьКДанным = СтрЗаменить(ПутьКДанным,"_Т_","");
					ПутьКДанным = СтрЗаменить(ПутьКДанным,".","");
					СтрокаСравнения = СтрЗаменить(СтрокаСравнения, "&", "&" + ПутьКДанным);
                    ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоДокументу + "
					|	И (" + СтрокаСравнения + ") " + "
					|ОБЪЕДИНИТЬ ВСЕ";
				КонецЕсли;	
			КонецЦикла;	
		КонецЦикла;
		Если СписокПовтПолей.Количество() = 0 Тогда	
			ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоДокументу + "
			|ОБЪЕДИНИТЬ ВСЕ";
		КонецЕсли;	
	КонецЦикла;
	ТекстЗапроса = Сред(ТекстЗапроса, 2, СтрДлина(ТекстЗапроса) - 16);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаНач", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(?(ДатаКонца = Дата("00010101000000"), ТекущаяДата(), ДатаКонца)));
	Для Каждого ТекОтбор Из ОтборФильтры Цикл
		Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		Если Найти(ТекОтбор.ПутьКДанным,"_Т_")<>0 Тогда
			ПутьКДанным = Прав(ТекОтбор.ПутьКДанным,СтрДлина(ТекОтбор.ПутьКДанным)-2);
			ПутьКДанным = СтрЗаменить(ПутьКДанным,"_Т_","");
			ПутьКДанным = СтрЗаменить(ПутьКДанным,".","");
			Запрос.УстановитьПараметр(ПутьКДанным, ТекОтбор.Значение);
		ИначеЕсли Найти(ТекОтбор.ПутьКДанным,"С_")=0 Тогда
			ПутьКДанным = Прав(ТекОтбор.ПутьКДанным,СтрДлина(ТекОтбор.ПутьКДанным)-2);
			ПутьКДанным = СтрЗаменить(ПутьКДанным,".","");
			Запрос.УстановитьПараметр(ПутьКДанным, ТекОтбор.Значение);
		Иначе
			Свойство = ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоКоду(Прав(ТекОтбор.ПутьКДанным,СтрДлина(ТекОтбор.ПутьКДанным)-2));
			Запрос.УстановитьПараметр(ТекОтбор.ПутьКДанным,Свойство);
			Запрос.УстановитьПараметр(ТекОтбор.ПутьКДанным+"Значение",ТекОтбор.Значение);
		КонецЕсли;
	КонецЦикла;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	РезультатЗапроса.Сортировать("Дата Возр,Ссылка Возр");
	
	//заполняем таблицу документов
	Если РезультатЗапроса.Количество() > 0 Тогда
		Для Каждого СтрокаДокумент Из РезультатЗапроса Цикл
			ДобавитьНаПечать(СтрокаДокумент, ТаблДокументы);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
	
// Проверяет есть ли уже элемент в списке печатаемых и, если нет - добавляет
Процедура ДобавитьНаПечать(СтрокаДокумент, ДокументыНаПечать)
	Если СтрокаДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	НоваяСтрока = ДокументыНаПечать.Добавить();
	НоваяСтрока.Документ = СтрокаДокумент.Ссылка;
	НоваяСтрока.Вид   = СтрокаДокумент.Представление;
	НоваяСтрока.Дата  = Формат(СтрокаДокумент.Дата, "ДФ = дд.ММ.гггг");
	НоваяСтрока.Время = Формат(СтрокаДокумент.Дата, "ДФ = чч:мм:сс");
	НоваяСтрока.Организация = СтрокаДокумент.Организация;
	НоваяСтрока.Подразделение = СтрокаДокумент.Подразделение;
	НоваяСтрока.Автор = СтрокаДокумент.Автор;
	НоваяСтрока.Обработан = Ложь;
КонецПроцедуры

#Если Клиент Тогда
		
// Выводит сформированный табличный документ в стандартную общую форму
Процедура ВывестиТабДок(ТабДок, ВидДокумента = "")
		ПечатнаяФорма = ПолучитьОбщуюФорму("ПечатнаяФормаДокументов", ,ВидДокумента);
		ПечатнаяФорма.Объект = Неопределено;
		ПечатнаяФорма.ОбъектПредставление = ВидДокумента;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ОтображатьЗаголовки	= Ложь;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ОтображатьСетку		= Ложь;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",глПрава);
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",глПрава);
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ПолеСлева = 9;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ПолеСправа = 6;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДок);
		ПечатнаяФорма.Открыть();	
	КонецПроцедуры

Функция НазначениеШК(Документ,ДокОбъект)
	РисунокШК=Неопределено; ШК="";
	//Проверим, есть ли на печатной форме ШК
	Попытка
		РисунокШК=Документ.Рисунки.ШК;
		//Если для данного документа ведется штрихкодирование
		Если Метаданные.РегистрыСведений.ШтрихКоды.Измерения.Объект.Тип.СодержитТип(ТипЗнч(ДокОбъект.Ссылка)) Тогда
			//Получим ШК документа
			ОбработкаОбъектШтрихКоды=Обработки.ШтрихКоды.Создать();
			ОбработкаОбъектШтрихКоды.Объект=ДокОбъект;
			ОбработкаОбъектШтрихКоды.ПрочитатьЗаполнитьШтрихКоды();
			Если ОбработкаОбъектШтрихКоды.Состав.Количество()>0 Тогда
				ШК=ОбработкаОбъектШтрихКоды.Состав[0].ШтрихКод;
			Иначе
				ШК="";
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	Если РисунокШК<>Неопределено Тогда
		//На печатной форме есть рисунок ШК
		Попытка
			Если ПустаяСтрока(ШК) Тогда
				//Если ШК не задан, удалим его с печатной формы
				Документ.Рисунки.Удалить(РисунокШК);
			Иначе
				//Вывод ШК на печатную форму
				ПрефиксВесовогоШК=Константы.ПрефиксВесовогоШК.Получить();
				Попытка
					ПрефиксШтрихКода=Число(Лев(ШК,2));
				Исключение
					ПрефиксШтрихКода=0;
				КонецПопытки;
				//РисунокШК.Объект.ТипКода=Число(НЕ (СтрДлина(ШК)=8 И ПрефиксШтрихКода<>ПрефиксВесовогоШК));
				зф1CBarCodУстановитьЗначение(РисунокШК.Объект,"ТипКода",Число(НЕ (СтрДлина(ШК)=8 И ПрефиксШтрихКода<>ПрефиксВесовогоШК)));
				Если РисунокШК.Объект.ТипКода=0 Тогда
					ДлинаШК=8;
				Иначе
					ДлинаШК=13;
				КонецЕсли; 
				ШК=ШК+"0000000000000"; ШК=Лев(ШК,ДлинаШК-1+РисунокШК.Объект.СодержитКС);
				//РисунокШК.Объект.Сообщение=ШК;
				зф1CBarCodУстановитьЗначение(РисунокШК.Объект,"Сообщение",ШК);
			КонецЕсли; 
		Исключение
		КонецПопытки;
	КонецЕсли;
	Возврат Документ;
КонецФункции
// Выводит на печать список документов
	//ВидПечати = 1, если печать в один ТабДок (по каждому виду документа), = 9, если печать в разные ТабДоки
Процедура ВывестиНаПечать(СписокДокументов, ЗаголовокКомментария, РезультатТекСтрока, ВидПечати=1) Экспорт
		ТабДок = Новый ТабличныйДокумент;
		ДокОбъект = Неопределено;
		НомерДок = 1;
		Если ВидПечати = 1 Тогда //печатаем всё в одну кучу по видам документа
			//отсортируем список документов по виду
			СписокДокументов1 = СписокДокументов.Скопировать();
			СписокДокументов1.Сортировать("Вид, Дата, Время");
			ВидДокументаПред = Неопределено;
			Для Каждого ТекСтрокаДокумент Из СписокДокументов1 Цикл
				//проверим, прерывался ли процесс пользователем
				ЗаголовокКомментария = "";
				ОбработкаПрерыванияПользователя();
				ЗаголовокКомментария = "Выполняется обработка...";
					
				Если НЕ ТекСтрокаДокумент.Пометка Тогда Продолжить; КонецЕсли;
				//проверим не изменился ли вид документа
				Если ТекСтрокаДокумент.Вид <> ВидДокументаПред Тогда
					Если ВидДокументаПред <> Неопределено Тогда //нужно показать предыдущий, прежде чем начинать новый
						ВывестиТабДок(ТабДок, ВидДокументаПред);
					КонецЕсли;
					ВидДокументаПред = ТекСтрокаДокумент.Вид;
					ТабДок = Новый ТабличныйДокумент;
					НомерДок = 1;
				КонецЕсли;
				
				//получаем документ-объект
				ДокОбъект = ТекСтрокаДокумент.Документ.ПолучитьОбъект();
					
				//получаем печатную форму
				НазваниеПечатнойФормы = ВосстановитьЗначение(СокрЛП(ТекСтрокаДокумент.Документ.Метаданные().Имя) + "ПечатнаяФорма");
					
				// Найдем имя макета по названию (значение по ключу)
				Попытка
					ПечатныеФормы = Документы[ДокОбъект.Метаданные().Имя].ПолучитьСписокПечатныхФорм(ДокОбъект.Ссылка);
				Исключение
					ПечатныеФормы = ДокОбъект.ПолучитьСписокПечатныхФорм();
				КонецПопытки;
				
				ПечатнаяФорма = Неопределено;
				Если НЕ обЗначениеНеЗаполнено(НазваниеПечатнойФормы) Тогда 
					ПечатнаяФорма=дкПолучитьПечатнуюФорму(ПечатныеФормы, НазваниеПечатнойФормы);
				КонецЕсли;
				
				ЭтоВнешняя = Ложь;
				Если ПечатнаяФорма = Неопределено Тогда
					ПечатнаяФорма = ПолучитьВнешнююПечатнуюФорму(НазваниеПечатнойФормы, ТекСтрокаДокумент.Документ.Метаданные().Имя);
					ЭтоВнешняя = НЕ обЗначениеНеЗаполнено(ПечатнаяФорма);
				КонецЕсли;
				
				// если и сейчас не получили печатную форму, то возьмем первую в списке (она является основной)
				Если ПечатнаяФорма=Неопределено Тогда 
					Для Каждого ТекМакет Из ПечатныеФормы Цикл
						ПечатнаяФорма=ТекМакет.Ключ; Прервать;
					КонецЦикла;
				КонецЕсли;
				//выведем горизонтальный разделитель между документами
				Если НомерДок <> 1 Тогда
					ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				//печатаем
				Попытка
					ТабДокВрем = Новый ТабличныйДокумент;
					Если НЕ ЭтоВнешняя Тогда
						ТабДокВрем = Вычислить("ДокОбъект.Печать" + ПечатнаяФорма + "(ТабДокВрем)");
					Иначе 
						ТабДокВрем = ПечатнаяФорма.ПолучитьОбъект().Печать(ДокОбъект,ТабДокВрем);
					КонецЕсли;
					// назначим ШК
					ТабДокВрем = НазначениеШК(ТабДокВрем,ДокОбъект);
					ТабДок.Вывести(ТабДокВрем);
						
					ТекСтрокаДокумент_ = СписокДокументов.Найти(ТекСтрокаДокумент.Документ, "Документ");
					ТекСтрокаДокумент_.Обработан = Истина;
					РезультатТекСтрока = ТекСтрокаДокумент_;
				Исключение
				КонецПопытки;
					
				НомерДок = НомерДок + 1;
			КонецЦикла;
			ВывестиТабДок(ТабДок, ТекСтрокаДокумент.Вид);
				
		ИначеЕсли ВидПечати = 9 Тогда //печатаем каждый документ отдельно
			Для Каждого ТекСтрокаДокумент Из СписокДокументов Цикл
				//проверим, прерывался ли процесс пользователем
				ЗаголовокКомментария = "";
				ОбработкаПрерыванияПользователя();
				ЗаголовокКомментария = "Выполняется обработка...";
					
				Если НЕ ТекСтрокаДокумент.Пометка Тогда Продолжить; КонецЕсли;
				//получаем документ-объект
				ДокОбъект = ТекСтрокаДокумент.Документ.ПолучитьОбъект(); 
				//выводим
				Попытка
					ДокОбъект.Печать();
						
					ТекСтрокаДокумент_ = СписокДокументов.Найти(ТекСтрокаДокумент.Документ, "Документ");
					ТекСтрокаДокумент_.Обработан = Истина;
					РезультатТекСтрока = ТекСтрокаДокумент_;
				Исключение
					Продолжить;
				КонецПопытки;
			КонецЦикла;	
		КонецЕсли;
	КонецПроцедуры
		
// Выводит реестр документов (фрагмент доработан из дкОсновныеДействияФормыРеестр())
Процедура ВывестиРеестрДокументов(СписокДокументов, ЗаголовокКомментария, РезультатТекСтрока) Экспорт
			
		ВыбратьВалюту = Ложь;	
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", глПрава);
		КолонкаСуммы = РеквизитыДляВыводаВРеестр.НайтиПоЗначению("Сумма");
			
		Если КолонкаСуммы.Пометка Тогда
			Если обПраво("ЗапросВалютыПечатногоДокумента") Тогда
				ФормаВыбораВалюты = Справочники.Валюты.ПолучитьФормуВыбора();
				Попытка
					Если РезультатТекСтрока<>Неопределено Тогда
						ФормаВыбораВалюты.НачальноеЗначениеВыбора=РезультатТекСтрока.Документ.ВалютаДокумента;
					КонецЕсли; 
				Исключение
				КонецПопытки; 
				ВыбВалютаРеестра = ФормаВыбораВалюты.ОткрытьМодально();
			Иначе
				ВыбВалютаРеестра = Неопределено;
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(ВыбВалютаРеестра) Тогда
				ВыбратьВалюту = Истина;	
				СтруктураКурсаВалютыРеестра = обКурсДляВалюты(ВыбВалютаРеестра, ТекущаяДата());
				КурсВалютыРеестра = СтруктураКурсаВалютыРеестра.Курс/?(СтруктураКурсаВалютыРеестра.Кратность=0,1,СтруктураКурсаВалютыРеестра.Кратность);
			КонецЕсли;
		КонецЕсли;
			
		ТаблицаКолонок = Новый ТаблицаЗначений;
		ТаблицаКолонок.Колонки.Добавить("Имя");
		ТаблицаКолонок.Колонки.Добавить("Представление");
		ТаблицаКолонок.Колонки.Добавить("Формат");
		ТаблицаКолонок.Колонки.Добавить("ЭтоПоказатель");
			
		СтруктураИтоговыхКолонок = Новый Структура();
		СтруктураКартинок = Неопределено;
			
		Для Каждого ТекРеквизит Из РеквизитыДляВыводаВРеестр Цикл
			Если НЕ ТекРеквизит.Пометка Тогда Продолжить; КонецЕсли;
				
			НоваяСтрока = ТаблицаКолонок.Добавить();
			НоваяСтрока.Имя = ТекРеквизит.Представление;
			НоваяСтрока.Представление = ТекРеквизит.Значение;
			Если ТекРеквизит.Значение = "Сумма" Тогда
				НоваяСтрока.Формат = ФорматВыводаСуммы;
				НоваяСтрока.ЭтоПоказатель = Истина;
				Если ВыбратьВалюту Тогда
					НоваяСтрока = ТаблицаКолонок.Добавить();
					НоваяСтрока.Имя = ТекРеквизит.Представление+"ВВалютеРеестра";
					НоваяСтрока.Представление = ТекРеквизит.Значение+ " (" + ВыбВалютаРеестра.Наименование+")";
					НоваяСтрока.Формат = ФорматВыводаСуммы;
					НоваяСтрока.ЭтоПоказатель = Истина;
					СтруктураИтоговыхКолонок.Вставить(ТекРеквизит.Представление+"ВВалютеРеестра");
				КонецЕсли;
			Иначе
				НоваяСтрока.Формат = "";
				НоваяСтрока.ЭтоПоказатель = Ложь;
			КонецЕсли;
				
			Если ТекРеквизит.Представление = "Картинка" Тогда
				СтруктураКартинок = Новый Соответствие();
				СтруктураКартинок.Вставить(1, БиблиотекаКартинок.ДокументПроведен);
				СтруктураКартинок.Вставить(2, БиблиотекаКартинок.ДокументПомеченНаУдаление);
				СтруктураКартинок.Вставить(3, БиблиотекаКартинок.ДокументНеПроведен);
				НоваяСтрока.Представление = "";
				Продолжить;
			КонецЕсли;
				
		КонецЦикла;
			
		ПараметрыРеестра = Новый Структура();
		//заголовок
		ПараметрыРеестра.Вставить("ЗаголовокРеестра", "Реестр документов");
		//Период
		ПараметрыРеестра.Вставить("ДатаНачала", ДатаНачала);
		ПараметрыРеестра.Вставить("ДатаОкончания", ДатаКонца);
		//Выбранная валюта реестра
		Если ВыбратьВалюту Тогда
			ПараметрыРеестра.Вставить("ВалютаРеестра", ВыбВалютаРеестра.Наименование);
		КонецЕсли;
			
		ИсточникДанных = Новый ТаблицаЗначений;
		Для Каждого ТекКолонка Из ТаблицаКолонок Цикл
			Если ТекКолонка.ЭтоПоказатель Тогда
				ИсточникДанных.Колонки.Добавить(ТекКолонка.Имя, Новый ОписаниеТипов("Число"));
			Иначе
				ИсточникДанных.Колонки.Добавить(ТекКолонка.Имя);
			КонецЕсли;
		КонецЦикла;
			
		//строки
		Для Каждого ТекСтрокаДокумент Из СписокДокументов Цикл
			//проверим, прерывался ли процесс пользователем
			ЗаголовокКомментария = "";
			ОбработкаПрерыванияПользователя();
			ЗаголовокКомментария = "Выполняется обработка...";
			Если НЕ ТекСтрокаДокумент.Пометка Тогда Продолжить; КонецЕсли;
			НоваяСтрока = ИсточникДанных.Добавить();
			ТекДокумент = ТекСтрокаДокумент.Документ;
			Для Каждого ТекКолонка Из ТаблицаКолонок Цикл
				ЗначениеРеквизита = "";
				Попытка
					Если ТекКолонка.Имя = "СуммаДокументаВВалютеРеестра" Тогда
						ЗначениеРеквизита = ТекДокумент.СуммаДокумента*ТекДокумент.КурсДокумента;
					ИначеЕсли ТекКолонка.Имя = "Картинка" Тогда
						Если ТекДокумент.Проведен Тогда
							ЗначениеРеквизита = 1;
						ИначеЕсли ТекДокумент.ПометкаУдаления Тогда
							ЗначениеРеквизита = 2;
						Иначе
							ЗначениеРеквизита = 3;
						КонецЕсли;
					Иначе
						ЗначениеРеквизита = ТекДокумент[ТекКолонка.Имя];
					КонецЕсли;
				Исключение
				КонецПопытки;
				НоваяСтрока[ТекКолонка.Имя] = ЗначениеРеквизита;
			КонецЦикла;
			ТекСтрокаДокумент.Обработан = Истина;
		КонецЦикла;
			
		ТабДокумент = дкСформироватьРеестр(ПараметрыРеестра, ИсточникДанных, ТаблицаКолонок, СтруктураИтоговыхКолонок, СтруктураКартинок);
			
		ПечатнаяФорма		= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,Новый УникальныйИдентификатор());
		ПечатнаяФорма.Объект = Неопределено;
		ПечатнаяФорма.ОбъектПредставление = "Реестр документов";
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ОтображатьЗаголовки	= Ложь;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ОтображатьСетку		= Ложь;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",глПрава);
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",глПрава);
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ПолеСлева = 9;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ПолеСправа = 6;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ПечатнаяФорма.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
		ПечатнаяФорма.Открыть();
			
	КонецПроцедуры
#КонецЕсли
	
// Выполняет основные действия по обработке
Процедура ВыполнитьОбработку(СписокДокументов, ВидОбработки, флНеПрервать, ЗаголовокКомментария, РезультатТекСтрока, ДопПараметры = "") Экспорт
	ВидОбработкиТек = ВидОбработки;
	
	Если СписокДокументов.Количество() = 0 Тогда Возврат; КонецЕсли;
	СписокДокументовВрем = СписокДокументов.Скопировать();
	СписокДокументовВрем.Свернуть("Пометка");
	Если СписокДокументовВрем.Количество() = 1 И НЕ СписокДокументовВрем[0].Пометка Тогда Возврат; КонецЕсли;
	Если ВидОбработки = 0 Тогда
		#Если Клиент Тогда
			Предупреждение ("Выберите вид обработки!");
		#КонецЕсли
		Возврат;
	КонецЕсли;
	 	
	ИзменениеРегламентированногоУчета = Ложь;
	ОтношениеКРегламентированномуУчету = обПраво("ОтношениеКРегламентированномуУчету",глПрава);
	Если ОтношениеКРегламентированномуУчету = Перечисления.ОтношениеКРегламентированномуУчету.РедактированиеВсехДокументовИстина ИЛИ
		 ОтношениеКРегламентированномуУчету = Перечисления.ОтношениеКРегламентированномуУчету.РедактированиеВсехДокументовЛожь Тогда
		 ИзменениеРегламентированногоУчета = Истина;
	КонецЕсли;
	Если (НЕ ИзменениеРегламентированногоУчета) И (ВидОбработки = 10 ИЛИ ВидОбработки = 11) Тогда
		#Если Клиент Тогда
			Предупреждение ("Изменение отношения документов к регламентированному учету запрещено!");
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	Если ВидОбработки = 15 Тогда
		Если ПутьКДанным = "ПометкаУдаления" Тогда
			ВидОбработки = ?(ПолеВводаРеквизит,6,7);
		КонецЕсли;
		Если ПутьКДанным = "Проведен" Тогда
			ВидОбработки = ?(ПолеВводаРеквизит,5,3);
		КонецЕсли;
	КонецЕсли;
	
	//сбросим флажки "Обработан" для всех документов
	Для Каждого ТекДок Из СписокДокументов Цикл
		Если ТекДок.Обработан Тогда
			ТекДок.Обработан = Ложь;
		КонецЕсли;
	КонецЦикла;
		
	// вывод на печать
	Если ВидОбработки=1 ИЛИ ВидОбработки=9 Тогда
		#Если Клиент Тогда
			ВывестиНаПечать(СписокДокументов, ЗаголовокКомментария, РезультатТекСтрока, ВидОбработки);
		#КонецЕсли
		Возврат;
	КонецЕсли;
		
	// вывод реестра
	Если ВидОбработки=2 Тогда
		#Если Клиент Тогда
			ВывестиРеестрДокументов(СписокДокументов, ЗаголовокКомментария, РезультатТекСтрока);
		#КонецЕсли
		Возврат;
	КонецЕсли;
		
	//ТабДок = Новый ТабличныйДокумент;
	//Макет = ПолучитьМакет("Отчет");
		
	//ОбластьИмяДокумента      = Макет.ПолучитьОбласть("ИмяДокумента");
	//ОбластьРеквизиты         = Макет.ПолучитьОбласть("Реквизиты");
	//ОбластьИмяТабличнойЧасти = Макет.ПолучитьОбласть("ИмяТабличнойЧасти");
	//ОбластьЗаголовокТаблицы  = Макет.ПолучитьОбласть("ЗаголовокТаблицы|КолонкаТаблицы");
	//ОбластьТаблица           = Макет.ПолучитьОбласть("Таблица|КолонкаТаблицы");
	//ОбластьПодвал            = Макет.ПолучитьОбласть("Подвал");
		
	//ОбластьЗаголовокТаблицыОтступ = Макет.ПолучитьОбласть("ЗаголовокТаблицы|Отступ");
	//ОбластьТаблицаОтступ = Макет.ПолучитьОбласть("Таблица|Отступ");
		
	// отключим вывод сообщений об ошибках в информационное поле
	#Если Клиент Тогда
		БылоВыводитьСообщениеОбОшибкахВИнформационноеПоле=глПрава[ПланыВидовХарактеристик.ПраваИНастройки.ВыводитьСообщениеОбОшибкахВИнформационноеПоле];
		глПрава[ПланыВидовХарактеристик.ПраваИНастройки.ВыводитьСообщениеОбОшибкахВИнформационноеПоле]=Ложь;
	#КонецЕсли
	
	Если ВыполнитьВТранзакции Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	КоличДок = 0;
	НомерДокПечать = 1;
	Для Каждого Стр Из СписокДокументов Цикл
		//проверим, прерывался ли процесс пользователем
		ЗаголовокКомментария = "";
		ОбработкаПрерыванияПользователя();
		ЗаголовокКомментария = "Выполняется обработка...";
			
		Док = Стр.Документ;
		Объект = Док.ПолучитьОбъект();
			
		//убираем лишние варианты
		Если НЕ Стр.Пометка Тогда Продолжить; КонецЕсли;
		Если Док = Неопределено Тогда
			Продолжить;
		ИначеЕсли ВидОбработки=3 И НЕ Док.Проведен Тогда // отмена проведения
			Продолжить;
		ИначеЕсли ВидОбработки=4 И НЕ Док.Проведен Тогда // перепроведение
			Продолжить;
		ИначеЕсли ВидОбработки=5 И ( Док.Проведен ИЛИ Док.ПометкаУдаления) Тогда // проведение непроведенных
			Продолжить;
		ИначеЕсли ВидОбработки=6 И Док.ПометкаУдаления Тогда // пометка на удаление
			Продолжить;
		ИначеЕсли ВидОбработки=7 И НЕ Док.ПометкаУдаления Тогда // отмена пометки на удаление
			Продолжить;               
		ИначеЕсли ВидОбработки=8 И Док.ПометкаУдаления Тогда // проведение на сервере 1с-предприятия
			Продолжить;		
		КонецЕсли;
			
		КоличДок = КоличДок + 1;
			
		//для всех видов обработки кроме печати выводим только название дока
		//ОбластьИмяДокумента.Параметры.ИмяДокумента = Строка(Док);
		//ТабДок.Вывести(ОбластьИмяДокумента);
		
			
		Попытка
			Если ВидОбработки=3 Тогда // отмена проведения
				Объект.Записать(РежимЗаписиДокумента.ОтменаПроведения, РежимПроведенияДокумента.Неоперативный);
			ИначеЕсли ВидОбработки=4 Тогда // перепроведение
				Объект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ИначеЕсли ВидОбработки=5 Тогда // проведение непроведенных
				Объект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ИначеЕсли ВидОбработки=6 Тогда // пометка на удаление
				Объект.УстановитьПометкуУдаления(Истина);
			ИначеЕсли ВидОбработки=7 Тогда // отмена пометки на удаление
				Объект.УстановитьПометкуУдаления(Ложь);
			ИначеЕсли ВидОбработки=8 Тогда // проведение документа на сервере 1с-предприятия
				дкПровестиНаСервере(Док);
			ИначеЕсли ВидОбработки=10 ИЛИ ВидОбработки=11 Тогда // Установка/снятие флага регламентированного учета
				Объект.РегламентированныйУчет=(ВидОбработки=10);
				Объект.Записать(РежимЗаписиДокумента.Запись);
			ИначеЕсли  ВидОбработки = 15 Тогда
				Ссылка = Объект.Ссылка;
				Попытка
					
					Объект[ПутьКДанным] = ПолеВводаРеквизит;	
					Если Объект.Проведен Тогда
						Объект.Записать(РежимЗаписиДокумента.Проведение);
					Иначе
						Объект.Записать(РежимЗаписиДокумента.Запись);
					КонецЕсли;
				Исключение
					Если ВыполнитьВТранзакции Тогда						
						Перейти ~ОткатТранзакции;
					Иначе
						Сообщить("Невозможно изменить реквизит "+ПутьКДанным+" у документа "+Ссылка);
					КонецЕсли;
				КонецПопытки;
			КонецЕсли;
				
			СтрДок = СписокДокументов.Найти(Док, "Документ");
			СтрДок.Обработан = Истина;
			Если ИндикацияОбработки Тогда
				СтрДок.Картинка = БиблиотекаКартинок.ДокументПомеченНаУдаление.ПолучитьДвоичныеДанные();
				РезультатТекСтрока = СтрДок;				
				
			КонецЕсли;
		Исключение
			Если ВыполнитьВТранзакции Тогда
				Перейти ~ОткатТранзакции;
			КонецЕсли;             
			
			Если НЕ флНеПрервать Тогда
				Возврат;
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	Если ВыполнитьВТранзакции Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	#Если Клиент Тогда
		// вернем прежнее значение
		глПрава[ПланыВидовХарактеристик.ПраваИНастройки.ВыводитьСообщениеОбОшибкахВИнформационноеПоле]=БылоВыводитьСообщениеОбОшибкахВИнформационноеПоле;
	#КонецЕсли
	
	ВидОбработки = ВидОбработкиТек;
	Возврат;
	~ОткатТранзакции:
	ОтменитьТранзакцию();
	Сообщить("Отмена транзакции. Действие не выполнено!", СтатусСообщения.Важное);
	ВидОбработки = ВидОбработкиТек;
		
КонецПроцедуры
	
Функция ПолучитьВнешнююПечатнуюФорму(ИмяПечФормы, ВидДокумента)
	Результат = Неопределено;
	
	// посмотрим есть ли у нас имя формы
	Если обЗначениеНеЗаполнено(ИмяПечФормы) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВнешниеПечатныеФормыПринадлежность.Ссылка
	|ИЗ
	|	Справочник.ВнешниеПечатныеФормы.Принадлежность КАК ВнешниеПечатныеФормыПринадлежность
	|ГДЕ
	|	ВнешниеПечатныеФормыПринадлежность.Ссылка.Наименование = &Наименование
	|	И ВнешниеПечатныеФормыПринадлежность.ИмяОбъекта = &ИмяОбъекта
	|	И ВнешниеПечатныеФормыПринадлежность.Ссылка.ВидОбработки = &ВидОбработки
	|	И ВнешниеПечатныеФормыПринадлежность.Ссылка.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("ВидОбработки", Перечисления.ВидыВнешнихОбработок.ПечатнаяФорма);
	Запрос.УстановитьПараметр("Наименование", СокрЛП(СтрЗаменить(ИмяПечФормы,"(внешняя)","")));
	Запрос.УстановитьПараметр("ИмяОбъекта"  , "Документ." + ВидДокумента);
	ВыборкаПЧ = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаПЧ.Следующий() Тогда
		Результат = ВыборкаПЧ.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

//////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

СписокАктивныхФильтров = Новый СписокЗначений;
СписокПомеченныхДокументов = Новый СписокЗначений;

//формируем список реквизитов для вывода в реестр документов (пометка - вывод по умолчанию)
//после подчеркивания - ширина выводимой колонки
РеквизитыДляВыводаВРеестр = Новый СписокЗначений;
РеквизитыДляВыводаВРеестр.Добавить("Картинка", "Картинка", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Документ", "Ссылка", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Операция", "ХозОперация", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Сумма", "СуммаДокумента", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Валюта", "ВалютаДокумента", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Курс", "КурсДокумента", Ложь);
РеквизитыДляВыводаВРеестр.Добавить("Организация", "Организация", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Подразделение", "ПодразделениеКомпании", Ложь);
РеквизитыДляВыводаВРеестр.Добавить("Автор", "Автор", Истина);
РеквизитыДляВыводаВРеестр.Добавить("Контрагент", "Контрагент", Ложь);
РеквизитыДляВыводаВРеестр.Добавить("Договор", "ДоговорВзаиморасчетов", Ложь);
РеквизитыДляВыводаВРеестр.Добавить("Склад", "СкладКомпании", Ложь);
РеквизитыДляВыводаВРеестр.Добавить("Касса", "КассаКомпании", Ложь);
РеквизитыДляВыводаВРеестр.Добавить("ТипЦен", "ТипЦен", Ложь);
РеквизитыДляВыводаВРеестрПоУмолчанию = Новый СписокЗначений;
РеквизитыДляВыводаВРеестрПоУмолчанию = РеквизитыДляВыводаВРеестр.Скопировать();
	
//формируем список доступных полей отбора по реквизитам
СписокПолейФильтров = Новый ТаблицаЗначений;
СписокПолейФильтров.Колонки.Добавить("Значение");
СписокПолейФильтров.Колонки.Добавить("Представление");
СписокПолейФильтров.Колонки.Добавить("ОписаниеТипов");

НоваяСтрока = СписокПолейФильтров.Добавить();
НоваяСтрока.Значение      = "Проведен";
НоваяСтрока.Представление = "Признак проведенности";
НоваяСтрока.ОписаниеТипов = Новый ОписаниеТипов("Булево");

НоваяСтрока = СписокПолейФильтров.Добавить();
НоваяСтрока.Значение      = "Автор";
НоваяСтрока.Представление = "Автор документа";
НоваяСтрока.ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.Пользователи");

НоваяСтрока = СписокПолейФильтров.Добавить();
НоваяСтрока.Значение      = "ХозОперация";
НоваяСтрока.Представление = "Хозяйственная операция";
НоваяСтрока.ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.ХозОперации");

НоваяСтрока = СписокПолейФильтров.Добавить();
НоваяСтрока.Значение      = "Организация"; 
НоваяСтрока.Представление = "Организация"; 
НоваяСтрока.ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.Организации"); 

НоваяСтрока = СписокПолейФильтров.Добавить(); 
НоваяСтрока.Значение      = "ПодразделениеКомпании";
НоваяСтрока.Представление = "Подразделение компании";
НоваяСтрока.ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании");

НоваяСтрока = СписокПолейФильтров.Добавить();
НоваяСтрока.Значение      = "ПометкаУдаления";
НоваяСтрока.Представление = "Пометка удаления";
НоваяСтрока.ОписаниеТипов = Новый ОписаниеТипов("Булево");

СписокФильтровПоУмолчанию = Новый СписокЗначений;
СписокФильтровПоУмолчанию.ЗагрузитьЗначения(СписокПолейФильтров.ВыгрузитьКолонку("Значение"));
#КонецЕсли



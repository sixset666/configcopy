Перем НаборСлучайных; // Набор случайных чисел
Перем ИндексСлучайных; // Индекс случайных чисел
Перем ПрефиксВесовогоШК; // Берется из константы ПрефиксВесовогоШК
Перем ПрефиксШтучногоШК; // Берется из константы ПрефиксШтучногоШК

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ШТРИХ КОДА МЕТОДОМ СЛУЧАЙНОЙ ПОСЛЕДОВАТЕЛЬНОСТИ 
// (не используется)

#Если Клиент Тогда

// Функция Округляет число до ближайшего верхнего целого
//
// Параметры:
//  Значение - число - значение для округления
//
// Возвращаемое значение:
//  ВремЗначение - число - результат округления.
//
Функция ОкрВверх(Значение)
	ВремЗначение = Цел(Значение);
	Если Значение > ВремЗначение Тогда
		Возврат ВремЗначение + 1;
	Иначе
		Возврат ВремЗначение;
	КонецЕсли;
КонецФункции // ОкрВверх()

// обновляет набор случайных индексов (для увеличения быстродействия)
//
//Процедура ОбновитьНаборСлучайных()
//	// инициализируем случайную последовательность
//	Если ИндексСлучайных=Неопределено ИЛИ НаборСлучайных=Неопределено Тогда
//		ИнициализацияСлучайнойПоследовательности();
//	КонецЕсли;
//	
//	НаборСлучайных[0] = НаборСлучайных[0]+0.0101;
//	Если НаборСлучайных[0]>=1 Тогда НаборСлучайных[0]=0.2354 КонецЕсли;
//	НаборСлучайных[1] = НаборСлучайных[1]+0.0101;
//	Если НаборСлучайных[1]>=1 Тогда НаборСлучайных[1]=0.5378 КонецЕсли;
//	НаборСлучайных[2] = НаборСлучайных[2]+0.0101;
//	Если НаборСлучайных[2]>=1 Тогда НаборСлучайных[2]=0.0932 КонецЕсли;
//	НаборСлучайных[3] = НаборСлучайных[3]+0.0101;
//	Если НаборСлучайных[3]>=1 Тогда НаборСлучайных[3]=0.9084 КонецЕсли;
//	НаборСлучайных[4] = НаборСлучайных[4]+0.0101;
//	Если НаборСлучайных[4]>=1 Тогда НаборСлучайных[4]=0.3490 КонецЕсли;
//	НаборСлучайных[5] = НаборСлучайных[5]+0.0101;
//	Если НаборСлучайных[5]>=1 Тогда НаборСлучайных[5]=0.3489 КонецЕсли;
//	НаборСлучайных[6] = НаборСлучайных[6]+0.0101;
//	Если НаборСлучайных[6]>=1 Тогда НаборСлучайных[6]=0.4884 КонецЕсли;
//	НаборСлучайных[7] = НаборСлучайных[7]+0.0101;
//	Если НаборСлучайных[7]>=1 Тогда НаборСлучайных[7]=0.8477 КонецЕсли;
//КонецПроцедуры // ОбновитьНаборСлучайных()

// Функция Возвращает псевдослучайное число указанного порядка GOAN
// (порядок равен количеству цифр в числе, [1..4])
// Используется массив весовых Коэффициентов НаборСлучайных[]
//
// Параметры:
//  Порядок - порядок числа
//
// Возвращаемое значение:
//  получено число
//
Функция СлучайноеЧисло_МетодСлП(Порядок)
	Перем Р, С;
	
	// инициализируем случайную последовательность
	Если ИндексСлучайных=Неопределено ИЛИ НаборСлучайных=Неопределено Тогда
		ИнициализацияСлучайнойПоследовательности();
	КонецЕсли;
	
	С = 0;
	Для К = 0 По 7 Цикл
		СлучайноеЧисло = НаборСлучайных[К];
		С = С + СлучайноеЧисло;
	КонецЦикла;
	Р = С - Цел(С);
	ИндексСлучайных = ИндексСлучайных + 1;
	Если ИндексСлучайных > 7 Тогда
		ИндексСлучайных = 1;
	КонецЕсли;
	НаборСлучайных[ИндексСлучайных] = Р;
	С = 1;
	Для К = 0 По Порядок-1 Цикл
		С = С * 10;
	КонецЦикла;
	Возврат Цел(Р * С);
КонецФункции // СлучайноеЧисло()

// Процедура создание новой условно случайной последовательности с указанным смещением GOAN
// Смещение рекомендуется задавать в виде трехзначного числа
// Используется массив весовых Коэффициентов НаборСлучайных[]
//
Процедура ИнициализацияСлучайнойПоследовательности(Смещение = 0)
	Перем Ч, М, С, Р, Д, К;
	НаборСлучайных = Новый Массив(8);
	НаборСлучайных[0] = 0.2354;
	НаборСлучайных[1] = 0.5378;
	НаборСлучайных[2] = 0.0932;
	НаборСлучайных[3] = 0.9084;
	НаборСлучайных[4] = 0.3490;
	НаборСлучайных[5] = 0.3489;
	НаборСлучайных[6] = 0.4884;
	НаборСлучайных[7] = 0.8477;
	ИндексСлучайных = 0;
	ТекДата = ТекущаяДата();
	Д = ДеньГода(ТекДата) % 1000;
	Ч = Час(ТекДата);
	М = Минута(ТекДата);
	С = Секунда(ТекДата);
	Р = Ч + М + С + Д + Смещение;
	Для К = 1 По Р Цикл
		СлучайноеЧисло_МетодСлП(0); // Простая инициализация новых значений
	КонецЦикла;
КонецПроцедуры // ИнициализацияСлучайнойПоследовательности()

// Формирует случайный код в произвольном алфавитно-цифровом формате (для служебных целей)
// Возвращает строку чисел если АлфавитноЦифровой=0 и буквенно-цифровую A-Z&0-9 иначе
//
// Параметры:
//  КоличествоЗнаков - число - длина кода,
//  АлфавитноЦифровой - число определяет тип кода.
//
// Возвращаемое значение:
//  Стр - строка - сформированный произвольный код.
//
Функция СформироватьПроизвольныйКод_МетодСлП(КоличествоЗнаков = 22, АлфавитноЦифровой = 0) Экспорт
	Стр = "";
	Если АлфавитноЦифровой = 0 Тогда // Только числа
		Для К = 1 По ОкрВверх(КоличествоЗнаков / 4) Цикл
			Стр = Стр + Формат(СлучайноеЧисло_МетодСлП(4),"ЧЦ=4; ЧН=; ЧВН=; ЧГ=0");
		КонецЦикла;
		Возврат Лев(Стр, КоличествоЗнаков);
	Иначе            				 // Числа и символы A - Z
		Пока СтрДлина(Стр) < КоличествоЗнаков Цикл
			Ч = СлучайноеЧисло_МетодСлП(1);
			Если Ч % 2 = 0 Тогда
				// Берем цифру
				Стр = Стр + Строка(СлучайноеЧисло_МетодСлП(1));
			Иначе
				// Берем букву
				Стр = Стр + Символ(65 + (СлучайноеЧисло_МетодСлП(2) % 26)); // A - Z
			КонецЕсли;
		КонецЦикла;
		Возврат Стр;
	КонецЕсли;
КонецФункции // СформироватьПроизвольныйКод()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ШТРИХ КОДА МЕТОДОМ ГЕНЕРАЦИИ GUID

// Функция Возвращает псевдослучайное число указанного порядка
// (порядок равен количеству цифр в числе, [1..10])
// Используется генератор GUID
//
// Параметры:
//  Порядок - порядок числа
//
// Возвращаемое значение:
//  получено число
//
Функция СлучайноеЧисло(Порядок)
	
	Мин = 0;   //минимальное допустимое значение произвольного кода
	Макс = 1;  //максимальное допустимое значение произвольного кода
    Для х = 0 По Порядок - 1 Цикл 
        Макс = Макс * 10; 
	КонецЦикла;       
	Макс = Макс-1;
	
	//генерируем GUID и оставляем только цифры
    НовыйУИ = СокрЛП(Новый УникальныйИдентификатор);
    НовыйУИ = СтрЗаменить(НовыйУИ,"-","");
    НовыйУИ = СтрЗаменить(НовыйУИ,"a","");
    НовыйУИ = СтрЗаменить(НовыйУИ,"b","");
    НовыйУИ = СтрЗаменить(НовыйУИ,"c","");
    НовыйУИ = СтрЗаменить(НовыйУИ,"d","");
    НовыйУИ = СтрЗаменить(НовыйУИ,"e","");
    НовыйУИ = СтрЗаменить(НовыйУИ,"f","");
	
    //знаменатель должен иметь такое же количество нулей + 1
    Знаменатель = 10;
    Для н = 2 По (СтрДлина(СтрЗаменить(НовыйУИ,Символы.НПП,""))) Цикл
        Знаменатель = Знаменатель * 10;
    КонецЦикла; 
	
	//получим дробное случайное число в диапазоне от 0 до 1
    Случ = Число(НовыйУИ) / Знаменатель;     
	
    //преобразуем его в случайное число из заданного интервала, округляем до целого
    ЧислоИзИнтервала = Мин(Макс(Окр(Мин + (Макс-Мин)*Случ),Мин),Макс);
	Возврат ЧислоИзИнтервала;
	
КонецФункции // СлучайноеЧисло()

// Формирует случайный код в произвольном алфавитно-цифровом формате
// Используется генератор GUID
// Возвращает строку чисел если АлфавитноЦифровой=0 и буквенно-цифровую A-Z&0-9 иначе
//
// Параметры:
//  КоличествоЗнаков - число - длина кода,
//  АлфавитноЦифровой - число определяет тип кода.
//
// Возвращаемое значение:
//  Стр - строка - сформированный произвольный код.
//
Функция СформироватьПроизвольныйКод(КоличествоЗнаков = 22, АлфавитноЦифровой = 0) Экспорт

	Стр = "";
	Если АлфавитноЦифровой = 0 Тогда // Только числа
		Для К = 1 По ОкрВверх(КоличествоЗнаков / 10) Цикл
			Стр = Стр + Формат(СлучайноеЧисло(10),"ЧЦ=10; ЧН=; ЧВН=; ЧГ=0");
		КонецЦикла;
		Возврат Лев(Стр, КоличествоЗнаков);
	Иначе            				 // Числа и символы A - Z
		Возврат СформироватьПроизвольныйКод_МетодСлП(КоличествоЗнаков, АлфавитноЦифровой);
	КонецЕсли;
	
КонецФункции // СформироватьПроизвольныйКод()

// Функция, приводящая строку к необходимой длине строки символом
//
// Параметры:
//  Реж    - число -  1 - добавлять символы слева, 2 - справа, 3 - слева и справа
//  Стр    - строка - строка которую редактируем,
//  Символ - строка - символ, который вставляем,
//  Длина  - Число - длина до которой приводим.
//
// Возвращаемое значение:
//  Стр - строка - результат редактирования строки.
//
Функция СтрокаПривести(Стр,Символ,Длина,Реж=1)
	ДопСтр=""; Сч=Длина-СтрДлина(Стр);
	Если Сч>0  Тогда
		Для к=1 По Сч Цикл
			ДопСтр=ДопСтр+Символ;
		КонецЦикла;
		Если Реж=1 Тогда Возврат ДопСтр+Стр;
		ИначеЕсли Реж=2 Тогда Возврат Стр+ДопСтр;
		Иначе к=Окр(Сч/2); Возврат Лев(ДопСтр,к)+Стр+Прав(ДопСтр,Сч-к);
		КонецЕсли;
	Иначе Возврат Стр;
	КонецЕсли;
КонецФункции // СтрокаПривести()

// Вычисляет контрольную цифру EAN-13/EAN-8  GOAN
// Возвращает штрих-код с контрольной цифрой
//
// Параметры:
//  Код - строка - код, по которому вычисляем контрольную цифру.
//
// Возвращаемое значение:
//  Рез - Строка - штрих-код с контрольной цифрой. 
//
Функция EAN13(Код) Экспорт
	ШтрКод = Формат(Число(Код),"ЧН=; ЧВН=; ЧГ=0; ЧЦ="+?(СтрДлина(Код)<8,"7","12"));
	Четн = 0; Нечетн = 0;
	Если СтрДлина(Код)=7 Тогда Итераций = 4; 
	Иначе Итераций = 6; 
	КонецЕсли;
	Для Индекс=1 По Итераций Цикл
		Четн=Четн+Сред(ШтрКод,2*Индекс,1);
		Нечетн=Нечетн+Сред(ШтрКод,2*Индекс-1,1);
	КонецЦикла;
	Если СтрДлина(Код)=7 Тогда Нечетн=Нечетн*3; 
	Иначе Четн=Четн*3;
	КонецЕсли;
	КонтЦифра=10-(Четн+Нечетн)%10;
	Если КонтЦифра=10 Тогда КонтЦифра=0;КонецЕсли;
	Рез=ШтрКод+Строка(КонтЦифра);
	Возврат Рез;
КонецФункции // EAN13()

// Формирует штрих-код в формате EAN-13 для товара
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  ШтрихКод
Функция СформироватьШтрихКодТовара(Весовой=Ложь) Экспорт
	СчПопыток=0;
	
	Если Весовой Тогда
	    К = 5; //Для весового товара длина штрихкода 7 (2 - префикс)
		ПрефиксКода = Строка(ПрефиксВесовогоШК);
		НовыйКод = ПрефиксКода + СтрокаПривести(СформироватьПроизвольныйКод(К), "0", 4, 2);
		Пока НЕ ШтрихКодУникален(НовыйКод) ИЛИ ШтрихКодЗапрещен(НовыйКод) Цикл
			НовыйКод = ПрефиксКода + СтрокаПривести(СформироватьПроизвольныйКод(К), "0", 4, 2);
			СчПопыток=СчПопыток+1;
			Если СчПопыток=50 Тогда
				СчПопыток=0; 
			КонецЕсли;
		КонецЦикла;
	Иначе
		К = 10;	//Для невесового товара длина штрихкода 12 (2 - префикс)
		ПрефиксКода = ПрефиксШтучногоШК;
		НовыйКод = EAN13(Строка(ПрефиксКода) + СтрокаПривести(СформироватьПроизвольныйКод(К), "0", 10, 2));
		Пока НЕ ШтрихКодУникален(НовыйКод) ИЛИ ШтрихКодЗапрещен(НовыйКод) Цикл
			НовыйКод = EAN13(Строка(ПрефиксКода) + СтрокаПривести(СформироватьПроизвольныйКод(К), "0", 10, 2));
			СчПопыток=СчПопыток+1;
			Если СчПопыток=50 Тогда
				СчПопыток=0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	Возврат НовыйКод;    
КонецФункции // СформироватьШтрихКод()

// Формирует ручной код товара в диапазоне 1..9999
//
// Возвращаемое значение:
//  НовыйКод - Строка - ручной код.
//
Функция СформироватьРучнойКодТовара() Экспорт
	// получим существующие ручные коды
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ШтрихКоды.ШтрихКод КАК ШтрихКод
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|ГДЕ
	|	ПОДСТРОКА(ШтрихКоды.ШтрихКод,1,4)=ШтрихКоды.ШтрихКод");
	СписокИмеющихсяКодов=Новый СписокЗначений();
	СписокИмеющихсяКодов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ШтрихКод"));
	
	НовыйКод="";
	// формируем код
	Для Сч=1 По 9999 Цикл
		НовыйКод=Формат(Сч,"ЧГ=0");
		// 1. нет ли такого кода в списке имеющихся
		Если СписокИмеющихсяКодов.НайтиПоЗначению(НовыйКод)<>Неопределено Тогда Продолжить; КонецЕсли;
		// 2. нет ли нашего кода в списке запрещенных
		Если ШтрихКодЗапрещен(НовыйКод) Тогда Продолжить; КонецЕсли;
		// проверки прошли
		Прервать;
	КонецЦикла;
	
	Возврат НовыйКод;    
КонецФункции // СформироватьШтрихКод()

// Формирует штрих-код карточки для товара
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  ШтрихКод
Функция СформироватьШтрихКодКарточки(ДлинаКода) Экспорт
	НовыйКод = СформироватьПроизвольныйКод(ДлинаКода);
	Пока НЕ ШтрихКодУникален(НовыйКод) Цикл
		НовыйКод = СформироватьПроизвольныйКод(ДлинаКода);
	КонецЦикла;
	Возврат НовыйКод;    
КонецФункции // СформироватьШтрихКод()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ТАБЛИЧНОЙ ЧАСТЬЮ СОСТАВ

// Процедура заполняет табличную часть обработки штрихкодами объекта.
// Объект - объект, штрихкоды которого отбираются.
//
// Параметры:
//  Нет.
//
Процедура ПрочитатьЗаполнитьШтрихКоды() Экспорт
	Запрос = Новый Запрос();	
    Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Если ТипЗнч(Объект)=Тип("СправочникОбъект.Номенклатура") И НЕ Объект = Неопределено Тогда
		ИспользованиеШтрихКодов=Объект.ТипНоменклатуры.ИспользованиеШтрихКодов;
		Если ИспользованиеШтрихКодов=2 Тогда
			// используем ШК для единиц
			ПодчинитьТипу=(Объект.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1);
		ИначеЕсли ИспользованиеШтрихКодов=3 Тогда
			// используем ШК для характеристик
			ПодчинитьТипу=(Объект.ТипНоменклатуры.ИспользованиеХарактеристик=1);
		КонецЕсли;
		ПодчинитьТипу=Ложь;
		// смотрим чему у нас подчинены единицы измерения и характеристики
		Если ИспользованиеШтрихКодов=Неопределено Тогда
			//Это для карточек
			ПодчинитьТипу=Неопределено;
		Иначе
			Если ИспользованиеШтрихКодов=2 Тогда
				// используем ШК для единиц
				ПодчинитьТипу=(Объект.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1);
			ИначеЕсли ИспользованиеШтрихКодов=3 Тогда
				// используем ШК для характеристик
				ПодчинитьТипу=(Объект.ТипНоменклатуры.ИспользованиеХарактеристик=1);
			КонецЕсли;
			Запрос.УстановитьПараметр("Владелец", ?(ПодчинитьТипу,Объект.ТипНоменклатуры,Объект.Ссылка));
		КонецЕсли;
		Запрос.УстановитьПараметр("Владелец", ?(ПодчинитьТипу,Объект.ТипНоменклатуры,Объект.Ссылка));
		Запрос.Текст="
		|ВЫБРАТЬ
		|	ШтрихКоды."+?(ИспользованиеШтрихКодов=1,"Объект",?(ИспользованиеШтрихКодов=2,"ЕдиницаИзмерения","ХарактеристикаНоменклатуры"))+" КАК Объект,
		|	ШтрихКоды.ШтрихКод,
		|	ШтрихКоды.Запрет КАК Запрещен
		|ИЗ
		|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|ГДЕ
		|	ШтрихКоды.Объект=&Ссылка
		|УПОРЯДОЧИТЬ ПО Объект
		|";
	Иначе
		Запрос.Текст="ВЫБРАТЬ
				 |	ШтрихКоды.Объект КАК Объект,
				 |	ШтрихКоды.ШтрихКод,
				 |	ШтрихКоды.Запрет КАК Запрещен
				 |ИЗ
				 |	РегистрСведений.ШтрихКоды КАК ШтрихКоды
				 |ГДЕ
				 |	ШтрихКоды.Объект=&Ссылка";
	КонецЕсли;  
	Состав.Очистить();
	Состав.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры // ПрочитатьЗаполнитьШтрихКоды()

// Функция поиск товара по штрих-коду
//
// Параметры:
//  ШтрихКод - штрих-код товара, по которому ведется поиск
//
// Возвращаемое значение:
//  структура
//
Функция НайтиСтруктуруПоШтрихКоду(ШтрихКод, флРугатьсяНаЗапрещенныйШтрихКод=Ложь) Экспорт
	
	ШтрихКод = РазобратьШтрихкодПоТипам(ШтрихКод);
	
	Попытка
		//ШК=СокрЛП(Формат(ШтрихКод,"ЧГ=0")); Вес=0;
		ШК=Строка(ШтрихКод);
		Попытка
			ПрефиксШтрихКода=Число(Лев(ШК,2));
		Исключение
			ПрефиксШтрихКода=0;
		КонецПопытки; 
		Попытка
			Если ПрефиксШтрихКода=ПрефиксВесовогоШК Тогда
				// Вытащим вес
				Вес=Сред(ШК,7+1,СтрДлина(ШК)-8);
				Если НЕ ПустаяСтрока(Вес) Тогда
					Попытка
						Вес=Число(Сред(ШК,7+1,СтрДлина(ШК)-8))/1000;
					Исключение
						Вес=0;
					КонецПопытки; 
				Иначе
					Вес=0;
				КонецЕсли;
				//если товар весовой, то ищем по первым семи символам
			    ШК=Формат(Лев(ШК,7),"ЧГ=0");
			КонецЕсли; 
		Исключение
		КонецПопытки; 
		
		Запрос = Новый Запрос();
	    Запрос.УстановитьПараметр("ШтрихКод", ШК);
		Запрос.Текст = "
	    |ВЫБРАТЬ
		|	ШтрихКоды.Объект,
		|	ШтрихКоды.ШтрихКод,
		|	ШтрихКоды.ЕдиницаИзмерения,
		|	ШтрихКоды.ХарактеристикаНоменклатуры,
		|	ШтрихКоды.Запрет
		|ИЗ
		|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	    |ГДЕ
		|	ШтрихКоды.ШтрихКод = &ШтрихКод
		|";

		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		СтуктураОбъект = Новый Структура();

		Если Выборка.Следующий() Тогда
			Если флРугатьсяНаЗапрещенныйШтрихКод И Выборка.Запрет Тогда
				#Если Клиент Тогда
				Предупреждение("Запрещенный штрихкод",3);
				#КонецЕсли
				Возврат Неопределено;
			КонецЕсли;
			ТекОбъект = Выборка.Объект;
			Если ТипЗнч(ТекОбъект)=Тип("СправочникСсылка.Номенклатура") Тогда
				//В системе с данным штрихкодом зарегистрирована номенклатура
				ИспользованиеШтрихКодов=ТекОбъект.ТипНоменклатуры.ИспользованиеШтрихКодов;
				СтуктураОбъект.Вставить("Объект",ТекОбъект);
				Если ИспользованиеШтрихКодов = 2 Тогда
					СтуктураОбъект.Вставить("ЕдиницаИзмерения",Выборка.ЕдиницаИзмерения);
				ИначеЕсли ИспользованиеШтрихКодов = 3 Тогда
					СтуктураОбъект.Вставить("ХарактеристикаНоменклатуры",Выборка.ХарактеристикаНоменклатуры);
				КонецЕсли; 
				СтуктураОбъект.Вставить("Вес",Вес);
				Возврат СтуктураОбъект;
			ИначеЕсли ТипЗнч(ТекОбъект)=Тип("СправочникСсылка.Карточки") Тогда
				//В системе с данным штрихкодом зарегистрирована карточка
				СтуктураОбъект.Вставить("Объект",ТекОбъект);
				Возврат СтуктураОбъект;
			КонецЕсли; 
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		#Если Клиент Тогда
		Предупреждение("Неверный штрихкод",3);
		#КонецЕсли
		Возврат Неопределено;
	КонецПопытки;
КонецФункции // НайтиСтруктуруПоШтрихКоду()

// Функция поиск товара по штрих-коду
//
// Параметры:
//  ШтрихКод - штрихкод товара, по которому ведется поиск
//
// Возвращаемое значение:
//  ссылка на найденный элемент справочника Номенклатура
//
Функция НайтиПоШтрихКоду(ШтрихКод,ЕстьПрефикс=1,ДополнительноеУсловие=Неопределено) Экспорт
	//ШК=СокрЛП(Формат(ШтрихКод,"ЧГ=0"));
	
	ШтрихКод = РазобратьШтрихкодПоТипам(ШтрихКод);
	
	ШК=Строка(ШтрихКод);
	Если ЕстьПрефикс = 1 Тогда
		Попытка
			ПрефиксШтрихКода=Число(Лев(ШК,2));
		Исключение
			ПрефиксШтрихКода=0;
		КонецПопытки; 
		Попытка
			Если ПрефиксШтрихКода=ПрефиксВесовогоШК Тогда
				//если товар весовой, то ищем по первым семи символам
		    	ШК=Формат(Лев(ШК,7),"ЧГ=0");
			КонецЕсли; 
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
    Запрос.УстановитьПараметр("ШтрихКод", ШК);
	Запрос.Текст = "
    |ВЫБРАТЬ
	|	ШтрихКоды.Объект,
	|	ШтрихКоды.ШтрихКод
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
    |ГДЕ
	|	ШтрихКоды.ШтрихКод = &ШтрихКод
	|	"+?(ДополнительноеУсловие=Неопределено,"","И "+ДополнительноеУсловие)+"
	|";

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
	    Возврат Выборка.Объект;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции // НайтиПоШтрихКоду()

Функция РазобратьШтрихкодПоТипам(Штрихкод) Экспорт
	
	ШтрихкодыПоТипам = Новый Массив;
	
	ТипШтрихкода = ОпределитьТипШтрихкода(Штрихкод);
	
	Если ТипШтрихкода = "ITF14" Или ТипШтрихкода = "EAN13" Или ТипШтрихкода = "EAN8" Тогда
		ШтрихкодыПоТипам.Добавить(Штрихкод);
	Иначе
		РезультатРазбора = РазобратьСтрокуШтрихкодаGS1(ШтрихКод);
		
		ШтрихкодEAN = Неопределено;
		Если РезультатРазбора.Разобран Тогда
			Идентификатор01 = РезультатРазбора.ДанныеШтрихкода["01"];
			Если НЕ Идентификатор01 = Неопределено Тогда
				ШтрихкодEAN = EANПоКодуМаркировки(Идентификатор01.Значение);
			КонецЕсли;
		КонецЕсли;
		
		Если ШтрихкодEAN = Неопределено Тогда
			ДлинаКодаМаркировки = СтрДлина(ШтрихКод);
			Если ДлинаКодаМаркировки = 29
				ИЛИ ДлинаКодаМаркировки = 25
				ИЛИ ДлинаКодаМаркировки = 21 Тогда
				ШтрихкодEAN = EANПоКодуМаркировки(ШтрихКод);
			Иначе
				ШтрихкодEAN = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ШтрихкодEAN = Неопределено Тогда
			ШтрихкодыПоТипам.Добавить(ШтрихкодEAN);
		Иначе
			ШтрихкодыПоТипам.Добавить(Штрихкод);
		КонецЕсли;
	КонецЕсли;
	
	Если ШтрихкодыПоТипам.Количество() > 0 Тогда
		Возврат ШтрихкодыПоТипам[0];
	КонецЕсли;
	
	Возврат Штрихкод;
	
КонецФункции

// Функция определяет тип штрихкода по значение кода.
// 
Функция ОпределитьТипШтрихкода(Знач Штрихкод) Экспорт
	
	ТипШтрихкодаЗнач = "";	
	ДлинаШтрихкода = СтрДлина(Штрихкод);
	
	Если ДлинаШтрихкода = 0 Тогда
		Возврат ТипШтрихкодаЗнач;
	КонецЕсли;
	
	Сумма = 0;
	
	Если ДлинаШтрихкода = 14 Тогда // ITF14
		
		Если РассчитатьКонтрольныйСимволGTIN14(Штрихкод) = Прав(Штрихкод, 1) Тогда
			ТипШтрихкодаЗнач = "ITF14";
 		КонецЕсли;
		
	ИначеЕсли ДлинаШтрихкода = 13 Тогда // EAN13
		
		Если РассчитатьКонтрольныйСимволGTIN13(Штрихкод) = Прав(Штрихкод, 1) Тогда
			ТипШтрихкодаЗнач = "EAN13";
		КонецЕсли;
		
	ИначеЕсли ДлинаШтрихкода = 8 Тогда // EAN8
		
		Если РассчитатьКонтрольныйСимволGTIN8(Штрихкод) = Прав(Штрихкод, 1) Тогда
			ТипШтрихкодаЗнач = "EAN8";
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипШтрихкодаЗнач= "" Тогда // CODE39
		
		CODE39 = Истина;
		Для Сч = 1 По ДлинаШтрихкода Цикл
			ВремКодСимвола = КодСимвола(Штрихкод, Сч);
			Если (ВремКодСимвола <> 32)
				И (ВремКодСимвола < 36 Или ВремКодСимвола > 37)
				И (ВремКодСимвола <> 43)
				И (ВремКодСимвола < 45 Или ВремКодСимвола > 57)
				И (ВремКодСимвола < 65 Или ВремКодСимвола > 90) Тогда
				CODE39 = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если CODE39 Тогда
			ТипШтрихкодаЗнач = "CODE39";
		КонецЕсли                                                     
		
	КонецЕсли;
	
	Если ТипШтрихкодаЗнач= ""  Тогда // CODE128
		// CODE128 символы ASCII от 0 до 127 (цифры от «0» до «9», буквы от «A» до «Z» и от «a» до «z») и специальные символы;
		CODE128 = Истина;
		Для Сч = 1 По ДлинаШтрихкода Цикл
			ВремКодСимвола = КодСимвола(Штрихкод, Сч);
			Если (ВремКодСимвола > 127) Тогда
				CODE128 = Ложь;
			Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если CODE128 Тогда
			ТипШтрихкодаЗнач = "CODE128";
		КонецЕсли                                                     
		
	КонецЕсли;
	
	Если ТипШтрихкодаЗнач = "CODE128"  Тогда // EAN128
		// В коде EAN128 регламентирован словарь CODE128 но регламентированы группы кодов, и возможны разделители GS.
		Если КодСимвола(Штрихкод, 1) = 40 Или СтрНайти(Штрихкод, КодСимвола(29)) > 0  Тогда
			ТипШтрихкодаЗнач = "EAN128";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипШтрихкодаЗнач;
	
КонецФункции

// Расчет контрольной цифры для GTIN-8. 
//
// Параметры:
//  GTIN - Текстовая строка с GTIN-8. Может содержать числа от 0 до 9. 
// 
// Возвращаемое значение:
//   - Контрольный символ (число) рассчитанный по алгоритму для GTIN.
//
Функция РассчитатьКонтрольныйСимволGTIN8(Знач GTIN) Экспорт
	
	Сумма = 0;
	Коэффициент = 3;
	
	Для Сч = 1 По 7 Цикл
		ВремКодСимвола = КодСимвола(GTIN, Сч);
		Сумма  = Сумма + Коэффициент * (ВремКодСимвола - 48);
		Коэффициент = 4 - Коэффициент;
	КонецЦикла;
	Сумма = (10 - Сумма % 10) % 10;
	КонтрольныйСимвол = Символ(Сумма + 48);
	
	Возврат КонтрольныйСимвол;
	
КонецФункции

// Расчет контрольной цифры для GTIN-12. 
//
// Параметры:
//  GTIN - Текстовая строка с GTIN-12. Может содержать числа от 0 до 9. 
// 
// Возвращаемое значение:
//   - Контрольный символ (число) рассчитанный по алгоритму для GTIN.
//
Функция РассчитатьКонтрольныйСимволGTIN12(Знач GTIN) Экспорт
	
	Сумма = 0;
	Коэффициент = 3;
	
	Для Сч = 1 По 11 Цикл
		ВремКодСимвола = КодСимвола(GTIN, Сч);
		Сумма  = Сумма + Коэффициент * (ВремКодСимвола - 48);
		Коэффициент = 4 - Коэффициент;
	КонецЦикла;
	Сумма = (10 - Сумма % 10) % 10;
	КонтрольныйСимвол = Символ(Сумма + 48);
	
	Возврат КонтрольныйСимвол;
	
КонецФункции

// Расчет контрольной цифры для GTIN-13. 
//
// Параметры:
//  GTIN - Текстовая строка с GTIN-13. Может содержать числа от 0 до 9.
// 
// Возвращаемое значение:
//   - Контрольный символ (число) рассчитанный по алгоритму для GTIN.
//
Функция РассчитатьКонтрольныйСимволGTIN13(Знач GTIN) Экспорт
	
	Сумма = 0;
	Коэффициент = 1;
	
	Для Сч = 1 По 12 Цикл
		ВремКодСимвола = КодСимвола(GTIN, Сч);
		Сумма  = Сумма + Коэффициент * (ВремКодСимвола - 48);
		Коэффициент = 4 - Коэффициент;
	КонецЦикла;
	Сумма = (10 - Сумма % 10) % 10;
	КонтрольныйСимвол = Символ(Сумма + 48);
	
	Возврат КонтрольныйСимвол;
	
КонецФункции

// Расчет контрольной цифры для GTIN-14. 
//
// Параметры:
//  GTIN - Текстовая строка с GTIN-14. Может содержать числа от 0 до 9. 
// 
// Возвращаемое значение:
//   - Контрольный символ (число) рассчитанный по алгоритму для GTIN.
//
Функция РассчитатьКонтрольныйСимволGTIN14(Знач GTIN) Экспорт
	
	Сумма = 0;
	Коэффициент = 3;
	
	Для Сч = 1 По 13 Цикл
		ВремКодСимвола = КодСимвола(GTIN, Сч);
		Сумма  = Сумма + Коэффициент * (ВремКодСимвола - 48);
		Коэффициент = 4 - Коэффициент;
	КонецЦикла;
	Сумма = (10 - Сумма % 10) % 10;
	КонтрольныйСимвол = Символ(Сумма + 48);
	
	Возврат КонтрольныйСимвол;
	
КонецФункции

// Универсальная функция расчета контрольной цифры GTIN.
// GTIN допускает в формате GTIN-8, GTIN-12, GTIN-13, GTIN-14 c контрольным символом.
//
// Параметры:
//  GTIN - Текстовая строка с GTIN(c контрольным символом). Может содержать числа от 0 до 9.
// 
// Возвращаемое значение:
//   - Контрольный символ (число) рассчитанный по алгоритму для GTIN.
//
Функция РассчитатьКонтрольныйСимволGTIN(Знач GTIN) Экспорт
	
	Сумма = 0;
	ДлиннаGTIN = СтрДлина(GTIN);
	Коэффициент = ?(ДлиннаGTIN % 2 = 0, 3, 1); 
	
	Для Сч = 1 По ДлиннаGTIN - 1 Цикл
		ВремКодСимвола = КодСимвола(GTIN, Сч);
		Сумма  = Сумма + Коэффициент * (ВремКодСимвола - 48);
		Коэффициент = 4 - Коэффициент;
	КонецЦикла;
	Сумма = (10 - Сумма % 10) % 10;
	КонтрольныйСимвол = Символ(Сумма + 48);
	
	Возврат КонтрольныйСимвол;
	
КонецФункции

// Функция проверяет корректность GTIN.
// GTIN допускает в формате GTIN-8, GTIN-12, GTIN-13, GTIN-14 c контрольным символом.
//
// Параметры:
//  GTIN - Текстовая строка с GTIN(c контрольным символом). Может содержать числа от 0 до 9.
// 
// Возвращаемое значение:
//   - Булево  
//
Функция ПроверитьКорректностьGTIN(Знач GTIN) Экспорт
	
	Результат = (СтрДлина(GTIN) = 8) Или (СтрДлина(GTIN) = 12) Или (СтрДлина(GTIN) = 13) Или (СтрДлина(GTIN) = 14);
	Возврат Результат И РассчитатьКонтрольныйСимволGTIN(GTIN) = Прав(GTIN, 1);
	
КонецФункции

Функция EANПоКодуМаркировки(КодМаркировки) Экспорт
	
	ДанныеGTIN = Лев(КодМаркировки, 14);
	
	ЭтоGTIN = ПроверитьКорректностьGTIN(ДанныеGTIN);
	Если Не ЭтоGTIN Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	EAN = ШтрихкодEANИзGTIN(ДанныеGTIN);
	
	Возврат EAN;
	
КонецФункции

// Вычисляет штрихкод EAN из GTIN.
// 
// Параметры:
// 	GTIN - Строка - GTIN.
// Возвращаемое значение:
// 	Строка - Вычисленное значение EAN.
Функция ШтрихкодEANИзGTIN(GTIN) Экспорт
	
	// Пытаемся получить штрикод EAN8 или EAN13 из GTIN.
	Пока Лев(GTIN, 1) = "0" И СтрДлина(GTIN) > 8 Цикл
		GTIN = Сред(GTIN, 2);
	КонецЦикла;
	
	Возврат GTIN;
	
КонецФункции

// Разобрать строку штрихкода в соответствии со стандартом GS1.
//
Функция РазобратьСтрокуШтрихкодаGS1(Штрихкод, УдалитьКриптохвост = Ложь, ФормироватьСкобками = Истина) Экспорт
	
	РезультатРазбора = Новый Структура;
	РезультатРазбора.Вставить("Разобран"      , Ложь);
	РезультатРазбора.Вставить("ОписаниеОшибки");
	РезультатРазбора.Вставить("ПредставлениеШтрихкода", "");
	РезультатРазбора.Вставить("ДанныеШтрихкода", Новый Соответствие);
	
	КодыGS1 = КодыGS1();
	ПозицияРазделителяGS1 = Ложь;
	
	Если СтрНачинаетсяС(Штрихкод, "(") Тогда
		РазобратьСтрокуШтрихкодаGS1СоСкобками(Штрихкод, РезультатРазбора, КодыGS1);
	Иначе
		ПозицияРазделителяGS1   = Найти(Штрихкод, РазделительGS1());
		ПозицияРазделителяЭкран = Найти(Штрихкод, ЭкранированныйСимволGS1());
		Если ПозицияРазделителяЭкран > 0 Тогда
			ЧастиШтрихкода = обРазложитьСтрокуВМассивПодстрок(Штрихкод, ЭкранированныйСимволGS1());
		ИначеЕсли ПозицияРазделителяGS1 > 0 Тогда
			ЧастиШтрихкода = СтрРазделить(Штрихкод, РазделительGS1(), Ложь);
		ИначеЕсли СтрНачинаетсяС(Штрихкод, "01") Тогда
			ЧастиШтрихкода = Новый Массив;
			
			GTIN = Сред(Штрихкод, 3, 14); // GTIN
			
			// GTIN корректный
			Если РассчитатьКонтрольныйСимволGTIN14(GTIN) = Прав(GTIN, 1)
				И Сред(Штрихкод, 17, 2) = "21" Тогда
				ДанныеСтроки = Сред(Штрихкод, 19);
				Если СтрДлина(ДанныеСтроки) >= 13 Тогда
					// Считаем, что серийный номер состоит из 13 символов
					СерийныйНомер = Сред(ДанныеСтроки, 1, 13);
					СледующаяГруппа2 = Сред(ДанныеСтроки, 14, 2);
					СледующаяГруппа3 = Сред(ДанныеСтроки, 14, 3);
					СледующаяГруппа4 = Сред(ДанныеСтроки, 14, 4);
					Если СледующаяГруппа2 = "91" Или СледующаяГруппа2 = "92" 
						Или СледующаяГруппа2 = "17" Или СледующаяГруппа4 = "7003" 
						Или СледующаяГруппа3 = "240" ИЛИ СтрДлина(ДанныеСтроки) = 13 Тогда
						
						ОписаниеДанныхGTIN = Новый Структура;
						ОписаниеДанныхGTIN.Вставить("ПоложениеДесятичнойТочки", 0);
						ОписаниеДанныхGTIN.Вставить("Значение", GTIN);
						
						РезультатРазбора.ДанныеШтрихкода.Вставить("01", ОписаниеДанныхGTIN);
						
						ОписаниеДанныхСН = Новый Структура;
						ОписаниеДанныхСН.Вставить("ПоложениеДесятичнойТочки", 0);
						ОписаниеДанныхСН.Вставить("Значение", СерийныйНомер);
						
						РезультатРазбора.ДанныеШтрихкода.Вставить("21", ОписаниеДанныхСН);
						РезультатРазбора.Разобран = Истина;
						
					Иначе
						Возврат РезультатРазбора;
					КонецЕсли;
				Иначе
					Возврат РезультатРазбора;
				КонецЕсли;
			Иначе
				Возврат РезультатРазбора;
			КонецЕсли;
			
		Иначе
			Возврат РезультатРазбора;
		КонецЕсли;
		Для Каждого ЧастьБезРазделителей Из ЧастиШтрихкода Цикл
			РазобратьСтрокуШтрихкодаGS1Служебный(ЧастьБезРазделителей, РезультатРазбора, КодыGS1);
		КонецЦикла;
	КонецЕсли;
	
	// Марикровка разобрана - избавимся от запрещенных символов
	Если РезультатРазбора.Разобран Тогда
		Если УдалитьКриптохвост
			И НЕ РезультатРазбора.ДанныеШтрихкода.Получить("01") = Неопределено
			И НЕ РезультатРазбора.ДанныеШтрихкода.Получить("21") = Неопределено Тогда
			Если ФормироватьСкобками Тогда
				
				Штрихкод = 
					СтрШаблон(
					"(01)%1(21)%2",
					РезультатРазбора.ДанныеШтрихкода["01"].Значение,
					РезультатРазбора.ДанныеШтрихкода["21"].Значение
				);
				
			Иначе
				
				МассивКода = Новый Массив;
				МассивКода.Добавить("01");
				МассивКода.Добавить(РезультатРазбора.ДанныеШтрихкода["01"].Значение);
				МассивКода.Добавить("21");
				МассивКода.Добавить(РезультатРазбора.ДанныеШтрихкода["21"].Значение);
				
				Штрихкод = СтрСоединить(МассивКода, "");
				
			КонецЕсли;
		Иначе
			Если ПозицияРазделителяGS1 Тогда
				Штрихкод = СтрЗаменить(Штрихкод, РазделительGS1(), ЭкранированныйСимволGS1());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатРазбора;
	
КонецФункции

Функция КодыGS1() Экспорт
	
	Коды = Новый Соответствие;
	
	ДобавитьКодGS1(Коды, "00"  , "SSCC"                      , 18);
	ДобавитьКодGS1(Коды, "01"  , "GTIN"                      , 14);
	ДобавитьКодGS1(Коды, "02"  , "CONTENT"                   , 14);
	ДобавитьКодGS1(Коды, "10"  , "BATCH_LOT"                 ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "11"  , "PROD_DATE"                 ,  6);
	ДобавитьКодGS1(Коды, "12"  , "DUE_DATE"                  ,  6);
	ДобавитьКодGS1(Коды, "13"  , "PACK_DATE"                 ,  6);
	ДобавитьКодGS1(Коды, "15"  , "BEST_BEFORE"               ,  6);
	ДобавитьКодGS1(Коды, "16"  , "SELL_BY"                   ,  6);
	ДобавитьКодGS1(Коды, "17"  , "EXPIRE"                    ,  6);
	ДобавитьКодGS1(Коды, "20"  , "VARIANT"                   ,  2);
	ДобавитьКодGS1(Коды, "21"  , "SERIAL"                    ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "22"  , "CPV"                       ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "240" , "ADDITIONAL_ID"             ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "241" , "CUSTOMER_PART_NO"          ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "242" , "MTO_VARIANT"               ,   ,  6);
	ДобавитьКодGS1(Коды, "243" , "PCN"                       ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "250" , "SECONDARY_SERIAL"          ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "251" , "REF_TO_SOURCE"             ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "253" , "GDTI"                      , 13, 17,  ТипGS1Число(), ТипGS1Строка());
	ДобавитьКодGS1(Коды, "254" , "GLN_EXTENSION_COMPONENT"   ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "255" , "GСТ"                       , 13, 12);
	ДобавитьКодGS1(Коды, "30"  , "VAR_COUNT"                 ,   , 8);
	ДобавитьКодGS1(Коды, "310n", "NET_WEIGHT_kg"             ,  6);
	ДобавитьКодGS1(Коды, "311n", "LENGTH_m"                  ,  6);
	ДобавитьКодGS1(Коды, "312n", "WIDTH_m"                   ,  6);
	ДобавитьКодGS1(Коды, "313n", "HEIGHT_m"                  ,  6);
	ДобавитьКодGS1(Коды, "314n", "AREA_m2"                   ,  6);
	ДобавитьКодGS1(Коды, "315n", "NET_VOLUME_l"              ,  6);
	ДобавитьКодGS1(Коды, "316n", "NET_VOLUME_m3"             ,  6);
	ДобавитьКодGS1(Коды, "320n", "NET_WEIGHT_lb"             ,  6);
	ДобавитьКодGS1(Коды, "321n", "LENGTH_i"                  ,  6);
	ДобавитьКодGS1(Коды, "322n", "LENGTH_f"                  ,  6);
	ДобавитьКодGS1(Коды, "323n", "LENGTH_y"                  ,  6);
	ДобавитьКодGS1(Коды, "324n", "WIDTH_i"                   ,  6);
	ДобавитьКодGS1(Коды, "325n", "WIDTH_f"                   ,  6);
	ДобавитьКодGS1(Коды, "326n", "WIDTH_y"                   ,  6);
	ДобавитьКодGS1(Коды, "327n", "HEIGHT_i"                  ,  6);
	ДобавитьКодGS1(Коды, "328n", "HEIGHT_f"                  ,  6);
	ДобавитьКодGS1(Коды, "329n", "HEIGHT_y"                  ,  6);
	ДобавитьКодGS1(Коды, "330n", "GROSS_WEIGHT_kg"           ,  6);
	ДобавитьКодGS1(Коды, "331n", "LENGTH_m_log"              ,  6);
	ДобавитьКодGS1(Коды, "332n", "WIDTH_m_log"               ,  6);
	ДобавитьКодGS1(Коды, "333n", "HEIGHT_m_log"              ,  6);
	ДобавитьКодGS1(Коды, "334n", "AREA_m2_log"               ,  6);
	ДобавитьКодGS1(Коды, "335n", "VOLUME_l_log"              ,  6);
	ДобавитьКодGS1(Коды, "336n", "VOLUME_m3_log"             ,  6);
	ДобавитьКодGS1(Коды, "337n", "KG_PER_m2"                 ,  6);
	ДобавитьКодGS1(Коды, "340n", "GROSS_WEIGHT_lb"           ,  6);
	ДобавитьКодGS1(Коды, "341n", "LENGTH_i_log"              ,  6);
	ДобавитьКодGS1(Коды, "342n", "LENGTH_f_log"              ,  6);
	ДобавитьКодGS1(Коды, "343n", "LENGTH_y_log"              ,  6);
	ДобавитьКодGS1(Коды, "344n", "WIDTH_i_log"               ,  6);
	ДобавитьКодGS1(Коды, "345n", "WIDTH_f_log"               ,  6);
	ДобавитьКодGS1(Коды, "346n", "WIDTH_y_log"               ,  6);
	ДобавитьКодGS1(Коды, "347n", "HEIGHT_i_log"              ,  6);
	ДобавитьКодGS1(Коды, "348n", "HEIGHT_f_log"              ,  6);
	ДобавитьКодGS1(Коды, "349n", "HEIGHT_y_log"              ,  6);
	ДобавитьКодGS1(Коды, "350n", "AREA_i2"                   ,  6);
	ДобавитьКодGS1(Коды, "351n", "AREA_f2"                   ,  6);
	ДобавитьКодGS1(Коды, "352n", "AREA_y2"                   ,  6);
	ДобавитьКодGS1(Коды, "353n", "AREA_i2_log"               ,  6);
	ДобавитьКодGS1(Коды, "354n", "AREA_f2_log"               ,  6);
	ДобавитьКодGS1(Коды, "355n", "AREA_y2_log"               ,  6);
	ДобавитьКодGS1(Коды, "356n", "NET_WEIGHT_t"              ,  6);
	ДобавитьКодGS1(Коды, "357n", "NET_VOLUME_oz"             ,  6);
	ДобавитьКодGS1(Коды, "360n", "NET_VOLUME_q"              ,  6);
	ДобавитьКодGS1(Коды, "361n", "NET_VOLUME_g"              ,  6);
	ДобавитьКодGS1(Коды, "362n", "VOLUME_q"                  ,  6);
	ДобавитьКодGS1(Коды, "363n", "VOLUME_g"                  ,  6);
	ДобавитьКодGS1(Коды, "364n", "VOLUME_i3"                 ,  6);
	ДобавитьКодGS1(Коды, "365n", "VOLUME_f3"                 ,  6);
	ДобавитьКодGS1(Коды, "366n", "VOLUME_y3"                 ,  6);
	ДобавитьКодGS1(Коды, "367n", "VOLUME_i3_log"             ,  6);
	ДобавитьКодGS1(Коды, "368n", "VOLUME_f3_log"             ,  6);
	ДобавитьКодGS1(Коды, "369n", "VOLUME_y3_log"             ,  6);
	ДобавитьКодGS1(Коды, "37"  , "COUNT"                     ,   ,  8);
	ДобавитьКодGS1(Коды, "390n", "AMOUNT"                    ,   , 15);
	ДобавитьКодGS1(Коды, "391n", "AMOUNT_ISO"                ,  3, 15);
	ДобавитьКодGS1(Коды, "392n", "PRICE"                     ,   , 15);
	ДобавитьКодGS1(Коды, "393n", "PRICE_ISO"                 ,  3, 15);
	ДобавитьКодGS1(Коды, "394n", "PRCNT_OFF"                 ,  4,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "400" , "ORDER_NUMBER"              ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "401" , "GINC"                      ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "402" , "GSIN"                      , 17,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "403" , "ROUTE"                     ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "410" , "SHIP_TO_LOC"               , 13);
	ДобавитьКодGS1(Коды, "411" , "BILL_TO"                   , 13);
	ДобавитьКодGS1(Коды, "412" , "PURCHASE_FROM"             , 13);
	ДобавитьКодGS1(Коды, "413" , "SHIP_FOR_LOC"              , 13);
	ДобавитьКодGS1(Коды, "414" , "LOC_No"                    , 13);
	ДобавитьКодGS1(Коды, "415" , "PAY_TO"                    , 13);
	ДобавитьКодGS1(Коды, "416" , "PROD_SERV_LOC"             , 13);
	ДобавитьКодGS1(Коды, "420" , "SHIP_TO_POST"              ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "421" , "SHIP_TO_POST_ISO"          ,  3,  9,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "422" , "ORIGIN"                    ,  3,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "423" , "CONTRY_INITIAL_PROCESS"    ,  3, 12);
	ДобавитьКодGS1(Коды, "424" , "CONTRY_PROCESS"            ,  3,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "425" , "CONTRY_DISASSEMBLY"        ,  3, 12);
	ДобавитьКодGS1(Коды, "426" , "CONTRY_FULL_PROCESS"       ,  3,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "427" , "ORIGIN_SUBDIVISION"        ,   ,  3,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7001", "NSN"                       , 13,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "7002", "MEAT_CUT"                  ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7003", "EXPIRY_TIME"               , 10,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "7004", "ACTIVE_POTENCY"            ,   ,  4);
	ДобавитьКодGS1(Коды, "7005", "CATCH_AREA"                ,   , 12,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7006", "FIRST_FREEZE_DATE"         ,  6,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "7007", "HARVEST_DATE"              ,  6,  6);
	ДобавитьКодGS1(Коды, "7008", "AQUATIC_SPECIES"           ,   ,  3,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7009", "FISHING_GEAR_TYPE"         ,   , 10,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7010", "PROD_METHOD"               ,   ,  2,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7020", "REFURB_LOT"                ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7021", "FUNC_STAT"                 ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7022", "REV_STAT"                  ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "7023", "GIAI_ASSEMBLY"             ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "703s", "PROCESSOR_s"               ,  3, 27,  ТипGS1Число(), ТипGS1Строка());
	ДобавитьКодGS1(Коды, "710" , "NHRN_PZN"                  ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "711" , "NHRN_CIP"                  ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "712" , "NHRN_CN"                   ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "713" , "NHRN_DRN"                  ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8001", "DIMENSIONS"                , 14,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8002", "CMT_No"                    ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8003", "GRAI"                      , 14, 16,  ТипGS1Число(), ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8004", "GIAI"                      ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8005", "PRICE_PER_UNIT"            ,  6,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8006", "ITIP_or_GCTIN"             , 18,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8007", "IBAN"                      ,   , 34,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8008", "PROD_TIME"                 ,  8, 4,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8010", "CPID"                      ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8011", "CPID_SERIAL"               ,   , 12);
	ДобавитьКодGS1(Коды, "8012", "VERSION"                   ,   , 20,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8017", "GSRN_PROVIDER"             , 18,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8018", "GSRN_RECIPIENT"            , 18,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8019", "SRIN"                      ,   , 10);
	ДобавитьКодGS1(Коды, "8020", "REF_No"                    ,   , 25,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8110", "COUPON_CODE_ID"            ,   , 70,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8111", "POINTS"                    ,  4,   ,               ,               , Истина);
	ДобавитьКодGS1(Коды, "8112", "PAPPERLESS_COUPON_CODE_ID" ,   , 70,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "8200", "PRODUCT_URL"               ,   , 70,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "90"  , "INTERNAL"                  ,   , 30,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "91"  , "INTERNAL1"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "92"  , "INTERNAL2"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "93"  , "INTERNAL3"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "94"  , "INTERNAL4"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "95"  , "INTERNAL5"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "96"  , "INTERNAL6"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "97"  , "INTERNAL7"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "98"  , "INTERNAL8"                 ,   , 90,               , ТипGS1Строка());
	ДобавитьКодGS1(Коды, "99"  , "INTERNAL9"                 ,   , 90,               , ТипGS1Строка());
	
	Возврат Коды
	
КонецФункции

Процедура РазобратьСтрокуШтрихкодаGS1СоСкобками(Штрихкод, РезультатРазбора, КодыGS1);
	
	РезультатРазбора.ПредставлениеШтрихкода = Штрихкод;
	
	ДлинаШтрихкода = СтрДлина(Штрихкод);
	МинимальнаяДлинаИдентификатораПрименения  = 2;
	МаксимальнаяДлинаИдентификатораПрименения = 4;
	
	НомерСимвола = 1;
	Пока НомерСимвола <= ДлинаШтрихкода Цикл
		
		Если Сред(Штрихкод, НомерСимвола, 1) <> "(" Тогда
			РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номер символа %1%. Отсутствует ""("".'"), НомерСимвола);
			Возврат;
		КонецЕсли;
		
		НомерСимвола = НомерСимвола + 1;
		
		Позиция = СтрНайти(Штрихкод, ")",, НомерСимвола);
		Если Позиция = 0 Тогда
			РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номер символа %1. Отсутствует "")"".'"), НомерСимвола);
			Возврат;
		КонецЕсли;
		
		ИдентификаторПрименения = Сред(Штрихкод, НомерСимвола, Позиция - НомерСимвола);
		ДлинаИдентификатора = СтрДлина(ИдентификаторПрименения);
		Если ДлинаИдентификатора < МинимальнаяДлинаИдентификатораПрименения Или ДлинаИдентификатора > МаксимальнаяДлинаИдентификатораПрименения Тогда
			РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номер символа %1. Неизвестный идентификатор применения(AI) %2.'"), НомерСимвола, ИдентификаторПрименения);
			Возврат;
		КонецЕсли;
		
		ПоложениеДесятичнойТочкиСтрокой = "";
		ОписаниеКода = КодыGS1[ИдентификаторПрименения];
		Если ОписаниеКода = Неопределено Тогда
			Если ДлинаИдентификатора = МаксимальнаяДлинаИдентификатораПрименения Тогда
				ОписаниеКода = КодыGS1[Лев(ИдентификаторПрименения, МаксимальнаяДлинаИдентификатораПрименения - 1)];
				ПоложениеДесятичнойТочкиСтрокой = Прав(ИдентификаторПрименения, 1);
			КонецЕсли;
		КонецЕсли;
		
		Если ОписаниеКода = Неопределено Тогда
			РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номер символа %1. Неизвестный идентификатор применения(AI) %2.'"), НомерСимвола, ИдентификаторПрименения);
			Возврат;
		КонецЕсли;
		
		НомерСимвола = Позиция + 1;
		
		Значение = "";
		Если ОписаниеКода.ФиксированнаяДлина > 0 Тогда
			Значение = Сред(ШтрихКод, НомерСимвола, ОписаниеКода.ФиксированнаяДлина);
			Если СтрДлина(Значение) <> ОписаниеКода.ФиксированнаяДлина Тогда
				РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номер символа %5. Длина значения (%3) для идентификатора применения(AI) ""%1 %2"" меньше требуемой (%4)'"),
						ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(Значение), ОписаниеКода.ФиксированнаяДлина, НомерСимвола);
				Возврат;
			КонецЕсли;
			
			Если ОписаниеКода.ТипФиксированногоЗначения = ТипGS1Число() Тогда
				Если Не сфпСтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Значение) Тогда
					РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Номер символа %4. Значение (%3) для идентификатора применения(AI) ""%1 %2"" должно содержать только цифры'"),
							ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(Значение), НомерСимвола);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			НомерСимвола = НомерСимвола + ОписаниеКода.ФиксированнаяДлина;
		КонецЕсли;
		
		Если ОписаниеКода.ПеременнаяДлина > 0 И Позиция < ДлинаШтрихкода Тогда
			ПозицияСледующегоИдентификатора = СтрНайти(Штрихкод, "(",, НомерСимвола);
			
			ПравильныйИдентификатор = Ложь;
			Пока ПозицияСледующегоИдентификатора > 0 И Не ПравильныйИдентификатор Цикл
				ПозицияЗакрывающегоИдентификатора = СтрНайти(Штрихкод, ")", , ПозицияСледующегоИдентификатора);
				ПредполагаемыйИдентификатор = Сред(Штрихкод, ПозицияСледующегоИдентификатора + 1,ПозицияЗакрывающегоИдентификатора - ПозицияСледующегоИдентификатора - 1);
				ПравильныйИдентификатор = СтрДлина(ПредполагаемыйИдентификатор) > 1 И СтрДлина(ПредполагаемыйИдентификатор) < 5 И сфпСтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПредполагаемыйИдентификатор)
					И НЕ ПозицияЗакрывающегоИдентификатора = 0 И КодыGS1[ПредполагаемыйИдентификатор] <> Неопределено;
				Если ПозицияСледующегоИдентификатора >= ДлинаШтрихкода Тогда
					ПозицияСледующегоИдентификатора = 0
				ИначеЕсли Не ПравильныйИдентификатор Тогда
					ПозицияСледующегоИдентификатора = СтрНайти(Штрихкод, "(",, ПозицияСледующегоИдентификатора  + 1);
				КонецЕсли;
			КонецЦикла;
			
			Если ПозицияСледующегоИдентификатора > 0 Тогда
				ЗначениеПеременное = Сред(Штрихкод, НомерСимвола, ПозицияСледующегоИдентификатора - НомерСимвола);
			Иначе
				ЗначениеПеременное = Сред(Штрихкод, НомерСимвола);
			КонецЕсли;
			
			Если СтрДлина(ЗначениеПеременное) > ОписаниеКода.ПеременнаяДлина Тогда
				РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номер символа %5. Длина значения (%3) переменной части для идентификатора применения(AI) ""%1 %2"" больше требуемой (%4)'"),
						ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(ЗначениеПеременное), ОписаниеКода.ПеременнаяДлина, НомерСимвола);
				Возврат;
			КонецЕсли;
			Если ОписаниеКода.ТипПеременногоЗначения = ТипGS1Число() Тогда
				Если Не сфпСтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЗначениеПеременное) Тогда
					РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Номер символа %4. Значение (%3) для идентификатора применения(AI) ""%1 %2"" должно содержать только цифры'"),
							ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(ЗначениеПеременное), НомерСимвола);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			НомерСимвола = НомерСимвола + СтрДлина(ЗначениеПеременное);
			Значение = Значение + ЗначениеПеременное;
		КонецЕсли;
		
		ПоложениеДесятичнойТочки = 0;
		Если Не ПустаяСтрока(ПоложениеДесятичнойТочкиСтрокой) Тогда
			ПоложениеДесятичнойТочки = Число(ПоложениеДесятичнойТочкиСтрокой);
			Если ПоложениеДесятичнойТочки > 0 Тогда
				Для Индекс = 0 По ПоложениеДесятичнойТочки - СтрДлина(Значение) Цикл
					Значение = "0" + Значение;
				КонецЦикла;
				Значение = Лев(Значение, СтрДлина(Значение) - ПоложениеДесятичнойТочки) + "." + Прав(Значение, ПоложениеДесятичнойТочки);
			КонецЕсли;
		КонецЕсли;
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ПоложениеДесятичнойТочки", ПоложениеДесятичнойТочки);
		ОписаниеДанных.Вставить("Значение", Значение);
		РезультатРазбора.ДанныеШтрихкода.Вставить(ОписаниеКода.Код, ОписаниеДанных);
		
	КонецЦикла;
	
	РезультатРазбора.Разобран = Истина;
	
КонецПроцедуры

Процедура РазобратьСтрокуШтрихкодаGS1Служебный(Штрихкод, РезультатРазбора, КодыGS1);
	
	ДлинаШтрихкода = СтрДлина(Штрихкод);
	ПредставлениеШтрихкода = "";
	
	НомерСимвола = 1;
	Пока НомерСимвола <= ДлинаШтрихкода Цикл
		
		ИдентификаторПрименения = Сред(Штрихкод, НомерСимвола, 2);
		ОписаниеКода = КодыGS1[ИдентификаторПрименения];
		Если ОписаниеКода = Неопределено Тогда
			ИдентификаторПрименения = Сред(Штрихкод, НомерСимвола, 3);
			ОписаниеКода = КодыGS1[ИдентификаторПрименения];
			Если ОписаниеКода = Неопределено Тогда
				ИдентификаторПрименения = Сред(Штрихкод, НомерСимвола, 4);
				ОписаниеКода = КодыGS1[ИдентификаторПрименения];
				Если ОписаниеКода = Неопределено Тогда
					РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Неизвестный идентификатор применения(AI) %1.'"), ИдентификаторПрименения);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		НомерСимвола = НомерСимвола + СтрДлина(ИдентификаторПрименения);
		
		ПоложениеДесятичнойТочкиСтрокой = "";
		Если ОписаниеКода.ЕстьПоложениеДесятичнойТочки Тогда
			ПоложениеДесятичнойТочкиСтрокой = Сред(Штрихкод, НомерСимвола, 1);
			НомерСимвола = НомерСимвола + 1;
		КонецЕсли;
		
		Значение = "";
		Если ОписаниеКода.ФиксированнаяДлина > 0 Тогда
			Значение = Сред(ШтрихКод, НомерСимвола, ОписаниеКода.ФиксированнаяДлина);
			Если СтрДлина(Значение) <> ОписаниеКода.ФиксированнаяДлина Тогда
				РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Длина значения (%3) для идентификатора применения(AI) ""%1 %2"" меньше требуемой (%4)'"),
						ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(Значение), ОписаниеКода.ФиксированнаяДлина);
				Возврат;
			КонецЕсли;
			Если ОписаниеКода.ТипФиксированногоЗначения = ТипGS1Число() Тогда
				Если Не сфпСтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Значение) Тогда
					РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Значение (%3) для идентификатора применения(AI) ""%1 %2"" должно содержать только цифры'"),
							ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(Значение));
					Возврат;
				КонецЕсли;
			КонецЕсли;
			НомерСимвола = НомерСимвола + ОписаниеКода.ФиксированнаяДлина;
		КонецЕсли;
		Если ОписаниеКода.ПеременнаяДлина > 0 Тогда
			ЗначениеПеременное = Сред(Штрихкод, НомерСимвола);
			Если СтрДлина(ЗначениеПеременное) > ОписаниеКода.ПеременнаяДлина Тогда
				РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Длина значения (%3) переменной части для идентификатора применения(AI) ""%1 %2"" больше требуемой (%4)'"),
						ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(ЗначениеПеременное), ОписаниеКода.ПеременнаяДлина);
				Возврат;
			КонецЕсли;
			Если ОписаниеКода.ТипПеременногоЗначения = ТипGS1Число() Тогда
				Если Не сфпСтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЗначениеПеременное) Тогда
					РезультатРазбора.ОписаниеОшибки = сфпСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Значение (%3) для идентификатора применения(AI) ""%1 %2"" должно содержать только цифры'"),
							ИдентификаторПрименения, ОписаниеКода.Имя, СтрДлина(ЗначениеПеременное));
					Возврат;
				КонецЕсли;
			КонецЕсли;
			НомерСимвола = НомерСимвола + СтрДлина(ЗначениеПеременное);
			Значение = Значение + ЗначениеПеременное;
		КонецЕсли;
		
		ПредставлениеШтрихкода = ПредставлениеШтрихкода + "(" + ИдентификаторПрименения + ПоложениеДесятичнойТочкиСтрокой + ")" + Значение;
		
		ПоложениеДесятичнойТочки = 0;
		Если Не ПустаяСтрока(ПоложениеДесятичнойТочкиСтрокой) Тогда
			ПоложениеДесятичнойТочки = Число(ПоложениеДесятичнойТочкиСтрокой);
			Если ПоложениеДесятичнойТочки > 0 Тогда
				Для Индекс = 0 По ПоложениеДесятичнойТочки - СтрДлина(Значение) Цикл
					Значение = "0" + Значение;
				КонецЦикла;
				Значение = Лев(Значение, СтрДлина(Значение) - ПоложениеДесятичнойТочки) + "." + Прав(Значение, ПоложениеДесятичнойТочки);
			КонецЕсли;
		КонецЕсли;
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ПоложениеДесятичнойТочки", ПоложениеДесятичнойТочки);
		ОписаниеДанных.Вставить("Значение", Значение);
		РезультатРазбора.ДанныеШтрихкода.Вставить(ОписаниеКода.Код, ОписаниеДанных);
		
	КонецЦикла;
	
	РезультатРазбора.ПредставлениеШтрихкода = РезультатРазбора.ПредставлениеШтрихкода + ПредставлениеШтрихкода;
	РезультатРазбора.Разобран = Истина;
	
КонецПроцедуры

Функция ТипGS1Число();
	
	Возврат "N";
	
КонецФункции

Функция ТипGS1Строка();
	
	Возврат "X";
	
КонецФункции

Процедура ДобавитьКодGS1(Коды, Код, Имя, ФиксированнаяДлина = 0, ПеременнаяДлина = 0, ТипФиксированногоЗначения = Неопределено, ТипПеременногоЗначения = Неопределено, ЕстьРазделитель = Неопределено);
	
	ВставляемыеКоды = Новый Массив;
	ПоследнийСимволКода = Прав(Код, 1);
	Если СтрНайти("0123456789", ПоследнийСимволКода) = 0 Тогда
		КодБезПоследнегоСимвола = Лев(Код, СтрДлина(Код) - 1);
		Если ПоследнийСимволКода = "n" Тогда
			Описание = ОписаниеКода(КодБезПоследнегоСимвола, Имя, ФиксированнаяДлина, ПеременнаяДлина, ТипФиксированногоЗначения, ТипПеременногоЗначения, ЕстьРазделитель);
			Описание.ЕстьПоложениеДесятичнойТочки = Истина;
			Коды.Вставить(КодБезПоследнегоСимвола, Описание);
		Иначе
			Для Индекс = 0 По 9 Цикл
				НовыйКод = КодБезПоследнегоСимвола + Строка(Индекс);
				Коды.Вставить(НовыйКод, ОписаниеКода(НовыйКод, Имя, ФиксированнаяДлина, ПеременнаяДлина, ТипФиксированногоЗначения, ТипПеременногоЗначения, ЕстьРазделитель));
			КонецЦикла;
		КонецЕсли;
	Иначе
		Коды.Вставить(Код, ОписаниеКода(Код, Имя, ФиксированнаяДлина, ПеременнаяДлина, ТипФиксированногоЗначения, ТипПеременногоЗначения, ЕстьРазделитель));
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеКода(Код, Имя, ФиксированнаяДлина = 0, ПеременнаяДлина = 0, ТипФиксированногоЗначения = Неопределено, ТипПеременногоЗначения = Неопределено, ЕстьРазделитель = Неопределено);
	
	ОписаниеКода = Новый Структура;
	ОписаниеКода.Вставить("Код", Код);
	ОписаниеКода.Вставить("Имя", Имя);
	ОписаниеКода.Вставить("ФиксированнаяДлина", ФиксированнаяДлина);
	Если ФиксированнаяДлина > 0 Тогда
		ОписаниеКода.Вставить("ТипФиксированногоЗначения", ?(ТипФиксированногоЗначения = Неопределено, ТипGS1Число(), ТипФиксированногоЗначения));
	КонецЕсли;
	ОписаниеКода.Вставить("ПеременнаяДлина", ПеременнаяДлина);
	Если ПеременнаяДлина > 0 Тогда
		ОписаниеКода.Вставить("ТипПеременногоЗначения", ?(ТипПеременногоЗначения = Неопределено, ТипGS1Число(), ТипПеременногоЗначения));
	КонецЕсли;
	ОписаниеКода.Вставить("ЕстьРазделитель", ?(ПеременнаяДлина > 0, Истина, ЗначениеЗаполнено(ЕстьРазделитель)));
	ОписаниеКода.Вставить("ЕстьПоложениеДесятичнойТочки", Ложь);
	
	Возврат ОписаниеКода;
	
КонецФункции

// Функция возвращает разделитель GS1.
//
Функция РазделительGS1() Экспорт;
	
	Возврат Символ(29); // Dec 29
	
КонецФункции

// Функция возвращает экранированный символ GS1.
//
Функция ЭкранированныйСимволGS1() Экспорт
	
	Возврат "\x1d"; // Используется для экранирования символа GS1.
	
КонецФункции

#Если Клиент Тогда

// Функция записывает значения штрихкодов в информационную базу.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Истина - если значения штрихкоды были записаны, или их не требуется записывать
//  Ложь   - если значения штрихкоды не удалось записать.
Функция ЗаписатьШтрихКоды() Экспорт
	НаборЗаписейШтрихКоды = РегистрыСведений.ШтрихКоды.СоздатьНаборЗаписей();

	Если ТипЗнч(Объект)=Тип("СправочникОбъект.Номенклатура") Тогда
		ИспользованиеШтрихКодов = Объект.ТипНоменклатуры.ИспользованиеШтрихКодов;
	Иначе
		ИспользованиеШтрихКодов = Неопределено;
	КонецЕсли;
	
	Для Каждого Строка Из Состав Цикл
		//Строка.ШтрихКод=СокрЛП(Строка.ШтрихКод);
		Если обЗначениеНеЗаполнено(Строка.ШтрихКод) Тогда Продолжить; КонецЕсли;
		Если НЕ ШтрихКодУникален(Строка.ШтрихКод) Тогда
			Сообщить(Строка.ШтрихКод+" - штрихкод не уникален! Штрихкод не записан!",СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;
		Если ШтрихКодЗапрещен(Строка.ШтрихКод) Тогда
			Сообщить(Строка.ШтрихКод+" - штрихкод запрещен! Штрихкод не записан!",СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;
		
		Запись = НаборЗаписейШтрихКоды.Добавить();
		Запись.Объект   = Объект.Ссылка;
		Запись.ШтрихКод = Строка.ШтрихКод;
		Запись.Запрет = Строка.Запрещен;
		Если ИспользованиеШтрихКодов <> Неопределено Тогда
			Если ИспользованиеШтрихКодов = 2 Тогда
				Запись.ЕдиницаИзмерения = Строка.Объект;
			ИначеЕсли ИспользованиеШтрихКодов = 3 Тогда
				Запись.ХарактеристикаНоменклатуры = Строка.Объект;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;

	НаборЗаписейШтрихКоды.Отбор.Объект.Установить(Объект.Ссылка);

	Попытка
		НаборЗаписейШтрихкоды.Записать();
	Исключение
		Предупреждение("Не удалось записать штрихкод:" + Символы.ПС + ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;

	Возврат Истина;
КонецФункции // ЗаписатьШтрихКоды()

// Функция проверяет уникальность штрихкода
//
// Параметры
//  <ШтрихКод>  – Проверяемый штрихкод
//                 <продолжение описания параметра>
// Возвращаемое значение:
//  Истина - если код уникальный
//  Ложь   - если код неуникальный
Функция ШтрихКодУникален(ШтрихКод) Экспорт
	Если ПустаяСтрока(ШтрихКод) Тогда
		Возврат Истина; // проверять нечего, говорим что все Ок
	КонецЕсли;
	// первый шаг. Проверим уникальность ШК в регистре
	Запрос = Новый Запрос();
    Запрос.УстановитьПараметр("ШтрихКод", ШтрихКод);
	Запрос.УстановитьПараметр("Объект", Объект.Ссылка);
	Запрос.Текст = "
    |ВЫБРАТЬ
	|	ШтрихКоды.ШтрихКод,
	|	ШтрихКоды.Объект
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
    |ГДЕ
	|	ШтрихКоды.ШтрихКод=&ШтрихКод И ШтрихКоды.Объект<>&Объект";

	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		// проверку не прошел
	    Возврат Ложь;
	Иначе
		// шаг 2. Проверим уникальность ШК среди строк таблицы "Состав"
		МассивСтрок=Состав.НайтиСтроки(Новый Структура("ШтрихКод",ШтрихКод));
		Возврат (МассивСтрок.Количество()<2);
	КонецЕсли; 
	
КонецФункции // ШтрихКодУникален()

// Функция проверяет штрих код на наличие в списке запрещенных штрих кодов
//
// Параметры
//  <ШтрихКод>  – Проверяемый штрихкод
// Возвращаемое значение:
//  Истина - если код запрещен
//  Ложь   - если код разрешен
Функция ШтрихКодЗапрещен(ШтрихКод) Экспорт
	//ШК=СокрЛП(ШтрихКод);
	ШК=Строка(ШтрихКод);
	НайденныйШК="";
	// 1. ищем ШК
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ЗапрещенныеШтрихКоды.ШтрихКод КАК ШтрихКод
	|ИЗ
	|	РегистрСведений.ЗапрещенныеШтрихКоды КАК ЗапрещенныеШтрихКоды
	|ГДЕ
	|	НЕ ЗапрещенныеШтрихКоды.ЭтоПрефикс И ЗапрещенныеШтрихКоды.ШтрихКод = &ШтрихКод");
	Запрос.УстановитьПараметр("ШтрихКод",ШК);
	Результат=Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		НайденныйШК=Результат.Выгрузить().Получить(0).ШтрихКод;
	КонецЕсли;
	
	// 2. если не нашли ШК, то поищем префикс
	Если ПустаяСтрока(НайденныйШК) Тогда
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ЗапрещенныеШтрихКоды.ШтрихКод КАК Префикс
		|ИЗ
		|	РегистрСведений.ЗапрещенныеШтрихКоды КАК ЗапрещенныеШтрихКоды
		|ГДЕ
		|	ЗапрещенныеШтрихКоды.ЭтоПрефикс");
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Сред(ШК,1,СтрДлина(Выборка.Префикс))=СокрЛП(Выборка.Префикс) Тогда
				НайденныйШК=Выборка.Префикс; Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат НЕ ПустаяСтрока(НайденныйШК)
КонецФункции // ШтрихКодЗапрещен()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТЧИКИ ДЕЙСТВИЙ КОМАНДНОЙ ПАНЕЛИ В КАРТОЧКЕ НОМЕНКЛАТУРЫ

// Создает штрих-код для новых объектов
//
Процедура НовыйОбъектШтрихКод() Экспорт
	Если ТипЗнч(Объект)=Тип("СправочникОбъект.Номенклатура") Тогда
		ИспользованиеШтрихКодов = Объект.ТипНоменклатуры.ИспользованиеШтрихКодов;
		Если ИспользованиеШтрихКодов = 1 Тогда
		    Возврат;
		ИначеЕсли (ИспользованиеШтрихКодов = 2) или (ИспользованиеШтрихКодов = 3) Тогда
	    	Строка = Состав.Добавить();	
			Если ИспользованиеШтрихКодов = 2 Тогда
				Спр = Справочники.ЕдиницыИзмерения;
			Иначе
	        	Спр = Справочники.ХарактеристикиНоменклатуры;
			КонецЕсли;
			Форма = Спр.ПолучитьФормуНовогоЭлемента();
			Форма.Владелец = Объект.Ссылка;
			Форма.ОткрытьМодально();
			ПрочитатьЗаполнитьШтрихКоды();
		КонецЕсли; 
	Иначе
	    Возврат;
	КонецЕсли; 
КонецПроцедуры // НовыйОбъектШтрихКод()

// Копирует штрих-код объекта
//
Процедура КопироватьОбъектШтрихКод(ТекОбъект)  Экспорт
	Если обЗначениеНеЗаполнено(ТекОбъект) Тогда
	    Возврат;	
	КонецЕсли; 
	НовОбъект = ТекОбъект.Скопировать();
	Форма = НовОбъект.ПолучитьФорму();
	Форма.ОткрытьМодально();
	ПрочитатьЗаполнитьШтрихКоды();
КонецПроцедуры // КопироватьОбъектШтрихКод()

// Изменяет штрих код объекта
//
Процедура ИзменитьОбъектШтрихКод(ТекОбъект)  Экспорт
	Если обЗначениеНеЗаполнено(ТекОбъект) Тогда
	    Возврат;	
	КонецЕсли; 
	Форма = ТекОбъект.ПолучитьФорму();
	Форма.ОткрытьМодально();
	ПрочитатьЗаполнитьШтрихКоды();
КонецПроцедуры // ИзменитьОбъектШтрихКод()

// очищаем штрих код в текущей строке
//
Процедура ОчиститьШтрихКод(ТекСтрока)  Экспорт
	Если обЗначениеНеЗаполнено(ТекСтрока) Тогда Возврат; КонецЕсли; 
	ТекСтрока.ШтрихКод="";
КонецПроцедуры // ОчиститьШтрихКод()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

// инициализируем случайную последовательность
ИндексСлучайных=Неопределено;
НаборСлучайных=Неопределено;
ГСЧ = Новый ГенераторСлучайныхЧисел;

ПрефиксВесовогоШК = Константы.ПрефиксВесовогоШК.Получить();
ПрефиксШтучногоШК = Константы.ПрефиксШтучногоШК.Получить();

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем ДоступностьХарактеристик Экспорт; // доступность характеристик

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Добавление позиции в документ при подборе, вызывается из формы подбора 
//и при стандартной обработке ОбработкеВыбора, в случае подбора по справочнику.
//В документ добавляется элемент справочника Номенклатура, если элемент представляет
//собой Группу справочника или Набор - то добавляются соотв. состаявляющие.
//Добавление в документ производится в основной единице измерения, если не указана иная.
//Если строка с указанной Номенклатурой и Единицей измерения отсутствует, 
//то добавляется новая строка или изменяется количество в противном случае.
//
//Параметры:
//	ЭтаФорма - Форма, из которой произведен вызов.
//	СтруктураПараметровПодбора - Структура, содержащая параметры подбора.
//	Номенклатура - СправочникСсылка.Номенклатура, добавляемая номенклатура (Элемент,Группа,Набор).
//	СпроситьКоличество - Булево, флаг запроса количества при подборе.
//	СпроситьЦену - Булево, флаг запроса цены при подборе.
//	Количество - Число, значение количества по умолчанию.
//	УстановитьТекущей - Булево, флаг необходимости установить добавляемую строку текущей в табличной части документа
//
Функция ДобавитьНоменклатуру(ЭтаФорма, СтруктураПараметровПодбора, ВыбНоменклатура, СпроситьКоличество, СпроситьЦену, Количество=1, УстановитьТекущей=ИСТИНА,СпроситьХарактеристику=ЛОЖЬ, Цена=0) Экспорт
	
    Если ВыбНоменклатура.Ссылка.ЭтоГруппа Тогда
        Номенклатура = ВыбНоменклатура.Ссылка;
    Иначе
        Попытка 
            Номенклатура = ВыбНоменклатура.Ссылка.Номенклатура;
        Исключение
            Номенклатура = ВыбНоменклатура;
        КонецПопытки;
    КонецЕсли;
    
	// Если пользователь решил отказаться от продолжения добавления товара
	#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Если НЕ обЗначениеНеЗаполнено(Номенклатура) Тогда
		РезПроверки = дкПроверитьВозможностьОперацийСНоменклатурой(Номенклатура,ЭтаФорма.ВладелецФормы, ЭтаФорма.ВладелецФормы.ЭтотОбъект);
		
		Если (РезПроверки.ЕстьБлок) И НЕ ПустаяСтрока(РезПроверки.Сообщение) Тогда
			Сообщить(РезПроверки.Сообщение,СтатусСообщения.Внимание);
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ОткрытаФормаКоличестваГруппы=Неопределено;
	ОткрытаФормаКоличестваСписка=Неопределено;
	СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваГруппы", ОткрытаФормаКоличестваГруппы);
	СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваСписка", ОткрытаФормаКоличестваСписка);
	Если ОткрытаФормаКоличестваГруппы=Неопределено Тогда
		ОткрытаФормаКоличестваГруппы = Ложь;
	КонецЕсли;
	
	Если ОткрытаФормаКоличестваСписка=Неопределено Тогда
		ОткрытаФормаКоличестваСписка = Ложь;
	КонецЕсли;
	
	Попытка
		ПодразделениеКомпании = ЭтаФорма.ПодразделениеКомпании;
	Исключение
		Попытка
			ПодразделениеКомпании = СтруктураПараметровПодбора.ФормаДокумента.ПодразделениеКомпании;
		Исключение
			ПодразделениеКомпании = Неопределено;
		КонецПопытки;
	КонецПопытки;
	
	// Получим имя реквизита табличной части "количество"
	ИмяРеквизитаКоличества = Неопределено;
	СтруктураПараметровПодбора.Свойство("ИмяРеквизитаКоличества",ИмяРеквизитаКоличества);
	Если ИмяРеквизитаКоличества = Неопределено Тогда
		ИмяРеквизитаКоличества = "Количество";
		СтруктураПараметровПодбора.Вставить("ИмяРеквизитаКоличества",ИмяРеквизитаКоличества);
	КонецЕсли;
	
	ЭтоГруппа = Номенклатура.ЭтоГруппа;
	ЭтоНабор  = (НЕ ЭтоГруппа) И (Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор);
	СтруктураПараметровПодбора.Вставить("ЭтоГруппа",ЭтоГруппа);
    СтруктураПараметровПодбора.Вставить("Номенклатура",Номенклатура);
	СтруктураПараметровПодбора.Вставить("ЭтоНабор",	ЭтоНабор);
	СтруктураПараметровПодбора.Вставить("УстановитьТекущей",УстановитьТекущей);
	
	//если группа или набор - нужно развернуть, спросим количество один раз для всех
	Если ЭтоГруппа ИЛИ ЭтоНабор Тогда
		ЭтаФорма.МногострочныйВыбор = ЭтоГруппа;
		
		ФормаКоличества=ПолучитьОбщуюФорму("ВводПараметровПодбора");
		ФормаКоличества.ИмяРеквизитаКоличества	= ИмяРеквизитаКоличества;
		ФормаКоличества.ВладелецФормы			= ЭтаФорма;
		ФормаКоличества.ЗакрыватьПриВыборе		= ЛОЖЬ;
		ФормаКоличества.Количество				= Количество;
		ФормаКоличества.Цена        	    	= 0;
		ФормаКоличества.Сумма					= 0;
		ФормаКоличества.Заголовок				= "Ввод параметров подбора для "+?(ЭтоГруппа,"группы","набора")+" """ + Номенклатура + """";
		ФормаКоличества.ЕдиницаИзмерения		= ?( ЭтоГруппа, Неопределено, Номенклатура.ОсновнаяЕдиницаИзмерения);
		ФормаКоличества.ВладелецЕдиницИзмерения = ?( ЭтоГруппа, Номенклатура ,?(Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1, Номенклатура.ТипНоменклатуры, Номенклатура));
		ФормаКоличества.Номенклатура = Номенклатура;
		ФормаКоличества.СтруктураПараметровПодбора = СтруктураПараметровПодбора;
		ФормаКоличества.ЗапрашиватьХарактеристикуНоменклатуры = Ложь;
        ФормаКоличества.ВидПодбора = 2;
		Если СпроситьКоличество ИЛИ СпроситьЦену ИЛИ СпроситьХарактеристику Тогда
			//перечисляем случаи, когда открывать количество для группы не надо -
			//уже было введено для списка или должно вводиться отдельно для каждого
			Если СпроситьЦену ИЛИ СпроситьХарактеристику ИЛИ ОткрытаФормаКоличестваГруппы ИЛИ ОткрытаФормаКоличестваСписка Тогда
				//не спрашиваем количество для всей группы, 
				//вызываем ВыполнитьОбработкуВыбора, чтобы
				//открыть форму отдельно для каждой номенклатуры
				СтруктураВыбор = Новый Структура("Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры");
				СтруктураВыбор.Количество 					= 1;
				СтруктураВыбор.Цена       					= 0;
				СтруктураВыбор.ЕдиницаИзмерения       		= ?(ЭтоГруппа,Неопределено,Номенклатура.ОсновнаяЕдиницаИзмерения);
				СтруктураВыбор.ХарактеристикаНоменклатуры  	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
				СтруктураВозврата = Новый Структура("СтруктураПараметровПодбора,СтруктураВыбор",СтруктураПараметровПодбора,СтруктураВыбор);
				ВыполнитьОбработкуВыбора(ЭтаФорма,СтруктураВозврата,ФормаКоличества)
			Иначе 
				//спрашиваем только количество для всей группы
				ФормаКоличества.СтруктураПараметровПодбора.Вставить("ОткрытаФормаКоличестваГруппы",Истина);
				ФормаКоличества.Открыть(); // немодально
			КонецЕсли;
		Иначе
			//если не спрашиваем ни количество, ни цену, ни характеристику, 
			//а просто добавляем каждой номенклатуры из группы по 1 штуке
			// форму не открываем, но оповестить необходимо
			СтруктураВыбор = Новый Структура("Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры");
			СтруктураВыбор.Количество 					= Количество;
			СтруктураВыбор.Цена       					= 0;
			СтруктураВыбор.ЕдиницаИзмерения       		= ?(ЭтоГруппа,Неопределено,Номенклатура.ОсновнаяЕдиницаИзмерения);
			СтруктураВыбор.ХарактеристикаНоменклатуры  	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			СтруктураВозврата = Новый Структура("СтруктураПараметровПодбора,СтруктураВыбор",СтруктураПараметровПодбора,СтруктураВыбор);
			ВыполнитьОбработкуВыбора(ЭтаФорма,СтруктураВозврата,ФормаКоличества);
		КонецЕсли;
		//добавляем одиночный элемент
	Иначе
		ОтображениеУслуг=Неопределено;
		СтруктураПараметровПодбора.Свойство("ОтображениеУслуг",ОтображениеУслуг);
		Если ОтображениеУслуг=Неопределено Тогда
			ОтображениеУслуг=Истина;
			СтруктураПараметровПодбора.Вставить("ОтображениеУслуг",ОтображениеУслуг);
		КонецЕсли;
		
		// получим табличную часть документа
		ФормаДокумента="";
		СтруктураПараметровПодбора.Свойство("ФормаДокумента",ФормаДокумента);
		Если обЗначениеНеЗаполнено(ФормаДокумента) Тогда
			ФормаДокумента=ЭтаФорма.ВладелецФормы;
			СтруктураПараметровПодбора.Вставить("ФормаДокумента",ФормаДокумента);
		КонецЕсли;
		
		ИмяТабличнойЧасти = "";
		СтруктураПараметровПодбора.Свойство("ИмяТабличнойЧасти",ИмяТабличнойЧасти);
		Если обЗначениеНеЗаполнено(ИмяТабличнойЧасти) Тогда
			Попытка
				ТабличнаяЧасть = ФормаДокумента.Товары;
				СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти","Товары");
				ИмяТабличнойЧасти = "Товары";
			Исключение
				Возврат Неопределено;
			КонецПопытки;
		Иначе
			ТабличнаяЧасть=ФормаДокумента[ИмяТабличнойЧасти];
            СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
		КонецЕсли;
		
		ИмяТабличногоПоля="";
		СтруктураПараметровПодбора.Свойство("ИмяТабличногоПоля",ИмяТабличногоПоля);
		Если обЗначениеНеЗаполнено(ИмяТабличногоПоля) Тогда
			ИмяТабличногоПоля="Товары";
			СтруктураПараметровПодбора.Вставить("ИмяТабличногоПоля",ИмяТабличногоПоля);
		КонецЕсли;
		
		// получим виды номенклатуры которые нельзя выбирать
		Попытка БлокироватьВидыНоменклатуры=ФормаДокумента.ЭтотОбъект.ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры(); Исключение БлокироватьВидыНоменклатуры=Неопределено; КонецПопытки;
		
		// Игнорируем запрещенную номенклатуру
		Если БлокироватьВидыНоменклатуры<>Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры)=Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
			Попытка ЕстьОшибка=(БлокироватьВидыНоменклатуры[Номенклатура.ВидНоменклатуры]<>Неопределено); Исключение ЕстьОшибка=Ложь; КонецПопытки;
			Если (НЕ ОтображениеУслуг) И (Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга) Тогда ЕстьОшибка=Истина; КонецЕсли;
			Если ЕстьОшибка Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;	
		
		// Оповестим о выборе документ, инициировавший подбор.
		ОбработкаВДокументе=Ложь;
		СтруктураПараметровПодбора.Свойство("ОбработкаВДокументе",ОбработкаВДокументе);
		Если ОбработкаВДокументе = Неопределено Тогда
			ОбработкаВДокументе = Ложь;
		КонецЕсли; 
		// если обработка заполнения товара находится в документе, то передадим выбор туда
		Если ОбработкаВДокументе Тогда 
			ЭтаФорма.ОповеститьОВыборе(Номенклатура);
			Возврат Неопределено;
		КонецЕсли;
		
		ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения;
		
		// поищем выбранную номенклатуру в таблице товаров
		МассивСтрок=Новый Массив();
		Попытка МассивСтрок=ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура",Номенклатура));
		Исключение Попытка МассивСтрок=ТабличнаяЧасть.НайтиСтроки(Новый Структура("НоменклатураРасход",Номенклатура)); Исключение; КонецПопытки; КонецПопытки;
		
		Если СпроситьЦену ИЛИ СпроситьХарактеристику ИЛИ (СпроситьКоличество И НЕ (ОткрытаФормаКоличестваГруппы ИЛИ ОткрытаФормаКоличестваСписка)) Тогда
			//если что-то нашли нужно заполнить текущие параметры формы подбора
			ВалютаЦены  = ЭтаФорма.ВалютаДокумента;
			Если Цена=0 Тогда
				Попытка
					КонтрагентДляЦены = ЭтаФорма.КонтрагентДляЦены;
				Исключение
					КонтрагентДляЦены = Справочники.Контрагенты.ПустаяСсылка();
				КонецПопытки;
				Цена = ?(ЭтаФорма.ОстаткиИЦеныНа,обПолучитьЦену(ЭтаФорма.ТипЦен,Номенклатура,ЭтаФорма.ДатаРасчетов, КонтрагентДляЦены,ВалютаЦены,,,,ПодразделениеКомпании),обПолучитьЦену(ЭтаФорма.ТипЦен,Номенклатура,,,ВалютаЦены,,,,ПодразделениеКомпании));
			КонецЕсли;
			Количество = 1;
			КоличествоСтарое=0;
			ЦенаСтарая=0;
			СуммаСтарая=0;
			ЕдиницаИзмеренияСтарая=Неопределено;
			Если МассивСтрок.Количество()>0 Тогда
				//если нашли возьмем первое вхождение, чтоб оттуда утянуть единицу
				Если МассивСтрок.Количество() = 1 Тогда
					ТекущаяСтрока=МассивСтрок[0];
					Попытка КоличествоСтарое=ТекущаяСтрока[ИмяРеквизитаКоличества]; Исключение КонецПопытки;
					Попытка ЦенаСтарая=ТекущаяСтрока["Цена"]; Исключение КонецПопытки;
					Попытка СуммаСтарая=ТекущаяСтрока["Сумма"]; Исключение КонецПопытки;
					Попытка ЕдиницаИзмеренияСтарая=ТекущаяСтрока["ЕдиницаИзмерения"]; Исключение КонецПопытки;
					Попытка ЕдиницаИзмерения=ТекущаяСтрока["ЕдиницаИзмерения"]; Исключение КонецПопытки;
				Иначе //заполним по всем строкам и приведем к основной единице
					КоэффициентОсновнойЕдиницы = ?(Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент = 0, 1, Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент);
					Для Каждого ТекущаяСтрока Из МассивСтрок Цикл
						Попытка КоличествоСтарое=КоличествоСтарое+ТекущаяСтрока[ИмяРеквизитаКоличества]*ТекущаяСтрока["Коэффициент"]/КоэффициентОсновнойЕдиницы; Исключение КонецПопытки;
						Попытка СуммаСтарая=СуммаСтарая+ТекущаяСтрока["Сумма"]; Исключение КонецПопытки;
					КонецЦикла;
					ЕдиницаИзмеренияСтарая = Номенклатура.ОсновнаяЕдиницаИзмерения;
					ЦенаСтарая = СуммаСтарая/?(КоличествоСтарое*ЕдиницаИзмеренияСтарая.Коэффициент=0,1,КоличествоСтарое*ЕдиницаИзмеренияСтарая.Коэффициент);
				КонецЕсли;
			КонецЕсли;
			//открываем форму для ввода значений
			ЭтаФорма.МногострочныйВыбор = Ложь;
			ФормаКоличества=ПолучитьОбщуюФорму("ВводПараметровПодбора");
			ФормаКоличества.ИмяРеквизитаКоличества=ИмяРеквизитаКоличества;
			ФормаКоличества.ВладелецФормы=ЭтаФорма;
			ФормаКоличества.Заголовок = "Ввод параметров подбора для "+ Номенклатура + """";
			ФормаКоличества.Количество=Количество;
			ФормаКоличества.Цена=Цена;
			ФормаКоличества.Сумма=Цена;
			ФормаКоличества.ЕдиницаИзмерения = ЕдиницаИзмерения;
			ФормаКоличества.ЕдиницаИзмеренияСтарая = ЕдиницаИзмеренияСтарая;
			ФормаКоличества.ЦенаСтарая = ЦенаСтарая;
			ФормаКоличества.СуммаСтарая = СуммаСтарая;
			ФормаКоличества.КоличествоСтарое = КоличествоСтарое;
			ФормаКоличества.Номенклатура = Номенклатура;
			ФормаКоличества.ВладелецЕдиницИзмерения = ?(Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1,Номенклатура.ТипНоменклатуры,Номенклатура);
			ФормаКоличества.ЗапрашиватьХарактеристикуНоменклатуры = СпроситьХарактеристику;
			СтруктураПараметровПодбора.Вставить("ТекущаяСтрока",ТекущаяСтрока);
			ФормаКоличества.СтруктураПараметровПодбора = СтруктураПараметровПодбора;
			ФормаКоличества.ВидПодбора = 2;
			ФормаКоличества.Открыть(); // немодально
		Иначе
			Если МассивСтрок.Количество()>0 Тогда
				ТекущаяСтрока=МассивСтрок[0];
			КонецЕсли;
			СтруктураВыбСтрока = Новый Структура("Номенклатура,Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры",
			Номенклатура,Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры);
			СтруктураПараметровПодбора.Вставить("СпроситьКоличество",СпроситьКоличество);
			СтруктураПараметровПодбора.Вставить("СпроситьЦену",СпроситьЦену);
			СтруктураПараметровПодбора.Вставить("УстановитьТекущей",УстановитьТекущей);
			ДобавитьСтрокуВДокумент(СтруктураВыбСтрока,СтруктураПараметровПодбора);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
КонецФункции // ДобавитьНоменклатуру()

// добавление выбранной строки в документа
Процедура ДобавитьСтрокуВДокумент(СтруктураВыбСтрока,СтруктураПараметровПодбора) Экспорт
	
	Номенклатура 				= СтруктураВыбСтрока.Номенклатура;
	Количество	 				= СтруктураВыбСтрока.Количество;
	Цена						= СтруктураВыбСтрока.Цена;
	ЕдиницаИзмерения			= СтруктураВыбСтрока.ЕдиницаИзмерения;
	ХарактеристикаНоменклатуры  = СтруктураВыбСтрока.ХарактеристикаНоменклатуры;
	// все необходимое уже использовалось ранее
	ФормаДокумента			= СтруктураПараметровПодбора.ФормаДокумента;
	ИмяТабличнойЧасти       = СтруктураПараметровПодбора.ИмяТабличнойЧасти;
	ИмяТабличногоПоля       = СтруктураПараметровПодбора.ИмяТабличногоПоля;
	ИмяРеквизитаКоличества  = СтруктураПараметровПодбора.ИмяРеквизитаКоличества;
	СпроситьКоличество      = СтруктураПараметровПодбора.СпроситьКоличество;
	СпроситьЦену       		= СтруктураПараметровПодбора.СпроситьЦену;
	УстановитьТекущей   	= СтруктураПараметровПодбора.УстановитьТекущей;
	ТабличнаяЧасть			= ФормаДокумента[ИмяТабличнойЧасти];

	#Если Клиент Тогда
		Состояние("Добавление элемента: " + Номенклатура);
	#КонецЕсли

	//теперь будем пробовать добавлять в документ
	//для начала поищем строку с выбранной единицей чтоб туда добавится
	Попытка
		МассивСтрок=ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры",Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры));
	Исключение
		Попытка
			МассивСтрок=ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура",Номенклатура));
		Исключение
			МассивСтрок=ТабличнаяЧасть.НайтиСтроки(Новый Структура("НоменклатураРасход, ЕдиницаИзмеренияРасход",Номенклатура,ЕдиницаИзмерения));
		КонецПопытки;
	КонецПопытки;
	
	НоваяСтрока = (МассивСтрок.Количество() = 0);
	Если НоваяСтрока Тогда
		ТекущаяСтрока = ТабличнаяЧасть.Добавить();
		Попытка	
			ТекущаяСтрока.Номенклатура = Номенклатура;
			Попытка
				ТекущаяСтрока.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;
			Исключение КонецПопытки;
			Попытка
				ДопПараметры = Новый Структура("ЕдиницаИзмерения",ЕдиницаИзмерения);
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Номенклатура",ТекущаяСтрока,ФормаДокумента, ДопПараметры);
				//ФормаДокумента.ОбработкаРеквизита("Товары.Номенклатура",ТекущаяСтрока,ФормаДокумента, ДопПараметры);
			Исключение КонецПопытки;
			Попытка
				ТекущаяСтрока[ИмяРеквизитаКоличества] = Количество;
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Количество",ТекущаяСтрока,ФормаДокумента);
				//ФормаДокумента.ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ФормаДокумента);
			Исключение КонецПопытки;
		Исключение
			ТекущаяСтрока.НоменклатураРасход = Номенклатура;
			Попытка
				ДопПараметры = Новый Структура("ЕдиницаИзмеренияРасход",ЕдиницаИзмерения);
				ФормаДокумента.ОбработкаРеквизита("Товары.НоменклатураРасход",ТекущаяСтрока,ФормаДокумента, ДопПараметры);
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".НоменклатураРасход",ТекущаяСтрока,ФормаДокумента, ДопПараметры);
			Исключение КонецПопытки;
			Попытка
				ТекущаяСтрока.ХарактеристикаНоменклатурыРасход = ХарактеристикаНоменклатуры;
			Исключение КонецПопытки;
			Если СпроситьКоличество Тогда
				Попытка
					ТекущаяСтрока.КоличествоРасход = Количество;
					ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Количество",ТекущаяСтрока,ФормаДокумента);
					//ФормаДокумента.ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ФормаДокумента);
				Исключение
				КонецПопытки; 
			КонецЕсли; 
		КонецПопытки;
		Попытка
			ТекущаяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		Исключение
		КонецПопытки;
	Иначе
		ТекущаяСтрока = МассивСтрок[0];
		//количество у нас в той единице что искали, можем смело прибавить
		Попытка
			ТекущаяСтрока[ИмяРеквизитаКоличества] = ТекущаяСтрока[ИмяРеквизитаКоличества] + Количество;
			ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + "." + ИмяРеквизитаКоличества,ТекущаяСтрока,ФормаДокумента);
			//ФормаДокумента.ОбработкаРеквизита("Товары."+ИмяРеквизитаКоличества,ТекущаяСтрока,ФормаДокумента);
		Исключение
			Попытка
				ТекущаяСтрока.КоличествоРасход = ТекущаяСтрока.КоличествоРасход + Количество;
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".КоличествоРасход",ТекущаяСтрока,ФормаДокумента);
				//ФормаДокумента.ОбработкаРеквизита("Товары.КоличествоРасход",ТекущаяСтрока,ФормаДокумента);
			Исключение
			КонецПопытки; 
		КонецПопытки;
	КонецЕсли;
	
	Если СпроситьЦену ИЛИ ЗначениеЗаполнено(Цена) Тогда
		// пересчитаем цену
		Попытка 
			ТекущаяСтрока.Цена = Цена;
			ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Цена",ТекущаяСтрока,ФормаДокумента); 
			//ФормаДокумента.ОбработкаРеквизита("Товары.Цена",ТекущаяСтрока,ФормаДокумента); 
		Исключение 
			Попытка 
				ТекущаяСтрока.ЦенаРасход = Цена;
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".ЦенаРасход",ТекущаяСтрока,ФормаДокумента); 
				//ФормаДокумента.ОбработкаРеквизита("Товары.ЦенаРасход",ТекущаяСтрока,ФормаДокумента); 
			Исключение 
			КонецПопытки;
		КонецПопытки; 	
	КонецЕсли;
	
	// Вызовем обработчики изменения в форме
	#Если Клиент Тогда
		// Проверим заполняется табличная часть документа или таблица значений
		// (если таблица значений, то обработчики изменения, нам не нужны)
		Если НЕ ТипЗнч(ФормаДокумента[ИмяТабличнойЧасти]) = Тип("ТаблицаЗначений") Тогда
			// Установим строку текущей
			Если УстановитьТекущей Тогда
				ФормаДокумента.ЭлементыФормы[ИмяТабличногоПоля].ТекущаяСтрока=ТекущаяСтрока;
			КонецЕсли;
			
			ОтменаРедактирования=ЛОЖЬ;
			дкТоварыПриОкончанииРедактирования(ФормаДокумента,ФормаДокумента.ЭлементыФормы[ИмяТабличногоПоля], НоваяСтрока, ОтменаРедактирования);
			дкВывестиЗаголовокСуммаДокумента(ФормаДокумента);
			//Состояние("");
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

// Выполнить обработку выбора
Процедура ВыполнитьОбработкуВыбора(ЭтаФорма,ЗначениеВыбора, Источник) Экспорт
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		ЭлементыФормы = ЭтаФорма.ЭлементыФормы;
		ФормаКоличества = Источник;
		СтруктураПараметровПодбора = ЗначениеВыбора.СтруктураПараметровПодбора; // входящая структура
		СтруктураВыбор = ЗначениеВыбора.СтруктураВыбор; // выходящая структура
		
		Если ЗначениеВыбора.СтруктураПараметровПодбора.Свойство("ИмяТабличногоПоляИсточника") Тогда
			НоменклатураДляПодбора = ЭлементыФормы[ЗначениеВыбора.СтруктураПараметровПодбора.ИмяТабличногоПоляИсточника];
		Иначе
			НоменклатураДляПодбора = ЭлементыФормы.ПрайсЛист;
		КонецЕсли;
		
		КоличествоВыделенныхСтрок = НоменклатураДляПодбора.ВыделенныеСтроки.Количество();
		Количество = СтруктураВыбор.Количество;
		ОткрытаФормаКоличестваГруппы=Неопределено;
		ОткрытаФормаКоличестваСписка=Неопределено;
		СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваГруппы", ОткрытаФормаКоличестваГруппы);
		СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваСписка", ОткрытаФормаКоличестваСписка);
		Если ОткрытаФормаКоличестваГруппы=Неопределено Тогда
			ОткрытаФормаКоличестваГруппы = Ложь;
		КонецЕсли;
		
		Если ОткрытаФормаКоличестваСписка=Неопределено Тогда
			ОткрытаФормаКоличестваСписка = Ложь;
		КонецЕсли;
		
		ВыделенныеСтроки = НоменклатураДляПодбора.ВыделенныеСтроки;
		
		Если (КоличествоВыделенныхСтрок > 1) И ОткрытаФормаКоличестваСписка И ЗначениеВыбора.СтруктураПараметровПодбора.Флажок Тогда
			//Добавляем по каждой строке
			//тут количество уже было введено для списка, значит не нужно спрашивать ничего больше,
			//формы тоже не будут открываться
			//флажок указывает, что при следующем вызове ВыполнитьОбработкуВыбора данный код не должен выполниться
			Цена = 0;
			Если СтруктураВыбор.Свойство("Цена") Тогда
				Цена = СтруктураВыбор.Цена;
			КонецЕсли;
            Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
                СтруктураПараметровПодбора.Вставить("Флажок",Ложь);
				ТекущаяСтрока = ДобавитьНоменклатуру(ЭтаФорма,  СтруктураПараметровПодбора, ВыделеннаяСтрока, ЛОЖЬ, ЛОЖЬ, Количество,ЛОЖЬ,, Цена);
				СтандартнаяОбработка = ЛОЖЬ;
			КонецЦикла; 
		    Флажок = Ложь;
		Иначе 
			Номенклатура = ФормаКоличества.Номенклатура;
			Попытка
				СпроситьКоличество = ЭтаФорма.ЗапрашиватьКоличество;
			Исключение
				СпроситьКоличество = Ложь;
			КонецПопытки;
			Попытка
				СпроситьЦену = ЭтаФорма.ЗапрашиватьЦену И ЭлементыФормы.ЗапрашиватьЦену.Доступность;
			Исключение
				СпроситьЦену = Ложь;
			КонецПопытки;
			Попытка
				СпроситьХарактеристику = ЭтаФорма.ЗапрашиватьХарактеристикуНоменклатуры И Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик <= 2 И ДоступностьХарактеристик;
			Исключение
				СпроситьХарактеристику = Ложь;
			КонецПопытки;
			// вызов из рекурсии функции ДобавитьНоменклатуру()
			//если группа или набор - нужно развернуть, спросим количество один раз для всех
            Номенклатура = СтруктураПараметровПодбора.Номенклатура;
			ЭтоГруппа = Номенклатура.ЭтоГруппа;
			ЭтоНабор  = СтруктураПараметровПодбора.ЭтоНабор;
			УстановитьТекущей  = СтруктураПараметровПодбора.УстановитьТекущей;
			Если ЭтоГруппа ИЛИ ЭтоНабор Тогда
				ЭтаФорма.МногострочныйВыбор = ЭтоГруппа;
				//разворачиваем наш список
				Если ЭтоГруппа Тогда
					ТекстЗапроса = 
					"ВЫБРАТЬ
                    |   ЕСТЬNULL(НоменклатураСоставНабора.Номенклатура, ПрайсЛист.Ссылка) КАК Ссылка,
                    |   СУММА(ВЫБОР
                    |           КОГДА НоменклатураСоставНабора.Номенклатура ЕСТЬ NULL 
                    |               ТОГДА &Количество
                    |           ИНАЧЕ ВЫБОР
                    |                   КОГДА ЕСТЬNULL(НоменклатураСоставНабора.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) = 0
                    |                       ТОГДА НоменклатураСоставНабора.Количество * &Количество
                    |                   ИНАЧЕ НоменклатураСоставНабора.Количество * &Количество / НоменклатураСоставНабора.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
                    |               КОНЕЦ
                    |       КОНЕЦ) КАК Количество
                    |ИЗ
                    |   Справочник.ПрайсЛист КАК ПрайсЛист
                    |       ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.СоставНабора КАК НоменклатураСоставНабора
                    |       ПО (НоменклатураСоставНабора.Ссылка = ПрайсЛист.Ссылка)
                    |ГДЕ
                    |   (НЕ ПрайсЛист.ЭтоГруппа)
                    |   И ПрайсЛист.Родитель В ИЕРАРХИИ(&Родитель)
                    |
                    |СГРУППИРОВАТЬ ПО
                    |   ЕСТЬNULL(НоменклатураСоставНабора.Номенклатура, ПрайсЛист.Ссылка)";
					Запрос = Новый Запрос(ТекстЗапроса);
					Запрос.УстановитьПараметр("Родитель",Номенклатура);
					Запрос.УстановитьПараметр("Количество",Количество);
					
					ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
					Для Каждого ТекТовар Из ТаблицаТоваров Цикл
						ТекущаяСтрока = ДобавитьНоменклатуру(ЭтаФорма,  СтруктураПараметровПодбора, ТекТовар, ЛОЖЬ, СпроситьЦену, ТекТовар.Количество, УстановитьТекущей,Ложь);
					КонецЦикла;
					
				ИначеЕсли ЭтоНабор Тогда
					Для Каждого НоменклатураИзНабора Из Номенклатура.СоставНабора Цикл
						ЕдиницаНоменклатурыИзНабора = НоменклатураИзНабора.Номенклатура.ОсновнаяЕдиницаИзмерения;
						ТекущаяСтрока = ДобавитьНоменклатуру(ЭтаФорма, СтруктураПараметровПодбора, НоменклатураИзНабора.Номенклатура, ЛОЖЬ, ЛОЖЬ, НоменклатураИзНабора.Количество*Количество/?(ЕдиницаНоменклатурыИзНабора.Коэффициент=0,1,ЕдиницаНоменклатурыИзНабора.Коэффициент),УстановитьТекущей);
					КонецЦикла;
				КонецЕсли;
				//добавляем одиночный элемент
			Иначе
				Если СпроситьКоличество ИЛИ СпроситьЦену ИЛИ СпроситьХарактеристику Тогда
					// гот. результат
					Количество 					= СтруктураВыбор.Количество;
					Цена 						= СтруктураВыбор.Цена;
					ЕдиницаИзмерения 			= СтруктураВыбор.ЕдиницаИзмерения;
					ХарактеристикаНоменклатуры 	= СтруктураВыбор.ХарактеристикаНоменклатуры;
				КонецЕсли;
			КонецЕсли;
			
			// поместим в структуру
			Если НЕ ЭтоНабор И НЕ ЭтоГруппа Тогда
				СтруктураВыбСтрока = Новый Структура("Номенклатура,Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры",
				Номенклатура,Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры);
				СтруктураПараметровПодбора.Вставить("СпроситьКоличество",СпроситьКоличество);
				СтруктураПараметровПодбора.Вставить("СпроситьЦену",СпроситьЦену);
				СтруктураПараметровПодбора.Вставить("УстановитьТекущей",УстановитьТекущей);
				ДобавитьСтрокуВДокумент(СтруктураВыбСтрока,СтруктураПараметровПодбора);
			КонецЕсли; 
			
			// ЗАВЕРШЕНИЕ
			// Покажем текущую строку
			Если ТекущаяСтрока<>Неопределено Тогда
				// Получим табличную часть документа
				ФормаДокумента="";
				Если (НЕ СтруктураПараметровПодбора.Свойство("ФормаДокумента",ФормаДокумента))ИЛИ(обЗначениеНеЗаполнено(ФормаДокумента)) Тогда
					ФормаДокумента=ЭтаФорма.ВладелецФормы;
				КонецЕсли;
				ИмяТабличногоПоля="";
				Если (НЕ СтруктураПараметровПодбора.Свойство("ИмяТабличногоПоля",ИмяТабличногоПоля))ИЛИ(обЗначениеНеЗаполнено(ИмяТабличногоПоля)) Тогда
					ИмяТабличногоПоля="Товары";
				КонецЕсли;
				// Установим текущей новую строку
				ФормаДокумента.ЭлементыФормы[ИмяТабличногоПоля].ТекущаяСтрока=ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры



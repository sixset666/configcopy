Перем УчетнаяЗапись;
Перем Хост;
Перем Логин;
Перем Пароль;
Перем Токен;
Перем Эндпоинт;
Перем SSL;
Перем ОшибкаЗапроса;

#Область ОсновныеФункцииОбмена
//Функция вызываемое регламентом
Функция ВыполнитьРегламентноеЗадание() Экспорт 
	ВосстановитьНастройки();
	ВыполнитьОбмен();
КонецФункции
//Функция выполняет первоначальные проверки и операции перед выгрузкой.
Функция ВыполнитьПервоначальныеОперации() Экспорт
	
	Если ВсеЛиОбязательныеРеквизитыЗаполнены() = Ложь Тогда
		Сообщить("Не заполнены обязательные реквизиты! Операция прервана.");
		Возврат Неопределено;
	КонецЕсли;
	
	ИспользоватьУчетныеДанные(СсылкаНаУчетнуюЗапись);
	
	Возврат Истина;
	
КонецФункции
Функция ВыполнитьОбмен() Экспорт 
	
	Если ВыгружатьОстатки Тогда
		ВыгрузитьОстатки();
	КонецЕсли;
	
	Если ВыгружатьРеализации Тогда
		ВыгрузитьРеализации();
	КонецЕсли
	
КонецФункции
Функция ВернутьТаблицуОстатков() Экспорт
	ТаблицаОстатков = ПолучитьТаблицуОстатков(КонтрагентыПоставщики,ПапкаНоменклатуры,СкладыКомпании);
	Возврат ТаблицаОстатков;
КонецФункции 
Функция ВыгрузитьОстатки() Экспорт
	
	Если ВыполнитьПервоначальныеОперации() <> ИСТИНА Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаОстатков = ПолучитьТаблицуОстатков(КонтрагентыПоставщики,ПапкаНоменклатуры,СкладыКомпании);
	
	МассивОстатков = Новый массив;
	
	Для Каждого СтрокаОстатков Из ТаблицаОстатков Цикл
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("id", 			МассивОстатков.Количество());
		СтруктураСтроки.Вставить("article", 	СтрокаОстатков.НоменклатураАртикул);
		СтруктураСтроки.Вставить("doc_date", 	Формат(СтрокаОстатков.ПартияДата,"ДФ=yyyy-MM-dd"));
		СтруктураСтроки.Вставить("count", 		СтрокаОстатков.КоличествоОстаток);
		СтруктураСтроки.Вставить("cost", 		СтрокаОстатков.Цена);
		МассивОстатков.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	ГодОтчета = Формат(Год(ТекущаяДатаСеанса()),"ЧЦ=4; ЧН=Ноль; ЧГ=0"); 
	МесяцОтчета = Формат(Месяц(ТекущаяДатаСеанса()),"ЧЦ=2; ЧН=Ноль; ЧГ=0");
	ДилерIDСтрокой = Формат(ДилерID,"ЧН=Ноль; ЧГ=0");
	
	Ссылка = Хост + "/api/service/stock-balance/balance?year="+ГодОтчета+"&month="+МесяцОтчета+"&dealer_id="+ДилерIDСтрокой; 
	
	СтруктураBody = новый Структура;
	СтруктураBody.Вставить("items",МассивОстатков); 
	
	Ответ = ВыполнитьHTTPЗапрос(Ссылка,"POST", СтруктураBody,,Токен,,Истина);
	Сообщить("Ответ: "+Ответ);
	Если ЗначениеЗаполнено(ОшибкаЗапроса) Тогда
		ЗаписатьВРегистр(ДилерID,"Остатки",Ложь,Ответ);
	Иначе
		ЗаписатьВРегистр(ДилерID,"Остатки",Истина,Ответ);
	КонецЕсли;
	
КонецФункции
Функция ВыгрузитьРеализации(НаДату=Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НаДату) Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ВыполнитьПервоначальныеОперации() <> ИСТИНА Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаРеализаций = ПолучитьТаблицуРеализаций(КонтрагентыПоставщики,ПапкаНоменклатуры,СкладыКомпании,КонтрагентыИсключенияДляРеализаций,НаДату);
	
	МассивРеализаций = Новый массив;
	
	Для Каждого СтрокаРеализации Из ТаблицаРеализаций Цикл
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("id", 			МассивРеализаций.Количество());
		СтруктураСтроки.Вставить("pn", 			СтрокаРеализации.НоменклатураАртикул);
		СтруктураСтроки.Вставить("cnt", 		СтрокаРеализации.КоличествоОстаток);
		СтруктураСтроки.Вставить("chn", 		СтрокаРеализации.КаналРеализации);
		СтруктураСтроки.Вставить("dn", 			СтрокаРеализации.ДокументПродажиНомер);
		СтруктураСтроки.Вставить("realization", Формат(СтрокаРеализации.ДокументПродажиДатаЗакрытия,"ДФ=yyyy-MM-dd"));
		СтруктураСтроки.Вставить("prc", 		СтрокаРеализации.Цена);
		МассивРеализаций.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	ГодОтчета = Формат(Год(ТекущаяДатаСеанса()),"ЧЦ=4; ЧН=Ноль; ЧГ=0"); 
	МесяцОтчета = Формат(Месяц(ТекущаяДатаСеанса()),"ЧЦ=2; ЧН=Ноль; ЧГ=0");
	ДилерIDСтрокой = Формат(ДилерID,"ЧН=Ноль; ЧГ=0");
	
	Ссылка = Хост + "/api/service/stock-balance/implementation?year="+ГодОтчета+"&month="+МесяцОтчета+"&dealer_id="+ДилерIDСтрокой; 
	
	СтруктураBody = новый Структура;
	СтруктураBody.Вставить("items",МассивРеализаций); 
	
	Ответ = ВыполнитьHTTPЗапрос(Ссылка,"POST", СтруктураBody,,Токен,,Истина);
	Сообщить("Ответ: "+Ответ);
	Если ЗначениеЗаполнено(ОшибкаЗапроса) Тогда
		ЗаписатьВРегистр(ДилерID,"Реализации",Ложь,Ответ);
	Иначе
		ЗаписатьВРегистр(ДилерID,"Реализации",Истина,Ответ);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Логирование
Функция ЛогированиеЗапросов(Хост,Метод,ПолнаяСсылка,ЗаголовкиЗапроса,ТелоЗапроса,КодОтвета,ЗаголовкиОтвета,ТелоОтвета)
	//Подготовка заголовков
	//Запроса  
	ТекстЗаголовкиЗапроса = "";
	Для Каждого Заголовок Из ЗаголовкиЗапроса Цикл
		ТекстЗаголовкиЗапроса = ТекстЗаголовкиЗапроса + Заголовок.Ключ + ": " + Заголовок.Значение + Символы.ПС;
	КонецЦикла;
	//Ответа  
	ТекстЗаголовкиОтвета = "";
	Для Каждого Заголовок Из ЗаголовкиОтвета Цикл
		ТекстЗаголовкиОтвета = ТекстЗаголовкиОтвета + Заголовок.Ключ + ": " + Заголовок.Значение + Символы.ПС;
	КонецЦикла;
	
	НоваяЗапись = РегистрыСведений.СБЕР_ЛогиЗапросов.СоздатьМенеджерЗаписи();
	НоваяЗапись.Период = ТекущаяДатаСеанса();
	НоваяЗапись.Хост = Хост;
	НоваяЗапись.Метод = Метод;
	НоваяЗапись.ПолнаяСсылка = ПолнаяСсылка;
	НоваяЗапись.ТелоЗапроса = ТелоЗапроса;
	НоваяЗапись.КодОтвета = КодОтвета;
	НоваяЗапись.ТелоОтвета = ТелоОтвета;
	НоваяЗапись.ЗаголовкиЗапроса = ТекстЗаголовкиЗапроса;
	НоваяЗапись.ЗаголовкиОтвета = ТекстЗаголовкиОтвета;
	НоваяЗапись.Записать();
КонецФункции
Функция ЗаписатьВРегистр(DealerID,ТипВыгрузки,Успех,ОтветСервиса)
	НоваяЗапись = РегистрыСведений.БТ_ЛогированиеИнфообменаGeely.СоздатьМенеджерЗаписи();
	НоваяЗапись.Период = ТекущаяДатаСеанса();
	НоваяЗапись.DealerID = DealerID;
	НоваяЗапись.ТипВыгрузки = ТипВыгрузки;
	НоваяЗапись.Успех = Успех;
	НоваяЗапись.ОтветСервиса = ОтветСервиса;
	НоваяЗапись.Записать();
КонецФункции
#КонецОбласти

#Область HTTPЗапросыИДанные
// Разбирает строку URI на составные части и возвращает в виде структуры.
// На основе RFC 3986.
//
// Параметры:
//  СтрокаURI - Строка - ссылка на ресурс в формате:
//                       <схема>://<логин>:<пароль>@<хост>:<порт>/<путь>?<параметры>#<якорь>.
//
// Возвращаемое значение:
//  Структура - составные части URI согласно формату:
//   * Схема         - Строка - схема из URI.
//   * Логин         - Строка - логин из URI.
//   * Пароль        - Строка - пароль из URI.
//   * ИмяСервера    - Строка - часть <хост>:<порт> из URI.
//   * Хост          - Строка - хост из URI.
//   * Порт          - Строка - порт из URI.
//   * ПутьНаСервере - Строка - часть <путь>?<параметры>#<якорь> из URI.
//
Функция СтруктураURI(Знач СтрокаURI) Экспорт
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// Строка соединения и путь на сервере.
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// Информация пользователя и имя сервера.
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@", НаправлениеПоиска.СКонца);
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
		Если Не ТолькоЦифрыВСтроке(Порт) Тогда
			Порт = "";
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(ПустаяСтрока(Порт), Неопределено, Число(Порт)));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции
//HTTP запрос
Функция ВыполнитьHTTPЗапрос(URL, МетодЗапроса = "GET", ТелоЗапроса = "", Заголовки = Неопределено, Авторизация = Неопределено,SSL = Неопределено,ОтветПолучитьКакСтроку = Ложь) Экспорт
	
	ОшибкаЗапроса = Неопределено;
	
	СтруктураURL = СтруктураURI(URL);
	
	Если SSL = Неопределено Тогда
		SSL = Новый ЗащищенноеСоединениеOpenSSL;	
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(СтруктураURL.Хост,СтруктураURL.Порт,,,,,SSL);
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURL.ПутьНаСервере);
	
	//Заголовки
	Если ЗначениеЗаполнено(Заголовки) И ТипЗнч(Заголовки) = Тип("Соответствие") тогда
		HTTPЗапрос.Заголовки = Заголовки;		
	КонецЕсли;
	//Обяз хост
	Если HTTPЗапрос.Заголовки.Получить("Host") = Неопределено Тогда
		HTTPЗапрос.Заголовки.Вставить("Host",СтруктураURL.Хост);	
	КонецЕсли;
	//Обяз ассерт
	Если HTTPЗапрос.Заголовки.Получить("Accept") = Неопределено Тогда
		HTTPЗапрос.Заголовки.Вставить("Accept","application/json");	
	КонецЕсли;
	
	//Авторизация
	Если ЗначениеЗаполнено(Авторизация) Тогда
		Если ТипЗнч(Авторизация) = тип("ТокенДоступа") Тогда
			HTTPЗапрос.ДобавитьТокенДоступа(Авторизация);			
		ИначеЕсли ТипЗнч(Авторизация) = тип("Строка") Тогда
			 HTTPЗапрос.Заголовки.Вставить("Authorization",Авторизация);
		КонецЕсли;
	КонецЕсли;
	
	//Тело запроса
	Если ЗначениеЗаполнено(ТелоЗапроса) И ТипЗнч(ТелоЗапроса) = Тип("Строка") Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса,,ИспользованиеByteOrderMark.НеИспользовать); 
		HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded")
	ИначеЕсли ЗначениеЗаполнено(ТелоЗапроса) И (ТипЗнч(ТелоЗапроса) = Тип("Структура") ИЛИ ТипЗнч(ТелоЗапроса) = Тип("Массив")) Тогда
		ЗаписьJSON = Новый ЗаписьJSON;			
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON,ТелоЗапроса,);
		ТелоЗапроса = ЗаписьJSON.Закрыть();
		//Для целых чисел с нулями после запятой ++
		ТелоЗапроса = СтрЗаменить(ТелоЗапроса,".123456789987654321",".00");
		//Для целых чисел с нулями после запятой --
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);
		HTTPЗапрос.Заголовки.Вставить("Content-Type","application/json");
	КонецЕсли;
	
	//Выполнение запроса 
	Попытка
		ОтветHTTP = HTTPСоединение.ВызватьHTTPМетод(МетодЗапроса, HTTPЗапрос); 
		
		КодСостояния = ОтветHTTP.КодСостояния;
		ЗаголовкиОтвета = ОтветHTTP.Заголовки;
		ТекстОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
		ДанныеСтруктура = Неопределено;
		
		Если ОтветПолучитьКакСтроку тогда
			Возврат ТекстОтвета;
		КонецЕсли;
		
		Попытка
			//Попытаемся прочитать JSON
			ЧтениеJson = Новый ЧтениеJSON;
			ЧтениеJson.УстановитьСтроку(ТекстОтвета);
			ДанныеСтруктура = ПрочитатьJSON(ЧтениеJson);
		Исключение
		КонецПопытки;
		
		//ЛогированиеЗапросов(СтруктураURL.Хост,МетодЗапроса,URL,HTTPЗапрос.Заголовки,ТелоЗапроса,КодСостояния,ЗаголовкиОтвета,ТекстОтвета);
		
		//ОшибкаЗапроса = ОбработкаОшибокЗапросов(ДанныеСтруктура,ТекстОтвета,ЗаголовкиОтвета,КодСостояния);
		
		Если ЗначениеЗаполнено(ДанныеСтруктура) Тогда
			Возврат ДанныеСтруктура;
		ИначеЕсли КодСостояния = 302 Тогда
			Возврат ЗаголовкиОтвета["location"]; 
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение
		Сообщить("Ошибка выполнения запроса!");
		ТекстОшибки = ОписаниеОшибки();
		Сообщить(ТекстОшибки);
		
		ВторойТекстОшибки = "";
		ВторойТекстОшибки = ВторойТекстОшибки + ?(ЗначениеЗаполнено(КодСостояния),"КодСостояния - " + Строка(КодСостояния) + Символы.ПС,"");
		ВторойТекстОшибки = ВторойТекстОшибки + ?(ЗначениеЗаполнено(ТекстОтвета),"ТекстОтвета - " + Строка(ТекстОтвета) + Символы.ПС,"");
		Если ЗначениеЗаполнено(ВторойТекстОшибки) Тогда
			Сообщить(ВторойТекстОшибки);
		КонецЕсли;
	КонецПопытки;
КонецФункции
//Получает данные из структуры если ключ существует
Функция ПолучитьЗначениеЕслиСуществуетИЗаполнено(Данные,Наименование) Экспорт
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если ТипЗнч(Данные) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если Данные.Свойство(Наименование) И ЗначениеЗаполнено(Данные[Наименование]) Тогда
		Возврат Данные[Наименование];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
//Получает данные из структуры по пути "ключ1.ключ2" если ключи сущестуют
Функция ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Данные,Путь) Экспорт
	МассивПути = СтрРазделить(Путь,".",Ложь);
	ТекущиеДанные = Данные;
	Для Каждого ТекущийКлюч из МассивПути Цикл
		ТекущиеДанные = ПолучитьЗначениеЕслиСуществуетИЗаполнено(ТекущиеДанные,ТекущийКлюч); 		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат ТекущиеДанные;
КонецФункции
//Возвращает Basic Base64(Логин:Пароль) 
Функция ПолучитьАвторизациюКакBASIC(Логин,Пароль)
	Возврат "Basic "+Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(Логин + ":" + Пароль));	
КонецФункции 
//Возвращает Bearer
Функция ПолучитьАвторизациюКакBearer(Токен)
	Возврат "Bearer "+Токен;	
КонецФункции
//добавляет к url структуру в виде ?param1=1&param2=2
Функция ДобавитьBodyURLEncoded(URL, Params)
	URL = URL + "?";
	Для Каждого Параметр из Params Цикл
		URL = URL + Параметр.Ключ + "=" + Параметр.Значение + "&";
	КонецЦикла;
	Возврат URL;
КонецФункции
Функция ВернутьBodyURLEncoded(Params)
	URL = "";
	Для Каждого Параметр из Params Цикл
		URL = URL + Параметр.Ключ + "=" + Параметр.Значение + "&";
	КонецЦикла;
	Возврат URL;
КонецФункции
//Добавляет сертификат к запросу
Функция ПолучитьСоединениеПоСертификату(ПутьКСертификату, Пароль)
	Если ЗначениеЗаполнено(Пароль) Тогда
		SSL = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаФайл(ПутьКСертификату, Пароль));
	Иначе
		SSL = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаФайл(ПутьКСертификату));
	КонецЕсли;
	Возврат SSL;
КонецФункции
//Конвертирует структуру в строку в формате JSON
Функция СтруктуруВJSONТекст(JSON)
	Попытка
		JSONобъект = новый ЗаписьJSON;
		JSONобъект.УстановитьСтроку();
		ЗаписатьJSON(JSONобъект,JSON);
		JSONТекстом = JSONобъект.Закрыть(); 
		Возврат JSONТекстом;
	Исключение
		JSONТекстом = "Не удалось получить JSON конкретного объекта";
		Возврат JSONТекстом;
	КонецПопытки;
КонецФункции
// Нормализует дату в формате 2024-12-03T21:00:00Z в дату 1с
Функция JSONДатаНормализовать(ДатаJSON)
	ДатаJSON = СтрЗаменить(ДатаJSON,"Z","");
	ДатаJSON = XMLЗначение(Тип("Дата"),ДатаJSON);
	Возврат ДатаJSON;	
КонецФункции
// Проверяет, содержит ли строка только цифры.
//
// Параметры:
//  Значение         - Строка - проверяемая строка.
//  Устаревший       - Булево - устаревший параметр, не используется.
//  ПробелыЗапрещены - Булево - если Ложь, то в строке допустимо наличие пробелов.
//
// Возвращаемое значение:
//   Булево - Истина - строка содержит только цифры или пустая, Ложь - строка содержит иные символы.
//
// Пример:
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("0123"); // Истина
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("0123abc"); // Ложь
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("01 2 3",, Ложь); // Истина
//
Функция ТолькоЦифрыВСтроке(Знач Значение, Знач Устаревший = Истина, Знач ПробелыЗапрещены = Истина) Экспорт

	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;

	Если Не ПробелыЗапрещены Тогда
		Значение = СтрЗаменить(Значение, " ", "");
	КонецЕсли;

	Если СтрДлина(Значение) = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Если содержит только цифры, то в результате замен должна быть получена пустая строка.
	// Проверять при помощи ПустаяСтрока нельзя, так как в исходной строке могут быть пробельные символы.
	Возврат СтрДлина(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( 
			Значение, "0", ""), "1", ""), "2", ""), "3", ""), "4", ""), "5", ""), "6", ""), "7", ""), "8", ""), "9",
		"")) = 0;

КонецФункции
#КонецОбласти   

#Область ПарамФункции
//Используется справочник БТ_ПрофилиПодключенияHTTP 
Функция ИспользоватьУчетныеДанные(СсылкаНаУчетныеДанные) Экспорт
	УчетнаяЗапись		= СсылкаНаУчетныеДанные.Ссылка;
	Хост 				= СсылкаНаУчетныеДанные.URL;
    Логин				= СсылкаНаУчетныеДанные.ИмяПользователя;
	Пароль				= СсылкаНаУчетныеДанные.ПарольПользователя;
	Токен				= СсылкаНаУчетныеДанные.Authorization;	
КонецФункции
Функция ОбработкаОшибокЗапросов(ДанныеСтруктура,ТекстОтвета,ЗаголовкиОтвета,КодСостояния,СообщатьЛи = Истина)
	//Если запрос неуспешен и ответ в JSON
	Если КодСостояния <> 200 И (ТипЗнч(ДанныеСтруктура) = Тип("Структура") И ДанныеСтруктура.Свойство("error")) тогда
		//ЕстьОшибка
		КодОшибки  = "";
		ОписаниеОшибки  = "";
		Если ДанныеСтруктура.Свойство("error") Тогда
			//Есть КодОшибки
			КодОшибки = ДанныеСтруктура["error"];
		КонецЕсли;
		Если ДанныеСтруктура.Свойство("error_description") Тогда
			//Есть ОписаниеОшибки
			ОписаниеОшибки = ДанныеСтруктура["error_description"];
		КонецЕсли;
	КонецЕсли;
	//Если это редирект то надо проверить есть ли в ссылке ошибка
	Если ЗначениеЗаполнено(ЗаголовкиОтвета.Получить("location")) Тогда
		//Запросим блоки регуляркой
		ВремКодОшибки = СтрНайтиПоРегулярномуВыражению(ЗаголовкиОтвета.Получить("location"),"error=([a-zA-Z0-9_]{1,1000})&").ПолучитьГруппы();
		Если ВремКодОшибки.Количество() > 0 Тогда КодОшибки = ВремКодОшибки[0].Значение; КонецЕсли;
		ВремОписаниеОшибки = СтрНайтиПоРегулярномуВыражению(ЗаголовкиОтвета.Получить("location"),"error_description=(.{1,1000})&").ПолучитьГруппы();
		Если ВремОписаниеОшибки.Количество() > 0 Тогда ОписаниеОшибки = ВремОписаниеОшибки[0].Значение; КонецЕсли;
		ОписаниеОшибки = РаскодироватьСтроку(ОписаниеОшибки, СпособКодированияСтроки.URLВКодировкеURL);
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки,"+"," ");
	КонецЕсли;
	
	//Если это ошибка бизнес логики
	Если КодСостояния <> 200 И (ТипЗнч(ДанныеСтруктура) = Тип("Структура") И ДанныеСтруктура.Свойство("errorCode")) Тогда
		//ЕстьОшибка
		КодОшибки  = "";
		ОписаниеОшибки  = "";
		Если ДанныеСтруктура.Свойство("errorCode") Тогда
			//Есть КодОшибки
			КодОшибки = ДанныеСтруктура["errorCode"];
		КонецЕсли;
		Если ДанныеСтруктура.Свойство("errorDesc") Тогда
			//Есть ОписаниеОшибки
			ОписаниеОшибки = ДанныеСтруктура["errorDesc"];
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодОшибки) ИЛИ ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ТекстСообщения = "Получен ответ с ошибкой." + ?(ЗначениеЗаполнено(КодОшибки),Символы.ПС+"Код ошибки: "+КодОшибки,"") + ?(ЗначениеЗаполнено(ОписаниеОшибки),Символы.ПС+"Описание ошибки: "+ОписаниеОшибки,"");
		Сообщить(ТекстСообщения);
		Возврат ТекстСообщения;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции
#КонецОбласти

#Область Настройки
Функция ЗаполнитьСтруктуруНастроек()
	СтруктураНастроек = Новый Структура;
	
	Для Каждого РеквизитОбработки из ЭтотОбъект.Метаданные().Реквизиты Цикл
		СтруктураНастроек.Вставить(РеквизитОбработки.Имя, ?(ЗначениеЗаполнено(ЭтотОбъект[РеквизитОбработки.Имя]),ЭтотОбъект[РеквизитОбработки.Имя],Неопределено)); 
	КонецЦикла;
	
	Возврат СтруктураНастроек;
КонецФункции
Процедура СохранитьНастройки() Экспорт
	
	СтруктураНастроек = ЗаполнитьСтруктуруНастроек();
	ИмяОбработки = ЭтотОбъект.Метаданные().ПолноеИмя();
	ХранилищеОбщихНастроек.Сохранить(ИмяОбработки+"/Общие",, СтруктураНастроек,,ИмяОбработки+"/Общие");
	
КонецПроцедуры
Процедура ВосстановитьНастройки() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().ПолноеИмя();
	СохраненныеНастройки = ХранилищеОбщихНастроек.Загрузить(ИмяОбработки+"/Общие",,,ИмяОбработки+"/Общие");
	Если СохраненныеНастройки = Неопределено ИЛИ ТипЗнч(СохраненныеНастройки) <> Тип("Структура") Тогда
		СохраненныеНастройки = новый Структура;
	КонецЕсли;
	
	Для Каждого ТекНастройка Из СохраненныеНастройки Цикл
		Если СохраненныеНастройки.Свойство(ТекНастройка.Ключ) Тогда
			СохраненнаяНастройка = СохраненныеНастройки[ТекНастройка.Ключ];
		КонецЕсли;
		ЭтотОбъект[ТекНастройка.Ключ] = ?(ЗначениеЗаполнено(СохраненнаяНастройка),СохраненнаяНастройка,Неопределено);
	КонецЦикла;
	
КонецПроцедуры
Функция ВсеЛиОбязательныеРеквизитыЗаполнены() Экспорт
	
	Для Каждого РеквизитОбработки из ЭтотОбъект.Метаданные().Реквизиты Цикл
		Если РеквизитОбработки.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку Тогда
			Если НЕ ЗначениеЗаполнено(ЭтотОбъект[РеквизитОбработки.Имя]) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
	
КонецФункции
#КонецОбласти

#Область Запросы
Функция ПолучитьТаблицуОстатков(КонтрагентыПоставщики,ПапкаНоменклатуры,СкладыКомпании) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпанииОстатки.Номенклатура.Артикул КАК НоменклатураАртикул,
		|	СУММА(ПартииТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	ПоступлениеТоваровТовары.Цена КАК Цена,
		|	НАЧАЛОПЕРИОДА(ПартииТоваровКомпанииОстатки.Партия.Дата, ДЕНЬ) КАК ПартияДата
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		|			,
		|			Номенклатура.Ссылка В ИЕРАРХИИ (&ПапкаНоменклатуры)
		|				И СкладКомпании В (&СкладыКомпании)
		|				И Партия ССЫЛКА Документ.ПоступлениеТоваров
		|				И Партия.Контрагент В (&КонтрагентыПоставщики)) КАК ПартииТоваровКомпанииОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|		ПО ПартииТоваровКомпанииОстатки.Партия = ПоступлениеТоваровТовары.Ссылка
		|			И ПартииТоваровКомпанииОстатки.Номенклатура = ПоступлениеТоваровТовары.Номенклатура
		|ГДЕ
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровКомпанииОстатки.Номенклатура,
		|	ПартииТоваровКомпанииОстатки.Номенклатура.Артикул,
		|	ПоступлениеТоваровТовары.Цена,
		|	НАЧАЛОПЕРИОДА(ПартииТоваровКомпанииОстатки.Партия.Дата, ДЕНЬ)";
	
	Запрос.УстановитьПараметр("КонтрагентыПоставщики", КонтрагентыПоставщики);
	Запрос.УстановитьПараметр("ПапкаНоменклатуры", ПапкаНоменклатуры);
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыКомпании);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;
	
КонецФункции
Функция ПолучитьТаблицуРеализаций(КонтрагентыПоставщики,ПапкаНоменклатуры,СкладыКомпании,КонтрагентыИсключенияДляРеализаций=Неопределено,Период) Экспорт
	
	КонтрагентыИсключения = Новый СписокЗначений;
	Если ЗначениеЗаполнено(КонтрагентыИсключенияДляРеализаций) и ТипЗнч(КонтрагентыИсключенияДляРеализаций) = Тип("СписокЗначений") Тогда
		КонтрагентыИсключения = КонтрагентыИсключенияДляРеализаций;
	ИначеЕсли ЗначениеЗаполнено(КонтрагентыИсключенияДляРеализаций) и ТипЗнч(КонтрагентыИсключенияДляРеализаций) = Тип("СправочникСсылка.Контрагенты") Тогда 
		КонтрагентыИсключения.Добавить(КонтрагентыИсключенияДляРеализаций);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ИзвлечениеТоваровИзПроизводства
		|			ТОГДА ПартииТоваровКомпании.Регистратор.ДокументОснование
		|		ИНАЧЕ ПартииТоваровКомпании.ДокументПродажи
		|	КОНЕЦ КАК ДокументПродажи,
		|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА ПартииТоваровКомпании.Количество
		|			ИНАЧЕ -ПартииТоваровКомпании.Количество
		|		КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА ПартииТоваровКомпании.Сумма
		|			ИНАЧЕ -ПартииТоваровКомпании.Сумма
		|		КОНЕЦ) КАК Сумма
		|ПОМЕСТИТЬ ВТ_РеализованныеПозиции
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|ГДЕ
		|	ПартииТоваровКомпании.Номенклатура.Ссылка В ИЕРАРХИИ(&ПапкаНоменклатуры)
		|	И ПартииТоваровКомпании.СкладКомпании В(&СкладыКомпании)
		|	И ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПоступлениеТоваров
		|	И ПартииТоваровКомпании.Партия.Контрагент В(&КонтрагентыПоставщики)
		|	И ПартииТоваровКомпании.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И (ВЫБОР
		|				КОГДА ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ИзвлечениеТоваровИзПроизводства
		|					ТОГДА ПартииТоваровКомпании.Регистратор.ДокументОснование
		|				ИНАЧЕ ПартииТоваровКомпании.ДокументПродажи
		|			КОНЕЦ ССЫЛКА Документ.ЗаказНаряд
		|			ИЛИ ПартииТоваровКомпании.ДокументПродажи ССЫЛКА Документ.РеализацияТоваров)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ИзвлечениеТоваровИзПроизводства
		|			ТОГДА ПартииТоваровКомпании.Регистратор.ДокументОснование
		|		ИНАЧЕ ПартииТоваровКомпании.ДокументПродажи
		|	КОНЕЦ,
		|	ПартииТоваровКомпании.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РеализованныеПозиции.Номенклатура КАК Номенклатура,
		|	ВТ_РеализованныеПозиции.Номенклатура.Артикул КАК НоменклатураАртикул,
		|	3 КАК КаналРеализации,
		|	ВТ_РеализованныеПозиции.ДокументПродажи.Номер КАК ДокументПродажиНомер,
		|	НАЧАЛОПЕРИОДА(ВТ_РеализованныеПозиции.ДокументПродажи.Дата, ДЕНЬ) КАК ДокументПродажиДатаЗакрытия,
		|	ВТ_РеализованныеПозиции.Количество КАК Количество,
		|	ОКР(ВТ_РеализованныеПозиции.Сумма / ВТ_РеализованныеПозиции.Количество, 2) КАК Цена,
		|	ВТ_РеализованныеПозиции.Сумма КАК Сумма,
		|	ВТ_РеализованныеПозиции.ДокументПродажи КАК ДокументПродажи,
		|	ВТ_РеализованныеПозиции.ДокументПродажи.Контрагент КАК ДокументПродажиКонтрагент
		|ИЗ
		|	ВТ_РеализованныеПозиции КАК ВТ_РеализованныеПозиции
		|ГДЕ
		|	ВТ_РеализованныеПозиции.Количество > 0
		|	И ВТ_РеализованныеПозиции.Сумма > 0
		|	И ВТ_РеализованныеПозиции.ДокументПродажи ССЫЛКА Документ.РеализацияТоваров
		|	И ВТ_РеализованныеПозиции.ДокументПродажи.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ВТ_РеализованныеПозиции.ДокументПродажи.Контрагент В (&КонтрагентыИсключения)";
	
	Запрос.УстановитьПараметр("ПапкаНоменклатуры", ПапкаНоменклатуры);
	Запрос.УстановитьПараметр("КонтрагентыИсключения", КонтрагентыИсключения);
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыКомпании);
	Запрос.УстановитьПараметр("КонтрагентыПоставщики", КонтрагентыПоставщики);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;
	
КонецФункции
#КонецОбласти
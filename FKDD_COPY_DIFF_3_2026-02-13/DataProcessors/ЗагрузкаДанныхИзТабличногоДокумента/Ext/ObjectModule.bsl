#Если Клиент Тогда
	
Перем ТЧДокумента Экспорт; // табличная часть документа
	
Перем Колонки; // Структура колонок загружаемых реквизитов, с описанием их свойств

Перем ПримитивныеТипы; // Структура примитивных типов (Булево, Дата, Строка, Число)

Перем МенеджерыОбъектовМетаданных; // Структура Менеджеров объектов метаданных

Перем КешМенеджеровПоТипу; // Соответствие, для кэширования информации о соответствии типа менеджеру

////////////////////////////////////////////////////////////////////////////////
// УНИВЕРСАЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Выводит сообщение об ошибке и выставляет параметр Отказ в "Истина". 
// В случае работы на клиенте или на сервере выводит в окно сообщений,
// в случае внешнего соединения вызывает исключение.
//
// Параметры:
//  ТекстСообщения - Строка	- Текст сообщения.
//  Отказ          - Булево	- Признак отказа (необязательный).
//
Процедура мСообщитьОбОшибке(ТекстСообщения, Отказ = Ложь, Заголовок = "") Экспорт

	НачалоСлужебногоСообщения    = Найти(ТекстСообщения, "{");
	ОкончаниеСлужебногоСообщения = Найти(ТекстСообщения, "}:");
	Если ОкончаниеСлужебногоСообщения > 0 И НачалоСлужебногоСообщения > 0 Тогда
		ТекстСообщения = Лев(ТекстСообщения, (НачалоСлужебногоСообщения - 1)) +
		                 Сред(ТекстСообщения, (ОкончаниеСлужебногоСообщения + 2));
	КонецЕсли;
	
	Отказ = Истина;
	
	#Если ВнешнееСоединение Тогда
		
		Если НЕ ЗначениеНеЗаполнено(Заголовок) Тогда
			ТекстСообщения = Заголовок + Символы.ПС + ТекстСообщения;
			Заголовок = "";
		КонецЕсли;
		
		ВызватьИсключение (ТекстСообщения);
		
	#Иначе
		
		Если НЕ обЗначениеНеЗаполнено(Заголовок) Тогда
			Сообщить(Заголовок);
			Заголовок = "";
		КонецЕсли;
		
		Сообщить(ТекстСообщения, СтатусСообщения.Важное);
		
	#КонецЕсли
	
КонецПроцедуры // СообщитьОбОшибке()

// Определяет заполнено ли переданное значение
//
// Параметры:
//  Значение - Неопределено	- Значение, заполнение которого надо проверить
//
// Возвращаемое значение:
// 	Булево 	- Истина - значение не заполнено, ложь - иначе.
//
Функция мЗначениеНеЗаполнено(Значение) Экспорт

	Результат = Ложь;
	ТипЗначения = ТипЗнч(Значение);

	// Сначала примитивные типы
	Если Значение = Неопределено Тогда
		Результат = Истина;
	ИначеЕсли Значение = NULL Тогда
		Результат = Истина;
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		Если СокрЛП(Значение) = "" Тогда
			Результат = Истина;
		КонецЕсли;
	ИначеЕсли ТипЗначения = Тип("Число") Тогда
		Если Значение = 0 Тогда
			Результат = Истина;
		КонецЕсли;
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Если Значение = Дата('00010101') Тогда
			Результат = Истина;
		КонецЕсли;
	ИначеЕсли ТипЗначения = Тип("Булево") Тогда
		Результат = Ложь; // Булево будем считать не пустым
		
	//Отдельное определение, так как конструкторов данного типа не существует
	ИначеЕсли ТипЗначения = Тип("РежимПроведенияДокумента") Тогда
		Если Значение = РежимПроведенияДокумента.Неоперативный ИЛИ Значение = РежимПроведенияДокумента.Оперативный Тогда
			Результат = Ложь;
		КонецЕсли;
		
	// Для остальных будем считать значение пустым, если оно равно
	// дефолтному значению своего типа
	Иначе
		Попытка
			Если Значение = Новый(ТипЗначения) Тогда
				Результат = Истина;
			КонецЕсли;
		Исключение
			Результат = Ложь;
		КонецПопытки;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции // ЗначениеНеЗаполнено()

// Функция "расщепляет" строку на подстроки, используя заданный
//		разделитель. Разделитель может иметь любую длину.
//		Если в качестве разделителя задан пробел, рядом стоящие пробелы
//		считаются одним разделителем, а ведущие и хвостовые пробелы параметра Стр
//		игнорируются.
//		Например,
//		РазложитьСтрокуВМассивПодстрок(",ку,,,му", ",") возвратит массив значений из пяти элементов,
//		три из которых - пустые строки, а
//		РазложитьСтрокуВМассивПодстрок(" ку   му", " ") возвратит массив значений из двух элементов
//
//	Параметры:
//		Стр - 			Строка	- Строка, которую необходимо разложить на подстроки.
//								Параметр передается по значению.
//		Разделитель - 	Строка	- Строка-разделитель, по умолчанию - запятая.
//
//	Возвращаемое значение:
//		Массив 	- Массив значений, элементы которого подстроки
//
Функция мРазложитьСтрокуВМассивПодстрок(Знач Стр, Разделитель = ",") Экспорт
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = СокрЛ(Сред(Стр,Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = Сред(Стр,Поз+ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции // глРазложить

// Функция интерактивно предлагает выбрать значение в зависимости от переданных параметров
// В форме, в списке выбора или выдает сообщение, что "Данная ячейка не может содержать значение"
//
// Параметры:
//  Значение 			- Неопределено	- Значение, которое необходимо выбрать
//  ОписаниеТипов 		- Строка		- Описание типов выбираемого значения
//  СвязьПоТипу   		- ПланВидовХарактеристикСсылка	- ПВХ связи значения по типу
//  ЭлементСвязиПоТипу 	- Число			- Номер элемента связи по типу
//
// Возвращаемое значение:
// Булево - Результат.
//
Функция мВыбратьЗначение(Значение, ОписаниеТипов, СвязьПоТипу, ЭлементСвязиПоТипу,СвязьПоВладельцу,ВыборГруппы = Ложь)
	
	Если СвязьПоТипу = Неопределено Тогда
		
		Типы = ОписаниеТипов;
		
	Иначе
		
		ВидыСубконто = СвязьПоТипу.ВидыСубконто;
		
		Если ЭлементСвязиПоТипу > ВидыСубконто.Количество() Тогда
			
			Предупреждение("Данная ячейка не может содержать значение");
			Возврат ложь;
		КонецЕсли;
		
		Типы = СвязьПоТипу.ВидыСубконто[ЭлементСвязиПоТипу-1].ВидСубконто.ТипЗначения;
		
	КонецЕсли;
	
	Если Типы.Типы().Количество() = 1 Тогда
		Тип = Типы.Типы()[0];
		Менеджер         = ПолучитьМенеджераПоТипу(Тип);
		Если Менеджер = Неопределено Тогда
			
			Возврат ВвестиЗначение(Значение,,Типы);
			
		Иначе
			
			Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
				
				Если ВыборГруппы  Тогда
					ФормаВыбора = Менеджер.ПолучитьФормуВыбораГруппы();
				Иначе
					ФормаВыбора = Менеджер.ПолучитьФормуВыбора();
				КонецЕсли;
				
				ФормаВыбора.ПараметрВыборПоВладельцу = СвязьПоВладельцу;
				ФормаВыбора.ПараметрОтборПоВладельцу = СвязьПоВладельцу;
				
			Иначе
				
				ФормаВыбора = Менеджер.ПолучитьФормуВыбора();
				
			КонецЕсли;
			
			ФормаВыбора.НачальноеЗначениеВыбора = Значение;
			Значение = ФормаВыбора.ОткрытьМодально();
			Возврат НЕ Значение = Неопределено;
		КонецЕсли;
	Иначе
		
		ФормаВыбораЗначения = ПолучитьФорму("ФормаВыбораЗначения");
		ФормаВыбораЗначения.ЭлементыФормы.Значение.ВыборПоВладельцу = СвязьПоВладельцу;
		ФормаВыбораЗначения.ЭлементыФормы.Значение.ТипЗначения = ОписаниеТипов;
		ФормаВыбораЗначения.ЭлементыФормы.Значение.ОграничениеТипа = Типы;
		ФормаВыбораЗначения.ЭлементыФормы.Значение.ЭлементСвязиПоТипу = ЭлементСвязиПоТипу;
		ФормаВыбораЗначения.ЭлементыФормы.Значение.Значение = Значение;
		Значение = ФормаВыбораЗначения.ОткрытьМодально();
		Возврат НЕ Значение = Неопределено;
	КонецЕсли;
	
	
КонецФункции // мВыбратьЗначение()

// Функция приводит строковое представление числа к его значению
//
// Параметры:
//  Представление - Строка			- Представление числа
//  ОписаниеТипов - ОписаниеТипов	- Допустимое описание типов численного значения
//
// Возвращаемое значение:
//  Число		- Значение типа число
//
Функция мПривестиКЧислу(Представление, Знач ОписаниеТипов = Неопределено, Примечание = "")
	
	Если ОписаниеТипов = Неопределено Тогда
		ОписаниеТипов = Новый ОписаниеТипов("Число");
	КонецЕсли;
	
	НРегПредставление = НРег(Представление);
	Если НРегПредставление = "да" ИЛИ НРегПредставление = "истина" ИЛИ НРегПредставление = "включено" Тогда
		Возврат 1;
	ИначеЕсли НРегПредставление = "нет" ИЛИ НРегПредставление = "ложь" ИЛИ НРегПредставление = "выключено" Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = СтрЗаменить(Представление, " ", "");
	Попытка
		Результат = Число(Результат);
	Исключение
		Примечание = "Неправильный формат числа";
		Возврат 0;
	КонецПопытки;
	
	Результат1 = ОписаниеТипов.ПривестиЗначение(Результат);
	
	Если НЕ Результат1 = Результат Тогда
		Примечание = "Недопустимое числовое значение";
	КонецЕсли;
	
	Возврат Результат1;
	
КонецФункции // мПривестиКЧислу()

// Функция возвращает части представления даты
//
// Параметры:
//  Представление - Строка	- Представление даты
//
// Возвращаемое значение:
//  Массив	- Массив частей даты
//
Функция ПолучитьЧастиПредставленияДаты(ЗНАЧ Представление)
	
	МассивЧастей = Новый Массив;
	НачалоЦифры = 0;
	Для к = 1 По СтрДлина(Представление) Цикл
		
		Символ = Сред(Представление, к ,1);
		ЭтоЦифра = Символ >= "0" И Символ <= "9";
		
		Если ЭтоЦифра Тогда
			
			Если НачалоЦифры = 0 Тогда
				НачалоЦифры = к;
			КонецЕсли;
			
		Иначе
			
			Если НЕ НачалоЦифры = 0 Тогда
				МассивЧастей.Добавить(Число(Сред(Представление,НачалоЦифры, к - НачалоЦифры)));
			КонецЕсли;
			
			НачалоЦифры = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ НачалоЦифры = 0 Тогда
		МассивЧастей.Добавить(Число(Сред(Представление,НачалоЦифры)));
	КонецЕсли;
	
	Возврат МассивЧастей;
КонецФункции //ПолучитьЧастиПредставленияДаты()

// Функция приводит строковое представление даты к его значению
//
// Параметры:
//  Представление - Строка			- Представление числа
//  ОписаниеТипов - ОписаниеТипов	- Допустимое описание типов значения типа дата
//
// Возвращаемое значение:
//  Дата 		- Значение типа дата
//
Функция мПривестиКДате(Представление, ТипРеквизита, Примечание = "")
	
	Результат = ТипРеквизита.ПривестиЗначение(Представление);
	Если Результат = '00010101' Тогда
		
		МассивЧастей = ПолучитьЧастиПредставленияДаты(Представление);
		Если ТипРеквизита.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время Тогда
			
			Попытка
				
				Если МассивЧастей.Количество() = 3 Тогда
					Результат = Дата(1,1,1, МассивЧастей[0],МассивЧастей[1],МассивЧастей[2]);
				ИначеЕсли МассивЧастей.Количество() = 6 Тогда
					Результат = Дата(1,1,1, МассивЧастей[3],МассивЧастей[4],МассивЧастей[5]);
				КонецЕсли;
				
			Исключение
				Примечание = "Неправильный формат даты";
			КонецПопытки;
			
		ИначеЕсли МассивЧастей.Количество() = 3 ИЛИ МассивЧастей.Количество() = 6 Тогда
			
			Если МассивЧастей[0] >= 1000 Тогда
				Временно = МассивЧастей[0];
				МассивЧастей[0] = МассивЧастей[2];
				МассивЧастей[2] = Временно;
			КонецЕсли;
			
			Если МассивЧастей[2] < 100 Тогда
				МассивЧастей[2] = МассивЧастей[2] + ?(МассивЧастей[2] < 30, 2000,1900);
			КонецЕсли;
			
			Попытка
				Если МассивЧастей.Количество() = 3 ИЛИ ТипРеквизита.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата Тогда
					Результат = Дата(МассивЧастей[2],МассивЧастей[1],МассивЧастей[0]);
				Иначе
					Результат = Дата(МассивЧастей[2],МассивЧастей[1],МассивЧастей[0],МассивЧастей[3],МассивЧастей[4],МассивЧастей[5]);
				КонецЕсли;
			Исключение
				Примечание = "Неправильный формат даты";
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции //мПривестиКДате()

// Функция возвращает менеджер по типу значения
//
// Параметры:
//  ТипЗначения - Неопределено	- Тип значения, по которому нужна вернуть менеджер
//
// Возвращаемое значение:
//  Строка		- Менеджер
//
Функция ПолучитьМенеджераПоТипу(ТипЗначения) Экспорт
	
	Если НЕ ТипЗначения = Неопределено Тогда
		Менеджер = КешМенеджеровПоТипу[ТипЗначения];
		Если Менеджер = Неопределено Тогда
			Для Каждого МенеджерОбъектаМетаданных Из МенеджерыОбъектовМетаданных Цикл
				Если МенеджерОбъектаМетаданных.Значение.ТипВсеСсылки().СодержитТип(ТипЗначения) Тогда
					Менеджер = МенеджерОбъектаМетаданных.Значение[Метаданные.НайтиПоТипу(ТипЗначения).Имя];
					Прервать;
				КонецЕсли;
			КонецЦикла;
			КешМенеджеровПоТипу.Вставить(ТипЗначения, Менеджер);
		КонецЕсли;
		Возврат Менеджер;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции //ПолучитьМенеджераПоТипу()

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция вычисляет значение ячейки для режима "Вычислять"
//
// Параметры:
//  Выражение 		- Строка	- Программный код, который необходимо выполнить
//  ТекущиеДанные  	- Структура	- Структура загруженных значений
//  ТекстЯчейки    	- Строка	- Текст текущей ячейки
//  ТекстыЯчеек    	- Массив	- Массив текстов ячеек строки
//  Результат      	- Число		- Результат вычисления
//
// Возвращаемое значение:
//  Структура		- Структура, содержащая Результат и ОписаниеОшибки
//
Функция ВычислитьЗначениеЯчейки(Знач Выражение,Знач ТекущиеДанные,Знач ТекстЯчейки, Знач ТекстыЯчеек,Знач Результат)
	
	ТекстЯчейки = СокрЛП(ТекстЯчейки);
	ОписаниеОшибки = "";
	Попытка
		Выполнить(Выражение);
	Исключение
		мСообщитьОбОшибке(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Новый Структура("Результат,ОписаниеОшибки",Результат,ОписаниеОшибки);
	
КонецФункции // ВычислитьЗначениеЯчейки()

// Функция записывает объект в информационную базу данных, используя
// события определенные пользователем в форме редактирования событий
//
// Параметры:
//  Объект      - ДокументСсылка	- Записываемый объект
//  ТекстыЯчеек - Массив			- Массив текстов ячеек, загружаемой строки
//
// Возвращаемое значение:
//  Булево		- Результат
//
Функция ЗаписатьОбъект(Объект, ТекстыЯчеек = Неопределено)
	
	Отказ = Ложь;
	НачатьТранзакцию();
	Попытка
		Объект.ОбменДанными.Загрузка=Истина;
	Исключение
	КонецПопытки; 
	Если НЕ ПустаяСтрока(ПередЗаписьюОбъекта) Тогда
		Попытка
			Выполнить(ПередЗаписьюОбъекта);
			Если Отказ Тогда
				ОписаниеОшибки = "";//Установлен отказ перед записью объекта
			КонецЕсли;
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	
	// Если это загрузка не в регистр сведений, то пытаемся установить код.
	Если (НЕ Отказ) И (РежимЗагрузки<>2) Тогда
		Попытка
			Код = Объект.Код;
			Попытка
				Если обЗначениеНеЗаполнено(Объект.Код) Тогда
					Объект.УстановитьНовыйКод("");
				КонецЕсли;
				Объект.Записать();
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = ОписаниеОшибки();
			КонецПопытки;
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	Если НЕ Отказ И НЕ ПустаяСтрока(ПриЗаписиОбъекта) Тогда  		
		Попытка
			Выполнить(ПриЗаписиОбъекта);
			Если Отказ Тогда
				ОписаниеОшибки = "";//Установлен отказ при записи объекта
			КонецЕсли;
			
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ Отказ  Тогда
		Попытка
			Объект.Записать();
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли; 	
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		мСообщитьОбОшибке(ОписаниеОшибки);
		ОтменитьТранзакцию();
	КонецЕсли;
	
	Попытка
		Объект.ОбменДанными.Загрузка=Ложь;
	Исключение
	КонецПопытки; 
	
	Возврат НЕ Отказ;
	
КонецФункции // ЗаписатьОбъект()

// Функция обрабатывает событие "После добавления строки",
// определенное пользователем в форме редактирования событий
//
// Параметры:
//  Объект      	- ТабличныйДокумент	- Записываемый объект
//  ТекущиеДанные  	- Структура			- Структура загруженных значений
//  ТекстыЯчеек    	- Массив			- Массив текстов ячеек строки
//
// Возвращаемое значение:
//  Булево			- Истина, если в событие "После добавления строки" не был установлен Отказ, Ложь - иначе
//
Функция ОбработатьСобытиеПослеДобавленияСтроки(Объект, ТекущиеДанные, ТекстыЯчеек)
	
	Попытка
		
		Выполнить(ПослеДобавленияСтроки);
		
	Исключение
		
		мСообщитьОбОшибке(ОписаниеОшибки());
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // ОбработатьСобытиеПослеДобавленияСтроки()

// Функция определяет, с помощью каких средств будет загружаться файл в формате XLS,XLSX
// доступна загрузка с использованием Microsoft Office либо Open Office
//
// Параметры:
//  ТабличныйДокумент  - ТабличныйДокумент	- ТабличныйДокумент, в который необходимо прочитать данные
//  ИмяФайла           - Строка				- Имя файла, из которого необходимо прочитать данные
//
// Возвращаемое значение:
//  Булево			- Истина, если файл прочитан, Ложь - иначе
//
Функция мПрочитатьТабличныйДокументXLS(ТабличныйДокумент, ИмяФайла, НомерЛистаExcel = 1) Экспорт
	
	MSOfficeДоступен = Ложь;
	Попытка
 		Excel = Новый COMОбъект("Excel.Application");
		MSOfficeДоступен = Истина;
	Исключение
		MSOfficeДоступен = Ложь;
	КонецПопытки;
	
	Если MSOfficeДоступен Тогда
		Возврат мПрочитатьТабличныйДокументИзExcel(ТабличныйДокумент, ИмяФайла, НомерЛистаExcel);
	КонецЕсли;
	
	OpenOfficeДоступен = Ложь;
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params = MakePropertyValue(ServiceManager,"Hidden",Истина);
		Args = Новый COMSafeArray("VT_DISPATCH", 1); Args.SetValue(0,Params);
		OpenOfficeДоступен = Истина;
	Исключение
		OpenOfficeДоступен = Ложь;	
	КонецПопытки;

	Если OpenOfficeДоступен Тогда
		ТекстВопроса = "На компьютере не установлен Microsoft Excel. Загрузить файл средствами Open Office?";
		ОтветНаВопрос = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Возврат мПрочитатьТабличныйДокументИзOpenOffice(ТабличныйДокумент, ИмяФайла);	
		КонецЕсли;
	КонецЕсли;
	
	Сообщить("Ошибка: для загрузки файла требуется установить Microsoft Office или Open Office.");
	Возврат Ложь;
КонецФункции

// Функция считывает в табличный документ данные из файла в формате Excel
//
// Параметры:
//  ТабличныйДокумент  - ТабличныйДокумент	- ТабличныйДокумент, в который необходимо прочитать данные
//  ИмяФайла           - Строка				- Имя файла в формате Excel, из которого необходимо прочитать данные
//  НомерЛистаExcel    - Число				- Номер листа книги Excel, из которого необходимо прочитать данные
//
// Возвращаемое значение:
//  Булево			- Истина, если файл прочитан, Ложь - иначе
//
Функция мПрочитатьТабличныйДокументИзExcel(ТабличныйДокумент, ИмяФайла, НомерЛистаExcel = 1) Экспорт
	
	xlLastCell = 11;
	
	ВыбФайл = Новый Файл(ИмяФайла);
	Если НЕ ВыбФайл.Существует() Тогда
		Сообщить("Файл не существует!");
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Excel.WorkBooks.Open(ИмяФайла);
		Состояние("Обработка файла Microsoft Excel...");
		ExcelЛист = Excel.Sheets(НомерЛистаExcel);
	Исключение
		Сообщить("Ошибка. Возможно неверно указан номер листа книги Excel.");
		Возврат ложь;
		
	КонецПопытки;
	
	ТабличныйДокумент.Очистить();
	
	ActiveCell = Excel.ActiveCell.SpecialCells(xlLastCell);
	RowCount = ActiveCell.Row;
	ColumnCount = ActiveCell.Column;
	Для Column = 1 По ColumnCount Цикл
		ТабличныйДокумент.Область("C" + Формат(Column, "ЧГ=")).ШиринаКолонки = ExcelЛист.Columns(Column).ColumnWidth;
	КонецЦикла;
	ТипЧисло = Тип("Число");
	Для Row = 1 По RowCount Цикл
		ОбработкаПрерыванияПользователя();
		Для Column = 1 По ColumnCount Цикл
			Значение = ExcelЛист.Cells(Row,Column).Value;
			Если ТипЗнч(Значение) = ТипЧисло Тогда
				Значение = Формат(Значение, "ЧГ=0");
			КонецЕсли;
			ТабличныйДокумент.Область("R" + Формат(Row, "ЧГ=") +"C" + Формат(Column, "ЧГ=")).Текст = Значение;
		КонецЦикла;
	КонецЦикла;
	
	Excel.WorkBooks.Close();
	Excel = 0;
	
	Возврат Истина;
	
КонецФункции // мПрочитатьТабличныйДокументИзExcel()

// "open office" заполняет массив свойств
Функция MakePropertyValue(ServiceManager,Name, Value)
	Struct = ServiceManager.Bridge_GetStruct("com.sun.star.beans.PropertyValue");
	Struct.Name = Name;
	Struct.Value = Value;
	Возврат Struct;
КонецФункции

// "open office" Функция преобразует Windows имя файла в URL OpenOffice
Функция ПреобразоватьВURL(ИмяФайла)
	Возврат "file:///" + СтрЗаменить(ИмяФайла, "\", "/");
КонецФункции

// Функция считывает в табличный документ данные из файла в формате ODS
//
// Параметры:
//  ТабличныйДокумент  - ТабличныйДокумент	- ТабличныйДокумент, в который необходимо прочитать данные
//
// Возвращаемое значение:
//  Булево			- Истина, если файл прочитан, Ложь - иначе
//
Функция мПрочитатьТабличныйДокументИзOpenOffice(ТабличныйДокумент, ИмяФайла) Экспорт
	
	ВыбФайл = Новый Файл(ИмяФайла);
	Если НЕ ВыбФайл.Существует() Тогда
		Сообщить("Ошибка: файл не существует!");
		Возврат Ложь;
	КонецЕсли;

	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params = MakePropertyValue(ServiceManager,"Hidden",Истина);
		Args = Новый COMSafeArray("VT_DISPATCH", 1); Args.SetValue(0,Params);
		OODoc = Desktop.loadComponentFromURL(ПреобразоватьВURL(ИмяФайла), "_blank", 0, Args);
		Sheets = OODoc.getSheets();
		Sheet = Sheets.getByIndex(0);
	Исключение
		Предупреждение("Ошибка: не установлен Open Office или установлена версия программы ниже версии 2.0",20);
		Возврат Ложь;
	КонецПопытки;

	Если ТипЗнч(Sheet)<>Тип("COMОбъект") Тогда
		Предупреждение("Ошибка: не удалось загрузить Open Office",20);
		Возврат Ложь;
	КонецЕсли;
	
	ТабличныйДокумент.Очистить();
	
	ЧислоСтрокЗаполненных = Sheet.Data().GetLength();
	
	ЧислоКолонок = 0;
	Для Каждого СтрокаТаблицыРеквизитов Из ТаблицаЗагружаемыхРеквизитов Цикл
		Если СтрокаТаблицыРеквизитов["НомерКолонки"] > ЧислоКолонок Тогда
			ЧислоКолонок = СтрокаТаблицыРеквизитов["НомерКолонки"];	
		КонецЕсли;
	КонецЦикла;
	
	Если ЧислоКолонок = 0 Тогда
		Предупреждение("Ошибка: не верно заданы колонки табличного документа",20);
		Возврат Ложь;
	КонецЕсли;
	
	Если ЧислоСтрокЗаполненных = 0 Тогда
		Предупреждение("Ошибка: не найдены данные для загрузки. Возможно данные располагаются не на первом листе. ",20);
		Возврат Ложь;
	КонецЕсли;	
	
	СчетчикСтрокОбщий = 0;
	СчетчикСтрокЗаполненных = 0;
	Пока (СчетчикСтрокЗаполненных < ЧислоСтрокЗаполненных) цикл
		ЕстьДанныеВСтроке = Ложь;
		Для СчетчикКолонок = 0 По ЧислоКолонок-1 Цикл
			Cell = Sheet.getCellByPosition(СчетчикКолонок, СчетчикСтрокОбщий);
			ТипЯчейки = Cell.getType();
			Если ТипЯчейки=1 Тогда
				ЗначениеПоля=Cell.getValue();
			ИначеЕсли ТипЯчейки=2 Тогда
				ЗначениеПоля=Cell.getString();
			ИначеЕсли ТипЯчейки=3 Тогда
				ЗначениеПоля=Cell.getFormula();
			Иначе
				ЗначениеПоля="";
			КонецЕсли;
			Если ТипЗнч(ЗначениеПоля)=Тип("Строка") Тогда
				Если КодоваяСтраница<>"cpCP1251" Тогда
					ЗначениеПоля=глРарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля,КодоваяСтраница,"cpCP1251");
				КонецЕсли; 
				//ЗначениеПоля=ВРег(ЗначениеПоля);	
			ИначеЕсли ТипЗнч(ЗначениеПоля)=Тип("Число") Тогда
				ЗначениеПоля=Формат(ЗначениеПоля,"ЧГ=0");
			КонецЕсли; 
			ТабличныйДокумент.Область("R" + Формат(СчетчикСтрокОбщий+1, "ЧГ=") +"C" + Формат(СчетчикКолонок+1, "ЧГ=")).Текст = ЗначениеПоля;		
			Если НЕ обЗначениеНеЗаполнено(ЗначениеПоля) Тогда
				ЕстьДанныеВСтроке = Истина;
			КонецЕсли;
		КонецЦикла;
		СчетчикСтрокОбщий = СчетчикСтрокОбщий+1;
		Если (ЕстьДанныеВСтроке И СчетчикСтрокЗаполненных = 0) ИЛИ СчетчикСтрокЗаполненных > 0 Тогда
			СчетчикСтрокЗаполненных = СчетчикСтрокЗаполненных + 1;
		КонецЕсли;	
    КонецЦикла;	
	
	ServiceManager=Неопределено;				
	
	Возврат Истина;
	
КонецФункции // мПрочитатьТабличныйДокументИзOpenOffice()

// Функция считывает в табличный документ данные из файла в формате TXT
//
// Параметры:
//  ТабличныйДокумент  - ТабличныйДокумент	- ТабличныйДокумент, в который необходимо прочитать данные
//  ИмяФайла           - Строка				- Имя файла в формате TXT, из которого необходимо прочитать данные
//
// Возвращаемое значение:
//  Булево			- Истина, если файл прочитан, Ложь - иначе
//
Функция мПрочитатьТабличныйДокументИзТекста(ТабличныйДокумент, ИмяФайла) Экспорт
	
	ВыбФайл = Новый Файл(ИмяФайла);
	Если НЕ ВыбФайл.Существует() Тогда
		Сообщить("Файл не существует!");
		Возврат Ложь;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Попытка
		ТекстовыйДокумент.Прочитать(ИмяФайла);
	Исключение
		Сообщить("Ошибка открытия файла!");
		Возврат Ложь;
	КонецПопытки;
	
	ТабличныйДокумент.Очистить();
	Для ТекущаяСтрока = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		ОбработкаПрерыванияПользователя();
		
		ТекущаяКолонка = 0;
		Для Каждого Значение Из мРазложитьСтрокуВМассивПодстрок(ТекстовыйДокумент.ПолучитьСтроку(ТекущаяСтрока),Символы.Таб) Цикл
			ТекущаяКолонка = ТекущаяКолонка + 1;
			ТабличныйДокумент.Область("R" + Формат(ТекущаяСтрока, "ЧГ=") +"C" + Формат(ТекущаяКолонка, "ЧГ=")).Текст = Значение;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции // мПрочитатьТабличныйДокументИзТекста()

// Функция считывает в табличный документ данные из файла в формате dBase III (*.dbf)
//
// Параметры:
//  ТабличныйДокумент  - ТабличныйДокумент	- ТабличныйДокумент, в который необходимо прочитать данные
//  ИмяФайла           - Строка				- Имя файла в формате TXT, из которого необходимо прочитать данные
//
// Возвращаемое значение:
//  Булево	-	Истина, если файл прочитан, Ложь - иначе
//
Функция мПрочитатьТабличныйДокументИзDBF(ТабличныйДокумент, ИмяФайла) Экспорт
	
	ВыбФайл = Новый Файл(ИмяФайла);
	Если НЕ ВыбФайл.Существует() Тогда
		Сообщить("Файл не существует!");
		Возврат Ложь;
	КонецЕсли;
	
	
	XBase  = Новый XBase;
	XBase.Кодировка = КодировкаXBase.OEM;
	Попытка
		XBase.ОткрытьФайл(ИмяФайла);
	Исключение
		Сообщить("Ошибка открытия файла!");
		Возврат Ложь;
	КонецПопытки;
	
	ТабличныйДокумент.Очистить();
	ТекущаяСтрока = 1;
	
	
	ТекущаяКолонка = 0;
	Для Каждого Поле Из XBase.поля Цикл
		ТекущаяКолонка = ТекущаяКолонка + 1;
		ТабличныйДокумент.Область("R" + Формат(ТекущаяСтрока, "ЧГ=") +"C" + Формат(ТекущаяКолонка, "ЧГ=")).Текст = Поле.Имя;
	КонецЦикла;
	
	
	Рез = XBase.Первая();
	Пока НЕ XBase.ВКонце() Цикл
		ОбработкаПрерыванияПользователя();
		
		ТекущаяСтрока = ТекущаяСтрока + 1;
		
		ТекущаяКолонка = 0;
		Для Каждого Поле Из XBase.поля Цикл
			ТекущаяКолонка = ТекущаяКолонка + 1;
			ТабличныйДокумент.Область("R" + Формат(ТекущаяСтрока, "ЧГ=") +"C" + Формат(ТекущаяКолонка, "ЧГ=")).Текст = XBase.ПолучитьЗначениеПоля(ТекущаяКолонка - 1);
		КонецЦикла;
		
		XBase.Следующая();
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции // мПрочитатьТабличныйДокументИзDBF()

////////////////////////////////////////////////////////////////////////////////
//

// Функция интерактивно предлагает выбрать значение в текущей ячейке табличного документа
//
// Параметры:
//  ТабличныйДокумент  - ТабличныйДокумент	- ТабличныйДокумент, в текущей ячейке которого необходимо выбрать значение
//
// Возвращаемое значение:
//  Булево		- Истина, если значение выбрано, Ложь - иначе
//
Процедура ВыбратьЗначениеВЯчейке(ТабличныйДокумент) Экспорт
	
	СформироватьСтруктуруКолонок();
	
	ОчиститьСообщения();
	ТекущаяОбласть = ТабличныйДокумент.ТекущаяОбласть;
	
	Если ТекущаяОбласть.Верх <> ТекущаяОбласть.Низ ИЛИ ТекущаяОбласть.Лево <> ТекущаяОбласть.Право Тогда
		Предупреждение("Для непосредственного выбора значения необходимо выбирать только одну ячейку");
		Возврат;
	КонецЕсли;
	
	ТекущаяКолонка = ПолучитьКолонку(ТекущаяОбласть);
	
	Если ТекущаяКолонка = Неопределено  Тогда
		
		Предупреждение("Значение данной колонки не выбирается");
		Возврат;
		
	КонецЕсли;
	
	ТекущаяСтрока = КонтрольЗаполненияСтроки(ТабличныйДокумент, ТекущаяОбласть.Верх);
	Значение = ТекущаяОбласть.Расшифровка;
	
	СвязьПоТипу = Неопределено;
	Если НЕ ПустаяСтрока(ТекущаяКолонка.СвязьПоТипу) Тогда
		Если ТипЗНЧ(ТекущаяКолонка.СвязьПоТипу) = Тип("Строка") Тогда
			ТекущаяСтрока.Свойство(ТекущаяКолонка.СвязьПоТипу,СвязьПоТипу);
		Иначе
			СвязьПоТипу = ТекущаяКолонка.СвязьПоТипу;
		КонецЕсли;
	КонецЕсли;
	
	СвязьПоВладельцу = Неопределено;
	Если НЕ ПустаяСтрока(ТекущаяКолонка.СвязьПоВладельцу) Тогда
		Если ТипЗНЧ(ТекущаяКолонка.СвязьПоВладельцу) = Тип("Строка") Тогда
			ТекущаяСтрока.Свойство(ТекущаяКолонка.СвязьПоВладельцу,СвязьПоВладельцу);
		Иначе
			СвязьПоВладельцу = ТекущаяКолонка.СвязьПоВладельцу;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементСвязиПоТипу = ?(ТекущаяКолонка.ЭлементСвязиПоТипу = 0,1,ТекущаяКолонка.ЭлементСвязиПоТипу);
	
	
	Если мВыбратьЗначение(Значение, ТекущаяКолонка.ОписаниеТипов,СвязьПоТипу, ЭлементСвязиПоТипу,СвязьПоВладельцу,ТекущаяКолонка.ИмяРеквизита = "Родитель") Тогда
		
		ТекущаяОбласть.Расшифровка = Значение;
		
		Если ПустаяСтрока(ТекущаяКолонка.ИскатьПо) Тогда
			ТекущаяОбласть.Текст = Строка(Значение);
		Иначе
			ТекущаяОбласть.Текст = Строка(Значение[ТекущаяКолонка.ИскатьПо]);
		КонецЕсли;
		ОчиститьСообщения();
		КонтрольЗаполненияСтроки(ТабличныйДокумент, ТекущаяОбласть.Верх);
	КонецЕсли;
	
КонецПроцедуры // ВыбратьЗначениеВЯчейке()

////////////////////////////////////////////////////////////////////////////////
//

// Функция возвращает метаданные источника данных
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   ОбъектМетаданныхКонфигурация	- Объект метаданных
//
Функция ПолучитьМетаданныеИсточника() Экспорт
	
	Если РежимЗагрузки = 0 Тогда
		Если НЕ СсылкаИсточника = Неопределено Тогда
			Возврат СсылкаИсточника.Метаданные();
		КонецЕсли; 
	ИначеЕсли РежимЗагрузки = 1 ИЛИ РежимЗагрузки = 3 Тогда
		
		ДляАРМ = Ложь;
		Попытка 
			ЕстьМетаданные = СсылкаИсточника.Метаданные(); 
			ДляАРМ = Ложь;
		Исключение
			ДляАРМ = Истина;	
		КонецПопытки;
		
		Если ДляАРМ Тогда
			Попытка
				Возврат Метаданные.Обработки.АРМКорзина.ТабличныеЧасти[ТабличнаяЧастьИсточника];
			Исключение
				Возврат Метаданные.Обработки.АРМКорзина.ТабличныеЧасти.Товары;
			КонецПопытки;
		Иначе
			Если НЕ СсылкаИсточника = Неопределено И НЕ ТабличнаяЧастьИсточника = Неопределено Тогда
				Возврат СсылкаИсточника.Метаданные().ТабличныеЧасти.Найти(ТабличнаяЧастьИсточника);
			КонецЕсли; 
		КонецЕсли;
		
	ИначеЕсли РежимЗагрузки = 2 Тогда
		Если НЕ ПустаяСтрока(ИмяВидаРегистра) Тогда
			Возврат Метаданные.РегистрыСведений[ИмяВидаРегистра];
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьМетаданныеИсточника()

// Функция возвращает значение структуры "Колонки" по области табличного документа,
// содержащее описание свойств колонки
//
// Параметры:
//  Область - Строка	 - Область табличного документа
//
// Возвращаемое значение:
//  Структура	- Значение структуры "Колонки"
//
Функция ПолучитьКолонку(Область)
	Для Каждого КлючИЗначение Из Колонки Цикл
		Колонка = КлючИЗначение.Значение;
		Если Область.Лево = Колонка.НомерКолонки Тогда
			Возврат Колонка;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции // ПолучитьКолонку(()

// Процедура формирует структуру колонок загружаемых реквизитов из табличной части "ТаблицаЗагружаемыхРеквизитов"
//
// Параметры:
//  нет
//
Процедура СформироватьСтруктуруКолонок() Экспорт
	
	НомерКолонки = 1;
	Колонки = Новый Структура;
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		Колонка = Новый Структура;
		Для Каждого КолонкаЗагружаемыхРеквизитов Из ТаблицаЗагружаемыхРеквизитов.Колонки Цикл
			Если НЕ РучнаяНумерацияКолонокТабличногоДокумента И КолонкаЗагружаемыхРеквизитов.Имя = "НомерКолонки" Тогда
				Если ЗагружаемыйРеквизит.Пометка Тогда
					Колонка.Вставить("НомерКолонки",НомерКолонки);
					НомерКолонки = НомерКолонки + 1;
				Иначе
					Колонка.Вставить("НомерКолонки",0);
				КонецЕсли;
			Иначе
				Колонка.Вставить(КолонкаЗагружаемыхРеквизитов.Имя,ЗагружаемыйРеквизит[КолонкаЗагружаемыхРеквизитов.Имя]);
			КонецЕсли;
			
		КонецЦикла;
		
		Колонки.Вставить(Колонка.ИмяРеквизита,Колонка);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьСтруктуруКолонок()

////////////////////////////////////////////////////////////////////////////////
//

// Процедура обновляет содержимое табличного документа, в соответствии с таблицей загружаемых реквизитов
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент	- ТабличныйДокумент, который необходимо обновить
//  БезВопросов       - Булево				- Если Ложь, спрашивать об очистке табличного документа,
//                                            если он не пустой, Истина - иначе
//
Процедура ОбновитьДанныеТабличногоДокумента(ТабличныйДокумент, БезВопросов = Ложь) Экспорт
	
	Если (РежимЗагрузки = 0 ИЛИ РежимЗагрузки = 2) И ТабличныйДокумент.ВысотаТаблицы > 1 И НЕ БезВопросов Тогда
		Результат = Вопрос("Табличный документ содержит данные. Очистить?", РежимДиалогаВопрос.ДаНетОтмена);
		Если  Результат = КодВозвратаДиалога.Да Тогда
			ТабличныйДокумент.Очистить();
		ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		Иначе
			Для к = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
				ТабличныйДокумент.Область("R1C"+Формат(К,"ЧГ=")).Текст = "";
			КонецЦикла;
		КонецЕсли;
	Иначе
		ТабличныйДокумент.Очистить();
	КонецЕсли;
	
	СформироватьСтруктуруКолонок();
	СформироватьШапкуТабличногоДокумента(ТабличныйДокумент);
	
	НомерСтроки = ПерваяСтрокаДанныхТабличногоДокумента;
	
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	Если РежимЗагрузки =0 ИЛИ РежимЗагрузки =2 ИЛИ МетаданныеИсточника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТЧДокумента=Неопределено Тогда
		Если обЗначениеНеЗаполнено(СсылкаИсточника) Тогда
			Возврат;					
		Иначе
			Источник = СсылкаИсточника[ТабличнаяЧастьИсточника];			
		КонецЕсли;
	Иначе
		Источник = ТЧДокумента;
	КонецЕсли; 
	
	Для Каждого Строка Из Источник Цикл
		
		НомерКолонки = 0;
		
		Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
			
			Если ЗагружаемыйРеквизит.Пометка Тогда
				
				Если РучнаяНумерацияКолонокТабличногоДокумента Тогда
					НомерКолонки = ЗагружаемыйРеквизит.НомерКолонки;
				Иначе
					НомерКолонки = НомерКолонки + 1;
				КонецЕсли;
				
				Область = ТабличныйДокумент.Область("R" + Формат(НомерСтроки, "ЧГ=") + "C"+НомерКолонки);
				Значение = Строка[ЗагружаемыйРеквизит.ИмяРеквизита];
				
				Попытка
					Представление = Значение[ЗагружаемыйРеквизит.ИскатьПо];
					
				Исключение
					
					Представление = Значение;
					
				КонецПопытки;
				
				Область.Текст = Представление;
				Область.Расшифровка = Значение;
				
			КонецЕсли;
			
		КонецЦикла;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры // ОбновитьДанныеТабличногоДокумента(()

// Процедура формирует шапку табличного документа, в соответствии с таблицей загружаемых реквизитов
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент	- ТабличныйДокумент, у которого необходимо сформировать шапку
//
Процедура СформироватьШапкуТабличногоДокумента(ТабличныйДокумент) Экспорт
	
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
	
	Таблица = ТаблицаЗагружаемыхРеквизитов.Скопировать();
	Таблица.Сортировать("НомерКолонки");
	Для Каждого КлючИЗначение Из Колонки Цикл
		ЗагружаемыйРеквизит = КлючИЗначение.Значение;
		НомерКолонки = ЗагружаемыйРеквизит.НомерКолонки;
		Если НЕ ЗагружаемыйРеквизит.Пометка ИЛИ НомерКолонки = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗагружаемыйРеквизит.ШиринаКолонки = 0 Тогда
			
			ШиринаКолонки = 40;
			Если ЗагружаемыйРеквизит.ОписаниеТипов.Типы().Количество() = 1 Тогда
				ПервыйТип = ЗагружаемыйРеквизит.ОписаниеТипов.Типы()[0];
				Если ПервыйТип = Тип("Строка") Тогда
					Если ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыСтроки.Длина = 0 Тогда
						ШиринаКолонки = 80;
					Иначе
						ШиринаКолонки = Мин(Макс(ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыСтроки.Длина,10),80);
					КонецЕсли;
				ИначеЕсли ПервыйТип = Тип("Число") Тогда
					ШиринаКолонки = Макс(ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыЧисла.Разрядность,10);
				ИначеЕсли ПервыйТип = Тип("Булево") Тогда
					ШиринаКолонки = 10;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ШиринаКолонки = ЗагружаемыйРеквизит.ШиринаКолонки;
		КонецЕсли;
		
		
		Область = ТабличныйДокумент.Область("R1C"+НомерКолонки);
		БылТекст = НЕ ПустаяСтрока(Область.Текст);
		Область.Текст       = ?(БылТекст,Область.Текст + Символы.ПС,"") + ЗагружаемыйРеквизит.ПредставлениеРеквизита;
		Область.Расшифровка = ЗагружаемыйРеквизит.ИмяРеквизита;
		Область.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
		Область.Обвести(Линия, Линия, Линия, Линия);
		
		ОбластьКолонки = ТабличныйДокумент.Область("C"+НомерКолонки);
		ОбластьКолонки.ШиринаКолонки = ?(БылТекст,Макс(ОбластьКолонки.ШиринаКолонки,ШиринаКолонки),ШиринаКолонки);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьШапкуТабличногоДокумента()

// Функция выполняет загрузку данных из табличного документа в справочник или табличную часть документа
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент	- ТабличныйДокумент, у которого необходимо сформировать шапку
//  Индикатор         - Индикатор			- Элемент управления индикатор, в котором необходимо отображать
//                                            процент выполнения загрузки
//
// Возвращаемое значение:
//  Булево	- Истина, если загрузка прошла без ошибок, Ложь - иначе
//
Функция ЗагрузитьДанные(ТабличныйДокумент, Индикатор) Экспорт
	
	ЗаписыватьОбъект = истина;
	
	СформироватьСтруктуруКолонок();
	
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	
	Если ТаблицаЗагружаемыхРеквизитов.НайтиСтроки(Новый Структура("Пометка",Истина)).Количество() = 0 Тогда
		Предупреждение("Не отмечен ни один загружаемый реквизит!");
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоЭлементов = ТабличныйДокумент.ВысотаТаблицы - ПерваяСтрокаДанныхТабличногоДокумента + 1;
	Если КоличествоЭлементов <= 0 Тогда
		Предупреждение("Нет данных для загрузки");
		Возврат Ложь;
	КонецЕсли;
	
	Если РежимЗагрузки = 0 Тогда
		СтрПоиска = ТаблицаЗагружаемыхРеквизитов.НайтиСтроки(Новый Структура("ПолеПоиска,Пометка",Истина,Истина)).Количество();
		Если СтрПоиска = 0 И Вопрос("Не выбрано ни одно поле поиска. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			//Если Вопрос("Не выбрано ни одно поле поиска. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
				Возврат Ложь;
			//КонецЕсли;
			
		КонецЕсли;
		
		Источник = СсылкаИсточника;
		ТекстВопросаИсточника = " элементов в справочник: """ + МетаданныеИсточника.Представление() + """";
		
	ИначеЕсли РежимЗагрузки = 1 Тогда
		
		Если СсылкаИсточника=Неопределено ИЛИ СсылкаИсточника.Пустая() Тогда
			Предупреждение("Не выбрана ссылка!");
			Возврат Ложь;
		КонецЕсли;
		 
		ОбъектИсточника = СсылкаИсточника.ПолучитьОбъект();
		Источник = ОбъектИсточника[ТабличнаяЧастьИсточника];
		ТекстВопросаИсточника = " строк в табличную часть: """ + МетаданныеИсточника.Представление() + """";
		
	ИначеЕсли РежимЗагрузки = 2 Тогда
		
		ТекстВопросаИсточника = " записей в регистр сведений: """ + МетаданныеИсточника.Представление() + """";
	ИначеЕсли РежимЗагрузки = 3 Тогда
		
		ТекстВопросаИсточника = " строк в табличную часть: """ + МетаданныеИсточника.Представление() + """";
		
	КонецЕсли;
	
	Если Вопрос("Загрузить "+КоличествоЭлементов  + ТекстВопросаИсточника + " ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		Запрос = Неопределено;
		Если РежимЗагрузки = 0 Тогда
			
			СтрокиПоиска = ТаблицаЗагружаемыхРеквизитов.НайтиСтроки(Новый Структура("ПолеПоиска,Пометка",Истина,Истина));
			Если НЕ СтрокиПоиска.Количество() = 0 Тогда
				
				ТекстЗапроса =
				"Выбрать Первые 1
				|Справочник.Ссылка КАК Ссылка
				|Из Справочник."+МетаданныеИсточника.Имя+" КАК Справочник
				|Где";
				
				Для Каждого СтрокаПоиска Из СтрокиПоиска Цикл
					ТекстЗапроса = ТекстЗапроса +"
					|Справочник."+СтрокаПоиска.ИмяРеквизита+" = &" + СтрокаПоиска.ИмяРеквизита + "
					|И";
					
				КонецЦикла;
				
				ТекстЗапроса = Лев(ТекстЗапроса,СтрДлина(ТекстЗапроса) - 2);
				Запрос = Новый Запрос (ТекстЗапроса);
			КонецЕсли;
		ИначеЕсли РежимЗагрузки = 1 И ПроверитьТЧ (СсылкаИсточника, МетаданныеИсточника) Тогда
			Источник.Очистить();
		ИначеЕсли РежимЗагрузки = 2 Тогда
			
			ИзмеренияРегистра = Новый Структура;
			Для Каждого Колонка Из Колонки Цикл
				Если Колонка.Значение.МожетБытьПолемПоиска Тогда
					ИзмеренияРегистра.Вставить(Колонка.Ключ,Колонка.Значение);
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли РежимЗагрузки = 3 Тогда
			
			Если ТЧДокумента.Количество() > 0 Тогда
			Ответ = Вопрос("Очистить табличную часть перед загрузкой?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Очистка таблицы");
			Если Ответ = КодВозвратаДиалога.Да Тогда
				ТЧДокумента.Очистить();
				ТабличнаяЧастьДокументаОбъекта.Очистить();
			Иначе
				ТЧДокумента.Очистить();
			КонецЕсли;
		КонецЕсли;
			
		КонецЕсли;
		 
		ОчиститьСообщения();
		Сообщить("Выполняется загрузка"+ ТекстВопросаИсточника, СтатусСообщения.Информация);
		Сообщить("Всего: " + КоличествоЭлементов, СтатусСообщения.Информация);
		Сообщить("---------------------------------------------", СтатусСообщения.БезСтатуса);
		Индикатор.Значение = 0;
		Индикатор.МаксимальноеЗначение = КоличествоЭлементов;
		Загружено = 0;
		Для К = ПерваяСтрокаДанныхТабличногоДокумента По ТабличныйДокумент.ВысотаТаблицы Цикл
			НомерТекущейСтроки = Индикатор.Значение + 1;
			ТекстыЯчеек = Неопределено;
			Отказ = Ложь;
			ТекущаяСтрока = КонтрольЗаполненияСтроки(ТабличныйДокумент, К, ТекстыЯчеек);
			Если РежимЗагрузки =0 Тогда
				
				Объект = Неопределено;
				Если НЕ Запрос = Неопределено Тогда
					СтрокаОшибок = "";
					Для Каждого СтрокаПоиска Из СтрокиПоиска Цикл
						
						ЗначениеРеквизита = Неопределено;
						
						ТекущаяСтрока.Свойство(СтрокаПоиска.ИмяРеквизита,ЗначениеРеквизита);
						Если ПустаяСтрока(ЗначениеРеквизита) Тогда
							СтрокаОшибок = ?(ПустаяСтрока(СтрокаОшибок),"",СтрокаОшибок + ", ") + СтрокаПоиска.ПредставлениеРеквизита;
						Иначе
							Запрос.УстановитьПараметр(СтрокаПоиска.ИмяРеквизита,ТекущаяСтрока[СтрокаПоиска.ИмяРеквизита]);
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ ПустаяСтрока(СтрокаОшибок) Тогда
						Сообщить("Строка " + НомерТекущейСтроки + " не может быть записана.Не указано значение ключевых реквизитов: " + СтрокаОшибок,СтатусСообщения.Важное);
						Индикатор.Значение = Индикатор.Значение + 1;
						Продолжить;
					КонецЕсли;
					
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Объект = Выборка.Ссылка.ПолучитьОбъект();
					КонецЕсли;
					 
				КонецЕсли;
				
				Попытка
					ТекущаяСтрокаЭтоГруппа=ТекущаяСтрока.ЭтоГруппа;
				Исключение
					ТекущаяСтрокаЭтоГруппа=Ложь;
				КонецПопытки; 
				ОбъектНайден = НЕ Объект = Неопределено;
				Если НЕ ОбъектНайден Тогда
					Если ТекущаяСтрокаЭтоГруппа Тогда
						Объект = Справочники[МетаданныеИсточника.Имя].СоздатьГруппу();
					Иначе
						Объект = Справочники[МетаданныеИсточника.Имя].СоздатьЭлемент();
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли РежимЗагрузки = 1 Тогда
				Если ПроверитьТЧ (СсылкаИсточника, МетаданныеИсточника) Тогда
					Объект = Источник.Добавить();
				КонецЕсли;
				ОбъектНайден = Ложь;
			ИначеЕсли РежимЗагрузки = 2 Тогда
				Объект = РегистрыСведений[МетаданныеИсточника.Имя].СоздатьМенеджерЗаписи();
				Для Каждого КлючИЗначение Из ТекущаяСтрока Цикл
					
					Если ИзмеренияРегистра.Свойство(КлючИЗначение.Ключ) Тогда
						Объект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
					КонецЕсли;
					
				КонецЦикла;
				
				Если НЕ ЗамещатьСуществующиеЗаписи Тогда
					Объект.Прочитать();
					ОбъектНайден = Объект.Выбран();
				Иначе
					ОбъектНайден = Ложь;
				КонецЕсли;
			ИначеЕсли РежимЗагрузки = 3 Тогда
				Объект = ТЧДокумента.Добавить();
				ОбъектНайден = Ложь;
			КонецЕсли;
			
			Для Каждого КлючИЗначение Из ТекущаяСтрока Цикл
					
				Если НЕ ОбъектНайден ИЛИ Колонки[КлючИЗначение.Ключ].Пометка  Тогда
					Попытка
						Если РежимЗагрузки=0 Тогда
							Если КлючИЗначение.Ключ<>"ЭтоГруппа" Тогда
								Если МетаданныеИсточника.Реквизиты.Найти(КлючИЗначение.Ключ)<>Неопределено Тогда
									Попытка
										ИспользованиеРеквизита=МетаданныеИсточника.Реквизиты[КлючИЗначение.Ключ].Использование;
									Исключение
										ИспользованиеРеквизита=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента;
									КонецПопытки; 
								Иначе
									ИспользованиеРеквизита=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента;
								КонецЕсли;
								Если ТекущаяСтрокаЭтоГруппа Тогда
									Если ИспользованиеРеквизита=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента ИЛИ
										 ИспользованиеРеквизита=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы Тогда
										 Объект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
									КонецЕсли; 
								Иначе
									Если ИспользованиеРеквизита=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента ИЛИ
										 ИспользованиеРеквизита=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента Тогда
										 Объект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
									КонецЕсли; 
								КонецЕсли; 
							КонецЕсли; 
						Иначе
							Объект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
						КонецЕсли; 
					Исключение
						мСообщитьОбОшибке("Ошибка при установке значения реквизита """ + КлючИЗначение.Ключ + "" + ОписаниеОшибки());
						Отказ = Истина;
						Прервать;
					КонецПопытки;
				КонецЕсли;
					
			КонецЦикла;
			
			Если РежимЗагрузки = 0 Тогда
				Если НеСоздаватьНовыхЭлементов = "Только старые" И ОбъектНайден Тогда
					Если НЕ Отказ И ЗаписатьОбъект(Объект, ТекстыЯчеек) Тогда
						Сообщить(?(ОбъектНайден,"Изменен","Загружен") + " элемент справочника: " + Объект.ссылка, СтатусСообщения.Информация);
						Загружено = Загружено + 1;
					Иначе
						Сообщить("Объект не " + ?(ОбъектНайден,"изменен","загружен") + ". Элемент справочника: " + Объект + ".", СтатусСообщения.Важное);
					КонецЕсли;
				ИначеЕсли НеСоздаватьНовыхЭлементов = "Только новые" И Не ОбъектНайден Тогда
					Если НЕ Отказ И ЗаписатьОбъект(Объект, ТекстыЯчеек) Тогда
						Сообщить(?(ОбъектНайден,"Изменен","Загружен") + " элемент справочника: " + Объект.ссылка, СтатусСообщения.Информация);
						Загружено = Загружено + 1;
					Иначе
						Сообщить("Объект не " + ?(ОбъектНайден,"изменен","загружен") + ". Элемент справочника: " + Объект + ".", СтатусСообщения.Важное);
					КонецЕсли;
				ИначеЕсли НеСоздаватьНовыхЭлементов = "Все" Тогда
					Если НЕ Отказ И ЗаписатьОбъект(Объект, ТекстыЯчеек) Тогда
						Сообщить(?(ОбъектНайден,"Изменен","Загружен") + " элемент справочника: " + Объект.ссылка, СтатусСообщения.Информация);
						Загружено = Загружено + 1;
					Иначе
						Сообщить("Объект не " + ?(ОбъектНайден,"изменен","загружен") + ". Элемент справочника: " + Объект + ".", СтатусСообщения.Важное);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли РежимЗагрузки = 1 Тогда
				
				Если НЕ ОбработатьСобытиеПослеДобавленияСтроки(ОбъектИсточника, Объект, ТекстыЯчеек) Тогда
					Отказ = Истина;
				КонецЕсли;
				
				Если НЕ Отказ Тогда
					Сообщить("Добавлена строка: " + (Загружено + 1));
				Иначе
					Сообщить("При добавлении строки " + (Загружено + 1) + " возникли ошибки. ");
					ЗаписыватьОбъект = Ложь;
				КонецЕсли;
				
				Загружено = Загружено + 1;
				
			ИначеЕсли РежимЗагрузки = 2 Тогда
				Если НЕ Отказ И ЗаписатьОбъект(Объект, ТекстыЯчеек) Тогда
					Сообщить(?(ОбъектНайден,"Изменена","Добавлена") + " запись № " + НомерТекущейСтроки + ".");
					Загружено = Загружено + 1;
				Иначе
					Сообщить("Запись не " + ?(ОбъектНайден,"изменена","загружена") + ". № записи: " + НомерТекущейСтроки + ".", СтатусСообщения.Важное);
				КонецЕсли;
			ИначеЕсли РежимЗагрузки = 3 Тогда
				
				Если НЕ ОбработатьСобытиеПослеДобавленияСтроки(СсылкаИсточника, Объект, ТекстыЯчеек) Тогда
					Отказ = Истина;
				КонецЕсли;
				
				Если НЕ Отказ Тогда
					Сообщить("Добавлена строка: " + (Загружено + 1));
				Иначе
					Сообщить("При добавлении строки " + (Загружено + 1) + " возникли ошибки. ");
					ЗаписыватьОбъект = Ложь;
				КонецЕсли;
				
				Загружено = Загружено + 1;
			КонецЕсли;
			
			Индикатор.Значение = Индикатор.Значение + 1;
			ОбработкаПрерыванияПользователя();
			
		КонецЦикла;
		Сообщить("---------------------------------------------", СтатусСообщения.БезСтатуса);
		
		Если РежимЗагрузки = 1 Тогда
			Если ЗаписыватьОбъект И ЗаписатьОбъект(ОбъектИсточника) Тогда
				
				Сообщить("Выполнена загрузка"+ 	ТекстВопросаИсточника, СтатусСообщения.Информация);
				Сообщить("" +Загружено +" из "+ КоличествоЭлементов + " элементов.", СтатусСообщения.Информация);
				Возврат Истина;
			Иначе
				Сообщить("Объект не записан: " + Объект + ".", СтатусСообщения.Важное);
				Возврат Ложь;
			КонецЕсли;
		ИначеЕсли РежимЗагрузки = 0 Тогда
			Сообщить("Выполнена загрузка"+ 	ТекстВопросаИсточника, СтатусСообщения.Информация);
			Сообщить("" +Загружено +" из "+ КоличествоЭлементов + " элементов.", СтатусСообщения.Информация);
			Возврат Истина;
		ИначеЕсли РежимЗагрузки = 2 Тогда
			Сообщить("Выполнена загрузка"+ 	ТекстВопросаИсточника, СтатусСообщения.Информация);
			Сообщить("" +Загружено +" из "+ КоличествоЭлементов + " записей.", СтатусСообщения.Информация);
			Возврат Истина;
		ИначеЕсли РежимЗагрузки = 3 Тогда
			Сообщить("Выполнена загрузка"+ 	ТекстВопросаИсточника, СтатусСообщения.Информация);
			Сообщить("" +Загружено +" из "+ КоличествоЭлементов + " элементов.", СтатусСообщения.Информация);
			Возврат Истина;
		КонецЕсли;
		
	Иначе
		Если РежимЗагрузки = 3 Тогда
			ТЧДокумента.Очистить();
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // ЗагрузитьДанные()

// Проверка наличия табличной части у элемента справочника
Функция ПроверитьТЧ (СсылкаИсточника, МетаданныеИсточника)
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(СсылкаИсточника));
	
	Если (Метаданные.Справочники.Содержит(ОбъектМетаданных) И ОбъектМетаданных.Иерархический) ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектМетаданных) Тогда
		
		// Для иерархических объектов определим в зависимости от вида переданного объекта
		Если МетаданныеИсточника.Использование=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента Тогда
			Возврат ИСТИНА;
			
		ИначеЕсли МетаданныеИсточника.Использование=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента Тогда
			Возврат НЕ СсылкаИсточника.ЭтоГруппа;
			
		ИначеЕсли МетаданныеИсточника.Использование=Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы Тогда
			Возврат СсылкаИсточника.ЭтоГруппа;
		КонецЕсли;
		
	Иначе
		Возврат ИСТИНА;
	КонецЕсли;

КонецФункции // ПроверитьТЧ() 

////////////////////////////////////////////////////////////////////////////////
//

// Процедура выполняет контроль заполнения данных табличного документа
// сообщает об ошибках и устанавливает комментарии к ошибочным ячейкам
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент	- ТабличныйДокумент, у которого необходимо сформировать шапку
//  Индикатор         - Индикатор			- Элемент управления индикатор, в котором необходимо отображать
//                                            процент выполнения операции
//
Процедура КонтрольЗаполнения(ТабличныйДокумент, Индикатор) Экспорт
	
	КоличествоЭлементов = ТабличныйДокумент.ВысотаТаблицы - ПерваяСтрокаДанныхТабличногоДокумента + 1;
	
	Индикатор.Значение = 0;
	
	ОчиститьСообщения();
	Индикатор.МаксимальноеЗначение = КоличествоЭлементов;
	КоличествоОшибок = 0;
	Для К = 0  По КоличествоЭлементов - 1 Цикл
		ОбработкаПрерыванияПользователя();
		Состояние("Выполняется контроль заполнения строки № " + (К + 1));
		КонтрольЗаполненияСтроки(ТабличныйДокумент, К + ПерваяСтрокаДанныхТабличногоДокумента,,КоличествоОшибок);
		Индикатор.Значение = К + 1;
	КонецЦикла;
	
	Сообщить("Контроль заполнения завершен. Проверено строк: " + КоличествоЭлементов);
	Если КоличествоОшибок Тогда
		Сообщить("Выявлено ячеек, содержащих ошибки/неоднозначное представление: " + КоличествоОшибок);
	Иначе
		Сообщить("Ячеек, содержащих ошибки не выявлено");
	КонецЕсли;
	
КонецПроцедуры // КонтрольЗаполнения()

// Функция выполняет контроль заполнения строки данных табличного документа
// сообщает об ошибках и устанавливает комментарии к ошибочным ячейкам
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент	- ТабличныйДокумент, у которого необходимо сформировать шапку
//  НомерСтроки       - Число				- Число, номер строки табличного документа
//  ТекстыЯчеек    	  - Массив				- Возвращает массив текстов ячеек строки,
//
// Возвращаемое значение:
//  Структура	- Структура, ключ - Имя загружаемого реквизита, Значение - Значение загружаемого реквизита
//
Функция КонтрольЗаполненияСтроки(ТабличныйДокумент, НомерСтроки, ТекстыЯчеек = Неопределено, КоличествоОшибок = 0)
	
	ТекстыЯчеек = Новый Массив;
	ТекстыЯчеек.Добавить(Неопределено);
	Для к = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
		ТекстыЯчеек.Добавить(СокрЛП(ТабличныйДокумент.Область("R"+Формат(НомерСтроки,"ЧГ=")+"C"+Формат(К,"ЧГ=")).Текст));
	КонецЦикла;
	
	ТекущаяСтрока     = Новый Структура;
	Для Каждого КлючИЗначение Из Колонки Цикл
		
		Колонка = КлючИЗначение.Значение;
		
		Если Колонка.Пометка Тогда
			
			Если Колонка.РежимЗагрузки = "Устанавливать" Тогда
				
				Результат = Колонка.ЗначениеПоУмолчанию;
				ТекущаяСтрока.Вставить(Колонка.ИмяРеквизита,Результат);
				
			ИначеЕсли НЕ Колонка.НомерКолонки = 0 Тогда
				
				Если НЕ ОбработатьОбласть(ТабличныйДокумент.Область("R"+Формат(НомерСтроки,"ЧГ=")+"C"+Формат(Колонка.НомерКолонки,"ЧГ=")), Колонка, ТекущаяСтрока, ТекстыЯчеек) Тогда
					КоличествоОшибок = КоличествоОшибок + 1;
				КонецЕсли;
				
			ИначеЕсли Колонка.РежимЗагрузки = "Вычислять" Тогда
				
				Вычисление  = ВычислитьЗначениеЯчейки(Колонка.Выражение,ТекущаяСтрока,"",ТекстыЯчеек,Колонка.ЗначениеПоУмолчанию);
				Результат   = Вычисление.Результат;
				Примечание  = Вычисление.ОписаниеОшибки;
				
				Если мЗначениеНеЗаполнено(Результат) Тогда
					Результат = Колонка.ЗначениеПоУмолчанию;
				КонецЕсли;
	
				ТекущаяСтрока.Вставить(Колонка.ИмяРеквизита,Результат);
				
				Если НЕ ПустаяСтрока(Примечание) Тогда
					Сообщить("Строка ["+НомерСтроки+"]("+Колонка.ПредставлениеРеквизита+"): "+ Примечание);
					КоличествоОшибок = КоличествоОшибок + 1;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	Возврат ТекущаяСтрока;
	
КонецФункции // КонтрольЗаполненияСтроки()

// Процедура выполняет обработку области табличного документа:
// заполняет расшифровку по представлению ячейки в соответствии со структурой загружаемых реквизитов
// сообщает об ошибке и устанавливает комментарий, если ячейка содержит ошибку
//
// Параметры:
//  Область 		- ТабличныйДокумент	- Область табличного документа
//  Колонка 		- Структура			- Структура, свойства, в соответствии с которыми необходимо выполнить обработку области
//  ТекущиеДанные  	- Структура			- Структура загруженных значений
//  ТекстыЯчеек    	- Массив			- Массив текстов ячеек строки
//
Функция ОбработатьОбласть(Область, Колонка, ТекущиеДанные, ТекстыЯчеек)
	
	Представление = Область.Текст;
	Примечание = "";
	
	Если Колонка.РежимЗагрузки = "Вычислять" Тогда
		
		Вычисление = ВычислитьЗначениеЯчейки(Колонка.Выражение,ТекущиеДанные,Представление, ТекстыЯчеек, Колонка.ЗначениеПоУмолчанию);
		Если НЕ ПустаяСтрока(Вычисление.ОписаниеОшибки) Тогда
			Результат   = Неопределено;
			Примечание = ""+ Вычисление.ОписаниеОшибки;
		Иначе
			Результат = Вычисление.Результат;
		КонецЕсли;
		
	ИначеЕсли ПустаяСтрока(Представление) Тогда
		Результат = Неопределено;
	Иначе
		НайденныеЗначения = ПолучитьВозможныеЗначения(Колонка, Представление, Примечание, ТекущиеДанные);
		
		Если НайденныеЗначения.Количество() = 0 Тогда
			
			Примечание = "Не найден"+?(Примечание = "","", Символы.ПС+Примечание);
			Результат = Неопределено;
			
		ИначеЕсли НайденныеЗначения.Количество() = 1 Тогда
			
			Результат = НайденныеЗначения[0];
			
			
		Иначе
			
			Примечание = "Не однозначное представление. Вариантов: "+НайденныеЗначения.Количество()+?(Примечание = "","", Символы.ПС+Примечание);
			
			Нашли = Ложь;
			НашлиЗначениеПоУмолчанию = Ложь;
			Для Каждого НайденноеЗначение Из НайденныеЗначения Цикл
				Если НайденноеЗначение = Область.Расшифровка Тогда
					Нашли = Истина;
					Прервать;
				КонецЕсли;
				Если НайденноеЗначение = Колонка.ЗначениеПоУмолчанию Тогда
					НашлиЗначениеПоУмолчанию = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ Нашли Тогда
				
				Если НашлиЗначениеПоУмолчанию Тогда
					НайденноеЗначение = Колонка.ЗначениеПоУмолчанию;
				Иначе
					НайденноеЗначение = НайденныеЗначения[0];
				КонецЕсли;
			КонецЕсли;
			Результат = НайденноеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Если мЗначениеНеЗаполнено(Результат) Тогда
		Результат = Колонка.ЗначениеПоУмолчанию;
	КонецЕсли;
			
	ТекущиеДанные.Вставить(Колонка.ИмяРеквизита,Результат);
	
	Область.Расшифровка = Результат;
	Область.Примечание.Текст = Примечание;
	
	Если НЕ ПустаяСтрока(Примечание) Тогда
		Сообщить("Ячейка["+Область.Имя+"]("+Колонка.ПредставлениеРеквизита+"): " + Примечание);
	КонецЕсли;
	
	Возврат ПустаяСтрока(Примечание);
	
КонецФункции // ОбработатьОбласть()

// Функция возвращает массив возможных значений для текущей колонки по представлению
//
// Параметры:
//  Колонка 		- Структура	- Структура, свойства, в соответствии с которыми необходимо получить возможные значения
//  Представление 	- Строка	- Строка, по которой необходимо вернуть массив значений
//  Примечание    	- Массив	- Массив текстов ячеек строки
//  ТекущиеДанные  	- Структура	- Структура загруженных значений
//
// Возвращаемое значение:
//  Массив			- Массив возможных значений
//
Функция ПолучитьВозможныеЗначения(Колонка, Представление, Примечание, ТекущиеДанные)
	
	
	Примечание = "";
	
	НайденныеЗначения = Новый Массив;
	
	Если ПустаяСтрока(Представление) Тогда
		
		Возврат НайденныеЗначения;
		
	Иначе
		СвязьПоТипу = Неопределено;
		Если НЕ ПустаяСтрока(Колонка.СвязьПоТипу)  Тогда
			
			Если ТипЗНЧ(Колонка.СвязьПоТипу) = Тип("Строка") Тогда
				ТекущиеДанные.Свойство(Колонка.СвязьПоТипу,СвязьПоТипу);
			Иначе
				СвязьПоТипу = Колонка.СвязьПоТипу;
			КонецЕсли;
			Если НЕ СвязьПоТипу = Неопределено Тогда
				
				ЭлементСвязиПоТипу = Колонка.ЭлементСвязиПоТипу;
				Если ЭлементСвязиПоТипу = 0 Тогда
					ЭлементСвязиПоТипу = 1;
				КонецЕсли;
				ВидыСубконто = СвязьПоТипу.ВидыСубконто;
				Если ЭлементСвязиПоТипу > ВидыСубконто.Количество() Тогда
					Возврат НайденныеЗначения;
				КонецЕсли;
				Тип = СвязьПоТипу.ВидыСубконто[ЭлементСвязиПоТипу-1].ВидСубконто.ТипЗначения;
			Иначе
				Тип = Колонка.ОписаниеТипов;
			КонецЕсли;
			 
		Иначе
			Тип = Колонка.ОписаниеТипов;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ТипРеквизита Из Тип.Типы() Цикл
		
		Если ТипРеквизита = ПримитивныеТипы.Число ИЛИ ТипРеквизита = ПримитивныеТипы.Булево Тогда
			НайденныеЗначения.Добавить(мПривестиКЧислу(Представление, Колонка.ОписаниеТипов, Примечание));
		ИначеЕсли ТипРеквизита = ПримитивныеТипы.Строка ИЛИ ТипРеквизита = ПримитивныеТипы.Дата Тогда
			НайденныеЗначения.Добавить(мПривестиКДате(Представление, Колонка.ОписаниеТипов, Примечание));
			
		Иначе
			
			МетаданныеТипа = Метаданные.НайтиПоТипу(ТипРеквизита);
			
			Если Перечисления.ТипВсеСсылки().СодержитТип(ТипРеквизита) Тогда
				
				//Это Перечисление
				Для Каждого Перечисление Из ПолучитьМенеджераПоТипу(ТипРеквизита) Цикл
					Если Строка(Перечисление) = Представление Тогда
						НайденныеЗначения.Добавить(Перечисление);
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипРеквизита) Тогда
				
				//Это документ
				
				Менеджер = ПолучитьМенеджераПоТипу(ТипРеквизита);
				Если Колонка.ИскатьПо = "Номер" Тогда
					//НайденноеЗначение = Менеджер.НайтиПоКоду(Представление);
				ИначеЕсли Колонка.ИскатьПо = "Дата" Тогда
					//НайденноеЗначение = Менеджер.Найти
				Иначе
						
					ДлиннаСинонима = СтрДлина(""+МетаданныеТипа);
						
					Если Лев(Представление, ДлиннаСинонима) = ""+МетаданныеТипа Тогда
						НомерИДата = СокрЛП(Сред(Представление, ДлиннаСинонима+1));
						ПозицияОт = Найти(НомерИДата, " от ");
						Если НЕ ПозицияОт = 0 Тогда
							НомерДок = Лев(НомерИДата, ПозицияОт-1);
							Попытка
								ДатаДок  = Дата(Сред(НомерИДата, ПозицияОт+4));
							Исключение
								ДатаДок = Неопределено;
							КонецПопытки;
							Если НЕ ДатаДок = Неопределено Тогда
								НайденноеЗначение = Менеджер.НайтиПоНомеру(НомерДок, ДатаДок);
								Если НЕ НайденноеЗначение.Пустая() Тогда
									НайденныеЗначения.Добавить(НайденноеЗначение);
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
						
				КонецЕсли;
					
			ИначеЕсли НЕ МетаданныеТипа = Неопределено Тогда
				
				ИскатьПо = Колонка.ИскатьПо;
				ЭтоСправочник = Справочники.ТипВсеСсылки().СодержитТип(ТипРеквизита);
				Если ПустаяСтрока(ИскатьПо) Тогда
					СтрокаОсновногоПредставления = Строка(МетаданныеТипа.ОсновноеПредставление);
					
					Если СтрокаОсновногоПредставления = "ВВидеКода" Тогда
						ИскатьПо = "Код";
					ИначеЕсли СтрокаОсновногоПредставления = "ВВидеНаименования" Тогда
						ИскатьПо = "Наименование";
					ИначеЕсли СтрокаОсновногоПредставления = "ВВидеНомера" Тогда
						ИскатьПо = "Номер";
					КонецЕсли;
				КонецЕсли;
					
				Запрос = Новый Запрос;
				Запрос.Текст =
				"ВЫБРАТЬ
				|	_Таблица.Ссылка
				|ИЗ
				|	" + МетаданныеТипа.ПолноеИмя() + " КАК _Таблица
				|ГДЕ";
				
				Запрос.Текст = Запрос.Текст + "
				|	_Таблица." + ИскатьПо + " = &Представление";
				Запрос.УстановитьПараметр("Представление",Представление);
				
				Если ЭтоСправочник И НЕ ПустаяСтрока(Колонка.СвязьПоВладельцу) И МетаданныеТипа.Владельцы.Количество() Тогда
					
					СвязьПоВладельцу = Неопределено;
					Если ВРег(Колонка.СвязьПоВладельцу) = "ВЫЧИСЛЯЕТСЯ ПО ТИПУ НОМЕНКЛАТУРЫ" Тогда
						
						// получим массив ключей
						МассивКлючей =Новый Массив;
						Для Каждого КлючЗначение Из ТекущиеДанные Цикл
							МассивКлючей.Добавить(КлючЗначение.Ключ);
						КонецЦикла;
						
						// получим список связей по владельцу
						СписокСвязей = ПолучитьСписокСвязейПоВладельцу(Колонка);
						
						// ищем номенклатуру
						ТекНоменклатура = Неопределено;
						Индекс = МассивКлючей.Количество()-1;
						Пока Индекс >= 0 Цикл
							Если СписокСвязей.НайтиПоЗначению(МассивКлючей[Индекс])<> Неопределено Тогда
								ТекущиеДанные.Свойство(МассивКлючей[Индекс], ТекНоменклатура);
								Прервать;
							КонецЕсли;
							Индекс = Индекс -1;
						КонецЦикла;
						
						Если НЕ обЗначениеНеЗаполнено(ТекНоменклатура) И ТипЗнч(ТекНоменклатура) <> Тип("СправочникСсылка.Автоработы") Тогда
							Если Колонка.ОписаниеТипов.Типы()[0] = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
								Если ТекНоменклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
									СвязьПоВладельцу = ТекНоменклатура.ТипНоменклатуры;
								Иначе
									СвязьПоВладельцу = ТекНоменклатура;
								КонецЕсли;
								
							ИначеЕсли Колонка.ОписаниеТипов.Типы()[0] = Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
								Если ТекНоменклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 Тогда
									СвязьПоВладельцу = ТекНоменклатура.ТипНоменклатуры;
								ИначеЕсли ТекНоменклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2 Тогда
									СвязьПоВладельцу = ТекНоменклатура;
								Иначе
									НайденныеЗначения.Добавить(Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку());
									Продолжить;
								КонецЕсли;
							КонецЕсли;
							
							Запрос.Текст = Запрос.Текст + Символы.ПС+ " И _Таблица.Владелец = &СвязьПоВладельцу";
						КонецЕсли;
					ИначеЕсли ВРег(Колонка.СвязьПоВладельцу) = "<СОЗДАВАЕМЫЙ ОБЪЕКТ>" Тогда
						СвязьПоВладельцу = Неопределено;
						
					ИначеЕсли ТипЗНЧ(Колонка.СвязьПоВладельцу) = Тип("Строка") Тогда
						ПозицияТочки = Найти(Колонка.СвязьПоВладельцу,".");
						Если ПозицияТочки>0 Тогда
							Если ТекущиеДанные.Свойство(Лев(Колонка.СвязьПоВладельцу,ПозицияТочки-1)) Тогда
								СвязьПоВладельцу = ТекущиеДанные[Лев(Колонка.СвязьПоВладельцу,ПозицияТочки-1)];
								СвязьПоВладельцу = СвязьПоВладельцу[Сред(Колонка.СвязьПоВладельцу,ПозицияТочки+1)];
							Иначе
								Сообщить("Для реквизита """+Колонка.ПредставлениеРеквизита+""" неверно задана связь по владельцу для типа """+Строка(ТипРеквизита)+"""!'");
								СвязьПоВладельцу = Неопределено;
							КонецЕсли;
						Иначе
							ТекущиеДанные.Свойство(Колонка.СвязьПоВладельцу,СвязьПоВладельцу);
						КонецЕсли;
						
					Иначе
						СвязьПоВладельцу = Колонка.СвязьПоВладельцу;
					КонецЕсли;
					
					Если СвязьПоВладельцу<>Неопределено Тогда
						Запрос.Текст = Запрос.Текст + Символы.ПС+ " И _Таблица.Владелец = &СвязьПоВладельцу";
						Запрос.УстановитьПараметр("СвязьПоВладельцу", СвязьПоВладельцу);
					КонецЕсли; 
				КонецЕсли;
				
				Выборка =  Запрос.Выполнить().Выбрать();
					
				Пока Выборка.Следующий() Цикл
					НайденныеЗначения.Добавить(Выборка.Ссылка);
				КонецЦикла;
			Иначе
				Примечание = "Не описан способ поиска";
				Примечание = "Для Колонки не определен тип значения";
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	Возврат НайденныеЗначения;
КонецФункции // ПолучитьВозможныеЗначения()

////////////////////////////////////////////////////////////////////////////////
//

// Функция возвращает текущие настройки загружаемых реквизитов в формате Табличного документа
//
// Возвращаемое значение:
//  ТабличныйДокумент	- Табличный документ
//
Функция мПолучитьНастройки() Экспорт
	
	МетаданныеОбъекта = ПолучитьМетаданныеИсточника();
	
	Если МетаданныеОбъекта = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидОбъекта     = МетаданныеОбъекта.ПолноеИмя();
	
	ДокументРезультат = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("МакетСохраненияНастроек");
	
	ОбластьШапки = Макет.ПолучитьОбласть("Шапка");
	
	
	Если РежимЗагрузки = 0 Тогда
		ОбластьШапки.Параметры.РежимЗагрузки = "в справочник";
	ИначеЕсли РежимЗагрузки = 1 ИЛИ РежимЗагрузки = 3 Тогда
		ОбластьШапки.Параметры.РежимЗагрузки = "в табличную часть";
	ИначеЕсли РежимЗагрузки = 2 Тогда
		ОбластьШапки.Параметры.РежимЗагрузки = "в регистр сведений";
	КонецЕсли;
	
	ОбластьШапки.Параметры.ВидОбъекта                                = ВидОбъекта;
	ОбластьШапки.Параметры.НеСоздаватьНовыхЭлементов                 = НеСоздаватьНовыхЭлементов;
	ОбластьШапки.Параметры.ЗамещатьСуществующиеЗаписи                 = ?(ЗамещатьСуществующиеЗаписи, "Х","");
	ОбластьШапки.Параметры.РучнаяНумерацияКолонокТабличногоДокумента = ?(РучнаяНумерацияКолонокТабличногоДокумента, "Х","");
	ОбластьШапки.Параметры.ПерваяСтрокаДанныхТабличногоДокумента     = ПерваяСтрокаДанныхТабличногоДокумента;
	
	ДокументРезультат.Вывести(ОбластьШапки);
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		ОбластьСтроки = Макет.ПолучитьОбласть("Строка" + ?(ЗагружаемыйРеквизит.РежимЗагрузки = "Вычислять","Выражение",""));
		
		ОбластьСтроки.Параметры.Пометка      = ?(ЗагружаемыйРеквизит.Пометка, "Х","");
		ОбластьСтроки.Параметры.ИмяРеквизита = ЗагружаемыйРеквизит.ИмяРеквизита;
		ОбластьСтроки.Параметры.ПолеПоиска   = ?(ЗагружаемыйРеквизит.ПолеПоиска, "Х","");
		
		ОписаниеТипов = "";
		Для Каждого Тип Из ЗагружаемыйРеквизит.ОписаниеТипов.Типы() Цикл
			МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
			Если НЕ МетаданныеТипа = Неопределено Тогда
				ОписаниеТипа = МетаданныеТипа.ПолноеИмя();
			ИначеЕсли Тип = Тип("Строка") Тогда
				
				ОписаниеТипа = "Строка";
				Если ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыСтроки.Длина Тогда
					ОписаниеТипа = ОписаниеТипа + ", " + ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыСтроки.Длина;
					Если ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина = ДопустимаяДлина.Фиксированная Тогда
						ОписаниеТипа = ОписаниеТипа + ", " + ДопустимаяДлина.Фиксированная;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли Тип = Тип("Число") Тогда
				ОписаниеТипа = "Число"
				+ ", "+ ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыЧисла.Разрядность
				+ ", "+ ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти
				+ ?(ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак = ДопустимыйЗнак.Неотрицательный,", Неотрицательный","");
			ИначеЕсли Тип = Тип("Дата") Тогда
				ОписаниеТипа = "" + ЗагружаемыйРеквизит.ОписаниеТипов.КвалификаторыДаты.ЧастиДаты;
			ИначеЕсли Тип = Тип("Булево") Тогда
				ОписаниеТипа = "Булево";
			Иначе
				Продолжить;
			КонецЕсли;
			
			ОписаниеТипов = ?(ПустаяСтрока(ОписаниеТипов),"",ОписаниеТипов +Символы.ПС) + ОписаниеТипа;
			
		КонецЦикла;
		ОбластьСтроки.Параметры.ОписаниеТипов       = ОписаниеТипов;
		ОбластьСтроки.Параметры.РежимЗагрузки       = ЗагружаемыйРеквизит.РежимЗагрузки;
		ОбластьСтроки.Параметры.НомерКолонки        = ЗагружаемыйРеквизит.НомерКолонки;
		Если ЗагружаемыйРеквизит.ОписаниеТипов.ПривестиЗначение(Неопределено) = ЗагружаемыйРеквизит.ЗначениеПоУмолчанию Тогда
			ОбластьСтроки.Параметры.ЗначениеПоУмолчанию = "";
		Иначе
			ОбластьСтроки.Параметры.ЗначениеПоУмолчанию = ЗначениеВСтрокуВнутр(ЗагружаемыйРеквизит.ЗначениеПоУмолчанию);
		КонецЕсли;
		
		Если ЗагружаемыйРеквизит.РежимЗагрузки = "Вычислять" Тогда
			
			ОбластьСтроки.Параметры.Выражение           = ЗагружаемыйРеквизит.Выражение;
			
		Иначе
			ОбластьСтроки.Параметры.ИскатьПо            = ЗагружаемыйРеквизит.ИскатьПо;
			ОбластьСтроки.Параметры.СвязьПоВладельцу    = ?(ТипЗнч(ЗагружаемыйРеквизит.СвязьПоВладельцу) = Тип("Строка"),ЗагружаемыйРеквизит.СвязьПоВладельцу,ЗначениеВСтрокуВнутр(ЗагружаемыйРеквизит.СвязьПоВладельцу));
			ОбластьСтроки.Параметры.СвязьПоТипу         = ?(ТипЗнч(ЗагружаемыйРеквизит.СвязьПоТипу) = Тип("Строка"),ЗагружаемыйРеквизит.СвязьПоТипу,ЗначениеВСтрокуВнутр(ЗагружаемыйРеквизит.СвязьПоТипу));
			ОбластьСтроки.Параметры.ЭлементСвязиПоТипу  = ЗагружаемыйРеквизит.ЭлементСвязиПоТипу;
		КонецЕсли;
		
		ДокументРезультат.Вывести(ОбластьСтроки);
		
	КонецЦикла;
	
	ОбластьПодвала = Макет.ПолучитьОбласть("События");
	ОбластьПодвала.Параметры.ПередЗаписьюОбъекта = ПередЗаписьюОбъекта;
	ОбластьПодвала.Параметры.ПриЗаписиОбъекта = ПриЗаписиОбъекта;
	ДокументРезультат.Вывести(ОбластьПодвала);
	Если РежимЗагрузки Тогда
		
		ОбластьПодвала = Макет.ПолучитьОбласть("СобытияПослеДобавленияСтроки");
		ОбластьПодвала.Параметры.ПослеДобавленияСтроки = ПослеДобавленияСтроки;
		ДокументРезультат.Вывести(ОбластьПодвала);
		
	КонецЕсли;
	
	Возврат ДокументРезультат;
	
КонецФункции // мПолучитьНастройки()

// Функция возвращает настройку, сохраненную в списке сохраненных настроек
//
// Параметры:
//  СписокСохраненныхНастроек  - СписокЗначений	- Список значений, список сохраненных настроек
//
// Возвращаемое значение:
//  ТабличныйДокумент	- Табличный документ - настройки загружаемых реквизитов
//
Функция ПоучитьНастройкуПоУмолчанию(СписокСохраненныхНастроек) Экспорт
	
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	Если МетаданныеИсточника = Неопределено ИЛИ НЕ ТипЗнч(СписокСохраненныхНастроек) = Тип("ТаблицаЗначений") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаСписка Из СписокСохраненныхНастроек Цикл
		Если СтрокаСписка.Пометка Тогда
			Возврат СтрокаСписка.Значение;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции // ПоучитьНастройкуПоУмолчанию()

////////////////////////////////////////////////////////////////////////////////
//

// Функция возвращает список, элементами которого выступают возможные имена представления загружаемого реквизита
//
// Параметры:
//  ЗагружаемыйРеквизит - Строка	- Строка таблицы значений загружаемого реквизита
//
// Возвращаемое значение:
//  СписокЗначений		- Список значений; значение списка - строка имя представления
//
Функция ПолучитьСписокИменПредставлений(ЗагружаемыйРеквизит) Экспорт
	
	СписокВыбора = Новый СписокЗначений;
	Если ЗагружаемыйРеквизит.ОписаниеТипов.Типы().Количество() = 1 Тогда
		
		Тип = ЗагружаемыйРеквизит.ОписаниеТипов.Типы()[0];
		
		МетаданныеТипа      = Метаданные.НайтиПоТипу(Тип);
		ЭтоСправочник       = Справочники.ТипВсеСсылки().СодержитТип(Тип);
		ЭтоСчет             = ПланыСчетов.ТипВсеСсылки().СодержитТип(Тип);
		ЭтоВидХарактеристик = ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(Тип);
		Если ЭтоСправочник ИЛИ ЭтоСчет ИЛИ ЭтоВидХарактеристик Тогда
			
			ЕстьКод = МетаданныеТипа.ДлинаКода > 0;
			ЕстьИмя = МетаданныеТипа.ДлинаНаименования > 0;
			
			ВидОсновногоПредставление = ?(ЭтоСправочник, Метаданные.СвойстваОбъектов.ОсновноеПредставлениеСправочника,
			?(ЭтоСчет,Метаданные.СвойстваОбъектов.ОсновноеПредставлениеСчета,Метаданные.СвойстваОбъектов.ОсновноеПредставлениеВидаХарактеристики));
			
			Если МетаданныеТипа.ОсновноеПредставление = ВидОсновногоПредставление.ВВидеКода Тогда
				
				Если ЕстьКод Тогда
					СписокВыбора.Добавить("Код", "Код");
				КонецЕсли;
				
				Если ЕстьИмя Тогда
					СписокВыбора.Добавить("Наименование", "Наименование");
				КонецЕсли;
				
			Иначе
				
				Если ЕстьИмя Тогда
					СписокВыбора.Добавить("Наименование", "Наименование");
				КонецЕсли;
				
				Если ЕстьКод Тогда
					СписокВыбора.Добавить("Код", "Код");
				КонецЕсли;
				
			КонецЕсли;
			
			Для Каждого Реквизит Из МетаданныеТипа.Реквизиты Цикл
				
				Если НЕ Реквизит.Индексирование = Метаданные.СвойстваОбъектов.Индексирование.НеИндексировать
					И Реквизит.Тип.Типы().Количество() = 1 И Реквизит.Тип.Типы()[0] = Тип ("Строка")
				Тогда
					
					СписокВыбора.Добавить(Реквизит.Имя, Реквизит.Представление());
					
				КонецЕсли;
				
			КонецЦикла;
			
			
		Иначе
		
		КонецЕсли;
		
	КонецЕсли;
	Возврат СписокВыбора;
КонецФункции // ПолучитьСписокИменПредставлений()


// Функция возвращает список, элементами которого выступают возможные связи по владельцу для загружаемого реквизита
//
// Параметры:
//  ЗагружаемыйРеквизит - Строка	- Строка таблицы значений загружаемого реквизита
//
// Возвращаемое значение:
//  СписокЗначений		- Список значений; значение списка - строка имя колонки связи или ссылка на элемент связи
//
Функция ПолучитьСписокСвязейПоВладельцу(ЗагружаемыйРеквизит) Экспорт
	
	ЕстьТипСамогоОбъекта = Ложь;
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	Если РежимЗагрузки = 0 Тогда
		ОписаниеТиповСправочника = Тип(СтрЗаменить(МетаданныеИсточника.ПолноеИмя(),".","Ссылка."));
	Иначе
		ОписаниеТиповСправочника = Неопределено;
	КонецЕсли;
	
	СписокВыбора = Новый СписокЗначений;
	ТипыВладельцев = Новый Соответствие;
	
	Если ЗагружаемыйРеквизит.ОписаниеТипов.Типы()[0] = Тип("СправочникСсылка.ЕдиницыИзмерения") 
	ИЛИ ЗагружаемыйРеквизит.ОписаниеТипов.Типы()[0] = Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
		СписокВыбора.Добавить("Вычисляется по типу номенклатуры", "Вычисляется по типу номенклатуры");
	КонецЕсли;
		
	Для Каждого ТипКолонки Из ЗагружаемыйРеквизит.ОписаниеТипов.Типы() Цикл
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипКолонки) Тогда
			ТекВладельцыКолонки = Метаданные.НайтиПоТипу(ТипКолонки).Владельцы;
			Для Каждого Владелец Из ТекВладельцыКолонки Цикл
				ТипВладельца   = Тип(СтрЗаменить(Владелец.ПолноеИмя(), ".", "Ссылка."));
				Если ТипыВладельцев[ТипВладельца] = Неопределено Тогда
 					Если ТипВладельца = ОписаниеТиповСправочника Тогда
						ЕстьТипСамогоОбъекта = Истина;
					КонецЕсли;
					ТипыВладельцев.Вставить(ТипВладельца, Владелец.ПолноеИмя());
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ТипыВладельцев Цикл
		Для Каждого КолонкаСвязиПоВладельцу Из ТаблицаЗагружаемыхРеквизитов Цикл
			Для Каждого ТекТипВладельца Из КолонкаСвязиПоВладельцу.ОписаниеТипов.Типы() Цикл
				Если ТекТипВладельца = КлючИЗначение.Ключ Тогда
					СписокВыбора.Добавить(КолонкаСвязиПоВладельцу.ИмяРеквизита,КолонкаСвязиПоВладельцу.ИмяРеквизита);
					Для Каждого ТекРеквизитВладельца Из Метаданные.НайтиПоТипу(ТекТипВладельца).Реквизиты Цикл
						Для Каждого ТекПроверкаТипов Из ТипыВладельцев Цикл
							Если ТекРеквизитВладельца.Тип.СодержитТип(ТекПроверкаТипов.Ключ) Тогда
								СписокВыбора.Добавить(КолонкаСвязиПоВладельцу.ИмяРеквизита+"."+ТекРеквизитВладельца.Имя, КолонкаСвязиПоВладельцу.ИмяРеквизита+"."+ТекРеквизитВладельца.Имя);
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если НЕ ТипыВладельцев.Количество() = 0 Тогда
		СписокВыбора.Добавить(Неопределено, "< пустое значение >");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из ТипыВладельцев Цикл
		СписокВыбора.Добавить(СтрЗаменить(КлючИЗначение.Значение, ".", "Ссылка."), "<"+КлючИЗначение.Значение+">");
	КонецЦикла;
	
	Если ЕстьТипСамогоОбъекта Тогда
		
		СписокВыбора.Вставить(0,"<Создаваемый объект>", "<Создаваемый объект>");
		
	КонецЕсли;
	
	Возврат СписокВыбора;
	
КонецФункции // ПолучитьСписокСвязейПоВладельцу()

// Функция возвращает список, элементами которого выступают возможные связи по типу для загружаемого реквизита
//
// Параметры:
//  ЗагружаемыйРеквизит - Строка	- Строка таблицы значений загружаемого реквизита
//
// Возвращаемое значение:
//  СписокЗначений		- Список значений; значение списка - строка имя колонки связи или ссылка на элемент связи
//
Функция ПолучитьСписокСвязейПоТипу(ЗагружаемыйРеквизит) Экспорт
	
	СписокВыбора = Новый СписокЗначений;
	
	ВозможныеПланыСчетов = Новый Структура;
 	Для Каждого ПланСчетов Из Метаданные.ПланыСчетов Цикл
		Если ПланСчетов.ВидыСубконто = Неопределено Тогда Продолжить; КонецЕсли;
		Если ПланСчетов.ВидыСубконто.Тип = ЗагружаемыйРеквизит.ОписаниеТипов Тогда
			ВозможныеПланыСчетов.Вставить(ПланСчетов.Имя,ПланыСчетов[ПланСчетов.Имя]);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПланСчетов Из ВозможныеПланыСчетов Цикл
		ТипЗНЧПланСчетов = ТипЗНЧ(ПланСчетов.Значение.ПустаяСсылка());
		Для Каждого КолонкаСвязиПоТипу Из ТаблицаЗагружаемыхРеквизитов Цикл
			Если КолонкаСвязиПоТипу.ОписаниеТипов.Типы()[0] = ТипЗНЧПланСчетов Тогда
				СписокВыбора.Добавить(КолонкаСвязиПоТипу.ИмяРеквизита,КолонкаСвязиПоТипу.ИмяРеквизита);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если НЕ ВозможныеПланыСчетов.Количество() = 0 Тогда
		СписокВыбора.Добавить(Неопределено, "< пустое значение >");
	КонецЕсли;
	
	Для Каждого ПланСчетов Из ВозможныеПланыСчетов Цикл
		СписокВыбора.Добавить("ПланСчетовСсылка."+ПланСчетов.Ключ,"<"+ПланСчетов.Ключ+">");
	КонецЦикла;
	
	Возврат СписокВыбора;
КонецФункции // ПолучитьСписокСвязейПоТипу()

////////////////////////////////////////////////////////////////////////////////
//

// Заполняет настройки колонок по умолчанию или по переданным настройкам
//
// Параметры:
//  Настройки - ТабличныйДокумент	- Табличный документ или неопределено
//
Процедура ЗаполнитьНастройкиКолонок(Настройки) Экспорт
	
	ПередЗаписьюОбъекта   = "";
	ПриЗаписиОбъекта      = "";
	ПослеДобавленияСтроки = "";
	
	Если ТипЗнч(Настройки) = Тип("ТабличныйДокумент") Тогда
		ВерсияОбработки = СокрЛП(Настройки.Область("R1C5").Текст);
		Если НЕ ВерсияОбработки = "1.2" Тогда
			ВерсияОбработки = "1.1";
			ТекущаяСтрока = 9; //Строка с которой начинается таблица реквизитов
		Иначе
			ТекущаяСтрока = 11; //Строка с которой начинается таблица реквизитов
		КонецЕсли;
		
		Попытка
			
			ТекстВосстановленногоРежимаЗагрузки = СокрЛП(Настройки.Область(?(ВерсияОбработки = "1.1","R1","R2") + "C5").Текст);
			Если ТекстВосстановленногоРежимаЗагрузки = "в справочник" ИЛИ ТекстВосстановленногоРежимаЗагрузки = "" Тогда
				ВосстановленныйРежимЗагрузки = 0;
			ИначеЕсли ТекстВосстановленногоРежимаЗагрузки = "в табличную часть" ИЛИ ТекстВосстановленногоРежимаЗагрузки = "Х" Тогда
				ВосстановленныйРежимЗагрузки = 1;
			ИначеЕсли ТекстВосстановленногоРежимаЗагрузки = "в регистр сведений" Тогда
				ВосстановленныйРежимЗагрузки = 2;
			КонецЕсли;
			
			Если РежимЗагрузки = 3 Тогда
				Если ВосстановленныйРежимЗагрузки = 1 Тогда
					ВосстановленныйРежимЗагрузки = 3;
				Иначе
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(Настройки.Область(?(ВерсияОбработки = "1.1","R2","R3") + "C5").Текст);
			Если МетаданныеОбъекта = Неопределено Тогда
				ВызватьИсключение "Неправильный формат файла";
			КонецЕсли;
			
			Если ВосстановленныйРежимЗагрузки = 0 Тогда
				ВосстановленныйСсылкаИсточника = Новый(СтрЗаменить(МетаданныеОбъекта.ПолноеИмя(),".","Ссылка."));
			ИначеЕсли ВосстановленныйРежимЗагрузки = 1 Тогда
				ВосстановленныйСсылкаИсточника = Новый(СтрЗаменить(МетаданныеОбъекта.Родитель().ПолноеИмя(),".","Ссылка."));
			ИначеЕсли ВосстановленныйРежимЗагрузки = 3 Тогда
				ВосстановленныйСсылкаИсточника = СсылкаИсточника;
			Иначе
				ВосстановленныйСсылкаИсточника = Неопределено;
			КонецЕсли;
			
			СтруктураУмолчаний = Новый Структура;
			ТекущаяСтрокаОбласти = "R" + Формат(ТекущаяСтрока, "ЧГ=");
			ИмяРеквизита = Настройки.Область(ТекущаяСтрокаОбласти + "C2").Текст;
			Пока НЕ ПустаяСтрока(ИмяРеквизита) Цикл
				СтруктураУмолчанияРеквизита = Новый Структура;
				СтруктураУмолчанияРеквизита.Вставить("ИмяРеквизита",ИмяРеквизита);
				СтруктураУмолчанияРеквизита.Вставить("Пометка",НЕ ПустаяСтрока(Настройки.Область(ТекущаяСтрокаОбласти + "C1").Текст));
				СтруктураУмолчанияРеквизита.Вставить("ПолеПоиска",НЕ ПустаяСтрока(Настройки.Область(ТекущаяСтрокаОбласти + "C3").Текст));
				
				Типы = Новый Массив;
				ОписаниеТиповСтрокой = Настройки.Область(ТекущаяСтрокаОбласти + "C4").Текст;
				Для к = 1 По СтрЧислоСтрок(ОписаниеТиповСтрокой)  Цикл
					
					кс = Неопределено;кч = Неопределено;кд = Неопределено;
					МассивЧастейТипа = мРазложитьСтрокуВМассивПодстрок(НРег(СокрЛП(СтрПолучитьСтроку(ОписаниеТиповСтрокой,к ))), ",");
					Если МассивЧастейТипа.Количество() = 0 Тогда
						Продолжить;
					ИначеЕсли Найти(МассивЧастейТипа[0],".") Тогда
						Тип = Тип(СтрЗаменить(МассивЧастейТипа[0],".","Ссылка."));
					ИначеЕсли МассивЧастейТипа[0] = "строка" Тогда
						Тип = Тип("Строка");
						Если МассивЧастейТипа.Количество() = 2 Тогда
							кс = Новый КвалификаторыСтроки(мПривестиКЧислу(МассивЧастейТипа[1]),ДопустимаяДлина.Переменная);
						ИначеЕсли МассивЧастейТипа.Количество() = 3 Тогда
							кс = Новый КвалификаторыСтроки(мПривестиКЧислу(МассивЧастейТипа[1]),ДопустимаяДлина.Фиксированная);
						Иначе
							кс = Новый КвалификаторыСтроки;
						КонецЕсли;
					ИначеЕсли МассивЧастейТипа[0] = "число" Тогда
						Тип = Тип("Число");
						кч = Новый КвалификаторыЧисла(мПривестиКЧислу(МассивЧастейТипа[1]),мПривестиКЧислу(МассивЧастейТипа[2]),?(МассивЧастейТипа.Количество() = 4, ДопустимыйЗнак.Неотрицательный, ДопустимыйЗнак.Любой));
					ИначеЕсли МассивЧастейТипа[0] = "булево" Тогда
						Тип = Тип("Булево");
					ИначеЕсли МассивЧастейТипа[0] = "дата" Тогда
						Тип = Тип("Дата");
						кд = Новый КвалификаторыДаты(ЧастиДаты.Дата);
					ИначеЕсли МассивЧастейТипа[0] = "время" Тогда
						Тип = Тип("Дата");
						кд = Новый КвалификаторыДаты(ЧастиДаты.Время);
					ИначеЕсли МассивЧастейТипа[0] = "дата и время" Тогда
						Тип = Тип("Дата");
						кд = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
					Иначе
						Продолжить;
					КонецЕсли;
					Типы.Добавить(Тип);
				КонецЦикла;
				ОписаниеТипов = Новый ОписаниеТипов(Типы, кч, кс, кд);
				СтруктураУмолчанияРеквизита.Вставить("ОписаниеТипов",ОписаниеТипов);
				
				РежимЗагрузкиРеквизита = Настройки.Область(ТекущаяСтрокаОбласти + "C5").Текст;
				
				СтруктураУмолчанияРеквизита.Вставить("РежимЗагрузки",РежимЗагрузкиРеквизита);
				СтруктураУмолчанияРеквизита.Вставить("НомерКолонки", Настройки.Область(ТекущаяСтрокаОбласти + "C6").Текст);
				ЗначениеПоУмолчанию = Настройки.Область(ТекущаяСтрокаОбласти + "C7").Текст;
				СтруктураУмолчанияРеквизита.Вставить("ЗначениеПоУмолчанию",?(ПустаяСтрока(ЗначениеПоУмолчанию), ОписаниеТипов.ПривестиЗначение(Неопределено), ЗначениеИзСтрокиВнутр(ЗначениеПоУмолчанию)));
				
				Если РежимЗагрузкиРеквизита = "Вычислять" Тогда
					СтруктураУмолчанияРеквизита.Вставить("Выражение",Настройки.Область(ТекущаяСтрокаОбласти + "C8").Текст);
				Иначе
					СтруктураУмолчанияРеквизита.Вставить("ИскатьПо",Настройки.Область(ТекущаяСтрокаОбласти + "C8").Текст);
					
					СвязьПоВладельцу   = Настройки.Область(ТекущаяСтрокаОбласти + "C9").Текст;
					СтруктураУмолчанияРеквизита.Вставить("СвязьПоВладельцу",?(Лев(СвязьПоВладельцу,1) = "{",ЗначениеИзСтрокиВнутр(СвязьПоВладельцу), СвязьПоВладельцу));
					
					СвязьПоТипу        = Настройки.Область(ТекущаяСтрокаОбласти + "C10").Текст;
					СтруктураУмолчанияРеквизита.Вставить("СвязьПоТипу",?(Лев(СвязьПоТипу,1) = "{",ЗначениеИзСтрокиВнутр(СвязьПоТипу), СвязьПоТипу));
					
					СтруктураУмолчанияРеквизита.Вставить("ЭлементСвязиПоТипу",мПривестиКЧислу(Настройки.Область(ТекущаяСтрокаОбласти + "C11").Текст));
				КонецЕсли;
				
				
				СтруктураУмолчаний.Вставить(ИмяРеквизита,СтруктураУмолчанияРеквизита);
				ТекущаяСтрока = ТекущаяСтрока + 1;
				ТекущаяСтрокаОбласти = "R" + Формат(ТекущаяСтрока, "ЧГ=");
				ИмяРеквизита = Настройки.Область(ТекущаяСтрокаОбласти + "C2").Текст;
				
			КонецЦикла;
			
		Исключение
			мСообщитьОбОшибке(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		
		МетаданныеИсточника = ПолучитьМетаданныеИсточника();
		Если МетаданныеИсточника = Неопределено Тогда
			Возврат;
		КонецЕсли;
	
		РежимЗагрузки   = ВосстановленныйРежимЗагрузки;
		СсылкаИсточника = ВосстановленныйСсылкаИсточника;
		ТабличнаяЧастьИсточника = ?(ВосстановленныйРежимЗагрузки,МетаданныеОбъекта.Имя,Неопределено);
		
		Если РежимЗагрузки = 3 Тогда
			ТЧДокумента = ТабличнаяЧастьДокументаОбъекта.Выгрузить();
		КонецЕсли;
		
		НеСоздаватьНовыхЭлементов                 = Настройки.Область(?(ВерсияОбработки = "1.1","R3","R4") + "C5").Текст;
		ЗамещатьСуществующиеЗаписи = ?(ВерсияОбработки = "1.1",Ложь, НЕ ПустаяСтрока(Настройки.Область("R5C5").Текст));
		РучнаяНумерацияКолонокТабличногоДокумента = НЕ ПустаяСтрока(Настройки.Область(?(ВерсияОбработки = "1.1","R4","R6") + "C5").Текст);
		ПерваяСтрокаДанныхТабличногоДокумента     = мПривестиКЧислу(Настройки.Область(?(ВерсияОбработки = "1.1","R5","R7") + "C5").Текст);
		
		ПередЗаписьюОбъекта = Настройки.Область("R" + Формат(ТекущаяСтрока + 2, "ЧГ=") + "C3").Текст;
		ПриЗаписиОбъекта    = Настройки.Область("R" + Формат(ТекущаяСтрока + 3, "ЧГ=") + "C3").Текст;
		
		Если РежимЗагрузки Тогда
			ПослеДобавленияСтроки = Настройки.Область("R" + Формат(ТекущаяСтрока + 4, "ЧГ=") + "C3").Текст;
		КонецЕсли;
		
		ТекущаяСтрока = ТекущаяСтрока + 1;
		
	КонецЕсли;
	Оформление = Неопределено;
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	ТаблицаЗагружаемыхРеквизитов.Очистить();
	
	Если МетаданныеИсточника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если РежимЗагрузки = 0 Тогда
		ЗаполнитьНастройкиКолонокСправочника();
	ИначеЕсли РежимЗагрузки = 1 ИЛИ РежимЗагрузки = 3 Тогда
		ЗаполнитьНастройкиКолонокТабличнойЧасти();
		
		ДляАРМ = Ложь;
		
		Попытка
			ОсновнаяФормаОбъекта = МетаданныеИсточника.Родитель().ОсновнаяФормаОбъекта;
			ДляАРМ = Ложь;
		Исключение 
			ДляАРМ = Истина;
		КонецПопытки;
		
		Если ДляАРМ Тогда
			//для АРМ Корзина
			ОсновнаяФормаОбъекта = МетаданныеИсточника.Родитель().ОсновнаяФорма;
			Если НЕ ОсновнаяФормаОбъекта = Неопределено И СтруктураУмолчаний = Неопределено Тогда
				Форма = Обработки.АРМКорзина.ПолучитьФорму();
				Попытка
					Оформление = Форма.ЭлементыФормы[ТабличнаяЧастьИсточника];
				Исключение
					Оформление = Форма.ЭлементыФормы.Товары;
				КонецПопытки;
			КонецЕсли;
		Иначе
			//для документов и др.объектов
			Если НЕ ОсновнаяФормаОбъекта = Неопределено И СтруктураУмолчаний = Неопределено Тогда
				ИмяОсновнойФормы = ОсновнаяФормаОбъекта.Имя;
				Если НЕ ПустаяСтрока(ИмяОсновнойФормы) Тогда
					Менеджер = ПолучитьМенеджераПоТипу(ТипЗНЧ(Новый(СтрЗаменить(МетаданныеИсточника.Родитель().ПолноеИмя(),".","Ссылка."))));
					Форма = Менеджер.ПолучитьФорму(ИмяОсновнойФормы);
					Для Каждого ЭлементФормы Из Форма.ЭлементыФормы Цикл
						Если ТипЗНЧ(ЭлементФормы) = Тип("ТабличноеПоле") И ЭлементФормы.Данные = МетаданныеИсточника.Имя Тогда
							Оформление = ЭлементФормы;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли РежимЗагрузки = 2 Тогда
		ЗаполнитьНастройкиКолонокРегистраСведений();
	КонецЕсли;
	
	Если НЕ Оформление = Неопределено Тогда
		НомерКолонкиОформления = 0;
		НомерКолонки = 1;
		Для Каждого Колонка Из Оформление.Колонки Цикл
			
			ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Найти(Колонка.ДанныеФлажка,"ИмяРеквизита");
			Если НЕ ЗагружаемыйРеквизит = Неопределено Тогда
				Индекс = ТаблицаЗагружаемыхРеквизитов.Индекс(ЗагружаемыйРеквизит);
				Если Индекс >= НомерКолонкиОформления  Тогда
					
					ЗагружаемыйРеквизит.ШиринаКолонки = 3;
					ТаблицаЗагружаемыхРеквизитов.Сдвинуть(ЗагружаемыйРеквизит, НомерКолонкиОформления - Индекс);
					
					Если Колонка.Видимость Тогда
						ЗагружаемыйРеквизит.Пометка = Колонка.Видимость;
						ЗагружаемыйРеквизит.НомерКолонки = НомерКолонки;
						НомерКолонки = НомерКолонки + 1;
					КонецЕсли;
					
					НомерКолонкиОформления = НомерКолонкиОформления + 1;
					
				КонецЕсли;
			КонецЕсли;
			
			ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Найти(Колонка.Данные,"ИмяРеквизита");
			Если НЕ ЗагружаемыйРеквизит = Неопределено Тогда
				Индекс = ТаблицаЗагружаемыхРеквизитов.Индекс(ЗагружаемыйРеквизит);
				Если Индекс >= НомерКолонкиОформления Тогда
					ЗагружаемыйРеквизит.ШиринаКолонки = Колонка.Ширина;
					ТаблицаЗагружаемыхРеквизитов.Сдвинуть(ЗагружаемыйРеквизит, НомерКолонкиОформления - Индекс);
					Если Колонка.Видимость Тогда
						ЗагружаемыйРеквизит.Пометка = Колонка.Видимость;
						ЗагружаемыйРеквизит.НомерКолонки = НомерКолонки;
						НомерКолонки = НомерКолонки + 1;
					КонецЕсли;
					НомерКолонкиОформления = НомерКолонкиОформления + 1;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	ИначеЕсли НЕ СтруктураУмолчаний = Неопределено Тогда
		
		НомерКолонкиОформления = 0;
		Для Каждого КлючИЗначение Из СтруктураУмолчаний Цикл
			Колонка = КлючИЗначение.Значение;
			ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Найти(Колонка.ИмяРеквизита,"ИмяРеквизита");
			Если НЕ ЗагружаемыйРеквизит = Неопределено Тогда
				Индекс = ТаблицаЗагружаемыхРеквизитов.Индекс(ЗагружаемыйРеквизит);
				Если Индекс >= НомерКолонкиОформления Тогда
					ЗаполнитьЗначенияСвойств(ЗагружаемыйРеквизит, Колонка);
					
					ТаблицаЗагружаемыхРеквизитов.Сдвинуть(ЗагружаемыйРеквизит, НомерКолонкиОформления - Индекс);
					НомерКолонкиОформления = НомерКолонкиОформления + 1;
					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		
		НомерКолонки = 1;
		Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
			
			ЗагружаемыйРеквизит.Пометка      = Истина;
			ЗагружаемыйРеквизит.НомерКолонки = НомерКолонки;
			НомерКолонки = НомерКолонки + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры // ЗаполнитьНастройкиКолонок()

// Заполняет настройки колонок по умолчанию для табличной части
Процедура ЗаполнитьНастройкиКолонокТабличнойЧасти()
	
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	
	Если МетаданныеИсточника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Реквизит Из МетаданныеИсточника.Реквизиты Цикл
		ЗагружаемыйРеквизит                        = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = Реквизит.Имя;
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = Реквизит.Представление();
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = МетаданныеИсточника.Реквизиты[ЗагружаемыйРеквизит.ИмяРеквизита].Тип;
	КонецЦикла;
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		ЗагружаемыйРеквизит.ОписаниеТипов = ЗагружаемыйРеквизит.ДоступноеОписаниеТипов;
	КонецЦикла;
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		
		СписокВыбора = ПолучитьСписокИменПредставлений(ЗагружаемыйРеквизит);
		ЗагружаемыйРеквизит.ИскатьПо = ?(СписокВыбора.Количество() = 0, "",СписокВыбора[0].Значение);
		
		СписокВыбора = ПолучитьСписокСвязейПоВладельцу(ЗагружаемыйРеквизит);
		ЗагружаемыйРеквизит.СвязьПоВладельцу = ?(СписокВыбора.Количество() = 0, "",СписокВыбора[0].Значение);
		
		СписокВыбора = ПолучитьСписокСвязейПоТипу(ЗагружаемыйРеквизит);
		Если СписокВыбора.Количество() = 0 Тогда
			ЗагружаемыйРеквизит.СвязьПоТипу = "";
			ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 0;
		Иначе
			ЗагружаемыйРеквизит.СвязьПоТипу = СписокВыбора[0].Значение;
			Если Найти(ЗагружаемыйРеквизит.ИмяРеквизита,"3") <> 0 Тогда
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 3;
				
			ИначеЕсли Найти(ЗагружаемыйРеквизит.ИмяРеквизита,"2") <> 0 Тогда
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 2;
				
			Иначе
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗагружаемыйРеквизит.ЗначениеПоУмолчанию = ЗагружаемыйРеквизит.ОписаниеТипов.ПривестиЗначение(Неопределено);
		ЗагружаемыйРеквизит.РежимЗагрузки = "Искать";
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьНастройкиКолонокТабличнойЧасти()

// Заполняет настройки колонок по умолчанию для справочника
Процедура ЗаполнитьНастройкиКолонокСправочника()
	
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	
	Если МетаданныеИсточника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если МетаданныеИсточника.ДлинаКода > 0 Тогда
		
		ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = "Код";
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = "Код";
		ЗагружаемыйРеквизит.МожетБытьПолемПоиска   =  Истина;
		
		Если МетаданныеИсточника.ТипКода = Метаданные.СвойстваОбъектов.ТипКодаСправочника.Строка Тогда
			ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(МетаданныеИсточника.ДлинаКода));
		Иначе
			ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(МетаданныеИсточника.ДлинаКода));
		КонецЕсли;
		
	КонецЕсли;
	
	Если МетаданныеИсточника.ДлинаНаименования > 0 Тогда
		
		ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = "Наименование";
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = "Наименование";
		ЗагружаемыйРеквизит.МожетБытьПолемПоиска   =  Истина;
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(МетаданныеИсточника.ДлинаНаименования));
		
	КонецЕсли;
	
	Если МетаданныеИсточника.Владельцы.Количество() > 0 Тогда
		
		ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = "Владелец";
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = "Владелец";
		ЗагружаемыйРеквизит.МожетБытьПолемПоиска   =  Истина;
		
		СтрокаОписанияТипов = "";
			
		Для Каждого Владелец Из МетаданныеИсточника.Владельцы Цикл
			СтрокаОписанияТипов = ?(ПустаяСтрока(СтрокаОписанияТипов),"",СтрокаОписанияТипов + ", ") + Владелец.ПолноеИмя();
		КонецЦикла;
			
		СтрокаОписанияТипов = СтрЗаменить(СтрокаОписанияТипов,".","Ссылка.");
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов(СтрокаОписанияТипов);
		
		
	КонецЕсли;
	
	Если МетаданныеИсточника.Иерархический Тогда
		
		Если МетаданныеИсточника.ВидИерархии=Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Добавить();
			ЗагружаемыйРеквизит.ИмяРеквизита           = "ЭтоГруппа";
			ЗагружаемыйРеквизит.ПредставлениеРеквизита = "Группа";
			ЗагружаемыйРеквизит.МожетБытьПолемПоиска   = Ложь;
			ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов("Булево");
		КонецЕсли; 
		
		ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = "Родитель";
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = "Родитель";
		ЗагружаемыйРеквизит.МожетБытьПолемПоиска   = Истина;
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов(СтрЗаменить(МетаданныеИсточника.ПолноеИмя(),".","Ссылка."));
		
	КонецЕсли;
	
	Для Каждого Реквизит Из МетаданныеИсточника.Реквизиты Цикл
		Если НЕ Реквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы Тогда
			ЗагружаемыйРеквизит                        = ТаблицаЗагружаемыхРеквизитов.Добавить();
			ЗагружаемыйРеквизит.ИмяРеквизита           = Реквизит.Имя;
			ЗагружаемыйРеквизит.ПредставлениеРеквизита = Реквизит.Представление();
			ЗагружаемыйРеквизит.МожетБытьПолемПоиска   = НЕ Реквизит.Индексирование = Метаданные.СвойстваОбъектов.Индексирование.НеИндексировать;
			ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = МетаданныеИсточника.Реквизиты[ЗагружаемыйРеквизит.ИмяРеквизита].Тип;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		ЗагружаемыйРеквизит.ОписаниеТипов = ЗагружаемыйРеквизит.ДоступноеОписаниеТипов;
	КонецЦикла;
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		
		СписокВыбора = ПолучитьСписокИменПредставлений(ЗагружаемыйРеквизит);
		ЗагружаемыйРеквизит.ИскатьПо = ?(СписокВыбора.Количество() = 0, "",СписокВыбора[0].Значение);
		
		СписокВыбора = ПолучитьСписокСвязейПоВладельцу(ЗагружаемыйРеквизит);
		ЗагружаемыйРеквизит.СвязьПоВладельцу = ?(СписокВыбора.Количество() = 0, "",СписокВыбора[0].Значение);
		
		СписокВыбора = ПолучитьСписокСвязейПоТипу(ЗагружаемыйРеквизит);
		Если СписокВыбора.Количество() = 0 Тогда
			ЗагружаемыйРеквизит.СвязьПоТипу = "";
			ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 0;
		Иначе
			ЗагружаемыйРеквизит.СвязьПоТипу = СписокВыбора[0].Значение;
			Если Найти(ЗагружаемыйРеквизит.ИмяРеквизита,"3") <> 0 Тогда
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 3;
				
			ИначеЕсли Найти(ЗагружаемыйРеквизит.ИмяРеквизита,"2") <> 0 Тогда
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 2;
				
			Иначе
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 1;
				
			КонецЕсли;
			 
			
		КонецЕсли;
		
		ЗагружаемыйРеквизит.ЗначениеПоУмолчанию = ЗагружаемыйРеквизит.ОписаниеТипов.ПривестиЗначение(Неопределено);
		
		ЗагружаемыйРеквизит.РежимЗагрузки = "Искать";
	КонецЦикла;
	
	
КонецПроцедуры // ЗаполнитьНастройкиКолонокСправочника()

// Заполняет настройки колонок по умолчанию для регистра сведений
Процедура ЗаполнитьНастройкиКолонокРегистраСведений()
	
	МетаданныеИсточника = ПолучитьМетаданныеИсточника();
	
	Если МетаданныеИсточника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ МетаданныеИсточника.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
		
		ЗагружаемыйРеквизит = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = "Период";
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = "Период";
		ЗагружаемыйРеквизит.МожетБытьПолемПоиска = Истина;
		ЗагружаемыйРеквизит.ПолеПоиска           = Истина;
		
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = Новый ОписаниеТипов("Дата", , , ,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
		
	КонецЕсли;
	
	Для Каждого Реквизит Из МетаданныеИсточника.Измерения Цикл
		ЗагружаемыйРеквизит                        = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.МожетБытьПолемПоиска = Истина;
		ЗагружаемыйРеквизит.ИмяРеквизита           = Реквизит.Имя;
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = Реквизит.Представление();
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = МетаданныеИсточника.Измерения[ЗагружаемыйРеквизит.ИмяРеквизита].Тип;
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеИсточника.Ресурсы Цикл
		ЗагружаемыйРеквизит                        = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = Реквизит.Имя;
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = Реквизит.Представление();
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = МетаданныеИсточника.Ресурсы[ЗагружаемыйРеквизит.ИмяРеквизита].Тип;
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеИсточника.Реквизиты Цикл
		ЗагружаемыйРеквизит                        = ТаблицаЗагружаемыхРеквизитов.Добавить();
		ЗагружаемыйРеквизит.ИмяРеквизита           = Реквизит.Имя;
		ЗагружаемыйРеквизит.ПредставлениеРеквизита = Реквизит.Представление();
		ЗагружаемыйРеквизит.ДоступноеОписаниеТипов = МетаданныеИсточника.Реквизиты[ЗагружаемыйРеквизит.ИмяРеквизита].Тип;
	КонецЦикла;
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		ЗагружаемыйРеквизит.ОписаниеТипов = ЗагружаемыйРеквизит.ДоступноеОписаниеТипов;
	КонецЦикла;
	
	
	Для Каждого ЗагружаемыйРеквизит Из ТаблицаЗагружаемыхРеквизитов Цикл
		
		СписокВыбора = ПолучитьСписокИменПредставлений(ЗагружаемыйРеквизит);
		ЗагружаемыйРеквизит.ИскатьПо = ?(СписокВыбора.Количество() = 0, "",СписокВыбора[0].Значение);
		
		СписокВыбора = ПолучитьСписокСвязейПоВладельцу(ЗагружаемыйРеквизит);
		ЗагружаемыйРеквизит.СвязьПоВладельцу = ?(СписокВыбора.Количество() = 0, "",СписокВыбора[0].Значение);
		
		СписокВыбора = ПолучитьСписокСвязейПоТипу(ЗагружаемыйРеквизит);
		Если СписокВыбора.Количество() = 0 Тогда
			ЗагружаемыйРеквизит.СвязьПоТипу = "";
			ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 0;
		Иначе
			ЗагружаемыйРеквизит.СвязьПоТипу = СписокВыбора[0].Значение;
			Если Найти(ЗагружаемыйРеквизит.ИмяРеквизита,"3") <> 0 Тогда
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 3;
				
			ИначеЕсли Найти(ЗагружаемыйРеквизит.ИмяРеквизита,"2") <> 0 Тогда
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 2;
				
			Иначе
				
				ЗагружаемыйРеквизит.ЭлементСвязиПоТипу = 1;
				
			КонецЕсли;
			 
			
		КонецЕсли;
		
		ЗагружаемыйРеквизит.ЗначениеПоУмолчанию = ЗагружаемыйРеквизит.ОписаниеТипов.ПривестиЗначение(Неопределено);
		
		ЗагружаемыйРеквизит.РежимЗагрузки = "Искать";
	КонецЦикла;
	
	
КонецПроцедуры // ЗаполнитьНастройкиКолонокРегистраСведений()


////////////////////////////////////////////////////////////////////////////////
//

// Процедура выполняет инициализацию служебных переменных и констант модуля
Процедура Инициализация() Экспорт
	
	МенеджерыОбъектовМетаданных = Новый Структура("Справочники, Перечисления, Документы, ПланыВидовХарактеристик, ПланыСчетов, ПланыВидовРасчета, БизнесПроцессы, Задачи",
	Справочники,
	Перечисления,
	Документы,
	ПланыВидовХарактеристик,
	ПланыСчетов,
	ПланыВидовРасчета,
	БизнесПроцессы,
	Задачи);

	КешМенеджеровПоТипу = Новый Соответствие;

	ПримитивныеТипы = Новый Структура ("Число, Строка, Дата, Булево",
	Тип("Число"), Тип("Строка"), Тип("Дата"), Тип("Булево"));
	
	Если ПерваяСтрокаДанныхТабличногоДокумента < 2 Тогда
		ПерваяСтрокаДанныхТабличногоДокумента = 2;
	КонецЕсли;
	

КонецПроцедуры // Инициализация()

#КонецЕсли

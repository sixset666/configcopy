/////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБМЕНА С ЕАИСТО //
/////////////////////////////////////////

// Формирование структуры параметров доступа к сайту
//
// Возвращаемое значение:
//   <Структура>   - Структура параметров доступа
//
Функция СформироватьСтруктуруДоступа()
	СтруктураДоступа=Новый Структура;
	
	СтруктураДоступа.Вставить("АдресСервера",АдресСервера);
	СтруктураДоступа.Вставить("Логин",Логин);
	СтруктураДоступа.Вставить("Пароль",Пароль);
	СтруктураДоступа.Вставить("ФамилияЭксперта",ФамилияЭксперта);
	СтруктураДоступа.Вставить("ИмяЭксперта",ИмяЭксперта);
	СтруктураДоступа.Вставить("ОтчествоЭксперт",ОтчествоЭксперт);
	СтруктураДоступа.Вставить("НаименованиеОрганизации",НаименованиеОрганизации);
	СтруктураДоступа.Вставить("КраткоеНаименованиеОрганизации",КраткоеНаименованиеОрганизации);
	
	Возврат СтруктураДоступа;
КонецФункции // СформироватьСтруктуруДоступа() 

// текст запроса
Функция ПолучитьТестЗапросаRegisterCard(ДокументСсылка,ВторичныйКонтроль,Изменение=Ложь) Экспорт
	СтруктураДоступа=СформироватьСтруктуруДоступа();
	Возврат зфЗащищенныеФункцииСервер.ПолучитьТестЗапросаRegisterCard(СтруктураДоступа,ДокументСсылка,ВторичныйКонтроль,Изменение,НеПередоватьДатуПриСозданииКарточки);
КонецФункции

// текст запроса 
Функция ПолучитьТестЗапросаGetCardByVin(ВидПроверки,VIN,ГосНомер,НомерКузова,НомерКарточки) Экспорт
	СтруктураДоступа=СформироватьСтруктуруДоступа();
	Возврат зфЗащищенныеФункцииСервер.ПолучитьТестЗапросаGetCardByVin(СтруктураДоступа,ВидПроверки,VIN,ГосНомер,НомерКузова,НомерКарточки);
КонецФункции

// текст запроса дубликат
Функция ПолучитьТекстЗапросаDublicate(СтруктураДанных) Экспорт
	Шаблон =
	"<?xml version=""1.0"" encoding=""UTF-8""?>
	|<soapenv:Envelope xmlns:tec=""http://schemas.datacontract.org/2004/07/TechInspection.Domain.DataContracts"" xmlns:tem=""http://tempuri.org/"" xmlns:soapenv=""http://schemas.xmlsoap.org/soap/envelope/"">
	|  <soapenv:Header/>
	|  <soapenv:Body>
	|	<tem:RegisterCard>
	|	  <tem:user>
	|		<tec:Name>#Логин#</tec:Name>
	|		<tec:Password>#Пароль#</tec:Password>
	|		<tec:ExtSystem>ALFA-RARUS</tec:ExtSystem>
	|	  </tem:user>
	|	  <tem:card>
	|		<tec:CardIsDublicateFor>#IDКарточкиДублирования#</tec:CardIsDublicateFor>
	|		<tem:Form>
	|			<tem:Comment>Дата</tem:Comment>
	|			<tem:Duplicate>true</tem:Duplicate>
	|			<tem:Validity>#ДействуетДо#</tem:Validity>
	|		</tem:Form>
	|		<tem:Expert>
	|			<tem:Name>#ЭкспертИмя#</tem:Name>
	|			<tem:FName>#ЭкспертФамилия#</tem:FName>
	|			<tem:MName>#ЭкспертОтчество#</tem:MName>
	|		</tem:Expert>
	|	  </tem:card>
	|	</tem:RegisterCard>
	|  </soapenv:Body>
	|</soapenv:Envelope>";
	
	
		// логин пароль
	Шаблон = СтрЗаменить(Шаблон,"#Логин#",Логин);
	Шаблон = СтрЗаменить(Шаблон,"#Пароль#",Пароль);
	Если обЗначениеНеЗаполнено(СтруктураДанных.ДокументОснование) Тогда
		ID = СокрЛП(СтруктураДанных.IDДублирования);
	Иначе
		ID = РегистрыСведений.ЖурналОтправкиВЕАИСТО.СоздатьНаборЗаписей().ПолучитьID(СтруктураДанных.ДокументОснование);
	КонецЕсли;
	Шаблон = СтрЗаменить(Шаблон,"#IDКарточкиДублирования#",ID);
	Если НЕ обЗначениеНеЗаполнено(СтруктураДанных.СрокОкончанияДействияТО) Тогда
		Шаблон = СтрЗаменить(Шаблон,"#ДействуетДо#",Формат(СтруктураДанных.СрокОкончанияДействияТО,"ДФ=yyyy-MM-ddThh:mm:ss"));
	Иначе
		Шаблон = СтрЗаменить(Шаблон,"#ДействуетДо#","");
	КонецЕсли;
	
	Шаблон = СтрЗаменить(Шаблон,"#ЭкспертИмя#",ИмяЭксперта);
	Шаблон = СтрЗаменить(Шаблон,"#ЭкспертФамилия#",ФамилияЭксперта);
	Шаблон = СтрЗаменить(Шаблон,"#ЭкспертОтчество#",ОтчествоЭксперт);
	Возврат Шаблон;
КонецФункции

// отправка запроса на сервер
Функция ПослатьЗапросСерверу(ТекстЗапроса,ДокументСсылка) Экспорт
	ОтветСервера = Новый Структура("Текст,ОшибкаСервера,ГуидОтпрвки");

	Попытка
		xmlhttp = Новый COMОбъект("Microsoft.XMLHTTP");
	Исключение
		ОтветСервера.Текст         = "Не удалось установить соединение с сервером.("+ОписаниеОшибки()+")";
		ОтветСервера.ОшибкаСервера = Истина;
		ОтветСервера.ГуидОтпрвки   = "";
		Возврат ОтветСервера;
	КонецПопытки;
	
	xmlhttp.open("POST",АдресСервера,0);
	xmlhttp.setRequestHeader("Content-Type", "text/xml");
	
	Попытка
		Guid = зфЗащищенныеФункцииСервер.ДобавитьЗаписьВЖурналОтправок(ДокументСсылка,ТекстЗапроса,Перечисления.ТипыОперацийПередачи.ОтправкаНаСервер);
		xmlhttp.send(ТекстЗапроса);
	Исключение
		ОтветСервера.Текст         = ОписаниеОшибки();
		ОтветСервера.ОшибкаСервера = Истина;
		ОтветСервера.ГуидОтпрвки   = Guid;
		Возврат ОтветСервера;
	КонецПопытки;
	
	ОтветСервера.Текст         = xmlhttp.responseText;
	ОтветСервера.ОшибкаСервера = Ложь;
	ОтветСервера.ГуидОтпрвки   = Guid;

	Возврат ОтветСервера;
КонецФункции

// функция разбора ответа от сервера
Функция РазобратьОтветСервера(Ответ,ДокументСсылка=Неопределено) Экспорт
	Возврат зфЗащищенныеФункцииСервер.РазобратьОтветСервера(Ответ,ДокументСсылка);
	//Возврат Обработки.ЗащитаТехосмотр.Создать().РазобратьОтветСервера(Ответ,ДокументСсылка);
КонецФункции

Функция ВыполнитьОбменПоРегламентномуЗаданию(СтруктураПараметров) Экспорт
	Результат = Новый Структура("Ошибка,ТекстОшибки",Ложь,"");
	// проверим всели параметры имеются
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда
		Результат.Ошибка      = Истина;
		Результат.ТекстОшибки = "Неуказаны параметры.";
		Возврат Результат;
	КонецЕсли;
	
	СписокНаПроверку = Новый Структура("пвЛогин,пвПароль,пвИмя,пвФамилия");
	СтрокаОшибок = "";
	Для Каждого Элемент Из СписокНаПроверку Цикл
		ВремЗначение = Неопределено;
		Если СтруктураПараметров.Свойство(Элемент.Ключ,ВремЗначение) И НЕ обЗначениеНеЗаполнено(ВремЗначение) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаОшибок = СтрокаОшибок + ?(ПустаяСтрока(СтрокаОшибок),"",", ") + Элемент.Ключ;
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(СтрокаОшибок) Тогда
		Результат.Ошибка      = Истина;
		Результат.ТекстОшибки = "Не указаны параметры: "+СтрокаОшибок;
		Возврат Результат;
	КонецЕсли;
	
	// получим дополнительные параметры запроса
	СписокДействий = Новый СписокЗначений;
	ВремЗначение = Неопределено;
	Если СтруктураПараметров.Свойство("флСоздатьНовый",ВремЗначение) Тогда
		Если ВремЗначение = Неопределено Тогда
			ВремЗначение = Ложь;
		КонецЕсли;
		Если ВремЗначение Тогда
			СписокДействий.Добавить(Перечисления.ДействияЕАИСТО.Добавить);
		КонецЕсли;
	КонецЕсли;
	ВремЗначение = Неопределено;
	Если СтруктураПараметров.Свойство("флИзменять",ВремЗначение) Тогда
		Если ВремЗначение = Неопределено Тогда
			ВремЗначение = Ложь;
		КонецЕсли;
		Если ВремЗначение Тогда
			СписокДействий.Добавить(Перечисления.ДействияЕАИСТО.Изменить);
		КонецЕсли;
	КонецЕсли;
	
	
	Сообщить("Получение списка диагностических карт для отправки...");
	// получим список документов
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЖурналОтправкиВЕАИСТО.Документ,
	|	ЖурналОтправкиВЕАИСТО.Документ.ХозОперация,
	|	ЖурналОтправкиВЕАИСТО.Действие
	|ИЗ
	|	РегистрСведений.ЖурналОтправкиВЕАИСТО КАК ЖурналОтправкиВЕАИСТО
	|ГДЕ
	|	ЖурналОтправкиВЕАИСТО.Статус = &Статус
	|	И ЖурналОтправкиВЕАИСТО.Действие В(&Действия)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЖурналОтправкиВЕАИСТО.Документ.МоментВремени";
	Запрос.УстановитьПараметр("Статус"   , Перечисления.СтатусыДокументаТО.Отправить);
	Запрос.УстановитьПараметр("Действия" , СписокДействий);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// заполним параметры пвЛогин,пвПароль,пвИмя,пвФамилия
	Логин           = СтруктураПараметров.пвЛогин;
	Пароль          = СтруктураПараметров.пвПароль;
	ИмяЭксперта     = СтруктураПараметров.пвИмя;
	ФамилияЭксперта = СтруктураПараметров.пвФамилия;
	
	
	Сообщить("Старт отправки диагностических карт на сервер...");
	Сообщить("----------------------------------------------------");
	Пока Выборка.Следующий() Цикл
		Пакет            = ПолучитьТестЗапросаRegisterCard(Выборка.Документ,Выборка.ДокументХозОперация = Справочники.ХозОперации.ТехническоеДиагностированиеПовторное,Выборка.Действие=Перечисления.ДействияЕАИСТО.Изменить);
		Ответ            = ПослатьЗапросСерверу(Пакет,Выборка.Документ);
		Ошибка = (Найти(Ответ,"faultcode") > 0);
		ОтветРазобранный = РазобратьОтветСервера(Ответ,Выборка.Документ);
		Сообщить(ОтветРазобранный,?(Ошибка,СтатусСообщения.Важное,СтатусСообщения.Информация));
	КонецЦикла;
	Сообщить("----------------------------------------------------");
	Сообщить("Окончание отправки диагностических карт на сервер...");
	
	Возврат Результат;
КонецФункции

Функция СоздатьДубликат(Основание) Экспорт
	Ошибки = "";
	ДокументДубликат = Документы.ТехническоеДиагностирование.СоздатьДокумент();
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		дкОбработкаЗаполненияПоУмолчанию(ДокументДубликат,,,Ложь);
		// заполняем по строкам
		ДокументДубликат.УстановитьНовыйНомер();
		ДокументДубликат.ХозОперация = Справочники.ХозОперации.ТехническоеДиагностированиеДубликат;
		ДокументДубликат.IDДублирования = Основание.ID;
		ДокументДубликат.Автомобиль = Основание.Модель + " " + Основание.Vin + " " + Основание.ГосНомер;
		ДокументДубликат.КатегорияТС = Основание.КатегорияТС2;
		//ДокументДубликат.СрокОкончанияДействияТО = ДатаГодности;
		
		// заполним ТЧ
		ДокументДубликат.Требования.Очистить();
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ТребованияКТранспортнымСредствамДиапазонЗначений.Ссылка КАК ТребованиеКТранспортномуСредству,
		|	ТребованияКТранспортнымСредствамДиапазонЗначений.ЗначениеМин КАК ЗначениеМин,
		|	ТребованияКТранспортнымСредствамДиапазонЗначений.ЗначениеМакс КАК ЗначениеМакс,
		|	ТребованияКТранспортнымСредствамДиапазонЗначений.Ссылка.ЗначениеМин КАК ЗначениеМинПоУмолчанию,
		|	ТребованияКТранспортнымСредствамДиапазонЗначений.Ссылка.ЗначениеМакс КАК ЗначениеМаксПоУмолчанию
		|ИЗ
		|	Справочник.ТребованияКТранспортнымСредствам.ДиапазонЗначений КАК ТребованияКТранспортнымСредствамДиапазонЗначений
		|ГДЕ
		|	ТребованияКТранспортнымСредствамДиапазонЗначений.КатегорияТранспортногоСредства.КодПоКлассификатору = &КатегорияТранспортногоСредства
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТребованиеКТранспортномуСредству";
		Запрос.УстановитьПараметр("КатегорияТранспортногоСредства",Основание.КатегорияТС2);
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НовоеТребование=ДокументДубликат.Требования.Добавить();
			НовоеТребование.ТребованиеКТранспортномуСредству=Выборка.ТребованиеКТранспортномуСредству;
			НовоеТребование.Выполнено=Истина;
			НовоеТребование.Значение=0;
			Если обЗначениеНеЗаполнено(Выборка.ЗначениеМин) Тогда
				НовоеТребование.ЗначениеМин=Выборка.ЗначениеМинПоУмолчанию;
			Иначе
				НовоеТребование.ЗначениеМин=Выборка.ЗначениеМин;
			КонецЕсли; 
			Если обЗначениеНеЗаполнено(Выборка.ЗначениеМакс) Тогда
				НовоеТребование.ЗначениеМакс=Выборка.ЗначениеМаксПоУмолчанию;
			Иначе
				НовоеТребование.ЗначениеМакс=Выборка.ЗначениеМакс;
			КонецЕсли; 
			НовоеТребование.ВыполненоНаПервичном=Ложь;
		КонецЦикла;
	Иначе
		ДокументДубликат.Заполнить(Основание);
	КонецЕсли;
	
	Попытка
		ДокументДубликат.Записать(РежимЗаписиДокумента.Проведение);
		ДокументДубликат.ДобавитьВСписокОтправкиВЕАИСТО(ДокументДубликат);
		Ошибки = Строка(ДокументДубликат.Ссылка) + "был успешно создан.";
	Исключение
		Ошибки = ИнформацияОбОшибке();
	КонецПопытки;
	
	Возврат Ошибки;
КонецФункции

///////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ //
///////////////////////////////////////////

// Заполняется список документов готовых к отправке
//Параметры:
//	ЗначениеОтбора - строка поиска подходящих значений
//
Процедура ЗаполнитьСписокКарточек(ЗначениеОтбора) Экспорт
	СписокСтатусов = Новый СписокЗначений;
	СписокДокументовТО.Очистить();
	
	// посмотрим поиск по статусам
	Если ПоказыватьОтправленные Тогда
		СписокСтатусов.Добавить(Перечисления.СтатусыДокументаТО.Отправлено);
	КонецЕсли;
	Если ПоказыватьГотовыеКОтправке Тогда
		СписокСтатусов.Добавить(Перечисления.СтатусыДокументаТО.Отправить);
	КонецЕсли;
	
	Если СписокСтатусов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// составим кусочки запроса
	
	// запрос по статусам
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЖурналОтправкиВЕАИСТО.Документ,
	|	ЖурналОтправкиВЕАИСТО.Статус
	|ИЗ
	|	РегистрСведений.ЖурналОтправкиВЕАИСТО КАК ЖурналОтправкиВЕАИСТО
	|ГДЕ
	|	ЖурналОтправкиВЕАИСТО.Статус В(&СпискеСтатусов)
	|	И ЖурналОтправкиВЕАИСТО.Документ.Проведен = ИСТИНА
	|	И ЖурналОтправкиВЕАИСТО.Документ.ПометкаУдаления = ЛОЖЬ
	|	И ЖурналОтправкиВЕАИСТО.Документ.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналОтправкиВЕАИСТО.Статус,
	|	ЖурналОтправкиВЕАИСТО.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЖурналОтправкиВЕАИСТО.Документ.Дата УБЫВ";
	Запрос.УстановитьПараметр("СпискеСтатусов" , СписокСтатусов);
	Запрос.УстановитьПараметр("ДатаНач"        , ?(обЗначениеНеЗаполнено(ДатаНач),Дата("00010101"),ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон"        , ?(обЗначениеНеЗаполнено(ДатаКон),КонецДня(ТекущаяДата()),ДатаКон));
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	
	
	
	// запрос по клиентам
	ЗапросКлиенты =
	"ВЫБРАТЬ
	|	ТехническоеДиагностирование.Ссылка КАК Документ
	|ИЗ
	|	Документ.ТехническоеДиагностирование КАК ТехническоеДиагностирование
	|ГДЕ
	|	ТехническоеДиагностирование.Контрагент.Наименование ПОДОБНО ""%"" + &ЗначениеОтбора + ""%""
	|	И ТехническоеДиагностирование.Ссылка В(&Список)";
	
	// запрос представитель
	ЗапросПредставитель =
	"ВЫБРАТЬ
	|	ТехническоеДиагностирование.Ссылка КАК Документ
	|ИЗ
	|	Документ.ТехническоеДиагностирование КАК ТехническоеДиагностирование
	|ГДЕ
	|	ТехническоеДиагностирование.ПредставительКонтрагента.Наименование ПОДОБНО ""%"" + &ЗначениеОтбора + ""%""
	|	И ТехническоеДиагностирование.Ссылка В(&Список)";
	
	// запрос vin номер
	ЗапросVIN =
	"ВЫБРАТЬ
	|	ТехническоеДиагностирование.Ссылка КАК Документ
	|ИЗ
	|	Документ.ТехническоеДиагностирование КАК ТехническоеДиагностирование
	|ГДЕ
	|	ТехническоеДиагностирование.Автомобиль.VIN ПОДОБНО ""%"" + &ЗначениеОтбора + ""%""
	|	И ТехническоеДиагностирование.Ссылка В(&Список)";
	
	// запрос модель
	ЗапросМодель =
	"ВЫБРАТЬ
	|	ТехническоеДиагностирование.Ссылка КАК Документ
	|ИЗ
	|	Документ.ТехническоеДиагностирование КАК ТехническоеДиагностирование
	|ГДЕ
	|	ТехническоеДиагностирование.Автомобиль.Модель.Наименование ПОДОБНО ""%"" + &ЗначениеОтбора + ""%""
	|	И ТехническоеДиагностирование.Ссылка В(&Список)";
	
	// запрос КИ
	ЗапросКИ =
	"ВЫБРАТЬ
	|	ТехническоеДиагностирование.Ссылка КАК Документ
	|ИЗ
	|	Документ.ТехническоеДиагностирование КАК ТехническоеДиагностирование
	|ГДЕ
	|	(ТехническоеДиагностирование.Контрагент В
	|				(ВЫБРАТЬ
	|					КонтактнаяИнформация.Объект
	|				ИЗ
	|					РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|				ГДЕ
	|					КонтактнаяИнформация.Представление ПОДОБНО ""%"" + &ЗначениеОтбора + ""%"")
	|			ИЛИ ТехническоеДиагностирование.ПредставительКонтрагента В
	|				(ВЫБРАТЬ
	|					КонтактнаяИнформация.Объект
	|				ИЗ
	|					РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|				ГДЕ
	|					КонтактнаяИнформация.Представление ПОДОБНО ""%"" + &ЗначениеОтбора + ""%""))
	|			И ТехническоеДиагностирование.Ссылка В(&Список)";
	
	// запрос объединение
	ЗапросОбъединить =
	"
	|ОБЪЕДИНИТЬ
	|";
	
	// запрос контейнер
	ЗапросКонтейнер =
	"ВЫБРАТЬ
	|	Документы.Документ КАК Документ
	|ИЗ
	|	(&СоставнойЗапрос) КАК Документы";
	
	
	ТаблицаДопДокументов = Неопределено;
	// составим запрос
	ЭтоПервыйФрагмент = Истина;
	ЗапросПустой = Истина;
	ТекстЗапроса = "";
	Для Каждого Настройка Из СтруктураПоиска Цикл
		Если Настройка.Значение Тогда
			Если НЕ ЭтоПервыйФрагмент Тогда
				ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить;
			КонецЕсли;
			Если Настройка.Ключ = "НаименованиеКлиента" Тогда
				ЗапросПустой = Ложь; ЭтоПервыйФрагмент = Ложь;
				ТекстЗапроса = ТекстЗапроса + ЗапросКлиенты;
			ИначеЕсли Настройка.Ключ = "КонтактнаяИнформация" Тогда
				ЗапросПустой = Ложь; ЭтоПервыйФрагмент = Ложь;
				ТекстЗапроса = ТекстЗапроса + ЗапросКИ;
			ИначеЕсли Настройка.Ключ = "Модель" Тогда
				ЗапросПустой = Ложь; ЭтоПервыйФрагмент = Ложь;
				ТекстЗапроса = ТекстЗапроса + ЗапросМодель;
			ИначеЕсли Настройка.Ключ = "VIN" Тогда
				ЗапросПустой = Ложь; ЭтоПервыйФрагмент = Ложь;
				ТекстЗапроса = ТекстЗапроса + ЗапросVIN;
			ИначеЕсли Настройка.Ключ = "НаименованиеПредставителяКонтрагента" Тогда
				ЗапросПустой = Ложь; ЭтоПервыйФрагмент = Ложь;
				ТекстЗапроса = ТекстЗапроса + ЗапросПредставитель;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЗапросПустой И НЕ ПустаяСтрока(ЗначениеОтбора) Тогда
		ЗапросКонтейнер = СтрЗаменить(ЗапросКонтейнер,"&СоставнойЗапрос",ТекстЗапроса);
		Запрос.Текст = ЗапросКонтейнер;
		Запрос.УстановитьПараметр("ЗначениеОтбора" , ЗначениеОтбора);
		Запрос.УстановитьПараметр("Список"         , ТаблицаДокументов.ВыгрузитьКолонку("Документ"));
		
		ТаблицаДопДокументов = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Если ТаблицаДопДокументов = Неопределено Тогда
		Для Каждого СтрокаДок Из ТаблицаДокументов Цикл
			НоваяСтрока = СписокДокументовТО.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаДок.Документ);
			НоваяСтрока.Ссылка = СтрокаДок.Документ;
			НоваяСтрока.Статус = СтрокаДок.Статус;
		КонецЦикла;
	Иначе
		Для Каждого СтрокаДок Из ТаблицаДокументов Цикл
			Если ТаблицаДопДокументов.НайтиСтроки(Новый Структура("Документ",СтрокаДок.Документ)).Количество() > 0 Тогда
				НоваяСтрока = СписокДокументовТО.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаДок.Документ);
				НоваяСтрока.Ссылка = СтрокаДок.Документ;
				НоваяСтрока.Статус = СтрокаДок.Статус;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

АдресСервера = обПраво("ВебСервисЕАИСТО");

//Тестовый сервис
//АдресСервера = "http://eaisto.mvd.dev.armd.ru/common/ws/arm_expert.php?wsdl";
//Боевой сервис
//АдресСервера = "http://eaisto.gibdd.ru/common/ws/arm_expert.php?wsdl";
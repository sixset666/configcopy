#Если Клиент Тогда

Перем Рарус_Компонента Экспорт; // Компонента рарус компонента

// сворачивает по новой строке
Процедура СвернутьПоНовойСтроке(НоваяСтрока) Экспорт
	
	Если обЗначениеНеЗаполнено(НоваяСтрока.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	// подготовим структуру отбора для поиска товара в ТЧ
	СтруктураОтбора=Новый Структура("Номенклатура",НоваяСтрока.Номенклатура);
    СтруктураОтбора.Вставить("ЕдиницаИзмерения",НоваяСтрока.ЕдиницаИзмерения);
	СтруктураОтбора.Вставить("Характеристика",НоваяСтрока.Характеристика);
	СтруктураОтбора.Вставить("Сумма",НоваяСтрока.Сумма);
	СтруктураОтбора.Вставить("ШтрихКод",НоваяСтрока.ШтрихКод);
	СтруктураОтбора.Вставить("Скидка",НоваяСтрока.Скидка);
	
	// проверим есть ли в ТЧ хоть одна строка, удовлетворяющая нашим условиям
	МассивСтрок = Товары.НайтиСтроки(СтруктураОтбора);
	Если МассивСтрок.Количество() > 1 Тогда
		Для Каждого СтрокаТЧ Из МассивСтрок Цикл
			Если СтрокаТЧ.НомерСтроки<>НоваяСтрока.НомерСтроки Тогда
				СтрокаТЧ.Количество = СтрокаТЧ.Количество + НоваяСтрока.Количество;
				Товары.Удалить(НоваяСтрока);
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// КОМАНДНАЯ ПАНЕЛЬ
////////////////////////////////////////////////////////////////////////////////

// Заполнение табличной части
Процедура ЗаполнитьТЧ(ЭтаФорма, ТипЗаполнения) Экспорт
	
	// проверим выбор типа цен
	Если обЗначениеНеЗаполнено(ТипЦен) Тогда
		Предупреждение("Не выбран тип цен!",10);
		Возврат;
	КонецЕсли;
	// спросим про очистку.
	Если Товары.Количество()>0 И Вопрос("Таблица перед заполнением будет очищена!
		|Продолжить?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Заполнение")<>КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	// чистим
	Товары.Очистить();
	
	// если заполнение связано с группой, то выберем группу
	Если ТипЗаполнения="ЗаполнитьПоГруппе" ИЛИ ТипЗаполнения="ЗаполнитьСкладскимиОстаткамиПоГруппе" Тогда
		ФормаГрупп=Справочники.Номенклатура.ПолучитьФормуВыбораГруппы();
		ФормаГрупп.МножественныйВыбор=Ложь;
		ФормаГрупп.РежимВыбора=Истина;
		ФормаГрупп.ЗакрыватьПриВыборе=Истина;
		ТекГруппа=ФормаГрупп.ОткрытьМодально();
		Если ТекГруппа=Неопределено Тогда Возврат; КонецЕсли;
	КонецЕсли;
	// если заполнение связано с указанием склада, то выберем склады
	Если ТипЗаполнения="ЗаполнитьСкладскимиОстаткамиПоГруппе" ИЛИ ТипЗаполнения="ЗаполнитьСкладскимиОстатками" Тогда
		ФормаВыбораСклада=Справочники.СкладыКомпании.ПолучитьФормуВыбора();
		ФормаВыбораСклада.МножественныйВыбор=Истина;
		ФормаВыбораСклада.РежимВыбора=Истина;
		ФормаВыбораСклада.ЗакрыватьПриВыборе=Истина;
		ВыбСклады=ФормаВыбораСклада.ОткрытьМодально();
		Если ВыбСклады=Неопределено ИЛИ ВыбСклады.Количество()=0 Тогда Возврат; КонецЕсли;
	КонецЕсли;
	
	// CASE по действиям
	Если ТипЗаполнения="ВсяНоменклатура" ИЛИ ТипЗаполнения="ЗаполнитьПоГруппе" Тогда
		// заполняем по списку товаров
		Запрос=Новый Запрос("
		|ВЫБРАТЬ 
		|	СпрНоменклатура.Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|ГДЕ
		|	НЕ СпрНоменклатура.ЭтоГруппа И 
		|	НЕ СпрНоменклатура.ПометкаУдаления И 
		|	НЕ СпрНоменклатура.ВидНоменклатуры В (&ВидыНоменклатуры)
		|	"+?(ТипЗаполнения="ЗаполнитьПоГруппе","И СпрНоменклатура.Ссылка В ИЕРАРХИИ(&ТекГруппа)",""));
		Запрос.УстановитьПараметр("ТекГруппа",ТекГруппа);
		ВидыНоменклатуры=Новый Массив;
		ВидыНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы);
		ВидыНоменклатуры.Добавить(Перечисления.ВидыНоменклатуры.Услуга);
		Запрос.УстановитьПараметр("ВидыНоменклатуры", ВидыНоменклатуры);
		Товары.Загрузить(Запрос.Выполнить().Выгрузить());
		
	ИначеЕсли ТипЗаполнения="ЗаполнитьСкладскимиОстаткамиПоГруппе" ИЛИ ТипЗаполнения="ЗаполнитьСкладскимиОстатками" Тогда
		// заполняем по остаткам
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
		|	ИСТИНА КАК Выбран
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,СкладКомпании В (&ВыбСклады) "+?(ТипЗаполнения="ЗаполнитьСкладскимиОстаткамиПоГруппе","И Номенклатура В ИЕРАРХИИ(&ТекГруппа)","")+") КАК ОстаткиТоваровКомпанииОстатки
		|");
		Запрос.УстановитьПараметр("ТекГруппа",ТекГруппа);
		Запрос.УстановитьПараметр("ВыбСклады",ВыбСклады);
		Товары.Загрузить(Запрос.Выполнить().Выгрузить());
		
	ИначеЕсли ТипЗаполнения="ЗаполнитьПоГруппеОборудования" Тогда

		
	ИначеЕсли ТипЗаполнения="ЗаполнитьИзДокументов" Тогда
		// выберем вид документа
		СписокВидовДокументов=Новый СписокЗначений();
		СписокВидовДокументов.Добавить("ИзменениеЦен","Изменение цен");
		СписокВидовДокументов.Добавить("Инвентаризация","Инвентаризация");
		СписокВидовДокументов.Добавить("ПеремещениеТоваров","Перемещение товаров");
		СписокВидовДокументов.Добавить("ПоступлениеТоваров","Поступление товаров");
		СписокВидовДокументов.Добавить("АвансовыйОтчет","Авансовый отчет");
		СписокВидовДокументов.Добавить("ВводОстатковТоваров","Ввод остатков товаров");
		СписокВидовДокументов.Добавить("ЗаказКодовМаркировки", "Заказ кодов маркировки");
		ВыбранныйЭлемент=СписокВидовДокументов.ВыбратьЭлемент("Выберите вид документа");
		Если ВыбранныйЭлемент=Неопределено Тогда Возврат; КонецЕсли;
		ВидДок=ВыбранныйЭлемент.Значение;
		
		// выберем документы
		ВыбДокументы=Неопределено;
		ФормаСписка=Документы[ВидДок].ПолучитьФормуСписка();
		ФормаСписка.РежимВыбора=Истина;
		ФормаСписка.МножественныйВыбор=Истина;
		ФормаСписка.ЗакрыватьПриВыборе=Истина;
		Если ФормаСписка.Открыта() Тогда // Форма уже кем-то открыта
			Предупреждение("Закройте форму списка документа """ + ВыбранныйЭлемент.Представление + """");
			Возврат;
		КонецЕсли;
		ВыбДокументы=ФормаСписка.ОткрытьМодально();
		Если ВыбДокументы=Неопределено Тогда Возврат; КонецЕсли;
		Если ВыбДокументы.Количество() > 0 Тогда
			ВыбДокумент = ВыбДокументы[0];
		Иначе
			Возврат; 
		КонецЕсли;
		
		СтрДопЦена = "";
		Если (ВидДок = "ПеремещениеТоваров") ИЛИ (ВидДок = "ПеремещениеТоваров") Тогда
			СтрДопЦена="Розничная";
		ИначеЕсли ВидДок = "Инвентаризация" Тогда
			СтрДопЦена="Отпускная";
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		ИмяРеквизитаЦена = "Цена" + СтрДопЦена;
		ИмяТабличнойЧасти = "Товары";
		Если ВидДок = "ИзменениеЦен" Тогда ИмяРеквизитаКоличество = Неопределено КонецЕсли;
		
		СтруктураПараметров.Вставить("ИмяТабличнойЧасти",ИмяТабличнойЧасти);
		СтруктураПараметров.Вставить("ИмяРеквизитаЦена",ИмяРеквизитаЦена);
		СтруктураПараметров.Вставить("ИмяРеквизитаКоличество",ИмяРеквизитаКоличество);
		СтруктураПараметров.Вставить("ТипЦен",ТипЦен);

		ЗаполнитьПоДокументу(ВыбДокумент,СтруктураПараметров);
		
	ИначеЕсли ТипЗаполнения="ЗаполнитьУслугами" Тогда
		 Запрос=Новый Запрос("
		|ВЫБРАТЬ 
		|	СпрНоменклатура.Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|ГДЕ
		|	НЕ СпрНоменклатура.ЭтоГруппа И 
		|	НЕ СпрНоменклатура.ПометкаУдаления И 
		|	СпрНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)");
		Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	ИначеЕсли ТипЗаполнения="ВсеАктивы" Тогда
		 Запрос=Новый Запрос("ВЫБРАТЬ
		                     |	ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив КАК Номенклатура
		                     |ИЗ
		                     |	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки КАК ПрочиеАктивыВЭксплуатацииОстатки
		                     |ГДЕ
		                     |	НЕ ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ЭтоГруппа
		                     |	И НЕ ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ПометкаУдаления");
	
		Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
	// заполним реквизиты в зависимости от Номенклатуры
	Если ТипЗаполнения<>"ЗаполнитьИзДокументов" Тогда		
		Для Каждого ТабличнаяСтрока Из Товары Цикл			
			ОбработкаРеквизита("Номенклатура", ТабличнаяСтрока,ЭтаФорма);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

//Заполнение ТЧ обработки по ТЧ документа
Функция ЗаполнитьПоДокументу(ДокументЗаполнения,СтруктураПараметров=Неопределено) Экспорт
    // Проверим является ли "ДокументЗаполнения" документом с хоз. операцией "ВводВЭксплуатацию".
    ВводВЭксплуатацию = ТипЗнч(ДокументЗаполнения) = Тип("ДокументОбъект.ВводВЭксплуатацию");
	ВводВЭксплуатациюАвтомобилей = ТипЗнч(ДокументЗаполнения) = Тип("ДокументОбъект.ВводВЭксплуатациюАвтомобилей");
	
	ЗаказКодовМаркировки = ТипЗнч(ДокументЗаполнения) = Тип("ДокументОбъект.ЗаказКодовМаркировки") ИЛИ ТипЗнч(ДокументЗаполнения) = Тип("ДокументСсылка.ЗаказКодовМаркировки");
	
    // установим параметры по умолчанию
	ИмяТабличнойЧасти="Товары";
	ИмяРеквизитаХарактеристика="ХарактеристикаНоменклатуры";
	ИмяРеквизитаЦена="Цена";
	ИмяРеквизитаКоличество="Количество";
	Попытка
		ТипЦен=ДокументЗаполнения.ТипЦен;
	Исключение
		Попытка
			ДокументОснование=ДокументЗаполнения.ДокументОснование;
			Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
				ТипЦен=ДокументОснование.ТипЦен;
			Иначе
				ТипЦен=обПраво("ОсновнойТипЦенПродажи",глПрава);
			КонецЕсли; 
		Исключение
			ТипЦен=обПраво("ОсновнойТипЦенПродажи",глПрава);
		КонецПопытки; 
	КонецПопытки; 
	
	Попытка
		ТипЦенРабот = ДокументЗаполнения.ТипЦенРабот;
	Исключение
		Попытка
			ДокументОснование = ДокументЗаполнения.ДокументОснование;
			Если ЗначениеЗаполнено(ДокументОснование) Тогда
				ТипЦенРабот = ДокументОснование.ТипЦенРабот;
			Иначе
				ТипЦенРабот = обПраво("ОсновнойТипЦенПродажи", глПрава);
			КонецЕсли;
		Исключение
			ТипЦенРабот = обПраво("ОсновнойТипЦенРабот",глПрава);
			Если НЕ ТипЦенРабот.ДляРабот Тогда
				ТипЦенРабот = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
			КонецЕсли;
		КонецПопытки;
	КонецПопытки;
	
	Попытка
		ПодразделениеКомпании=ДокументОснование.ПодразделениеКомпании;
	Исключение
		Попытка
			ПодразделениеКомпании=ДокументОснование.Подразделение;
		Исключение
		КонецПопытки; 
	КонецПопытки; 
	Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		ПодразделениеКомпании=ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли; 
	ДатаЦены = ДокументЗаполнения.Дата;
	
	// проверим переданные параметры
	Если ТипЗнч(СтруктураПараметров)=Тип("Структура") И СтруктураПараметров.Количество()>0 Тогда
		// получим переданные параметры
		ВремЗначение=Неопределено; СтруктураПараметров.Свойство("ИмяТабличнойЧасти",ВремЗначение);
		Если ВремЗначение<>Неопределено Тогда ИмяТабличнойЧасти=ВремЗначение; КонецЕсли;
		
		ВремЗначение=Неопределено; СтруктураПараметров.Свойство("ИмяРеквизитаХарактеристика",ВремЗначение);
		Если ВремЗначение<>Неопределено Тогда ИмяРеквизитаХарактеристика=ВремЗначение; КонецЕсли;
		
		ВремЗначение=Неопределено; СтруктураПараметров.Свойство("ИмяРеквизитаЦена",ВремЗначение);
		Если ВремЗначение<>Неопределено Тогда ИмяРеквизитаЦена=ВремЗначение; КонецЕсли;
		
		ВремЗначение=Неопределено; СтруктураПараметров.Свойство("ИмяРеквизитаКоличество",ВремЗначение);
		Если ВремЗначение<>Неопределено Тогда ИмяРеквизитаКоличество=ВремЗначение; КонецЕсли;
		
		ВремЗначение=Неопределено; СтруктураПараметров.Свойство("ТипЦен",ВремЗначение);
		Если ВремЗначение<>Неопределено Тогда ТипЦен=ВремЗначение; КонецЕсли;
	КонецЕсли;
	
	ТабличнаяЧастьОбъекта=ДокументЗаполнения[ИмяТабличнойЧасти];
	
	//Проверим возможность документа двигать регистр цен
	МетаданныеДокумента = ДокументЗаполнения.Метаданные();
	ДокументДвигаетРегистрЦен=Ложь;
	Для Каждого Регистр Из МетаданныеДокумента.Движения Цикл
		Если Регистр.Имя="Цены" Тогда
			ДокументДвигаетРегистрЦен=Истина; Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	// не будем спрашивать, а загрузим сразу весь документ
	// но для будущего оставим
	
	// спросим что печатаем
	//ФормаВыбора=ПолучитьФорму("ВыборВариантаПечати");
	//ФормаВыбора.ДокументДвигаетРегистрЦен=ДокументДвигаетРегистрЦен;
	//РезультатВыбора=ФормаВыбора.ОткрытьМодально();
	РезультатВыбора = "ВесьДокумент";
	
	// смотрим, что мы выбрали
	Если РезультатВыбора=Неопределено Тогда Возврат Ложь; КонецЕсли;
	// посмотрим есть ли реквизиты "количество" и "цена" в документе
	ЕстьХарактеристика=Истина; ЕстьКоличество=Истина; ЕстьЦена=Истина;
	МетаданныеРеквизитовТЧ=МетаданныеДокумента.ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты;
	Попытка ЕстьХарактеристика=МетаданныеРеквизитовТЧ.Найти(ИмяРеквизитаХарактеристика)<>Неопределено; Исключение ЕстьХарактеристика=Ложь; КонецПопытки;
	Попытка ЕстьЦена=МетаданныеРеквизитовТЧ.Найти(ИмяРеквизитаЦена)<>Неопределено; Исключение ЕстьЦена=Ложь; КонецПопытки;
	Попытка ЕстьКоличество=МетаданныеРеквизитовТЧ.Найти(ИмяРеквизитаКоличество)<>Неопределено; Исключение ЕстьКоличество=Ложь; КонецПопытки;
	Попытка ЕстьЦена=МетаданныеРеквизитовТЧ.Найти(ИмяРеквизитаЦена)<>Неопределено; Исключение ЕстьЦена=Ложь; КонецПопытки;
    ЕстьКодМаркировки = Ложь;
	// получим таблицу заполняемых товаров
    // Если это "группа документов" ввода в эксплуатацию выберем данные
    Если ВводВЭксплуатацию ИЛИ ВводВЭксплуатациюАвтомобилей Тогда
        Запрос=Новый Запрос();
        ТекстЗапроса="ВЫБРАТЬ
        |  ВводВЭксплуатациюТЧ.Актив КАК Номенклатура,
        |  &ХарактеристикаНоменклатурыПустая КАК Характеристика,
        |  &ЕдиницаИзмеренияПустая КАК ЕдиницаИзмерения,
        |  1 КАК Количество,
        |  ВводВЭксплуатациюТЧ.БалансоваяСтоимость КАК Цена
        |ИЗ                
        |  Документ."+?(ВводВЭксплуатацию,"ВводВЭксплуатацию.Товары","ВводВЭксплуатациюАвтомобилей.Автомобили")+" КАК ВводВЭксплуатациюТЧ
        |ГДЕ
        |  ВводВЭксплуатациюТЧ.Ссылка = &Ссылка";
        Запрос.УстановитьПараметр("Ссылка", ДокументЗаполнения.Ссылка);
        Запрос.УстановитьПараметр("ХарактеристикаНоменклатурыПустая",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
        Запрос.УстановитьПараметр("ЕдиницаИзмеренияПустая",Справочники.ЕдиницыИзмерения.ПустаяСсылка());
        Запрос.Текст = ТекстЗапроса;
        ТаблицаТоваров=Запрос.Выполнить().Выгрузить();
	ИначеЕсли ЗаказКодовМаркировки Тогда
		Запрос = Новый Запрос;
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	КодыМаркировкиДляПечати.Номенклатура КАК Номенклатура,
		               |	КодыМаркировкиДляПечати.ХарактеристикаНоменклатуры КАК Характеристика,
		               |	ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
		               |	КодыМаркировкиДляПечати.КодМаркировки КАК КодМаркировки,
		               |	1 КАК Количество,
		               |	0 КАК Цена
		               |ИЗ
		               |	РегистрСведений.КодыМаркировкиДляПечати КАК КодыМаркировкиДляПечати
		               |ГДЕ
		               |	КодыМаркировкиДляПечати.ДокументОснование = &ДокументОснование
		               |	И КодыМаркировкиДляПечати.КодМаркировки <> """"";
		Запрос.УстановитьПараметр("ДокументОснование", ДокументЗаполнения.Ссылка);
		ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
		УстановитьПривилегированныйРежим(Ложь);
		ЕстьКодМаркировки = Истина;
	Иначе
        Запрос=Новый Запрос();
        ТекстЗапроса="
        |ВЫБРАТЬ РАЗЛИЧНЫЕ
        |	ДокТовары.Номенклатура КАК Номенклатура,
        |	ДокТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
        |	"+?(ЕстьХарактеристика,"ДокТовары."+ИмяРеквизитаХарактеристика,"&ХарактеристикаНоменклатурыПустая")+" КАК Характеристика,
        |	СУММА("+?(ЕстьКоличество,"ДокТовары."+ИмяРеквизитаКоличество,"1")+") КАК Количество,
        |	"+?(ЕстьЦена,"ДокТовары."+ИмяРеквизитаЦена,"0")+" КАК Цена
        //|	"+?(РезультатВыбора="ИзменившиесяЦены","ЦеныСрезПоследних.Цена","0")+" КАК ЦенаСтарая
        |ИЗ
        |	Документ."+МетаданныеДокумента.Имя+".Товары КАК ДокТовары";
        Если РезультатВыбора="ВнутреннийШтрихКоды" Тогда
            ТекстЗапроса=ТекстЗапроса+"
            |	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихКоды КАК ШтрихКоды
            |	ПО ШтрихКоды.Объект=ДокТовары.Номенклатура";
        КонецЕсли;
        Если РезультатВыбора="ИзменившиесяЦены" Тогда
            ТекстЗапроса=ТекстЗапроса+"
            |	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&Момент,ТипЦен=&ТипЦен И Номенклатура В (&Номенклатура) И Контрагент=&ПустойКонтрагент) КАК ЦеныСрезПоследних
            |	ПО ЦеныСрезПоследних.Номенклатура=ДокТовары.Номенклатура";
        КонецЕсли;
        ТекстЗапроса=ТекстЗапроса+"
        |ГДЕ
        |	ДокТовары.Ссылка=&Ссылка";
        Если РезультатВыбора="ВнутреннийШтрихКоды" Тогда
            ТекстЗапроса=ТекстЗапроса+"
            |	И НЕ ШтрихКоды.ШтрихКод ЕСТЬ NULL
            |	И ПОДСТРОКА(ШтрихКоды.ШтрихКод,1,2)=&ПрефиксВнутреннегоШК";
            Запрос.УстановитьПараметр("ПрефиксВнутреннегоШК",СокрЛП(Константы.ПрефиксШтучногоШК.Получить()));
        КонецЕсли;
        Если (РезультатВыбора="ИзменившиесяЦены") И ЕстьЦена Тогда
            ТекстЗапроса=ТекстЗапроса+"
            |	И ((ЦеныСрезПоследних.Цена ЕСТЬ NULL И НЕ " + "ДокТовары."+ИмяРеквизитаЦена + " = 0) ИЛИ ЦеныСрезПоследних.Цена<>ДокТовары."+ИмяРеквизитаЦена+")";
            
            Попытка
                Момент=Новый Граница(НачалоДня(ДокументЗаполнения.ДатаНачалаДействия),ВидГраницы.Исключая);
            Исключение
                Попытка
                    ЭтоНовый=ДокументЗаполнения.ЭтоНовый();
                Исключение
                    ЭтоНовый=Ложь;
                КонецПопытки; 
                Момент=?(ЭтоНовый,Новый МоментВремени(КонецДня(ДокументЗаполнения.Дата)),Новый Граница(ДокументЗаполнения.МоментВремени(),ВидГраницы.Исключая));
            КонецПопытки; 
            Запрос.УстановитьПараметр("Момент",Момент);
            Запрос.УстановитьПараметр("ТипЦен",ТипЦен);
            Запрос.УстановитьПараметр("ПустойКонтрагент",Справочники.Контрагенты.ПустаяСсылка());
            Запрос.УстановитьПараметр("Номенклатура",ДокументЗаполнения[ИмяТабличнойЧасти].ВыгрузитьКолонку("Номенклатура"));
        КонецЕсли;
        ТекстЗапроса=ТекстЗапроса+"
        |СГРУППИРОВАТЬ ПО
        |	ДокТовары.Номенклатура, ДокТовары.ЕдиницаИзмерения"+?(ЕстьХарактеристика,", ДокТовары."+ИмяРеквизитаХарактеристика,"")+""+?(ЕстьЦена,", ДокТовары."+ИмяРеквизитаЦена,"");
        
        Запрос.УстановитьПараметр("ХарактеристикаНоменклатурыПустая",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
        
        Запрос.Текст=ТекстЗапроса;
        Запрос.УстановитьПараметр("Ссылка",ДокументЗаполнения.Ссылка);
        ТаблицаТоваров=Запрос.Выполнить().Выгрузить();
    КонецЕсли;
	
	ВВалютеУчета=ТипЦен.ВВалютеУчета;
	Если НЕ ВВалютеУчета Тогда
		Валюта=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
	КонецЕсли;
	
	Попытка
		ЭтоНовый=ДокументЗаполнения.ЭтоНовый();
	Исключение
		ЭтоНовый=Ложь;
	КонецПопытки; 
	МоментВремени = ?(ЭтоНовый,КонецДня(ДокументЗаполнения.Дата),ДокументЗаполнения.Дата);
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	ЭкранированныйСимвол = ОбъектШК.ЭкранированныйСимволGS1();
	РазделительGS1 = ОбъектШК.РазделительGS1();
	
	// идем по таблице товаров
	Товары.Очистить();
	Для Каждого СтрокаТовар Из ТаблицаТоваров Цикл
		// получим цену
		Цена=0;
		Если ЕстьЦена Тогда
			Цена=СтрокаТовар.Цена;
		Иначе
			Если ВВалютеУчета Тогда
				Валюта=СтрокаТовар.Номенклатура.ВалютаУчета;
			КонецЕсли; 
			Цена=обПолучитьЦену(ТипЦен,СтрокаТовар.Номенклатура,МоментВремени,,,,СтрокаТовар.Характеристика,СтрокаТовар.ЕдиницаИзмерения,ПодразделениеКомпании);    
		КонецЕсли;
		
		НоваяСтрока=Товары.Добавить();
		НоваяСтрока.Номенклатура=СтрокаТовар.Номенклатура;
		НоваяСтрока.Артикул=?(ТипЗнч(СтрокаТовар.Номенклатура)=Тип("СправочникСсылка.ПрочиеАктивы"),СтрокаТовар.Номенклатура.Номенклатура.Артикул,СтрокаТовар.Номенклатура.Артикул);
		НоваяСтрока.ПроизвольныйТекст1	= ?(ТипЗнч(СтрокаТовар.Номенклатура)=Тип("СправочникСсылка.ПрочиеАктивы"),СтрокаТовар.Номенклатура.Номенклатура.Производитель,СтрокаТовар.Номенклатура.Производитель);
		НоваяСтрока.ЕдиницаИзмерения=СтрокаТовар.ЕдиницаИзмерения;
		НоваяСтрока.Характеристика=СтрокаТовар.Характеристика;
		НоваяСтрока.КоличествоТовара=СтрокаТовар.Количество;
		НоваяСтрока.Количество = СтрокаТовар.Количество;
		Если ТипЗнч(НоваяСтрока.ЕдиницаИзмерения)=Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
			НоваяСтрока.Коэффициент=НоваяСтрока.ЕдиницаИзмерения.Коэффициент;
		Иначе
			НоваяСтрока.Коэффициент=1;
		КонецЕсли;
		Если ЕстьКодМаркировки Тогда
			НоваяСтрока.ШтрихКод=СтрЗаменить(СтрокаТовар.КодМаркировки, ЭкранированныйСимвол, РазделительGS1);
		Иначе
			мсШК=ПолучитьШКТовара(НоваяСтрока.Номенклатура,НоваяСтрока.ЕдиницаИзмерения,НоваяСтрока.Характеристика);
			НоваяСтрока.ШтрихКод=мсШК[0];
		КонецЕсли;
		НоваяСтрока.Цена=Цена;
		НоваяСтрока.Выбран=Истина;
	КонецЦикла;
	ДокументОбъект=ДокументЗаполнения;
	Возврат Истина;
КонецФункции

// Заполнение по группе оборудования
Процедура ЗаполнитьПоГруппеОборудования(ЭтаФорма) Экспорт
	//Выбираем группу товаров для заполнения
	ФормаВыбора=Справочники.ГруппыТоваровОборудования.ПолучитьФормуВыбора(,ЭтаФорма);
	ГруппаТоваров=ФормаВыбора.ОткрытьМодально();
	Если ГруппаТоваров=Неопределено Тогда Возврат; КонецЕсли;
	// формируем текст запроса
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТоварыВОборудовании.Номенклатура
	               |ИЗ
	               |	РегистрСведений.ТоварыВОборудовании КАК ТоварыВОборудовании
	               |ГДЕ
	               |	ТоварыВОборудовании.ГруппаТоваров = &ГруппаТоваров
	               |	И ТоварыВОборудовании.Номенклатура.ПометкаУдаления = ЛОЖЬ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТоварыВОборудовании.Номенклатура.Наименование";
	Запрос=Новый Запрос;
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("ГруппаТоваров",ГруппаТоваров);
	// получаем выборку
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		//Для Каждого следующего товара добавляем его в список
		СтрокаНоменклатуры=Товары.Найти(Выборка.Номенклатура,"Номенклатура");
		Если СтрокаНоменклатуры=Неопределено Тогда
			ДобавитьНоменклатуру(ЭтаФорма, Выборка.Номенклатура);
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

// Заполнить ТЧ из ТСД
Процедура ЗаполнитьИзТСД(ЭтаФорма) Экспорт

	// откроем форму загрузки
	ФормаПолученияДанныхИхТСД = Обработки.ПолучениеДанныхИзТСД.ПолучитьФорму();
	Результат = ФормаПолученияДанныхИхТСД.ОткрытьМодально();
	// смотрим что есть
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда Возврат;
	Иначе
		ТаблицаПодбора = Результат.ТаблицаДанных;
		Если Результат.ВыводитьОтчет Тогда Результат.Отчет.Показать(); КонецЕсли; 
		Если ТаблицаПодбора.Количество() = 0 Тогда Возврат; КонецЕсли;
	КонецЕсли; 
	
	// заполняем
	Для Каждого СтрокаТовар Из ТаблицаПодбора Цикл
		ДобавитьНоменклатуру(ЭтаФорма, СтрокаТовар.Номенклатура,, СтрокаТовар.ЕдиницаИзмерения,СтрокаТовар.Количество); 
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

// Получает ШтрихКод товара или актива
// Товар - Номенклатура или актив
// ЕдиницаИзмерения - единица измерения, если ШК-ние ведется по ЕИ
// ХарактеристикаНоменклатуры - Характеристика номенклатуры , если ШК-ние ведется по ХН
// Возвращает массив со ШК, удовлетворяющими переданным параметрам, иначе - 0
Функция ПолучитьШКТовара(Товар, ЕдиницаИзмерения="", ХарактеристикаНоменклатуры="") Экспорт
		
	// создаем массив с единственным значением 0, по умолчанию
	мсШК = Новый Массив;
	мсШК.Добавить(0);
	
	Если обЗначениеНеЗаполнено(Товар) Тогда Возврат мсШК; КонецЕсли;
	
	// определяем на основании чего формируется ШК
	Если ТипЗнч(Товар)=Тип("СправочникСсылка.Номенклатура") Тогда
		ИспользованиеШтрихКодов = Товар.ТипНоменклатуры.ИспользованиеШтрихКодов;
	Иначе
		ИспользованиеШтрихКодов = 1;
	КонецЕсли; 

	Если ИспользованиеШтрихКодов = 4 Тогда  // ШК не используется
		Возврат мсШК;
	КонецЕсли;
	
	// Ищем нужный нам ШК
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихКоды.ШтрихКод КАК ШтрихКод
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|ГДЕ
	|	Не ШтрихКоды.Запрет И 
	|	ШтрихКоды.Объект = &Объект";
	Если ИспользованиеШтрихКодов = 2 И ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда  // для ед измерения
		Запрос.Текст = Запрос.Текст + Символы.ПС + "	И ШтрихКоды.ЕдиницаИзмерения = &ЕдиницаИзмерения";
		Запрос.УстановитьПараметр("ЕдиницаИзмерения",ЕдиницаИзмерения);
	ИначеЕсли ИспользованиеШтрихКодов = 3 И ЗначениеЗаполнено(ХарактеристикаНоменклатуры) Тогда  // для хар-ки номенклатуры
		Запрос.Текст = Запрос.Текст + Символы.ПС + "	И ШтрихКоды.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Объект",Товар);
	
	РезультатЗапроса = Запрос.Выполнить();
	// возвращаем массив с ШК, удовлетворяющими заданным параметрам
	Если Не РезультатЗапроса.Пустой() Тогда
		мсШК = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ШтрихКод");
	КонецЕсли;
	
	Возврат мсШК;
	
КонецФункции

// Функция возвращает характеристику к переданному товару
Функция ПолучитьХарактеристикуТовара(Товар) Экспорт
	
	Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();	
	
	ИспХарактеристик = Товар.ТипНоменклатуры.ИспользованиеХарактеристик;
	Если ИспХарактеристик = 1 Тогда
		ХарактеристикиВыбТовара = Справочники.ХарактеристикиНоменклатуры.Выбрать(,Товар.ТипНоменклатуры);	
		Если ХарактеристикиВыбТовара.Следующий() Тогда
			Характеристика = ХарактеристикиВыбТовара.Ссылка;	
		КонецЕсли;
	ИначеЕсли ИспХарактеристик = 2 Тогда
		ХарактеристикиВыбТовара = Справочники.ХарактеристикиНоменклатуры.Выбрать(,Товар);	
		Если ХарактеристикиВыбТовара.Следующий() Тогда
			Характеристика = ХарактеристикиВыбТовара.Ссылка;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Характеристика;
КонецФункции

// производит расчет строки
Процедура РасчетСтроки(СтрокаТЧ) Экспорт
	
	Номенклатура = СтрокаТЧ.Номенклатура;
	Характеристика = СтрокаТЧ.Характеристика;
	ЕдИзмерения = СтрокаТЧ.ЕдиницаИзмерения;
	Количество = СтрокаТЧ.КоличествоТовара;
	Коэффициент = СтрокаТЧ.Коэффициент;
	
	Если ТипЗнч(Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
		Цена = обПолучитьЦену(ТипЦен,Номенклатура,КонецДня(ДатаЦены),,обВалютаТипаЦены(Номенклатура,ТипЦен,Ложь),ТекущаяДата(),Характеристика,ЕдИзмерения,ПодразделениеКомпании);
	ИначеЕсли ТипЗнч(Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда	
		ЦенаРаботы=орПолучитьЦенуАвтоработы(ТипЦенРабот, Номенклатура, Справочники.Модели.ПустаяСсылка(), Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка(), Справочники.Цеха.ПустаяСсылка(), Справочники.ВидыРемонта.ПустаяСсылка(),,обВалютаТипаЦены(,ТипЦенРабот,Ложь),ТекущаяДата());
		ЕдиницаИзмерения=ЦенаРаботы.Нормочас;
		СтрокаТЧ.ЕдиницаИзмерения=ЕдиницаИзмерения;
		Коэффициент=?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени),СтрокаТЧ.Коэффициент,ЦенаРаботы.НормаВремени);
		СтрокаТЧ.Коэффициент=Коэффициент;
		Цена=ЦенаРаботы.Цена;
	Иначе
		Цена=0;
	КонецЕсли; 
		
	Сумма =  Окр(Количество*Цена*Коэффициент, 2);
	СтрокаТЧ.Цена = Цена;
	СтрокаТЧ.Сумма = Сумма;
КонецПроцедуры

// проверяет наличие установленной компоненты "1С-BarCode"
// Возвращает Истина-Компонента установлена, Ложь - нет.
Функция КомпонентаПечатиШтрихКодовУстановлена() Экспорт
	Попытка
		БарКодПроверка=Новый COMОбъект("V8.Barcod.1");
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции

// загружает настройки, сохраненные во внешнем макете
Функция ЗагрузитьНастройки(ИмяМакета) Экспорт
	СоответствиеНастроек=Новый Соответствие();
	
	Макет=Новый ТабличныйДокумент();
	Попытка 
		Макет.Прочитать(ИмяМакета); 
		Макет=Макет.ПолучитьОбласть("Настройки");
	Исключение 
		Возврат СоответствиеНастроек; 
	КонецПопытки;
	
	ВСтроке=2;					Попытка Обл=Макет.ПолучитьОбласть("ВСтроке"); ВСтроке=Число(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	ВСтолбце=2;					Попытка Обл=Макет.ПолучитьОбласть("ВСтолбце"); ВСтолбце=Число(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	ПолеСверху=0;				Попытка Обл=Макет.ПолучитьОбласть("ПолеСверху"); ПолеСверху=Число(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	ПолеСнизу=0;				Попытка Обл=Макет.ПолучитьОбласть("ПолеСнизу"); ПолеСнизу=Число(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	ПолеСлева=0;				Попытка Обл=Макет.ПолучитьОбласть("ПолеСлева"); ПолеСлева=Число(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	ПолеСправа=0;				Попытка Обл=Макет.ПолучитьОбласть("ПолеСправа"); ПолеСправа=Число(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	АвтоМасштаб=Ложь;			Попытка Обл=Макет.ПолучитьОбласть("АвтоМасштаб"); АвтоМасштаб=Булево(Обл.Область(1,1).Текст); Исключение КонецПопытки;
	СтрОриентация="Портрет";	Попытка Обл=Макет.ПолучитьОбласть("Ориентация"); СтрОриентация=СокрЛП(Обл.Область(1,1).Текст); Исключение КонецПопытки;
		
	СоответствиеНастроек.Вставить("ВСтроке",ВСтроке);
	СоответствиеНастроек.Вставить("ВСтолбце",ВСтолбце);
	СоответствиеНастроек.Вставить("ПолеСверху",ПолеСверху);
	СоответствиеНастроек.Вставить("ПолеСнизу",ПолеСнизу);
	СоответствиеНастроек.Вставить("ПолеСлева",ПолеСлева);
	СоответствиеНастроек.Вставить("ПолеСправа",ПолеСправа);
	СоответствиеНастроек.Вставить("АвтоМасштаб",АвтоМасштаб);
	СоответствиеНастроек.Вставить("Ориентация",СтрОриентация);
	
	Возврат СоответствиеНастроек;
КонецФункции

//Печать списка этикеток
Процедура ПечатьСпискаЭтикеток() Экспорт
	Если СписокКодовШаблонДляПечатиWindows="" Тогда
		//Если шаблон не задан - используем шаблон по умолчанию
		Макет=ПолучитьМакет("Список");
	Иначе
		//Иначе считываем шаблон из файла
		ФайлМакета=Новый Файл(СписокКодовШаблонДляПечатиWindows);
		Если НЕ ФайлМакета.Существует() Тогда
			Сообщить("Файл макета "+СписокКодовШаблонДляПечатиWindows+" не найден!"); Возврат;
		КонецЕсли;
		Макет=Новый ТабличныйДокумент();
		Макет.Прочитать(СписокКодовШаблонДляПечатиWindows);
	КонецЕсли; 
	ТабДокумент=Новый ТабличныйДокумент();
	
	//Выводим шапку списка ценников
	Попытка
		ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.СтрДокумент="";
		// если указан документ из которого печатаем, то выведем его
		Если Метаданные.НайтиПоТипу(ТипЗнч(ДокументОбъект))<>Неопределено Тогда
			ОбластьМакета.Параметры.СтрДокумент=СокрЛП(ДокументОбъект);
			Попытка ОбластьМакета.Параметры.СтрДокумент=ОбластьМакета.Параметры.СтрДокумент+" Контрагент: "+СокрЛП(ДокументОбъект.Контрагент); Исключение КонецПопытки;
			Попытка ОбластьМакета.Параметры.СтрДокумент=ОбластьМакета.Параметры.СтрДокумент+" Вх. номер: "+СокрЛП(ДокументОбъект.ВхДокНомер); Исключение КонецПопытки;
			Попытка ОбластьМакета.Параметры.СтрДокумент=ОбластьМакета.Параметры.СтрДокумент+" Вх. дата: "+Формат(ДокументОбъект.ВхДокДата,"ДФ=dd.MM.yyyy"); Исключение КонецПопытки;
			Попытка ОбластьМакета.Параметры.СтрДокумент=ОбластьМакета.Параметры.СтрДокумент+" Сумма документа: "+СокрЛП(ДокументОбъект.СуммаДокумента)+" "+СокрЛП(ДокументОбъект.ВалютаДокумента); Исключение КонецПопытки;
		КонецЕсли;
	Исключение
		Сообщить("В макете отсутствует область 'Шапка'"); Возврат;
	КонецПопытки; 
	ТабДокумент.Вывести(ОбластьМакета);
	
	//Формирование запроса получения ШК
	//ТекстЗапроса =
	//"ВЫБРАТЬ
	//|	ШтрихКоды.ШтрихКод
	//|ИЗ
	//|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	//|ГДЕ
	//|	ШтрихКоды.Объект = &Объект И
	//|	ШтрихКоды.ЕдиницаИзмерения = &ЕдиницаИзмерения";
	//Запрос=Новый Запрос;
	
	//Получение области строки
	Попытка
		ОбластьМакета=Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Области.Строка.АвтоВысотаСтроки=Истина;
	Исключение
		Сообщить("В макете отсутствует область 'Строка'"); Возврат;
	КонецПопытки; 
	ПрефВнутр=Константы.ПрефиксШтучногоШК.Получить(); Ном=0;
	Для Каждого СтрокаНоменклатуры Из Товары Цикл
		Если НЕ СтрокаНоменклатуры.Выбран Тогда Продолжить; КонецЕсли;
		//Вывод очередной номенклатуры из списка
		ОбластьМакета.Параметры.Товар=СтрокаНоменклатуры.Номенклатура;
		//Запрос.Текст=ТекстЗапроса;
		//Запрос.УстановитьПараметр("Объект",СтрокаНоменклатуры.Номенклатура);
		//Запрос.УстановитьПараметр("ЕдиницаИзмерения",СтрокаНоменклатуры.ЕдиницаИзмерения);
		//Выборка=Запрос.Выполнить().Выбрать();
		//Если Выборка.Следующий() Тогда
		//	ОбластьМакета.Параметры.Код=Выборка.ШтрихКод;
		//	// если печатаем только без штрих кодов, то пропустим
		//	Если ТолькоБезШК И НЕ ТолькоВнутренниеШК Тогда Продолжить; КонецЕсли;
		//	// если печатаем только внутренние ШК и наш ШК не внутренний, то пропустим
		//	Если ТолькоВнутренниеШК И Число(Лев(Выборка.ШтрихКод,2))<>ПрефВнутр Тогда Продолжить; КонецЕсли;
		//Иначе
		//	// ШК не найден. проверим, а надо ли печатать такие коды
		//	Если НЕ ТолькоБезШК И ТолькоВнутренниеШК Тогда Продолжить; КонецЕсли;
		//КонецЕсли;
		ШК=СокрЛП(СтрокаНоменклатуры.ШтрихКод);
		Если ПустаяСтрока(ШК) Тогда
			Если НЕ ТолькоБезШК И ТолькоВнутренниеШК Тогда Продолжить; КонецЕсли;
		Иначе
			Если ТолькоБезШК И НЕ ТолькоВнутренниеШК Тогда Продолжить; КонецЕсли;
			// если печатаем только внутренние ШК и наш ШК не внутренний, то пропустим
			Если ТолькоВнутренниеШК И (Лев(ШК, 2) <> Строка(ПрефВнутр)) Тогда Продолжить; КонецЕсли;
		КонецЕсли; 
		ОбластьМакета.Параметры.Код=ШК;
		Ном=Ном+1;
		ОбластьМакета.Параметры.Позиция=Ном;
		Попытка
			ОбластьМакета.Параметры.Артикул=СтрокаНоменклатуры.Номенклатура.Артикул;
			ОбластьМакета.Параметры.Производитель = СтрокаНоменклатуры.Номенклатура.Производитель;
		Исключение
			ОбластьМакета.Параметры.Артикул=СтрокаНоменклатуры.Номенклатура.Номенклатура.Артикул;
			ОбластьМакета.Параметры.Производитель = СтрокаНоменклатуры.Номенклатура.Номенклатура.Производитель;
		КонецПопытки; 
		ОбластьМакета.Параметры.Цена=СтрокаНоменклатуры.Цена;
		ОбластьМакета.Параметры.Количество=СтрокаНоменклатуры.Количество;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла; 
	
	//Выводим подвал списка
	Попытка
		ОбластьМакета=Макет.ПолучитьОбласть("Подвал");
	Исключение
	    Сообщить("В макете отсутствует область 'Подвал'"); Возврат;
	КонецПопытки; 
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ОтображатьЗаголовки=Ложь;
	ТабДокумент.ОтображатьСетку=Ложь;
	ТабДокумент.ТолькоПросмотр=Истина;
	ТабДокумент.Показать();
КонецПроцедуры

//Печать этикеток и ценников
//Параметры:
//	РежимПечати - число (0-печать этикеток, 1-печать ценников)
Процедура ПечатьЭтикеток(РежимПечати) Экспорт
	Если РежимПечати=0 Тогда
		//Режим печати этикеток
		// проверим установлена ли компонента печати
		Если НЕ КомпонентаПечатиШтрихКодовУстановлена() Тогда
			Предупреждение("Не установлена компонента печати этикеток ""1C-Barcode""!",10);
			Возврат;
		КонецЕсли;
		//Проверка корректности числа этикеток на странице
		Если ЭтикеткиКоличествоВСтроке<=0 ИЛИ ЭтикеткиКоличествоВСтолбце<=0 Тогда
			Сообщить("Неверно указано количество экземпляров на листе"); Возврат;
		КонецЕсли; 
		СтрокНаЛисте=ЭтикеткиКоличествоВСтолбце;
		КолонокНаЛисте=ЭтикеткиКоличествоВСтроке;
		Если ЭтикеткиШаблонДляПечатиWindows="" Тогда
			//Если шаблон не задан - используем шаблон по умолчанию
			Макет=ПолучитьМакет("Этикетка");
		Иначе
			//Иначе считываем шаблон из файла
			ФайлМакета=Новый Файл(ЭтикеткиШаблонДляПечатиWindows);
			Если НЕ ФайлМакета.Существует() Тогда
				Сообщить("Файл макета "+ЭтикеткиШаблонДляПечатиWindows+" не найден!"); Возврат;
			КонецЕсли;
			Макет=Новый ТабличныйДокумент();
			Макет.Прочитать(ЭтикеткиШаблонДляПечатиWindows);
		КонецЕсли; 
	ИначеЕсли РежимПечати=1 Тогда
		//Режим печати ценников
		//Проверка выбора типа цен
		Если обЗначениеНеЗаполнено(ТипЦен) Тогда
			Сообщить("Не выбран тип цен!"); Возврат;
		КонецЕсли; 
		//Проверка корректности числа ценников на странице
		Если ЦенникиКоличествоВСтроке<=0 ИЛИ ЦенникиКоличествоВСтолбце<=0 Тогда
			Сообщить("Неверно указано количество экземпляров на листе"); Возврат;
		КонецЕсли; 
		СтрокНаЛисте=ЦенникиКоличествоВСтолбце;
		КолонокНаЛисте=ЦенникиКоличествоВСтроке;
		Если ЦенникиШаблонДляПечатиWindows="" Тогда
			//Если шаблон не задан - используем шаблон по умолчанию
			Макет=ПолучитьМакет("Ценник");
		Иначе
			//Иначе считываем шаблон из файла
			ФайлМакета=Новый Файл(ЦенникиШаблонДляПечатиWindows);
			Если НЕ ФайлМакета.Существует() Тогда
				Сообщить("Файл макета "+ЦенникиШаблонДляПечатиWindows+" не найден!"); Возврат;
			КонецЕсли;
			Макет=Новый ТабличныйДокумент();
			Макет.Прочитать(ЦенникиШаблонДляПечатиWindows);
		КонецЕсли; 
	КонецЕсли; 
	
	//Считаем применяемость
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	НоменклатураПрименяемость.Номенклатура КАК Номенклатура,
				   |	НоменклатураПрименяемость.Модель.Наименование КАК Модель
				   |ИЗ
				   |	РегистрСведений.НоменклатураПрименяемость КАК НоменклатураПрименяемость
				   |ГДЕ
				   |	НоменклатураПрименяемость.Номенклатура В(&Номенклатура)";
	Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
	ВыборкаПрименяемости = Запрос.Выполнить().Выбрать();
	
	// получим область дополнительных параметров, определенных в макете
	ОбластьДопПараметров=Неопределено;
	Попытка ОбластьДопПараметров=Макет.ПолучитьОбласть("Параметры"); Исключение КонецПопытки;
	
	//Получаем область макета для вывода этикетки/ценника
	Попытка
		ОбластьМакета=Макет.ПолучитьОбласть("Товар|Ценник");
	Исключение
	    Сообщить("В макете отсутствует область 'Товар|Ценник'"); Возврат;
	КонецПопытки; 
	ТабДокумент=Новый ТабличныйДокумент();
	
	СтрокаНаЛисте=0; КолонкаНаЛисте=КолонокНаЛисте+1; ПрефиксВесовогоШК=Константы.ПрефиксВесовогоШК.Получить();
	Для Каждого СтрокаНоменклатуры Из Товары Цикл
		Если НЕ СтрокаНоменклатуры.Выбран Тогда Продолжить; КонецЕсли;
		//Если ТипЗнч(СтрокаНоменклатуры.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
		//	ПараметрШК=0;
		//Иначе
		//	ПараметрШК=СтрокаНоменклатуры.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов;
		//КонецЕсли;
		Для СчетчикНоменклатуры=1 По СтрокаНоменклатуры.Количество Цикл
			//// получим штрихкод
			//ШК=""; ЕстьШК=Ложь;
			//ТекстЗапроса="
			//|ВЫБРАТЬ ПЕРВЫЕ 1
			//|	ШтрихКоды.ШтрихКод КАК ШтрихКод
			//|ИЗ
			//|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
			//|ГДЕ";
			//Если ПараметрШК=Неопределено Тогда
			//	//Ничего не делаем
			//ИначеЕсли ПараметрШК=1 Тогда // для Каждого товара
			//	ТекстЗапроса=ТекстЗапроса+"
			//	|	ШтрихКоды.Объект=&Номенклатура";
			//	ЕстьШК=Истина;
			//ИначеЕсли ПараметрШК=2 Тогда // для каждой единицы
			//	ТекстЗапроса=ТекстЗапроса+"
			//	|	ШтрихКоды.Объект=&Номенклатура И ШтрихКоды.ЕдиницаИзмерения=&ЕдиницаИзмерения";
			//	ЕстьШК=Истина;
			//ИначеЕсли ПараметрШК=3 Тогда // для каждой характеристики
			//	// ПОДЛЕЖИТ ИСПРАВЛЕНИЮ FEDY
			//	//Продолжить;
			//Иначе // штрих кодирование не ведется
			//	//Продолжить;
			//КонецЕсли;
			//Если ЕстьШК Тогда
			//	Запрос=Новый Запрос(ТекстЗапроса);
			//	Запрос.УстановитьПараметр("Номенклатура",СтрокаНоменклатуры.Номенклатура);
			//	Запрос.УстановитьПараметр("ЕдиницаИзмерения",СтрокаНоменклатуры.ЕдиницаИзмерения);
			//	Выборка=Запрос.Выполнить().Выбрать(); ШК=""; 
			//	Если Выборка.Следующий() Тогда
			//		ШК=СокрЛП(Выборка.ШтрихКод);
			//	КонецЕсли;
			//КонецЕсли;
			ШК=СокрЛП(СтрокаНоменклатуры.ШтрихКод);
			
			// заполним структуру параметров, что бы люди сами могли добавить то что надо на этикетку/ценник
			ДопПараметрыЭтикетки=Новый Структура();
			
			Для Каждого РеквизитТЧ Из ЭтотОбъект.Метаданные().ТабличныеЧасти.Товары.Реквизиты Цикл
				ДопПараметрыЭтикетки.Вставить(РеквизитТЧ.Имя,СтрокаНоменклатуры[РеквизитТЧ.Имя]);
			КонецЦикла; 
			
			ДопПараметрыЭтикетки.Вставить("Наименование",СокрЛП(СтрокаНоменклатуры.Номенклатура));
			ДопПараметрыЭтикетки.Вставить("Код",СокрЛП(СтрокаНоменклатуры.Номенклатура.Код));
			ДопПараметрыЭтикетки.Вставить("Цена",СтрокаНоменклатуры.Цена);
			ДопПараметрыЭтикетки.Вставить("Рубли",Цел(СтрокаНоменклатуры.Цена));
			ДопПараметрыЭтикетки.Вставить("Копейки",(СтрокаНоменклатуры.Цена-Цел(СтрокаНоменклатуры.Цена))*100);
			Попытка
				ДопПараметрыЭтикетки.Вставить("Описание",СокрЛП(СтрокаНоменклатуры.Номенклатура.Комментарий));
			Исключение
			КонецПопытки; 
			ДопПараметрыЭтикетки.Вставить("ШтрихКод",ШК);
			Попытка
				ДопПараметрыЭтикетки.Вставить("СтранаПроисхождения",СокрЛП(СтрокаНоменклатуры.Номенклатура.СтранаПроисхождения));
			Исключение
			КонецПопытки;
			Попытка
				ДопПараметрыЭтикетки.Вставить("БазоваяЕдиницаИзмерения",СокрЛП(СтрокаНоменклатуры.Номенклатура.БазоваяЕдиницаИзмерения));
			Исключение
			КонецПопытки; 
			ДопПараметрыЭтикетки.Вставить("ЕдиницаИзмерения",СокрЛП(СтрокаНоменклатуры.ЕдиницаИзмерения));
			Попытка
				ДопПараметрыЭтикетки.Вставить("Артикул",СокрЛП(СтрокаНоменклатуры.Номенклатура.Артикул));
			Исключение
			КонецПопытки; 
			ДопПараметрыЭтикетки.Вставить("РабочаяДата",РабочаяДата);
			Попытка
				ДопПараметрыЭтикетки.Вставить("Комментарий",СтрокаНоменклатуры.Номенклатура.Комментарий);
			Исключение
			КонецПопытки;
			Попытка
				ДопПараметрыЭтикетки.Вставить("Производитель", СокрЛП(СтрокаНоменклатуры.Номенклатура.Производитель));
			Исключение
			КонецПопытки;
			ДопПараметрыЭтикетки.Вставить("Организация",ПараметрыСеанса.Пользователь.Организация);
			СтрокаПрименяемости=""; ВыборкаПрименяемости.Сбросить();
			Пока ВыборкаПрименяемости.НайтиСледующий(Новый Структура("Номенклатура",СтрокаНоменклатуры.Номенклатура)) Цикл
				СтрокаПрименяемости=СтрокаПрименяемости+ВыборкаПрименяемости.Модель+"; ";
			КонецЦикла; 
			ДопПараметрыЭтикетки.Вставить("Применяемость",СтрокаПрименяемости);
			Если НЕ ПустаяСтрока(Логотип) Тогда
				ФайлЛоготипа=Новый Файл(Логотип);
				Если ФайлЛоготипа.Существует() Тогда
					Попытка
						ОбластьМакета.Рисунки.Логотип.Картинка=Новый Картинка(Логотип);
					Исключение
					КонецПопытки; 
				КонецЕсли; 
			КонецЕсли; 
			
			// добавим дополнительные параметры
			Если ОбластьДопПараметров<>Неопределено Тогда
				Для Каждого Обл Из ОбластьДопПараметров.Области Цикл
					Попытка
						Ключ=Обл.Имя;
						Выражение=СокрЛП(Обл.Текст);
						ДопПараметрыЭтикетки.Вставить(Ключ,Вычислить(Выражение));
					Исключение
					КонецПопытки;
				КонецЦикла;
			КонецЕсли;
			
			// выводим параметры в макет
			ОбластьМакета.Параметры.Заполнить(ДопПараметрыЭтикетки);			
			
			// попытаемся напечатать ШК
			Если НЕ ПустаяСтрока(ШК) Тогда
				Попытка
					ПрефиксШтрихКода=Число(Лев(ШК,2));
				Исключение
					ПрефиксШтрихКода=0;
				КонецПопытки;
				Попытка
					ОбластьМакета.Рисунки.ШК.Объект.ТипКода=Число(НЕ (СтрДлина(ШК)=8 И ПрефиксШтрихКода<>ПрефиксВесовогоШК));
					ДлинаШК=?(ОбластьМакета.Рисунки.ШК.Объект.ТипКода=0,8,13);
					ШК=ШК+"0000000000000"; ШК=Лев(ШК,ДлинаШК-1+ОбластьМакета.Рисунки.ШК.Объект.СодержитКС);
					ОбластьМакета.Рисунки.ШК.Объект.Сообщение=ШК;
				Исключение
				КонецПопытки;
			КонецЕсли; 
			
			Если КолонкаНаЛисте>=КолонокНаЛисте Тогда
				//Если в строке выведено этикеток более заданного - начнем новую строку
				СтрокаНаЛисте=СтрокаНаЛисте+1;
				КолонкаНаЛисте=1;
				ТабДокумент.Вывести(ОбластьМакета);
			Иначе
				//Иначе продолжаем вывод в текущую строку
				КолонкаНаЛисте=КолонкаНаЛисте+1;
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
			Если СтрокаНаЛисте>=СтрокНаЛисте Тогда
				//Если на листе выведено этикеток более заданного - начнем новый лист
				СтрокаНаЛисте=0;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли; 
			
		КонецЦикла; 
	КонецЦикла; 
	
	ТабДокумент.ОтображатьЗаголовки=Ложь;
	ТабДокумент.ОтображатьСетку=Ложь;
	ТабДокумент.ТолькоПросмотр=Истина;
	
	СоответствиеНастроек=ЗагрузитьНастройки(?(РежимПечати=0,ЭтикеткиШаблонДляПечатиWindows,ЦенникиШаблонДляПечатиWindows));
	
	Если СоответствиеНастроек["Ориентация"]<>Неопределено Тогда
		ТабДокумент.ОриентацияСтраницы=ОриентацияСтраницы[СоответствиеНастроек["Ориентация"]];
	КонецЕсли;
		
	//ТабДокумент.ПолеСверху=?(СоответствиеНастроек["ПолеСверху"]=
	//Неопределено,ТабДокумент.ПолеСверху,СоответствиеНастроек["ПолеСверху"]);
	//ТабДокумент.ПолеСнизу=?(СоответствиеНастроек["ПолеСнизу"]=
	//Неопределено,ТабДокумент.ПолеСнизу,СоответствиеНастроек["ПолеСнизу"]);
	//ТабДокумент.ПолеСправа=?(СоответствиеНастроек["ПолеСправа"]=
	//Неопределено,ТабДокумент.ПолеСправа,СоответствиеНастроек["ПолеСправа"]);
	//ТабДокумент.ПолеСлева=?(СоответствиеНастроек["ПолеСлева"]=
	//Неопределено,ТабДокумент.ПолеСлева,СоответствиеНастроек["ПолеСлева"]);
	// DORA	
	ТабДокумент.ПолеСверху=?(СоответствиеНастроек["ПолеСверху"]=Неопределено,0,СоответствиеНастроек["ПолеСверху"]);
	ТабДокумент.ПолеСнизу=?(СоответствиеНастроек["ПолеСнизу"]=Неопределено,0,СоответствиеНастроек["ПолеСнизу"]);
	ТабДокумент.ПолеСправа=?(СоответствиеНастроек["ПолеСправа"]=Неопределено,0,СоответствиеНастроек["ПолеСправа"]);
	ТабДокумент.ПолеСлева=?(СоответствиеНастроек["ПолеСлева"]=Неопределено,0,СоответствиеНастроек["ПолеСлева"]);
	Если СоответствиеНастроек["АвтоМасштаб"]<>Неопределено Тогда
		ТабДокумент.АвтоМасштаб=СоответствиеНастроек["АвтоМасштаб"];
	КонецЕсли;
	ТабДокумент.Показать();
КонецПроцедуры

// Приводит таблицу к виду, готовому для передачи всех значений из нее на ПЭЦ
// удаляет лишние колонки и добавляет нужные
Процедура ПодготовитьТаблицуДляПечатиНаПЭЦ3(тзТовары)
// тзТовары - Таблица значений, на основании которой формируется Таблица для печати на ПЭЦ
// Колонки:
//Наименование
//Комментарий
//Штрихкод
//Количество
//Вес
//Сумма
//Цена за единицу веса
//Группа
//Срок продажи
//Срок использования
//Дата упаковки
//Вес тары
//Скидка
//Произвольный текст 1-5
//Свойство 1-5
	
	тзТаблицаДляПечати = Новый ТаблицаЗначений;	
	
	// Выбираем только помеченные строки
	Отбор = Новый Структура();
	Отбор.Вставить("Выбран",Истина);
	тзТаблицаДляПечати = тзТовары.Скопировать(Отбор);
	
	// добавим нову колонку
	тзТаблицаДляПечати.Колонки.Добавить("Наименование",,);
	тзТаблицаДляПечати.Колонки.Добавить("НаименованиеХарактеристики",,);
	
	// Создадим Заголовок у колонок, чтобы из него кидать наименование колонки в оборудование
	Для Каждого Колонка Из тзТаблицаДляПечати.Колонки Цикл
		Колонка.Заголовок = Колонка.Имя;
	КонецЦикла;
	тзТаблицаДляПечати.Колонки.Количество.Заголовок = "ЧислоКопий";	
	тзТаблицаДляПечати.Колонки.ЦенаЕд.Заголовок = "Цена за единицу веса"; 
	тзТаблицаДляПечати.Колонки.СрокПродажи.Заголовок = "Срок продажи"; 
	тзТаблицаДляПечати.Колонки.СрокИспользования.Заголовок = "Срок использования"; 
	тзТаблицаДляПечати.Колонки.ДатаУпаковки.Заголовок = "Дата упаковки"; 
	тзТаблицаДляПечати.Колонки.ВесТары.Заголовок = "Вес тары"; 
	тзТаблицаДляПечати.Колонки.КоличествоТовара.Заголовок = "Количество"; 
	тзТаблицаДляПечати.Колонки.ПроизвольныйТекст1.Заголовок = "Произвольный текст 1"; 
	тзТаблицаДляПечати.Колонки.ПроизвольныйТекст2.Заголовок = "Произвольный текст 2"; 
	тзТаблицаДляПечати.Колонки.ПроизвольныйТекст3.Заголовок = "Произвольный текст 3"; 
	тзТаблицаДляПечати.Колонки.ПроизвольныйТекст4.Заголовок = "Произвольный текст 4"; 
	тзТаблицаДляПечати.Колонки.ПроизвольныйТекст5.Заголовок = "Произвольный текст 5"; 
	тзТаблицаДляПечати.Колонки.НаименованиеХарактеристики.Заголовок = "Свойство 1";
	тзТаблицаДляПечати.Колонки.Артикул.Заголовок = "Артикул";
	тзТаблицаДляПечати.Колонки.СтранаПроисхождения.Заголовок = "Страна происхождения";
	
	// Заполним наименование для каждого товара
	Для Каждого Стр Из тзТаблицаДляПечати Цикл
		Стр.Наименование = Стр.Номенклатура.Наименование;
		Стр.НаименованиеХарактеристики = Стр.Характеристика.Наименование;
	КонецЦикла;
	
	// удаляем лишние колонки
	тзТаблицаДляПечати.Колонки.Удалить("Выбран");
	//тзТаблицаДляПечати.Колонки.Удалить("Цена");
	тзТаблицаДляПечати.Колонки.Удалить("Коэффициент");
	//тзТаблицаДляПечати.Колонки.Удалить("Номенклатура");
	тзТаблицаДляПечати.Колонки.Удалить("НомерСтроки");
	//тзТаблицаДляПечати.Колонки.Удалить("ЕдиницаИзмерения");
	//тзТаблицаДляПечати.Колонки.Удалить("Характеристика");
	
	тзТаблицаДляПечати.Колонки.НаименованиеХарактеристики.Заголовок = "Характеристика";
	
	// Проведем добавление начального символа FNC1
	Если Типзнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказКодовМаркировки") Тогда
		
		ОбъектШК = Обработки.ШтрихКоды.Создать();
		
		Для Каждого ТекущаяСтрока Из тзТаблицаДляПечати Цикл
			СтруктураРазбора = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.ШтрихКод);
			Если СтруктураРазбора.Разобран И СтруктураРазбора.Свойство("ДанныеШтрихкода") Тогда
				МассивЧастейШК = Новый Массив;
				Для Каждого ЧастьШтрихКода Из СтруктураРазбора.ДанныеШтрихкода Цикл
					МассивЧастейШК.Добавить(Символ(29) + ЧастьШтрихКода.Ключ + ЧастьШтрихКода.Значение.Значение);
				КонецЦикла;
				Если МассивЧастейШК.Количество() > 1 Тогда
					ТекущаяСтрока.ШтрихКод = СтрСоединить(МассивЧастейШК, "");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	тзТовары = тзТаблицаДляПечати.Скопировать();
	
КонецПроцедуры

Процедура УстановитьДатуПечатиКодовМаркировки()
	
	СписокКодовМаркировки = Товары.ВыгрузитьКолонку("ШтрихКод");
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	ЭкранированныйСимвол = ОбъектШК.ЭкранированныйСимволGS1();
	РазделительGS1 = ОбъектШК.РазделительGS1();
	
	Для Индекс = 0 По СписокКодовМаркировки.Количество() - 1 Цикл
		СписокКодовМаркировки[Индекс] = СтрЗаменить(СписокКодовМаркировки[Индекс], ЭкранированныйСимвол, РазделительGS1);
	КонецЦикла;
	
	ДатаПечати = ТекущаяДатаСеанса();
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КодыМаркировкиДляПечати.ДокументОснование КАК ДокументОснование,
	               |	КодыМаркировкиДляПечати.Номенклатура КАК Номенклатура,
	               |	КодыМаркировкиДляПечати.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	КодыМаркировкиДляПечати.КодМаркировки КАК КодМаркировки
	               |ИЗ
	               |	РегистрСведений.КодыМаркировкиДляПечати КАК КодыМаркировкиДляПечати
	               |ГДЕ
	               |	КодыМаркировкиДляПечати.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказКодовМаркировки.ПустаяСсылка)
	               |	И КодыМаркировкиДляПечати.КодМаркировки В(&СписокКодовМаркировки)
	               |	И КодыМаркировкиДляПечати.ДатаПечати <> ДАТАВРЕМЯ(1, 1, 1)";
	Запрос.УстановитьПараметр("СписокКодовМаркировки", СписокКодовМаркировки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаписьКМ = РегистрыСведений.КодыМаркировкиДляПечати.СоздатьМенеджерЗаписи();
		ЗаписьКМ.ДокументОснование = Выборка.ДокументОснование;
		ЗаписьКМ.Номенклатура = Выборка.Номенклатура;
		ЗаписьКМ.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		ЗаписьКМ.ДатаПечати = ДатаПечати;
		ЗаписьКМ.КодМаркировки = Выборка.КодМаркировки;
		ЗаписьКМ.Записать(Истина);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

///Печать этикеток и ценников на принтере ценников
//Параметры:
//	РежимПечати - число (0-печать этикеток, 1-печать ценников)
//  Таблица - таблица значений с заполненными данными
Процедура ПечатьЭтикетокНаПринтереЦенников(РежимПечати) Экспорт
	Если обЗначениеНеЗаполнено(ЭтикеткиОборудованиеДляПечати)и(РежимПечати=0) Тогда
		Сообщить("Не выбрано оборудование для печати!"); Возврат;
	ИначеЕсли обЗначениеНеЗаполнено(ЦенникиОборудованиеДляПечати)и(РежимПечати=1) Тогда
		Сообщить("Не выбрано оборудование для печати!"); Возврат;
	КонецЕсли;
			
	ТаблицаПечати=Новый ТаблицаЗначений;
	ТаблицаПечати=Товары.Выгрузить();
	
	// Подготавливаем тз для отправки в ПЭЦ
	ПодготовитьТаблицуДляПечатиНаПЭЦ3(ТаблицаПечати);

	РазмерСписка=ТаблицаПечати.Количество();	
	
	Если РазмерСписка = 0 Тогда
		Возврат; 
	КонецЕсли;
	
	// Подготовка данных для передачи оборудованию
	Данные = Рарус_Компонента.СоздатьПараметры(3, 1);
	
	КоличествоКолонок = ТаблицаПечати.Колонки.Количество();
	ТабличныеПоляИмена = Рарус_Компонента.СоздатьПараметры(КоличествоКолонок, 1); // количество "табличных" параметров
	МассивЗначенийТП = Рарус_Компонента.СоздатьПараметры(РазмерСписка, КоличествоКолонок); // 1ое - количество видов этикеток, 2ое - количество "табличных" параметров
	
	смещение=0;
	Если ПустаяСтрока(?(РежимПечати=0,?(обЗначениеНеЗаполнено(ЭтикеткиШаблонДляПечати),"",ЭтикеткиШаблонДляПечати),?(обЗначениеНеЗаполнено(ЦенникиШаблонДляПечати),"",ЦенникиШаблонДляПечати))) Тогда
		ШапочныеПоля = Рарус_Компонента.СоздатьПараметры(6, 2); // количество "шапочных" параметров
		смещение=1;
	Иначе	
		ШапочныеПоля = Рарус_Компонента.СоздатьПараметры(7, 2); // количество "шапочных" параметров	
		ШапочныеПоля.SetRowValues(0, "Шаблон", ?(РежимПечати=0,ЭтикеткиШаблонДляПечати,ЦенникиШаблонДляПечати)); // шаблон только в локальном каталоге
	КонецЕсли;	
	ШапочныеПоля.SetRowValues(1-смещение, "Реклама1", "");
	ШапочныеПоля.SetRowValues(2-смещение, "Реклама2", "");
	ШапочныеПоля.SetRowValues(3-смещение, "Реклама3", "");
	ШапочныеПоля.SetRowValues(4-смещение, "Картинка 1", ""); // Имя картинки. Файл картинки только в локальном каталоге
	ШапочныеПоля.SetRowValues(5-смещение, "Картинка 2", ""); // Имя картинки. Файл картинки только в локальном каталоге
	ШапочныеПоля.SetRowValues(6-смещение, "Картинка 3", ""); // Имя картинки. Файл картинки только в локальном каталоге
	
	// заполнить таблицу с наименованиями колонок
	Для Сч = 0 По КоличествоКолонок-1 Цикл         
		НаимКолонки = "";
		НаимКолонки = ТаблицаПечати.Колонки.Получить(Сч).Заголовок;
		ТабличныеПоляИмена.SetValue(Сч, 0, НаимКолонки); 
	КонецЦикла;   
	
	// заполнить таблицу со значениями
	Количество = 0;
	КоличествоСтрок = ТаблицаПечати.Количество();
    Для СчСтр = 0 По КоличествоСтрок-1 Цикл      
    	Для СчКол = 0 По КоличествоКолонок-1 Цикл      
			Значение = Строка(ТаблицаПечати.Получить(СчСтр).Получить(СчКол));
			МассивЗначенийТП.SetValue(СчСтр,СчКол,Значение);		
		КонецЦикла;            
		Количество = Количество + ТаблицаПечати.Получить(СчСтр).Количество;
	КонецЦикла;   
	
	Данные.SetValue(0, 0, ШапочныеПоля);
	Данные.SetValue(1, 0, ТабличныеПоляИмена);
	Данные.SetValue(2, 0, МассивЗначенийТП);

	Если РежимПечати=0 Тогда
		GUID=ЭтикеткиОборудованиеДляПечати.ИдентификаторОборудования;
	Иначе
		GUID=ЦенникиОборудованиеДляПечати.ИдентификаторОборудования;
	КонецЕсли;
	
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID,"ВыполнитьПечать",Данные,30);
	
	// Информируем пользователя о завершении печати
	Состояние("Печать этикеток завершена.");
	
	Попытка
		Если КодОшибки=0 Тогда
			Предупреждение("Печать успешно завершена.");
			УстановитьДатуПечатиКодовМаркировки();
		Иначе
			Предупреждение("Ошибка печати: "+Рарус_Компонента.ОписаниеОшибки);
		КонецЕсли;
	Исключение
		Сообщить("Неизвестная ошибка при печати");
	КонецПопытки;
КонецПроцедуры

// Производит обработку реквизита при его выборе
// Параметры:
//	Имя - имя обрабатываемого реквизита
//	ТекСтрока - строка табличной части
//	ЭтаФорма - форма выбора
//	Дополнительные параметры - строка доп параметров
Функция ОбработкаРеквизита(Имя, ТекСтрока, ЭтаФорма, ДопПараметры="") Экспорт
	
	Если Имя="Номенклатура" Тогда
		// получим необходимые нам переменные
		Номенклатура = ТекСтрока.Номенклатура;
		Количество = ТекСтрока.КоличествоТовара;
		ШтрихКод = ТекСтрока.ШтрихКод;
		КЭтикеток = ТекСтрока.Количество;
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(Количество) Тогда
			Количество = 1;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(КЭтикеток) Тогда
			КЭтикеток = 1;
		КонецЕсли;
		
		ПроизвольныйТекст1 = "";   
		
		//anton
		ПроизвольныйТекст2 = "";
		//anton
		
		// произведем необходимые преобразования и расчеты
		Если ТипЗнч(Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения;
			Коэффициент = ЕдиницаИзмерения.Коэффициент;
			Характеристика = ПолучитьХарактеристикуТовара(Номенклатура);
			Артикул = Номенклатура.Артикул;
			ПроизвольныйТекст1 = Номенклатура.Производитель.Наименование;   
			
			//anton
			ПроизвольныйТекст2 = ПолучитьЯчейкуДляНоменклатуры(Номенклатура);
			//anton
			
			СтранаПроисхождения = Номенклатура.СтранаПроисхождения;
		ИначеЕсли ТипЗнч(Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
			ЕдиницаИзмерения = Справочники.Нормочасы.ПустаяСсылка();
			Коэффициент = 1;
			Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			Артикул = Номенклатура.Артикул;
			СтранаПроисхождения = Справочники.КлассификаторСтранМира.ПустаяСсылка();
		Иначе
			ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
			Коэффициент = 1;
			Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			Артикул = "";
			СтранаПроисхождения = Справочники.КлассификаторСтранМира.ПустаяСсылка();
		КонецЕсли;
		
		мсШК = ПолучитьШКТовара(Номенклатура, ЕдиницаИзмерения, Характеристика);
		Если мсШК.Найти(ШтрихКод) = Неопределено Тогда
			ШтрихКод = мсШК[0];
		КонецЕсли;
		
		// заполняем ТЧ и без вызова обработчиков
		// Количество
		ТекСтрока.КоличествоТовара = Количество;
		// ЕдиницаИзмерения 
		ТекСтрока.ЕдиницаИзмерения = ЕдиницаИзмерения;
		// Характеристика
		ТекСтрока.Характеристика = Характеристика;
		// Коэффициент
		ТекСтрока.Коэффициент = Коэффициент;
		// ШтрихКод
		ТекСтрока.ШтрихКод = ШтрихКод;
		//Выбран
		ТекСтрока.Выбран = Истина;
		// Количество этикеток
		ТекСтрока.Количество = КЭтикеток;
		// Артикул
		ТекСтрока.Артикул = Артикул;
		// Страна происхождения
		ТекСтрока.СтранаПроисхождения = СтранаПроисхождения;
		// Производитель
		ТекСтрока.ПроизвольныйТекст1 = ПроизвольныйТекст1; 
		
		//anton
		//ТекСтрока.ПроизвольныйТекст1 = ПроизвольныйТекст1;
		ТекСтрока.ПроизвольныйТекст2 = ПроизвольныйТекст2;
		//anton
		
		// пересчет строки
		РасчетСтроки(ТекСтрока)
		
	ИначеЕсли Имя="Количество" Тогда
		// получим необходимые нам переменные
		Количество = ТекСтрока.КоличествоТовара;
		ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Количество) Тогда 
			Возврат 0;
		КонецЕсли; 
		
		// произведем необходимые преобразования и расчеты
		Точность = ЕдиницаИзмерения.Точность;
		Количество = Окр(Количество, Точность, 1);
		
		// заполняем ТЧ и вызываем соответствующие обработчики   
		//Количество
		Если ТекСтрока.КоличествоТовара <> Количество Тогда
			ТекСтрока.КоличествоТовара = Количество;
			ОбработкаРеквизита("Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			Предупреждение("Для данного товара количество можно задавать только с точностью: " + Строка(Точность), 10);
		КонецЕсли;
		
		// пересчет строки
		РасчетСтроки(ТекСтрока)
		
	ИначеЕсли Имя="ЕдиницаИзмерения" Тогда
		// получим необходимые нам переменные
		ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
		ШтрихКод = ТекСтрока.ШтрихКод;
		Номенклатура = ТекСтрока.Номенклатура;
		Характеристика = ТекСтрока.Характеристика;
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			Возврат 0;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Номенклатура) Тогда
			Возврат 0;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Характеристика) Тогда
			Возврат 0;
		КонецЕсли;
		
		// произведем необходимые преобразования и расчеты
		Коэффициент = ЕдиницаИзмерения.Коэффициент;
		мсШК = ПолучитьШКТовара(Номенклатура, ЕдиницаИзмерения, Характеристика);
		Если мсШК.Найти(ШтрихКод) = Неопределено Тогда
			ШтрихКод = мсШК[0];
		КонецЕсли;

		// заполняем ТЧ и вызываем соответствующие обработчики
		// Коэффициент
        ТекСтрока.Коэффициент = Коэффициент;
		ОбработкаРеквизита("Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		// ШтрихКод
		ТекСтрока.ШтрихКод = ШтрихКод;

	ИначеЕсли Имя="Цена" Тогда
		
		// пересчет строки
		РасчетСтроки(ТекСтрока)

	ИначеЕсли Имя="Коэффициент" Тогда
		// пересчет строки
		РасчетСтроки(ТекСтрока)

	ИначеЕсли Имя="Вес" Тогда
	ИначеЕсли Имя="ЦенаЕд" Тогда
	ИначеЕсли Имя="ВесТары" Тогда

	ИначеЕсли Имя="Сумма" Тогда
	ИначеЕсли Имя="Характеристика" Тогда
		// получим добавляемую нами позицию
		Номенклатура = ТекСтрока.Номенклатура;
		ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
		Характеристика = ТекСтрока.Характеристика;
		ШтрихКод = ТекСтрока.ШтрихКод;

		// проверим переменные
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			Возврат 0;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Номенклатура) Тогда
			Возврат 0;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Характеристика) Тогда
			Возврат 0;
		КонецЕсли;
		
		// произведем необходимые преобразования и расчеты
		мсШК = ПолучитьШКТовара(Номенклатура, ЕдиницаИзмерения, Характеристика);
		Если мсШК.Найти(ШтрихКод) = Неопределено Тогда
			ШтрихКод = мсШК[0];
		КонецЕсли;
		
		// заполняем ТЧ и вызываем соответствующие обработчики
		// ШтрихКод
		ТекСтрока.ШтрихКод = ШтрихКод;
		
		РасчетСтроки(ТекСтрока);
	КонецЕсли;
		
КонецФункции

//anton
Функция ПолучитьЯчейкуДляНоменклатуры(Номенклатура)
	
	Если НЕ обЗначениеНеЗаполнено(СкладДляЯчейки) Тогда		
		
		РС=РегистрыСведений.ЯчейкиХраненияПоУмолчанию.СоздатьНаборЗаписей();
		РС.Отбор.Номенклатура.Установить(Номенклатура);
		РС.Отбор.СкладКомпании.Установить(СкладДляЯчейки);
		РС.Прочитать();
		
		Если РС.Количество() >0 Тогда
			Возврат СокрЛП(Строка(РС[0].ЯчейкаХранения));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Функция поиск товара по штрихкоду
//
// Параметры:
//  ШтрихКод - штрихкод товара, по которому ведется поиск
//
// Возвращаемое значение:
//  структура
//
Функция НайтиСтруктуруПоШтрихКоду(ШтрихКод, флРугатьсяНаЗапрещенныйШтрихКод=Ложь) Экспорт
	ПрефиксВесовогоШК = Константы.ПрефиксВесовогоШК.Получить();
	ПрефиксШтучногоШК = Константы.ПрефиксШтучногоШК.Получить();

	ШК=СокрЛП(Формат(ШтрихКод,"ЧГ=0")); Вес=0;
	Если ПустаяСтрока(ШК) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	ЭкранированныйСимвол = ОбъектШК.ЭкранированныйСимволGS1();
	РазделительGS1 = ОбъектШК.РазделительGS1();
	
	ШК = ОбъектШК.РазобратьШтрихкодПоТипам(ШК);
	
	// проверим, может это ШК для весового товара
	Попытка
		ПрефиксШтрихКода=Число(Лев(ШК,2));
	Исключение
		ПрефиксШтрихКода= 0;
	КонецПопытки;
	Если ПрефиксШтрихКода=ПрефиксВесовогоШК Тогда
		// Вытащим вес
		Вес=Сред(ШК,7+1,СтрДлина(ШК)-8);
		Если НЕ ПустаяСтрока(Вес) Тогда
			Вес=Число(Вес)/1000;
		Иначе
			Вес=0;
		КонецЕсли;
		//если товар весовой, то ищем по первым семи символам
		ШК=Формат(Лев(ШК,7),"ЧГ=0");
	КонецЕсли; 
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ШтрихКод", ШК);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ШтрихКоды.Объект,
	|	ШтрихКоды.ШтрихКод,
	|	ШтрихКоды.ЕдиницаИзмерения,
	|	ШтрихКоды.ХарактеристикаНоменклатуры,
	|	ШтрихКоды.Запрет
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|ГДЕ
	|	ШтрихКоды.ШтрихКод = &ШтрихКод
	|";

	Выборка = Запрос.Выполнить().Выбрать();
	СтуктураОбъект = Новый Структура();

	Если Выборка.Следующий() Тогда
		Если флРугатьсяНаЗапрещенныйШтрихКод И Выборка.Запрет Тогда
			Предупреждение("Запрещенный штрихкод", 10);
			Возврат Неопределено;
		КонецЕсли;
		ТекОбъект = Выборка.Объект;
		Если ТипЗнч(ТекОбъект)=Тип("СправочникСсылка.Номенклатура") Тогда
			//В системе с данным штрихкодом зарегистрирована номенклатура
			ИспользованиеШтрихКодов=ТекОбъект.ТипНоменклатуры.ИспользованиеШтрихКодов;
			СтуктураОбъект.Вставить("Объект",ТекОбъект);
			Если ИспользованиеШтрихКодов = 2 Тогда
				СтуктураОбъект.Вставить("ЕдиницаИзмерения",Выборка.ЕдиницаИзмерения);
			ИначеЕсли ИспользованиеШтрихКодов = 3 Тогда
				СтуктураОбъект.Вставить("ХарактеристикаНоменклатуры",Выборка.ХарактеристикаНоменклатуры);
			КонецЕсли; 
			СтуктураОбъект.Вставить("Вес",Вес);
			Возврат СтуктураОбъект;
		ИначеЕсли ТипЗнч(ТекОбъект)=Тип("СправочникСсылка.Карточки") Тогда
			//В системе с данным штрихкодом зарегистрирована карточка
			СтуктураОбъект.Вставить("Объект",ТекОбъект);
			Возврат СтуктураОбъект;
		КонецЕсли; 
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // НайтиСтруктуруПоШтрихКоду()

// Добавление номенклатуры
Процедура ДобавитьНоменклатуру(ЭтаФорма, Номенклатура,ХарактеристикаНоменклатуры="",ЕдиницаИзмерения="",Количество=1, ШтрихКод = "") Экспорт
	
	Если обЗначениеНезаполнено(Номенклатура) Тогда	Возврат; КонецЕсли;
					
	// подготовим структуру отбора для поиска товара в ТЧ
	СтруктураОтбора=Новый Структура("Номенклатура",Номенклатура);

	Если ЕдиницаИзмерения<>Неопределено Тогда
		СтруктураОтбора.Вставить("ЕдиницаИзмерения",ЕдиницаИзмерения);
	КонецЕсли;
	
	Если ХарактеристикаНоменклатуры<>Неопределено Тогда
		СтруктураОтбора.Вставить("Характеристика",ХарактеристикаНоменклатуры);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ШтрихКод) Тогда
		СтруктураОтбора.Вставить("ШтрихКод", ШтрихКод);
	КонецЕсли;

	// проверим есть ли в ТЧ хоть одна строка, удовлетворяющая нашим условиям
	СтрокаНоменклатура = Неопределено;
	МассивСтрок = Товары.НайтиСтроки(СтруктураОтбора);
	Если МассивСтрок.Количество() > 0 Тогда
		СтрокаНоменклатура = МассивСтрок[0];
	КонецЕсли;
	
	//если товар в ТЧ не найден то добавим строку
	Если обЗначениеНеЗаполнено(СтрокаНоменклатура) Тогда
		//добавим новую строчку в ТЧ 
		СтрокаНоменклатура = Товары.Добавить();
		СтрокаНоменклатура.Номенклатура = Номенклатура;
		Если ТипЗнч(ЕдиницаИзмерения)=Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
			СтрокаНоменклатура.ЕдиницаИзмерения = ЕдиницаИзмерения;
		КонецЕсли; 
		Если ТипЗнч(ХарактеристикаНоменклатуры)=Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
			СтрокаНоменклатура.Характеристика = ХарактеристикаНоменклатуры;
		КонецЕсли; 
		Если ТипЗнч(Количество)=Тип("Число") Тогда
			СтрокаНоменклатура.КоличествоТовара=Количество;
		КонецЕсли;
		
		// Обработаем реквизиты строки ТЧ
		ОбработкаРеквизита("Номенклатура",СтрокаНоменклатура,ЭтаФорма);
        СтрокаНоменклатура.Выбран=Истина;
        СтрокаНоменклатура.Количество = 1;
		
		Если НЕ ПустаяСтрока(ШтрихКод) Тогда
			СтрокаНоменклатура.ШтрихКод = ШтрихКод;
		КонецЕсли;
		
	Иначе
		Если ТипЗнч(Количество)=Тип("Число") Тогда
			СтрокаНоменклатура.Количество = СтрокаНоменклатура.Количество + Количество;
		Иначе
			СтрокаНоменклатура.Количество = СтрокаНоменклатура.Количество + 1;
		КонецЕсли;	
		// Обработаем реквизиты строки ТЧ
		ОбработкаРеквизита("Количество",СтрокаНоменклатура,ЭтаФорма);

	КонецЕсли;
	
					
КонецПроцедуры;


ЭтикеткиКоличествоВСтолбце=11;
ЭтикеткиКоличествоВСтроке=4;
ЦенникиКоличествоВСтолбце=3;
ЦенникиКоличествоВСтроке=2;
Рарус_Компонента = глРарус_Компонента;

#КонецЕсли

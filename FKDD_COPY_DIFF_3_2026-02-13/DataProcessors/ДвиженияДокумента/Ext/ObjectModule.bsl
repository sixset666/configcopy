#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем ТабличноеПолеВыбор;     // Выбранное табличное поле
Перем КоманднаяПанельСправка; // Справка командной панели

///////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// процедура переформирования формы
//
// Параметр:
//	ЭтаФорма	- Форма		- Переданная форма
//
Процедура Переформировать(ЭтаФорма = Неопределено) Экспорт
	// Получим форму
	Если ЭтаФорма = Неопределено Тогда
		ЭтаФорма = ЭтотОбъект.ПолучитьФорму();
	КонецЕсли;
	
	// Проверим стоит ли вообще что-либо делать
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Видимость = ЛОЖЬ;
	Если обЗначениеНеЗаполнено(ВыбранныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	// Произведем очищение страниц основной панели
	ИмяТекущейСтраницы = ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница.Имя;
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы.Очистить();
	
	
	// ПЕРВАЯ СТРАНИЦА: Отчет "Все движения"
	
	// Добавим первую страницу с отчетом "Движения документа"
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[0].Имя		= "ВсеДвижения";
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[0].Заголовок	= "Все движения";
	
	// Расположим на текущей страница элемент управления поле табличного документа
	ТабличныйДокумент = ЭтаФорма.ЭлементыФормы.Добавить(Тип("ПолеТабличногоДокумента"),"ПолеТабличногоДокументаВсеДвижения", ИСТИНА, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель);
	
	// Произведем настройку созданных элементов управления
	ТабличныйДокумент.Лево	= 4;
	ТабличныйДокумент.Верх	= 4;
	ТабличныйДокумент.Ширина= ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Ширина - (4 + 1)*2;
	ТабличныйДокумент.Высота= ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Высота - 28;
	
	ТабличныйДокумент.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Право);
	ТабличныйДокумент.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Низ);
	
	// Заполним табличный документ при помощи отчета "Движения документа"
	ОтчетДвиженияДокумента = Отчеты.ДвиженияДокумента.Создать();
	ОтчетДвиженияДокумента.Документ = ВыбранныйДокумент;
	ОтчетДвиженияДокумента.СформироватьОтчет(ТабличныйДокумент);
	
	
	// ПОСЛЕДУЮЩИЕ СТРАНИЦЫ: По регистрам
	
	// Получим информацию по каким регистрам документ сделал движения
	ШаблонЗапроса = "ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""$ВидРегистра$"" КАК Вид,
	|	""$ИмяРегистра$"" КАК Имя,
	|	""$СинонимРегистра$"" КАК Синоним,
	|	КОЛИЧЕСТВО($ИмяРегистра$.НомерСтроки) КАК КоличествоДвижений
	|ИЗ
	|	Регистр$ВидРегистра$.$ИмяРегистра$ КАК $ИмяРегистра$
	|ГДЕ
	|	$ИмяРегистра$.Регистратор = &Регистратор
	|
	|";
	
	ТекстЗапроса = "//";
	Для каждого ТекРегистр Из ВыбранныйДокумент.Метаданные().Движения Цикл
		// Определим вид регистра
		Если Метаданные.РегистрыНакопления.Индекс(ТекРегистр) >= 0 Тогда
			ВидРегистра = "Накопления"
		ИначеЕсли Метаданные.РегистрыСведений.Индекс(ТекРегистр) >= 0 Тогда
			ВидРегистра = "Сведений"
		ИначеЕсли  Метаданные.РегистрыРасчета.Индекс(ТекРегистр) >= 0 Тогда
			ВидРегистра = "Расчета"
		ИначеЕсли  Метаданные.РегистрыБухгалтерии.Индекс(ТекРегистр) >= 0 Тогда
			ВидРегистра = "Бухгалтерии"
		КонецЕсли;
		
		// Заполним шаблон фрагмента запроса
		Фрагмент = СтрЗаменить(ШаблонЗапроса,"$ВидРегистра$",ВидРегистра);
		Фрагмент = СтрЗаменить(Фрагмент		,"$ИмяРегистра$",ТекРегистр.Имя);
		Фрагмент = СтрЗаменить(Фрагмент		,"$СинонимРегистра$",ТекРегистр.Синоним);
		ТекстЗапроса = ТекстЗапроса + Фрагмент;
	КонецЦикла;
	Если ТекстЗапроса = "//" Тогда Возврат; КонецЕсли; // Документ не связан с регистром :(
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Регистратор", ВыбранныйДокумент);
	РезультатЗапроса = Запрос.Выполнить();
	ЕстьДвижения = (РезультатЗапроса.Выгрузить().Итог("КоличествоДвижений") > 0);
	
	
	// Обходим движения документа, создаем закладки на панели формы и добавляем табличные поля списков
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		// Стоит ли вообще что-то делать
		Если ЕстьДвижения И (Выборка.КоличествоДвижений = 0) Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавим новую страницу на панель и поместим на нее табличное поле списка регистра
		ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы.Добавить(Выборка.Имя,Выборка.Синоним);
		ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы.Количество()-1];
		
		// Расположим на текущей странице необходимые элементы управления
		КоманднаяПанель	= ЭтаФорма.ЭлементыФормы.Добавить(Тип("КоманднаяПанель"),"КоманднаяПанель"+ Выборка.Имя, ИСТИНА, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель);
		ТабличноеПоле	= ЭтаФорма.ЭлементыФормы.Добавить(Тип("ТабличноеПоле")	,"ТабличноеПоле"  + Выборка.Имя, ИСТИНА, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель);
		
		// Свяжем командную панель с таблицей и установим обработчик события для табличного поля
		КоманднаяПанель.ИсточникДействий= ТабличноеПоле;
		КоманднаяПанель.Автозаполнение	= ИСТИНА;
		ТабличноеПоле.УстановитьДействие("Выбор", ТабличноеПолеВыбор);
		
		// Свяжем табличное поле с данными и произведем дополнительные настройки
		ТипСписка = "Регистр" + Выборка.Вид + "НаборЗаписей." + Выборка.Имя;
		ТабличноеПоле.ТипЗначения = Новый ОписаниеТипов(ТипСписка);
		ТабличноеПоле.СоздатьКолонки();
		
		ТабличноеПоле.Колонки.Регистратор.Видимость = ЛОЖЬ;
		ТабличноеПоле.Значение.Отбор.Регистратор.Установить(ВыбранныйДокумент, ИСТИНА);
		ТабличноеПоле.Значение.Прочитать();
		
		// Уберем с видимости служебные поля
		Если ТабличноеПоле.Колонки.Найти("КодМаркировкиBase64") <> Неопределено Тогда
			ТабличноеПоле.Колонки.КодМаркировкиBase64.Видимость = Ложь;
		КонецЕсли;
		
		Если Выборка.Вид="Накопления" Тогда
			ТабличноеПоле.Подвал = Истина;					
			// Пройдемся по всем ресурсам регистра
			ОбъектМетаданных = Метаданные.РегистрыНакопления[Выборка.Имя];
			Для каждого ТекРесурс Из ОбъектМетаданных.Ресурсы Цикл
				Колонка = ТабличноеПоле.Колонки[ТекРесурс.Имя];
				Колонка.ОтображатьВПодвале            = Истина;
				Колонка.ГоризонтальноеПоложениеВШапке = ГоризонтальноеПоложение.Право;
				Колонка.ОтображатьИтогиВПодвале       = Истина;
			КонецЦикла;
            			
		КонецЕсли;

		// На командную панель добавим кнопку открытия справки по данному регистру
		КоманднаяПанель.Кнопки.Добавить("РазделительСправка", ТипКнопкиКоманднойПанели.Разделитель);
		Справка = КоманднаяПанель.Кнопки.Добавить("Регистры"+Выборка.Вид, ТипКнопкиКоманднойПанели.Действие, "Справка", КоманднаяПанельСправка);
		Справка.Картинка	= БиблиотекаКартинок.Справка;
		Справка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
		
		// Произведем настройку созданных элементов управления
		КоманднаяПанель.Лево	= 4;
		КоманднаяПанель.Верх	= 4;
		КоманднаяПанель.Ширина	= ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Ширина - (4 + 1)*2;
		КоманднаяПанель.Высота	= 24;
		
		ТабличноеПоле.Лево		= КоманднаяПанель.Лево;
		ТабличноеПоле.Верх		= КоманднаяПанель.Верх + 24;
		ТабличноеПоле.Ширина	= КоманднаяПанель.Ширина;
		ТабличноеПоле.Высота	= ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Высота - ТабличноеПоле.Верх - 24;
		
		ТабличноеПоле.ТолькоПросмотр		= ИСТИНА;
		ТабличноеПоле.РежимВыделенияСтроки	= РежимВыделенияСтрокиТабличногоПоля.Строка;
		ТабличноеПоле.ЧередованиеЦветовСтрок= ИСТИНА;
		ТабличноеПоле.ГоризонтальныеЛинии	= ИСТИНА;
		ТабличноеПоле.ВертикальныеЛинии		= ЛОЖЬ;
		ТабличноеПоле.ЦветТекстаПодвала		= ЦветаСтиля.ТекстПодвалаТабличногоПоля;
		
		КоманднаяПанель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Право);
		ТабличноеПоле.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Право);
		ТабличноеПоле.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Низ);
	КонецЦикла; // Цикл по движениям документа
	
	// ПОСЛЕДНЯЯ СТРАНИЦА: "Активы и пассивы"	  
	
	// Добавим последнюю страницу с отчетом "Активы и пассивы"
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы.Добавить("АктивыИПассивы","Активы и пассивы");
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы.Количество()-1];

	// Расположим на текущей страница элемент управления поле табличного документа
	ТабличныйДокумент = ЭтаФорма.ЭлементыФормы.Добавить(Тип("ПолеТабличногоДокумента"),"ПолеТабличногоДокументаАктивыИПассивы", ИСТИНА, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель);
	
	// Произведем настройку созданных элементов управления
	ТабличныйДокумент.Лево	= 4;
	ТабличныйДокумент.Верх	= 4;
	ТабличныйДокумент.Ширина= ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Ширина - (4 + 1)*2;
	ТабличныйДокумент.Высота= ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Высота - 28;
	
	ТабличныйДокумент.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Право);
	ТабличныйДокумент.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ЭтаФорма.ЭлементыФормы.ОсновнаяПанель, ГраницаЭлементаУправления.Низ);
	
	// Заполним табличный документ при помощи отчета "Движения документа"
	Отч = Отчеты.АктивыИПассивы.Создать();	
	Отч.ВыбРегистратор = ВыбранныйДокумент;
	Отч.пкДетализация = 3;
	Отч.пкФильтр = 3;                                  
	Отч.ДатаНачала = ВыбранныйДокумент.Дата;
	Отч.ДатаКонца = ВыбранныйДокумент.Дата;
	Отч.ИмяМакетаОформленияТабличногоДокумента = "МакетОформлениеОтчета";
	Отч.СформироватьТабличныйДокумент(ТабличныйДокумент);	
	
	// Попробуем установить ранее сохраненную текущую страницу
	Попытка
		ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[ИмяТекущейСтраницы]
	Исключение
		ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[0];
	КонецПопытки;
		
	// Возвращаем видимость основной пенели
	ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Видимость = ИСТИНА;
	
	// Покажем форму обработки
	Если ЭтаФорма.Открыта() Тогда
		ЭтаФорма.Активизировать();
	Иначе
		ЭтаФорма.Открыть();
	КонецЕсли;		
КонецПроцедуры // Переформировать()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ТабличноеПолеВыбор		= Новый Действие("ТабличноеПолеВыбор");
КоманднаяПанельСправка	= Новый Действие("КоманднаяПанельСправка");
#КонецЕсли
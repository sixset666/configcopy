////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	Если ХозОперация=Справочники.ХозОперации.Сторно Тогда
		ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Организация",           1); 
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1); 
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Сторно",         "Сторно", Истина);
	СписокХозОпераций.Добавить("Корректировка",  "Корректировка");	
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Дата" Тогда  				
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Для Каждого ТекНаборЗаписей Из Движения Цикл
			Для Каждого ТекЗапись Из ТекНаборЗаписей Цикл
				ТекЗапись.Период = Дата;	
			КонецЦикла;	
		КонецЦикла;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СПЕЦИАЛЬНОГО НАЗНАЧЕНИЯ
//////////////////////////////////////////////////////////////////////////////////

// Процедура удаляет все движения по документу "Корректировка".
//
Процедура УдалитьДвижения() Экспорт
	Для Каждого Набор Из Движения Цикл
		Набор.Очистить();
		Попытка 
			Набор.Записать();
		Исключение;
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры // УдалитьДвижения()
       
// Функция производит сторнирование выбранного документа,
// т.е. инвертирует все ресурсы у всех регистров, по которым были движения
//
// Параметры:
//  ЗаполняемыйНаборЗаписей - набор записей движения документа,
//  МетаданныеРегистр       - движение.
//
// Возвращаемое значение:
//   Булево	- Добавлена ли страница сторнируемых записей
Функция ЗаполнитьНаборЗаписейСторнирование(ЗаполняемыйНаборЗаписей, МетаданныеРегистр) Экспорт
	ВидРегистра = -1;
	
	Если Метаданные.РегистрыРасчета.Найти(МетаданныеРегистр.Имя)<>Неопределено Тогда
		ВидРегистра = 0;
		НаборЗаписей = РегистрыРасчета[МетаданныеРегистр.Имя].СоздатьНаборЗаписей();
	ИначеЕсли Метаданные.РегистрыБухгалтерии.Найти(МетаданныеРегистр.Имя)<>Неопределено Тогда
		ВидРегистра = 1;
		НаборЗаписей = РегистрыБухгалтерии[МетаданныеРегистр.Имя].СоздатьНаборЗаписей();
	ИначеЕсли Метаданные.РегистрыНакопления.Найти(МетаданныеРегистр.Имя)<>Неопределено Тогда
		ВидРегистра = 2;
		НаборЗаписей = РегистрыНакопления[МетаданныеРегистр.Имя].СоздатьНаборЗаписей();
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	// считывает записи из базы данных по установленному отбору(Регистратор-ДокументОснование). 
	НаборЗаписей.Отбор.Регистратор.Значение = ДокументОснование;
	НаборЗаписей.Прочитать();
	
	Для Каждого ДвижениеСторнируемое Из НаборЗаписей Цикл
		// ЗаполняемыйНаборЗаписей - это табличное поле для отображения записей регистра
		ДвижениеСторно = ЗаполняемыйНаборЗаписей.Добавить();
		
		// копирует движение по регистру: измерения, ресурсы, реквизиты
		ЗаполнитьЗначенияСвойств(ДвижениеСторно, ДвижениеСторнируемое, , "НомерСтроки, Регистратор, ВидДвижения");
		
		// ресурсы	
		Для Каждого МДОбъект Из МетаданныеРегистр.Ресурсы Цикл 		
			ДвижениеСторно[МДОбъект.Имя] = - ДвижениеСторно[МДОбъект.Имя];  		
		КонецЦикла;
		
		ДвижениеСторно.Регистратор = Ссылка;
	
		// реквизиты
		Если ВидРегистра=0 Тогда
			ДвижениеСторно.ПериодРегистрации 	= Дата;
			ДвижениеСторно.ВидРасчета 			= ДвижениеСторнируемое.ВидРасчета;
			ДвижениеСторно.Сторно 				= Истина;
			
			Если МетаданныеРегистр.ПериодДействия Тогда
				ДвижениеСторно.ПериодДействияНачало = ДвижениеСторнируемое.ПериодДействияНачало;
				ДвижениеСторно.ПериодДействияКонец = ДвижениеСторнируемое.ПериодДействияКонец;
			КонецЕсли;
			Если МетаданныеРегистр.БазовыйПериод Тогда
				ДвижениеСторно.БазовыйПериодНачало 	= ДвижениеСторнируемое.БазовыйПериодНачало;
				ДвижениеСторно.БазовыйПериодКонец 	= ДвижениеСторнируемое.БазовыйПериодКонец;
			КонецЕсли;
		ИначеЕсли ВидРегистра=1 Тогда 
			ДвижениеСторно.Период = Дата;
		ИначеЕсли ВидРегистра=2 Тогда  
			Если МетаданныеРегистр.ВидРегистра=Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
				ДвижениеСторно.ВидДвижения = ДвижениеСторнируемое.ВидДвижения;	
			КонецЕсли;
			ДвижениеСторно.Период = Дата;   			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции // ЗаполнитьНаборЗаписейСторнирование()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

Процедура ОчиститьСкладДляДвиженийУслугПоРегиструПродажи()

	ДвиженияПродажи = Движения.Продажи;
	
	Если ДвиженияПродажи.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВидНоменклатурыУслуга = Перечисления.ВидыНоменклатуры.Услуга;
	ТипСкладыКомпании = Тип("СправочникСсылка.СкладыКомпании");
	
    МассивНоменклатуры = ДвиженияПродажи.ВыгрузитьКолонку("Номенклатура");
	ЗапросВидовНоменклатуры = Новый Запрос();
	ЗапросВидовНоменклатуры.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	                                |	Номенклатура.Ссылка КАК Номенклатура,
	                                |	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	                                |ИЗ
	                                |	Справочник.Номенклатура КАК Номенклатура
	                                |ГДЕ
	                                |	Номенклатура.Ссылка В(&Ссылка)";
	ЗапросВидовНоменклатуры.УстановитьПараметр("Ссылка", МассивНоменклатуры);
	ТаблицаВидовНоменклатуры = ЗапросВидовНоменклатуры.Выполнить().Выгрузить();
	
	Для Каждого ДвижениеПродажи Из ДвиженияПродажи Цикл
		
		// Выбираем только услуги
		СтрокаВидаНоменклатуры = ТаблицаВидовНоменклатуры.Найти(ДвижениеПродажи.Номенклатура, "Номенклатура");
		Если СтрокаВидаНоменклатуры.ВидНоменклатуры <> ВидНоменклатурыУслуга Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверяем на заполненность склада
		Если ЗначениеЗаполнено(ДвижениеПродажи.СкладКомпании)
			И ТипЗнч(ДвижениеПродажи.СкладКомпании) = ТипСкладыКомпании Тогда
			
			ДвижениеПродажи.СкладКомпании = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// печать движения
//
// Параметры:
//  ТаблДокумент - табличный документ.
//
Функция ПечатьДвижения(ТаблДокумент=Неопределено) Экспорт
	Если ТаблДокумент=Неопределено Тогда
		ТаблДокумент = Новый ТабличныйДокумент;
	КонецЕсли;
	
	ОтчетДвиженияДокумента = Отчеты.ДвиженияДокумента.Создать();
	ОтчетДвиженияДокумента.Документ                 = Ссылка;
	ОтчетДвиженияДокумента.НеПоказыватьТаблДокумент = Истина;
	ОтчетДвиженияДокумента.СформироватьОтчет(ТаблДокумент); 
	
	Возврат ТаблДокумент;
КонецФункции // ПечатьДвижения()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,,Ложь) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Активность = Истина;
	Иначе
		Активность = Ложь;
	КонецЕсли;

	ОчиститьСкладДляДвиженийУслугПоРегиструПродажи();
	
	Если ХозОперация=Справочники.ХозОперации.Корректировка Тогда
		Для Каждого ТекСтрокаТЧ Из ДвиженияКорректировки Цикл
			НаборЗаписей = Движения[ТекСтрокаТЧ.ИмяРегистра];
			Если ТекСтрокаТЧ.Использование Тогда
				Если (НЕ НаборЗаписей.Выбран()) И (НЕ Ссылка.Пустая()) Тогда 
					НаборЗаписей.Прочитать();
				КонецЕсли;
				Для Каждого ТекДвижение Из НаборЗаписей Цикл
					ТекДвижение.Период = Дата;
					ТекДвижение.Активность = Активность;
				КонецЦикла;
			Иначе
				Если НЕ Ссылка.Пустая() Тогда
					НаборЗаписей.Очистить();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого ТекНаборЗаписей Из Движения Цикл
			Если НЕ ТекНаборЗаписей.Выбран() И (НЕ Ссылка.Пустая()) Тогда 
				ТекНаборЗаписей.Прочитать();
			КонецЕсли;
			Для Каждого ТекЗапись Из ТекНаборЗаписей Цикл
				ТекЗапись.Период     = ДокументОснование.Дата;
				ТекЗапись.Активность = Активность;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;		
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли; 
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим, Истина) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


// Модуль документа ПлатежноеПоручение

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Формирует назначение платежа
//
Процедура СформироватьНазначениеПлатежа() Экспорт

	Если ПеречислениеНалога Тогда
		Возврат;
	КонецЕсли; 
	
	НазначениеПлатежа = "";
	
	ПозицияСуммы=Найти(НазначениеПлатежа,"Сумма");
	
	Если ПозицияСуммы>0 Тогда
		ТекстНазначение=Лев(НазначениеПлатежа,ПозицияСуммы-2);
	Иначе
		ТекстНазначение=НазначениеПлатежа;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстНазначение) ИЛИ ТекстНазначение = "Оплата по: " Тогда
		Если НЕ ОбЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекстНазначение = "Оплата по договору " + Строка(ДоговорВзаиморасчетов);
		КонецЕсли;
	КонецЕсли; 
	
	ТекстСумма="Сумма " + Формат(СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ=") + Символы.ПС;
	Если обЗначениеНеЗаполнено(СтавкаНДС) ИЛИ СтавкаНДС=Справочники.СтавкиНДС.БезНДС Тогда
		ТекстСумма = ТекстСумма + "Без налога (НДС)" + Символы.ПС;
	Иначе
		ТекстСумма = ТекстСумма + "НДС(" + СтавкаНДС + ") " + Формат(СуммаНДС, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ=")+Символы.ПС;
	КонецЕсли; 
	
	НазначениеПлатежа=ТекстНазначение+Символы.ПС+ТекстСумма;

КонецПроцедуры // СформироватьНазначениеПлатежа()

// Проверяет правильность заполнения
//
// Возвращаемое значение:
//  Ошибка - строка - содержит описание ошибок заполнения.
//
Функция Проверить()

	Ошибка = Новый СписокЗначений();

	П101 = СокрЛП(СтатусСоставителя);
	П104 = СокрЛП(КодБК);
	П105 = СокрЛП(КодОКТМО);
	П106 = СокрЛП(ПоказательОснования);
	П107 = ?(ПустаяСтрока(СокрЛП(СтрЗаменить(ПоказательПериода,".","")))=1,"", ПоказательПериода);
	П107 = ?(СокрЛП(СтрЗаменить(ПоказательПериода,".",""))="0","",ПоказательПериода);
	П108 = СокрЛП(ПоказательНомера);
	П109 = ПоказательДаты;
	П110 = СокрЛП(ПоказательТипа);

	Если (Найти("01,02,03,04,05,06,07,08,09,10,11,12,13,14,15", П101) =0) ИЛИ
		(ПустаяСтрока(СокрЛП(П101))) Тогда

		Ошибка.Добавить("Неверное значение поля ""Статус составителя"".");
	КонецЕсли;

	Если (СтрЗаменить(П104,"0","")="")И(Найти("06,07", П101) =0) Тогда

		Ошибка.Добавить("Необходимо заполнить поле ""Код БК"".");
	КонецЕсли;
	
	Если ПустаяСтрока(П105) Тогда
		Ошибка.Добавить("Необходимо заполнить поле ""Код ОКТМО"".");
	КонецЕсли;

	// Проверяем в зависимости от статуса составителя
	Если П101 = "08" Тогда
		Если СтрЗаменить(П106,"0","")<>"" Тогда 
			Ошибка.Добавить("При статусе составителя ""08"" не следует заполнять поле ""Основание платежа"".");
		КонецЕсли;
		Если СтрЗаменить(П107,"0","")<>"" Тогда
			Ошибка.Добавить("При статусе составителя ""08"" не следует заполнять поле ""Период"".");
		КонецЕсли;
		Если СтрЗаменить(П108,"0","")<>"" Тогда
			Ошибка.Добавить("При статусе составителя ""08"" не следует заполнять поле ""Номер"".");
		КонецЕсли;
		Если обЗначениеНеЗаполнено(П109) = Ложь Тогда
			Ошибка.Добавить("При статусе составителя ""08"" не следует заполнять поле ""Дата"".");
		КонецЕсли;
		Если СтрЗаменить(П110,"0","")<>"" Тогда
			Ошибка.Добавить("При статусе составителя ""08"" не следует заполнять поле ""Тип платежа"".");
		КонецЕсли;
	Иначе
		// Проверяем в зависимости от основания платежа
		Если СтрЗаменить(СокрЛП(П106),"0","") = "" Тогда
			Если СтрЗаменить(П107,"0","")<>"" Тогда
				Если обЗначениеНеЗаполнено(П107) Тогда
					Ошибка.Добавить("Возможно, неверно указана дата в поле ""Период""."); 
				КонецЕсли;
			КонецЕсли;
			Если СтрЗаменить(П109,"0","")<>"" Тогда
				Если обЗначениеНеЗаполнено(Дата(П109)) Тогда
					Ошибка.Добавить("Возможно, неверно указана дата в поле ""Дата""."); 
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли СтрДлина(П106)<>2 Тогда
			Ошибка.ДобавитьЗначение("Возможно, неверно заполнено поле ""Основание платежа"".");
			Если СтрЗаменить(П107,"0","")<>"" Тогда
				Если обЗначениеНеЗаполнено(П107) Тогда
					Ошибка.Добавить("Возможно, неверно указана дата в поле ""Период""."); 
				КонецЕсли;
			КонецЕсли;
			Если СтрЗаменить(П109,"0","")<>"" Тогда
				Если обЗначениеНеЗаполнено(Дата(П109)) Тогда
					Ошибка.Добавить("Возможно, неверно указана дата в поле ""Дата""."); 
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Найти("АП, АР", П106) > 0 Тогда
			Если СтрЗаменить(П107,"0","")<>"" Тогда
				Ошибка.Добавить("При основании платежа ""АП"" или ""АР"" в поле ""Период"" необходимо указывать ""0"".");
			КонецЕсли;
		ИначеЕсли Найти("ТР, РС, ОТ, РТ, ВУ, ПР", П106) > 0 Тогда
			Если СтрЗаменить(П107,"0","")<>"" Тогда
				Если обЗначениеНеЗаполнено(П107) Тогда
					Ошибка.Добавить("Возможно, неверно указана дата в поле ""Период""."); 
				КонецЕсли;
			КонецЕсли;
			Если СтрЗаменить(П109,"0","")<>"" Тогда
				Если обЗначениеНеЗаполнено(Дата(П109)) Тогда
					Ошибка.Добавить("Возможно, неверно указана дата в поле ""Дата""."); 
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Найти("ТП ,ЗД ", П106) > 0 Тогда
			Если СтрЗаменить(П107,"0","")<>"" Тогда
				ДД = Сред((П107), 1, 2);
				ММ = Сред((П107), 4, 2);
				ГГ = Сред((П107), 7, 4);
				
				Если НЕ ММ="" Тогда
					ММ = Число(Сред((П107), 4, 2));
				Иначе
					ММ = 0;
				КонецЕсли;	
							
				Если НЕ ГГ="" Тогда
					ГГ = Число(Сред((П107), 7, 4));
				Иначе
					ГГ = 0;
				КонецЕсли;
				
				Если (Найти("Д1, Д2, Д3, МС", ДД) > 0) Тогда
					Если (ММ<1)ИЛИ
						(ММ>12)ИЛИ 
						(ГГ<2000)ИЛИ
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
						Ошибка.Добавить("Возможно, неверно указана дата в поле ""Период""."); 
					КонецЕсли;
				ИначеЕсли (Найти("КВ", ДД) > 0) Тогда
					Если (ММ<1)ИЛИ
						(ММ>4)ИЛИ 
						(ГГ<2000)ИЛИ
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
							Ошибка.Добавить("Неверно указан показатель налогового периода в поле ""Период"".");
					КонецЕсли;
				ИначеЕсли (Найти("ПЛ", ДД) > 0) Тогда
					Если (ММ<1)ИЛИ
						(ММ>2)ИЛИ 
						(ГГ<2000)ИЛИ
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
							Ошибка.Добавить("Неверно указан показатель налогового периода в поле ""Период"".");
					КонецЕсли;
				ИначеЕсли (Найти("ГД", ДД) > 0) Тогда
					Если (ММ<>0)ИЛИ
						(ГГ<2000)ИЛИ
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
							Ошибка.Добавить("Неверно указан показатель налогового периода в поле ""Период"".");
					КонецЕсли;
				Иначе
					Если обЗначениеНеЗаполнено(П107) Тогда
						Ошибка.Добавить("Возможно, неверно указана дата в поле ""Период""."); 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если СтрЗаменить(П108,"0","")<>"" Тогда
				Ошибка.Добавить("При основании платежа ""ТП"" или ""ЗД"" в поле ""Номер"" необходимо указывать ""0"".");
			КонецЕсли;
			Если Найти("ЗД ", П106) > 0 Тогда
				Если СтрЗаменить(П109,"0","")<>"" Тогда
					Ошибка.Добавить("Не должна заполнятся дата");
				КонецЕсли;
			Иначе
				Если СтрЗаменить(П109,"0","")<>"" Тогда
					Если обЗначениеНеЗаполнено(Дата(П109)) Тогда
						Ошибка.Добавить("Возможно, неверно указана дата в поле ""Дата""."); 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Найти("БФ", П106) > 0 Тогда
		Иначе
			Ошибка.Добавить("Неверно указано значение в поле ""Основание платежа"".");
		КонецЕсли;
		Если СтрЗаменить(П110,"0","")="" Тогда
		ИначеЕсли Найти("НС, АВ, ПЕ, ПЦ, СА, АШ, ИШ, ПЛ, ГП, ВЗ", П110) > 0 Тогда
		Иначе
			Ошибка.Добавить("Некорректный тип платежа");
		КонецЕсли;
	КонецЕсли;

	Если Не ОбЗначениеНеЗаполнено(КодУИН) И СтрДлина(КодУИН) <> 20 И КодУИН <> "0" Тогда
		Ошибка.Добавить("Неверно указано значение в поле ""Код УИН"".");
	КонецЕсли;
	
	//Выводим список найденных ошибок
	Для Ном = 0 По Ошибка.Количество()-1 Цикл
		Сообщить(Ошибка.Получить(Ном), СтатусСообщения.Важное);
	КонецЦикла;

	Возврат Ошибка;

КонецФункции // Проверить()

// удаляет из номера платежки буквы и символы
//
// Возвращаемое значение
//  Строка
//
Функция ПолучитьНомерДляПечати();
	
	СтрНомер = СокрЛП(Номер);
	Длина    = СтрДлина(СтрНомер);
	ЧисловойНомер = "";
	Для ин=1 По Длина Цикл
		Символ = Сред(СтрНомер,Длина-ин+1,1);
		Если Найти("0123456789", Символ) Тогда
			ЧисловойНомер = Символ + ЧисловойНомер;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// удаление ведущих нулей
	Пока Лев(ЧисловойНомер, 1)="0" Цикл
		ЧисловойНомер = Сред(ЧисловойНомер, 2);
	КонецЦикла;
	
	Возврат ЧисловойНомер;
КонецФункции

// Форматирует сумму  документа
//
// Параметры:
//  СуммаДок - число - реквизит, который надо отформатировать
//  СуммаБезКопеек - булево - флаг представления суммы без копеек
//
// Возвращаемое значение
//  Отформатированную строку
//
Функция ФорматироватьСумму(СуммаДок,СуммаБезКопеек)
	
	Результат  = СуммаДок;
	ЦелаяЧасть = Цел(СуммаДок);
	
	Если (Результат - ЦелаяЧасть) = 0 Тогда
		Если СуммаБезКопеек Тогда
			Результат = Формат(Результат,"ЧДЦ=2; ЧРД='='; ЧГ=0");
			Результат = Лев(Результат,Найти(Результат,"="));
		Иначе
			Результат = Формат(Результат,"ЧДЦ=2; ЧРД='-'; ЧГ=0");
		КонецЕсли;
	Иначе
		Результат = Формат(Результат,"ЧДЦ=2; ЧРД='-'; ЧГ=0");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ФорматироватьСумму()

// Определяет номер расчетного счета по
// переданному банковскому счету
//
// Параметры:
//  СчетКонтрагента - справочник.БанковскиеСчета
//
// Возвращаемое значение
//  Номер расчетного счета
//
Функция ВернутьРасчетныйСчет(СчетКонтрагента)

	БанкДляРасчетов = СчетКонтрагента.БанкДляРасчетов;
	Результат       = ?(БанкДляРасчетов.Пустая(), СчетКонтрагента.НомерСчета, СчетКонтрагента.Банк.КоррСчет);

	Возврат Результат;

КонецФункции // ВернутьРасчетныйСчет()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыБанковскиеСчета = Новый Структура();
				КонтролируемыеРеквизитыБанковскиеСчета.Вставить("СчетОрганизации");
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыБанковскиеСчета",КонтролируемыеРеквизитыБанковскиеСчета);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПлатежноеПоручение","Платежное поручение",Истина);
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Организация) Тогда
			ИННПлательщика=Организация.ИНН;
			Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
				КПППлательщика = ПодразделениеКомпании.КПП;
			Иначе	
				КПППлательщика = Организация.КПП;
			КонецЕсли;
			Если СчетОрганизации.Владелец<>Организация Тогда
				СчетОрганизации=Организация.ОсновнойБанковскийСчет;
				Рез=ОбработкаРеквизита("СчетОрганизации",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;		
			
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Если НЕ обЗначениеНеЗаполнено(СчетОрганизации) Тогда
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("ВалютаВзаиморасчетов",СчетОрганизации.ВалютаДенежныхСредств);
		КонецЕсли;
		ДопПараметры.Вставить("ВидДоговора", Перечисления.ВидыДоговоров.ПустаяСсылка());
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			ИННПолучателя=?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение, Контрагент.ГоловнойКонтрагент.ИНН, Контрагент.ИНН);
			КПППолучателя=Контрагент.КПП;
			Если СчетКонтрагента.Владелец<>Контрагент Тогда
				СчетКонтрагента=Контрагент.ОсновнойБанковскийСчет;
			КонецЕсли; 
		КонецЕсли;
		
		ОсвобожденОтНДС	= Контрагент.ОсвобожденОтНДС ИЛИ
				Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
				
		Если ОсвобожденОтНДС Тогда					
			Если СтавкаНДС <> Справочники.СтавкиНДС.БезНДС Тогда
				СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				Рез=ОбработкаРеквизита("СуммаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		Иначе
			Если СтавкаНДС = Справочники.СтавкиНДС.БезНДС Тогда 
				СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				Рез=ОбработкаРеквизита("СуммаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		КонецЕсли;
	
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаДокумента" Тогда 	
		Если обЗначениеНеЗаполнено(СуммаДокумента) ИЛИ обЗначениеНеЗаполнено(СтавкаНДС) Тогда
			СуммаНДС=0;
		Иначе
			СуммаБезНДС=(100*СуммаДокумента)/(100+СтавкаНДС.Ставка);
			СуммаНДС=СуммаДокумента-СуммаБезНДС;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СчетОрганизации" Тогда
		Рез=Истина;
		НоваяВалюта = СчетОрганизации.ВалютаДенежныхСредств;
		СтруктураКурса = обКурсДляВалюты(НоваяВалюта,Дата);
		НовыйКурс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ИзменилиВалюту = (НоваяВалюта<>ВалютаДокумента ИЛИ НовыйКурс<>КурсДокумента);
		СпрашиватьПроПересчет = (обПраво("РедактированиеЦенИСуммВНоменклатурныхТаблицах",Права,,ЭтотОбъект) И (ЭтаФорма <> Неопределено));
		#Если Клиент Тогда
		ТребуетсяПересчет=?(ЭтаФорма = Неопределено, Истина, Ложь);
		Если ИзменилиВалюту И СпрашиватьПроПересчет Тогда
			Если Вопрос("Были изменены данные влияющие на суммовые показатели документа !
						|Выполнить пересчет ?",РежимДиалогаВопрос.ДаНет,обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект),КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
				ТребуетсяПересчет=Истина;
			КонецЕсли;
		КонецЕсли;
		#Иначе
			ТребуетсяПересчет=Истина;
		#КонецЕсли
		Если ТребуетсяПересчет Тогда
			НоваяСуммаДокумента=СуммаДокумента;
			НоваяСуммаДокумента = обПересчет(НоваяСуммаДокумента, ВалютаДокумента, КурсДокумента, НоваяВалюта, НовыйКурс);
			Если НоваяСуммаДокумента<>СуммаДокумента Тогда
				СуммаДокумента = НоваяСуммаДокумента;
				Рез=ОбработкаРеквизита("СуммаДокумента") И Рез;
			КонецЕсли;
			//Если изменилась валюта, то надо подобрать новый договор контрагента
			Если ВалютаДокумента <> НоваяВалюта Тогда
				ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				ОбработкаРеквизита("Контрагент",,ЭтаФорма);
			КонецЕсли;
		КонецЕсли;
		ВалютаДокумента = НоваяВалюта;
		КурсДокумента = НовыйКурс;
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Если НЕ обЗначениеНеЗаполнено(СчетОрганизации) И НЕ ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = СчетОрганизации.ВалютаДенежныхСредств Тогда
			#Если Клиент Тогда
				ТекстСообщения = "Необходимо выбрать другой договор взаиморасчетов. Валюта договора должна совпадать с валютой счета организации";
				Предупреждение(ТекстСообщения,обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект));
			#КонецЕсли
				ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			Возврат Истина;
		Иначе
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
		КонецЕсли;
			
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПлатежноеПоручение"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПлатежноеПоручение(ТабДокумент) Экспорт
	
	Если Организация.Пустая() Тогда
		Сообщить("Не указана организация.", СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецЕсли;
	
	Если Контрагент.Пустая() Тогда
		Сообщить("Не указан контрагент.", СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецЕсли;
	
	НомерПечать = ПолучитьНомерДляПечати();
	
	Если Прав(НомерПечать,3)="000" Тогда
		Сообщить("Номер платежного поручения не может оканчиваться на ""000""!", СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецЕсли;
	
	Макет = ПолучитьМакет("ПлатежноеПоручение");
	Обл   = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	МесяцПрописью	= СчетОрганизации.МесяцПрописью;
	СуммаБезКопеек	= СчетОрганизации.СуммаБезКопеек;
	ФорматДаты		= "ДФ=" + ?(МесяцПрописью = 1,"'дд ММММ гггг'","'дд.ММ.гггг'");
	БанкОрганизации	= ?(ОбЗначениеНеЗаполнено(СчетОрганизации.БанкДляРасчетов), СчетОрганизации.Банк, СчетОрганизации.БанкДляРасчетов);
	БанкКонтрагента	= ?(ОбЗначениеНеЗаполнено(СчетКонтрагента.БанкДляРасчетов), СчетКонтрагента.Банк, СчетКонтрагента.БанкДляРасчетов);
	
	Обл.Параметры.НаименованиеНомер	= "ПЛАТЕЖНОЕ ПОРУЧЕНИЕ № " + НомерПечать;
	Обл.Параметры.ДатаДокумента		= Формат(Дата,ФорматДаты);
	Обл.Параметры.ВидПлатежа		= ВидПлатежа;
	Обл.Параметры.СуммаЧислом		= ФорматироватьСумму(СуммаДокумента,СуммаБезКопеек);
	Обл.Параметры.СуммаПрописью		= обЧислоПрописью(СуммаДокумента,ЭтотОбъект.ВалютаДокумента);
	
	Обл.Параметры.ПлательщикИНН		= "ИНН " + ?(ПустаяСтрока(ИННПлательщика), Организация.ИНН, СокрЛП(ИННПлательщика));
	Обл.Параметры.ПлательщикКПП		= "КПП " + ?(ПустаяСтрока(КПППлательщика),?(ПеречислениеНалога,"0",""),СокрЛП(КПППлательщика));
	
	Если СчетОрганизации.БанкДляРасчетов.Пустая() Тогда
		СтрКорреспондент = "";
	Иначе	
		СтрКорреспондент = " р/с " + СчетОрганизации.НомерСчета+ " в " + СчетОрганизации.Банк + " " + СчетОрганизации.Банк.Город;	
	КонецЕсли;
	
	ТекстПлательщикПечать = ?(ПустаяСтрока(ТекстПлательщика),Организация.НаименованиеПолное,СокрЛП(ТекстПлательщика));
	
	Обл.Параметры.Плательщик			= ТекстПлательщикПечать;
	Обл.Параметры.БанкПлательщика		= "" + БанкОрганизации + " " + БанкОрганизации.Город;
	
	Обл.Параметры.НомерСчетаПлательщика	= ВернутьРасчетныйСчет(СчетОрганизации);
	
	Обл.Параметры.БикБанкаПлательщика	= БанкОрганизации.Код;
	Обл.Параметры.СчетБанкаПлательщика	= БанкОрганизации.КоррСчет;
	
	Обл.Параметры.ПолучательИНН			= "ИНН " + ?(ПустаяСтрока(ИННПолучателя), Контрагент.ИНН, СокрЛП(ИННПолучателя));
	Обл.Параметры.ПолучательКПП			= "КПП " + ?(ПустаяСтрока(КПППолучателя),?(ПеречислениеНалога,"0",""),СокрЛП(КПППолучателя));
	
	Обл.Параметры.Получатель			= ?(ПустаяСтрока(ТекстПолучателя),Контрагент.НаименованиеПолное,СокрЛП(ТекстПолучателя));
	
	Обл.Параметры.БанкПолучателя		= "" + БанкКонтрагента + " " + БанкКонтрагента.Город;
	Обл.Параметры.БикБанкаПолучателя	= БанкКонтрагента.Код;
	Обл.Параметры.СчетБанкаПолучателя	= БанкКонтрагента.КоррСчет;
	
	Обл.Параметры.НомерСчетаПолучателя	= ВернутьРасчетныйСчет(СчетКонтрагента);
	
	Обл.Параметры.НазначениеПлатежа		= СокрЛП(НазначениеПлатежа);
	Обл.Параметры.Очередность			= ОчередностьПлатежа;
	Обл.Параметры.СрокПлатежа			= "";
	
	Если НЕ ПеречислениеНалога Тогда
		Если ЗначениеЗаполнено(КодУИН) И СтрДлина(КодУИН) <> 25 И КодУИН <> "0" Тогда
			Сообщить("Неверно указано значение в поле ""Код УИН"".");
		КонецЕсли;
		Обл.Параметры.КодУИН				= ?(ОбЗначениеНеЗаполнено(КодУИН) ИЛИ КодУИН = "", "0", КодУИН);
	Иначе
		Проверить();
		
		Обл.Параметры.КодУИН				= ?(ОбЗначениеНеЗаполнено(КодУИН) ИЛИ КодУИН = "", "0", КодУИН);
		
		Обл.Параметры.СтатусСоставителя		= ?(ПустаяСтрока(СтатусСоставителя),"0",СокрЛП(СтатусСоставителя));
		Обл.Параметры.КодБК					= ?(ПустаяСтрока(КодБК),"",СокрЛП(КодБК));
		ОКТМОДляПечати						= СокрЛП(КодОКТМО);
		Если НЕ ПустаяСтрока(ОКТМОДляПечати) Тогда
			Пока СтрДлина(ОКТМОДляПечати)<11 Цикл
				ОКТМОДляПечати = ОКТМОДляПечати+"-";
			КонецЦикла; 
		КонецЕсли; 
		Обл.Параметры.КодОКТМО				= ОКТМОДляПечати;
		Обл.Параметры.ПоказательОснования	= ?(ПустаяСтрока(ПоказательОснования),"0",СокрЛП(ПоказательОснования));
		Обл.Параметры.ПоказательНомера		= ?(ПустаяСтрока(ПоказательНомера),"0",СокрЛП(ПоказательНомера));
		Обл.Параметры.ПоказательДаты		= ?(ПоказательДаты = '00010101000000',"0",Формат(ПоказательДаты,"ДФ='дд.ММ.гггг'"));
		Обл.Параметры.ПоказательТипа		= ?(ПустаяСтрока(ПоказательТипа),"0",СокрЛП(ПоказательТипа));
		Если (ПустаяСтрока(ПоказательПериода)) ИЛИ (ПоказательПериода = "  .  .    ") Тогда
			Обл.Параметры.ПоказательПериода = "0";
		Иначе
			Обл.Параметры.ПоказательПериода = СокрЛП(ПоказательПериода);
		КонецЕсли;
	КонецЕсли;
	
	Если ДействуетУказаниеБанкаРоссии5286У(Дата) И ЗначениеЗаполнено(КодВидаДохода) Тогда
		Обл.Параметры.КодВидаДохода = КодВидаДохода;
	КонецЕсли;
	
	ТабДокумент.Вывести(Обл);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПлатежноеПоручение()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

// Возвращает список статусов отправителя п/п
//
// Возвращаемое значение:
//  СписокЗначений – в котором создаются элементы со статусами отправителя/
//
Функция ПолучитьСписокСтатусовОтправителя() Экспорт

	Список = Новый СписокЗначений;
	Список.Добавить("01", "01 - налогоплательщик (плательщик сборов) - юридическое лицо");
	Список.Добавить("02", "02 - налоговый агент");
	Список.Добавить("03", "03 - сборщик налогов и сборов");
	Список.Добавить("04", "04 - налоговый орган");
	Список.Добавить("05", "05 - территориальные органы Федеральной службы судебных приставов");
	Список.Добавить("06", "06 - участник внешнеэкономической деятельности");
	Список.Добавить("07", "07 - таможенный орган");
	Список.Добавить("08", "08 - плательщик иных платежей, осуществляющий перечисление платежей в бюджетную систему Российской Федерации (кроме платежей, администрируемых налоговыми органами)");
	
	Если Дата >= '20050101' Тогда
	
		Список.Добавить("09", "09 - налогоплательщик (плательщик сборов) – индивидуальный предприниматель");
		Список.Добавить("10", "10 - налогоплательщик (плательщик сборов) – частный нотариус");
		Список.Добавить("11", "11 - налогоплательщик (плательщик сборов) – адвокат, учредивший адвокатский кабинет");
		Список.Добавить("12", "12 - налогоплательщик (плательщик сборов) – глава крестьянского (фермерского) хозяйства");
		Список.Добавить("13", "13 - налогоплательщик (плательщик сборов) – иное физическое лицо – клиент банка (владелец счета)");
		Список.Добавить("14", "14 - налогоплательщик, производящий выплаты физическим лицам (п.п. 1 п.1 ст. 235 Налогового кодекса Российской Федерации)");
		Список.Добавить("15", "15 - кредитная организация, оформившая расчетный документ на общую сумму на перечисление налогов, сборов и иных платежей в бюджетную систему Российской Федерации, уплачиваемых физическими лицами без открытия банковского счета");
	
	КонецЕсли;
	
	Возврат Список;

КонецФункции // ПолучитьСписокСтатусовОтправителя()

// Возвращает список статусов отправителя п/п
//
// Возвращаемое значение:
//  СписокЗначений – в котором создаются элементы со статусами отправителя/
//
Функция ПолучитьСписокОснованийПлатежа() Экспорт

	ОснованиеПлатежа = Новый СписокЗначений;
	ОснованиеПлатежа.Добавить("ТП", "ТП - платежи текущего года");
	ОснованиеПлатежа.Добавить("ЗД", "ЗД - добровольное погашение задолженности по истекшим налоговым периодам");
	
	Если Дата >= '20050101' Тогда
		ОснованиеПлатежа.Добавить("БФ","БФ - текущие платежи физических лиц – клиентов банка (владельцев счета), уплачиваемые со своего банковского счета");
	КонецЕсли;

	ОснованиеПлатежа.Добавить("ТР", "ТР - погашение задолженности по требованию об уплате налогов (сборов) от налогового органа");
	ОснованиеПлатежа.Добавить("РС", "РС - погашение рассроченной задолженности");
	ОснованиеПлатежа.Добавить("ОТ", "ОТ - погашение отсроченной задолженности");
	ОснованиеПлатежа.Добавить("РТ", "РТ - погашение реструктурируемой задолженности");
	ОснованиеПлатежа.Добавить("ВУ", "ВУ - погашение отсроченной задолженности в связи с введением внешнего управления");
	ОснованиеПлатежа.Добавить("ПР", "ПР - погашение задолженности, приостановленной к взысканию");
	ОснованиеПлатежа.Добавить("АП", "АП - погашение задолженности по акту проверки");
	ОснованиеПлатежа.Добавить("АР", "АР - погашение задолженности по исполнительному документу");
		
	ОснованиеПлатежа.Добавить("0", "0 - Невозможно указать конкретное значение показателя");
	
	Возврат ОснованиеПлатежа;

КонецФункции // ПолучитьСписокСтатусовОтправителя()

// Возвращает список статусов отправителя п/п
//
// Возвращаемое значение:
//  СписокЗначений – в котором создаются элементы со статусами отправителя/
//
Функция ПолучитьСписокПоказателейТипа() Экспорт

	Список = Новый СписокЗначений;
	Список.Добавить("НС", "НС - уплата налога или сбора");
	
	Если Дата >= '20050101' Тогда
		Список.Добавить("ПЛ", "ПЛ - уплата платежа");
		Список.Добавить("ГП", "ГП - уплата пошлины");
		Список.Добавить("ВЗ", "ВЗ - уплата взноса");
	КонецЕсли;
	
	Список.Добавить("АВ", "АВ - уплата аванса или предоплата (в том числе декадные платежи)");
	Список.Добавить("ПЕ", "ПЕ - уплата пени");
	Список.Добавить("ПЦ", "ПЦ - уплата процентов");
	Список.Добавить("СА", "СА - налоговые санкции, установленные Налоговым кодексом РФ");
	Список.Добавить("АШ", "АШ - административные штрафы");
	Список.Добавить("ИШ", "ИШ - иные штрафы, установленные соответствующими нормативными актами");
	Список.Добавить("0", "0 - Конкретное значение указать невозможно");
	
	Возврат Список;

КонецФункции // ПолучитьСписокСтатусовОтправителя()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	ДокОснование = Основание; 
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	ОбработкаРеквизита("Организация");
	Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		ИННПолучателя=?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение, Контрагент.ГоловнойКонтрагент.ИНН, Контрагент.ИНН);
		КПППолучателя=Контрагент.КПП;
		СчетКонтрагента=Контрагент.ОсновнойБанковскийСчет;
	КонецЕсли; 
	
	Если ТипЗнч(ДокОснование)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СУММА(ВложеннаяТаблица.СуммаЗаказа) КАК СуммаЗаказа,
		|	СУММА(ВложеннаяТаблица.СуммаЗаказаУпр) КАК СуммаЗаказаУпр
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикамОстатки.СуммаОстаток КАК СуммаЗаказа,
		|		ЗаказыПоставщикамОстатки.СуммаУпрОстаток КАК СуммаЗаказаУпр
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам.Остатки(&МоментВремени, ЗаказПоставщику = &Заказ) КАК ЗаказыПоставщикамОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		-ВзаиморасчетыКомпанииОстатки.СуммаОстаток,
		|		-ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток
		|	ИЗ
		|		РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&МоментВремени, Сделка ССЫЛКА Документ.ЗаказПоставщику И Сделка = &Заказ) КАК ВзаиморасчетыКомпанииОстатки) КАК ВложеннаяТаблица
		|");
		Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());
		Запрос.УстановитьПараметр("Заказ",         ДокОснование);
		
		СуммаСделки    = 0;
		СуммаСделкиУпр = 0;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			СуммаСделки    = Выборка.СуммаЗаказа;
			СуммаСделкиУпр = Выборка.СуммаЗаказаУпр;
			
		КонецЕсли;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
			СуммаДокумента = обПересчет(СуммаСделки, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ДокОснование.КурсДокумента, ВалютаДокумента, КурсДокумента);
		Иначе
			СуммаДокумента = обПересчет(СуммаСделкиУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		
		ОбработкаРеквизита("СуммаДокумента");
		
	ИначеЕсли ТипЗнч(ДокОснование)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СУММА(ВложеннаяТаблица.СуммаЗаказа) КАК СуммаЗаказа,
		|	СУММА(ВложеннаяТаблица.СуммаЗаказаУпр) КАК СуммаЗаказаУпр
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикамОстатки.СуммаОстаток КАК СуммаЗаказа,
		|		ЗаказыПоставщикамОстатки.СуммаУпрОстаток КАК СуммаЗаказаУпр
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки(&МоментВремени, ЗаказПоставщику = &Заказ) КАК ЗаказыПоставщикамОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		-ВзаиморасчетыКомпанииОстатки.СуммаОстаток,
		|		-ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток
		|	ИЗ
		|		РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&МоментВремени, Сделка ССЫЛКА Документ.ЗаказПоставщику И Сделка = &Заказ) КАК ВзаиморасчетыКомпанииОстатки) КАК ВложеннаяТаблица
		|");
		Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());
		Запрос.УстановитьПараметр("Заказ",         ДокОснование);
		
		СуммаСделки    = 0;
		СуммаСделкиУпр = 0;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			СуммаСделки    = Выборка.СуммаЗаказа;
			СуммаСделкиУпр = Выборка.СуммаЗаказаУпр;
			
		КонецЕсли;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
			СуммаДокумента = обПересчет(СуммаСделки, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ДокОснование.КурсДокумента, ВалютаДокумента, КурсДокумента);
		Иначе
			СуммаДокумента = обПересчет(СуммаСделкиУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		
		ОбработкаРеквизита("СуммаДокумента");
		
	ИначеЕсли ТипЗнч(ДокОснование)=Тип("ДокументСсылка.СчетОтПоставщика") ИЛИ
		ТипЗнч(ДокОснование)=Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили") Тогда
		
		//Проверим сумму предоплаты
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаПриход КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрПриход КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		|		Контрагент = &Контрагент И
		|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		|		(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)
		|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|";
		Запрос.УстановитьПараметр("Контрагент",            ДокОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка",                ДокОснование);
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		ДокументОснованиеСуммаДокумента = ДокОснование.СуммаДокумента;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			СуммаДокумента = ДокументОснованиеСуммаДокумента-обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		Иначе
			СуммаДокумента = ДокументОснованиеСуммаДокумента-обПересчет(тзОплаты.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсДокумента), Дата, ЭтотОбъект.КурсДокумента), РежимОкругления.Окр15как20);
		КонецЕсли;
		
		ОбработкаРеквизита("СуммаДокумента");
		
	//Получим задолженность по сделке
	ИначеЕсли НЕ обЗначениеНеЗаполнено(ДокОснование) Тогда
		
		СтруктураОтбора=Новый Структура(); 
		СтруктураОтбора.Вставить("Контрагент",ДокОснование.ДоговорВзаиморасчетов.Владелец);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДокОснование.ДоговорВзаиморасчетов);
		СтруктураОтбора.Вставить("Сделка",ДокОснование);
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ТекущаяДата(),СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		Если ВалютаДокумента = Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов Тогда
			ОстатокПоСделке=обПересчет(тзДолги.Итог("Сумма"),ДокОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
		Иначе
			ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокОснование.КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		Если ОстатокПоСделке<0 Тогда
			СуммаДокумента=-ОстатокПоСделке;
		Иначе
			СуммаДокумента=ОстатокПоСделке;
		КонецЕсли; 
		ОбработкаРеквизита("СуммаДокумента");
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаРасходДС") Тогда
		Если Основание.ХозОперация = Справочники.ХозОперации.ЗаявкаНаРасходСРС Тогда
			СчетОрганизации = Основание.СтруктурнаяЕдиница;
		КонецЕсли;
		СуммаДокумента = Основание.СуммаДокумента;
		ОбработкаРеквизита("СуммаДокумента");
	КонецЕсли;
	
	дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, ЭтотОбъект.ДокументОснование, СуммаДокумента);
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
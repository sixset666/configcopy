// Модуль документа ЗаказВнутренний

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ДвиженияЗаказа Экспорт; 		// Результат запроса по движениям заказов
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Получение остатков распределения заказа по заказам поставщикам
//
// Возвращаемое значение:
//  Возвращает выборку.
//
Функция ПолучитьРаспределенияЗаказа()
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ЗаказыРаспределение.Номенклатура КАК Номенклатура,
	             |	ЗаказыРаспределение.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	СУММА(ЗаказыРаспределение.Количество) КАК Количество
	             |ИЗ
	             |	РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
	             |ГДЕ
	             |	ЗаказыРаспределение.ВидДвижения = &ВидДвиженияПриход
	             |	И ЗаказыРаспределение.ЗаказПокупателя = &ЗаказПокупателя
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ЗаказыРаспределение.Номенклатура,
	             |	ЗаказыРаспределение.ХарактеристикаНоменклатуры";
	Запрос.УстановитьПараметр("ВидДвиженияПриход",ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ЗаказПокупателя",Ссылка);
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции // ПолучитьРаспределенияЗаказа()

//Перерасчет таблицы товаров в соответствии с таблицей распределений
//
// Параметры:
//  ЭтаФорма - Форма - Содержит данную форму.
//
Процедура ПерерасчетРаспределения(ЭтаФорма=Неопределено) Экспорт
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		СтрокаТоваров.Распределено	= 0;
	КонецЦикла;
	
	СтруктураНоменклатуры = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	Для Каждого СтрокаРаспределения Из РаспределениеЗаказа Цикл
		Если НЕ СтрокаРаспределения.Номенклатура.Пустая() Тогда
			
			СтруктураНоменклатуры.Вставить("Номенклатура",СтрокаРаспределения.Номенклатура);
			СтруктураНоменклатуры.Вставить("ХарактеристикаНоменклатуры",СтрокаРаспределения.ХарактеристикаНоменклатуры);
			
			МассивСтрокТоваров = Товары.НайтиСтроки(СтруктураНоменклатуры);
			Если МассивСтрокТоваров.Количество() <= 0 Тогда
				СтрокаТоваров = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТоваров, СтрокаРаспределения,,"Количество, Цена");
				ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров,ЭтаФорма);
				СтрокаТоваров.Количество = 0;
				СтрокаТоваров.Распределено = 0;
			Иначе
				СтрокаТоваров=МассивСтрокТоваров[0];
			КонецЕсли;
			Количество=((СтрокаРаспределения.Количество*СтрокаРаспределения.Коэффициент)/СтрокаТоваров.Коэффициент);
			СтрокаТоваров.Распределено = СтрокаТоваров.Распределено+Количество;
			ОбработкаРеквизита("Товары.Распределено",СтрокаТоваров,ЭтаФорма);
		КонецЕсли; 
	КонецЦикла; 
	
	МассивСтрокТоваров=Товары.НайтиСтроки(Новый Структура("Количество, Резерв, Распределено", 0, 0, 0));
	Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
		Товары.Удалить(СтрокаТоваров);
	КонецЦикла;
	
	#Если Клиент Тогда
	Если ЭтаФорма<>Неопределено Тогда
		дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
	КонецЕсли; 
	#КонецЕсли
КонецПроцедуры // ПерерасчетРаспределения()

//Получает количество нераспределенного товара. Если полностью распределено или товар в заказе не найден,
//то возвращает Неопределено
//Параметры:
//	Заказ - {ЗаказПокупателя, ЗаказВнутренний} Ссылка - ссылка на заказ,
//	Номенклатура - номенклатурная позиция,
//	Характеристика - характеристика номенклатуры.
Функция ПолучитьТекущееСостояниеЗаказа(Заказ, Номенклатура, Характеристика)
	ТекстЗапроса = "ВЫБРАТЬ
	|	(ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(
	|		&НаДату,
	|		Заказ = &Заказ И 
	|		Номенклатура = &Номенклатура И 
	|		ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыПокупателейОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыРаспределение.Остатки(
	|		&НаДату,
	|		ЗаказПокупателя = &Заказ И 
	|		Номенклатура = &Номенклатура И 
	|		ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыРаспределениеОстатки
	|ПО
	|	ЗаказыПокупателейОстатки.Заказ = ЗаказыРаспределениеОстатки.ЗаказПокупателя И 
	|	ЗаказыПокупателейОстатки.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура И 
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) > ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", ?(ЭтоНовый(), ТекущаяДата(), Новый Граница(Дата, ?(Проведен, ВидГраницы.Исключая, ВидГраницы.Включая))));
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", Характеристика);
	Рез = Запрос.Выполнить().Выбрать();
	Если Рез.Следующий() Тогда
		Возврат Рез.Количество;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции // ПолучитьТекущееСостояниеЗаказа()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Получить состояние заказа клиента
//
// Возвращаемое значение:
//   ТаблицаЗначений – таблица значений с результатами движений по заказу
//
Функция ПолучитьДвиженияЗаказа() Экспорт
	//Сначала получим список номенклатуры по документу
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ
	|	ТаблицаЗаказа
	|ИЗ
	|	Документ.ЗаказВнутренний.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка = &Заказ
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) КАК Заказано,
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) КАК Зарезервировано,
	|	ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)КАК Распределено,
	|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК СкладОстаток,
	|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0) КАК СкладЗарезервировано
	|ИЗ
	|	ТаблицаЗаказа КАК ДокументТовары
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(, Заказ=&Заказ) КАК ЗаказыПокупателейОстатки
	|ПО
	|	ДокументТовары.Номенклатура               = ЗаказыПокупателейОстатки.Номенклатура И 
	|	ДокументТовары.ХарактеристикаНоменклатуры = ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыРаспределение.Остатки(,ЗаказПокупателя=&Заказ) КАК ЗаказыРаспределениеОстатки
	|ПО
	|	ДокументТовары.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
	|	ДокументТовары.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,
	|				(Номенклатура, ХарактеристикаНоменклатуры) В 
	|					(ВЫБРАТЬ
	|						ТаблицаЗаказа.Номенклатура КАК Номенклатура,
	|						ТаблицаЗаказа.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|					ИЗ
	|						ТаблицаЗаказа КАК ТаблицаЗаказа)) КАК ОстаткиТоваровКомпанииОстатки
	|ПО
	|	ДокументТовары.Номенклатура               = ОстаткиТоваровКомпанииОстатки.Номенклатура И 
	|	ДокументТовары.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|");
	Запрос.УстановитьПараметр("Заказ", Ссылка);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	//Выгрузка.Свернуть("Номенклатура,ХарактеристикаНоменклатуры","Заказано,Зарезервировано,Распределено,СкладОстаток,СкладЗарезервировано");
	
	Возврат Выгрузка;
	
КонецФункции // ПолучитьДвиженияЗаказа()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеРаспределения=Новый Структура();
	ОбязательныеРаспределения.Вставить("Номенклатура",3);
	ОбязательныеРаспределения.Вставить("Количество",1);
	ОбязательныеРаспределения.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеРаспределения.Вставить("Коэффициент",1);
	ОбязательныеРаспределения.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеРаспределения.Вставить("ЗаказПокупателя",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеПолучатель",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("РаспределениеЗаказа",ОбязательныеРаспределения);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ПодразделениеПолучатель");
			Если ХозОперация=Справочники.ХозОперации.ЗаказРезервированиеВнутренние
				ИЛИ ХозОперация=Справочники.ХозОперации.РезервированиеВнутреннее Тогда
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;	
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыРаспределениеЗаказа = Новый Структура();
				КонтролируемыеРеквизитыРаспределениеЗаказа.Вставить("ЗаказПокупателя", Ложь);
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("РаспределениеЗаказа",КонтролируемыеРеквизитыРаспределениеЗаказа);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);		
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Товары.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗаказВнутренний","Заказ внутренний",Ложь);
	СписокХозОпераций.Добавить("ЗаказРезервированиеВнутренние","Заказ и резервирование внутренние",Истина);
	СписокХозОпераций.Добавить("РезервированиеВнутреннее","Резервирование внутреннее",Ложь);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//						   производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ХозОперация" Тогда
		Если ХозОперация=Справочники.ХозОперации.ЗаказВнутренний Тогда
			Для Каждого СтрокаТоваров Из Товары Цикл
				Если СтрокаТоваров.Резерв<>0 Тогда
					СтрокаТоваров.Резерв=0;
				КонецЕсли; 
			КонецЦикла; 
		ИначеЕсли ХозОперация=Справочники.ХозОперации.РезервированиеВнутреннее Тогда
			Для Каждого СтрокаТоваров Из Товары Цикл
				Если СтрокаТоваров.Количество<>СтрокаТоваров.Резерв Тогда
					СтрокаТоваров.Количество = СтрокаТоваров.Резерв;
					ОбработкаРеквизита("Товары.Количество", СтрокаТоваров, ЭтаФорма, ДопПараметры);
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// нельзя выбирать розничный склад
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
			Предупреждение("На розничном складе товар не резервируется !",10); 
			#КонецЕсли
			СкладКомпании=Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОбновитьПодборЗамен();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
	
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ХозОперация=Справочники.ХозОперации.РезервированиеВнутреннее Тогда
			Если ТекСтрока.Резерв<ТекСтрока.Распределено Тогда
				ТекСтрока.Резерв = ТекСтрока.Распределено;
				Сообщить("Количество номенклатуры не может быть меньше распределенной");
			КонецЕсли; 
			ТекСтрока.Количество=ТекСтрока.Резерв;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ЗаказВнутренний Тогда
			ТекСтрока.Резерв=0;
			Если ТекСтрока.Количество<ТекСтрока.Распределено Тогда
				ТекСтрока.Количество=ТекСтрока.Распределено;
				Сообщить("Количество номенклатуры не может быть меньше распределенной");
			КонецЕсли; 
		Иначе
			Если ТекСтрока.Количество<ТекСтрока.Распределено Тогда
				ТекСтрока.Количество=ТекСтрока.Распределено;
				Сообщить("Количество номенклатуры не может быть меньше распределенной");
			КонецЕсли; 
		КонецЕсли; 
		
		НовоеКоличество=ТекСтрока.Количество;
		Если НовоеКоличество<ТекСтрока.Резерв Тогда
			НовоеКоличество=ТекСтрока.Резерв;
		КонецЕсли; 
		ТекСтрока.Количество=НовоеКоличество;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Резерв" Тогда
		Рез=Истина;
		Если ХозОперация=Справочники.ХозОперации.ЗаказВнутренний Тогда
			ТекСтрока.Резерв=0;
		Иначе
			Если ТекСтрока.Резерв>ТекСтрока.Количество ИЛИ ХозОперация=Справочники.ХозОперации.РезервированиеВнутреннее Тогда
				ТекСтрока.Количество = ТекСтрока.Резерв;
				Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Распределено" Тогда
		Возврат ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Если ТекСтрока.Коэффициент = ТекСтрока.ЕдиницаИзмерения.Коэффициент Тогда
			Возврат Ложь;
		Иначе
			КоэффВрем = ?(ТекСтрока.Коэффициент = 0, 1, ТекСтрока.Коэффициент); //запоминаем старый коэффициент
			ТекСтрока.Коэффициент  = ТекСтрока.ЕдиницаИзмерения.Коэффициент; //установим новый
			ТекСтрока.Распределено = ТекСтрока.Распределено * КоэффВрем / ТекСтрока.Коэффициент; //рассчитаем колич.
			ТекСтрока.Количество   = ТекСтрока.Количество * КоэффВрем / ТекСтрока.Коэффициент; //рассчитаем колич.
			Возврат ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "РАСПРЕДЕЛЕНИЕ ЗАКАЗА"
	ИначеЕсли Имя="РаспределениеЗаказа.Номенклатура" Тогда
		Если ТекСтрока.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Набор Тогда
			// получим виды номенклатуры которые нельзя выбирать
			БлокироватьВидыНоменклатуры=ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
			Если БлокироватьВидыНоменклатуры<>Неопределено И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
				Попытка ЕстьОшибка=(БлокироватьВидыНоменклатуры[ТекСтрока.Номенклатура.ВидНоменклатуры]<>Неопределено); Исключение ЕстьОшибка=Ложь; КонецПопытки;
				Если ЕстьОшибка Тогда
					#Если Клиент Тогда
					Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
					#КонецЕсли
					ТекСтрока.Номенклатура=Неопределено;
					ОбработкаРеквизита("РаспределениеЗаказа.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			// установим количество
			Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
			// сбросим характеристику, если она не подходит
			Если (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура) И (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
				ТекСтрока.ХарактеристикаНоменклатуры=Неопределено;
			КонецЕсли;
			// заполним единицу измерения
			Если ДопПараметры=Неопределено Тогда
				НоваяЕдиницаИзмерения=ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
			Иначе
				Если НЕ ДопПараметры.Свойство("ЕдиницаИзмерения",НоваяЕдиницаИзмерения) Тогда
					НоваяЕдиницаИзмерения=ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				КонецЕсли; 
			КонецЕсли;
			// обработаем изменение единицы измерения
			ТекСтрока.ЕдиницаИзмерения=НоваяЕдиницаИзмерения;
			Рез=ОбработкаРеквизита("РаспределениеЗаказа.ЕдиницаИзмерения",ТекСтрока,ЭтаФорма,ДопПараметры);
			#Если Клиент Тогда
			// покажем если надо колонку "характеристика номенклатуры"
			Если ЭтаФорма<>Неопределено Тогда
				//ТекСтрока=ЭлементыФормы.РаспределениеЗаказа.ТекущиеДанные;
				Если обПраво("АвтоматическиСкрыватьКолонкуХарактеристик",Права,,ЭтотОбъект) И НЕ
					 ЭтаФорма.ЭлементыФормы.РаспределениеЗаказа.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
					 ЭтаФорма.ЭлементыФормы.РаспределениеЗаказа.Колонки.ХарактеристикаНоменклатуры.Видимость = (ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=1 ИЛИ ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=2);
				КонецЕсли;
			КонецЕсли;
			#КонецЕсли
		Иначе
			Рез=Истина;
			// имеем дело с набором, разложим его...
			Набор=ТекСтрока.Номенклатура; КоличествоНабора=ТекСтрока.Количество;
			Если КоличествоНабора=0 Тогда КоличествоНабора=1; КонецЕсли; 
			// удалим строку из табличной части
			ЭтотОбъект.РаспределениеЗаказа.Удалить(ТекСтрока);
			// теперь будем рекурсивно добавлять состав набора
			Для Каждого ТоварИзНабора Из Набор.СоставНабора Цикл
				СтрокаТЧ=ЭтотОбъект.РаспределениеЗаказа.Добавить();
				СтрокаТЧ.Номенклатура=ТоварИзНабора.Номенклатура;
				Рез=ОбработкаРеквизита("РаспределениеЗаказа.Номенклатура",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				СтрокаТЧ.Количество=ТоварИзНабора.Количество*КоличествоНабора/?(СтрокаТЧ.Коэффициент=0,1,СтрокаТЧ.Коэффициент);
			КонецЦикла;
			ПерерасчетРаспределения(ЭтаФорма);
		КонецЕсли;
		Возврат Рез И ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="РаспределениеЗаказа.ХарактеристикаНоменклатуры" Тогда
		Возврат ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="РаспределениеЗаказа.ЕдиницаИзмерения" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
			ТекСтрока.Коэффициент = ТекСтрока.ЕдиницаИзмерения.Коэффициент;
			Возврат Истина;
		КонецЕсли;
		Возврат Ложь;
		
	ИначеЕсли Имя = "РаспределениеЗаказа.ЗаказПокупателя" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ЗаказПокупателя) Тогда
			ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя = "РаспределениеЗаказа.Количество" Тогда
		Возврат ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя = "РаспределениеЗаказа.ПроверкаЗаполнения" Тогда
		Если ТекСтрока.ЗаказПокупателя = Неопределено ИЛИ ТекСтрока.ЗаказПокупателя.Пустая() Тогда Возврат Ложь; КонецЕсли;
		
		КоличествоНераспределенного = ПолучитьТекущееСостояниеЗаказа(ТекСтрока.ЗаказПокупателя, ТекСтрока.Номенклатура, ТекСтрока.ХарактеристикаНоменклатуры);
		Если КоличествоНераспределенного = Неопределено Тогда
			Сообщить("Номенклатура <" + ТекСтрока.Номенклатура.Наименование
				+ ?(ТекСтрока.ХарактеристикаНоменклатуры.Пустая(), "", " / " + ТекСтрока.ХарактеристикаНоменклатуры.Наименование)
				+ "> отсутствует в заказе <" + ТекСтрока.ЗаказПокупателя + "> или полностью распределена!");
				
			Если ТипЗнч(ТекСтрока.ЗаказПокупателя) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				ТекСтрока.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
			Иначе
				ТекСтрока.ЗаказПокупателя = Документы.ЗаказВнутренний.ПустаяСсылка();
			КонецЕсли;
		Иначе
			Если КоличествоНераспределенного < ТекСтрока.Количество * ТекСтрока.Коэффициент Тогда
				ТекСтрока.Количество = КоличествоНераспределенного / ТекСтрока.Коэффициент;
			КонецЕсли;
			мсвСтрок = ТекСтрока.ЗаказПокупателя.Товары.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", ТекСтрока.Номенклатура, ТекСтрока.ХарактеристикаНоменклатуры));
			Если мсвСтрок.Количество() > 0 Тогда
				ТекСтрока.Цена = мсвСтрок[0].Цена;
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()
	
// Формирует печатную форму "СчетЗаказ"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗаказВнутренний(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ЗаказВнутренний");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ОбластьМакета.Параметры.Поставщик = ПодразделениеКомпании;
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(ПодразделениеКомпании);
	ОбластьМакета.Параметры.Покупатель = ПодразделениеПолучатель;
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(ПодразделениеПолучатель);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("Сумма",Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	Сумма = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьПодвал.Параметры.Сумма = Формат(Сумма,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(Сумма,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительПодразделение");
	Исполнитель.ИсполнительПодразделениеПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительПодразделение),"","/ "+ Исполнитель.ИсполнительПодразделениеПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик    = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикПодразделение");
	Заказчик.ЗаказчикПодразделениеПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикПодразделение),"","/ "+ Заказчик.ЗаказчикПодразделениеПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	Возврат ТабДокумент;
КонецФункции // ПечатьЗаказВнутренний()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	ПодразделениеПолучатель=ПодразделениеКомпании;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") ИЛИ
		 ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя")Тогда
		Товары.Очистить();
		РаспределениеЗаказа.Очистить();
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЗаказыПокупателейОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) КАК Количество
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&КонецПериода, Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ЗаказыРаспределение.Остатки(&КонецПериода, ЗаказПокупателя = &Заказ) КАК ЗаказыРаспределениеОстатки
		|ПО
		|	ЗаказыПокупателейОстатки.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
		|ГДЕ
		|	(ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) > 0
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры";
		Запрос.УстановитьПараметр("Заказ",Основание);
		Запрос.УстановитьПараметр("КонецПериода",?(Ссылка.Пустая(),ТекущаяДата(),Ссылка));
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрокаРаспределения = РаспределениеЗаказа.Добавить();
			НоваяСтрокаРаспределения.Номенклатура               = Выборка.Номенклатура;
			НоваяСтрокаРаспределения.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
			НоваяСтрокаРаспределения.ЗаказПокупателя            = Основание;
			НоваяСтрокаРаспределения.Количество                 = Выборка.Количество;
			НоваяСтрокаРаспределения.ЕдиницаИзмерения           = Выборка.ЕдиницаИзмерения;
			
			ОбработкаРеквизита("РаспределениеЗаказа.ЕдиницаИзмерения", НоваяСтрокаРаспределения);
			
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("ЕдиницаИзмерения", НоваяСтрокаРаспределения.ЕдиницаИзмерения);
			
			Если НЕ НоваяСтрокаРаспределения.Коэффициент = 0 Тогда
				НоваяСтрокаРаспределения.Количество = НоваяСтрокаРаспределения.Количество/НоваяСтрокаРаспределения.Коэффициент
			КонецЕсли;
			
			ОбработкаРеквизита("РаспределениеЗаказа.Номенклатура", НоваяСтрокаРаспределения,, ДопПараметры);
		КонецЦикла;
		ПерерасчетРаспределения();
		СуммаДокумента = РассчитатьСуммуВсего();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ ЭтотОбъект.ОбменДанными.Загрузка Тогда
		ОбработкаРеквизита("ХозОперация");
	КонецЕсли;

	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам",Права,,ЭтотОбъект) Тогда
		//Проверим наличие распределения данного заказа
		Если НЕ обПраво("РедактированиеЗаказовПриНаличииРаспределения",Права,,ЭтотОбъект) Тогда
			ВыборкаРаспределения=ПолучитьРаспределенияЗаказа();
			Если РежимЗаписи=РежимЗаписиДокумента.Запись Тогда
				Если ВыборкаРаспределения.Количество()>0 Тогда
					Сообщить("Товары по внутреннему заказу распределены по заказам поставщикам. Запись без проведения запрещена.",СтатусСообщения.Важное);
					Отказ=Истина;
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат;
				КонецЕсли; 
			ИначеЕсли РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения Тогда
				Если ВыборкаРаспределения.Количество()>0 Тогда
					Сообщить("Товары по внутреннему заказу распределены по заказам поставщикам. Отмена проведения запрещена.",СтатусСообщения.Важное);
					Отказ=Истина;
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат;
				КонецЕсли; 
			ИначеЕсли РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
				//Свернем товары с учетом единиц измерения
				ТоварыКопия=Товары.Выгрузить();
				Для Каждого СтрокаТоваров Из ТоварыКопия Цикл
					СтрокаТоваров.Количество=СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
				КонецЦикла; 
				ТоварыКопия.Свернуть("Номенклатура,ХарактеристикаНоменклатуры","Количество");
				ВыборкаРаспределения.Сбросить();
				Пока ВыборкаРаспределения.Следующий() Цикл
					СтрокиТоваров=ТоварыКопия.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ВыборкаРаспределения.Номенклатура,ВыборкаРаспределения.ХарактеристикаНоменклатуры));
					ТоварКоличество=0;
					Если СтрокиТоваров.Количество()>0 Тогда
						ТоварКоличество=СтрокиТоваров[0].Количество;
					КонецЕсли;
					Если ТоварКоличество<ВыборкаРаспределения.Количество Тогда
						Если обЗначениеНеЗаполнено(ВыборкаРаспределения.ХарактеристикаНоменклатуры) Тогда
							Сообщить("["+СокрЛП(ВыборкаРаспределения.Номенклатура.Код)+"] Товар """+СокрЛП(ВыборкаРаспределения.Номенклатура)+""" распределен по заказам поставщикам в количестве "+Формат(ВыборкаРаспределения.Количество,"ЧЦ=15; ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(ВыборкаРаспределения.Номенклатура.БазоваяЕдиницаИзмерения)+" !",СтатусСообщения.Внимание);
						Иначе
							Сообщить("["+СокрЛП(ВыборкаРаспределения.Номенклатура.Код)+"] Товар """+СокрЛП(ВыборкаРаспределения.Номенклатура)+""" с характеристикой """+СокрЛП(ВыборкаРаспределения.ХарактеристикаНоменклатуры)+""" распределен по заказам поставщикам в количестве "+Формат(ВыборкаРаспределения.Количество,"ЧЦ=15; ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(ВыборкаРаспределения.Номенклатура.БазоваяЕдиницаИзмерения)+" !",СтатусСообщения.Внимание);
						КонецЕсли;
						Отказ=Истина;
					КонецЕсли;
				КонецЦикла; 
				Если Отказ Тогда
					Если НЕ Ссылка.Пустая() Тогда    
						Сообщить("Проведение внутреннего заказа <"+СокрЛП(Ссылка)+"> отменено!",СтатусСообщения.Важное);	       
					Иначе
						Сообщить("Проведение внутреннего заказа отменено! Не заполнено одно из полей, обязательных для заполнения.",СтатусСообщения.Важное);
					КонецЕсли;
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ЗаказВнутренний Тогда
		Для Каждого СтрокаТабличнойЧасти Из Товары Цикл
			СтрокаТабличнойЧасти.Резерв = 0;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ Отказ И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		ТаблицаЗаказов = РаспределениеЗаказа.Выгрузить(, "ЗаказПокупателя");
		ТаблицаЗаказов.Свернуть("ЗаказПокупателя");
		ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПокупателя"));
		
		Если ТаблицаДолгов.Количество()>0 Тогда
			Для Каждого ТекСтрока Из РаспределениеЗаказа Цикл
				СтрокаПоиска = ТаблицаДолгов.Найти(ТекСтрока.ЗаказПокупателя, "Заказ");
				Если НЕ СтрокаПоиска = Неопределено Тогда
					Сообщить("Строка: " + ТекСтрока.НомерСтроки + ", Заказ: """+СокрЛП(ТекСтрока.ЗаказПокупателя)+""" нельзя распределить. Долг по предоплате составляет: "+Формат(СтрокаПоиска.Долг,"ЧЦ=15; ЧДЦ=2")+" "+СокрЛП(СтрокаПоиска.Валюта)+".", СтатусСообщения.Важное);
					Отказ = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	Заказывать    = Истина;
	Резервировать = ((ХозОперация=Справочники.ХозОперации.ЗаказРезервированиеВнутренние) ИЛИ (ХозОперация=Справочники.ХозОперации.РезервированиеВнутреннее));
	
	// проводим внутренний заказ
	НаборЗаписейЗаказыПокупателей=Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейЗаказыПокупателей.Контрагент=ПодразделениеПолучатель;
	НаборЗаписейЗаказыПокупателей.Заказ=Ссылка;
	НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладКомпании;
	НаборЗаписейЗаказыПокупателей.Заказывать=Заказывать;
	НаборЗаписейЗаказыПокупателей.Резервировать=Резервировать;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
	Если НЕ Отказ Тогда
		НаборЗаписейЗаказыПокупателей.Записать();
	КонецЕсли; 
	
	Если Резервировать Тогда
		// резервирование внутреннего заказа
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.ДвиженияПоРознице=Ложь;
		Отказ=НЕ НаборЗаписейОстатки.Зарезервировать() ИЛИ Отказ;
	КонецЕсли; 
	
	// зафиксируем распределение заказов покупателей на внутреннем заказе
	НаборЗаписейЗаказыРаспределение=Движения.ЗаказыРаспределение;
	НаборЗаписейЗаказыРаспределение.РежимПроведения=Режим;
	НаборЗаписейЗаказыРаспределение.ДокументОбъект=ЭтотОбъект;
	Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя КАК ЗаказПокупателя,
		|	СУММА(ДокументТовары.Количество*ДокументТовары.Коэффициент) КАК Количество
		|ИЗ
		|	Документ.ЗаказВнутренний.РаспределениеЗаказа КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка=&Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя
		|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	НаборЗаписейЗаказыРаспределение.РезультатЗапросаПоТоварам=Запрос.Выполнить();
	НаборЗаписейЗаказыРаспределение.ЗаказПокупателя=Неопределено; //Заказ находится в табличной части
	НаборЗаписейЗаказыРаспределение.ЗаказПоставщика=Ссылка;
	Отказ=НЕ НаборЗаписейЗаказыРаспределение.Приход() ИЛИ Отказ;
	
	// двигаем границу последовательности заказов
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаЗаказов = Движения.ЗаказыРаспределение.Выгрузить();
			ТаблицаЗаказов.Свернуть("ЗаказПокупателя","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПокупателя");
			НаборЗаписейГраницыЗаказы.Заказ.Добавить(Ссылка);
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	Если Не Отказ Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ЗаказВнутреннийТовары.ИдентификаторСтроки,
		|	ЗаказВнутреннийТовары.Номенклатура,
		|	ЗаказВнутреннийТовары.ХарактеристикаНоменклатуры,
		|	ЗаказВнутреннийТовары.Количество*ЗаказВнутреннийТовары.Коэффициент КАК КоличествоЗаказа,
		|	ГрафикПоставок.Количество,
		|	ГрафикПоставок.ДатаПоставки,
		|	ГрафикПоставок.Контрагент,
		|	ГрафикПоставок.ДатаПоступления,
		|	ГрафикПоставок.Информация
		|ИЗ
		|	Документ.ЗаказВнутренний.Товары КАК ЗаказВнутреннийТовары
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ГрафикПоставок КАК ГрафикПоставок
		|ПО
		|	ЗаказВнутреннийТовары.Ссылка = &Ссылка И 
		|	ЗаказВнутреннийТовары.Ссылка = ГрафикПоставок.ЗаказПокупателя И 
		|	ЗаказВнутреннийТовары.ИдентификаторСтроки = ГрафикПоставок.ИдентификаторСтроки
		|ГДЕ
		|	ЗаказВнутреннийТовары.Ссылка = &Ссылка
		|ИТОГИ
		|	МАКСИМУМ(КоличествоЗаказа)
		|ПО
		|	ЗаказВнутреннийТовары.ИдентификаторСтроки
		|";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ВыборкаСтроки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		НаборЗаписейГрафикПоставок = РегистрыСведений.ГрафикПоставок.СоздатьНаборЗаписей();
		НаборЗаписейГрафикПоставок.Отбор.ЗаказПокупателя.Установить(Ссылка);
		
		Пока ВыборкаСтроки.Следующий() Цикл
			КоличествоЗаказа    = ВыборкаСтроки.КоличествоЗаказа;
			Выборка = ВыборкаСтроки.Выбрать(ОбходРезультатаЗапроса.Прямой);
			Пока Выборка.Следующий() Цикл
				Если КоличествоЗаказа <= 0 Тогда
					Прервать;
				КонецЕсли;
				Если Выборка.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				Количество = Мин(Выборка.Количество, КоличествоЗаказа);
				Запись = НаборЗаписейГрафикПоставок.Добавить();
				Запись.ЗаказПокупателя            = Ссылка;
				Запись.ИдентификаторСтроки        = Выборка.ИдентификаторСтроки;
				Запись.Номенклатура               = Выборка.Номенклатура;
				Запись.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				Запись.Количество                 = Количество;
				Запись.ДатаПоставки               = Выборка.ДатаПоставки;
				Запись.Контрагент                 = Выборка.Контрагент;
				Запись.ДатаПоступления            = Выборка.ДатаПоступления;
				Запись.Информация                 = Выборка.Информация;
				КоличествоЗаказа = КоличествоЗаказа - Количество;
			КонецЦикла;
		КонецЦикла;
		
		НаборЗаписейГрафикПоставок.Записать();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
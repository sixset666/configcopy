// Модуль документа Корректировка реализации

#Область ОписаниеПеременных

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;                            // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;               // Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ТекущаяВалютаДокумента Экспорт;           // Текущая валюта документа перед изменением
Перем ТекущийКурсДокумента Экспорт;             //Текущий курс документа перед изменением
Перем СуммаРазногласий;                         //Текущая сумма разногласий

Перем ИмяРеквизитаКода Экспорт;                 // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТекущаяВерсияОбъекта;

#КонецОбласти

// Формирует движения накоплений по карточкам контрагентов
//
Процедура ОбработкаПроведенияНакопленияПоКарточкамКонтрагентов()
	
	Если НЕ ЗначениеЗаполнено(Карточка) ИЛИ Сделка.ХозОперация <> Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаРеализацииТовары.Ссылка.Контрагент,
	|	КорректировкаРеализацииТовары.Ссылка.Карточка,
	|	КорректировкаРеализацииТовары.Ссылка.Дата КАК ПериодНакопления,
	|	СУММА(КорректировкаРеализацииТовары.КоличествоРазница) КАК КоличествоНоменклатуры,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоРазница) КАК Сумма,
	|	СУММА(0) КАК КоличествоЧеков
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииТовары.КоличествоРазница <> 0
	|	ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.Ссылка.Контрагент,
	|	КорректировкаРеализацииТовары.Ссылка.Карточка,
	|	КорректировкаРеализацииТовары.Ссылка.Дата";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	НаборНакоплениеСумм = Движения.НакопленияПоКарточкамКонтрагентов;
	НаборНакоплениеСумм.ДокументСсылка   = Ссылка;
	НаборНакоплениеСумм.РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	НаборНакоплениеСумм.ВыполнитьДвижения();
	
КонецПроцедуры

// Возвращает результат запроса по таблице товаров  
//
// Возвращаемое значение: 
//	ТаблицаЗначений - таблица, заполненная товарам из документа Корректировки реализации 
//
Функция ПолучитьТаблицуТоваров()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокТовары.ГТД КАК ГТД,
		|	СУММА(ДокТовары.Количество * ДокТовары.Коэффициент) КАК Количество,
		|	ДокТовары.Партия КАК Партия,
		|	ДокТовары.Номенклатура КАК Номенклатура,
		|	КорректировкаРеализации.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	Документ.КорректировкаРеализации.Товары КАК ДокТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|		ПО ДокТовары.Ссылка = КорректировкаРеализации.Ссылка
		|ГДЕ
		|	ДокТовары.Ссылка = &Ссылка
		|	И ДокТовары.Номенклатура.ВидНоменклатуры <> &Услуга
		|	И ДокТовары.Количество > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокТовары.Номенклатура,
		|	ДокТовары.ХарактеристикаНоменклатуры,
		|	ДокТовары.Партия,
		|	ДокТовары.ГТД,
		|	КорректировкаРеализации.СкладКомпании";    
	
	ЗапросТовары = Новый Запрос(ТекстЗапроса);
	ЗапросТовары.УстановитьПараметр("Ссылка", Ссылка);
	ЗапросТовары.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
	
	Возврат ЗапросТовары.Выполнить().Выгрузить();
	
КонецФункции

Функция ПерезаполнитьСкладыТаблицыТоваров(ТаблицаТовары, СтарыеДвиженияПартий)
	
	НоваяТаблица = ТаблицаТовары.Скопировать();
	НоваяТаблица.Очистить();
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура", Строка.Номенклатура);
		
		Если ЗначениеЗаполнено(Строка.ХарактеристикаНоменклатуры) Тогда
			ПараметрыОтбора.Вставить("ХарактеристикаНоменклатуры", Строка.ХарактеристикаНоменклатуры);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.Партия) Тогда
			ПараметрыОтбора.Вставить("Партия", Строка.Партия);
		КонецЕсли;
		
		НайденныеСтроки = СтарыеДвиженияПартий.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ВсегоКоличество = Строка.Количество;
			
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				НоваяСтрока = НоваяТаблица.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, "Номенклатура, ХарактеристикаНоменклатуры, Партия");
				НоваяСтрока.СкладКомпании = НайденнаяСтрока.СкладКомпании;
				НоваяСтрока.Количество = Мин(ВсегоКоличество, НайденнаяСтрока.Количество);
				
				ВсегоКоличество = ВсегоКоличество - НоваяСтрока.Количество;
				Если ВсегоКоличество = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ВсегоКоличество > 0 Тогда
				НоваяСтрока = НоваяТаблица.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, "Номенклатура, ХарактеристикаНоменклатуры, Партия");
				НоваяСтрока.Количество = ВсегоКоличество;
				НоваяСтрока.СкладКомпании = Строка.СкладКомпании;
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока = НоваяТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НоваяТаблица;
	
КонецФункции

// Считает разницу движений по партиям
//
// Параметры:
//  ПрошлыеДвижения	 - ТаблицаЗначений	- таблица движений по регистру Партий товаров компании до проведения документа.
//  БудущиеДвижения	 - ТаблицаЗначений	- таблица движений по регистру Партий товаров компании после проведения документа.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - 	таблица, с новыми движениями, в которой:
//							- удалены идентичные движения обоих таблиц;
//							- посчитана разница движений, которые отличаются количеством;
//                          - оставлены уникальные движения, которые отличаются чем-то, кроме как количеством
//
Функция ПосчитатьРазницуДвиженийПоПартиям(ПрошлыеДвижения, БудущиеДвижения)
	
	СтарыеДвижения = ПрошлыеДвижения.Скопировать();
	СтарыеДвижения.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, Партия, СкладКомпании", "Количество");
	
	НовыеДвижения = БудущиеДвижения.Скопировать();
	НовыеДвижения.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, Партия, СкладКомпании", "Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтарыеДвижения.Номенклатура КАК Номенклатура,
		|	СтарыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СтарыеДвижения.Партия КАК Партия,
		|	СтарыеДвижения.СкладКомпании КАК СкладКомпании,
		|	СтарыеДвижения.Количество КАК Количество
		|ПОМЕСТИТЬ ВТ_СтарыеДвижения
		|ИЗ
		|	&СтарыеДвижения КАК СтарыеДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеДвижения.Номенклатура КАК Номенклатура,
		|	НовыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	НовыеДвижения.Партия КАК Партия,
		|	НовыеДвижения.СкладКомпании КАК СкладКомпании,
		|	НовыеДвижения.Количество КАК Количество
		|ПОМЕСТИТЬ ВТ_НовыеДвижения
		|ИЗ
		|	&НовыеДвижения КАК НовыеДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НовыеДвижения.Номенклатура КАК Номенклатура,
		|	ВТ_НовыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_НовыеДвижения.Партия КАК Партия,
		|	ВТ_НовыеДвижения.СкладКомпании КАК СкладКомпании,
		|	ВТ_НовыеДвижения.Количество КАК Количество
		|ИЗ
		|	ВТ_СтарыеДвижения КАК ВТ_СтарыеДвижения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НовыеДвижения КАК ВТ_НовыеДвижения
		|		ПО ВТ_СтарыеДвижения.Номенклатура = ВТ_НовыеДвижения.Номенклатура
		|			И ВТ_СтарыеДвижения.ХарактеристикаНоменклатуры = ВТ_НовыеДвижения.ХарактеристикаНоменклатуры
		|			И ВТ_СтарыеДвижения.Партия = ВТ_НовыеДвижения.Партия
		|			И ВТ_СтарыеДвижения.СкладКомпании = ВТ_НовыеДвижения.СкладКомпании
		|			И ВТ_СтарыеДвижения.Количество = ВТ_НовыеДвижения.Количество";
	
	Запрос.УстановитьПараметр("СтарыеДвижения", СтарыеДвижения);
	Запрос.УстановитьПараметр("НовыеДвижения", НовыеДвижения);
	
	ИдентичныеДвижения = Запрос.Выполнить().Выбрать();
	
	// Удалим идентичные движения из обеих таблиц.
	Пока ИдентичныеДвижения.Следующий() Цикл
		
		ОтборДвижения = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Партия, СкладКомпании, Количество", 
			ИдентичныеДвижения.Номенклатура,
			ИдентичныеДвижения.ХарактеристикаНоменклатуры,
			ИдентичныеДвижения.Партия,
			ИдентичныеДвижения.СкладКомпании,
			ИдентичныеДвижения.Количество);
			
		МассивСтрокСтарыхДвижений = СтарыеДвижения.НайтиСтроки(ОтборДвижения); 
		МассивСтрокНовыхДвижений = НовыеДвижения.НайтиСтроки(ОтборДвижения);
		
		Для Сч = 0 По МассивСтрокСтарыхДвижений.ВГраница() Цикл
			СтарыеДвижения.Удалить(МассивСтрокСтарыхДвижений[Сч]);
		КонецЦикла;
		
		Для Сч = 0 По МассивСтрокНовыхДвижений.ВГраница() Цикл
			НовыеДвижения.Удалить(МассивСтрокНовыхДвижений[Сч]);
		КонецЦикла;
		
	КонецЦикла;
	
	// Обрабатываем движения, отличающиеся только количеством.
	Для каждого НовоеДвижение Из НовыеДвижения Цикл
		ОтборДвижения = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Партия, СкладКомпании", 
			НовоеДвижение.Номенклатура,
			НовоеДвижение.ХарактеристикаНоменклатуры,
			НовоеДвижение.Партия,
			НовоеДвижение.СкладКомпании);
			
		МассивСтрокСтарыхДвижений = СтарыеДвижения.НайтиСтроки(ОтборДвижения); 
		
		Для Сч = 0 По МассивСтрокСтарыхДвижений.ВГраница() Цикл
			НовоеДвижение.Количество = НовоеДвижение.Количество - МассивСтрокСтарыхДвижений[Сч].Количество;
			СтарыеДвижения.Удалить(МассивСтрокСтарыхДвижений[Сч]);
		КонецЦикла;
	КонецЦикла;
	
	// Объединяем в одну таблицу разницы движений.
	Для каждого СтароеДвижение Из СтарыеДвижения Цикл
		СтрокаНовогоДвижения = НовыеДвижения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНовогоДвижения, СтароеДвижение);
		СтрокаНовогоДвижения.Количество = -СтрокаНовогоДвижения.Количество;
	КонецЦикла;
	
	Возврат НовыеДвижения;

КонецФункции // ПосчитатьРазницуДвиженийПоПартиям()

// Считает разницу движений по ГТД
//
// Параметры:
//  ПрошлыеДвижения	 - ТаблицаЗначений	- таблица движений по регистру ГТД партий товаров компании до проведения документа.
//  БудущиеДвижения	 - ТаблицаЗначений	- таблица движений по регистру ГТД партий товаров компании после проведения документа.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - 	таблица, с новыми движениями, в которой:
//							- удалены идентичные движения обоих таблиц;
//							- посчитана разница движений, которые отличаются количеством;
//                          - оставлены уникальные движения, которые отличаются чем-то, кроме как количеством
//
Функция ПосчитатьРазницуДвиженийПоГТД(ПрошлыеДвижения, БудущиеДвижения)
	
	СтарыеДвижения = ПрошлыеДвижения.Скопировать();
	СтарыеДвижения.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, Партия, СкладКомпании, ГТД", "Количество");
	
	НовыеДвижения = БудущиеДвижения.Скопировать();
	НовыеДвижения.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, Партия, СкладКомпании, ГТД", "Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтарыеДвижения.Номенклатура КАК Номенклатура,
		|	СтарыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СтарыеДвижения.Партия КАК Партия,
		|	СтарыеДвижения.СкладКомпании КАК СкладКомпании,
		|	СтарыеДвижения.Количество КАК Количество,
		|	СтарыеДвижения.ГТД КАК ГТД
		|ПОМЕСТИТЬ ВТ_СтарыеДвижения
		|ИЗ
		|	&СтарыеДвижения КАК СтарыеДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеДвижения.Номенклатура КАК Номенклатура,
		|	НовыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	НовыеДвижения.Партия КАК Партия,
		|	НовыеДвижения.СкладКомпании КАК СкладКомпании,
		|	НовыеДвижения.Количество КАК Количество,
		|	НовыеДвижения.ГТД КАК ГТД
		|ПОМЕСТИТЬ ВТ_НовыеДвижения
		|ИЗ
		|	&НовыеДвижения КАК НовыеДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НовыеДвижения.Номенклатура КАК Номенклатура,
		|	ВТ_НовыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_НовыеДвижения.Партия КАК Партия,
		|	ВТ_НовыеДвижения.СкладКомпании КАК СкладКомпании,
		|	ВТ_НовыеДвижения.Количество КАК Количество,
		|	ВТ_НовыеДвижения.ГТД КАК ГТД
		|ИЗ
		|	ВТ_СтарыеДвижения КАК ВТ_СтарыеДвижения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НовыеДвижения КАК ВТ_НовыеДвижения
		|		ПО ВТ_СтарыеДвижения.Номенклатура = ВТ_НовыеДвижения.Номенклатура
		|			И ВТ_СтарыеДвижения.ХарактеристикаНоменклатуры = ВТ_НовыеДвижения.ХарактеристикаНоменклатуры
		|			И ВТ_СтарыеДвижения.Партия = ВТ_НовыеДвижения.Партия
		|			И ВТ_СтарыеДвижения.СкладКомпании = ВТ_НовыеДвижения.СкладКомпании
		|			И ВТ_СтарыеДвижения.Количество = ВТ_НовыеДвижения.Количество
		|			И ВТ_СтарыеДвижения.ГТД = ВТ_НовыеДвижения.ГТД";
	
	Запрос.УстановитьПараметр("СтарыеДвижения", СтарыеДвижения);
	Запрос.УстановитьПараметр("НовыеДвижения", НовыеДвижения);
	
	ИдентичныеДвижения = Запрос.Выполнить().Выбрать();
	
	// Удалим идентичные движения из обеих таблиц.
	Пока ИдентичныеДвижения.Следующий() Цикл
		
		ОтборДвижения = Новый Структура();
		ОтборДвижения.Вставить("Номенклатура",               ИдентичныеДвижения.Номенклатура);
		ОтборДвижения.Вставить("ХарактеристикаНоменклатуры", ИдентичныеДвижения.ХарактеристикаНоменклатуры);
		ОтборДвижения.Вставить("Партия",                     ИдентичныеДвижения.Партия);
		ОтборДвижения.Вставить("СкладКомпании",              ИдентичныеДвижения.СкладКомпании);
		ОтборДвижения.Вставить("ГТД",                        ИдентичныеДвижения.ГТД);
		ОтборДвижения.Вставить("Количество",                 ИдентичныеДвижения.Количество);
			
		МассивСтрокСтарыхДвижений = СтарыеДвижения.НайтиСтроки(ОтборДвижения); 
		МассивСтрокНовыхДвижений = НовыеДвижения.НайтиСтроки(ОтборДвижения);
		
		Для Сч = 0 По МассивСтрокСтарыхДвижений.ВГраница() Цикл 
			
			СтарыеДвижения.Удалить(МассивСтрокСтарыхДвижений[Сч]);
			
		КонецЦикла;
		
		Для Сч = 0 По МассивСтрокНовыхДвижений.ВГраница() Цикл
			
			НовыеДвижения.Удалить(МассивСтрокНовыхДвижений[Сч]);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Обрабатываем движения, отличающиеся только количеством.
	Для каждого НовоеДвижение Из НовыеДвижения Цикл
		
		ОтборДвижения = Новый Структура();
		ОтборДвижения.Вставить("Номенклатура",               НовоеДвижение.Номенклатура);
		ОтборДвижения.Вставить("ХарактеристикаНоменклатуры", НовоеДвижение.ХарактеристикаНоменклатуры);
		ОтборДвижения.Вставить("Партия",                     НовоеДвижение.Партия);
		ОтборДвижения.Вставить("СкладКомпании",              НовоеДвижение.СкладКомпании);
		ОтборДвижения.Вставить("ГТД",                        НовоеДвижение.ГТД);
			
		МассивСтрокСтарыхДвижений = СтарыеДвижения.НайтиСтроки(ОтборДвижения); 
		
		Для Сч = 0 По МассивСтрокСтарыхДвижений.ВГраница() Цикл
			
			НовоеДвижение.Количество = НовоеДвижение.Количество - МассивСтрокСтарыхДвижений[Сч].Количество;
			СтарыеДвижения.Удалить(МассивСтрокСтарыхДвижений[Сч]);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Объединяем в одну таблицу разницы движений.
	Для каждого СтароеДвижение Из СтарыеДвижения Цикл
		
		СтрокаНовогоДвижения = НовыеДвижения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНовогоДвижения, СтароеДвижение);
		СтрокаНовогоДвижения.Количество = -СтрокаНовогоДвижения.Количество;
		
	КонецЦикла;
	
	Возврат НовыеДвижения;
	
КонецФункции // ПосчитатьРазницуДвиженийПоГТД()

Функция ПосчитатьРазницуДвиженийПоПродажам(СтарыеДвижения, НовыеДвижения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтарыеДвижения.Номенклатура КАК Номенклатура,
		|	СтарыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СтарыеДвижения.Партия КАК Партия,
		|	СтарыеДвижения.СкладКомпании КАК СкладКомпании,
		|	СтарыеДвижения.Количество КАК Количество,
		|	СтарыеДвижения.ГТД КАК ГТД,
		|	СтарыеДвижения.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_СтарыеДвижения
		|ИЗ
		|	&СтарыеДвижения КАК СтарыеДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеДвижения.Номенклатура КАК Номенклатура,
		|	НовыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	НовыеДвижения.Партия КАК Партия,
		|	НовыеДвижения.СкладКомпании КАК СкладКомпании,
		|	НовыеДвижения.Количество КАК Количество,
		|	НовыеДвижения.ГТД КАК ГТД,
		|	НовыеДвижения.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_НовыеДвижения
		|ИЗ
		|	&НовыеДвижения КАК НовыеДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НовыеДвижения.Номенклатура КАК Номенклатура,
		|	ВТ_НовыеДвижения.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_НовыеДвижения.Партия КАК Партия,
		|	ВТ_НовыеДвижения.СкладКомпании КАК СкладКомпании,
		|	ВТ_НовыеДвижения.Количество КАК Количество,
		|	ВТ_НовыеДвижения.ГТД КАК ГТД
		|ИЗ
		|	ВТ_СтарыеДвижения КАК ВТ_СтарыеДвижения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НовыеДвижения КАК ВТ_НовыеДвижения
		|		ПО ВТ_СтарыеДвижения.Номенклатура = ВТ_НовыеДвижения.Номенклатура
		|			И ВТ_СтарыеДвижения.ХарактеристикаНоменклатуры = ВТ_НовыеДвижения.ХарактеристикаНоменклатуры
		|			И ВТ_СтарыеДвижения.Партия = ВТ_НовыеДвижения.Партия
		|			И ВТ_СтарыеДвижения.СкладКомпании = ВТ_НовыеДвижения.СкладКомпании
		|			И ВТ_СтарыеДвижения.Количество = ВТ_НовыеДвижения.Количество
		|			И ВТ_СтарыеДвижения.ГТД = ВТ_НовыеДвижения.ГТД
		|			И ВТ_СтарыеДвижения.Сумма = ВТ_НовыеДвижения.Сумма";
	
	Запрос.УстановитьПараметр("СтарыеДвижения", СтарыеДвижения);
	Запрос.УстановитьПараметр("НовыеДвижения", НовыеДвижения);
	
	ИдентичныеДвижения = Запрос.Выполнить().Выбрать();
	
	// Удалим идентичные движения из обеих таблиц.
	Пока ИдентичныеДвижения.Следующий() Цикл
		
		ОтборДвижения = Новый Структура();
		ОтборДвижения.Вставить("Номенклатура",               ИдентичныеДвижения.Номенклатура);
		ОтборДвижения.Вставить("ХарактеристикаНоменклатуры", ИдентичныеДвижения.ХарактеристикаНоменклатуры);
		ОтборДвижения.Вставить("Партия",                     ИдентичныеДвижения.Партия);
		ОтборДвижения.Вставить("СкладКомпании",              ИдентичныеДвижения.СкладКомпании);
		ОтборДвижения.Вставить("ГТД",                        ИдентичныеДвижения.ГТД);
		ОтборДвижения.Вставить("Количество",                 ИдентичныеДвижения.Количество);
			
		МассивСтрокСтарыхДвижений = СтарыеДвижения.НайтиСтроки(ОтборДвижения); 
		МассивСтрокНовыхДвижений = НовыеДвижения.НайтиСтроки(ОтборДвижения);
		
		Для Сч = 0 По МассивСтрокСтарыхДвижений.ВГраница() Цикл 
			
			СтарыеДвижения.Удалить(МассивСтрокСтарыхДвижений[Сч]);
			
		КонецЦикла;
		
		Для Сч = 0 По МассивСтрокНовыхДвижений.ВГраница() Цикл
			
			НовыеДвижения.Удалить(МассивСтрокНовыхДвижений[Сч]);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Обрабатываем движения, отличающиеся только количеством.
	НомерСтроки = 0; 
	Для каждого НовоеДвижение Из НовыеДвижения Цикл
		
		ОтборДвижения = Новый Структура();
		ОтборДвижения.Вставить("Номенклатура",               НовоеДвижение.Номенклатура);
		ОтборДвижения.Вставить("ХарактеристикаНоменклатуры", НовоеДвижение.ХарактеристикаНоменклатуры);
		ОтборДвижения.Вставить("Партия",                     НовоеДвижение.Партия);
		ОтборДвижения.Вставить("СкладКомпании",              НовоеДвижение.СкладКомпании);
		ОтборДвижения.Вставить("ГТД",                        НовоеДвижение.ГТД);
			
		МассивСтрокСтарыхДвижений = СтарыеДвижения.НайтиСтроки(ОтборДвижения); 
		
		Для Сч = 0 По МассивСтрокСтарыхДвижений.ВГраница() Цикл
			
			НовоеДвижение.Количество = НовоеДвижение.Количество - МассивСтрокСтарыхДвижений[Сч].Количество;
			СтарыеДвижения.Удалить(МассивСтрокСтарыхДвижений[Сч]);
			
		КонецЦикла;
		
		
		НовоеДвижение.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;

	Возврат НовыеДвижения;
	
КонецФункции

// Возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку.
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.Проект КАК Проект,
	|	Док.Организация КАК Организация,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СуммаНоменклатурыДокумента КАК СуммаНоменклатурыДокумента,
	|	"+?(ТипЗнч(Сделка)=Тип("ДокументСсылка.ЗаказНаряд"),"Док.СуммаРаботДокумента","0")+" КАК СуммаРаботДокумента,
	|	&СуммаРазногласий КАК СуммаДокумента,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.ДокументОснование.ДокументОснование КАК ДокументОснованиеОснование,
	|	Док.Сделка КАК Сделка
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Если ТипЗнч(Сделка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Запрос.УстановитьПараметр("СуммаРазногласий",Товары.Итог("СуммаВсегоРазница")+Работы.Итог("СуммаВсегоРазница"));
	Иначе
		Запрос.УстановитьПараметр("СуммаРазногласий",Товары.Итог("СуммаВсегоРазница"));
	КонецЕсли; 
	Запрос.УстановитьПараметр("СуммаРазногласий",Товары.Итог("СуммаВсегоРазница"));
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

Процедура ПроверитьПравильностьЦепочкиДокументов(Отказ)
	
	Если ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииИсправлениеВПервичныхДокументах Тогда
		
		// проверим введенные на основании корректировки
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодчиненныеДокументы.Ссылка КАК Документ
		|ИЗ
		|	КритерийОтбора.ПодчиненныеДокументы(&Основание) КАК ПодчиненныеДокументы
		|ГДЕ
		|	ПодчиненныеДокументы.Ссылка ССЫЛКА Документ.КорректировкаРеализации
		|	И НЕ ПодчиненныеДокументы.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.КорректировкаРеализацииКорректировкаПоСогласованиюСторон)
		|	И НЕ ПодчиненныеДокументы.Ссылка = &Ссылка";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Основание", ДокументОснование);
		Запрос.УстановитьПараметр("Ссылка",    Ссылка);
		ВыполнениеЗапроса = Запрос.Выполнить();
		
		Если Не ВыполнениеЗапроса.Пустой() Тогда
			
			ТекстСообщения = НСтр(
				"ru = 'К документу %1 введено больше одного корректировочного документа с хоз. операцией ""Исправление первичных документов"".
							|Каждую последующую корректировку следует вводить на основании предыдущей.'"
			);
			Сообщить(ТекстСообщения, СтатусСообщения.Информация);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события заполнения объекта копированием.
//
// Параметры:
//  ОбъектКопирования - ДокументОбъект - Исходный объект, который является источником копирования.
//
Процедура ПриКопировании(ОбъектКопирования)

	ВерсияОбъекта = ТекущаяВерсияОбъекта;
	
	// Вызываем общий обработчик события
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	КодыМаркировки.Очистить();
	
КонецПроцедуры

// Обработчик события возникающего после записи объекта в базу данных, но до окончания транзакции записи.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ПриЗаписи(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события возникающего в транзакции удаления перед непосредственным удалением объекта из базы данных.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ПередУдалением(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистру Партий товаров компании и ГТД партий товаров (уменьшение товарного запаса)  
//
// Возвращаемое значение: 
//	Булево. Истина - все ОК, Ложь - чего-то не так.
// 
Функция ПровестиПоПартиямТоваровКомпанииИГТД(Основания, РежимПроведения)
	
	ВсеОК = Истина; 
	ТекстСообщения = "";
	ЭтоКорректировкаПоЗаказНаряду = Ложь;
	ЦехОснования = СкладКомпании;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд") Тогда 
		ЭтоКорректировкаПоЗаказНаряду = Истина; 
		ЦехОснования = ДокументОснование.Цех;
	КонецЕсли;
	
	Основания_Документы = Документы.КорректировкаРеализации.ДобавитьПеремещенияТоваровЗаказНарядаИзОснований(Основания, Ложь);
	
	СтарыеДвиженияПартий = ДвиженияПартийДоКорректировки(Основания_Документы, Основания);
	СтарыеДвиженияГТД = ДвиженияГТДДоКорректировки(Основания_Документы, Основания);
	
	НовыеДвиженияПартий = ДвиженияПартийПослеКорректировки(Неопределено, СтарыеДвиженияПартий, ТекстСообщения);
	ВсеОК = НовыеДвиженияПартий.ВсеОК И ВсеОК;
	
	НовыеДвиженияГТД = ДвиженияГТДПослеКорректировки(НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий, Ложь, СтарыеДвиженияГТД, ТекстСообщения);
	ВсеОК = НовыеДвиженияГТД.ВсеОК И ВсеОК;

	Если ВсеОК Тогда 
		
		РазницаПартий = ПосчитатьРазницуДвиженийПоПартиям(СтарыеДвиженияПартий, НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий);
		РазницаГТД = ПосчитатьРазницуДвиженийПоГТД(СтарыеДвиженияГТД, НовыеДвиженияГТД.ТаблицаНовыхДвиженийГТД);
		
		Если ЗначениеЗаполнено(РазницаПартий) Тогда
			СтруткураДвиженийДляЗаписиВРегистрПартий = ДвиженияПартийПослеКорректировки(РазницаПартий, СтарыеДвиженияПартий, ТекстСообщения);
			ВсеОК = СтруткураДвиженийДляЗаписиВРегистрПартий.ВсеОК И ВсеОК;
			Если ВсеОК Тогда
				
				Для каждого ДвижениеПартии Из СтруткураДвиженийДляЗаписиВРегистрПартий.ТаблицаНовыхДвиженийПартий Цикл
					
					НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеПартии);
					НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяЗапись.Регистратор = Ссылка;
					НоваяЗапись.Период      = Дата;
					
				КонецЦикла;
				
				Движения.ПартииТоваровКомпании.Записать();
			КонецЕсли;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РазницаГТД) Тогда
			
			СтруткураДвиженийДляЗаписиВРегистрГТД = ДвиженияГТДПослеКорректировки(РазницаГТД, Истина, СтарыеДвиженияГТД, ТекстСообщения);
			ВсеОК = СтруткураДвиженийДляЗаписиВРегистрГТД.ВсеОК И ВсеОК;
			
			Если ВсеОК Тогда 
				
				Для каждого ДвижениеГТД Из СтруткураДвиженийДляЗаписиВРегистрГТД.ТаблицаНовыхДвиженийГТД Цикл
					
					НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеГТД);
					НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяЗапись.Регистратор = Ссылка;
					НоваяЗапись.Период      = Дата;
					
				КонецЦикла;
				
				Движения.ГТДПартийТоваровКомпании.Записать(); 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
		
	СтарыеДвиженияПродаж = ДвиженияПродажДоКорректировки(Основания);
	ДвиженияДляЗаписиПродаж = ДвиженияПродажПослеКорректировки(
		РежимПроведения,
		НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий,
		НовыеДвиженияГТД.ТаблицаНовыхДвиженийГТД,
		СтарыеДвиженияПродаж,
		ЭтоКорректировкаПоЗаказНаряду,
		ЦехОснования,
		ТекстСообщения
	);
	ВсеОК = ДвиженияДляЗаписиПродаж.ВсеОК И ВсеОК; 
	
	Если ВсеОК И ЗначениеЗаполнено(ДвиженияДляЗаписиПродаж.ТаблицаНовыхДвиженийПродаж) Тогда
		
		Для Каждого ДвижениеПродажи Из ДвиженияДляЗаписиПродаж.ТаблицаНовыхДвиженийПродаж Цикл
			
			НоваяЗапись = Движения.Продажи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеПродажи);
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.Период      = Дата;
			
		КонецЦикла;
		
	КонецЕсли;
		
	Если НЕ ВсеОК Тогда
		дкСообщитьРезультат(ТекстСообщения,"Важное&Партии товаров компании:", ЭтотОбъект);
	КонецЕсли;
		
	Возврат ВсеОК;
	
КонецФункции

Процедура ПровестиПоРеализованнымТоварам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	СУММА(КорректировкаРеализацииТовары.Количество*КорректировкаРеализацииТовары.Коэффициент) КАК Количество,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего) КАК СуммаВсего,
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС) КАК СуммаНДС,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	(КорректировкаРеализацииТовары.КоличествоРазница <> 0
	|	ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0)
	|	И КорректировкаРеализацииТовары.Количество > 0
	|	И КорректировкаРеализацииТовары.Ссылка = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.СтавкаНДС";
	Запрос.УстановитьПараметр("Регистратор",Ссылка);
	ТаблДок =  Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТоваровКомпании.СкладКомпании,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.СтатусПартии,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.Количество,
	|	ПартииТоваровКомпании.Сумма,
	|	ПартииТоваровКомпании.СуммаНДС,
	|	ПартииТоваровКомпании.СуммаУпр,
	|	ПартииТоваровКомпании.ХозОперация,
	|	ПартииТоваровКомпании.СтавкаНДС,
	|	ПартииТоваровКомпании.Проект
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Регистратор
	|	И ПартииТоваровКомпании.Номенклатура В(&Номенклатура)
	|	И ПартииТоваровКомпании.СтатусПартии = &СтатусПартии
	|	И ПартииТоваровКомпании.ВидДвижения = &ВидДвижения";
	
	Запрос.УстановитьПараметр("ВидДвижения"  , ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Номенклатура" , ТаблДок.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Регистратор"  , Ссылка);
	Запрос.УстановитьПараметр("СтатусПартии" , Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	ТаблДвижений = Запрос.Выполнить().Выгрузить();
	
	ТаблГТД = Движения.ГТДПартийТоваровКомпании.Выгрузить().Скопировать();
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	Для каждого стрДвижений Из ТаблДвижений Цикл
		НадоОприходовать                = стрДвижений.Количество;
		НадоОприходоватьСумма           = стрДвижений.СуммаУпр;
		НадоОприходоватьСуммаРегл       = стрДвижений.Сумма;
		НадоОприходоватьСуммаНДСРегл    = стрДвижений.СуммаНДС;
		
		// пройдемся по ГТД
		стрПоиск = Новый Структура;
		стрПоиск.Вставить("Номенклатура",стрДвижений.Номенклатура);
		стрПоиск.Вставить("Партия", стрДвижений.Партия);
		Если ЗначениеЗаполнено(стрДвижений.ХарактеристикаНоменклатуры) Тогда
			стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрДвижений.ХарактеристикаНоменклатуры);
		КонецЕсли;
		МассивГТД = ТаблГТД.НайтиСтроки(стрПоиск);
		
		Для Каждого стрГТД Из МассивГТД Цикл
			Если стрГТД.Количество = 0 Тогда Продолжить; КонецЕсли;
			Если НадоОприходовать = 0 Тогда Прервать; КонецЕсли;
			
			КоличествоПриходуем      = Мин(стрГТД.Количество, НадоОприходовать);
			СуммаПриходуем           = ?(НадоОприходовать = КоличествоПриходуем,НадоОприходоватьСумма,НадоОприходоватьСумма/НадоОприходовать*КоличествоПриходуем);
			СуммаРеглПриходуем       = ?(НадоОприходовать = КоличествоПриходуем,НадоОприходоватьСуммаРегл,НадоОприходоватьСуммаРегл/НадоОприходовать*КоличествоПриходуем);
			СуммаНДСРеглПриходуем    = ?(НадоОприходовать = КоличествоПриходуем,НадоОприходоватьСуммаНДСРегл,НадоОприходоватьСуммаНДСРегл/НадоОприходовать*КоличествоПриходуем);
			
			// получим массивы строк
			МассивСХар   = ТаблДок.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",стрДвижений.Номенклатура,стрДвижений.ХарактеристикаНоменклатуры));
			МассивБезХар = ТаблДок.НайтиСтроки(Новый Структура("Номенклатура",стрДвижений.Номенклатура));
			
			Если МассивСХар.Количество() > 0 Тогда
				ТекСтрока = МассивСХар[0];
			ИначеЕсли МассивБезХар.Количество() > 0 Тогда
				ТекСтрока = МассивБезХар[0];
			Иначе
				ТекСтрока = Неопределено;
			КонецЕсли;
			
			НоваяЗапись             = Движения.РеализованныеТовары.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.Период      = Дата;
			
			// измерения
			НоваяЗапись.Контрагент                 = стрДвижений.Партия.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов      = стрДвижений.Партия.ДоговорВзаиморасчетов;
			НоваяЗапись.Номенклатура               = стрДвижений.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = стрДвижений.ХарактеристикаНоменклатуры;
			НоваяЗапись.ДокументПередачи           = стрДвижений.Партия;
			НоваяЗапись.ГТД                        = стрГТД.ГТД;
			
			// ресурсы
			НоваяЗапись.Количество     = КоличествоПриходуем;
			НоваяЗапись.СуммаУпр       = СуммаПриходуем;
			НоваяЗапись.СуммаРегл      = СуммаРеглПриходуем;
			НоваяЗапись.СуммаНДС       = СуммаНДСРеглПриходуем;
			
			Если ТекСтрока <> Неопределено Тогда
				Если КоличествоПриходуем = ТекСтрока.Количество Тогда
					СуммаПродажи = ТекСтрока.СуммаВсего;
				Иначе
					СуммаПродажи = ?(ТекСтрока.Количество = 0,0, ТекСтрока.СуммаВсего/ТекСтрока.Количество*КоличествоПриходуем);
				КонецЕсли;
				
				ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
				Если ВалютаУпр = ВалютаДокумента Тогда
					НоваяЗапись.СуммаПродажи = СуммаПродажи;
				Иначе
					НоваяЗапись.СуммаПродажи = обПересчет(СуммаПродажи,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсВалютыУпр);
				КонецЕсли;
				
				НоваяЗапись.СуммаПродажиРегл = обПересчет(СуммаПродажи,ВалютаДокумента,КурсДокумента,ВалютаРегл,Дата);
				
				// Себестоимость равна продаже
				Если (НЕ НоваяЗапись.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж) Тогда
					НоваяЗапись.СуммаУпр  = НоваяЗапись.СуммаПродажи;
					НоваяЗапись.СуммаРегл = НоваяЗапись.СуммаПродажиРегл;
					// пересчитаем НДС
					НоваяЗапись.СуммаНДС       = Окр(НоваяЗапись.СуммаРегл*ТекСтрока.СтавкаНДС.Ставка /(100 + ТекСтрока.СтавкаНДС.Ставка),2);
				КонецЕсли;
				
			Иначе
				ТекстСообщения = "Не удалось получить сумму продажи товара <" + стрДвижений.Номенклатура + ">.";
				Сообщить(ТекстСообщения, СтатусСообщения.Информация);
			КонецЕсли;
			
			// реквизиты
			НоваяЗапись.ХозОперация = ХозОперация;
			
			// уменьшим оприходованное
			НадоОприходовать                = НадоОприходовать - КоличествоПриходуем;
			НадоОприходоватьСумма           = НадоОприходоватьСумма - СуммаПриходуем;
			НадоОприходоватьСуммаРегл       = НадоОприходоватьСуммаРегл - СуммаРеглПриходуем;
			НадоОприходоватьСуммаНДСРегл    = НадоОприходоватьСуммаНДСРегл - СуммаНДСРеглПриходуем;
			
			Если КоличествоПриходуем = стрГТД.Количество Тогда
				ТаблГТД.Удалить(стрГТД);
			Иначе
				стрГТД.Количество = стрГТД.Количество - КоличествоПриходуем;
			КонецЕсли;
		КонецЦикла;
		
		Если НадоОприходовать > 0 Тогда
			// получим массивы строк
			МассивСХар   = ТаблДок.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",стрДвижений.Номенклатура,стрДвижений.ХарактеристикаНоменклатуры));
			МассивБезХар = ТаблДок.НайтиСтроки(Новый Структура("Номенклатура",стрДвижений.Номенклатура));
			
			Если МассивСХар.Количество() > 0 Тогда
				ТекСтрока = МассивСХар[0];
			ИначеЕсли МассивБезХар.Количество() > 0 Тогда
				ТекСтрока = МассивБезХар[0];
			Иначе
				ТекСтрока = Неопределено;
			КонецЕсли;
			
			НоваяЗапись             = Движения.РеализованныеТовары.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.Период      = Дата;
			
			// измерения
			НоваяЗапись.Контрагент                 = стрДвижений.Партия.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов      = стрДвижений.Партия.ДоговорВзаиморасчетов;
			НоваяЗапись.Номенклатура               = стрДвижений.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = стрДвижений.ХарактеристикаНоменклатуры;
			НоваяЗапись.ДокументПередачи           = стрДвижений.Партия;
			
			// ресурсы
			
			НоваяЗапись.Количество     = НадоОприходовать;
			НоваяЗапись.СуммаУпр       = НадоОприходоватьСумма;
			НоваяЗапись.СуммаРегл      = НадоОприходоватьСуммаРегл;
			НоваяЗапись.СуммаНДС       = НадоОприходоватьСуммаНДСРегл;
			
			Если ТекСтрока <> Неопределено Тогда
				Если НадоОприходовать = ТекСтрока.Количество Тогда
					СуммаПродажи = ТекСтрока.СуммаВсего;
				Иначе
					СуммаПродажи = ?(ТекСтрока.Количество = 0,0, ТекСтрока.СуммаВсего/ТекСтрока.Количество*НадоОприходовать);
				КонецЕсли;
				
				ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
				Если ВалютаУпр = ВалютаДокумента Тогда
					НоваяЗапись.СуммаПродажи = СуммаПродажи;
				Иначе
					НоваяЗапись.СуммаПродажи = обПересчет(СуммаПродажи,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсВалютыУпр);
				КонецЕсли;
				
				НоваяЗапись.СуммаПродажиРегл = обПересчет(СуммаПродажи,ВалютаДокумента,КурсДокумента,ВалютаРегл,Дата);
				
				// Себестоимость равна продаже
				Если (НЕ НоваяЗапись.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж) Тогда
					НоваяЗапись.СуммаУпр  = НоваяЗапись.СуммаПродажи;
					НоваяЗапись.СуммаРегл = НоваяЗапись.СуммаПродажиРегл;
					// пересчитаем НДС
					НоваяЗапись.СуммаНДС       = Окр(НоваяЗапись.СуммаРегл*ТекСтрока.СтавкаНДС.Ставка /(100 + ТекСтрока.СтавкаНДС.Ставка),2);
				КонецЕсли;
				
			Иначе
				ТекстСообщения = "Не удалось получить сумму продажи товара <" + стрДвижений.Номенклатура + ">.";
				Сообщить(ТекстСообщения, СтатусСообщения.Информация);
			КонецЕсли;
			
			// реквизиты
			НоваяЗапись.ХозОперация = ХозОперация;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПровестиВозвратТоваровПоСубподряду(Основания)
	
	// получим значения валют
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// получим субподрядные работы
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализацииРаботы.Работа,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	КорректировкаРеализацииРаботы.ДоговорВзаиморасчетов,
	|	КорректировкаРеализацииРаботы.СуммаВсегоРазница,
	|	КорректировкаРеализацииРаботы.СуммаНДСРазница
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
	|ГДЕ
	|	КорректировкаРеализацииРаботы.Ссылка = &Ссылка
	|	И КорректировкаРеализацииРаботы.СуммаВсегоРазница <> 0
	|	И КорректировкаРеализацииРаботы.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	СубподрядДок = Запрос.Выполнить().Выгрузить();
	
	// получим движения по субподряду
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Субподряд.ПодразделениеКомпании,
	|	Субподряд.Контрагент,
	|	Субподряд.ДоговорВзаиморасчетов,
	|	Субподряд.ЗаказНаряд,
	|	Субподряд.Работа,
	|	СУММА(Субподряд.Сумма) КАК Сумма,
	|	СУММА(Субподряд.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Субподряд.СуммаУпр) КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.Субподряд КАК Субподряд
	|ГДЕ
	|	Субподряд.Регистратор В(&Регистраторы)
	|	И Субподряд.Работа В(&Автоработы)
	|
	|СГРУППИРОВАТЬ ПО
	|	Субподряд.Работа,
	|	Субподряд.ЗаказНаряд,
	|	Субподряд.ДоговорВзаиморасчетов,
	|	Субподряд.ПодразделениеКомпании,
	|	Субподряд.Контрагент";
	Запрос.УстановитьПараметр("Регистраторы", Основания);
	Запрос.УстановитьПараметр("Автоработы" ,СубподрядДок.ВыгрузитьКолонку("Работа"));
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДок Из СубподрядДок Цикл
		
		ПодходящиеДвижения = ТаблицаДвижений.НайтиСтроки(
			Новый Структура(
				"Контрагент,ДоговорВзаиморасчетов,Работа",
				СтрокаДок.Контрагент,
				СтрокаДок.ДоговорВзаиморасчетов,  
				//БизТех 10.10.23г. ошибка <<
				//СтрокаДок.Авторабота
				СтрокаДок.Работа //>>
			)
		);
		
		Если ПодходящиеДвижения.Количество() = 0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТекСтрока = ПодходящиеДвижения[0];
		
		// заполним регистр
		НоваяЗапись = Движения.Субподряд.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
		НоваяЗапись.Период = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ХозОперация = ХозОперация;
		
		// суммовые показатели
		НоваяЗапись.Сумма    = обПересчет(СтрокаДок.СуммаВсегоРазница, ВалютаДокумента, Дата, ВалютаРегл, Дата);
		НоваяЗапись.СуммаНДС = обПересчет(СтрокаДок.СуммаНДСРазница, ВалютаДокумента, Дата, ВалютаРегл, Дата);
		НоваяЗапись.СуммаУпр = обПересчет(СтрокаДок.СуммаВсегоРазница, ВалютаДокумента, Дата, ВалютаУпр, Дата);
		
	КонецЦикла;

КонецФункции

// Формирует движения документа по партионным регистрам
// Режим - режим проведения (оперативный/неоперативный)
// ДокументСсылка - ссылка на документ который надо допровести по партиям
// Возвращает Истина - все нормально, ложь - чего-то не так.
Функция ПровестиПоПартиям(Режим, ДокументСсылка, СуммаРазногласий) Экспорт
	
	Отказ=Ложь;
	// Получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем.
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение=ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
		НаборЗаписейДоходыИРасходы.Доход = СуммаРазногласий;
		НаборЗаписейДоходыИРасходы.Приход();
		Возврат НЕ Отказ;
	КонецЕсли;
	
	ХозОперацияРеализации = Сделка.ХозОперация;
		
	// Проверим возможность проведения документа на основании непроведенного документа основания
	Если НЕ ДокументОснование.Проведен Тогда
		
		ТекстСообщения = "Проведение документа корректировки реализации на основании непроведенного документа основания запрещено.";
		Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
		Отказ = Истина; 
		
	КонецЕсли; 
	
	Основания = Документы.КорректировкаРеализации.ОснованияЦепочкиКорректировкиРеализации(Ссылка);	
	Отказ = Не ПровестиПоПартиямТоваровКомпанииИГТД(Основания, Режим) ИЛИ Отказ;

	ПровестиВозвратТоваровПоСубподряду(Основания);
		
	// добавим новые реализованные товары
	ПровестиПоРеализованнымТоварам();
	
	// Подготовим таблицу движений в разрезе подразделений только собственных списанных партий.
	ТаблицаСписанийПартий = Движения.ПартииТоваровКомпании.Выгрузить();
	ТаблицаСписанийПартий.Свернуть("СкладКомпании,СтатусПартии","СуммаУпр");
	СтруктураОтбора=Новый Структура("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	МассивНайденныхСтрок=ТаблицаСписанийПартий.НайтиСтроки(СтруктураОтбора);
	Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
		ТаблицаСписанийПартий.Удалить(МассивНайденныхСтрок[Сч]);
	КонецЦикла;
	
	СебестоимостьУпр = ТаблицаСписанийПартий.Итог("СуммаУпр");
	
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
	// В случае, если ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
	Если ВедетсяБалансПоПодразделению Тогда
		НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
	КонецЕсли;
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Себестоимость;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
	НаборЗаписейДоходыИРасходы.Расход=СебестоимостьУпр;	
	НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	
	// Подготовим таблицу движений в разрезе подразделений комиссионных товаров
	ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
	ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");
	
	ОписаниеТипов=Новый ОписаниеТипов;
	ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение",ОписаниеТипов);
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
	КонецЦикла;	
	ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
	
	// В случае если, ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
	Если ВедетсяБалансПоПодразделению Тогда
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=СтрокаСписания.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
			НаборЗаписейДоходыИРасходы.Расход=СтрокаСписания.СуммаУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЦикла;
	Иначе
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
		НаборЗаписейДоходыИРасходы.Расход=ТаблицаСписанийПартийРеализованных.Итог("СуммаУпр");
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// Доход от начисления дебиторской задолженности по отгруженному товару и оказанным услугам.
	Запрос=Новый Запрос();
	ТекстЗапроса="
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СУММА(КорректировкаРеализацииТовары.СуммаВсегоРазница),0) КАК СуммаТоваров,
	|	ЕСТЬNULL(СУММА(0),0) КАК СуммаУслуг
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &ТекДок
	|	И КорректировкаРеализацииТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(0),0),
	|	ЕСТЬNULL(СУММА(КорректировкаРеализацииТовары.СуммаВсегоРазница),0)
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &ТекДок
	|	И КорректировкаРеализацииТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(0),0),
	|	ЕСТЬNULL(СУММА(КорректировкаРеализацииРаботы.СуммаВсегоРазница),0)
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
	|ГДЕ
	|	КорректировкаРеализацииРаботы.Ссылка = &ТекДок";
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("ТекДок",ШапкаДокумента.Ссылка);
	Выборка=Запрос.Выполнить().Выбрать();
	СуммаТоваров=0; СуммаУслуг=0;
	Пока Выборка.Следующий() Цикл
		СуммаТоваров=СуммаТоваров+Выборка.СуммаТоваров;
		СуммаУслуг=СуммаУслуг+Выборка.СуммаУслуг;
	КонецЦикла;
	
	ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
	// В случае, если ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
	Если ВедетсяБалансПоПодразделению Тогда
		НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
	КонецЕсли;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте=ВУпрВалюте;
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
	НаборЗаписейДоходыИРасходы.Доход=СуммаТоваров;
	НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
	// В случае, если ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
	Если ВедетсяБалансПоПодразделению Тогда
		НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
	КонецЕсли;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте=ВУпрВалюте;
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоУслугам;
	НаборЗаписейДоходыИРасходы.Доход=СуммаУслуг;
	НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	
	// двигаем границу последовательности партий
	Если Режим <> РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда   
		//БизТех 09.10.2023г. Ошибка, когда склад пустой <<
		Если ШапкаДокумента.СкладКомпании<>Справочники.СкладыКомпании.ПустаяСсылка() Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Результат = Запрос.Выполнить();
			СкладКомпанииБыл = Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();   
		КонецЕсли;
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

Процедура ПровестиПоКодамМаркировки(РежимПроведения, Отказ)
	
	// Отменим записи состояний документа
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
	ТаблицаМаркировки = НаборЗаписейСостоянияКодовМаркировки.ТаблицаКодовМаркировки();
	
	ТаблицаМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	ТаблицаМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	// Разберем маркировку на состовляющие для поиска
	Для Каждого ТекущийКодМаркировки Из ТаблицаМаркировки Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировки);
		
		// Это не маркировка товара
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийКодМаркировки.GTIN = СтруктураМаркировки.GTIN;
		ТекущийКодМаркировки.СерийныйНомер = СтруктураМаркировки.СерийныйНомер;
		
	КонецЦикла;
	
	ТекущиеСтатусыМаркировки = РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(
		ТаблицаМаркировки,
		МоментВремени()
	);
	
	// Проверим коды на выбытие
	СостоянияВОбороте = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
	
	// Коды Маркировок, которые надо вывести из оборота
	ТаблицаВывода = ТекущиеСтатусыМаркировки.СкопироватьКолонки();
	
	// Таблица маркирвки, которые необходимо вернуть
	ТаблицаВвода = ТекущиеСтатусыМаркировки.СкопироватьКолонки();
	ТаблицаНеПодтвержденных = ТекущиеСтатусыМаркировки.СкопироватьКолонки();
	
	Для Каждого ТекущаяСтрока Из ТекущиеСтатусыМаркировки Цикл
		СтрокаМаркировки = КодыМаркировки.Найти(ТекущаяСтрока.КодМаркировки, "КодМаркировки");
		СтрокаТоваров = Товары.Найти(СтрокаМаркировки.ИдентификаторТовара, "ИдентификаторТовара");
		ЭтоВозврат = СтрокаМаркировки.Возврат;
		Если СостоянияВОбороте.Найти(ТекущаяСтрока.Состояние) <> Неопределено И НЕ ЭтоВозврат Тогда
			НоваяСтрока = ТаблицаВывода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		ИначеЕсли СостоянияВОбороте.Найти(ТекущаяСтрока.Состояние) = Неопределено И ЭтоВозврат Тогда
			НоваяСтрока = ТаблицаВвода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		ИначеЕсли СтрокаТоваров <> Неопределено И НЕ СтрокаТоваров.Подтверждение Тогда
			НоваяСтрока = ТаблицаНеПодтвержденных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	// Для перемаркируемых поставим состояние выбытия из оборота
	ОчищатьЗаписи = Истина;
	УдалятьДвижения = Истина;
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
	НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = РежимПроведения;
	
	// Выведем указанную маркировку из оборота
	Если ТаблицаВывода.Количество() > 0 Тогда
		СостояниеКодаМаркировки = ?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо,
			Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа,
			Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику);
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = СостояниеКодаМаркировки;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаВывода;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
		ОчищатьЗаписи = Ложь;
		УдалятьДвижения = Ложь;
	КонецЕсли;
	
	// Введем в оборот возврат
	Если ТаблицаВвода.Количество() > 0 Тогда
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборотПриВозврате;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаВвода;
		НаборЗаписейСостоянияКодовМаркировки.ОчищатьЗаписи = ОчищатьЗаписи;
		НаборЗаписейСостоянияКодовМаркировки.УдалятьДвижения = УдалятьДвижения;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Для неподтверженных товаров с кодами сделаем в обороте
	Если ТаблицаНеПодтвержденных.Количество() > 0 Тогда
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборот;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаНеПодтвержденных;
		НаборЗаписейСостоянияКодовМаркировки.ОчищатьЗаписи = ОчищатьЗаписи;
		НаборЗаписейСостоянияКодовМаркировки.УдалятьДвижения = УдалятьДвижения;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Определяет возможен ли ввод корректировки на основании данного документа основания.
//
// Параметры:
//	Основание - документ реализации товаров или заказ-наряд или корректировки реализации.
//
Функция КорректировкаНеДоступна(Основание)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Истина КАК ЕстьВозврат
	|ИЗ
	|	Документ.ВозвратОтПокупателя КАК ТаблицаОбъекта
	|ГДЕ
	|	ТаблицаОбъекта.ДокументОснование = &Основание
	|	И ТаблицаОбъекта.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Истина
	|ИЗ
	|	Документ.ВозвратОтПокупателя.Товары КАК ТаблицаОбъекта
	|ГДЕ
	|	ТаблицаОбъекта.ДокументПродажи = &Основание
	|	И ТаблицаОбъекта.Ссылка.Проведен
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Основание", Основание);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // КорректировкаНеДоступна()

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	ВремСуммаНоменклатурыДокумента=Товары.Итог("СуммаВсего");
	Если ТипЗнч(Сделка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ВремСуммаРаботДокумента=Работы.Итог("СуммаВсего");
	Иначе
		ВремСуммаРаботДокумента=0;
	КонецЕсли; 
	ВремСуммаДокумента=ВремСуммаНоменклатурыДокумента+ВремСуммаРаботДокумента;
	Если СуммаНоменклатурыДокумента<>ВремСуммаНоменклатурыДокумента Тогда
		СуммаНоменклатурыДокумента=ВремСуммаНоменклатурыДокумента;
	КонецЕсли; 
	Если СуммаРаботДокумента<>ВремСуммаРаботДокумента Тогда
		СуммаРаботДокумента=ВремСуммаРаботДокумента;
	КонецЕсли; 
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции

Функция ПолучитьТаблицуПродажиТоваров()
	
	ТипыПартий = Метаданные.ПланыВидовХарактеристик.ТипыПартий.Тип.Типы();
	
	ПустыеЗначенияТиповПартий = Новый Массив;
	ПустыеЗначенияТиповПартий.Добавить(Неопределено);
	
	Для Каждого ТипДокумента Из ТипыПартий Цикл
		
		ПустыеЗначенияТиповПартий.Добавить(Новый(ТипДокумента));
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументаТовары.Ссылка КАК Ссылка,
	|	ТаблицаДокументаТовары.ПоДокументуРеализации КАК ПоДокументуРеализации,
	|	ТаблицаДокументаТовары.Подтверждение КАК Подтверждение,
	|	ТаблицаДокументаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаДокументаТовары.Количество КАК Количество,
	|	ТаблицаДокументаТовары.КоличествоПоДокументуРеализации КАК КоличествоПоДокументуРеализации,
	|	ТаблицаДокументаТовары.КоличествоДоКорректировки КАК КоличествоДоКорректировки,
	|	ТаблицаДокументаТовары.КоличествоРазница КАК КоличествоРазница,
	|	ТаблицаДокументаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаДокументаТовары.ЕдиницаИзмеренияПоДокументуРеализации КАК ЕдиницаИзмеренияПоДокументуРеализации,
	|	ТаблицаДокументаТовары.Коэффициент КАК Коэффициент,
	|	ТаблицаДокументаТовары.КоэффициентПоДокументуРеализации КАК КоэффициентПоДокументуРеализации,
	|	ТаблицаДокументаТовары.Цена КАК Цена,
	|	ТаблицаДокументаТовары.Сумма КАК Сумма,
	|	ТаблицаДокументаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокументаТовары.СтавкаНДСПоДокументуРеализации КАК СтавкаНДСПоДокументуРеализации,
	|	ТаблицаДокументаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокументаТовары.СуммаНДСПоДокументуРеализации КАК СуммаНДСПоДокументуРеализации,
	|	ТаблицаДокументаТовары.СуммаНДСДоКорректировки КАК СуммаНДСДоКорректировки,
	|	ТаблицаДокументаТовары.СуммаНДСРазница КАК СуммаНДСРазница,
	|	ТаблицаДокументаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаДокументаТовары.СуммаВсего КАК СуммаВсего,
	|	ТаблицаДокументаТовары.СуммаВсегоПоДокументуРеализации КАК СуммаВсегоПоДокументуРеализации,
	|	ТаблицаДокументаТовары.СуммаВсегоДоКорректировки КАК СуммаВсегоДоКорректировки,
	|	ТаблицаДокументаТовары.СуммаВсегоРазница КАК СуммаВсегоРазница,
	|	ТаблицаДокументаТовары.Источник КАК Источник,
	|	ТаблицаДокументаТовары.ИдентификаторТовара КАК ИдентификаторТовара,
	|	ТаблицаДокументаТовары.Партия КАК Партия,
	|	ТаблицаДокументаТовары.ПартияПоДокументуРеализации КАК ПартияПоДокументуРеализации,
	|	ТаблицаДокументаТовары.ПартияДоКорректировки КАК ПартияДоКорректировки,
	|	ТаблицаДокументаТовары.ГТД КАК ГТД,
	|	ТаблицаДокументаТовары.ГТДПоДокументуРеализации КАК ГТДПоДокументуРеализации,
	|	ТаблицаДокументаТовары.ГТДДоКорректировки КАК ГТДДоКорректировки,
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка) КАК НормоЧас,
	|	0 КАК СуммаСкидки,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаТовары.Партия В (&ПустыеЗначенияТиповПартий)
	|				И ТаблицаДокументаТовары.ПартияПоДокументуРеализации В (&ПустыеЗначенияТиповПартий)
	|			ТОГДА ИСТИНА
	|		КОГДА ТаблицаДокументаТовары.Партия = ТаблицаДокументаТовары.ПартияПоДокументуРеализации
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПартииРавны
	|ПОМЕСТИТЬ ТаблицаДокументаТовары
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаДокументаТовары
	|ГДЕ
	|	ТаблицаДокументаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(КорректировкаРеализацииТовары.Количество * КорректировкаРеализацииТовары.Коэффициент) КАК Количество,
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка) КАК НормоЧас,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего) КАК СуммаВсего,
	|	0 КАК СуммаСкидки,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего) КАК СуммаВсегоДляЦены,
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница) КАК СуммаНДСРазница,
	|	СУММА(КорректировкаРеализацииТовары.Количество) КАК КоличествоДляЦены,
	|	КорректировкаРеализацииТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	КорректировкаРеализацииТовары.Партия КАК Партия,
	|	КорректировкаРеализацииТовары.ГТД КАК ГТД,
	|	0 КАК КоличествоНормочасов
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаНДСРазница <> 0)
	|	И КорректировкаРеализацииТовары.ПартииРавны
	|	И КорректировкаРеализацииТовары.ГТД = КорректировкаРеализацииТовары.ГТДПоДокументуРеализации
	|	И КорректировкаРеализацииТовары.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(-(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации * КорректировкаРеализацииТовары.КоэффициентПоДокументуРеализации)),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаНДСРазница <> 0)
	|	И КорректировкаРеализацииТовары.ПартииРавны
	|	И КорректировкаРеализацииТовары.ГТД = КорректировкаРеализацииТовары.ГТДПоДокументуРеализации
	|	И КорректировкаРеализацииТовары.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(-(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации * КорректировкаРеализацииТовары.КоэффициентПоДокументуРеализации)),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаНДСРазница <> 0)
	|	И КорректировкаРеализацииТовары.ПартииРавны
	|	И КорректировкаРеализацииТовары.ГТД = КорректировкаРеализацииТовары.ГТДПоДокументуРеализации
	|	И КорректировкаРеализацииТовары.Количество = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоРазница),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииТовары.Количество),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (НЕ КорректировкаРеализацииТовары.ПартииРавны
	|			ИЛИ КорректировкаРеализацииТовары.ГТД <> КорректировкаРеализацииТовары.ГТДПоДокументуРеализации)
	|	И НЕ КорректировкаРеализацииТовары.ПоДокументуРеализации = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(-КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации * КорректировкаРеализацииТовары.КоэффициентПоДокументуРеализации),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(-КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	СУММА(-КорректировкаРеализацииТовары.СуммаНДСПоДокументуРеализации),
	|	СУММА(0),
	|	СУММА(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииТовары.СтавкаНДСПоДокументуРеализации,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.ПартияПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.ГТДПоДокументуРеализации,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (НЕ КорректировкаРеализацииТовары.ПартииРавны
	|			ИЛИ КорректировкаРеализацииТовары.ГТД <> КорректировкаРеализацииТовары.ГТДПоДокументуРеализации)
	|	И КорректировкаРеализацииТовары.ПоДокументуРеализации = ИСТИНА
	|	И НЕ КорректировкаРеализацииТовары.Партия В (&ПустыеЗначенияТиповПартий)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ПартияПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.ГТДПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.СтавкаНДСПоДокументуРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(КорректировкаРеализацииТовары.Количество * КорректировкаРеализацииТовары.Коэффициент),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС),
	|	СУММА(0),
	|	СУММА(КорректировкаРеализацииТовары.Количество),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (НЕ КорректировкаРеализацииТовары.ПартииРавны
	|			ИЛИ КорректировкаРеализацииТовары.ГТД <> КорректировкаРеализацииТовары.ГТДПоДокументуРеализации)
	|	И КорректировкаРеализацииТовары.ПоДокументуРеализации = ИСТИНА
	|	И КорректировкаРеализацииТовары.Количество <> 0
	|	И НЕ КорректировкаРеализацииТовары.Партия В (&ПустыеЗначенияТиповПартий)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(-КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации * КорректировкаРеализацииТовары.КоэффициентПоДокументуРеализации),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСПоДокументуРеализации),
	|	СУММА(0),
	|	СУММА(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.ПартияПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.ГТДПоДокументуРеализации,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (НЕ КорректировкаРеализацииТовары.ПартииРавны
	|			ИЛИ КорректировкаРеализацииТовары.ГТД <> КорректировкаРеализацииТовары.ГТДПоДокументуРеализации)
	|	И КорректировкаРеализацииТовары.ПоДокументуРеализации = ИСТИНА
	|	И КорректировкаРеализацииТовары.Количество <> 0
	|	И КорректировкаРеализацииТовары.Партия В(&ПустыеЗначенияТиповПартий)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.ПартияПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.ГТДПоДокументуРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(КорректировкаРеализацииТовары.Количество * КорректировкаРеализацииТовары.Коэффициент),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииТовары.Количество),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (НЕ КорректировкаРеализацииТовары.ПартииРавны
	|			ИЛИ КорректировкаРеализацииТовары.ГТД <> КорректировкаРеализацииТовары.ГТДПоДокументуРеализации)
	|	И КорректировкаРеализацииТовары.ПоДокументуРеализации = ИСТИНА
	|	И КорректировкаРеализацииТовары.Количество <> 0
	|	И КорректировкаРеализацииТовары.Партия В(&ПустыеЗначенияТиповПартий)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Партия,
	|	КорректировкаРеализацииТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(-КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации * КорректировкаРеализацииТовары.КоэффициентПоДокументуРеализации),
	|	ЗНАЧЕНИЕ(Справочник.Нормочасы.ПустаяСсылка),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	0,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	КорректировкаРеализацииТовары.ПартияПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.ГТДПоДокументуРеализации,
	|	0
	|ИЗ
	|	ТаблицаДокументаТовары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (НЕ КорректировкаРеализацииТовары.ПартииРавны
	|			ИЛИ КорректировкаРеализацииТовары.ГТД <> КорректировкаРеализацииТовары.ГТДПоДокументуРеализации)
	|	И КорректировкаРеализацииТовары.ПоДокументуРеализации = ИСТИНА
	|	И КорректировкаРеализацииТовары.Количество = 0
	|	И КорректировкаРеализацииТовары.Партия В(&ПустыеЗначенияТиповПартий)
	|	И КорректировкаРеализацииТовары.КоличествоРазница <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.ПартияПоДокументуРеализации,
	|	КорректировкаРеализацииТовары.ГТДПоДокументуРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииРаботы.Работа,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(КорректировкаРеализацииРаботы.Количество),
	|	КорректировкаРеализацииРаботы.Нормочас,
	|	СУММА(КорректировкаРеализацииРаботы.СуммаВсего),
	|	0,
	|	СУММА(КорректировкаРеализацииРаботы.СуммаВсего),
	|	СУММА(КорректировкаРеализацииРаботы.СуммаНДС),
	|	СУММА(КорректировкаРеализацииРаботы.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииРаботы.Количество),
	|	КорректировкаРеализацииРаботы.СтавкаНДС,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка),
	|	КорректировкаРеализацииРаботы.Количество * КорректировкаРеализацииРаботы.Коэффициент
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
	|ГДЕ
	|	КорректировкаРеализацииРаботы.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииРаботы.КоличествоРазница <> 0
	|			ИЛИ КорректировкаРеализацииРаботы.СуммаВсегоРазница <> 0
	|			ИЛИ КорректировкаРеализацииРаботы.КоэффициентРазница <> 0
	|			ИЛИ КорректировкаРеализацииРаботы.СуммаНДСРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииРаботы.Работа,
	|	КорректировкаРеализацииРаботы.СтавкаНДС,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	КорректировкаРеализацииРаботы.Нормочас,
	|	КорректировкаРеализацииРаботы.Количество * КорректировкаРеализацииРаботы.Коэффициент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииРаботы.Работа,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(-КорректировкаРеализацииРаботы.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииРаботы.Нормочас,
	|	СУММА(КорректировкаРеализацииРаботы.СуммаВсегоПоДокументуРеализации),
	|	0,
	|	СУММА(КорректировкаРеализацииРаботы.СуммаВсего),
	|	СУММА(КорректировкаРеализацииРаботы.СуммаНДСПоДокументуРеализации),
	|	СУММА(КорректировкаРеализацииРаботы.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииРаботы.КоличествоПоДокументуРеализации),
	|	КорректировкаРеализацииРаботы.СтавкаНДС,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка),
	|	-(КорректировкаРеализацииРаботы.КоличествоПоДокументуРеализации * КорректировкаРеализацииРаботы.КоэффициентПоДокументуРеализации)
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
	|ГДЕ
	|	КорректировкаРеализацииРаботы.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииРаботы.КоличествоРазница <> 0
	|			ИЛИ КорректировкаРеализацииРаботы.СуммаВсегоРазница <> 0
	|			ИЛИ КорректировкаРеализацииРаботы.КоэффициентРазница <> 0
	|			ИЛИ КорректировкаРеализацииРаботы.СуммаНДСРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииРаботы.Работа,
	|	КорректировкаРеализацииРаботы.СтавкаНДС,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	КорректировкаРеализацииРаботы.Нормочас,
	|	-(КорректировкаРеализацииРаботы.КоличествоПоДокументуРеализации * КорректировкаРеализацииРаботы.КоэффициентПоДокументуРеализации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварыДокумента.Количество КАК Количество,
	|	ТоварыДокумента.НормоЧас КАК НормоЧас,
	|	ТоварыДокумента.СуммаВсего КАК Сумма,
	|	ТоварыДокумента.СуммаСкидки КАК СуммаСкидки,
	|	ТоварыДокумента.СуммаВсегоДляЦены КАК СуммаВсегоДляЦены,
	|	ТоварыДокумента.СуммаНДС КАК СуммаНДС,
	|	ТоварыДокумента.СуммаНДСРазница КАК СуммаНДСРазница,
	|	ТоварыДокумента.КоличествоДляЦены КАК КоличествоДляЦены,
	|	ТоварыДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТоварыДокумента.Контрагент КАК Контрагент,
	|	ТоварыДокумента.Партия КАК Партия,
	|	ТоварыДокумента.ГТД КАК ГТД,
	|	ВЫБОР
	|		КОГДА ТоварыДокумента.Количество < 0
	|				И НЕ ТоварыДокумента.ГТД = ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)
	|			ТОГДА 4
	|		КОГДА ТоварыДокумента.Количество < 0
	|				И НЕ ТоварыДокумента.Партия В (&ПустыеЗначенияТиповПартий)
	|			ТОГДА 3
	|		КОГДА НЕ ТоварыДокумента.ГТД = ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)
	|			ТОГДА 2
	|		КОГДА НЕ ТоварыДокумента.Партия В (&ПустыеЗначенияТиповПартий)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолеСортировки,
	|	ТоварыДокумента.КоличествоНормочасов КАК КоличествоНормочасов
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|ГДЕ
	|	ТоварыДокумента.Количество <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПолеСортировки УБЫВ";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияТиповПартий", ПустыеЗначенияТиповПартий);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	ТабДок = Запрос.Выполнить().Выгрузить();
	
	Возврат ТабДок;

КонецФункции

// Получает таблицу с остатками ГТД
//
// Параметры:
//  ТаблицаПартий - ТаблицаЗначений - Таблица партий для получения остатков.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат остатоков партий по ГТД
//
Функция ПолучитьОстаткиГТДПартий(ТаблицаПартий, СтарыеДвиженияГТД)
	
	ДатаМоментаВремени = Ссылка.Дата;
	ГраницаРасчетаОстатков = Новый МоментВремени(ДатаМоментаВремени, Ссылка);
	
	// Получим дату среза
	ДатаСреза = ГраницаРасчетаОстатков.Дата;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтарыеДвиженияГТД.СкладКомпании КАК СкладКомпании,
	               |	СтарыеДвиженияГТД.Номенклатура КАК Номенклатура,
	               |	СтарыеДвиженияГТД.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СтарыеДвиженияГТД.Партия КАК Партия,
	               |	СтарыеДвиженияГТД.ГТД КАК ГТД,
	               |	СтарыеДвиженияГТД.Количество КАК Количество,
	               |	АВТОНОМЕРЗАПИСИ() КАК ПорядокСтарыхДвиженийГТД
	               |ПОМЕСТИТЬ СтарыеДвиженияГТД
	               |ИЗ
	               |	&СтарыеДвиженияГТД КАК СтарыеДвиженияГТД
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ГТДПартийТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	               |	ГТДПартийТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	               |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ГТДПартийТоваровКомпанииОстатки.Партия КАК Партия,
	               |	ГТДПартийТоваровКомпанииОстатки.ГТД КАК ГТД,
	               |	СУММА(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток) КАК Количество,
	               |	0 КАК ПорядокСтарыхДвиженийГТД
	               |ПОМЕСТИТЬ ОстаткиГТДСОснованиями
	               |ИЗ
	               |	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	               |			&Момент,
	               |			СкладКомпании = &СкладКомпанииШапка
	               |				И Номенклатура В (&Номенклатура)
	               |				И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	               |				И Партия В (&Партия)) КАК ГТДПартийТоваровКомпанииОстатки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ГТДПартийТоваровКомпанииОстатки.СкладКомпании,
	               |	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
	               |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	               |	ГТДПартийТоваровКомпанииОстатки.Партия,
	               |	ГТДПартийТоваровКомпанииОстатки.ГТД
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СтарыеДвиженияГТД.СкладКомпании,
	               |	СтарыеДвиженияГТД.Номенклатура,
	               |	СтарыеДвиженияГТД.ХарактеристикаНоменклатуры,
	               |	СтарыеДвиженияГТД.Партия,
	               |	СтарыеДвиженияГТД.ГТД,
	               |	СтарыеДвиженияГТД.Количество,
	               |	СтарыеДвиженияГТД.ПорядокСтарыхДвиженийГТД
	               |ИЗ
	               |	СтарыеДвиженияГТД КАК СтарыеДвиженияГТД
	               |ГДЕ
	               |	СтарыеДвиженияГТД.СкладКомпании В(&СкладКомпании)
	               |	И СтарыеДвиженияГТД.Номенклатура В(&Номенклатура)
	               |	И СтарыеДвиженияГТД.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	               |	И СтарыеДвиженияГТД.Партия В(&Партия)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОстаткиГТДСОснованиями.СкладКомпании КАК СкладКомпании,
	               |	ОстаткиГТДСОснованиями.Номенклатура КАК Номенклатура,
	               |	ОстаткиГТДСОснованиями.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиГТДСОснованиями.Партия КАК Партия,
	               |	ОстаткиГТДСОснованиями.ГТД КАК ГТД,
	               |	СУММА(ОстаткиГТДСОснованиями.Количество) КАК Количество,
	               |	МАКСИМУМ(ОстаткиГТДСОснованиями.ПорядокСтарыхДвиженийГТД) КАК ПорядокСтарыхДвиженийГТД
	               |ПОМЕСТИТЬ ОстаткиГТДСгруппированные
	               |ИЗ
	               |	ОстаткиГТДСОснованиями КАК ОстаткиГТДСОснованиями
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстаткиГТДСОснованиями.СкладКомпании,
	               |	ОстаткиГТДСОснованиями.Номенклатура,
	               |	ОстаткиГТДСОснованиями.ХарактеристикаНоменклатуры,
	               |	ОстаткиГТДСОснованиями.Партия,
	               |	ОстаткиГТДСОснованиями.ГТД
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОстаткиГТДСгруппированные.СкладКомпании КАК СкладКомпании,
	               |	ОстаткиГТДСгруппированные.Номенклатура КАК Номенклатура,
	               |	ОстаткиГТДСгруппированные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиГТДСгруппированные.Партия КАК Партия,
	               |	ОстаткиГТДСгруппированные.ГТД КАК ГТД,
	               |	ОстаткиГТДСгруппированные.Количество КАК Количество,
	               |	ВЫБОР
	               |		КОГДА ОстаткиГТДСгруппированные.ПорядокСтарыхДвиженийГТД = 0
	               |			ТОГДА 999999
	               |		ИНАЧЕ ОстаткиГТДСгруппированные.ПорядокСтарыхДвиженийГТД
	               |	КОНЕЦ КАК ПорядокСтарыхДвиженийГТД
	               |ИЗ
	               |	ОстаткиГТДСгруппированные КАК ОстаткиГТДСгруппированные
	               |ГДЕ
	               |	ОстаткиГТДСгруппированные.Количество <> 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПорядокСтарыхДвиженийГТД";
	
	Запрос.УстановитьПараметр("СтарыеДвиженияГТД"			, СтарыеДвиженияГТД);
	Запрос.УстановитьПараметр("Момент"						, ГраницаРасчетаОстатков);
	Запрос.УстановитьПараметр("СкладКомпании"				, ТаблицаПартий.ВыгрузитьКолонку("СкладКомпании"));
	Запрос.УстановитьПараметр("СкладКомпанииШапка"			, СкладКомпании);
	Запрос.УстановитьПараметр("Номенклатура"				, ТаблицаПартий.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры"	, ТаблицаПартий.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.УстановитьПараметр("Партия"						, ТаблицаПартий.ВыгрузитьКолонку("Партия"));
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ГТДПартийТоваровКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ДатаСреза)); 
	Если СкладКомпании <> Неопределено Тогда
		ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании); 
	КонецЕсли; 
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаПартий);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("Партия", "Партия");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	ТаблицаГТД = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаГТД; 
	
КонецФункции // ПолучитьОстаткиГТДПартий() 

// Возвращает таблицу движений документов в цепочке корректировки. 
//
// Возвращаемое значение: 
//	ТаблицаЗначений - 	таблица, заполненая движениями по регистру Партий товаров компании всех документов 
//						в цепочке Корректировки, наяходящихся до текущего документа Корректировки.
//
Функция ДвиженияПартийДоКорректировки(ДокументыОснований, Основания)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварыВПроизводстве.Цех КАК СкладКомпании,
	|	ТоварыВПроизводстве.Номенклатура КАК Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.СтатусПартии КАК СтатусПартии,
	|	ТоварыВПроизводстве.Партия КАК Партия,
	|	-ТоварыВПроизводстве.Количество КАК Количество,
	|	ТоварыВПроизводстве.Сумма КАК Сумма,
	|	ТоварыВПроизводстве.СуммаНДС КАК СуммаНДС,
	|	ТоварыВПроизводстве.СуммаУпр КАК СуммаУпр
	|ПОМЕСТИТЬ ВТ_ТоварыВПроизводствеПриходРасход
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор ССЫЛКА Документ.ПеремещениеТоваровВПроизводство
	|	И ТоварыВПроизводстве.ЗаказНаряд В(&Основания)
	|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВПроизводстве.Цех,
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.СтатусПартии,
	|	ТоварыВПроизводстве.Партия,
	|	ТоварыВПроизводстве.Количество,
	|	ТоварыВПроизводстве.Сумма,
	|	ТоварыВПроизводстве.СуммаНДС,
	|	ТоварыВПроизводстве.СуммаУпр
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор ССЫЛКА Документ.ИзвлечениеТоваровИзПроизводства
	|	И ТоварыВПроизводстве.ЗаказНаряд В(&Основания)
	|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыВПроизводствеПриходРасход.СкладКомпании КАК СкладКомпании,
	|	ВТ_ТоварыВПроизводствеПриходРасход.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыВПроизводствеПриходРасход.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТоварыВПроизводствеПриходРасход.СтатусПартии КАК СтатусПартии,
	|	ВТ_ТоварыВПроизводствеПриходРасход.Партия КАК Партия,
	|	СУММА(ВТ_ТоварыВПроизводствеПриходРасход.Количество) КАК Количество,
	|	СУММА(ВТ_ТоварыВПроизводствеПриходРасход.Сумма) КАК Сумма,
	|	СУММА(ВТ_ТоварыВПроизводствеПриходРасход.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВТ_ТоварыВПроизводствеПриходРасход.СуммаУпр) КАК СуммаУпр
	|ПОМЕСТИТЬ ВТ_ТоварыВПроизводствеИтог
	|ИЗ
	|	ВТ_ТоварыВПроизводствеПриходРасход КАК ВТ_ТоварыВПроизводствеПриходРасход
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТоварыВПроизводствеПриходРасход.СкладКомпании,
	|	ВТ_ТоварыВПроизводствеПриходРасход.Номенклатура,
	|	ВТ_ТоварыВПроизводствеПриходРасход.ХарактеристикаНоменклатуры,
	|	ВТ_ТоварыВПроизводствеПриходРасход.СтатусПартии,
	|	ВТ_ТоварыВПроизводствеПриходРасход.Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ТоварыВПроизводствеИтог.СкладКомпании КАК СкладКомпании,
	|	ВТ_ТоварыВПроизводствеИтог.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыВПроизводствеИтог.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТоварыВПроизводствеИтог.СтатусПартии КАК СтатусПартии,
	|	ВТ_ТоварыВПроизводствеИтог.Партия КАК Партия,
	|	ВТ_ТоварыВПроизводствеИтог.Количество КАК Количество,
	|	ВТ_ТоварыВПроизводствеИтог.Сумма КАК Сумма,
	|	ВТ_ТоварыВПроизводствеИтог.СуммаНДС КАК СуммаНДС,
	|	ВТ_ТоварыВПроизводствеИтог.СуммаУпр КАК СуммаУпр
	|ПОМЕСТИТЬ ВТ_ТоварыВПроизводстве
	|ИЗ
	|	ВТ_ТоварыВПроизводствеИтог КАК ВТ_ТоварыВПроизводствеИтог
	|ГДЕ
	|	ВТ_ТоварыВПроизводствеИтог.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВПроизводстве.Цех,
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.СтатусПартии,
	|	ТоварыВПроизводстве.Партия,
	|	ТоварыВПроизводстве.Количество,
	|	ТоварыВПроизводстве.Сумма,
	|	ТоварыВПроизводстве.СуммаНДС,
	|	ТоварыВПроизводстве.СуммаУпр
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор В(&Основания)
	|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ТоварыВПроизводстве.СкладКомпании КАК СкладКомпании,
	|	ВТ_ТоварыВПроизводстве.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыВПроизводстве.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТоварыВПроизводстве.СтатусПартии КАК СтатусПартии,
	|	ВТ_ТоварыВПроизводстве.Партия КАК Партия,
	|	СУММА(ВТ_ТоварыВПроизводстве.Количество) КАК Количество,
	|	СУММА(ВТ_ТоварыВПроизводстве.Сумма) КАК Сумма,
	|	СУММА(ВТ_ТоварыВПроизводстве.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВТ_ТоварыВПроизводстве.СуммаУпр) КАК СуммаУпр,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|ПОМЕСТИТЬ ВТ_ДвиженияТоваровВПроизводстве
	|ИЗ
	|	ВТ_ТоварыВПроизводстве КАК ВТ_ТоварыВПроизводстве
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ВТ_ТоварыВПроизводстве.Номенклатура,
	|	ВТ_ТоварыВПроизводстве.Партия,
	|	ВТ_ТоварыВПроизводстве.СкладКомпании,
	|	ВТ_ТоварыВПроизводстве.СтатусПартии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТоваровКомпании.СкладКомпании КАК СкладКомпании,
	|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.СтатусПартии КАК СтатусПартии,
	|	ПартииТоваровКомпании.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ПартииТоваровКомпании.Количество
	|		ИНАЧЕ ПартииТоваровКомпании.Количество
	|	КОНЕЦ КАК Количество,
	|	ПартииТоваровКомпании.Сумма КАК Сумма,
	|	ПартииТоваровКомпании.СуммаНДС КАК СуммаНДС,
	|	ПартииТоваровКомпании.СуммаУпр КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ПартииТоваровКомпании.ВидДвижения
	|	КОНЕЦ КАК ВидДвижения
	|ПОМЕСТИТЬ ВТ_ДвиженияПартийТоваров
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор В(&ДокументыОснований)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ДвиженияТоваровВПроизводстве.СкладКомпании,
	|	ВТ_ДвиженияТоваровВПроизводстве.Номенклатура,
	|	ВТ_ДвиженияТоваровВПроизводстве.ХарактеристикаНоменклатуры,
	|	ВТ_ДвиженияТоваровВПроизводстве.СтатусПартии,
	|	ВТ_ДвиженияТоваровВПроизводстве.Партия,
	|	ВТ_ДвиженияТоваровВПроизводстве.Количество,
	|	ВТ_ДвиженияТоваровВПроизводстве.Сумма,
	|	ВТ_ДвиженияТоваровВПроизводстве.СуммаНДС,
	|	ВТ_ДвиженияТоваровВПроизводстве.СуммаУпр,
	|	ВТ_ДвиженияТоваровВПроизводстве.ВидДвижения
	|ИЗ
	|	ВТ_ДвиженияТоваровВПроизводстве КАК ВТ_ДвиженияТоваровВПроизводстве
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ДвиженияПартийТоваров.СкладКомпании КАК СкладКомпании,
	|	ВТ_ДвиженияПартийТоваров.Номенклатура КАК Номенклатура,
	|	ВТ_ДвиженияПартийТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ДвиженияПартийТоваров.СтатусПартии КАК СтатусПартии,
	|	ВТ_ДвиженияПартийТоваров.Партия КАК Партия,
	|	СУММА(ВТ_ДвиженияПартийТоваров.Количество) КАК Количество,
	|	СУММА(ВТ_ДвиженияПартийТоваров.Сумма) КАК Сумма,
	|	СУММА(ВТ_ДвиженияПартийТоваров.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВТ_ДвиженияПартийТоваров.СуммаУпр) КАК СуммаУпр,
	|	ВТ_ДвиженияПартийТоваров.ВидДвижения КАК ВидДвижения
	|ПОМЕСТИТЬ ВТ_ДвиженияТоваров
	|ИЗ
	|	ВТ_ДвиженияПартийТоваров КАК ВТ_ДвиженияПартийТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДвиженияПартийТоваров.ХарактеристикаНоменклатуры,
	|	ВТ_ДвиженияПартийТоваров.Номенклатура,
	|	ВТ_ДвиженияПартийТоваров.Партия,
	|	ВТ_ДвиженияПартийТоваров.СкладКомпании,
	|	ВТ_ДвиженияПартийТоваров.СтатусПартии,
	|	ВТ_ДвиженияПартийТоваров.ВидДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДвиженияТоваров.СкладКомпании КАК СкладКомпании,
	|	ВТ_ДвиженияТоваров.Номенклатура КАК Номенклатура,
	|	ВТ_ДвиженияТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ДвиженияТоваров.СтатусПартии КАК СтатусПартии,
	|	ВТ_ДвиженияТоваров.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ВТ_ДвиженияТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ВТ_ДвиженияТоваров.Количество
	|		ИНАЧЕ ВТ_ДвиженияТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ВТ_ДвиженияТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ВТ_ДвиженияТоваров.Сумма
	|		ИНАЧЕ ВТ_ДвиженияТоваров.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВТ_ДвиженияТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ВТ_ДвиженияТоваров.СуммаНДС
	|		ИНАЧЕ ВТ_ДвиженияТоваров.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ВТ_ДвиженияТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ВТ_ДвиженияТоваров.СуммаУпр
	|		ИНАЧЕ ВТ_ДвиженияТоваров.СуммаУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ВТ_ДвиженияТоваров.ВидДвижения КАК ВидДвижения
	|ИЗ
	|	ВТ_ДвиженияТоваров КАК ВТ_ДвиженияТоваров
	|ГДЕ
	|	ВТ_ДвиженияТоваров.Количество <> 0";
	
	Запрос.УстановитьПараметр("ДокументыОснований", ДокументыОснований);
	Запрос.УстановитьПараметр("Основания", Основания);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает таблицу движений документов в цепочке корректировки.
// 
// Возвращаемое значение: 
//	ТаблицаЗначений - 	таблица, заполненая движениями по регистру ГТД партий товаров всех документов 
//						в цепочке Корректировки, наяходящихся до текущего документа Корректировки. 
//
Функция ДвиженияГТДДоКорректировки(ДокументыОснований, Основания)

	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыВПроизводстве.Цех КАК СкладКомпании,
		|	ТоварыВПроизводстве.Номенклатура КАК Номенклатура,
		|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТоварыВПроизводстве.Партия КАК Партия,
		|	ТоварыВПроизводстве.ГТД КАК ГТД,
		|	-ТоварыВПроизводстве.Количество КАК Количество,
		|	ТоварыВПроизводстве.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ТоварыВПроизводствеПриходРасход
		|ИЗ
		|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
		|ГДЕ
		|	ТоварыВПроизводстве.Регистратор ССЫЛКА Документ.ПеремещениеТоваровВПроизводство
		|	И ТоварыВПроизводстве.ЗаказНаряд В(&Основания)
		|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыВПроизводстве.Цех,
		|	ТоварыВПроизводстве.Номенклатура,
		|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
		|	ТоварыВПроизводстве.Партия,
		|	ТоварыВПроизводстве.ГТД,
		|	ТоварыВПроизводстве.Количество,
		|	ТоварыВПроизводстве.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
		|ГДЕ
		|	ТоварыВПроизводстве.Регистратор ССЫЛКА Документ.ИзвлечениеТоваровИзПроизводства
		|	И ТоварыВПроизводстве.ЗаказНаряд В(&Основания)
		|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ТоварыВПроизводствеПриходРасход.СкладКомпании КАК СкладКомпании,
		|	ВТ_ТоварыВПроизводствеПриходРасход.Номенклатура КАК Номенклатура,
		|	ВТ_ТоварыВПроизводствеПриходРасход.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_ТоварыВПроизводствеПриходРасход.Партия КАК Партия,
		|	ВТ_ТоварыВПроизводствеПриходРасход.ГТД КАК ГТД,
		|	СУММА(ВТ_ТоварыВПроизводствеПриходРасход.Количество) КАК Количество,
		|	ВТ_ТоварыВПроизводствеПриходРасход.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ТоварыВПроизводствеИтог
		|ИЗ
		|	ВТ_ТоварыВПроизводствеПриходРасход КАК ВТ_ТоварыВПроизводствеПриходРасход
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТоварыВПроизводствеПриходРасход.СкладКомпании,
		|	ВТ_ТоварыВПроизводствеПриходРасход.Номенклатура,
		|	ВТ_ТоварыВПроизводствеПриходРасход.ХарактеристикаНоменклатуры,
		|	ВТ_ТоварыВПроизводствеПриходРасход.Партия,
		|	ВТ_ТоварыВПроизводствеПриходРасход.ГТД,
		|	ВТ_ТоварыВПроизводствеПриходРасход.НомерСтроки
		|;
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ТоварыВПроизводствеИтог.СкладКомпании КАК СкладКомпании,
		|	ВТ_ТоварыВПроизводствеИтог.Номенклатура КАК Номенклатура,
		|	ВТ_ТоварыВПроизводствеИтог.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_ТоварыВПроизводствеИтог.Партия КАК Партия,
		|	ВТ_ТоварыВПроизводствеИтог.ГТД КАК ГТД,
		|	ВТ_ТоварыВПроизводствеИтог.Количество КАК Количество,
		|	ВТ_ТоварыВПроизводствеИтог.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ТоварыВПроизводстве
		|ИЗ
		|	ВТ_ТоварыВПроизводствеИтог КАК ВТ_ТоварыВПроизводствеИтог
		|ГДЕ
		|	ВТ_ТоварыВПроизводствеИтог.Количество <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыВПроизводстве.Цех,
		|	ТоварыВПроизводстве.Номенклатура,
		|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
		|	ТоварыВПроизводстве.Партия,
		|	ТоварыВПроизводстве.ГТД,
		|	ТоварыВПроизводстве.Количество,
		|	ТоварыВПроизводстве.НомерСтроки
		|ИЗ
		|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
		|ГДЕ
		|	ТоварыВПроизводстве.Регистратор В(&Основания)
		|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ТоварыВПроизводстве.СкладКомпании КАК СкладКомпании,
		|	ВТ_ТоварыВПроизводстве.Номенклатура КАК Номенклатура,
		|	ВТ_ТоварыВПроизводстве.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_ТоварыВПроизводстве.Партия КАК Партия,
		|	ВТ_ТоварыВПроизводстве.ГТД КАК ГТД,
		|	СУММА(ВТ_ТоварыВПроизводстве.Количество) КАК Количество,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВТ_ТоварыВПроизводстве.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ДвиженияТоваровВПроизводстве
		|ИЗ
		|	ВТ_ТоварыВПроизводстве КАК ВТ_ТоварыВПроизводстве
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
		|	ВТ_ТоварыВПроизводстве.Номенклатура,
		|	ВТ_ТоварыВПроизводстве.Партия,
		|	ВТ_ТоварыВПроизводстве.СкладКомпании,
		|	ВТ_ТоварыВПроизводстве.ГТД,
		|	ВТ_ТоварыВПроизводстве.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГТДПартийТоваровКомпании.СкладКомпании КАК СкладКомпании,
		|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ГТДПартийТоваровКомпании.Партия КАК Партия,
		|	ГТДПартийТоваровКомпании.ГТД КАК ГТД,
		|	ВЫБОР
		|		КОГДА ГТДПартийТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ГТДПартийТоваровКомпании.Количество
		|		ИНАЧЕ - ГТДПартийТоваровКомпании.Количество
		|	КОНЕЦ КАК Количество,
		|	ВЫБОР
		|		КОГДА ГТДПартийТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ГТДПартийТоваровКомпании.ВидДвижения
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения,
		|	ГТДПартийТоваровКомпании.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ДвиженияПоГТДДоСвертки
		|ИЗ
		|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
		|ГДЕ
		|	ГТДПартийТоваровКомпании.Регистратор В(&ДокументыОснований)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ДвиженияТоваровВПроизводстве.СкладКомпании,
		|	ВТ_ДвиженияТоваровВПроизводстве.Номенклатура,
		|	ВТ_ДвиженияТоваровВПроизводстве.ХарактеристикаНоменклатуры,
		|	ВТ_ДвиженияТоваровВПроизводстве.Партия,
		|	ВТ_ДвиженияТоваровВПроизводстве.ГТД,
		|	ВТ_ДвиженияТоваровВПроизводстве.Количество,
		|	ВТ_ДвиженияТоваровВПроизводстве.ВидДвижения,
		|	ВТ_ДвиженияТоваровВПроизводстве.НомерСтроки
		|ИЗ
		|	ВТ_ДвиженияТоваровВПроизводстве КАК ВТ_ДвиженияТоваровВПроизводстве
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ДвиженияПоГТДДоСвертки.СкладКомпании КАК СкладКомпании,
		|	ВТ_ДвиженияПоГТДДоСвертки.Номенклатура КАК Номенклатура,
		|	ВТ_ДвиженияПоГТДДоСвертки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_ДвиженияПоГТДДоСвертки.Партия КАК Партия,
		|	ВТ_ДвиженияПоГТДДоСвертки.ГТД КАК ГТД,
		|	СУММА(ВТ_ДвиженияПоГТДДоСвертки.Количество) КАК Количество,
		|	ВТ_ДвиженияПоГТДДоСвертки.ВидДвижения КАК ВидДвижения,
		|	МИНИМУМ(ВТ_ДвиженияПоГТДДоСвертки.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ДвиженияПоГТД
		|ИЗ
		|	ВТ_ДвиженияПоГТДДоСвертки КАК ВТ_ДвиженияПоГТДДоСвертки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДвиженияПоГТДДоСвертки.ХарактеристикаНоменклатуры,
		|	ВТ_ДвиженияПоГТДДоСвертки.Номенклатура,
		|	ВТ_ДвиженияПоГТДДоСвертки.Партия,
		|	ВТ_ДвиженияПоГТДДоСвертки.СкладКомпании,
		|	ВТ_ДвиженияПоГТДДоСвертки.ГТД,
		|	ВТ_ДвиженияПоГТДДоСвертки.ВидДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДвиженияПоГТД.СкладКомпании КАК СкладКомпании,
		|	ВТ_ДвиженияПоГТД.Номенклатура КАК Номенклатура,
		|	ВТ_ДвиженияПоГТД.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_ДвиженияПоГТД.Партия КАК Партия,
		|	ВТ_ДвиженияПоГТД.ГТД КАК ГТД,
		|	ВЫБОР
		|		КОГДА ВТ_ДвиженияПоГТД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА -ВТ_ДвиженияПоГТД.Количество
		|		ИНАЧЕ ВТ_ДвиженияПоГТД.Количество
		|	КОНЕЦ КАК Количество,
		|	ВТ_ДвиженияПоГТД.ВидДвижения КАК ВидДвижения
		|ИЗ
		|	ВТ_ДвиженияПоГТД КАК ВТ_ДвиженияПоГТД
		|ГДЕ
		|	ВТ_ДвиженияПоГТД.Количество <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ДвиженияПоГТД.НомерСтроки");
	Запрос.УстановитьПараметр("ДокументыОснований", ДокументыОснований);
	Запрос.УстановитьПараметр("Основания", Основания);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Формирует движения по регистру Партий товаров компании.
// Таблицу товаров получаем из текущего документа, иначе, при передаче параметра 
// ТаблицаТоваровДляДвижений берем из него.
//
// Параметры: 
//	ТаблицаТоваровДляДвижений - ТаблицаЗначений - таблица, содержащая разницу между движениями по регистру 
//								Партии товаров компании до корректировки и после корректировки.
//	СтарыеДвиженияПартий - 	ТаблицаЗначений - таблица, движений партий документов-оснований, где значения движений с
//							видом движения Приход взяты с обратным знаком
//
// Возвращаемое значение:
//	Стуртура:
//		* ВсеОК - Булево - Истина - все ОК, Ложь - чего-то не так.
//   	* ТаблицаНовыхДвижений - 	ТаблицаЗначений - таблица с новыми движениями по регистру Партии товаров компании 
//									после корректировки.
//
Функция ДвиженияПартийПослеКорректировки(ТаблицаТоваровДляДвижений = Неопределено, СтарыеДвиженияПартий, ТекстСообщения)
	
	ВсеОК = Истина;
	СтруктураВозврата = Новый Структура("ВсеОК", ВсеОК);
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	
	// проверяем, присутствуют ли партии в табличной части
	ПартииУказаны = Ложь;
	Для Каждого СтрокаТовар Из Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
			ПартииУказаны = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла; 
	
	ДокументПродажи = Документы.КорректировкаРеализации.ПолучитьДокументПродажиКорректировкиРеализации(Ссылка);
	
	Если ДокументПродажи = Неопределено Тогда
		
		ДокументПродажи = Ссылка;
		
	КонецЕсли;  
	
	// Получим права пользователя
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки", Права)
		<> Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	
	ПартияТоваровОтрицательныхОстатков = Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	
	// Получим таблицу товаров
	Если ТаблицаТоваровДляДвижений = Неопределено Тогда
		ТаблицаТовары = ПолучитьТаблицуТоваров();
		ТаблицаТовары = ПерезаполнитьСкладыТаблицыТоваров(ТаблицаТовары, СтарыеДвиженияПартий);
	Иначе
		ТаблицаТовары = ТаблицаТоваровДляДвижений;
	КонецЕсли;
	
	// Списываем партии
	ДеревоПартий = ПолучитьДеревоПартий(ТаблицаТовары, СтарыеДвиженияПартий);
	
	// Упорядочим по нормальному
	ТаблицаТовары.Сортировать("Номенклатура Возр, ХарактеристикаНоменклатуры Убыв" + ?(ПартииУказаны, ",Партия Убыв", ""));
	
	// Получим таблицу номенклатуры с ручным списанием характеристик
	ТаблицаРучныхХарактеристик = дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(Ссылка);
	
	ВыгружемыеКолонки = "ДокументПродажи, Количество, Номенклатура, Партия, Проект, СкладКомпании, СтавкаНДС, СтатусПартии, "
		+ "Сумма, СуммаНДС, СуммаУпр, ХарактеристикаНоменклатуры, ХозОперация";
	ТаблицаНовыхДвижений = РегистрыНакопления.ПартииТоваровКомпании.СоздатьНаборЗаписей().Выгрузить(, ВыгружемыеКолонки);  
	
	Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
		
		НадоСписать = Окр(СтрокаТовар.Количество, 3);
		// Получим строки таблицы партий с нашим товаром
		СтрокаПартийНоменклатуры = ДеревоПартий.Строки.Найти(СтрокаТовар.Номенклатура, "Номенклатура");
		ПоследнийСтатус = Перечисления.СтатусыПартий.ТоварКупленный;
		
		// Инициализируем переменные для расчета усредненной цены списанных партий
		ОбщееКоличество 	= 0;
		ОбщаяСумма 			= 0;
		ОбщаяСуммаНДС 		= 0;
		ОбщаяСуммаУпр 		= 0;
		КоличествоОстаток 	= 0;
		КоличествоБезПартии	= 0;
		ЕстьПартииТоваров 	= Ложь;
		ПартияЗаполнена 	= ЗначениеЗаполнено(СтрокаТовар.Партия);
		КоличествоСписывается = СтрокаТовар.Количество;
		СтрокаВозврата 		= Неопределено;
		
		Если СтрокаПартийНоменклатуры <> Неопределено Тогда
			СтруктураОтбора = Новый Структура("Номенклатура");
			СтруктураОтбора.Номенклатура = СтрокаТовар.Номенклатура; 
			Если ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры)
				ИЛИ (ТаблицаРучныхХарактеристик <> Неопределено
				И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура, "Номенклатура") <> Неопределено) Тогда 
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТовар.СкладКомпании) Тогда
				СтруктураОтбора.Вставить("СкладКомпании", СтрокаТовар.СкладКомпании);
			КонецЕсли;
			
			Если ПартияЗаполнена Тогда
				СтруктураОтбора.Вставить("Партия", СтрокаТовар.Партия);
			Иначе
				КоличествоОстаток = СтрокаПартийНоменклатуры.Количество;
				КоличествоБезПартии = СтрокаПартийНоменклатуры.Количество;
			КонецЕсли;
			
			МассивНайденныхСтрок = СтрокаПартийНоменклатуры.Строки.НайтиСтроки(СтруктураОтбора);
			// Теперь идем по партиям товаров и списываем в соответствии с выбранной стратегией.
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				ТекСтрока = МассивНайденныхСтрок[Сч];
				КоличествоОстаток = ТекСтрока.Количество;
				// Проверки на нулевую партию или партию отрицательных остатков
				Если ТекСтрока.Количество = NULL ИЛИ ТекСтрока.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				// Если указаны партии, то пропустим все партии не наши
				Если ПартииУказаны И ПартияЗаполнена И ТекСтрока.Партия <> СтрокаТовар.Партия Тогда
					Продолжить;
				КонецЕсли;
				
				ЕстьПартииТоваров = Истина; // если есть партия с которой производим списание, значит и цены будем брать из списаний
				
				Если ТекСтрока.Количество < 0 Тогда
					// Запомним удельные значения списания для отрицательной партии товаров
					ОбщееКоличество		= ОбщееКоличество     + ТекСтрока.Количество;
					ОбщаяСумма			= ОбщаяСумма          + Окр(ТекСтрока.Сумма, 2);
					ОбщаяСуммаНДС		= ОбщаяСуммаНДС       + Окр(ТекСтрока.СуммаНДС, 2);
					ОбщаяСуммаУпр		= ОбщаяСуммаУпр       + Окр(ТекСтрока.СуммаУпр, 2);
					Продолжить;
				КонецЕсли;
				
				НоваяЗапись = ТаблицаНовыхДвижений.Добавить(); 
				НоваяЗапись.Партия                     = ТекСтрока.Партия;
				НоваяЗапись.СтатусПартии               = ТекСтрока.СтатусПартии;
				ПоследнийСтатус						   = ТекСтрока.СтатусПартии;
				НоваяЗапись.СкладКомпании              = ТекСтрока.СкладКомпании;
				НоваяЗапись.Номенклатура               = ТекСтрока.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
				НоваяЗапись.ХозОперация				   = ХозОперация;
				
				Попытка
					НоваяЗапись.Проект = ШапкаДокумента.Проект;
				Исключение
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Проведение по партиям. Нет реквизита ""Проект""'"),
						УровеньЖурналаРегистрации.Предупреждение,
						,
						ШапкаДокумента.Ссылка);
				КонецПопытки;
				
				Попытка
					НоваяЗапись.ДокументПродажи = ДокументПродажи;
				Исключение
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Проведение по партиям. Нет реквизита ""Документ продажи""'"),
						УровеньЖурналаРегистрации.Предупреждение,
						,
						ШапкаДокумента.Ссылка);
				КонецПопытки;
				
				Если ТекСтрока.Количество > НадоСписать И ТекСтрока.Количество > 0 Тогда
					КоличествоСписания		= НадоСписать;
					СуммаСписания			= Окр(ТекСтрока.Сумма / ТекСтрока.Количество * НадоСписать, 2);
					СуммаНДССписания		= Окр(ТекСтрока.СуммаНДС / ТекСтрока.Количество * НадоСписать, 2);
					СуммаУпрСписания		= Окр(ТекСтрока.СуммаУпр / ТекСтрока.Количество * НадоСписать, 2);
				Иначе
					КоличествоСписания		= ТекСтрока.Количество;
					СуммаСписания			= Окр(ТекСтрока.Сумма, 2);
					СуммаНДССписания		= Окр(ТекСтрока.СуммаНДС, 2);
					СуммаУпрСписания		= Окр(ТекСтрока.СуммаУпр, 2);
				КонецЕсли;
				
				НоваяЗапись.Количество		= КоличествоСписания;
				НоваяЗапись.Сумма			= СуммаСписания;
				НоваяЗапись.СуммаНДС		= СуммаНДССписания;
				НоваяЗапись.СуммаУпр		= СуммаУпрСписания;
				
				// Запомним удельные значения списания для отрицательной партии товаров
				ОбщееКоличество		= ОбщееКоличество + КоличествоСписания;
				ОбщаяСумма			= ОбщаяСумма + СуммаСписания;
				ОбщаяСуммаНДС		= ОбщаяСуммаНДС + СуммаНДССписания;
				ОбщаяСуммаУпр		= ОбщаяСуммаУпр + СуммаУпрСписания;
							
				Если КоличествоСписания >= ТекСтрока.Количество Тогда
					СтрокаПартийНоменклатуры.Строки.Удалить(ТекСтрока);
				Иначе
					ТекСтрока.Количество		= ТекСтрока.Количество		- КоличествоСписания;
					ТекСтрока.Сумма				= ТекСтрока.Сумма			- СуммаСписания;
					ТекСтрока.СуммаНДС			= ТекСтрока.СуммаНДС		- СуммаНДССписания;
					ТекСтрока.СуммаУпр			= ТекСтрока.СуммаУпр		- СуммаУпрСписания;
				КонецЕсли;
				
				// Уменьшаем количество которое надо списать (или увеличиваем если это коррекция отрицательной партии).
				НадоСписать = НадоСписать - КоличествоСписания;
				Если НадоСписать <= 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			// Уменьшим ресурсы для остатка по номенклатуре
			Если ОбщееКоличество >= СтрокаПартийНоменклатуры.Количество Тогда
				ДеревоПартий.Строки.Удалить(СтрокаПартийНоменклатуры);
			Иначе
				СтрокаПартийНоменклатуры.Количество = СтрокаПартийНоменклатуры.Количество - ОбщееКоличество;
			КонецЕсли;
		КонецЕсли;
			
		// Если после списания по партиям осталось еще что-то, то либо образуем партию отрицательных остатков,
		// либо предупредим о не распределении по партиям.
		// Проверим чего осталось.
		Если НадоСписать <> 0 И ОтрицательныеОстаткиРазрешены Тогда
			НоваяЗапись = ТаблицаНовыхДвижений.Добавить(); 
			// Если партия указана непосредственно, то на нее. Иначе служебная
			Если ПартииУказаны И ПартияЗаполнена Тогда
				НоваяЗапись.Партия = СтрокаТовар.Партия;
				НоваяЗапись.СтатусПартии = ПоследнийСтатус;
			Иначе
				НоваяЗапись.Партия = ПартияТоваровОтрицательныхОстатков;
				НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
			КонецЕсли;
				
			НоваяЗапись.СкладКомпании = СтрокаТовар.СкладКомпании;
			НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
			НоваяЗапись.ХозОперация = ХозОперация;
			
			Попытка
				НоваяЗапись.Проект = ШапкаДокумента.Проект;
			Исключение
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Проведение по партиям. Нет реквизита ""Проект""'"),
					УровеньЖурналаРегистрации.Предупреждение,
					,
					ШапкаДокумента.Ссылка);
			КонецПопытки;
			
			Попытка
				НоваяЗапись.ДокументПродажи = ДокументПродажи;
			Исключение
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Проведение по партиям. Нет реквизита ""Документ продажи""'"),
					УровеньЖурналаРегистрации.Предупреждение,
					,
					ШапкаДокумента.Ссылка);
			КонецПопытки;
			
			НоваяЗапись.Количество = НадоСписать;
			
			// Теперь попробуем определиться с суммой списания
			Если ЕстьПартииТоваров Тогда
				Если ОбщееКоличество = 0 Тогда
					ЦенаПоследнейПартии 			= 0;
					УдельныйНДСПоследнейПартии 		= 0;
					ЦенаУпрПоследнейПартии 			= 0;
				Иначе
					ЦенаПоследнейПартии 			= ОбщаяСумма / ОбщееКоличество;
					УдельныйНДСПоследнейПартии 		= ОбщаяСуммаНДС / ОбщееКоличество;
					ЦенаУпрПоследнейПартии 			= ОбщаяСуммаУпр / ОбщееКоличество;
				КонецЕсли; 
			Иначе // В этом случае пробуем достать оценку из регистра сведений Цены по типу цен Нормативная цена
				ТипЦенРасчета = Справочники.ТипыЦен.НормативнаяЦена;
				// Получим цену в валюте управленческого учета компании
				ЦенаПоследнейПартии = обПолучитьЦену(ТипЦенРасчета, СтрокаТовар.Номенклатура,
					ШапкаДокумента.МоментВремени, , Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ,
					СтрокаТовар.ХарактеристикаНоменклатуры, , СтрокаТовар.СкладКомпании.Подразделение);
				ЦенаУпрПоследнейПартии = обПолучитьЦену(ТипЦенРасчета, СтрокаТовар.Номенклатура,
					ШапкаДокумента.МоментВремени, , Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ,
					СтрокаТовар.ХарактеристикаНоменклатуры, , СтрокаТовар.СкладКомпании.Подразделение);
				// Розничную оценку берем по типу цен основной тип цен отгрузки
				ТипЦенРасчета = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
				// Для расчета НДС сначала определимся со ставкой
				СтавкаНДС = СтрокаТовар.Номенклатура.СтавкаНДС;
				УдельныйНДСПоследнейПартии = (ЦенаПоследнейПартии * СтавкаНДС.Ставка) / 100;
			КонецЕсли;
			
			НоваяЗапись.Сумма			= Окр(ЦенаПоследнейПартии * НадоСписать, 2);
			НоваяЗапись.СуммаНДС 		= Окр(УдельныйНДСПоследнейПартии * НадоСписать, 2);
			НоваяЗапись.СуммаУпр 		= Окр(ЦенаУпрПоследнейПартии * НадоСписать, 2);
			
		ИначеЕсли НадоСписать <> 0 И КоличествоСписывается <> 0 Тогда
			ВсеОК = Ложь;
				
			Если ПартияЗаполнена Тогда  
				// Не хватило товара по партии. Ошибка
				ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+СтрокаТовар.Номенклатура+"> не распределился по партии " + СокрЛП(СтрокаТовар.Партия);
				Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
			Иначе
				// Не хватает товара на остатках
				ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+СтрокаТовар.Номенклатура+">. Остаток "+Формат(КоличествоБезПартии, "ЧДЦ=3; ЧН=0,00")+" "+
					СокрЛП(СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения)+". Списывается "+Формат(КоличествоСписывается, "ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения)+". Превышение " + Формат(КоличествоСписывается - КоличествоБезПартии, "ЧДЦ=3; ЧН=0,00");
				Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураВозврата.Вставить("ВсеОК", ВсеОК);
	СтруктураВозврата.Вставить("ТаблицаНовыхДвиженийПартий", ТаблицаНовыхДвижений);
	
	Возврат СтруктураВозврата;

КонецФункции // ДвиженияПартийПослеКорректировки()

// Формирует движения по регистру Партий товаров компании.
//
// Параметры: 
//		БудущиеДвижения - ТаблицаЗначений - таблица, содержащая разницу между движениями по регистру 
//											ГТД партий товаров до корректировки и после корректировки.
//		БратьТоварыИзДвижений - Булево - указывает на источник таблицы товаров. Истина - получаем товары из
//										переданного параметра БудущиеДвижения. Ложь - из текущего документа.
//		СтарыеДвиженияГТД - ТаблицаЗначений - таблица, движений ГТД документов-оснований, где значения движений с
//							видом движения Приход взяты с обратным знаком
//
// Возвращаемое значение:
//		Стуртура:
//			* ВсеОК - Булево - Истина - все ОК, Ложь - чего-то не так.
//   		* ТаблицаНовыхДвижений - Таблица значений - Таблица с новыми движениями по регистру Партии товаров компании 
//														после корректировки.
//
Функция ДвиженияГТДПослеКорректировки(БудущиеДвижения, БратьТоварыИзДвижений, СтарыеДвиженияГТД, ТекстСообщения)

	ВсеОК = Истина;
	
	СтруктураВозврата = Новый Структура("ВсеОК", ВсеОК);
	
	// Получим права пользователя
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки", Права)
		<> Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	
	// Списываем ГТД партий 
	ТаблицаПартий = БудущиеДвижения.Скопировать();
	ОстаткиГТДПартий = ПолучитьОстаткиГТДПартий(ТаблицаПартий, СтарыеДвиженияГТД);
	ТаблицаПартий.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, Партия", "Количество");
	
	// Получим таблицу товаров
	Если БратьТоварыИзДвижений Тогда
		
		ТаблицаТовары = БудущиеДвижения;
		
	Иначе
		
		ТаблицаТовары = ПолучитьТаблицуТоваров();
		
	КонецЕсли;
	
	ТаблицаТовары.Сортировать("ГТД Убыв, Партия Убыв");
	
	ВыгружемыеКолонки = "ГТД, Количество, Номенклатура, Партия, СкладКомпании, ХарактеристикаНоменклатуры, ХозОперация";
	ТаблицаНовыхДвиженийГТД = РегистрыНакопления.ГТДПартийТоваровКомпании.СоздатьНаборЗаписей().Выгрузить(, ВыгружемыеКолонки);
	
	Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
		
		ПартияЗаполнена = ЗначениеЗаполнено(СтрокаТовар.Партия);
		ГТДЗаполнена = ЗначениеЗаполнено(СтрокаТовар.ГТД);
		
		Если ПартияЗаполнена Тогда
			
			// Получим строки таблицы партий с нашим товаром
			СтруктураОтбора = Новый Структура("Номенклатура", СтрокаТовар.Номенклатура);
			Если ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
				
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
				
			КонецЕсли;
			
			Если ПартияЗаполнена Тогда
				
				СтруктураОтбора.Вставить("Партия", СтрокаТовар.Партия);
				
			КонецЕсли;
			
			МассивНайденныхПартий = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
			
			Для СчПартий = 0 По МассивНайденныхПартий.ВГраница() Цикл
				// Идем по списанным партиям
				СтрокаПартия = МассивНайденныхПартий[СчПартий];
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаПартия.ХарактеристикаНоменклатуры);
				СтруктураОтбора.Вставить("Партия", СтрокаПартия.Партия);
				
				Если ГТДЗаполнена Тогда
					
					СтруктураОтбора.Вставить("ГТД", СтрокаТовар.ГТД);
					
				КонецЕсли;
				
				МассивНайденныхГТД = ОстаткиГТДПартий.НайтиСтроки(СтруктураОтбора);
				
				// Сформируем расход ГТД
				Для СчГТД = 0 По МассивНайденныхГТД.ВГраница() Цикл
					
					// Списываем ГТД партии
					СтрокаГТД 								= МассивНайденныхГТД[СчГТД];
					НоваяЗапись 							= ТаблицаНовыхДвиженийГТД.Добавить();
					НоваяЗапись.СкладКомпании 				= СтрокаГТД.СкладКомпании;
					НоваяЗапись.Номенклатура 				= СтрокаГТД.Номенклатура;
					НоваяЗапись.ХарактеристикаНоменклатуры 	= СтрокаГТД.ХарактеристикаНоменклатуры;
					НоваяЗапись.Партия 						= СтрокаГТД.Партия;
					НоваяЗапись.ГТД 						= СтрокаГТД.ГТД;
					НоваяЗапись.ХозОперация 				= ХозОперация;
					НоваяЗапись.Количество 					= Мин(СтрокаГТД.Количество, СтрокаТовар.Количество);
					
					СтрокаГТД.Количество = СтрокаГТД.Количество - НоваяЗапись.Количество;
					СтрокаПартия.Количество = СтрокаПартия.Количество - НоваяЗапись.Количество;
					
					СтрокаТовар.Количество = СтрокаТовар.Количество - НоваяЗапись.Количество;
					Если СтрокаГТД.Количество = 0 Тогда
						
						ОстаткиГТДПартий.Удалить(СтрокаГТД);
						
					КонецЕсли;
					
					Если СтрокаПартия.Количество = 0 И НЕ БратьТоварыИзДвижений Тогда
						
						Прервать;
						
					КонецЕсли;
					
					Если СтрокаТовар.Количество = 0 Тогда
						
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если СтрокаПартия.Количество = 0 И НЕ БратьТоварыИзДвижений Тогда
					
					ТаблицаПартий.Удалить(СтрокаПартия);
					
				КонецЕсли;
				
				Если СтрокаТовар.Количество = 0 Тогда
					
					Прервать;
					
				КонецЕсли;
				
				// Необходимо списать количество ГТД на Отсутсвующую партия, т.к по партии движения были
				Если МассивНайденныхГТД.Количество() = 0 И Не ПартияЗаполнена Тогда
					
					СтрокаТовар.Количество = СтрокаТовар.Количество - СтрокаПартия.Количество;
					ТаблицаПартий.Удалить(СтрокаПартия);
					
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			СтруктураОтбора = Новый Структура("Номенклатура", СтрокаТовар.Номенклатура);
			
			Если ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
				
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
				
			КонецЕсли;
			
			МассивНайденныхГТД = ОстаткиГТДПартий.НайтиСтроки(СтруктураОтбора);
			
			// Сформируем расход ГТД
			Для СчГТД = 0 По МассивНайденныхГТД.ВГраница() Цикл
				
				// Списываем ГТД партии
				СтрокаГТД 								= МассивНайденныхГТД[СчГТД];
				НоваяЗапись 							= ТаблицаНовыхДвиженийГТД.Добавить();
				НоваяЗапись.СкладКомпании 				= СтрокаГТД.СкладКомпании;
				НоваяЗапись.Номенклатура 				= СтрокаГТД.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры 	= СтрокаГТД.ХарактеристикаНоменклатуры;
				НоваяЗапись.Партия 						= СтрокаГТД.Партия;
				НоваяЗапись.ГТД 						= СтрокаГТД.ГТД;
				НоваяЗапись.ХозОперация 				= ХозОперация;
				НоваяЗапись.Количество 					= Мин(СтрокаГТД.Количество, СтрокаТовар.Количество);
				
				СтрокаГТД.Количество = СтрокаГТД.Количество - НоваяЗапись.Количество;
				
				// Списываем из таблицы партий
				ОтборПартий = Новый Структура("Номенклатура", СтрокаТовар.Номенклатура);
				
				Если ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					
					ОтборПартий.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
					
				КонецЕсли;
				
				ОтборПартий.Вставить("Партия", СтрокаГТД.Партия);
				
				МассивНайденныхПартий = ТаблицаПартий.НайтиСтроки(ОтборПартий);
				СтрокаПартияНайдена = Ложь;
				
				Если МассивНайденныхПартий.Количество() > 0 Тогда
					
					СтрокаПартияНайдена = Истина;
					СтрокаПартия = МассивНайденныхПартий[0];
					
				КонецЕсли;
				
				СтрокаТовар.Количество = СтрокаТовар.Количество - НоваяЗапись.Количество;
				
				Если СтрокаГТД.Количество = 0 Тогда
					
					ОстаткиГТДПартий.Удалить(СтрокаГТД);
					
				КонецЕсли;
				
				Если СтрокаПартияНайдена Тогда
					
					СтрокаПартия.Количество = СтрокаПартия.Количество - НоваяЗапись.Количество;
					
					Если СтрокаПартия.Количество = 0 И НЕ БратьТоварыИзДвижений Тогда
						
						ТаблицаПартий.Удалить(СтрокаПартия);
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если СтрокаТовар.Количество = 0 Тогда
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Если еще остались ГТД для списания, то возможно уходим в минус
		// Не хватает товара на остатках
		Если СтрокаТовар.Количество > 0 Тогда
			
			Если ГТДЗаполнена Тогда
				
				Если НЕ ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					
					ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+СтрокаТовар.Номенклатура+"> не распределился по ГТД " + СтрокаТовар.ГТД;
					Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
					
				Иначе
					
					ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+СтрокаТовар.Номенклатура+"> с характеристикой <"+СтрокаТовар.ХарактеристикаНоменклатуры+"> не распределился по ГТД " + СтрокаТовар.ГТД;
					Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
					
				КонецЕсли;
				
				Если НЕ ОтрицательныеОстаткиРазрешены Тогда
					
					ВсеОК = Ложь;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураВозврата.Вставить("ВсеОК", ВсеОК);
	СтруктураВозврата.Вставить("ТаблицаНовыхДвиженийГТД", ТаблицаНовыхДвиженийГТД);
	
	Возврат СтруктураВозврата;

КонецФункции 

// БизТех Аляль 24.06.2025г.
//Формирует движения по регистру Товары в производстве  
//
// Возвращаемое значение: 
//	Булево. Истина - все ОК, Ложь - чего-то не так.
// 
Функция ПровестиПоТоварыВПроизводстве(Основания, РежимПроведения, ДокОснование)
	
	ВсеОК = Истина; 
	ТекстСообщения = "";  
	ЭтоКорректировкаПоЗаказНаряду = Истина;
	ЦехОснования = ДокументОснование.Цех;
	
	Основания_Документы = Документы.КорректировкаРеализации.ДобавитьПеремещенияТоваровЗаказНарядаИзОснований(Основания, Ложь);
	
	СтарыеДвиженияПартий = ДвиженияПартийДоКорректировки(Основания_Документы, Основания);
	
	НовыеДвиженияПартий = ДвиженияПартийПослеКорректировки(Неопределено, СтарыеДвиженияПартий, ТекстСообщения);
	ВсеОК = НовыеДвиженияПартий.ВсеОК И ВсеОК;
	
	Если ВсеОК Тогда 
		
		РазницаПартий = ПосчитатьРазницуДвиженийПоПартиям(СтарыеДвиженияПартий, НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий);
		
		Если ЗначениеЗаполнено(РазницаПартий) Тогда
			СтруткураДвиженийДляЗаписиВРегистрПартий = ДвиженияПартийПослеКорректировки(РазницаПартий, СтарыеДвиженияПартий, ТекстСообщения);
			ВсеОК = СтруткураДвиженийДляЗаписиВРегистрПартий.ВсеОК И ВсеОК;
			Если ВсеОК Тогда
				
				Для каждого ДвижениеПартии Из СтруткураДвиженийДляЗаписиВРегистрПартий.ТаблицаНовыхДвиженийПартий Цикл
					
					НоваяЗапись = Движения.ТоварыВПроизводстве.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеПартии);
					НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяЗапись.Регистратор = Ссылка;
					НоваяЗапись.Период      = Дата; 
					НоваяЗапись.ХозОперация = ДокОснование.ХозОперация;
					НоваяЗапись.Цех 		= ЦехОснования;  
					НоваяЗапись.ЗаказНаряд  = ДокОснование;    
					
					//Дубль по Извлечению
					НоваяЗаписьИзвлечение = Движения.ТоварыВПроизводстве.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗаписьИзвлечение, ДвижениеПартии);
					НоваяЗаписьИзвлечение.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяЗаписьИзвлечение.Регистратор = Ссылка;
					НоваяЗаписьИзвлечение.Период      = Дата; 
					НоваяЗаписьИзвлечение.ХозОперация = Справочники.ХозОперации.ИзвлечениеТоваровИзПроизводства;
					НоваяЗаписьИзвлечение.Цех 		  = ЦехОснования;  
					НоваяЗаписьИзвлечение.ЗаказНаряд  = ДокОснование; 
					НоваяЗаписьИзвлечение.Количество  = - НоваяЗаписьИзвлечение.Количество;
					НоваяЗаписьИзвлечение.Сумма		  = - НоваяЗаписьИзвлечение.Сумма;
					НоваяЗаписьИзвлечение.СуммаУпр	  = - НоваяЗаписьИзвлечение.СуммаУпр;
				КонецЦикла;
				
				Движения.ТоварыВПроизводстве.Записать();
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;
		
			
	Если НЕ ВсеОК Тогда
		дкСообщитьРезультат(ТекстСообщения,"Важное&Партии товаров компании:", ЭтотОбъект);
	КонецЕсли;
		
	Возврат ВсеОК;
	
КонецФункции

// Возвращает результат запроса, с таблицей незакрытых партий.
//
// Параметры:
//	ТаблицаТоваров - ТаблицаЗначений - таблица с товарами, для которых необходимо получить дерево партий.
//	СтарыеДвиженияПартий - 	ТаблицаЗначений - таблица, движений партий документов-оснований, где значения движений с
//							видом движения Приход взяты с обратным знаком.
//
// Возвращаемое значение:
//  ДеревоЗначений - Результат остатоков партий   
//
Функция ПолучитьДеревоПартий(ТаблицаТоваров, СтарыеДвиженияПартий)
	
	ДатаСреза = Неопределено;
	ГраницаРасчетаОстатков = Неопределено; 
	
	// Получим статус партии. Если и передаем товар на комиссию, то только собственный.
	СтатусПартии = Неопределено;
	ХозОперацияСделки = Сделка.ХозОперация;
	Если ХозОперацияСделки = Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
		СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
	КонецЕсли;
	
	ДатаМоментаВремени = Ссылка.Дата;
	ГраницаРасчетаОстатков = Новый МоментВремени(ДатаМоментаВремени, Ссылка);
	
	// Получим дату среза.
	ДатаСреза = ГраницаРасчетаОстатков.Дата;

	// Получим значения настроек 
	РежимВозврата = обПраво("РежимСписанияПриВозвратеПоставщику", Права,, ЭтотОбъект);
	
	// Чтение значения для списания по датам
	Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
		СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	Иначе
		СтратегияСписанияПартийТоваровПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
	КонецЕсли;
	
	// Чтение значения для списания по статусам
	СтратегияСписанияПартийТоваровПоСтатусам=Константы.СтратегияСписанияПартийТоваровПоСтатусам.Получить();
	Если СтратегияСписанияПартийТоваровПоСтатусам = Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаКупленныеПотомПринятые Тогда
		ПорядокСтатусов = "Убыв";
	ИначеЕсли СтратегияСписанияПартийТоваровПоСтатусам=Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаПринятыеПотомКупленные Тогда
		ПорядокСтатусов = "Возр";
	Иначе
		ПорядокСтатусов = "";
	КонецЕсли;
	Если СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.ЛИФО Тогда
		ПорядокСписанияПартий = "Убыв"; 
	Иначе
		ПорядокСписанияПартий = "Возр";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	0 КАК ПорядокСортировки,
		|	СтарыеДвиженияПартий.СкладКомпании КАК СкладКомпании,
		|	СтарыеДвиженияПартий.Номенклатура КАК Номенклатура,
		|	СтарыеДвиженияПартий.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СтарыеДвиженияПартий.СтатусПартии КАК СтатусПартии,
		|	СтарыеДвиженияПартий.Партия КАК Партия,
		|	СтарыеДвиженияПартий.Количество КАК Количество,
		|	СтарыеДвиженияПартий.Сумма КАК Сумма,
		|	СтарыеДвиженияПартий.СуммаНДС КАК СуммаНДС,
		|	СтарыеДвиженияПартий.СуммаУпр КАК СуммаУпр
		|ПОМЕСТИТЬ СтарыеДвиженияПартий
		|ИЗ
		|	&СтарыеДвиженияПартий КАК СтарыеДвиженияПартий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	1 КАК ПорядокСортировки,
		|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпанииОстатки.СтатусПартии КАК СтатусПартии,
		|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.СтатусПартии = &Купленные
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПорядокСтатуса,
		|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК Количество,
		|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаОстаток, 0) КАК Сумма,
		|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаНДСОстаток, 0) КАК СуммаНДС,
		|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр
		|ПОМЕСТИТЬ ОстаткиПартийБезОснований
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		|			&Момент,
		|			Номенклатура В (&Номенклатура)
		|				И СкладКомпании = &СкладКомпанииШапка) КАК ПартииТоваровКомпанииОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтарыеДвиженияПартий.ПорядокСортировки,
		|	СтарыеДвиженияПартий.СкладКомпании,
		|	СтарыеДвиженияПартий.Номенклатура,
		|	СтарыеДвиженияПартий.ХарактеристикаНоменклатуры,
		|	СтарыеДвиженияПартий.СтатусПартии,
		|	СтарыеДвиженияПартий.Партия,
		|	ВЫБОР
		|		КОГДА СтарыеДвиженияПартий.СтатусПартии = &Купленные
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	СтарыеДвиженияПартий.Количество,
		|	СтарыеДвиженияПартий.Сумма,
		|	СтарыеДвиженияПартий.СуммаНДС,
		|	СтарыеДвиженияПартий.СуммаУпр
		|ИЗ
		|	СтарыеДвиженияПартий КАК СтарыеДвиженияПартий
		|ГДЕ
		|	СтарыеДвиженияПартий.Номенклатура В (&Номенклатура)
		|	И СтарыеДвиженияПартий.СкладКомпании В (&СкладКомпании)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиПартийБезОснований.ПорядокСортировки КАК ПорядокСортировки,
		|	ОстаткиПартийБезОснований.СкладКомпании КАК СкладКомпании,
		|	ОстаткиПартийБезОснований.Номенклатура КАК Номенклатура,
		|	ОстаткиПартийБезОснований.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиПартийБезОснований.СтатусПартии КАК СтатусПартии,
		|	ОстаткиПартийБезОснований.Партия КАК Партия,
		|	ОстаткиПартийБезОснований.ПорядокСтатуса КАК ПорядокСтатуса,
		|	СУММА(ОстаткиПартийБезОснований.Количество) КАК Количество,
		|	СУММА(ОстаткиПартийБезОснований.Сумма) КАК Сумма,
		|	СУММА(ОстаткиПартийБезОснований.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ОстаткиПартийБезОснований.СуммаУпр) КАК СуммаУпр,
		|	ОстаткиПартийБезОснований.ХарактеристикаНоменклатуры.Сортировка КАК ХарактеристикаНоменклатурыСортировка,
		|	ОстаткиПартийБезОснований.Партия.Дата КАК ПартияДата
		|ПОМЕСТИТЬ ОстаткиПартийСгруппированные
		|ИЗ
		|	ОстаткиПартийБезОснований КАК ОстаткиПартийБезОснований
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиПартийБезОснований.ПорядокСортировки,
		|	ОстаткиПартийБезОснований.СкладКомпании,
		|	ОстаткиПартийБезОснований.Номенклатура,
		|	ОстаткиПартийБезОснований.ХарактеристикаНоменклатуры,
		|	ОстаткиПартийБезОснований.СтатусПартии,
		|	ОстаткиПартийБезОснований.Партия,
		|	ОстаткиПартийБезОснований.ПорядокСтатуса,
		|	ОстаткиПартийБезОснований.ХарактеристикаНоменклатуры.Сортировка,
		|	ОстаткиПартийБезОснований.Партия.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиПартийСгруппированные.ПорядокСортировки КАК ПорядокСортировки,
		|	ОстаткиПартийСгруппированные.СкладКомпании КАК СкладКомпании,
		|	ОстаткиПартийСгруппированные.Номенклатура КАК Номенклатура,
		|	ОстаткиПартийСгруппированные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиПартийСгруппированные.СтатусПартии КАК СтатусПартии,
		|	ОстаткиПартийСгруппированные.Партия КАК Партия,
		|	ОстаткиПартийСгруппированные.ПорядокСтатуса КАК ПорядокСтатуса,
		|	ОстаткиПартийСгруппированные.Количество КАК Количество,
		|	ОстаткиПартийСгруппированные.Сумма КАК Сумма,
		|	ОстаткиПартийСгруппированные.СуммаНДС КАК СуммаНДС,
		|	ОстаткиПартийСгруппированные.СуммаУпр КАК СуммаУпр,
		|	ОстаткиПартийСгруппированные.ХарактеристикаНоменклатурыСортировка КАК ХарактеристикаНоменклатурыСортировка,
		|	ОстаткиПартийСгруппированные.ПартияДата КАК ПартияДата
		|ИЗ
		|	ОстаткиПартийСгруппированные КАК ОстаткиПартийСгруппированные
		|
		|УПОРЯДОЧИТЬ ПО
		|	//ПОРЯДОКСОРТИРОВКИ
		|	" + ?(ПустаяСтрока(ПорядокСтатусов), "", "ПорядокСтатуса " + ПорядокСтатусов + ",")
			+ " ХарактеристикаНоменклатурыСортировка ВОЗР,"
			+ " ПартияДата " + ПорядокСписанияПартий + ","
			+ " Партия " + ПорядокСписанияПартий + "
		|	
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Сумма),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаУпр)
		|ПО
		|	Номенклатура";
			
	// Добавляется сортировка в зависимости от версии объекта.
	// Для старых объектов эта сортировка не требуется,
	// чтобы не изменились старые движения по партиям.
	Если ВерсияОбъекта >= "02.00" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПОРЯДОКСОРТИРОВКИ", "ПорядокСортировки ВОЗР,");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СтарыеДвиженияПартий", СтарыеДвиженияПартий);
	Запрос.УстановитьПараметр("Купленные"           , Перечисления.СтатусыПартий.ТоварКупленный);
	Запрос.УстановитьПараметр("Момент"              , ГраницаРасчетаОстатков);
	СкладыКомпании = ТаблицаТоваров.ВыгрузитьКолонку("СкладКомпании");
	Запрос.УстановитьПараметр("СкладКомпании"       , СкладыКомпании);
	Запрос.УстановитьПараметр("СкладКомпанииШапка"  , СкладКомпании);
	Запрос.УстановитьПараметр("Номенклатура"        , ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура"));
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПартииТоваровКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата));
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаТоваров);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("СкладКомпании", "СкладКомпании");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ПолучитьДеревоПартий()

Функция ВозможенВВодНаОсновании(ДанныеЗаполнения)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения)
		И (ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваров")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказНаряд")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КорректировкаРеализации")) Тогда
		
		Если ДанныеЗаполнения = Ссылка Тогда
			Сообщить("Ввод корректировки реализации на основании самой себя запрещен.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если НЕ ДанныеЗаполнения.Проведен Тогда
			Сообщить("Ввод корректировки реализации возможен только на основании проведенного документа.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			Если ДанныеЗаполнения.Состояние <> Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
				Сообщить("Ввод корректировки реализации на основании заказ-наряда возможен только в состоянии ""Закрыт"".", СтатусСообщения.Внимание);
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		ХозОперацияОснования = ДанныеЗаполнения.ХозОперация;
		Если ХозОперацияОснования = Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
			Сообщить("Ввод корректировки реализации не возможен на основании реализации товаров комиссия.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если КорректировкаНеДоступна(ДанныеЗаполнения) Тогда
			Сообщить("На основании """ + ДанныеЗаполнения + """ введен возврат товаров. Ввод корректировки реализации невозможен.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПроверитьПродажуНижеСебестоимости(ТаблицаТовары, СтрокаТовар, Показатели, ТекстСообщения)
	
	Результат = ИСТИНА;
	НижеСебестоимости = Ложь;
	
	РасчетРегл = (ВалютаДокумента = Показатели.ВалютаРегл) И (КурсДокумента = Показатели.КурсРегл);
	Если РасчетРегл Тогда
		МинимальнаяСумма = Показатели.СебестоимостьБезНДС;
		Если МинимальнаяСумма > Показатели.СуммаБезНДС Тогда 
			НижеСебестоимости = Истина; 
		КонецЕсли;
	Иначе
		МинимальнаяСуммаУпр = Показатели.СебестоимостьБезНДСУпр;
		Если МинимальнаяСуммаУпр > Показатели.СуммаБезНДСУпр Тогда 
			НижеСебестоимости = Истина; 
		КонецЕсли;
	КонецЕсли; 
	
	Если НижеСебестоимости Тогда
		Если РасчетРегл Тогда
			РасчетВалюта = Показатели.ВалютаРегл;
		Иначе
			РасчетВалюта = Показатели.ВалютаУпр;
		КонецЕсли;
		ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+СтрокаТовар.Номенклатура+">. Себестоимость товара "+?(РасчетРегл,МинимальнаяСумма,МинимальнаяСуммаУпр)+" "+РасчетВалюта+" без НДС, сумма продажи "+?(РасчетРегл,Показатели.СуммаБезНДС,Показатели.СуммаБезНДСУпр)+" "+РасчетВалюта+" без НДС. Продажа ниже себестоимости запрещена.";
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДвиженияПродажДоКорректировки(Основания)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Продажи.Номенклатура КАК Номенклатура,
		|	Продажи.Автомобиль КАК Автомобиль,
		|	Продажи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Продажи.СкладКомпании КАК СкладКомпании,
		|	Продажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	Продажи.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	Продажи.Покупатель КАК Покупатель,
		|	Продажи.Поставщик КАК Поставщик,
		|	Продажи.Партия КАК Партия,
		|	Продажи.СтатусПартии КАК СтатусПартии,
		|	Продажи.Авторабота КАК Авторабота,
		|	Продажи.ГТД КАК ГТД,
		|	Продажи.СтавкаНДС КАК СтавкаНДС,
		|	Продажи.ДокументПродажи КАК ДокументПродажи,
		|	СУММА(Продажи.Количество) КАК Количество,
		|	СУММА(Продажи.КоличествоНормочасов) КАК КоличествоНормочасов,
		|	СУММА(Продажи.Сумма) КАК Сумма,
		|	СУММА(Продажи.СуммаНДС) КАК СуммаНДС,
		|	СУММА(Продажи.СуммаСкидки) КАК СуммаСкидки,
		|	СУММА(Продажи.Себестоимость) КАК Себестоимость,
		|	СУММА(Продажи.СуммаНДСВходящий) КАК СуммаНДСВходящий,
		|	СУММА(Продажи.СуммаУпр) КАК СуммаУпр,
		|	СУММА(Продажи.СебестоимостьУпр) КАК СебестоимостьУпр
		|ПОМЕСТИТЬ втПродажи
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	Продажи.Регистратор В(&СписокОснований)
		|
		|СГРУППИРОВАТЬ ПО
		|	Продажи.ДокументПродажи,
		|	Продажи.ПодразделениеКомпании,
		|	Продажи.ДоговорВзаиморасчетов,
		|	Продажи.Номенклатура,
		|	Продажи.СтавкаНДС,
		|	Продажи.ГТД,
		|	Продажи.Автомобиль,
		|	Продажи.ХарактеристикаНоменклатуры,
		|	Продажи.СкладКомпании,
		|	Продажи.Поставщик,
		|	Продажи.Покупатель,
		|	Продажи.Авторабота,
		|	Продажи.Партия,
		|	Продажи.СтатусПартии
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПродажи.Номенклатура КАК Номенклатура,
		|	втПродажи.Автомобиль КАК Автомобиль,
		|	втПродажи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	втПродажи.СкладКомпании КАК СкладКомпании,
		|	втПродажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	втПродажи.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	втПродажи.Покупатель КАК Покупатель,
		|	втПродажи.Поставщик КАК Поставщик,
		|	втПродажи.Партия КАК Партия,
		|	втПродажи.СтатусПартии КАК СтатусПартии,
		|	втПродажи.Авторабота КАК Авторабота,
		|	втПродажи.ГТД КАК ГТД,
		|	втПродажи.СтавкаНДС КАК СтавкаНДС,
		|	втПродажи.ДокументПродажи КАК ДокументПродажи,
		|	втПродажи.Количество КАК Количество,
		|	втПродажи.КоличествоНормочасов КАК КоличествоНормочасов,
		|	втПродажи.Сумма КАК Сумма,
		|	втПродажи.СуммаНДС КАК СуммаНДС,
		|	втПродажи.СуммаСкидки КАК СуммаСкидки,
		|	втПродажи.Себестоимость КАК Себестоимость,
		|	втПродажи.СуммаНДСВходящий КАК СуммаНДСВходящий,
		|	втПродажи.СуммаУпр КАК СуммаУпр,
		|	втПродажи.СебестоимостьУпр КАК СебестоимостьУпр
		|ИЗ
		|	втПродажи КАК втПродажи
		|ГДЕ
		|	(втПродажи.Количество <> 0
		|			ИЛИ втПродажи.Сумма <> 0
		|			ИЛИ втПродажи.СуммаНДС <> 0
		|			ИЛИ втПродажи.Себестоимость <> 0
		|			ИЛИ втПродажи.СуммаНДСВходящий <> 0
		|			ИЛИ втПродажи.СуммаУпр <> 0
		|			ИЛИ втПродажи.СебестоимостьУпр <> 0
		|)";
	
	Запрос.УстановитьПараметр("СписокОснований", Основания);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ДвиженияПродажПослеКорректировки(РежимПроведения, НовыеДвиженияПартий, НовыеДвиженияГТД, СтарыеДвиженияПродаж, ЭтоКорректировкаПоЗаказНаряду, ЦехОснования, ТестСообщения)

	ВсеОК = Истина;

	СтруктураВозврата = Новый Структура("ВсеОК", ВсеОК);
	
	ТаблицаНовыхДвиженийПродаж = РегистрыНакопления.Продажи.СоздатьНаборЗаписей().Выгрузить();  

	// получаем товарную таблицу
	ТаблицаТовары = ПолучитьТаблицуПродажиТоваров();
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурсРегл   = обКурсДляВалюты(ВалютаРегл, Дата);
	КурсРегл = СтруктураКурсРегл.Курс / ?(СтруктураКурсРегл.Кратность=0, 1, СтруктураКурсРегл.Кратность);
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	КурсУпр = КурсВалютыУпр;
	
	Если НЕ ЗначениеЗаполнено(КурсУпр) Тогда
		СтруктураКурсУпр = обКурсДляВалюты(ВалютаУпр, Дата);
		КурсУпр = СтруктураКурсУпр.Курс / ?(СтруктураКурсУпр.Кратность=0, 1, СтруктураКурсУпр.Кратность);
	КонецЕсли;
	
	// получаем таблицу партий для расчета себестоимости
	ТаблицаПартий        = НовыеДвиженияПартий;
	ТаблицаГТД           = НовыеДвиженияГТД;
	
	КэшСебестоимостиПриКомиссии = Новый ТаблицаЗначений;

	// свернем таблицу движений
	ЕстьПартия         = Истина;
	ЕстьГТДПартий      = Истина;
	ЕстьГТД            = Истина;
	ЕстьГТДКомиссия    = Ложь;
	ЕстьСтавкаНДС      = Истина;
	ЕстьГТДТоваров     = Истина;
	Комиссия           = Ложь;
	АвтомобильВТаблице = Ложь;
	ПартииУказаны      = Истина;
	Сторно             = Ложь;
	Покупатель         = Ссылка.Контрагент;
	
	ДокументОснованиеОснование = ДокументОснование.ДокументОснование;
	
	ДокументПродажи = Документы.КорректировкаРеализации.ПолучитьСделку(Ссылка);
	
	Если ДокументПродажи = Неопределено Тогда
		
		ДокументПродажи = Ссылка;
		
	КонецЕсли;  
	
	// надо закэшировать контрагента и договор
	КэшКонтрагент = Новый Соответствие();
	Для Каждого СтрокаПартия Из ТаблицаПартий Цикл
		Если КэшКонтрагент[СтрокаПартия.Партия] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 Контрагент ИЗ Документ."+СтрокаПартия.Партия.Метаданные().Имя+" ГДЕ Ссылка=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка", СтрокаПартия.Партия);
		Попытка
			РезультатКэш = Запрос.Выполнить();
			Если НЕ РезультатКэш.Пустой() Тогда
				КэшКонтрагент.Вставить(СтрокаПартия.Партия, РезультатКэш.Выгрузить().Получить(0).Контрагент);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	// Получим порядок проверки себестоимости для Организации проводимого документа
	ПорядокПроверкиСебестоимостиТовараПриПродаже = обПраво("ЗапретитьПродажуНижеСебестоимости",Права,,ЭтотОбъект);
	
	КонтролироватьСебестоимость = ДополнительныеСвойства.Свойство("ПроверкаПродажиНижеСебестоимости")
		И ДополнительныеСвойства.ПроверкаПродажиНижеСебестоимости
		ИЛИ ПорядокПроверкиСебестоимостиТовараПриПродаже;
	
	СтруктураОтбораСебестоимостьКомиссии = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Партия");
	// получим таблицу номенклатуры с ручным списанием характеристик
	ТаблицаРучныхХарактеристик = дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(Ссылка);
	
	Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
		
		ЭтоАвторабота = (ТипЗнч(СтрокаТовар.Номенклатура) = Тип("СправочникСсылка.Автоработы"));
		
		КоличествоДляСумм     = ?(СтрокаТовар.Количество=0, 1, СтрокаТовар.Количество);
		СуммаПродажи          = Окр(обПересчет(СтрокаТовар.Сумма, ВалютаДокумента, КурсДокумента, ВалютаРегл, КурсРегл),2);
		ЦенаПродажи           = СуммаПродажи/КоличествоДляСумм;
		СуммаПродажиУпр       = Окр(обПересчет(СтрокаТовар.Сумма, ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсУпр),2);
		ЦенаПродажиУпр        = СуммаПродажиУпр/КоличествоДляСумм;
		СуммаПродажиСкидки    = Окр(обПересчет(СтрокаТовар.СуммаСкидки,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл),2);
		ЦенаПродажиСкидки     = СуммаПродажиСкидки/КоличествоДляСумм;
		СуммаПродажиНДС       = Окр(обПересчет(СтрокаТовар.СуммаНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл),2);
		ЦенаПродажиНДС        = СуммаПродажиНДС/КоличествоДляСумм;
		СуммаПродажиНДСУпр    = Окр(обПересчет(СтрокаТовар.СуммаНДС,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсУпр),2);
		ЦенаПродажиНДСУпр     = СуммаПродажиНДСУпр/КоличествоДляСумм;
		
		СуммаПродажиБезНДС    = СуммаПродажи - СуммаПродажиНДС;
		ЦенаПродажиБезНДС     = ЦенаПродажи - ЦенаПродажиНДС;
		СуммаПродажиБезНДСУпр = СуммаПродажиУпр - СуммаПродажиНДСУпр;
		ЦенаПродажиБезНДСУпр  = ЦенаПродажиУпр - ЦенаПродажиНДСУпр;
		
		СтавкаНДС = СтрокаТовар.СтавкаНДС.Ставка;
		СтавкаНДС = ?(СтавкаНДС = Неопределено, 0, СтавкаНДС);
		
		СуммаПродажиИтого                 = 0;
		СуммаПродажиУпрИтого              = 0;
		СуммаПродажиСкидкиИтого           = 0;
		СуммаПродажиНДСИтого              = 0;
		СуммаПродажиНДСУпрИтого           = 0;
		СебестоимостьПартийБезНДСИтого    = 0;
		СебестоимостьПартийБезНДСУпрИтого = 0;
		
		Если ЭтоАвторабота Тогда
			СтруктураОтбора = Новый Структура("Номенклатура,Авторабота", СтрокаТовар.Номенклатура.Номенклатура, СтрокаТовар.Номенклатура);
		Иначе
			СтруктураОтбора = Новый Структура("Номенклатура", СтрокаТовар.Номенклатура);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры)
			ИЛИ (ТаблицаРучныхХарактеристик <> Неопределено
			И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура") <> Неопределено) Тогда
			
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТовар.ХарактеристикаНоменклатуры);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
			СтруктураОтбора.Вставить("Партия", СтрокаТовар.Партия);
		КонецЕсли;

		Если ЭтоКорректировкаПоЗаказНаряду И НЕ ЭтоАвторабота Тогда
			
		Иначе
			
			Если ЗначениеЗаполнено(СкладКомпании) Тогда
				СтруктураОтбора.Вставить("СкладКомпании", СкладКомпании);
			КонецЕсли;			
			
		КонецЕсли;
		
		// Если сумма < 0, то это возврат товара. Значит списываем из таблицы Продаж.
		Если КоличествоДляСумм < 0 Тогда
			
			Если ЕстьГТДТоваров И ЕстьГТДПартий И ЗначениеЗаполнено(СтрокаТовар.ГТД) Тогда
				СтруктураОтбора.Вставить("ГТД", СтрокаТовар.ГТД);
			КонецЕсли; 
			
			СуммаРазница          = обПересчет(СтрокаТовар.Сумма, ВалютаДокумента, Дата, ВалютаРегл, Дата);
			СуммаРазницаНДС       = обПересчет(СтрокаТовар.СуммаНДСРазница,ВалютаДокумента,Дата,ВалютаРегл,Дата);
			СуммаНаЕд             = Окр(?(СтрокаТовар.КоличествоДляЦены=0,0,обПересчет(СтрокаТовар.СуммаВсегоДляЦены,ВалютаДокумента,Дата,ВалютаРегл,Дата)/СтрокаТовар.КоличествоДляЦены),2);
			СуммаНДСНаЕд          = Окр(?(СтрокаТовар.КоличествоДляЦены=0,0,обПересчет(СтрокаТовар.СуммаНДС,ВалютаДокумента,Дата,ВалютаРегл,Дата)/СтрокаТовар.КоличествоДляЦены),2);
			НадоСписатьКоличество = -КоличествоДляСумм;
			
			МассивСтрокПродаж = СтарыеДвиженияПродаж.НайтиСтроки(СтруктураОтбора);
			Для Каждого стрПродаж Из МассивСтрокПродаж Цикл
				Если НадоСписатьКоличество = 0 Тогда Прервать; КонецЕсли;
				
				КоличествоСписываем = Мин(НадоСписатьКоличество,стрПродаж.Количество);
				Если КоличествоСписываем = стрПродаж.Количество Тогда
					СуммаСписываем    = -стрПродаж.Сумма;
					СуммаНДССписываем = -стрПродаж.СуммаНДС;
					СуммаУпрСписываем = -стрПродаж.СуммаУпр;
					СуммаСкидкиСписываем       = -стрПродаж.СуммаСкидки;
					
					СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр;
					СебестоимостьСписываем    = -стрПродаж.Себестоимость;
					СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий;
				Иначе
					СуммаСписываем    = -стрПродаж.Сумма/стрПродаж.Количество*КоличествоСписываем;
					СуммаНДССписываем = -стрПродаж.СуммаНДС/стрПродаж.Количество*КоличествоСписываем;
					СуммаУпрСписываем = -стрПродаж.СуммаУпр/стрПродаж.Количество*КоличествоСписываем;
					СуммаСкидкиСписываем       = -стрПродаж.СуммаСкидки / стрПродаж.Количество * КоличествоСписываем;
					
					СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр/стрПродаж.Количество*КоличествоСписываем;
					СебестоимостьСписываем    = -стрПродаж.Себестоимость/стрПродаж.Количество*КоличествоСписываем;
					СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий/стрПродаж.Количество*КоличествоСписываем;
				КонецЕсли;
				
				НоваяЗапись = ТаблицаНовыхДвиженийПродаж.Добавить();
				НоваяЗапись.Период                     = Дата;
				НоваяЗапись.Регистратор                = Ссылка;
				НоваяЗапись.ХозОперация                = ХозОперация;
				НоваяЗапись.ПодразделениеКомпании      = стрПродаж.ПодразделениеКомпании;
				НоваяЗапись.Покупатель                 = стрПродаж.Покупатель;
				НоваяЗапись.ДоговорВзаиморасчетов      = стрПродаж.ДоговорВзаиморасчетов;
				НоваяЗапись.ДокументПродажи            = стрПродаж.ДокументПродажи;
				НоваяЗапись.Номенклатура = стрПродаж.Номенклатура;
				Если ЭтоАвторабота Тогда
					НоваяЗапись.Авторабота = стрПродаж.Авторабота;
				Иначе
					НоваяЗапись.ХарактеристикаНоменклатуры = стрПродаж.ХарактеристикаНоменклатуры;
				КонецЕсли;
				ЭтоУслуга = НоваяЗапись.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга;
				НоваяЗапись.СтатусПартии               = стрПродаж.СтатусПартии;
				НоваяЗапись.СкладКомпании              = стрПродаж.СкладКомпании;
				НоваяЗапись.Партия                     = стрПродаж.Партия;
				НоваяЗапись.ГТД                        = стрПродаж.ГТД;
				НоваяЗапись.Поставщик                  = стрПродаж.Поставщик;
				НоваяЗапись.СтавкаНДС                  = стрПродаж.СтавкаНДС;
				НоваяЗапись.Количество                 = -КоличествоСписываем;
				Если ЭтоАвторабота Тогда
					НоваяЗапись.КоличествоНормочасов   = -стрПродаж.КоличествоНормочасов;					
				КонецЕсли;
				НоваяЗапись.СуммаСкидки                = СуммаСкидкиСписываем;
				
				НоваяЗапись.СебестоимостьУпр = СебестоимостьУпрСписываем;
				НоваяЗапись.Себестоимость    = СебестоимостьСписываем;
				НоваяЗапись.СуммаНДСВходящий = СуммаНДСВходящийСписываем;

				НоваяЗапись.Сумма            = СуммаСписываем;
				НоваяЗапись.СуммаНДС         = СуммаНДССписываем;
				НоваяЗапись.СуммаУпр         = СуммаУпрСписываем;
				
				НадоСписатьКоличество = НадоСписатьКоличество - КоличествоСписываем;
				СуммаРазница          = СуммаРазница - СуммаСписываем;
				СуммаРазницаНДС       = СуммаРазницаНДС - СуммаНДССписываем;
				
				стрПродаж.Количество = стрПродаж.Количество - КоличествоСписываем;
				Если стрПродаж.Количество = 0 Тогда
					СтарыеДвиженияПродаж.Удалить(стрПродаж);
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Если НЕ ЭтоАвторабота Тогда
				МассивНайденныхПартий = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
			КонецЕсли;
		
			Если НЕ ЭтоАвторабота И МассивНайденныхПартий.Количество() > 0 Тогда
				
				НоваяЗапись = Неопределено;
				
				Для Каждого СтрокаПартий Из МассивНайденныхПартий Цикл
					
					ТоварПринятыйНаКомиссию = Ложь;
					
					// если указаны партии, то пропустим все партии не наши
					Если ПартииУказаны И ЗначениеЗаполнено(СтрокаТовар.Партия) И СтрокаПартий.Партия <> СтрокаТовар.Партия Тогда
						Продолжить;
					КонецЕсли;
					
					СебестоимостьРавнаПродаже = Ложь;
					Попытка
						Если СтрокаПартий.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия И (НЕ СтрокаПартий.Партия.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж) Тогда
							СебестоимостьРавнаПродаже = Истина;
							ТоварПринятыйНаКомиссию = Истина;
						КонецЕсли;
					Исключение
						СебестоимостьРавнаПродаже = Ложь;
					КонецПопытки;
					
					СтруктураОтбора = Новый Структура(
						"Номенклатура, ХарактеристикаНоменклатуры, Партия",
						СтрокаПартий.Номенклатура, СтрокаПартий.ХарактеристикаНоменклатуры, СтрокаПартий.Партия
					);
					
					Если АвтомобильВТаблице Тогда
						СтруктураОтбора.Вставить("Автомобиль", СтрокаПартий.Автомобиль);
					КонецЕсли;
					
					Если ЕстьГТДТоваров И ЗначениеЗаполнено(СтрокаТовар.ГТД) Тогда
						СтруктураОтбора.Вставить("ГТД", СтрокаТовар.ГТД);
					ИначеЕсли (НЕ ЕстьГТДТоваров) И ЕстьГТДПартий И ЗначениеЗаполнено(СтрокаПартий.ГТД) Тогда
						СтруктураОтбора.Вставить("ГТД", СтрокаПартий.ГТД);
					КонецЕсли; 
					
					МассивНайденныхГТД = ТаблицаГТД.НайтиСтроки(СтруктураОтбора);
					БезГТД = МассивНайденныхГТД.Количество() = 0;
					Для Каждого СтрокаГТД Из МассивНайденныхГТД Цикл
						
						КоличествоПродажи = Мин(СтрокаТовар.Количество,СтрокаПартий.Количество,СтрокаГТД.Количество);
						
						Сумма             = Окр(ЦенаПродажи*КоличествоПродажи,             2);
						СуммаУпр          = Окр(ЦенаПродажиУпр*КоличествоПродажи,          2);
						СуммаБезНДС       = Окр(ЦенаПродажиБезНДС*КоличествоПродажи,       2);
						СуммаБезНДСУпр    = Окр(ЦенаПродажиБезНДСУпр*КоличествоПродажи,    2);
						
						// При комиссии с себестоимостью не так все просто
						СебестоимостьУпр    = 0;
						Себестоимость       = 0;
						СуммаНДСВходящий    = 0;
						СебестоимостьБезНДС = 0;
						СебестоимостьБезНДСУпр = 0;
						
						Если СтрокаПартий.Количество > КоличествоПродажи Тогда
							// при выборочной партии списано больше чем в строке
							КоличествоДляСумм   = ?(СтрокаПартий.Количество=0, 1, СтрокаПартий.Количество);
							Себестоимость       = Окр(СтрокаПартий.Сумма/КоличествоДляСумм*КоличествоПродажи, 2);
							СебестоимостьУпр    = Окр(СтрокаПартий.СуммаУпр/КоличествоДляСумм*КоличествоПродажи, 2);
							СуммаНДСВходящий    = Окр(СтрокаПартий.СуммаНДС/КоличествоДляСумм*КоличествоПродажи, 2);
							
							СтрокаПартий.Сумма       = СтрокаПартий.Сумма - Себестоимость;
							СтрокаПартий.СуммаУпр    = СтрокаПартий.СуммаУпр - СебестоимостьУпр;
							СтрокаПартий.СуммаНДС    = СтрокаПартий.СуммаНДС - СуммаНДСВходящий;
						Иначе
							СебестоимостьУпр    = СтрокаПартий.СуммаУпр;
							Себестоимость       = СтрокаПартий.Сумма;
							СуммаНДСВходящий    = СтрокаПартий.СуммаНДС;
						КонецЕсли;
						
						СебестоимостьБезНДС    = Себестоимость - СуммаНДСВходящий;
						СебестоимостьБезНДСУпр = СебестоимостьУпр - обПересчет(СуммаНДСВходящий, ВалютаРегл, КурсРегл, ВалютаУпр, КурсУпр);
						
						Если ПорядокПроверкиСебестоимостиТовараПриПродаже И КонтролироватьСебестоимость
							И НЕ ТоварПринятыйНаКомиссию И СтрокаТовар.Количество>0 Тогда
							Показатели = Новый Структура;
							Показатели.Вставить("СебестоимостьБезНДС",    СебестоимостьБезНДС);
							Показатели.Вставить("СебестоимостьБезНДСУпр", СебестоимостьБезНДСУпр);
							Показатели.Вставить("СуммаБезНДС",            СуммаБезНДС);
							Показатели.Вставить("СуммаБезНДСУпр",         СуммаБезНДСУпр);
							Показатели.Вставить("ВалютаРегл",             ВалютаРегл);
							Показатели.Вставить("КурсРегл",               КурсРегл);
							Показатели.Вставить("ВалютаУпр",              ВалютаУпр);
							РезультатПроверки = ПроверитьПродажуНижеСебестоимости(ТаблицаТовары, СтрокаТовар, Показатели, ТестСообщения);
							ВсеОК = ВсеОК И РезультатПроверки;
						КонецЕсли;
						
						НоваяЗапись = ТаблицаНовыхДвиженийПродаж.Добавить();
						НоваяЗапись.Период                     = Дата;
						НоваяЗапись.Регистратор                = Ссылка;
						НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
						НоваяЗапись.Номенклатура               = СтрокаГТД.Номенклатура;
						НоваяЗапись.ДокументПродажи            = ДокументПродажи;
						
						// Возврат введен без основания
						НоваяЗапись.Поставщик = КэшКонтрагент[СтрокаГТД.Партия];
						
						НоваяЗапись.Покупатель                 = Покупатель;
						НоваяЗапись.СтатусПартии               = ?(Комиссия,Перечисления.СтатусыПартий.ТоварКупленный,СтрокаПартий.СтатусПартии);
						НоваяЗапись.ХозОперация                = ХозОперация;
						НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
						НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаГТД.ХарактеристикаНоменклатуры;
						НоваяЗапись.СкладКомпании              = ?(ЭтоКорректировкаПоЗаказНаряду, ЦехОснования, СкладКомпании);

						НоваяЗапись.СтавкаНДС                  = СтрокаТовар.СтавкаНДС;
						НоваяЗапись.Партия                     = СтрокаПартий.Партия;
						Попытка
							НоваяЗапись.Проект                 = Проект;
						Исключение
						КонецПопытки;
						Если ЕстьГТД Тогда
							НоваяЗапись.ГТД                    = СтрокаГТД.ГТД;
						Иначе
							НоваяЗапись.ГТД                    = Справочники.ГТД.ПустаяСсылка();
						КонецЕсли;
						
						// количество
						НоваяЗапись.Количество                 = КоличествоПродажи;
						
						// суммы
						НоваяЗапись.СуммаНДСВходящий                   = Окр(СуммаНДСВходящий, 2);
						НоваяЗапись.СебестоимостьУпр                   = Окр(СебестоимостьУпр, 2);
						НоваяЗапись.Себестоимость                      = Окр(Себестоимость, 2);
						НоваяЗапись.Сумма                              = Сумма;
						НоваяЗапись.СуммаСкидки                        = Окр(ЦенаПродажиСкидки*КоличествоПродажи,2);
						НоваяЗапись.СуммаУпр                           = СуммаУпр;
						НоваяЗапись.СуммаНДС                           = Окр(ЦенаПродажиНДС*КоличествоПродажи,2);
						
						Если СебестоимостьРавнаПродаже Тогда
							
							СебестоимостьБезНДСВходящий = НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий;
							СтавкаНДСВходящее   =
								?(СебестоимостьБезНДСВходящий = 0,
								0,
								Окр(НоваяЗапись.СуммаНДСВходящий/СебестоимостьБезНДСВходящий, 2));
							
							СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
							СебестоимостьБезНДСУпр = (НоваяЗапись.СуммаУпр - обПересчет(НоваяЗапись.СуммаНДС, ВалютаРегл, КурсРегл, ВалютаУпр, КурсУпр));
							
							НоваяЗапись.Себестоимость    = Сумма;
							НоваяЗапись.СебестоимостьУпр = СуммаУпр;
							НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
							
							СуммаПродажиБезНДС = СуммаПродажиБезНДС - СебестоимостьБезНДС;
							СуммаПродажиБезНДСУпр = СуммаПродажиБезНДСУпр - СебестоимостьБезНДСУпр;
						КонецЕсли;
						
						СуммаПродажиИтого                 = СуммаПродажиИтого + Сумма;
						СуммаПродажиУпрИтого              = СуммаПродажиУпрИтого + СуммаУпр;
						СуммаПродажиСкидкиИтого           = СуммаПродажиСкидкиИтого + НоваяЗапись.СуммаСкидки;
						СуммаПродажиНДСИтого              = СуммаПродажиНДСИтого + НоваяЗапись.СуммаНДС;
						
						Если НЕ ТоварПринятыйНаКомиссию Тогда
							СебестоимостьПартийБезНДСИтого    = СебестоимостьПартийБезНДСИтого + СебестоимостьБезНДС;
							СебестоимостьПартийБезНДСУпрИтого = СебестоимостьПартийБезНДСУпрИтого + СебестоимостьБезНДСУпр;
						КонецЕсли;
						
						СтрокаТовар.Количество  = СтрокаТовар.Количество-НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,1,-1);
						СтрокаПартий.Количество = СтрокаПартий.Количество-НоваяЗапись.Количество;
						СтрокаГТД.Количество    = СтрокаГТД.Количество-НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,1,-1);
						
						// удалим за ненадобностью
						Если СтрокаГТД.Количество = 0 Тогда
							ТаблицаГТД.Удалить(СтрокаГТД);
							СтрокаГТД = Неопределено;
						КонецЕсли;
						
						// несколько ГТД в одной партии
						Если СтрокаПартий.Количество = 0 Тогда
							Прервать;
						КонецЕсли;
						
						Если СтрокаТовар.Количество = 0 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если БезГТД ИЛИ (СтрокаТовар.Количество>0 И СтрокаПартий.Количество<>0) Тогда
						
						КоличествоПродажи = Мин(СтрокаТовар.Количество, СтрокаПартий.Количество);
						
						Сумма             = Окр(ЦенаПродажи*КоличествоПродажи,             2);
						СуммаУпр          = Окр(ЦенаПродажиУпр*КоличествоПродажи,          2);
						СуммаБезНДС       = Окр(ЦенаПродажиБезНДС*КоличествоПродажи,       2);
						СуммаБезНДСУпр    = Окр(ЦенаПродажиБезНДСУпр*КоличествоПродажи,    2);
						
						СебестоимостьУпр       = 0;
						Себестоимость          = 0;
						СебестоимостьБезНДС    = 0;
						СебестоимостьБезНДСУпр = 0;
						СуммаНДСВходящий    = 0;
						Если Комиссия Тогда
							СтруктураОтбораСебестоимостьКомиссии.Номенклатура               = СтрокаПартий.Номенклатура;
							СтруктураОтбораСебестоимостьКомиссии.ХарактеристикаНоменклатуры = СтрокаПартий.ХарактеристикаНоменклатуры;
							СтруктураОтбораСебестоимостьКомиссии.Партия                     = СтрокаПартий.Партия;
							
							Если АвтомобильВТаблице Тогда
								СтруктураОтбораСебестоимостьКомиссии.Вставить("Автомобиль", СтрокаПартий.Автомобиль);
							КонецЕсли;
							
							СчетчикКоличествоПродажи = КоличествоПродажи;
							МассивНайденныхСтрокСебестоимости = КэшСебестоимостиПриКомиссии.НайтиСтроки(СтруктураОтбораСебестоимостьКомиссии);
							Для Каждого СтрокаСебестоимости Из МассивНайденныхСтрокСебестоимости Цикл
								
								Если СтрокаСебестоимости.Количество = 0 Тогда
									Продолжить;
								КонецЕсли;
								
								Если СчетчикКоличествоПродажи = 0 Тогда
									Прервать;
								КонецЕсли;
								
								Если СтрокаСебестоимости.Количество > СчетчикКоличествоПродажи Тогда
									КоличествоДляСумм = ?(СтрокаСебестоимости.Количество=0, 1, СтрокаСебестоимости.Количество);
									ТекСебестоимость    = Окр(СтрокаСебестоимости.Сумма/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
									ТекСебестоимостьУпр = Окр(СтрокаСебестоимости.СуммаУпр/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
									ТекСуммаНДСВходящий = Окр(СтрокаСебестоимости.СуммаНДС/КоличествоДляСумм*СчетчикКоличествоПродажи, 2);
									
									СтрокаСебестоимости.Сумма       = СтрокаСебестоимости.Сумма - ТекСебестоимость;
									СтрокаСебестоимости.СуммаУпр    = СтрокаСебестоимости.СуммаУпр - ТекСебестоимостьУпр;
									СтрокаСебестоимости.СуммаНДС    = СтрокаСебестоимости.СуммаНДС - ТекСуммаНДСВходящий;
									СтрокаСебестоимости.Количество  = СтрокаСебестоимости.Количество - СчетчикКоличествоПродажи;
									
									СчетчикКоличествоПродажи = 0;
								Иначе
									ТекСебестоимость       = СтрокаСебестоимости.Сумма;
									ТекСебестоимостьУпр    = СтрокаСебестоимости.СуммаУпр;
									ТекСуммаНДСВходящий    = СтрокаСебестоимости.СуммаНДС;
									
									СчетчикКоличествоПродажи = СчетчикКоличествоПродажи - СтрокаСебестоимости.Количество;
									
									КэшСебестоимостиПриКомиссии.Удалить(СтрокаСебестоимости);
									
								КонецЕсли;
								
								Себестоимость    = Себестоимость + ТекСебестоимость;
								СебестоимостьУпр = СебестоимостьУпр + ТекСебестоимостьУпр;
								СуммаНДСВходящий = СуммаНДСВходящий + ТекСуммаНДСВходящий;
							КонецЦикла;
							
						ИначеЕсли СтрокаПартий.Количество > СтрокаТовар.Количество Тогда
							// при выборочной партии списано больше чем в строке
							КоличествоДляСумм = ?(СтрокаПартий.Количество=0, 1, СтрокаПартий.Количество);
							Себестоимость    = Окр(СтрокаПартий.Сумма/КоличествоДляСумм*КоличествоПродажи, 2);
							СебестоимостьУпр = Окр(СтрокаПартий.СуммаУпр/КоличествоДляСумм*КоличествоПродажи, 2);
							СуммаНДСВходящий = Окр(СтрокаПартий.СуммаНДС/КоличествоДляСумм*КоличествоПродажи, 2);
							
							СтрокаПартий.Сумма    = СтрокаПартий.Сумма - Себестоимость;
							СтрокаПартий.СуммаУпр = СтрокаПартий.СуммаУпр - СебестоимостьУпр;
							СтрокаПартий.СуммаНДС = СтрокаПартий.СуммаНДС - СуммаНДСВходящий;
						Иначе
							СебестоимостьУпр    = СтрокаПартий.СуммаУпр;
							Себестоимость       = СтрокаПартий.Сумма;
							СуммаНДСВходящий    = СтрокаПартий.СуммаНДС;
						КонецЕсли;
						
						СебестоимостьБезНДС    = Себестоимость - СуммаНДСВходящий;
						СебестоимостьБезНДСУпр = СебестоимостьУпр - обПересчет(СуммаНДСВходящий, ВалютаРегл, КурсРегл, ВалютаУпр, КурсУпр);
						
						// Проверим, что партия была сформирована возвратом из закрытия смены.
						ВозвратПоЗакрытиюСмены = (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗакрытиеСмены") И Ссылка = ДокументПродажи);
						
						Если ПорядокПроверкиСебестоимостиТовараПриПродаже И КонтролироватьСебестоимость
							И НЕ ТоварПринятыйНаКомиссию И СтрокаТовар.Количество > 0 И НЕ ВозвратПоЗакрытиюСмены Тогда
							Показатели = Новый Структура;
							Показатели.Вставить("СебестоимостьБезНДС",    СебестоимостьБезНДС);
							Показатели.Вставить("СебестоимостьБезНДСУпр", СебестоимостьБезНДСУпр);
							Показатели.Вставить("СуммаБезНДС",            СуммаБезНДС);
							Показатели.Вставить("СуммаБезНДСУпр",         СуммаБезНДСУпр);
							Показатели.Вставить("ВалютаРегл",             ВалютаРегл);
							Показатели.Вставить("КурсРегл",               КурсРегл);
							Показатели.Вставить("ВалютаУпр",              ВалютаУпр);
							РезультатПроверки = ПроверитьПродажуНижеСебестоимости(ТаблицаТовары, СтрокаТовар, Показатели, ТестСообщения);
							ВсеОК = ВсеОК И РезультатПроверки;
						КонецЕсли;
						
						НоваяЗапись = ТаблицаНовыхДвиженийПродаж.Добавить();
						НоваяЗапись.Период                     = Дата;
						НоваяЗапись.Регистратор                = Ссылка;
						НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
						НоваяЗапись.Номенклатура               = СтрокаПартий.Номенклатура;
						НоваяЗапись.ДокументПродажи            = ДокументПродажи;
						НоваяЗапись.Поставщик                  = КэшКонтрагент[СтрокаПартий.Партия];
						НоваяЗапись.Покупатель                 = Покупатель;
						НоваяЗапись.СтатусПартии               = ?(Комиссия, Перечисления.СтатусыПартий.ТоварКупленный, СтрокаПартий.СтатусПартии);
						НоваяЗапись.ХозОперация                = ХозОперация;
						НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
						НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаПартий.ХарактеристикаНоменклатуры;
						НоваяЗапись.СкладКомпании			   = СтрокаПартий.СкладКомпании;
						НоваяЗапись.СтавкаНДС                  = СтрокаТовар.СтавкаНДС;
						НоваяЗапись.Партия                     = СтрокаПартий.Партия;
						
						Попытка
							НоваяЗапись.Проект                 = Проект;
						Исключение
						КонецПопытки;
						НоваяЗапись.ГТД                        = Справочники.ГТД.ПустаяСсылка();
						
						// количество
						НоваяЗапись.Количество                 = КоличествоПродажи;
						
						// суммы
						НоваяЗапись.СуммаНДСВходящий                      = Окр(СуммаНДСВходящий, 2);
						НоваяЗапись.СебестоимостьУпр                      = Окр(СебестоимостьУпр, 2);
						НоваяЗапись.Себестоимость                         = Окр(Себестоимость, 2);
						НоваяЗапись.Сумма                                 = Сумма;
						НоваяЗапись.СуммаСкидки                           = Окр(ЦенаПродажиСкидки*КоличествоПродажи, 2);
						НоваяЗапись.СуммаУпр                              = СуммаУпр;
						НоваяЗапись.СуммаНДС                              = Окр(ЦенаПродажиНДС*КоличествоПродажи, 2);
						
						Если СебестоимостьРавнаПродаже Тогда
							
							СебестоимостьБезНДСВходящий = НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий;
							СтавкаНДСВходящее   =
								?(СебестоимостьБезНДСВходящий = 0,
								0,
								Окр(НоваяЗапись.СуммаНДСВходящий/СебестоимостьБезНДСВходящий, 2));
							СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
							СебестоимостьБезНДСУпр = (НоваяЗапись.СуммаУпр - обПересчет(НоваяЗапись.СуммаНДС, ВалютаРегл, КурсРегл, ВалютаУпр, КурсУпр));
							
							НоваяЗапись.Себестоимость    = Сумма;
							НоваяЗапись.СебестоимостьУпр = СуммаУпр;
							НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
							
						КонецЕсли;
						
						Если СебестоимостьРавнаПродаже ИЛИ ВозвратПоЗакрытиюСмены Тогда
							СуммаПродажиБезНДС = СуммаПродажиБезНДС - СебестоимостьБезНДС;
							СуммаПродажиБезНДСУпр = СуммаПродажиБезНДСУпр - СебестоимостьБезНДСУпр;
						КонецЕсли;
						
						СуммаПродажиИтого                 = СуммаПродажиИтого + Сумма;
						СуммаПродажиУпрИтого              = СуммаПродажиУпрИтого + СуммаУпр;
						СуммаПродажиСкидкиИтого           = СуммаПродажиСкидкиИтого + НоваяЗапись.СуммаСкидки;
						СуммаПродажиНДСИтого              = СуммаПродажиНДСИтого + НоваяЗапись.СуммаНДС;
						
						Если НЕ ТоварПринятыйНаКомиссию И НЕ ВозвратПоЗакрытиюСмены Тогда
							СебестоимостьПартийБезНДСИтого    = СебестоимостьПартийБезНДСИтого + СебестоимостьБезНДС;
							СебестоимостьПартийБезНДСУпрИтого = СебестоимостьПартийБезНДСУпрИтого + СебестоимостьБезНДСУпр;
						КонецЕсли;
						
						СтрокаТовар.Количество  = СтрокаТовар.Количество-НоваяЗапись.Количество*?(НоваяЗапись.Количество>0,1,-1);
						СтрокаПартий.Количество = СтрокаПартий.Количество-НоваяЗапись.Количество;
					КонецЕсли;
					
					Если СтрокаПартий.Количество = 0 Тогда
						ТаблицаПартий.Удалить(СтрокаПартий);
					КонецЕсли;
					
					Если СтрокаТовар.Количество = 0 Тогда
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				Если НоваяЗапись <> Неопределено Тогда
					// Записи по возврату может и не быть если возвращается товар, который не продавался.
					// Прибавим погрешность округления к последней партии.
					СуммаПродажиПогрешность = СуммаПродажи-СуммаПродажиИтого;
					Если СуммаПродажиПогрешность <> 0 Тогда
						НоваяЗапись.Сумма = НоваяЗапись.Сумма + СуммаПродажиПогрешность;
					КонецЕсли; 
					СуммаПродажиПогрешностьУпр = СуммаПродажиУпр-СуммаПродажиУпрИтого;
					Если СуммаПродажиПогрешностьУпр <> 0 Тогда
						НоваяЗапись.СуммаУпр = НоваяЗапись.СуммаУпр + СуммаПродажиПогрешностьУпр;
					КонецЕсли; 
					СуммаПродажиСкидкиПогрешность = СуммаПродажиСкидки-СуммаПродажиСкидкиИтого;
					Если СуммаПродажиСкидкиПогрешность <> 0 Тогда
						НоваяЗапись.СуммаСкидки = НоваяЗапись.СуммаСкидки + СуммаПродажиСкидкиПогрешность;
					КонецЕсли; 
					СуммаПродажиНДСПогрешность = СуммаПродажиНДС - СуммаПродажиНДСИтого;
					Если СуммаПродажиНДСПогрешность <> 0 Тогда
						НоваяЗапись.СуммаНДС = НоваяЗапись.СуммаНДС + СуммаПродажиНДСПогрешность;
					КонецЕсли; 
				КонецЕсли; 
				
			Иначе
				
				// наверное услуга, т.к. нет движений по партиям
				СебестоимостьУпр = 0;
				СуммаУпр = обПересчет(СтрокаТовар.Сумма,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсУпр);
				
				НоваяЗапись = ТаблицаНовыхДвиженийПродаж.Добавить();
				НоваяЗапись.Период      = Дата;
				НоваяЗапись.Регистратор = Ссылка;
				
				// всякие измерения и реквизиты
				НоваяЗапись.ПодразделениеКомпании=ПодразделениеКомпании;
				
				Попытка
					НоваяЗапись.Проект = Проект;
				Исключение
				КонецПопытки;
				
				Если ЭтоАвторабота Тогда
					НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура.Номенклатура;
					НоваяЗапись.Авторабота = СтрокаТовар.Номенклатура;
				Иначе
					НоваяЗапись.Номенклатура = СтрокаТовар.Номенклатура;
				КонецЕсли;
				НоваяЗапись.ДокументПродажи = ДокументПродажи;
				
				ЭтоУслуга = НоваяЗапись.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга;

				// Возврат введен без основания
				Если ДокументПродажи = Неопределено Тогда
					НоваяЗапись.ДокументПродажи = Ссылка;
					НоваяЗапись.Поставщик       = Контрагент;
				КонецЕсли;
				
				НоваяЗапись.СтавкаНДС                  = СтрокаТовар.СтавкаНДС;
				НоваяЗапись.Покупатель                 = Покупатель;
				НоваяЗапись.СтатусПартии               = Перечисления.СтатусыПартий.ТоварКупленный;
				НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
				НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;

				// склад
				Если (ЭтоКорректировкаПоЗаказНаряду И ЭтоАвторабота)
					ИЛИ (ЭтоКорректировкаПоЗаказНаряду И НЕ ЭтоУслуга) Тогда

					НоваяЗапись.СкладКомпании = ЦехОснования;
					
				ИначеЕсли НЕ (Комиссия ИЛИ ЭтоУслуга) Тогда

					Если СкладКомпании = Неопределено Тогда
						НоваяЗапись.СкладКомпании = СтрокаТовар.СкладКомпании;
					Иначе
						НоваяЗапись.СкладКомпании = СкладКомпании;
					КонецЕсли;

				КонецЕсли;
				НоваяЗапись.ХозОперация = ХозОперация;
				
				// количество
				НоваяЗапись.Количество 				 = СтрокаТовар.Количество;
				Если ЭтоАвторабота Тогда
					НоваяЗапись.КоличествоНормочасов = СтрокаТовар.КоличествоНормочасов;
				КонецЕсли;	
				// суммы
				НоваяЗапись.СебестоимостьУпр = Окр(СебестоимостьУпр,2);
				НоваяЗапись.Сумма            = Окр(обПересчет(СтрокаТовар.Сумма,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл),2);
				НоваяЗапись.СуммаСкидки      = Окр(обПересчет(СтрокаТовар.СуммаСкидки,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл),2);
				НоваяЗапись.СуммаУпр         = Окр(СуммаУпр,2);
				НоваяЗапись.СуммаНДС         = Окр(обПересчет(СтрокаТовар.СуммаНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл),2);
				
			КонецЕсли;
		
		КонецЕсли;	
		
	КонецЦикла;
	
	ТаблицаНовыхДвиженийПродаж.ЗаполнитьЗначения(Истина, "Активность");
	
	СтруктураВозврата.Вставить("ВсеОК", ВсеОК);
	СтруктураВозврата.Вставить("ТаблицаНовыхДвиженийПродаж", ТаблицаНовыхДвиженийПродаж);
	
	Возврат СтруктураВозврата;

КонецФункции

// формирует движения по регистру партий товаров с возвратом
Функция ПровестиВозвратТоваровПоПартии(Отказ,ШапкаДокумента,Режим)
	
	ЭтоКорректировкаПоЗаказНаряду = Ложь;
	ТекстСообщения = "";
	ЦехОснования = СкладКомпании;
	ВсеОК = Истина;
	
	// получим значения валют
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// составим списко документов движений
	ДокОснование = ДокументОснование;
	СписокОснований = Новый СписокЗначений;
	Пока Истина Цикл
		СписокОснований.Добавить(ДокОснование);
		Если ТипЗнч(ДокОснование) = Тип("ДокументСсылка.ЗаказНаряд") Тогда 
			ЭтоКорректировкаПоЗаказНаряду = Истина; 
			ЦехОснования = ДокОснование.Цех;
		КонецЕсли;
		Если ТипЗнч(ДокОснование) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			Прервать;
		КонецЕсли;
		ДокОснование = ДокОснование.ДокументОснование;
	КонецЦикла;
	
	ОснованияДокументов = Документы.КорректировкаРеализации.ДобавитьПеремещенияТоваровЗаказНарядаИзОснований(СписокОснований, Ложь);
	
	СтарыеДвиженияПартий = ДвиженияПартийДоКорректировки(ОснованияДокументов, СписокОснований);
	СтарыеДвиженияГТД = ДвиженияГТДДоКорректировки(ОснованияДокументов, СписокОснований);
	
	НовыеДвиженияПартий = ДвиженияПартийПослеКорректировки(, СтарыеДвиженияПартий, ТекстСообщения);
	ВсеОК = НовыеДвиженияПартий.ВсеОК И ВсеОК;
	
	НовыеДвиженияГТД = ДвиженияГТДПослеКорректировки(НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий, Ложь, СтарыеДвиженияГТД, ТекстСообщения); 
	ВсеОК = НовыеДвиженияГТД.ВсеОК И ВсеОК;
	
	Если ВсеОК Тогда 
		
		РазницаПартий = ПосчитатьРазницуДвиженийПоПартиям(СтарыеДвиженияПартий, НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий);
		РазницаГТД = ПосчитатьРазницуДвиженийПоГТД(СтарыеДвиженияГТД, НовыеДвиженияГТД.ТаблицаНовыхДвиженийГТД);
		
		Если ЗначениеЗаполнено(РазницаПартий) Тогда
			СтруткураДвиженийДляЗаписиВРегистрПартий = ДвиженияПартийПослеКорректировки(РазницаПартий, СтарыеДвиженияПартий, ТекстСообщения);
			ВсеОК = СтруткураДвиженийДляЗаписиВРегистрПартий.ВсеОК И ВсеОК;
			Если ВсеОК Тогда
				
				Для каждого ДвижениеПартии Из СтруткураДвиженийДляЗаписиВРегистрПартий.ТаблицаНовыхДвиженийПартий Цикл
					
					НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеПартии);
					НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяЗапись.Регистратор = Ссылка;
					НоваяЗапись.Период      = Дата;
					
				КонецЦикла;
				
				Движения.ПартииТоваровКомпании.Записать();
			КонецЕсли;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РазницаГТД) Тогда
			
			СтруткураДвиженийДляЗаписиВРегистрГТД = ДвиженияГТДПослеКорректировки(РазницаГТД, Истина, СтарыеДвиженияГТД, ТекстСообщения);
			ВсеОК = СтруткураДвиженийДляЗаписиВРегистрГТД.ВсеОК И ВсеОК;
			
			Если ВсеОК Тогда 
				
				Для каждого ДвижениеГТД Из СтруткураДвиженийДляЗаписиВРегистрГТД.ТаблицаНовыхДвиженийГТД Цикл
					
					НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеГТД);
					НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяЗапись.Регистратор = Ссылка;
					НоваяЗапись.Период      = Дата;
					
				КонецЦикла;
				
				Движения.ГТДПартийТоваровКомпании.Записать(); 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтарыеДвиженияПродаж = ДвиженияПродажДоКорректировки(СписокОснований);
	ДвиженияДляЗаписиПродаж = ДвиженияПродажПослеКорректировки(
		Режим,
		НовыеДвиженияПартий.ТаблицаНовыхДвиженийПартий,
		НовыеДвиженияГТД.ТаблицаНовыхДвиженийГТД,
		СтарыеДвиженияПродаж,
		ЭтоКорректировкаПоЗаказНаряду,
		ЦехОснования,
		ТекстСообщения
	);
	ВсеОК = ДвиженияДляЗаписиПродаж.ВсеОК И ВсеОК; 
	
	Если ВсеОК И ЗначениеЗаполнено(ДвиженияДляЗаписиПродаж.ТаблицаНовыхДвиженийПродаж) Тогда
		
		Для Каждого ДвижениеПродажи Из ДвиженияДляЗаписиПродаж.ТаблицаНовыхДвиженийПродаж Цикл
			
			НоваяЗапись = Движения.Продажи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДвижениеПродажи);
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.Период      = Дата;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ВсеОК Тогда
		Отказ = Истина;
		дкСообщитьРезультат(ТекстСообщения,"Важное&Партии товаров компании:", ЭтотОбъект);
		Возврат НЕ Отказ;
	КонецЕсли;
	
	/////////////////////////////////////
	// ВОЗВРАТ ДОПРОДАЖА ПО СУБПОДРЯДУ //
	/////////////////////////////////////
	
	// получим субподрядные работы
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализацииРаботы.Работа,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	КорректировкаРеализацииРаботы.ДоговорВзаиморасчетов,
	|	КорректировкаРеализацииРаботы.СуммаВсегоРазница,
	|	КорректировкаРеализацииРаботы.СуммаНДСРазница
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
	|ГДЕ
	|	КорректировкаРеализацииРаботы.Ссылка = &Ссылка
	|	И КорректировкаРеализацииРаботы.СуммаВсегоРазница <> 0
	|	И КорректировкаРеализацииРаботы.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	СубподрядДок = Запрос.Выполнить().Выгрузить();
	
	// получим движения по субподряду
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Субподряд.ПодразделениеКомпании,
	|	Субподряд.Контрагент,
	|	Субподряд.ДоговорВзаиморасчетов,
	|	Субподряд.ЗаказНаряд,
	|	Субподряд.Работа,
	|	СУММА(Субподряд.Сумма) КАК Сумма,
	|	СУММА(Субподряд.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Субподряд.СуммаУпр) КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.Субподряд КАК Субподряд
	|ГДЕ
	|	Субподряд.Регистратор В(&Регистраторы)
	|	И Субподряд.Работа В(&Работы)
	|
	|СГРУППИРОВАТЬ ПО
	|	Субподряд.Работа,
	|	Субподряд.ЗаказНаряд,
	|	Субподряд.ДоговорВзаиморасчетов,
	|	Субподряд.ПодразделениеКомпании,
	|	Субподряд.Контрагент";
	Запрос.УстановитьПараметр("Регистраторы", СписокОснований);
	Запрос.УстановитьПараметр("Работы",СубподрядДок.ВыгрузитьКолонку("Работа"));
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДок Из СубподрядДок Цикл
		МассивДвиж = ТаблицаДвижений.НайтиСтроки(Новый Структура("Контрагент,ДоговорВзаиморасчетов,Работа",СтрокаДок.Контрагент,СтрокаДок.ДоговорВзаиморасчетов,СтрокаДок.Работа));
		Если МассивДвиж.Количество() = 0 Тогда Продолжить; КонецЕсли;
		
		ТекСтрока = МассивДвиж[0];
		
		// заполним регистр
		НоваяЗапись = Движения.Субподряд.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись,ТекСтрока);
		НоваяЗапись.Период = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ХозОперация = ХозОперация;
		
		// суммовые показатели
		НоваяЗапись.Сумма    = обПересчет(СтрокаДок.СуммаВсегоРазница,ВалютаДокумента,Дата,ВалютаРегл,Дата);
		НоваяЗапись.СуммаНДС = обПересчет(СтрокаДок.СуммаНДСРазница,ВалютаДокумента,Дата,ВалютаРегл,Дата);
		НоваяЗапись.СуммаУпр = обПересчет(СтрокаДок.СуммаВсегоРазница,ВалютаДокумента,Дата,ВалютаУпр,Дата);
	КонецЦикла;
	
КонецФункции

// формирует движения по регистру партий товаров с возвратом
Функция ПровестиВозвратТоваров(Отказ,ШапкаДокумента,Режим)
	ЕстьЗН = Ложь; 
	ЦехОснования = СкладКомпании;
	
	// получим значения валют
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// составим списко документов движений
	ДокОснование = ДокументОснование;
	СписокОснований = Новый СписокЗначений;
	Пока Истина Цикл
		СписокОснований.Добавить(ДокОснование);
		Если ТипЗнч(ДокОснование) = Тип("ДокументСсылка.ЗаказНаряд") Тогда 
			ЕстьЗН = Истина; 
			ЦехОснования = ДокОснование.Цех;
		КонецЕсли;
		Если ТипЗнч(ДокОснование) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			Прервать;
		КонецЕсли;
		ДокОснование = ДокОснование.ДокументОснование;
	КонецЦикла;
	
	////////////////////////////////
	// ВОЗВРАТ ТОВАРОВ ПО ПАРТИЯМ //
	////////////////////////////////
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(-КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент) КАК Количество
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент < 0
	|	И КорректировкаРеализацииТовары.Номенклатура.ВидНоменклатуры <> &Услуга
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
	ТабДок = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.СтатусПартии КАК СтатусПартии,
	|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
	|	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма,
	|	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ПартииТоваровКомпании.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	РегистрНакопления."+?(ЕстьЗН,"ТоварыВПроизводстве","ПартииТоваровКомпании")+" КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
	|	И ПартииТоваровКомпании.Номенклатура В(&Номенклатура)
	|	И ПартииТоваровКомпании.Регистратор В(&Регистратор)
	|	И ПартииТоваровКомпании.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.СтатусПартии"
	+?(ЕстьЗН,"
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.СтатусПартии,
	|	СУММА(ПартииТоваровКомпании.Количество),
	|	СУММА(ПартииТоваровКомпании.Сумма),
	|	СУММА(ПартииТоваровКомпании.СуммаУпр),
	|	СУММА(ПартииТоваровКомпании.СуммаНДС)
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
	|	И ПартииТоваровКомпании.Номенклатура В(&Номенклатура)
	|	И ПартииТоваровКомпании.Регистратор В(&Регистратор)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.СтатусПартии,
	|	ПартииТоваровКомпании.ВидДвижения",
	"")
	+"
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатусПартии";
	Запрос.УстановитьПараметр("ВидДвижения"  , ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Номенклатура" , ТабДок.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Регистратор"  , СписокОснований);
	
	ТабДвижений = Запрос.Выполнить().Выгрузить();
	
	// получим таблицу ручных характеристик
	ТаблицаРучныхХарактеристик=дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(Ссылка);
	ВсеОК = Истина; ТекстСообщения = "";
	Для Каждого СтрокаТовар Из ТабДок Цикл
		Если СтрокаТовар.Количество = 0 Тогда Продолжить; КонецЕсли;
		СтруктураОтбора=Новый Структура("Номенклатура");
		СтруктураОтбора.Номенклатура=СтрокаТовар.Номенклатура; 
		Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ (ТаблицаРучныхХарактеристик<>Неопределено И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда 
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
		КонецЕсли;

		МассивНайденныхСтрок=ТабДвижений.НайтиСтроки(СтруктураОтбора);
		НадоВернуть = СтрокаТовар.Количество;
		Для Сч = 0 По МассивНайденныхСтрок.ВГраница() Цикл
			Если НадоВернуть = 0 Тогда Прервать; КонецЕсли;
			ТекСтрока = МассивНайденныхСтрок[Сч];
			КоличествоВозвращаем = Мин(НадоВернуть, ТекСтрока.Количество);
			
			Если КоличествоВозвращаем =  ТекСтрока.Количество Тогда
				СуммаВозвращаем      = Окр(ТекСтрока.Сумма,2);
				СуммаВозвращаемУпр   = Окр(ТекСтрока.СуммаУпр,2);
				СуммаВозвращаемНДС   = Окр(ТекСтрока.СуммаНДС,2);
			Иначе
				СуммаВозвращаем      = Окр(?(ТекСтрока.Количество = 0, 0,ТекСтрока.Сумма/ТекСтрока.Количество*КоличествоВозвращаем),2);
				СуммаВозвращаемУпр   = Окр(?(ТекСтрока.Количество = 0, 0,ТекСтрока.СуммаУпр/ТекСтрока.Количество*КоличествоВозвращаем),2);
				СуммаВозвращаемНДС   = Окр(?(ТекСтрока.Количество = 0, 0,ТекСтрока.СуммаНДС/ТекСтрока.Количество*КоличествоВозвращаем),2);
			КонецЕсли;
			
			НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.Период      = Дата;
			// измерения
			НоваяЗапись.СкладКомпании              = СкладКомпании;
			НоваяЗапись.Номенклатура               = СтрокаТовар.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
			НоваяЗапись.СтатусПартии               = ТекСтрока.СтатусПартии;
			НоваяЗапись.Партия                     = ТекСтрока.Партия;
			
			// ресурсы
			НоваяЗапись.Количество = -КоличествоВозвращаем;
			НоваяЗапись.Сумма      = -СуммаВозвращаем;
			НоваяЗапись.СуммаУпр   = -СуммаВозвращаемУпр;
			НоваяЗапись.СуммаНДС   = -СуммаВозвращаемНДС;
			
			// ресурсы
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.Проект      = Проект;
			
			// уменьшим остатки
			НадоВернуть = НадоВернуть - КоличествоВозвращаем;
			Если КоличествоВозвращаем >= ТекСтрока.Количество Тогда
				ТабДвижений.Удалить(ТекСтрока);
			Иначе
				ТекСтрока.Количество = ТекСтрока.Количество - КоличествоВозвращаем;
				ТекСтрока.Сумма      = ТекСтрока.Сумма - СуммаВозвращаем;
				ТекСтрока.СуммаУпр   = ТекСтрока.СуммаУпр - СуммаВозвращаемУпр;
				ТекСтрока.СуммаНДС   = ТекСтрока.СуммаНДС - СуммаВозвращаемНДС;
			КонецЕсли;
		КонецЦикла;
		
		// не возвращаем не проданный товар
		Если НадоВернуть > 0 Тогда
			ВсеОК = Ложь;
			ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+СтрокаТовар.Номенклатура+"> не был реализован в количестве " + СтрокаТовар.Количество;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ВсеОК Тогда
		Отказ = Истина;
		дкСообщитьРезультат(ТекстСообщения,"Важное&Партии товаров компании:", ЭтотОбъект, СтрокаТовар.Номенклатура);
		Возврат НЕ Отказ;
	КонецЕсли;
	
	Движения.ПартииТоваровКомпании.Записать();
	
	
	////////////////////////////////////////////////
	// СПИСШЕМ ГТД ПО РЕГИСТРУ ГТД ПАРТИИ ТОВАРОВ //
	////////////////////////////////////////////////
	
	// получим таблицу партий
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Партия,
	|	-ПартииТоваровКомпании.Количество КАК Количество
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Регистратор
	|	И ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
	|	И ПартииТоваровКомпании.Количество < 0";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	РезЗапроса = Запрос.Выполнить();
	
	// Получим список списанных ГТД
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГТДПартийТоваровКомпании.Номенклатура,
	|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ГТДПартийТоваровКомпании.ГТД,
	|	ГТДПартийТоваровКомпании.Партия,
	|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК Количество,
	|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК КоличествоОсталось
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
	|ГДЕ
	|	ГТДПартийТоваровКомпании.Регистратор В (&СписокОснований)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ГТДПартийТоваровКомпании.ГТД,
	|	ГТДПартийТоваровКомпании.Номенклатура,
	|	ГТДПартийТоваровКомпании.Партия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.ГТД,
	|	ТоварыВПроизводстве.Партия,
	|	СУММА(ТоварыВПроизводстве.Количество) КАК Количество,
	|	СУММА(ТоварыВПроизводстве.Количество) КАК КоличествоОсталось
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор В (&СписокОснований)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.Партия,
	|	ТоварыВПроизводстве.ГТД
	|";
	Запрос.УстановитьПараметр("СписокОснований",СписокОснований);
	РезультатЗапросаПоГТД = Запрос.Выполнить().Выгрузить();

	Если НЕ РезЗапроса.Пустой() Тогда
		Выборка = РезЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НадоВернуть = Выборка.Количество;
			Если НадоВернуть = 0 Тогда Продолжить; КонецЕсли;
			
			// получм массив ГТД
			МассивГТД = РезультатЗапросаПоГТД.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Партия",Выборка.Номенклатура,Выборка.ХарактеристикаНоменклатуры,Выборка.Партия));
			Для Каждого стрГТД Из МассивГТД Цикл
				Если стрГТД.ГТД = справочники.ГТД.ПустаяСсылка() Тогда Продолжить; КонецЕсли;
				Если  НадоВернуть = 0 ИЛИ стрГТД.Количество = 0 Тогда Продолжить; КонецЕсли;
				КолвоСписываем = Мин(НадоВернуть,стрГТД.Количество);
				
				НоваяЗапись               = Движения.ГТДПартийТоваровКомпании.Добавить();
				НоваяЗапись.Период        = Дата;
				НоваяЗапись.ВидДвижения   = ВидДвиженияНакопления.Расход;
				НоваяЗапись.СкладКомпании = СкладКомпании;
				НоваяЗапись.Номенклатура  = стрГТД.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = стрГТД.ХарактеристикаНоменклатуры;
				НоваяЗапись.Партия = стрГТД.Партия;
				НоваяЗапись.ГТД = стрГТД.ГТД;
				НоваяЗапись.Количество = -КолвоСписываем;
				
				НадоВернуть = НадоВернуть - КолвоСписываем;
				стрГТД.Количество = стрГТД.Количество - КолвоСписываем;
				
				Если НадоВернуть = 0 Тогда Прервать; КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	/////////////////////////////////////
	// ВОЗВРАТ ДОПРОДАЖА ПО СУБПОДРЯДУ //
	/////////////////////////////////////
	
	// получим субподрядные работы
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализацииРаботы.Работа,
	|	КорректировкаРеализацииРаботы.Контрагент,
	|	КорректировкаРеализацииРаботы.ДоговорВзаиморасчетов,
	|	КорректировкаРеализацииРаботы.СуммаВсегоРазница,
	|	КорректировкаРеализацииРаботы.СуммаНДСРазница
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
	|ГДЕ
	|	КорректировкаРеализацииРаботы.Ссылка = &Ссылка
	|	И КорректировкаРеализацииРаботы.СуммаВсегоРазница <> 0
	|	И КорректировкаРеализацииРаботы.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	СубподрядДок = Запрос.Выполнить().Выгрузить();
	
	// получим движения по субподряду
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Субподряд.ПодразделениеКомпании,
	|	Субподряд.Контрагент,
	|	Субподряд.ДоговорВзаиморасчетов,
	|	Субподряд.ЗаказНаряд,
	|	Субподряд.Работа,
	|	СУММА(Субподряд.Сумма) КАК Сумма,
	|	СУММА(Субподряд.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Субподряд.СуммаУпр) КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.Субподряд КАК Субподряд
	|ГДЕ
	|	Субподряд.Регистратор В(&Регистраторы)
	|	И Субподряд.Работа В(&Работы)
	|
	|СГРУППИРОВАТЬ ПО
	|	Субподряд.Работа,
	|	Субподряд.ЗаказНаряд,
	|	Субподряд.ДоговорВзаиморасчетов,
	|	Субподряд.ПодразделениеКомпании,
	|	Субподряд.Контрагент";
	Запрос.УстановитьПараметр("Регистраторы", СписокОснований);
	Запрос.УстановитьПараметр("Работы",СубподрядДок.ВыгрузитьКолонку("Работа"));
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДок Из СубподрядДок Цикл
		МассивДвиж = ТаблицаДвижений.НайтиСтроки(Новый Структура("Контрагент,ДоговорВзаиморасчетов,Работа",СтрокаДок.Контрагент,СтрокаДок.ДоговорВзаиморасчетов,СтрокаДок.Работа));
		Если МассивДвиж.Количество() = 0 Тогда Продолжить; КонецЕсли;
		
		ТекСтрока = МассивДвиж[0];
		
		// заполним регистр
		НоваяЗапись = Движения.Субподряд.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись,ТекСтрока);
		НоваяЗапись.Период = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ХозОперация = ХозОперация;
		
		// суммовые показатели
		НоваяЗапись.Сумма    = обПересчет(СтрокаДок.СуммаВсегоРазница,ВалютаДокумента,Дата,ВалютаРегл,Дата);
		НоваяЗапись.СуммаНДС = обПересчет(СтрокаДок.СуммаНДСРазница,ВалютаДокумента,Дата,ВалютаРегл,Дата);
		НоваяЗапись.СуммаУпр = обПересчет(СтрокаДок.СуммаВсегоРазница,ВалютаДокумента,Дата,ВалютаУпр,Дата);
	КонецЦикла;
	
	
	/////////////////////
	// РЕГИСТР ПРОДАЖИ //
	/////////////////////
	
	// получим валюты
	врТаблицаГТД = Движения.ГТДПартийТоваровКомпании.Выгрузить();
	ТаблицаГТД = врТаблицаГТД.Скопировать();
	
	// таблица партий
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.СкладКомпании,
	|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
	|	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма,
	|	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ПартииТоваровКомпании.СуммаНДС) КАК СуммаНДС,
	|	ПартииТоваровКомпании.СтавкаНДС,
	|	ПартииТоваровКомпании.СтатусПартии
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Регистратор
	|	И ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.СкладКомпании,
	|	ПартииТоваровКомпании.СтавкаНДС,
	|	ПартииТоваровКомпании.СтатусПартии";
	
	Запрос.УстановитьПараметр("Регистратор"  , Ссылка);
	Запрос.УстановитьПараметр("ВидДвижения"  , ВидДвиженияНакопления.Расход);
	ТаблПартий = Запрос.Выполнить().Выгрузить();
	
	// таблиа документа
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	СУММА(КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент) КАК Количество,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоРазница) КАК СуммаВсего,
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего) КАК СуммаВсегоДляЦены,
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница) КАК СуммаНДСРазница,
	|	СУММА(КорректировкаРеализацииТовары.Количество) КАК КоличествоДляЦены,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииТовары.КоличествоРазница * КорректировкаРеализацииТовары.Коэффициент <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Работа,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(КорректировкаРеализацииТовары.КоличествоРазница),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсегоРазница),
	|	СУММА(КорректировкаРеализацииТовары.СуммаВсего),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДС),
	|	СУММА(КорректировкаРеализацииТовары.СуммаНДСРазница),
	|	СУММА(КорректировкаРеализацииТовары.Количество),
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Контрагент
	|ИЗ
	|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И (КорректировкаРеализацииТовары.КоличествоРазница <> 0
	|			ИЛИ КорректировкаРеализацииТовары.СуммаВсегоРазница <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.Работа,
	|	КорректировкаРеализацииТовары.СтавкаНДС,
	|	КорректировкаРеализацииТовары.Контрагент";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	ТабДок = Запрос.Выполнить().Выгрузить();
	
	
	// продажи партии
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Продажи.ПодразделениеКомпании,
	|	Продажи.Номенклатура,
	|	Продажи.Поставщик,
	|	Продажи.Покупатель,
	|	Продажи.СтатусПартии,
	|	Продажи.ДокументПродажи,
	|	Продажи.ДоговорВзаиморасчетов,
	|	Продажи.ХарактеристикаНоменклатуры,
	|	Продажи.СкладКомпании,
	|	Продажи.СтавкаНДС,
	|	Продажи.Партия,
	|	Продажи.Авторабота,
	|	Продажи.ГТД,
	|	Продажи.Проект,
	|	СУММА(Продажи.Количество) КАК Количество,
	|	СУММА(Продажи.Сумма) КАК Сумма,
	|	СУММА(Продажи.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Продажи.СуммаСкидки) КАК СуммаСкидки,
	|	СУММА(Продажи.СуммаУпр) КАК СуммаУпр,
	|	СУММА(Продажи.СебестоимостьУпр) КАК СебестоимостьУпр,
	|	СУММА(Продажи.Себестоимость) КАК Себестоимость,
	|	СУММА(Продажи.СуммаНДСВходящий) КАК СуммаНДСВходящий
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Регистратор В(&Регистратор)
	|	И (Продажи.Номенклатура В(&Номенклатура) ИЛИ Продажи.Авторабота В(&Номенклатура))
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Проект,
	|	Продажи.ПодразделениеКомпании,
	|	Продажи.Номенклатура,
	|	Продажи.Поставщик,
	|	Продажи.Покупатель,
	|	Продажи.ГТД,
	|	Продажи.СтатусПартии,
	|	Продажи.ДокументПродажи,
	|	Продажи.ДоговорВзаиморасчетов,
	|	Продажи.ХарактеристикаНоменклатуры,
	|	Продажи.СкладКомпании,
	|	Продажи.СтавкаНДС,
	|	Продажи.Партия,
	|	Продажи.Авторабота";
	Запрос.УстановитьПараметр("Регистратор"  , СписокОснований);
	Запрос.УстановитьПараметр("Номенклатура" , ТабДок.ВыгрузитьКолонку("Номенклатура"));
	ТаблПродаж = Запрос.Выполнить().Выгрузить();
	ТекстСообщения = "";
	Для Каждого стрДок Из ТабДок Цикл
		СуммаРазница          = обПересчет(стрДок.СуммаВсего,ВалютаДокумента,Дата,ВалютаРегл,Дата);
		СуммаРазницаНДС       = обПересчет(стрДок.СуммаНДСРазница,ВалютаДокумента,Дата,ВалютаРегл,Дата);
		НадоСписатьКоличество = стрДок.Количество;
		СуммаНаЕд             = Окр(?(стрДок.КоличествоДляЦены=0,0,обПересчет(стрДок.СуммаВсегоДляЦены,ВалютаДокумента,Дата,ВалютаРегл,Дата)/стрДок.КоличествоДляЦены),2);
		СуммаНДСНаЕд          = Окр(?(стрДок.КоличествоДляЦены=0,0,обПересчет(стрДок.СуммаНДС,ВалютаДокумента,Дата,ВалютаРегл,Дата)/стрДок.КоличествоДляЦены),2);
		ЭтоРабота             = ТипЗнч(стрДок.Номенклатура) <> Тип("СправочникСсылка.Номенклатура");
		
		Если стрДок.Количество > 0 Тогда // увелечение количества(до продажа)
			стрПоиск      = Новый Структура("Номенклатура",стрДок.Номенклатура);
			Если НЕ обЗначениеНеЗаполнено(стрДок.ХарактеристикаНоменклатуры) Тогда
				стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрДок.ХарактеристикаНоменклатуры);
			КонецЕсли;
			
			МассивСтрокПартий = ТаблПартий.НайтиСтроки(стрПоиск);
			// если услуга
			Если МассивСтрокПартий.Количество() = 0 Тогда
				НоваяЗапись = Движения.Продажи.Добавить();
				НоваяЗапись.Период                     = Дата;
				НоваяЗапись.Регистратор                = Ссылка;
				НоваяЗапись.ХозОперация                = ХозОперация;
				НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
				НоваяЗапись.Покупатель                 = Контрагент;
				НоваяЗапись.ДокументПродажи            = Сделка;
				НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
				НоваяЗапись.СкладКомпании              = ?(ЭтоРабота,ЦехОснования,СкладКомпании);
				НоваяЗапись.Номенклатура               = ?(ЭтоРабота,Справочники.Номенклатура.Авторабота,стрДок.Номенклатура);
				НоваяЗапись.Поставщик                  = стрДок.Контрагент;
				Если ЭтоРабота Тогда
					НоваяЗапись.Авторабота             = стрДок.Номенклатура;
				Иначе
					НоваяЗапись.ХарактеристикаНоменклатуры = стрДок.ХарактеристикаНоменклатуры;
				КонецЕсли;
				НоваяЗапись.СтатусПартии               = Перечисления.СтатусыПартий.ТоварКупленный;
				НоваяЗапись.СтавкаНДС                  = стрДок.СтавкаНДС;
				НоваяЗапись.Количество                 = НадоСписатьКоличество;
				
				
				НоваяЗапись.Сумма    = НадоСписатьКоличество*СуммаНаЕд;
				НоваяЗапись.СуммаНДС = НадоСписатьКоличество*СуммаНДСНаЕд;
				НоваяЗапись.СуммаУпр = обПересчет(НадоСписатьКоличество*СуммаНаЕд,ВалютаРегл,Дата,ВалютаУпр,Дата);
				
				СуммаРазница          = СуммаРазница - НадоСписатьКоличество*СуммаНаЕд;
				СуммаРазницаНДС       = СуммаРазницаНДС - НадоСписатьКоличество*СуммаНДСНаЕд;
			Иначе // если товар
				Для Каждого стрМассива Из МассивСтрокПартий Цикл
					// получим список ГТД для партии
					стрПоиск      = Новый Структура("Номенклатура",стрМассива.Номенклатура);
					Если НЕ обЗначениеНеЗаполнено(стрМассива.ХарактеристикаНоменклатуры) Тогда
						стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрМассива.ХарактеристикаНоменклатуры);
					КонецЕсли;
					
					СебестоимостьРавнаПродаже = Ложь;
					Попытка
						Если стрМассива.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия И (НЕ стрМассива.Партия.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж) Тогда
							СебестоимостьРавнаПродаже = Истина;
						КонецЕсли;
					Исключение
						СебестоимостьРавнаПродаже = Ложь;
					КонецПопытки;
					
					стрПоиск.Вставить("Партия",стрМассива.Партия);
					МассивГТД = ТаблицаГТД.НайтиСтроки(стрПоиск);
					Если МассивГТД.Количество() > 0 Тогда
						НадоОприходовать = стрМассива.Количество;
						Для Каждого стрГТД Из МассивГТД Цикл
							Если НадоОприходовать = 0 Тогда Прервать; КонецЕсли;
							Если стрГТД.Количество = 0 Тогда Продолжить; КонецЕсли;
							КоличествоСписываем = Мин(стрГТД.Количество,НадоОприходовать);
							
							НоваяЗапись = Движения.Продажи.Добавить();
							НоваяЗапись.Период                     = Дата;
							НоваяЗапись.Регистратор                = Ссылка;
							НоваяЗапись.ХозОперация                = ХозОперация;
							НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
							НоваяЗапись.Покупатель                 = Контрагент;
							НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
							НоваяЗапись.ДокументПродажи            = Сделка;
							НоваяЗапись.Номенклатура               = стрМассива.Номенклатура;
							НоваяЗапись.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
							НоваяЗапись.СтатусПартии               = стрМассива.СтатусПартии;
							НоваяЗапись.ГТД                        = стрГТД.ГТД;
							НоваяЗапись.СкладКомпании              = СкладКомпании;
							НоваяЗапись.Партия                     = стрМассива.Партия;
							НоваяЗапись.СтавкаНДС                  = стрДок.СтавкаНДС;
							Попытка
								НоваяЗапись.Поставщик              = стрМассива.Партия.Контрагент;
							Исключение
							КонецПопытки;
							НоваяЗапись.Количество                 = КоличествоСписываем;
							
							НоваяЗапись.СебестоимостьУпр = ?(стрМассива.Количество=НоваяЗапись.Количество, стрМассива.СуммаУпр, стрМассива.СуммаУпр/стрМассива.Количество*НоваяЗапись.Количество);
							НоваяЗапись.Себестоимость    = ?(стрМассива.Количество=НоваяЗапись.Количество, стрМассива.Сумма,стрМассива.Сумма/стрМассива.Количество*НоваяЗапись.Количество);
							НоваяЗапись.СуммаНДСВходящий = ?(стрМассива.Количество=НоваяЗапись.Количество, стрМассива.СуммаНДС,стрМассива.СуммаНДС/стрМассива.Количество*НоваяЗапись.Количество);
							НоваяЗапись.Сумма            = КоличествоСписываем*СуммаНаЕд;
							НоваяЗапись.СуммаНДС         = КоличествоСписываем*СуммаНДСНаЕд;
							НоваяЗапись.СуммаУпр         = обПересчет(КоличествоСписываем*СуммаНаЕд,ВалютаРегл,Дата,ВалютаУпр,Дата);
							
							СуммаРазница    = СуммаРазница - КоличествоСписываем*СуммаНаЕд;
							СуммаРазницаНДС = СуммаРазницаНДС - КоличествоСписываем*СуммаНДСНаЕд;
							стрМассива.СуммаУпр = стрМассива.СуммаУпр - НоваяЗапись.СебестоимостьУпр;
							стрМассива.Сумма    = стрМассива.Сумма    - НоваяЗапись.Себестоимость;
							стрМассива.СуммаНДС = стрМассива.СуммаНДС - НоваяЗапись.СуммаНДСВходящий;
							НадоОприходовать  = НадоОприходовать - КоличествоСписываем;
							стрГТД.Количество = стрГТД.Количество - КоличествоСписываем;
							
							Если СебестоимостьРавнаПродаже Тогда
								СтавкаНДСВходящее   = Окр(НоваяЗапись.СуммаНДСВходящий/(НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий), 2);
								СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
								
								НоваяЗапись.Себестоимость    = НоваяЗапись.Сумма;
								НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СуммаУпр;
								НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
							КонецЕсли;
							
						КонецЦикла;
						
						Если НадоОприходовать > 0 Тогда
							КоличествоСписываем = НадоОприходовать;
							НоваяЗапись = Движения.Продажи.Добавить();
							НоваяЗапись.Период                     = Дата;
							НоваяЗапись.Регистратор                = Ссылка;
							НоваяЗапись.ХозОперация                = ХозОперация;
							НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
							НоваяЗапись.Покупатель                 = Контрагент;
							НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
							НоваяЗапись.ДокументПродажи            = Сделка;
							НоваяЗапись.Номенклатура               = стрМассива.Номенклатура;
							НоваяЗапись.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
							НоваяЗапись.СтатусПартии               = стрМассива.СтатусПартии;
							НоваяЗапись.СкладКомпании              = СкладКомпании;
							НоваяЗапись.Партия                     = стрМассива.Партия;
							НоваяЗапись.СтавкаНДС                  = стрДок.СтавкаНДС;
							Попытка
								НоваяЗапись.Поставщик              = стрМассива.Партия.Контрагент;
							Исключение
							КонецПопытки;
							НоваяЗапись.Количество                 = КоличествоСписываем;
							
							НоваяЗапись.СебестоимостьУпр = стрМассива.СуммаУпр;
							НоваяЗапись.Себестоимость    = стрМассива.Сумма;
							НоваяЗапись.СуммаНДСВходящий = стрМассива.СуммаНДС;
							НоваяЗапись.Сумма            = КоличествоСписываем*СуммаНаЕд;
							НоваяЗапись.СуммаНДС         = КоличествоСписываем*СуммаНДСНаЕд;
							НоваяЗапись.СуммаУпр         = обПересчет(КоличествоСписываем*СуммаНаЕд,ВалютаРегл,Дата,ВалютаУпр,Дата);
							
							Если СебестоимостьРавнаПродаже Тогда
								СтавкаНДСВходящее   = Окр(НоваяЗапись.СуммаНДСВходящий/(НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий), 2);
								СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
								
								НоваяЗапись.Себестоимость    = НоваяЗапись.Сумма;
								НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СуммаУпр;
								НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
							КонецЕсли;
							
							СуммаРазница    = СуммаРазница - КоличествоСписываем*СуммаНаЕд;
							СуммаРазницаНДС = СуммаРазницаНДС - КоличествоСписываем*СуммаНДСНаЕд;
						КонецЕсли;
					Иначе
						КоличествоСписываем = стрМассива.Количество;
						НоваяЗапись = Движения.Продажи.Добавить();
						НоваяЗапись.Период                     = Дата;
						НоваяЗапись.Регистратор                = Ссылка;
						НоваяЗапись.ХозОперация                = ХозОперация;
						НоваяЗапись.ПодразделениеКомпании      = ПодразделениеКомпании;
						НоваяЗапись.Покупатель                 = Контрагент;
						НоваяЗапись.ДоговорВзаиморасчетов      = ДоговорВзаиморасчетов;
						НоваяЗапись.ДокументПродажи            = Сделка;
						НоваяЗапись.Номенклатура               = стрМассива.Номенклатура;
						НоваяЗапись.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
						НоваяЗапись.СтатусПартии               = стрМассива.СтатусПартии;
						НоваяЗапись.СкладКомпании              = СкладКомпании;
						НоваяЗапись.Партия                     = стрМассива.Партия;
						НоваяЗапись.СтавкаНДС                  = стрДок.СтавкаНДС;
						Попытка
							НоваяЗапись.Поставщик              = стрМассива.Партия.Контрагент;
						Исключение
						КонецПопытки;
						НоваяЗапись.Количество                 = КоличествоСписываем;
						
						НоваяЗапись.СебестоимостьУпр = стрМассива.СуммаУпр;
						НоваяЗапись.Себестоимость    = стрМассива.Сумма;
						НоваяЗапись.СуммаНДСВходящий = стрМассива.СуммаНДС;
						НоваяЗапись.Сумма            = КоличествоСписываем*СуммаНаЕд;
						НоваяЗапись.СуммаНДС         = КоличествоСписываем*СуммаНДСНаЕд;
						НоваяЗапись.СуммаУпр         = обПересчет(КоличествоСписываем*СуммаНаЕд,ВалютаРегл,Дата,ВалютаУпр,Дата);
						
						Если СебестоимостьРавнаПродаже Тогда
							СтавкаНДСВходящее   = Окр(НоваяЗапись.СуммаНДСВходящий/(НоваяЗапись.Себестоимость-НоваяЗапись.СуммаНДСВходящий), 2);
							СебестоимостьБезНДС = (НоваяЗапись.Сумма-НоваяЗапись.СуммаНДС);
							
							НоваяЗапись.Себестоимость    = НоваяЗапись.Сумма;
							НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СуммаУпр;
							НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
						КонецЕсли;
						
						СуммаРазница    = СуммаРазница - КоличествоСписываем*СуммаНаЕд;
						СуммаРазницаНДС = СуммаРазницаНДС - КоличествоСписываем*СуммаНДСНаЕд;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе // возврат товаров
			стрПоиск      = Новый Структура("Номенклатура",стрДок.Номенклатура);
			Если НЕ обЗначениеНеЗаполнено(стрДок.ХарактеристикаНоменклатуры) Тогда
				стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрДок.ХарактеристикаНоменклатуры);
			КонецЕсли;
			
			МассивСтрокПартий = ТаблПартий.НайтиСтроки(стрПоиск);
			Если МассивСтрокПартий.Количество() = 0 Тогда
				НадоСписатьКоличество = -НадоСписатьКоличество;
				Если ЭтоРабота Тогда
					стрПоиск      = Новый Структура("Авторабота",стрДок.Номенклатура);
				Иначе
					стрПоиск      = Новый Структура("Номенклатура",стрДок.Номенклатура);
					Если НЕ обЗначениеНеЗаполнено(стрДок.ХарактеристикаНоменклатуры) Тогда
						стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрДок.ХарактеристикаНоменклатуры);
					КонецЕсли;
				КонецЕсли;
				МассивСтрокПродаж = ТаблПродаж.НайтиСтроки(стрПоиск);
				Для Каждого стрПродаж Из МассивСтрокПродаж Цикл
					Если НадоСписатьКоличество = 0 Тогда Прервать; КонецЕсли;
					
					КоличествоСписываем = Мин(НадоСписатьКоличество,стрПродаж.Количество);
					Если КоличествоСписываем = стрПродаж.Количество Тогда
						СуммаСписываем    = -стрПродаж.Сумма;
						СуммаНДССписываем = -стрПродаж.СуммаНДС;
						СуммаУпрСписываем = -стрПродаж.СуммаУпр;
						
						СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр;
						СебестоимостьСписываем    = -стрПродаж.Себестоимость;
						СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий;
					Иначе
						СуммаСписываем    = -стрПродаж.Сумма/стрПродаж.Количество*КоличествоСписываем;
						СуммаНДССписываем = -стрПродаж.СуммаНДС/стрПродаж.Количество*КоличествоСписываем;
						СуммаУпрСписываем = -стрПродаж.СуммаУпр/стрПродаж.Количество*КоличествоСписываем;
						
						СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр/стрПродаж.Количество*КоличествоСписываем;
						СебестоимостьСписываем    = -стрПродаж.Себестоимость/стрПродаж.Количество*КоличествоСписываем;
						СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий/стрПродаж.Количество*КоличествоСписываем;
					КонецЕсли;
					
					
					НоваяЗапись = Движения.Продажи.Добавить();
					НоваяЗапись.Период                     = Дата;
					НоваяЗапись.Регистратор                = Ссылка;
					НоваяЗапись.ХозОперация                = ХозОперация;
					НоваяЗапись.ПодразделениеКомпании      = стрПродаж.ПодразделениеКомпании;
					НоваяЗапись.Покупатель                 = стрПродаж.Покупатель;
					НоваяЗапись.ДоговорВзаиморасчетов      = стрПродаж.ДоговорВзаиморасчетов;
					НоваяЗапись.ДокументПродажи            = стрПродаж.ДокументПродажи;
					НоваяЗапись.Номенклатура               = ?(ЭтоРабота,Справочники.Номенклатура.Авторабота,стрПродаж.Номенклатура);
					Если ЭтоРабота Тогда
						НоваяЗапись.Авторабота             = стрПродаж.Авторабота;
					Иначе
						НоваяЗапись.ХарактеристикаНоменклатуры = стрПродаж.ХарактеристикаНоменклатуры;
					КонецЕсли;
					НоваяЗапись.СтатусПартии               = стрПродаж.СтатусПартии;
					НоваяЗапись.СкладКомпании              = стрПродаж.СкладКомпании;
					НоваяЗапись.Партия                     = стрПродаж.Партия;
					НоваяЗапись.ГТД                        = стрПродаж.ГТД;
					НоваяЗапись.Поставщик                  = стрПродаж.Поставщик;
					НоваяЗапись.СтавкаНДС                  = стрПродаж.СтавкаНДС;
					НоваяЗапись.Количество                 = -КоличествоСписываем;
					
					НоваяЗапись.СебестоимостьУпр = СебестоимостьУпрСписываем;
					НоваяЗапись.Себестоимость    = СебестоимостьСписываем;
					НоваяЗапись.СуммаНДСВходящий = СуммаНДСВходящийСписываем;
					НоваяЗапись.Сумма            = СуммаСписываем;
					НоваяЗапись.СуммаНДС         = СуммаНДССписываем;
					НоваяЗапись.СуммаУпр         = СуммаУпрСписываем;
					
					НадоСписатьКоличество = НадоСписатьКоличество - КоличествоСписываем;
					СуммаРазница          = СуммаРазница - СуммаСписываем;
					СуммаРазницаНДС       = СуммаРазницаНДС - СуммаНДССписываем;
					
					стрПродаж.Количество = стрПродаж.Количество - КоличествоСписываем;
					Если стрПродаж.Количество = 0 Тогда
						ТаблПродаж.Удалить(стрПродаж);
					КонецЕсли;
				КонецЦикла;
			Иначе
				Для Каждого стрМассива Из МассивСтрокПартий Цикл
					НадоСписатьКоличество = -стрМассива.Количество;
					стрПоиск      = Новый Структура("Номенклатура,Партия",стрМассива.Номенклатура,стрМассива.Партия);
					Если НЕ обЗначениеНеЗаполнено(стрМассива.ХарактеристикаНоменклатуры) Тогда
						стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрМассива.ХарактеристикаНоменклатуры);
					КонецЕсли;
					// списание с учетом ГТД
					// получим список ГТД для партии
					стрПоискГТД      = Новый Структура("Номенклатура",стрМассива.Номенклатура);
					Если НЕ обЗначениеНеЗаполнено(стрМассива.ХарактеристикаНоменклатуры) Тогда
						стрПоискГТД.Вставить("ХарактеристикаНоменклатуры",стрМассива.ХарактеристикаНоменклатуры);
					КонецЕсли;
					стрПоискГТД.Вставить("Партия",стрМассива.Партия);
					МассивГТД = ТаблицаГТД.НайтиСтроки(стрПоискГТД);
					
					Если МассивГТД.Количество() > 0  Тогда
						Для Каждого стрГТД Из МассивГТД Цикл
							Если НадоСписатьКоличество = 0 Тогда Прервать; КонецЕсли;
							стрПоиск.Вставить("ГТД",стрГТД.ГТД);
							МассивСтрокПродаж = ТаблПродаж.НайтиСтроки(стрПоиск);
							Для Каждого стрПродаж Из МассивСтрокПродаж Цикл
								Если НадоСписатьКоличество = 0 Тогда Прервать; КонецЕсли;
								
								КоличествоСписываем = Мин(НадоСписатьКоличество,стрПродаж.Количество);
								Если КоличествоСписываем = стрПродаж.Количество Тогда
									СуммаСписываем    = -стрПродаж.Сумма;
									СуммаНДССписываем = -стрПродаж.СуммаНДС;
									СуммаУпрСписываем = -стрПродаж.СуммаУпр;
									
									СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр;
									СебестоимостьСписываем    = -стрПродаж.Себестоимость;
									СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий;
								Иначе
									СуммаСписываем    = -стрПродаж.Сумма/стрПродаж.Количество*КоличествоСписываем;
									СуммаНДССписываем = -стрПродаж.СуммаНДС/стрПродаж.Количество*КоличествоСписываем;
									СуммаУпрСписываем = -стрПродаж.СуммаУпр/стрПродаж.Количество*КоличествоСписываем;
									
									СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр/стрПродаж.Количество*КоличествоСписываем;
									СебестоимостьСписываем    = -стрПродаж.Себестоимость/стрПродаж.Количество*КоличествоСписываем;
									СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий/стрПродаж.Количество*КоличествоСписываем;
								КонецЕсли;
								
								НоваяЗапись = Движения.Продажи.Добавить();
								НоваяЗапись.Период                     = Дата;
								НоваяЗапись.Регистратор                = Ссылка;
								НоваяЗапись.ХозОперация                = ХозОперация;
								НоваяЗапись.ПодразделениеКомпании      = стрПродаж.ПодразделениеКомпании;
								НоваяЗапись.Покупатель                 = стрПродаж.Покупатель;
								НоваяЗапись.ДоговорВзаиморасчетов      = стрПродаж.ДоговорВзаиморасчетов;
								НоваяЗапись.Номенклатура               = стрПродаж.Номенклатура;
								НоваяЗапись.ХарактеристикаНоменклатуры = стрПродаж.ХарактеристикаНоменклатуры;
								НоваяЗапись.СтатусПартии               = стрПродаж.СтатусПартии;
								НоваяЗапись.ДокументПродажи            = стрПродаж.ДокументПродажи;
								НоваяЗапись.СкладКомпании              = стрПродаж.СкладКомпании;
								НоваяЗапись.Партия                     = стрПродаж.Партия;
								НоваяЗапись.ГТД                        = стрПродаж.ГТД;
								НоваяЗапись.Поставщик                  = стрПродаж.Поставщик;
								НоваяЗапись.СтавкаНДС                  = стрПродаж.СтавкаНДС;
								НоваяЗапись.Количество                 = -КоличествоСписываем;
								
								НоваяЗапись.СебестоимостьУпр = СебестоимостьУпрСписываем;
								НоваяЗапись.Себестоимость    = СебестоимостьСписываем;
								НоваяЗапись.СуммаНДСВходящий = СуммаНДСВходящийСписываем;
								НоваяЗапись.Сумма            = СуммаСписываем;
								НоваяЗапись.СуммаНДС         = СуммаНДССписываем;
								НоваяЗапись.СуммаУпр         = СуммаУпрСписываем;
								
								НадоСписатьКоличество = НадоСписатьКоличество - КоличествоСписываем;
								СуммаРазница          = СуммаРазница - СуммаСписываем;
								СуммаРазницаНДС       = СуммаРазницаНДС - СуммаНДССписываем;
								
								стрПродаж.Количество = стрПродаж.Количество - КоличествоСписываем;
								Если стрПродаж.Количество = 0 Тогда
									ТаблПродаж.Удалить(стрПродаж);
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
					
					// списание без учета ГТД
					стрПоиск      = Новый Структура("Номенклатура,Партия",стрМассива.Номенклатура,стрМассива.Партия);
					Если НЕ обЗначениеНеЗаполнено(стрМассива.ХарактеристикаНоменклатуры) Тогда
						стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрМассива.ХарактеристикаНоменклатуры);
					КонецЕсли;
					МассивСтрокПродаж = ТаблПродаж.НайтиСтроки(стрПоиск);
					Для Каждого стрПродаж Из МассивСтрокПродаж Цикл
						Если НадоСписатьКоличество = 0 Тогда Прервать; КонецЕсли;
						
						КоличествоСписываем = Мин(НадоСписатьКоличество,стрПродаж.Количество);
						Если КоличествоСписываем = стрПродаж.Количество Тогда
							СуммаСписываем    = -стрПродаж.Сумма;
							СуммаНДССписываем = -стрПродаж.СуммаНДС;
							СуммаУпрСписываем = -стрПродаж.СуммаУпр;
							
							СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр;
							СебестоимостьСписываем    = -стрПродаж.Себестоимость;
							СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий;
						Иначе
							СуммаСписываем    = -стрПродаж.Сумма/стрПродаж.Количество*КоличествоСписываем;
							СуммаНДССписываем = -стрПродаж.СуммаНДС/стрПродаж.Количество*КоличествоСписываем;
							СуммаУпрСписываем = -стрПродаж.СуммаУпр/стрПродаж.Количество*КоличествоСписываем;
							
							СебестоимостьУпрСписываем = -стрПродаж.СебестоимостьУпр/стрПродаж.Количество*КоличествоСписываем;
							СебестоимостьСписываем    = -стрПродаж.Себестоимость/стрПродаж.Количество*КоличествоСписываем;
							СуммаНДСВходящийСписываем = -стрПродаж.СуммаНДСВходящий/стрПродаж.Количество*КоличествоСписываем;
						КонецЕсли;
						
						НоваяЗапись = Движения.Продажи.Добавить();
						НоваяЗапись.Период                     = Дата;
						НоваяЗапись.Регистратор                = Ссылка;
						НоваяЗапись.ХозОперация                = ХозОперация;
						НоваяЗапись.ПодразделениеКомпании      = стрПродаж.ПодразделениеКомпании;
						НоваяЗапись.Покупатель                 = стрПродаж.Покупатель;
						НоваяЗапись.ДоговорВзаиморасчетов      = стрПродаж.ДоговорВзаиморасчетов;
						НоваяЗапись.Номенклатура               = стрПродаж.Номенклатура;
						НоваяЗапись.ХарактеристикаНоменклатуры = стрПродаж.ХарактеристикаНоменклатуры;
						НоваяЗапись.СтатусПартии               = стрПродаж.СтатусПартии;
						НоваяЗапись.ДокументПродажи            = стрПродаж.ДокументПродажи;
						НоваяЗапись.СкладКомпании              = стрПродаж.СкладКомпании;
						НоваяЗапись.Партия                     = стрПродаж.Партия;
						НоваяЗапись.ГТД                        = стрПродаж.ГТД;
						НоваяЗапись.Поставщик                  = стрПродаж.Поставщик;
						НоваяЗапись.СтавкаНДС                  = стрПродаж.СтавкаНДС;
						НоваяЗапись.Количество                 = -КоличествоСписываем;
						
						НоваяЗапись.СебестоимостьУпр = СебестоимостьУпрСписываем;
						НоваяЗапись.Себестоимость    = СебестоимостьСписываем;
						НоваяЗапись.СуммаНДСВходящий = СуммаНДСВходящийСписываем;
						НоваяЗапись.Сумма            = СуммаСписываем;
						НоваяЗапись.СуммаНДС         = СуммаНДССписываем;
						НоваяЗапись.СуммаУпр         = СуммаУпрСписываем;
						
						НадоСписатьКоличество = НадоСписатьКоличество - КоличествоСписываем;
						СуммаРазница          = СуммаРазница - СуммаСписываем;
						СуммаРазницаНДС       = СуммаРазницаНДС - СуммаНДССписываем;
						
						стрПродаж.Количество = стрПродаж.Количество - КоличествоСписываем;
						Если стрПродаж.Количество = 0 Тогда
							ТаблПродаж.Удалить(стрПродаж);
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
			Если НадоСписатьКоличество <> 0 Тогда
				ВсеОК = Ложь;
				ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения),"",Символы.ПС) + "Товар <"+стрДок.Номенклатура+"> не был реализован в количестве " + Строка(-стрДок.Количество);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ВсеОК Тогда
			Отказ = Истина;
			дкСообщитьРезультат(ТекстСообщения,"Важное&Партии товаров компании:", ЭтотОбъект, стрДок.Номенклатура);
			Возврат НЕ Отказ;
		КонецЕсли;
		
		Если Окр(СуммаРазница,2) <> 0 ИЛИ Окр(СуммаРазницаНДС,2) <> 0 Тогда
			// получим движения по продажам
			
			Если ЭтоРабота Тогда
				стрПоиск      = Новый Структура("Авторабота",стрДок.Номенклатура);
			Иначе
				стрПоиск      = Новый Структура("Номенклатура",стрДок.Номенклатура);
				Если НЕ обЗначениеНеЗаполнено(стрДок.ХарактеристикаНоменклатуры) Тогда
					стрПоиск.Вставить("ХарактеристикаНоменклатуры",стрДок.ХарактеристикаНоменклатуры);
				КонецЕсли;
			КонецЕсли;
			МассивСтрок = ТаблПродаж.НайтиСтроки(стрПоиск);
			
			Если МассивСтрок.Количество() > 0 Тогда
				// получим количество всего
				КоличествоВсего = 0;
				Для Каждого стрМассива Из МассивСтрок Цикл
					КоличествоВсего = КоличествоВсего + стрМассива.Количество;
				КонецЦикла;
				
				Для Каждого стрМассива Из МассивСтрок Цикл
					Если КоличествоВсего = 0 ИЛИ (СуммаРазница = 0 И СуммаРазницаНДС = 0) Тогда Прервать; КонецЕсли;
					
					КолСписываем = Мин(КоличествоВсего,стрМассива.Количество);
					СуммаСпис    = Окр(?(КолСписываем = КоличествоВсего, СуммаРазница, СуммаРазница/КоличествоВсего*КолСписываем),2);
					СуммаСписНДС = Окр(?(КолСписываем = КоличествоВсего, СуммаРазницаНДС, СуммаРазницаНДС/КоличествоВсего*КолСписываем),2);
					
					Если СуммаСписНДС <> 0 ИЛИ СуммаСпис <> 0 Тогда
						// добавим запись
						НоваяЗапись = Движения.Продажи.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, стрМассива);
						НоваяЗапись.Регистратор = Ссылка;
						НоваяЗапись.Период      = Дата;
						НоваяЗапись.ХозОперация = ХозОперация;
						НоваяЗапись.Количество  = 0;
						
						НоваяЗапись.СебестоимостьУпр = 0;
						НоваяЗапись.Себестоимость    = 0;
						НоваяЗапись.СуммаНДСВходящий = 0;
						НоваяЗапись.СуммаСкидки      = 0;
						НоваяЗапись.Сумма            = СуммаСпис;
						НоваяЗапись.СуммаНДС         = СуммаСписНДС;
						НоваяЗапись.СуммаУпр         = обПересчет(СуммаСпис,ВалютаРегл,Дата,ВалютаУпр,Дата);
					КонецЕсли;
					
					КоличествоВсего = КоличествоВсего - КолСписываем;
					СуммаРазница    = СуммаРазница - СуммаСпис;
					СуммаРазницаНДС = СуммаРазницаНДС - СуммаСписНДС;
				КонецЦикла;
			Иначе
				НоваяЗапись.Сумма            = НоваяЗапись.Сумма + Окр(СуммаРазница,2);
				НоваяЗапись.СуммаНДС         = НоваяЗапись.СуммаНДС + Окр(СуммаРазницаНДС,2);
				НоваяЗапись.СуммаУпр         = НоваяЗапись.СуммаУпр + обПересчет(СуммаРазница,ВалютаРегл,Дата,ВалютаУпр,Дата);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа.
//	Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если ДопПараметры=Неопределено Тогда
		ДопПараметры=Новый Структура;
	КонецЕсли;
	Если ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
		ДопПараметры.Вставить("НеРассчитыватьСкидки",Истина);
	КонецЕсли; 
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаРабот Из Работы Цикл
				НоваяЦена=обПересчет(СтрокаРабот.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаРабот.Цена Тогда
					СтрокаРабот.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Работы.Цена",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Организация может является плательщиком НДС, а может и нет.. надо обработать таблицу работ
		ТаблицаРабот=ЭтотОбъект.Работы;
		ОсвобожденОтНДС=ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаРабот Из ТаблицаРабот Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаРабот.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаРабот.СтавкаНДС=СтрокаРабот.Работа.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			Рез=ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		Возврат Рез;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Подразделение может является плательщиком НДС, а может и нет.. надо обработать таблицу работ
		ТаблицаРабот=ЭтотОбъект.Работы;
		ОсвобожденОтНДС=ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаРабот Из ТаблицаРабот Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаРабот.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаРабот.СтавкаНДС=СтрокаРабот.Работа.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			Рез=ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		Возврат Рез;
		
	ИначеЕсли Имя="ДокументОснование" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			Если ДокументОснование = Ссылка Тогда
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Сообщить("Ввод корректировки реализации на основании самой себя запрещен.",СтатусСообщения.Внимание);
				Возврат Истина;
			КонецЕсли;
			
			Если КорректировкаНеДоступна(ДокументОснование) Тогда
				Сообщить("На основании " + ДокументОснование + " введен возврат товаров. Ввод корректировки реализации невозможен.",СтатусСообщения.Внимание);
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
			
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") И (ДокументОснование.ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный) Тогда
				Сообщить("В заказ-наряде выбран бесплатный ремонт. Нельзя заполнить корректировку реализации!",СтатусСообщения.Важное);
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
			
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") И (ДокументОснование.Состояние <> Справочники.ВидыСостоянийЗаказНарядов.Закрыт) Тогда
				Сообщить("Заказ наряд не находится в состоянии ""Закрыт"". Нельзя заполнить корректировку реализации!",СтатусСообщения.Важное);
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
			
			Если НЕ ДокументОснование.Проведен Тогда
				Сообщить("Ввод корректировки реализации возможен только на основании проведенного документа.",СтатусСообщения.Внимание);
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
			
			Если ДокументОснование.ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
				Сообщить("Ввод корректировки реализации невозможен на основании реализации товаров комиссия.",СтатусСообщения.Внимание);
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КорректировкаРеализации.Ссылка КАК Документ
		|ИЗ
		|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|ГДЕ
		|	КорректировкаРеализации.ДокументОснование = &ДокументОснование
		|	И КорректировкаРеализации.Ссылка <> &Ссылка";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ДокументОснование",ДокументОснование);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выгрузить();
		
		Если Выборка.Количество() > 0 Тогда
			Сообщить("На основании документа """ + ДокументОснование + """ уже введен """ + Выборка[0].Документ + """.",СтатусСообщения.Внимание);
			ДокументОснование=Неопределено;
			ОбработкаРеквизита("ДокументОснование");
			Возврат Истина;
		КонецЕсли;
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда 
			Если Вопрос("Заполнить по документу-основания?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
				ЭтотОбъект.Заполнить(ДокументОснование);
				ЭтаФорма.УправлениеДиалогом();
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Сделка=Документы.КорректировкаРеализации.ПолучитьСделку(ДокументОснование);
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	
	ИначеЕсли Имя="Товары.Подтверждение" Тогда
		Если ТекСтрока.Подтверждение Тогда
			ТекСтрока.Количество=(ТекСтрока.КоличествоПоДокументуРеализации*ТекСтрока.КоэффициентПоДокументуРеализации)/ТекСтрока.Коэффициент;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаВсего=ТекСтрока.СуммаВсегоПоДокументуРеализации;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			ТекСтрока.Количество=0;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Рез=ОбработкаРеквизита("Товары.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда
			Возврат Рез;
		Иначе
			ТекСтрока.Подтверждение = Истина;
		КонецЕсли;
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	ИначеЕсли Имя="Товары.РасчетРазницы" Тогда
		Рез=Истина;
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда
			Возврат Рез;
		КонецЕсли;
		Если ТекСтрока.Коэффициент = 0 Тогда
			ТекСтрока.КоличествоРазница=0;
		Иначе
			ТекСтрока.КоличествоРазница=(ТекСтрока.Количество)-((ТекСтрока.КоличествоПоДокументуРеализации*ТекСтрока.КоэффициентПоДокументуРеализации)/ТекСтрока.Коэффициент);
		КонецЕсли;
		ТекСтрока.СуммаНДСРазница=ТекСтрока.СуммаНДС-ТекСтрока.СуммаНДСПоДокументуРеализации;
		ТекСтрока.СуммаВсегоРазница=ТекСтрока.СуммаВсего-ТекСтрока.СуммаВсегоПоДокументуРеализации;
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "РАБОТЫ"
	
	ИначеЕсли Имя="Работы.Подтверждение" Тогда
		Если ТекСтрока.Подтверждение Тогда
			ТекСтрока.Количество=ТекСтрока.КоличествоПоДокументуРеализации;
			ТекСтрока.Коэффициент=ТекСтрока.КоэффициентПоДокументуРеализации;
			Рез=ОбработкаРеквизита("Работы.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаВсего=ТекСтрока.СуммаВсегоПоДокументуРеализации;
			Рез=ОбработкаРеквизита("Работы.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			ТекСтрока.Количество=0;
			Рез=ОбработкаРеквизита("Работы.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Рез=ОбработкаРеквизита("Работы.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
	
	ИначеЕсли Имя="Работы.РасчетРазницы" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ТекСтрока.КоличествоРазница=ТекСтрока.Количество-ТекСтрока.КоличествоПоДокументуРеализации;
		ТекСтрока.КоэффициентРазница=ТекСтрока.Коэффициент-ТекСтрока.КоэффициентПоДокументуРеализации;
		ТекСтрока.СуммаНДСРазница=ТекСтрока.СуммаНДС-ТекСтрока.СуммаНДСПоДокументуРеализации;
		ТекСтрока.СуммаВсегоРазница=ТекСтрока.СуммаВсего-ТекСтрока.СуммаВсегоПоДокументуРеализации;
		Возврат Истина;
	
	ИначеЕсли Имя="Работы.Работа" Тогда
		//Проверим на повторение строки в таблице
		//Получим из формы последнюю текущую работу
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1; КонецЕсли;
		
		//Рассчитаем цену текущей работы
		ОбработкаРеквизита("РасчетЦеныРаботы",ТекСтрока,ЭтаФорма,ДопПараметры);
		//Заполнение ставок налогов
		Если ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС Тогда
			ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
		Иначе
			ТекСтрока.СтавкаНДС=ТекСтрока.Работа.Номенклатура.СтавкаНДС;
		КонецЕсли; 
		//Расчет сумм по строке
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Количество" Тогда
		Возврат ОбработкаРеквизита("Работы.Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Нормочас" Тогда
		ТекСтрока.Цена=обПересчет(ТекСтрока.Нормочас.Цена,ТекСтрока.Нормочас.Валюта,Дата,ВалютаДокумента,КурсДокумента);
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Коэффициент" Тогда
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат ОбработкаРеквизита("Работы.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
	ИначеЕсли Имя="Работы.Сумма" Тогда
		Если ТекСтрока.Количество*ТекСтрока.Коэффициент <> 0 Тогда
			ТекСтрока.Цена=ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент);
		КонецЕсли;
		
		// расчет суммы всего и суммы ндс
		ЦенаВключаетНДС = ТипЦенРабот.ЦенаВключаетНДС;
		ТекСтрока.СуммаНДС   = ТекСтрока.Сумма/(100+?(ЦенаВключаетНДС,ТекСтрока.СтавкаНДС.Ставка,0))*ТекСтрока.СтавкаНДС.Ставка;
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаВсего = ТекСтрока.Сумма;
		Иначе
			ТекСтрока.СуммаВсего = ТекСтрока.Сумма + ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;
	ИначеЕсли Имя="Работы.ПроцентСкидки" Тогда
		Рез=Истина;
		Возврат Рез;
		
	ИначеЕсли Имя="Работы.СуммаСкидки" Тогда
		Попытка	// Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцентСкидки=ДопПараметры.ПересчитыватьПроцентСкидки;
		Исключение 
			ПересчитыватьПроцентСкидки=Истина;	
		КонецПопытки;
		Если ПересчитыватьПроцентСкидки Тогда
			Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
			ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
			ТекСтрока.ПроцентСкидки=?(ТекСтрока_СуммаБезСкидки=0,0,Окр(ТекСтрока.СуммаСкидки*100/ТекСтрока_СуммаБезСкидки,2));
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиРабот=Работы.Итог("СуммаСкидки");
		СуммаСкидкиНаценкиРабот=СуммаСкидкиНаценкиРабот+Работы.Итог("СуммаСкидкиСтроки");
		// Ничего делать не будем - весь расчет от НДС зависит (включено или нет)
		Возврат ОбработкаРеквизита("Работы.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.СуммаВсего" Тогда
		//Получим новую сумму скидки как процент от суммы всего (уже со скидкой)
		СуммаБезСкидки = ТекСтрока.СуммаВсего;
		//Получим новую сумму НДС как процент от суммы всего
		СуммаБезСкидкиБезНДС=Окр((100*СуммаБезСкидки)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		СуммаБезНДС=Окр((100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		СуммаНДСБезСкидки=СуммаБезСкидки-СуммаБезСкидкиБезНДС;
		ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Если ЭтотОбъект.ТипЦенРабот.ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			//Получим сумму как разницу суммы всего и суммы скидки
			ТекСтрока.Сумма=СуммаБезСкидки;
		Иначе
			//НДС в цену не включен
			//Получим сумму как разницу суммы всего и суммы скидки и суммы НДС
			ТекСтрока.Сумма=СуммаБезСкидки-СуммаНДСБезСкидки;
		КонецЕсли;
		ТекСтрока.Цена=?(ТекСтрока.Количество*ТекСтрока.Коэффициент=0,0,ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент));
		Возврат Истина;
		
	ИначеЕсли Имя="Работы.СтавкаНДС" Тогда
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
		СуммаСоСкидкой=дкПолучитьСуммуСтрокиСоСкидкой(ЭтотОбъект,ТекСтрока,ДопПараметры);
		СтавкаНДС=ТекСтрока.СтавкаНДС.Ставка;
		ТекСтрока.СуммаНДС=Окр((СуммаСоСкидкой*СтавкаНДС)/(100+СтавкаНДС),2);
		ТекСтрока.СуммаВсего=СуммаСоСкидкой;
		Возврат Истина;
		
	ИначеЕсли Имя="Работы.СуммаНДС" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="РасчетЦеныРаботы" Тогда
		//Расчет цены работы
		Если обЗначениеНеЗаполнено(ТекСтрока.Работа) Тогда Возврат Ложь; КонецЕсли;
		//Если работа задана - получим ее цену согласно класса автомобиля (если автомобиль выбран)
		Если обЗначениеНеЗаполнено(Автомобиль) Тогда
			ВариантКомплектации = Справочники.Модели.ПустаяСсылка();
		Иначе
			Если ЗначениеЗаполнено(Автомобиль.ВариантКомплектации) Тогда
				ВариантКомплектации = Автомобиль.ВариантКомплектации;
			Иначе
				ВариантКомплектации = Автомобиль.Модель;
			КонецЕсли;
		КонецЕсли;
		ЦенаРаботы = орПолучитьЦенуАвтоработы(ТипЦенРабот, ТекСтрока.Работа, ВариантКомплектации, ДоговорВзаиморасчетов, Справочники.Цеха.ПустаяСсылка(), Справочники.ВидыРемонта.ПустаяСсылка(),, ВалютаДокумента, КурсДокумента);
		ТекСтрока.Нормочас=ЦенаРаботы.Нормочас;
		ТекСтрока.Коэффициент=?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени),ТекСтрока.Коэффициент,ЦенаРаботы.НормаВремени);
		ТекСтрока.Цена=ЦенаРаботы.Цена;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//Зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

Функция ТоварыДляУКД(Корректировка)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
		|	КорректировкаРеализацииТовары.Количество КАК Количество,
		|	КорректировкаРеализацииТовары.КоличествоРазница КАК КоличествоРазница,
		|	КорректировкаРеализацииТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	КорректировкаРеализацииТовары.Коэффициент КАК Коэффициент,
		|	0 КАК КоэффициентРазница,
		|	КорректировкаРеализацииТовары.Цена КАК Цена,
		|	КорректировкаРеализацииТовары.Сумма КАК Сумма,
		|	КорректировкаРеализацииТовары.СтавкаНДС КАК СтавкаНДС,
		|	КорректировкаРеализацииТовары.СуммаНДС КАК СуммаНДС,
		|	КорректировкаРеализацииТовары.СуммаНДСРазница КАК СуммаНДСРазница,
		|	КорректировкаРеализацииТовары.СуммаВсего КАК СуммаВсего,
		|	КорректировкаРеализацииТовары.СуммаВсегоРазница КАК СуммаВсегоРазница,
		|	КорректировкаРеализацииТовары.Номенклатура.Артикул КАК ТоварКод,
		|	0 КАК НомерСтроки
		|ИЗ
		|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
		|ГДЕ
		|	КорректировкаРеализацииТовары.Ссылка = &Корректировка
		|	И (КорректировкаРеализацииТовары.Количество <> КорректировкаРеализацииТовары.КоличествоПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииТовары.ЕдиницаИзмерения <> КорректировкаРеализацииТовары.ЕдиницаИзмеренияПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииТовары.Коэффициент <> КорректировкаРеализацииТовары.КоэффициентПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииТовары.СтавкаНДС <> КорректировкаРеализацииТовары.СтавкаНДСПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииТовары.СуммаНДС <> КорректировкаРеализацииТовары.СуммаНДСПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииТовары.СуммаВсего <> КорректировкаРеализацииТовары.СуммаВсегоПоДокументуРеализации)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КорректировкаРеализацииРаботы.Работа,
		|	КорректировкаРеализацииРаботы.Количество,
		|	КорректировкаРеализацииРаботы.КоличествоРазница,
		|	ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка),
		|	КорректировкаРеализацииРаботы.Коэффициент,
		|	КорректировкаРеализацииРаботы.КоэффициентРазница,
		|	КорректировкаРеализацииРаботы.Цена,
		|	КорректировкаРеализацииРаботы.Сумма,
		|	КорректировкаРеализацииРаботы.СтавкаНДС,
		|	КорректировкаРеализацииРаботы.СуммаНДС,
		|	КорректировкаРеализацииРаботы.СуммаНДСРазница,
		|	КорректировкаРеализацииРаботы.СуммаВсего,
		|	КорректировкаРеализацииРаботы.СуммаВсегоРазница,
		|	КорректировкаРеализацииРаботы.Работа.Артикул,
		|	0
		|ИЗ
		|	Документ.КорректировкаРеализации.Работы КАК КорректировкаРеализацииРаботы
		|ГДЕ
		|	КорректировкаРеализацииРаботы.Ссылка = &Корректировка
		|	И (КорректировкаРеализацииРаботы.Количество <> КорректировкаРеализацииРаботы.КоличествоПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииРаботы.Коэффициент <> КорректировкаРеализацииРаботы.КоэффициентПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииРаботы.СтавкаНДС <> КорректировкаРеализацииРаботы.СтавкаНДСПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииРаботы.СуммаНДС <> КорректировкаРеализацииРаботы.СуммаНДСПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииРаботы.СуммаВсего <> КорректировкаРеализацииРаботы.СуммаВсегоПоДокументуРеализации)"
	);
	Запрос.УстановитьПараметр("Корректировка", Корректировка);
	Возврат ЗаполнитьНомераСтрокВТаблице(Запрос.Выполнить().Выгрузить());
	
КонецФункции

Функция ЗаполнитьНомераСтрокВТаблице(Знач Таблица)
	
	НомераСтрок = Новый Массив();
	НомерСтроки = 1;
	
	Пока НомерСтроки <= Таблица.Количество() Цикл
		
		НомераСтрок.Добавить(НомерСтроки);
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	Таблица.ЗагрузитьКолонку(НомераСтрок, "НомерСтроки");
	
	Возврат Таблица;
	
КонецФункции

Функция ХозОперацияУказанаНеВерно()
	
	Если ДополнительныеСвойства.Свойство("ГрупповаяОбработкаДокументов")
		И ДополнительныеСвойства.ГрупповаяОбработкаДокументов Тогда
		Возврат Ложь;
	КонецЕсли;

	КонтролироватьВидИсправительногоДокумента = обПраво("КонтролироватьВидИсправительногоДокумента", Права);
	Если Не КонтролироватьВидИсправительногоДокумента Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьИзмененияСуммовыхПоказателей = ЕстьИзмененияСуммовыхПоказателей();
	
	Если
		ЕстьИзмененияСуммовыхПоказателей
		И ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииИсправлениеВПервичныхДокументах
	Тогда
		
		Сообщить(
			НСтр("ru = 'В документе изменены суммовые показатели. Следует использовать хоз. операцию <Корректировка по согласованию сторон>.'")
		);
		Возврат Истина;
		
	ИначеЕсли
		Не ЕстьИзмененияСуммовыхПоказателей
		И ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииКорректировкаПоСогласованиюСторон
	Тогда 
		
		Сообщить(
			НСтр("ru = 'В документе нет изменений суммовых показателей. Следует использовать хоз. операцию <Исправление в первичных документах>.'")
		);
		Возврат Истина;
	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ЕстьИзмененияСуммовыхПоказателей()
	
	Для Каждого Товар Из Товары Цикл
		
		Если
			Товар.КоличествоРазница <> 0
			Или Товар.СуммаВсегоРазница <> 0
			Или Товар.СуммаНДСРазница <> 0
		Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Работа Из Работы Цикл
		
		Если
			Работа.КоличествоРазница <> 0
			Или Работа.КоэффициентРазница <> 0
			Или Работа.СуммаНДСРазница <> 0
			Или Работа.СуммаВсегоРазница <> 0
		Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат Ложь;
	
КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,0);
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Сделка",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	
	// Уберем обязательность заполнения склада для корректировок с реализацией агентских услуг
	Если Документы.КорректировкаРеализации.НеобходимоИспользоватьСклад(ЭтотОбъект) Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
	//Иначе
	КонецЕсли;
	ОбязательныеТовары.Вставить("ГТД",2);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Если ТипЗнч(Сделка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ОбязательныеРаботы=Новый Структура();
		ОбязательныеРаботы.Вставить("Работа",3);
		ОбязательныеРаботы.Вставить("Нормочас",1);
		ОбязательныеРеквизиты.Вставить("Работы",ОбязательныеРаботы);
	КонецЕсли;
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности договора и склада документа. 
	// Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("КорректировкаРеализацииИсправлениеВПервичныхДокументах","Исправление в первичных документах",Истина);
	СписокХозОпераций.Добавить("КорректировкаРеализацииКорректировкаПоСогласованиюСторон","Корректировка по согласованию сторон",Ложь);
	Возврат СписокХозОпераций;
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРасходнаяНаклодная(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РеализацияТоваров");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	Если ПустаяСтрока(ФорматВыводаСуммы) Тогда
		ФорматВыводаСуммы = ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00";
	КонецЕсли;
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
	Если ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг Тогда
		ТекстЗаголовка = ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy");
	Иначе
		ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	КонецЕсли; 
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
    //                        
    Если ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг Тогда
        ОбластьШапкаТаблицы.Параметры.Товар = "Услуга";
    Иначе
        ОбластьШапкаТаблицы.Параметры.Товар = "Товар";
    КонецЕсли;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	Если ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг Тогда
		ОбластьПодвал = Макет.ПолучитьОбласть("ПодвалАкта");
	Иначе
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	КонецЕсли;	
	
	//перебор строк
	ВыборкаТабличнойЧасти=ЭтотОбъект.Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаПечатногоДокумента);
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		Если НЕ СтрокаТабличнойЧасти.Количество > 0 Тогда Продолжить; КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРеализацияТоваров()

// Формирует печатную форму "ТОРГ-12"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.ПодразделениеКомпании, СтруктураПредставления2, ДополнительныеПараметры);
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикОрганизация",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = ""; 
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	
	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		

	ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 

	ВыборкаСтрок = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();

	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	КолвоИтогБаз = 0;
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		Если НЕ СтрокаТоваров.Количество > 0 Тогда Продолжить; КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,,ЭтотОбъект);
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.ТоварНаименование 			= СтруктураСтроки.ТоварНаименование + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "");

		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС    = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", Формат(СуммаНДС, ФорматВыводаСуммы));
		ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
		
		// Рассчитаем цену.
		ОбластьСтрока.Параметры.ЦенаБезНДС = Формат(СуммаБезНДС / (СтрокаТоваров.Количество * ?(СтрокаТоваров.Коэффициент = 0, 1, СтрокаТоваров.Коэффициент)), ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьВсего);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		Если КоличествоСтрокНаТекущейСтранице=0 Тогда
			ТабДокумент.Вывести(ОбластьСтрока);
		Иначе
			//выводим строку, делая проверку попадания на лист		
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		КонецЕсли; 
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		КолвоИтогБаз = КолвоИтогБаз + СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			КоличествоСтрокНаТекущейСтранице=0;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Иначе
			КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 1 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = Формат(КолвоИтогБаз,   ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма,     ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС,       ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьТОРГ12()

// Формирует печатную форму "УПД"
// Возвращает сформированный табличный документ:
Функция ПечатьУПД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
	Возврат ТабДокумент;
	
КонецФункции //ПечатьУПД()

// Формирует печатную форму "УКД"
// Возвращает сформированный табличный документ:
Функция ПечатьУКД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУКД(ЭтотОбъект, ТабДокумент);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьУКД

// Функция получения данных для УКД.
//
Функция ПолучитьДанныеДляПечатиУКД() Экспорт
	
	ДокументФактура = Документы.СчетФактураВыданный.НайтиПоРеквизиту("ДокументОснование", ЭтотОбъект.Ссылка);
	ДокументОбъект = ДокументФактура;
	ДанныеОбъекта = Новый Структура();
	
	// Сформируем статус документа
	Если ДокументФактура <> Документы.СчетФактураВыданный.ПустаяСсылка() И НЕ ДокументФактура.ПометкаУдаления Тогда
		Статус = 1;
		ДокументОбъект = ДокументФактура;
	Иначе
		Статус = 2;
		ДокументОбъект = ЭтотОбъект;
	КонецЕсли;
	
	Если Дата < Дата('20210701') Тогда
		ТаблицаТоваров = ТоварыДляУКД(ЭтотОбъект.Ссылка);
	Иначе
		ТаблицаТоваров = Документы.СчетФактураВыданный.ПолучитьДанныеДляЗаполненияПФКорректировки(ЭтотОбъект);
	КонецЕсли;
	
	// данные документа
	ДанныеОбъекта.Вставить("Дата"                   	, ДокументОбъект.Дата);
	ДанныеОбъекта.Вставить("Номер"                  	, дкПолучитьНомерДляПечати(ДокументОбъект));
	ДанныеОбъекта.Вставить("ХозОперация"            	, ДокументОбъект.ХозОперация);
	ДанныеОбъекта.Вставить("ДокументОснование"      	, ДокументОбъект.ДокументОснование);
	ДанныеОбъекта.Вставить("ВалютаДокумента"      		, ЭтотОбъект.ВалютаДокумента);
	ДанныеОбъекта.Вставить("Поставщик"              	, ДокументОбъект.Организация);
	ДанныеОбъекта.Вставить("ПодразделениеКомпании"  	, ДокументОбъект.ПодразделениеКомпании);
	ДанныеОбъекта.Вставить("Покупатель"              	, ДокументОбъект.Контрагент);
	ДанныеОбъекта.Вставить("Организация"              	, ЭтотОбъект.Организация);
	ДанныеОбъекта.Вставить("ДоговорВзаиморасчетов"     	, ?(ЗначениеЗаполнено(ДокументОбъект.ДоговорВзаиморасчетов), ЭтотОбъект.ДоговорВзаиморасчетов, "--"));
	ДанныеОбъекта.Вставить("Товары"              		, ТаблицаТоваров);
	ДанныеОбъекта.Вставить("Статус"              		, Статус);
	ДанныеОбъекта.Вставить("Ссылка"              		, ДокументОбъект.Ссылка);
	ДанныеОбъекта.Вставить("ИдентификаторГосударственногоКонтракта", ЭтотОбъект.ИдентификаторГосударственногоКонтракта);
	ДанныеОбъекта.Вставить("ДатаОтгрузки", Неопределено);
	
	Если Статус = 1 Тогда
		ДанныеОбъекта.Вставить("НомерИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.НомерИсходногоДокумента),
				дкПолучитьНомерДляПечати(ДокументОбъект,,"НомерИсходногоДокумента"),
				"--"));
		ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.ДатаИсходногоДокумента), ДокументОбъект.ДатаИсходногоДокумента, "--"));
		ДанныеОбъекта.Вставить("ПлатежноРасчетныеДокументы" , ДокументОбъект.ПлатежноРасчетныеДокументы);
	КонецЕсли;
	
	// Свойства
	ДанныеОбъекта.Вставить("Руководитель"     		, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.Руководитель));
	ДанныеОбъекта.Вставить("ГлавныйБухгалтер" 		, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер));
	ДанныеОбъекта.Вставить("Менеджер" 				, дкОтветственноеЛицо(ДокументОбъект, "Менеджер"));
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события объекта возникает в момент, когда выполняется установка нового номера.
//
// Параметры:
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//  Префикс              - Строка - Префикс, который будет использоваться для генерации номера.
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)

	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(ВерсияОбъекта) Тогда
		ВерсияОбъекта = ТекущаяВерсияОбъекта;
	КонецЕсли;

	Если НЕ ВозможенВводНаОсновании(Основание) Тогда
		ДополнительныеСвойства.Вставить("ВводНаОснованииЗапрещен", Истина);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Основание<>Неопределено Тогда
		Если Основание <> ДокументОснование ТОгда
			Пока Истина Цикл
				// проверим введенные на основании корректировки
				Корректировка     = Неопределено;
				МассивВидов       = Новый Массив(1); 
				МассивВидов[0]    = "КорректировкаРеализации";
				МассивПодчиненных = дкПолучитьМассивПодчиненных(Основание, МассивВидов, Истина);
				
				Если ЗначениеЗаполнено(МассивПодчиненных) И МассивПодчиненных.Количество()>0 Тогда
					Основание = МассивПодчиненных[0].Ссылка;
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Основание = Ссылка Тогда
			Сообщить("Ввод корректировки реализации на основании самой себя запрещен.",СтатусСообщения.Внимание);
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		Если НЕ Основание.Проведен Тогда
			Сообщить("Ввод корректировки реализации возможен только на основании проведенного документа.",СтатусСообщения.Внимание);
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		Если Основание.ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
			Сообщить("Ввод корректировки реализации не возможен на основании реализации товаров комиссия.",СтатусСообщения.Внимание);
			Основание         = Неопределено;
			ДокументОснование = Неопределено;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		Если КорректировкаНеДоступна(Основание) Тогда
			Сообщить("На основании " + Основание + " введен возврат товаров. Ввод корректировки реализации невозможен.",СтатусСообщения.Внимание);
			Основание         = Неопределено;
			ДокументОснование = Неопределено;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") И (Основание.ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный) Тогда
		#Если Клиент Тогда
			Сообщить("В заказ-наряде выбран бесплатный ремонт. Нельзя создать корректировку реализации!",СтатусСообщения.Важное);
			дкОтменаВводаДокумента(ЭтотОбъект);
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			Возврат;
		#КонецЕсли
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") И (Основание.Состояние <> Справочники.ВидыСостоянийЗаказНарядов.Закрыт) Тогда
		#Если Клиент Тогда
			Сообщить("В заказ-наряде не находится в состоянии ""Закрыт"". Нельзя создать корректировку реализации!",СтатусСообщения.Важное);
			дкОтменаВводаДокумента(ЭтотОбъект);
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			Возврат;
		#КонецЕсли
	КонецЕсли;

	
	Сделка=Документы.КорректировкаРеализации.ПолучитьСделку(Основание);
	
	Товары.Очистить();
	Работы.Очистить();
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	ПоДокументуРеализации=Истина;
	
	// Перезаполним контрагента и договор
	Если ЗначениеЗаполнено(Основание)
		И (Контрагент <> Основание.Контрагент
		ИЛИ ДоговорВзаиморасчетов <> Основание.ДоговорВзаиморасчетов) Тогда
		Контрагент = Основание.Контрагент;
		ОбработкаРеквизита("Контрагент");
		ДопПараметры=Новый Структура;
		ДопПараметры.Вставить("НеПерезаполнятьПоДоговоруВзаиморасчетов",Истина);
		ДоговорВзаиморасчетов = Основание.ДоговорВзаиморасчетов;
		ОбработкаРеквизита("ДоговорВзаиморасчетов",,, ДопПараметры);
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		Для каждого СтрокаТЧ Из Работы Цикл
			Если ПоДокументуРеализации Тогда
				СтрокаТЧ.ПоДокументуРеализации=Истина;
			КонецЕсли; 
			СтрокаТЧ.Подтверждение=Истина;
			СтрокаТЧ.КоличествоПоДокументуРеализации=СтрокаТЧ.Количество;
			СтрокаТЧ.КоэффициентПоДокументуРеализации=СтрокаТЧ.Коэффициент;
			СтрокаТЧ.СтавкаНДСПоДокументуРеализации=СтрокаТЧ.СтавкаНДС;
			СтрокаТЧ.СуммаНДСПоДокументуРеализации=СтрокаТЧ.СуммаНДС;
			СтрокаТЧ.СуммаВсегоПоДокументуРеализации=СтрокаТЧ.СуммаВсего;
		КонецЦикла; 
	КонецЕсли;
	Для каждого СтрокаТЧ Из Работы Цикл	
		СтрокаТЧ.Цена=?(СтрокаТЧ.Количество*СтрокаТЧ.Коэффициент=0,0,СтрокаТЧ.Сумма/(СтрокаТЧ.Количество*СтрокаТЧ.Коэффициент));
		ОбработкаРеквизита("Работы.РасчетРазницы",СтрокаТЧ);
	КонецЦикла; 
	
	ПерваяКорректировка = (ТипЗнч(Основание) <> Тип("ДокументСсылка.КорректировкаРеализации"));
	
	Если ПерваяКорректировка Тогда
		
		// Заполним коды маркировки
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			// Получим коды маркировки для ЗН
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	МаркировкаТоваровВПроизводстве.Номенклатура КАК Номенклатура,
			               |	МаркировкаТоваровВПроизводстве.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			               |	МаркировкаТоваровВПроизводстве.КодМаркировки КАК КодМаркировки,
			               |	МаркировкаТоваровВПроизводстве.Количество КАК Количество,
			               |	МаркировкаТоваровВПроизводстве.КодМаркировкиBase64 КАК КодМаркировкиBase64
			               |ИЗ
			               |	РегистрНакопления.МаркировкаТоваровВПроизводстве КАК МаркировкаТоваровВПроизводстве
			               |ГДЕ
			               |	МаркировкаТоваровВПроизводстве.Регистратор = &Основание";
			Запрос.УстановитьПараметр("Основание", Основание);
			ТаблицаКодовМаркировок = Запрос.Выполнить().Выгрузить();
		Иначе
			ТаблицаКодовМаркировок = Новый ТаблицаЗначений;
			ТаблицаКодовМаркировок.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
			ТаблицаКодовМаркировок.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
			ТаблицаКодовМаркировок.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)));
			ТаблицаКодовМаркировок.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
			ТаблицаКодовМаркировок.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
			ТаблицаКодовМаркировок.Колонки.Добавить("КодМаркировкиBase64",Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)));
			
			Для Каждого ТекущаяСтрока Из Товары Цикл
				НайденныеСтроки = КодыМаркировки.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
				Для Каждого ТекущаяСтрокаМаркировки Из НайденныеСтроки Цикл
					НоваяСтрока = ТаблицаКодовМаркировок.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрокаМаркировки);
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.Цена  = ?(СтрокаТЧ.Количество <> 0, СтрокаТЧ.Сумма/СтрокаТЧ.Количество, 0);
		Если ПоДокументуРеализации Тогда
			СтрокаТЧ.ПоДокументуРеализации=Истина;
		КонецЕсли; 
		СтрокаТЧ.Подтверждение=Истина;
		
		// Заполним доп. поля для товарной строки
		Если ПустаяСтрока(СтрокаТЧ.ИдентификаторТовара) Тогда
			СтрокаТЧ.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		СтрокаТЧ.КоличествоПоДокументуРеализации=СтрокаТЧ.Количество;
		СтрокаТЧ.ЕдиницаИзмеренияПоДокументуРеализации=СтрокаТЧ.ЕдиницаИзмерения;
		СтрокаТЧ.КоэффициентПоДокументуРеализации=СтрокаТЧ.Коэффициент;
		СтрокаТЧ.СтавкаНДСПоДокументуРеализации=СтрокаТЧ.СтавкаНДС;
		СтрокаТЧ.СуммаНДСПоДокументуРеализации=СтрокаТЧ.СуммаНДС;
		СтрокаТЧ.СуммаВсегоПоДокументуРеализации=СтрокаТЧ.СуммаВсего;
		СтрокаТЧ.ПартияПоДокументуРеализации=СтрокаТЧ.Партия;
		СтрокаТЧ.ГТДПоДокументуРеализации=СтрокаТЧ.ГТД;
		ОбработкаРеквизита("Товары.СуммаВсего",СтрокаТЧ);
		ОбработкаРеквизита("Товары.РасчетРазницы",СтрокаТЧ);
	КонецЦикла; 
	ПустыеСтроки=Товары.НайтиСтроки(Новый Структура("Количество",0));
	Для каждого ПустаяСтрока Из ПустыеСтроки Цикл
		Товары.Удалить(ПустаяСтрока);
	КонецЦикла; 
	ПустыеСтроки=Работы.НайтиСтроки(Новый Структура("Количество",0));
	Для каждого ПустаяСтрока Из ПустыеСтроки Цикл
		Работы.Удалить(ПустаяСтрока);
	КонецЦикла; 
	
	// Заполним коды маркировки
	Если ПерваяКорректировка Тогда
		КодыМаркировки.Очистить();
		
		СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
		
		Если НЕ ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			СтруктураПоиска.Вставить("ЕдиницаИзмерения");
			СтруктураПоиска.Вставить("СтавкаНДС");
		КонецЕсли;
		
		Для Каждого ТекущаяСтрока Из Товары Цикл
			
			// Найдем коды маркировки для строки
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
			НайденныеСтроки = ТаблицаКодовМаркировок.НайтиСтроки(СтруктураПоиска);
			МассивУдалить = Новый Массив;
			КоличествоМарок = 0;
			КоличествоТовара = ТекущаяСтрока.Количество * ТекущаяСтрока.Коэффициент;
			
			// Заполним по текущей строки
			Для Каждого ТекущаяМаркировка Из НайденныеСтроки Цикл
				КоличествоМарок = КоличествоМарок + 1;
				НоваяСтрока = КодыМаркировки.Добавить();
				НоваяСтрока.ИдентификаторТовара = ТекущаяСтрока.ИдентификаторТовара;
				НоваяСтрока.КодМаркировки = ТекущаяМаркировка.КодМаркировки;
				НоваяСтрока.КодМаркировкиBase64 = ТекущаяМаркировка.КодМаркировкиBase64;
				МассивУдалить.Добавить(ТекущаяМаркировка);
				Если КоличествоТовара = КоличествоМарок Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			// Уберем из рассмотрения ранее указанные марки
			Для Каждого ТекущаяМаркировка Из МассивУдалить Цикл
				ТаблицаКодовМаркировок.Удалить(ТекущаяМаркировка);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
	
	ИдентификаторГосударственногоКонтракта = ?(ЗначениеЗаполнено(ИдентификаторГосударственногоКонтракта),
		ИдентификаторГосударственногоКонтракта,
		ДоговорВзаиморасчетов.ИдентификаторГосударственногоКонтракта);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ТипЗнч(Сделка)<>Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Если Работы.Количество()>0 Тогда
			Работы.Очистить();
		КонецЕсли; 
	КонецЕсли;
	
	Если ХозОперацияУказанаНеВерно() Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	// проверим хоз. операции
	// составим список документов движений
	Если ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииКорректировкаПоСогласованиюСторон Тогда		
		// заполним значение до корректировки
		Для Каждого стрТовары Из Товары Цикл
			стрТовары.КоличествоДоКорректировки = стрТовары.КоличествоПоДокументуРеализации;
			стрТовары.СуммаВсегоДоКорректировки = стрТовары.СуммаВсегоПоДокументуРеализации;
			стрТовары.СуммаНДСДоКорректировки   = стрТовары.СуммаНДСПоДокументуРеализации;
			стрТовары.ГТДДоКорректировки 		= стрТовары.ГТДПоДокументуРеализации;       
			стрТовары.ПартияДоКорректировки 	= стрТовары.ПартияПоДокументуРеализации;
		КонецЦикла;
		
		Для Каждого стрРаботы Из Работы Цикл
			стрРаботы.КоличествоДоКорректировки = стрРаботы.КоличествоПоДокументуРеализации;
			стрРаботы.СуммаВсегоДоКорректировки = стрРаботы.СуммаВсегоПоДокументуРеализации;
			стрРаботы.СуммаНДСДоКорректировки   = стрРаботы.СуммаНДСПоДокументуРеализации;
		КонецЦикла;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииИсправлениеВПервичныхДокументах Тогда
		
		// проверим введенные на основании корректировки
		МассивВидов = Новый Массив(1); 
		МассивВидов[0] = "КорректировкаРеализации";
		МассивПодчиненных = дкПолучитьМассивПодчиненных(ДокументОснование, МассивВидов, Истина);
		
		ЕстьКорректировка = Ложь;
		
		Если НЕ обЗначениеНеЗаполнено(МассивПодчиненных) И МассивПодчиненных.Количество()>0 Тогда
			Для Каждого Корректировка Из МассивПодчиненных Цикл
				Если НЕ Корректировка.Ссылка = Ссылка
					И НЕ Корректировка.ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииКорректировкаПоСогласованиюСторон Тогда
					ЕстьКорректировка = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЕстьКорректировка Тогда
			Сообщить("К документу """+Строка(ДокументОснование)+""" введено больше одного корректировочного документа с хоз. операцей ""Исправление первичных документов"". 
					|Каждую последующую корректировку следует вводить на основании предыдущей.");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверим наличие не только услуг при реализации агентских услуг
	Если НЕ Документы.КорректировкаРеализации.НеобходимоИспользоватьСклад(ЭтотОбъект) Тогда
		Для Каждого СтрокаТовара Из Товары Цикл
			Если СтрокаТовара.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
				Сообщить("Табличная часть Товары. Строка " + СтрокаТовара.НомерСтроки + ". Нельзя добавлять номенклатуру с видом " + Строка(СтрокаТовара.Номенклатура.ВидНоменклатуры) + ".");
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ Документы.КорректировкаРеализации.НеобходимоИспользоватьСклад(ЭтотОбъект) Тогда
		СкладКомпании = Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// Партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст  = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		                      |	Заказы.Заказ КАК Заказ
		                      |ИЗ
		                      |	(ВЫБРАТЬ
		                      |		ЗаказыПокупателей.Заказ КАК Заказ
		                      |	ИЗ
		                      |		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		                      |	ГДЕ
		                      |		ЗаказыПокупателей.Регистратор = &Ссылка
		                      |	
		                      |	ОБЪЕДИНИТЬ
		                      |	
		                      |	ВЫБРАТЬ
		                      |		ЗаказыРаспределение.ЗаказПокупателя
		                      |	ИЗ
		                      |		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		                      |	ГДЕ
		                      |		ЗаказыРаспределение.Регистратор = &Ссылка
		                      |	
		                      |	ОБЪЕДИНИТЬ
		                      |	
		                      |	ВЫБРАТЬ
		                      |		ЗаказыРаспределение.ЗаказПоставщика
		                      |	ИЗ
		                      |		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		                      |	ГДЕ
		                      |		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
	РассчитатьСуммуВсего();
	
КонецПроцедуры

// Обработчик события возникающего при отмене проведения документа. Выполняется в транзакции записи.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	//Удалим накопление сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.ОтменаПроведения();
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры

// Получаем таблицу товаров которые надо оприходовать на остатки
//
// Возвращаемое значение:
//  ТаблицаТоваров - таблица значений.
//
Функция ПолучитьТаблицуТоваровВозврат()
	// Нам фактически надо сформировать таблицу товаров которые были проданы, что бы их заново оприходовать.
	// Все из-за автоматического списания характеристик.
	ЗапросСписание=Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка=&Ссылка
	|	И ДокументТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|	И ДокументТовары.КоличествоРазница < 0
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпании.Регистратор КАК ДокументПродажи,
	|	ОстаткиТоваровКомпании.Количество КАК Количество
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
	|ГДЕ
	|	ОстаткиТоваровКомпании.Регистратор В (&ДокументПродажи)
	|	И ОстаткиТоваровКомпании.Номенклатура В (
	|		ВЫБРАТЬ Номенклатура
	|		ИЗ ТаблицаНоменклатуры
	|	)
	|	И ОстаткиТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ОстаткиТоваровКомпании
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВПроизводстве.Номенклатура,
	|	ТоварыВПроизводстве.ХарактеристикаНоменклатуры,
	|	ТоварыВПроизводстве.Регистратор,
	|	ТоварыВПроизводстве.Количество
	|ИЗ
	|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
	|ГДЕ
	|	ТоварыВПроизводстве.Регистратор В (&ДокументПродажи)
	|	И ТоварыВПроизводстве.Номенклатура В (
	|		ВЫБРАТЬ Номенклатура
	|		ИЗ ТаблицаНоменклатуры
	|	)
	|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыВПроизводстве
	|
	|");
	ЗапросСписание.УстановитьПараметр("Ссылка",Ссылка);
	СписокДокументовОснований=Новый СписокЗначений;
	Основание = ДокументОснование;
	Пока ЗначениеЗаполнено(Основание) Цикл
		СписокДокументовОснований.Добавить(Основание);
		Основание=Основание.ДокументОснование;
		
		//++бизтех_саа++
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.DN_АктСверкиКредитСтрах") Тогда
			Основание = "";
		КонецЕсли;
		//--бизтех_саа--
		
	КонецЦикла; 
	ЗапросСписание.УстановитьПараметр("ДокументПродажи",СписокДокументовОснований);
	ТаблицаСписания = ЗапросСписание.Выполнить().Выгрузить();
	ТаблицаСписания.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,ДокументПродажи","Количество");
	// Получим таблицу документа
	ЗапросТоваров=Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	-ДокументТовары.КоличествоРазница*ДокументТовары.Коэффициент КАК Количество,
	|	ДокументТовары.Цена КАК Цена
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка=&Ссылка
	|	И ДокументТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|	И ДокументТовары.КоличествоРазница < 0
	|");
	ЗапросТоваров.УстановитьПараметр("Ссылка",Ссылка);
	ТаблицаДокумента=ЗапросТоваров.Выполнить().Выгрузить();
	ТаблицаДокумента.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения","Количество,Цена");
	ТаблицаДокумента.Сортировать("Номенклатура Возр,ХарактеристикаНоменклатуры Убыв");
	
	// Создадим и заполним таблицу товаров
	ТаблицаТоваров=Новый ТаблицаЗначений();
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаТоваров.Колонки.Добавить("СкладКомпании");
	ТаблицаТоваров.Колонки.Добавить("Количество");
	ТаблицаТоваров.Колонки.Добавить("Резерв");
	ТаблицаТоваров.Колонки.Добавить("ЦенаРозничная");
	
	ТаблицаРучныхХарактеристик = дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(Ссылка);
	
	Для Каждого СтрокаТовар Из ТаблицаДокумента Цикл
		Если СтрокаТовар.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
			Продолжить;
		КонецЕсли; 
		СтруктураОтбора=Новый Структура("Номенклатура",СтрокаТовар.Номенклатура);
		Если ЗначениеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ (ТаблицаРучныхХарактеристик<>Неопределено И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
		КонецЕсли;
		МассивНайденныхСтрок = ТаблицаСписания.НайтиСтроки(СтруктураОтбора);
		НадоВернуть    = СтрокаТовар.Количество;
		Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
			
			ТекСтрока = МассивНайденныхСтрок[Сч];
			КоличествоКПоступлению = Мин(НадоВернуть, ТекСтрока.Количество);
			
			НоваяСтрока=ТаблицаТоваров.Добавить();
			НоваяСтрока.Номенклатура               = ТекСтрока.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании              = СкладКомпании;
			НоваяСтрока.Количество                 = КоличествоКПоступлению;
			НоваяСтрока.Резерв                     = 0;
			НоваяСтрока.ЦенаРозничная = 0;
			
			Если ТекСтрока.Количество > НадоВернуть Тогда
				ТекСтрока.Количество = ТекСтрока.Количество - НадоВернуть;
			Иначе
				// Удалим ненужную строку
				ТаблицаСписания.Удалить(ТекСтрока);
			КонецЕсли;
			
			НадоВернуть = НадоВернуть - КоличествоКПоступлению;
			Если НадоВернуть<=0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		// Может еще что-то осталось
		Если НадоВернуть > 0 Тогда
			НоваяСтрока=ТаблицаТоваров.Добавить();
			НоваяСтрока.Номенклатура               = СтрокаТовар.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании              = СкладКомпании;
			НоваяСтрока.Количество                 = НадоВернуть;
			НоваяСтрока.Резерв                     = 0;
			НоваяСтрока.ЦенаРозничная  = 0;
		КонецЕсли;
	КонецЦикла;
	ТаблицаТоваров.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,СкладКомпании,ЦенаРозничная","Количество, Резерв");
	
	Возврат ТаблицаТоваров;
КонецФункции // ПолучитьТаблицуТоваров()

// Обработчик события возникающего в транзакции записи для формирования движений документу по подчиненным регистрам.
//
// Параметры:
//  Отказ           - Булево                   - Признак отказа от совершения операции.
//  РежимПроведения - РежимПроведенияДокумента - Текущий режим проведения.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Вызываем общий обработчик события
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ВозможенВВодНаОсновании(Сделка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	СуммаРазногласий = Товары.Итог("СуммаВсегоРазница") + Работы.Итог("СуммаВсегоРазница");
	
	Если Сделка.ХозОперация <> Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
		Если СуммаРазногласий <> 0 Тогда
			НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
			НаборЗаписейВзаиморасчеты.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.РежимПроведения = РежимПроведения;
			НаборЗаписейВзаиморасчеты.Контрагент = Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
			НаборЗаписейВзаиморасчеты.Сделка = Сделка;
			НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок = Ложь;
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
			НаборЗаписейВзаиморасчеты.СписатьНераспределеннуюСуммуПоСделке = Истина;
			Если СуммаРазногласий > 0 Тогда                                  
				НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Ложь;
				НаборЗаписейВзаиморасчеты.Сумма = СуммаРазногласий;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			Иначе
				НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
				НаборЗаписейВзаиморасчеты.Сумма = -СуммаРазногласий;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
			КонецЕсли;
			// Доходы и расходы по суммовым разницам
			СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			Если СуммаДоходаРасходаСуммовыхРазниц <> 0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				// В случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции.
				Если ВедетсяБалансПоПодразделению Тогда
					НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				Если СуммаДоходаРасходаСуммовыхРазниц < 0 Тогда
					НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
				Иначе
					НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	// Закрываем заказы покупателя по FIFO
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.КоличествоРазница*ДокументТовары.Коэффициент КАК Количество,
	|	ДокументТовары.Цена КАК ЦенаРозничная,
	|	0 КАК Резерв,
	|	ДокументТовары.СуммаВсегоРазница КАК Сумма
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка=&Ссылка
	|	И ДокументТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|	И ДокументТовары.КоличествоРазница > 0
	|";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	// Закрываем заказы
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = РежимПроведения;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
	НаборЗаписейЗаказыПокупателей.Контрагент = Контрагент;
	НаборЗаписейЗаказыПокупателей.Заказ = Неопределено;
	НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
	НаборЗаписейЗаказыПокупателей.Заказывать = Истина;
	НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
	НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству = Истина;
	Отказ = НЕ НаборЗаписейЗаказыПокупателей.ЗакрытиеЗаказовПокупателя() ИЛИ Отказ;
	// Если было списание резервов то таблица товаров содержит списанные резервы.
	РезультатЗапросаПоТоварам = НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам;

	// Снимаем распределение заказов покупателя
	РезультатЗакрытияЗаказов = НаборЗаписейЗаказыПокупателей.Выгрузить();
	КолонкаКоличества = РезультатЗакрытияЗаказов.Колонки.Найти("Заказано");
	КолонкаКоличества.Имя = "Количество";
	КолонкаКоличества.Заголовок = "Количество";
	РезультатЗакрытияЗаказов.Свернуть("Заказ,Контрагент,Номенклатура,ХарактеристикаНоменклатуры", "Количество,Резерв");
	Для каждого СтрокаЗаказа Из РезультатЗакрытияЗаказов Цикл
		СтрокаЗаказа.Количество = -(СтрокаЗаказа.Количество - СтрокаЗаказа.Резерв);
	КонецЦикла; 
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения = РежимПроведения;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗакрытияЗаказов;
	НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = "Заказ";
	НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
	НаборЗаписейРаспределениеЗаказов.Контрагент = Контрагент;
	НаборЗаписейРаспределениеЗаказов.ПоБазовомуКоличеству = Истина;
	Отказ = НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	
	// Проведем партии товаров
	Если НЕ ПровестиПоПартиям(РежимПроведения, Ссылка, СуммаРазногласий) Тогда
		Отказ = Истина;
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // Дальше смысла не имеет
	КонецЕсли; 
	
	// Проведем остатки товаров (расход)
	НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения = РежимПроведения;
	НаборЗаписейОстатки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
	НаборЗаписейОстатки.СкладКомпании = СкладКомпании;
	НаборЗаписейОстатки.Приходовать = Истина;
	НаборЗаписейОстатки.Контрагент = Контрагент;
	НаборЗаписейОстатки.ПоБазовомуКоличеству = Истина;
	НаборЗаписейОстатки.ДвиженияПоРознице = СкладКомпании.Розничный;
	НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная = "Цена";
	НаборЗаписейОстатки.Резервировать = РезультатЗапросаПоТоварам <> Неопределено;
	Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // Дальше смысла не имеет
	КонецЕсли;
	
	// Проведем остатки товаров (возврат)
	НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании = Неопределено;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = ПолучитьТаблицуТоваровВозврат();
	НаборЗаписейОстатки.ПоБазовомуКоличеству = Истина;
	НаборЗаписейОстатки.Контрагент = Контрагент;
	НаборЗаписейОстатки.ДвиженияПоРознице = Ложь;
	НаборЗаписейОстатки.РазрешитьПереоценку = Ложь;
	Если НЕ НаборЗаписейОстатки.Приход() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ОбработкаПроведенияНакопленияПоКарточкамКонтрагентов();
	КонецЕсли;
	
	// Запишим коды маркировки
	Если НЕ Отказ Тогда
		ПровестиПоКодамМаркировки(РежимПроведения, Отказ);
	КонецЕсли;
	
	// Проверим налиие прослеживаемых товаров, которые были реализованы
	Если НЕ Отказ Тогда
		Движения.Продажи.Записать();
	КонецЕсли;
	ТаблицаПрослеживаемыхТоваров = Документы.КорректировкаРеализации.ОперацииСПрослеживаемымиТоварами(
		ЭтотОбъект);
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ТаблицаПрослеживаемыхТоваров;
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;

	// Двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (РежимПроведения<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (РежимПроведения<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыПокупателей.Количество()>0 ИЛИ Движения.ЗаказыРаспределение.Количество()>0));
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	//БизТех Аляль 24.06.2025г. если док-основание ЗН и товар возвращается, то необходимо вернуть его по РН Товары компании в производстве
	Если ТипЗНЧ(Сделка) = Тип("ДокументСсылка.ЗаказНаряд") и НЕ (Товары.Итог("Количество") = Товары.Итог("КоличествоДоКорректировки")) Тогда  
		Основания = Документы.КорректировкаРеализации.ОснованияЦепочкиКорректировкиРеализации(Ссылка);
		ПровестиПоТоварыВПроизводстве(Основания, РежимПроведения, Сделка); 
	КонецЕсли;
	//БизТех Аляль 24.06.2025г. >>
	// Выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ТекущаяВерсияОбъекта = "02.00";

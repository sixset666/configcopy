// Модуль документа Ввод в оборот кодов маркировки


////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;							// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ЗаполнитьКодыМаркировкиИзЗаказовНаЭмиссию() Экспорт
	
	// Проверим связанные Заказы с документом
	ТекстЗапроса = Новый Массив;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		ТекстЗапроса.Добавить("ВЫБРАТЬ
		|	ЗаказКодовМаркировки.Ссылка КАК Заказ
		|ПОМЕСТИТЬ СписокЗаказов
		|ИЗ
		|	Документ.ЗаказКодовМаркировки КАК ЗаказКодовМаркировки
		|ГДЕ
		|	ЗаказКодовМаркировки.ДокументОснование = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияКодовМаркировки.Номенклатура КАК Номенклатура,
		|	СостоянияКодовМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СостоянияКодовМаркировки.GTIN КАК GTIN,
		|	СостоянияКодовМаркировки.СерийныйНомер КАК СерийныйНомер
		|ИЗ
		|	СписокЗаказов КАК СписокЗаказов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
		|		ПО СписокЗаказов.Заказ = СостоянияКодовМаркировки.ДокументОснование");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование)
		И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказКодовМаркировки") Тогда
	
		ТекстЗапроса.Добавить("ВЫБРАТЬ
		|	СостоянияКодовМаркировки.Номенклатура КАК Номенклатура,
		|	СостоянияКодовМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СостоянияКодовМаркировки.GTIN КАК GTIN,
		|	СостоянияКодовМаркировки.СерийныйНомер КАК СерийныйНомер
		|ИЗ
		|	РегистрСведений.СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
		|ГДЕ
		|	СостоянияКодовМаркировки.ДокументОснование = &ДокументОснование");
		
	КонецЕсли;
	
	Если ТекстЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСоединения = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстЗапроса, СтрокаСоединения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	СписокМаркировок = Запрос.Выполнить().Выгрузить();
	
	СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	Для Каждого ТекущаяСтрока Из СписокМаркировок Цикл
		
		КодМаркировки = "01" + ТекущаяСтрока.GTIN + "21" + ТекущаяСтрока.СерийныйНомер;
		ОбъектШК.РазобратьСтрокуШтрихкодаGS1(КодМаркировки, Истина);
		
		// Такой код уже добавлен
		Если КодыМаркировки.Найти(КодМаркировки, "КодМаркировки") <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Найдем строку с товаром
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		НайденныеСтроки = Товары.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаТовара Из НайденныеСтроки Цикл
			
			КоличествоКодов = КодыМаркировки.НайтиСтроки(
				Новый Структура("ИдентификаторТовара", СтрокаТовара.ИдентификаторТовара)).Количество();
			Если СтрокаТовара.Количество * СтрокаТовара.Коэффициент = КоличествоКодов Тогда
				// Для данной строки заполнили
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = КодыМаркировки.Добавить();
			НоваяСтрока.ИдентификаторТовара = СтрокаТовара.ИдентификаторТовара;
			НоваяСтрока.КодМаркировки = КодМаркировки;
			
			Прервать;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("Статус",1);
	ОбязательныеРеквизиты.Вставить("СпособВводаВОборот",1);
	ОбязательныеРеквизиты.Вставить("ТоварнаяГруппа",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	Результат = Результат И дкПроверитьСоответствиеНоменклатурыТоварнойГруппе(ЭтотОбъект,,, Ошибки);
	
	Возврат Результат;	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВводВОборотКодовМаркировки", "Ввод в оборот кодов маркировки", Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	Если Имя = "Товары.Номенклатура" Тогда
		
		// Проверим номенклатуру
		Если ЗначениеЗаполнено(ТекСтрока.Номенклатура)
			И НЕ ТекСтрока.Номенклатура.ТипНоменклатуры.ВедетсяМаркировка Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру по которой не ведется учет по маркировке",5);
			#КонецЕсли
			ТекСтрока.Номенклатура = Неопределено;
		КонецЕсли;
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		ТекСтрока.СтранаПроизводства = ТекСтрока.Номенклатура.СтранаПроисхождения;
		
		Возврат Рез;
		
	КонецЕсли;
	
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события установки нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Статус) Тогда
		Статус = Перечисления.СтатусыВводаВОборот.Новый;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ЭтотОбъект.Автор.Сотрудник;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказКодовМаркировки") Тогда
		
		#Если Клиент Тогда
			Если Основание.Статус <> Перечисления.СтатусыЗаказаКодовМаркировки.Выполнен Тогда
				Сообщить("Состояние заказа кодов маркировок не выполнено. Проверьте наличие всех кодов маркировок.");
			КонецЕсли;
			Если ЗначениеЗаполнено(Основание.ДокументОснование) Тогда
				Сообщить("Текущий заказ введен на основании документа " + Основание.ДокументОснование);
			КонецЕсли;
		#КонецЕсли
		
		Если Основание.СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.МаркировкаОстатков Тогда
			СпособВводаВОборот = Перечисления.СпособыВводаВОборот.МаркировкаОстатков
		КонецЕсли;
		
		Товары.Очистить();
		КодыМаркировки.Очистить();
		
		ТоварыДокумента = Основание.Товары.Выгрузить();
		ТоварыДокумента.Свернуть("Номенклатура,ХарактеристикаНоменклатуры", "Количество");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	СостоянияКодовМаркировки.Номенклатура КАК Номенклатура,
		               |	СостоянияКодовМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	СостоянияКодовМаркировки.GTIN КАК GTIN,
		               |	СостоянияКодовМаркировки.СерийныйНомер КАК СерийныйНомер
		               |ПОМЕСТИТЬ СписокКодов
		               |ИЗ
		               |	РегистрСведений.СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
		               |ГДЕ
		               |	СостоянияКодовМаркировки.ДокументОснование = &ДокументОснование
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	СписокКодов.Номенклатура КАК Номенклатура,
		               |	СписокКодов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	СостоянияКодовМаркировкиСрезПоследних.Период КАК Период,
		               |	СостоянияКодовМаркировкиСрезПоследних.Состояние КАК Состояние,
		               |	СписокКодов.GTIN КАК GTIN,
		               |	СписокКодов.СерийныйНомер КАК СерийныйНомер
		               |ПОМЕСТИТЬ ТаблицаСостояний
		               |ИЗ
		               |	СписокКодов КАК СписокКодов
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияКодовМаркировки.СрезПоследних(
		               |				,
		               |				(Номенклатура, ХарактеристикаНоменклатуры) В
		               |					(ВЫБРАТЬ
		               |						СписокКодов.Номенклатура КАК Номенклатура,
		               |						СписокКодов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		               |					ИЗ
		               |						СписокКодов КАК СписокКодов)) КАК СостоянияКодовМаркировкиСрезПоследних
		               |		ПО СписокКодов.Номенклатура = СостоянияКодовМаркировкиСрезПоследних.Номенклатура
		               |			И СписокКодов.ХарактеристикаНоменклатуры = СостоянияКодовМаркировкиСрезПоследних.ХарактеристикаНоменклатуры
		               |			И СписокКодов.GTIN = СостоянияКодовМаркировкиСрезПоследних.GTIN
		               |			И СписокКодов.СерийныйНомер = СостоянияКодовМаркировкиСрезПоследних.СерийныйНомер
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	МАКСИМУМ(ТаблицаСостояний.Период) КАК Период,
		               |	ТаблицаСостояний.GTIN КАК GTIN,
		               |	ТаблицаСостояний.СерийныйНомер КАК СерийныйНомер
		               |ПОМЕСТИТЬ МаксимальныеПериоды
		               |ИЗ
		               |	ТаблицаСостояний КАК ТаблицаСостояний
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТаблицаСостояний.GTIN,
		               |	ТаблицаСостояний.СерийныйНомер
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТаблицаСостояний.Номенклатура КАК Номенклатура,
		               |	ТаблицаСостояний.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ТаблицаСостояний.Период КАК Период,
		               |	ТаблицаСостояний.Состояние КАК Состояние,
		               |	ТаблицаСостояний.GTIN КАК GTIN,
		               |	ТаблицаСостояний.СерийныйНомер КАК СерийныйНомер
		               |ИЗ
		               |	ТаблицаСостояний КАК ТаблицаСостояний
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныеПериоды КАК МаксимальныеПериоды
		               |		ПО ТаблицаСостояний.GTIN = МаксимальныеПериоды.GTIN
		               |			И ТаблицаСостояний.Период = МаксимальныеПериоды.Период
		               |			И ТаблицаСостояний.СерийныйНомер = МаксимальныеПериоды.СерийныйНомер
		               |ГДЕ
		               |	ТаблицаСостояний.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияКодовМаркировки.Эмитирован)";
		Запрос.УстановитьПараметр("ДокументОснование", Основание);
		
		СписокМаркировок = Запрос.Выполнить().Выгрузить();
		
		СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
		
		ОбъектШК = Обработки.ШтрихКоды.Создать();
		
		Для Каждого ТекущаяСтрока Из СписокМаркировок Цикл
			
			КодМаркировки = "01" + ТекущаяСтрока.GTIN + "21" + ТекущаяСтрока.СерийныйНомер;
			ОбъектШК.РазобратьСтрокуШтрихкодаGS1(КодМаркировки, Истина);
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
			НайденныеСтроки = Товары.НайтиСтроки(СтруктураПоиска);
			
			НоваяСтрока = КодыМаркировки.Добавить();
			НоваяСтрока.КодМаркировки = КодМаркировки;
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				НоваяСтрока.ИдентификаторТовара = НайденныеСтроки[0].ИдентификаторТовара;
				НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество + 1;
			Иначе
				СтрокаТовара = Товары.Добавить();
				СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор;
				СтрокаТовара.Номенклатура = ТекущаяСтрока.Номенклатура;
				ОбработкаРеквизита("Товары.Номенклатура", СтрокаТовара);
				СтрокаТовара.Количество = 1;
				СтрокаТовара.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатуры;
				ОбработкаРеквизита("Товары.ХарактеристикаНоменклатуры", СтрокаТовара);
				НоваяСтрока.ИдентификаторТовара = СтрокаТовара.ИдентификаторТовара;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ИдентификаторТовара) Тогда
			ТекущаяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
	КонецЦикла;
	
	Если обЗначениеНеЗаполнено(ТоварнаяГруппа) Тогда
		ТоварнаяГруппа = дкТоварнаяГруппа(Товары.ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Статус = Перечисления.СтатусыВводаВОборот.Новый;
	
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		орЖурналСостояний(Ссылка, Статус);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Обработчик события удаления проведения
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события проведения
//
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И Статус = Перечисления.СтатусыВводаВОборот.Выполнен Тогда
		
		// Изменим состояние маркировки
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборот;
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		НаборЗаписейСостоянияКодовМаркировки.ПроверятьВводВОборот = Истина;
		Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
		
	КонецЕсли;
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = БТ_Склад;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Количество = тс.Количество;  
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

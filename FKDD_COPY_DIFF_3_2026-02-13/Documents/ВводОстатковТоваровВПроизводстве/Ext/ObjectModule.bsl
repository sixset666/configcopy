// Модуль документа ВводОстатковТоваровВПроизводстве

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	ОбязательныеТовары.Вставить("Количество",1);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	//ОбязательныеРеквизиты.Вставить("Контрагент",1);
	//ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Цех",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			КонтролируемыеРеквизиты = Новый Структура();
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);								
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВводОстатковТоваровВПроизводстве","Ввод остатков товаров в производстве",Истина);	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по колонке "СуммаВсего".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Товары.Итог("СуммаВсего");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		//Параметры для подстановки или создания договора контрагента
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.Покупка);
		ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
		ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("Контрагент",Контрагент);
        КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если НЕ обЗначениеНеЗаполнено(Контрагент) И Контрагент.ОсвобожденОтНДС Тогда
			Запрос=Новый Запрос("
			|ВЫБРАТЬ ПЕРВЫЕ 1 СпрСтавкиНДС.Ссылка КАК СтавкаБезНДС
			|ИЗ Справочник.СтавкиНДС КАК СпрСтавкиНДС
			|ГДЕ СпрСтавкиНДС.Ставка=0");
			РезультатСтавкаБезНДС=Запрос.Выполнить();
			Если РезультатСтавкаБезНДС.Пустой() Тогда
				ТекСтрока.СтавкаНДС=Неопределено;
			Иначе
				ТекСтрока.СтавкаНДС=РезультатСтавкаБезНДС.Выгрузить().Получить(0).СтавкаБезНДС;
			КонецЕсли;
			ОбработкаРеквизита("Товары.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;				
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  ШапкаДокумента - Документ.Ссылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента)
	ТекстЗапроса="
		|ВЫБРАТЬ
		|	ДокТовары.Номенклатура КАК Номенклатура,
		|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокТовары.СуммаВсего КАК Сумма,
		|   ДокТовары.СтавкаНДС КАК СтавкаНДС,
		|	ДокТовары.СуммаНДС КАК СуммаНДС,
		|	ДокТовары.Количество*ДокТовары.Коэффициент КАК Количество,
		|	&СсылкаПартия КАК Партия,
		|	&СтатусПартии КАК СтатусПартии,
		|	ДокТовары.ГТД КАК ГТД
		|ИЗ
		|	Документ.ВводОстатковТоваровВПроизводстве.Товары КАК ДокТовары
		|ГДЕ
		|	  ДокТовары.Ссылка = &Ссылка
		|	И ДокТовары.Количество > 0
		|";
		
	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	Если СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
		ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
		Запрос.УстановитьПараметр("СсылкаПартия",ПартияТоваровОтрицательныхОстатков);
	Иначе
		Запрос.УстановитьПараметр("СсылкаПартия",ШапкаДокумента.Ссылка);
	КонецЕсли; 	
	Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварКупленный);	
	Запрос.Текст=ТекстЗапроса;
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Документ.Ссылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - Выборка по шапке.
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СтатьяОприходованияТМЦ КАК СтатьяОприходованияТМЦ,
	|	Док.Цех КАК Цех,
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ.ВводОстатковТоваровВПроизводстве КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

//заполнение табличной части Товары строками из заказ-наряда
Процедура ЗаполнитьТЧПоЗаказНаряду(ЗаказНаряд) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.ЕдиницаИзмерения,
	|	ТаблицаТоваров.Коэффициент,
	|	ТаблицаТоваров.СтавкаНДС,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказНарядТовары.Номенклатура,
	|		ЗаказНарядТовары.Количество,
	|		ЗаказНарядТовары.ЕдиницаИзмерения,
	|		ЗаказНарядТовары.Коэффициент,
	|		ЗаказНарядТовары.СтавкаНДС,
	|		ЗаказНарядТовары.ХарактеристикаНоменклатуры
	|	ИЗ
	|		Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
	|	ГДЕ
	|		ЗаказНарядТовары.Ссылка = &Основание
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказНарядМатериалы.Номенклатура,
	|		ЗаказНарядМатериалы.Количество,
	|		ЗаказНарядМатериалы.ЕдиницаИзмерения,
	|		ЗаказНарядМатериалы.Коэффициент,
	|		ЗаказНарядМатериалы.Номенклатура.СтавкаНДС,
	|		ЗаказНарядМатериалы.ХарактеристикаНоменклатуры
	|	ИЗ
	|		Документ.ЗаказНаряд.Материалы КАК ЗаказНарядМатериалы
	|	ГДЕ
	|		ЗаказНарядМатериалы.Ссылка = &Основание) КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.ЕдиницаИзмерения,
	|	ТаблицаТоваров.Коэффициент,
	|	ТаблицаТоваров.СтавкаНДС,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры
	|";
	Запрос.УстановитьПараметр("Основание", ЗаказНаряд);
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Ввод остатков товаров в производстве"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВводОстатковТоваровВПроизводстве(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВводОстатковТоваровВПроизводстве");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеКонтрагента = спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");

	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВводОстатковТоваров()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,,Ложь) Тогда Возврат; КонецЕсли;
	Если Основание = Неопределено Тогда
		Цех=обПраво("ОсновнойЦех",Права,,ЭтотОбъект);
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ДокументОснование = Основание;
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		
		//заполнение ТЧ в двух вариантах - деталями заказчика или остальными деталями
		ЗаполнитьТЧПоЗаказНаряду(Основание);
		
		//розничный тип цен из основания нам не подходит, поэтому меняем его на закупочный и пересчитываем все суммы 
		ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
		ОбработкаРеквизита("ТипЦен");
	КонецЕсли;	
	
	ОбработкаРеквизита("Контрагент");

	Если обЗначениеНеЗаполнено(СтатьяОприходованияТМЦ) Тогда
		СтатьяОприходованияТМЦ=Справочники.СтатьиДоходовИРасходов.ВводОстатковТоваров;
	КонецЕсли; 
	
	// Заполним доп. поля для товарной строки
	Для Каждого СтрокаТовар Из Товары Цикл
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	ДополнительныеСвойства.Вставить("ЗаказНарядБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		//заказ-наряд
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаряд ИЗ РегистрНакопления.ТоварыВПроизводстве ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказНарядБыл", Результат.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭтоЗагрузка") Тогда
		ЭтоЗагрузка = ДополнительныеСвойства.ЭтоЗагрузка;
	Иначе
		ЭтоЗагрузка = Ложь;
	КонецЕсли;
	
	Если (НЕ РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения) И (НЕ ЭтотОбъект.Проведен)
		И ((ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен И (Не ЭтоЗагрузка)) ИЛИ
		   (ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт))
		И (ЭтотОбъект.МоментВремени().Сравнить(ДокументОснование.МоментВремени())=1) Тогда
		Сообщить("Остатки в производстве не могут быть введены после окончания работ по заказ-наряду!");
		Отказ = Истина;
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	РезультатЗапросаПоТоварам=ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект);
	
	ШапкаДокумента=ПолучитьШапкуДокумента(Ссылка);

	// оприходуем товар
	НаборЗаписейТоварыВПроизводстве=Движения.ТоварыВПроизводстве;
	НаборЗаписейТоварыВПроизводстве.РежимПроведения=Режим;
	НаборЗаписейТоварыВПроизводстве.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейТоварыВПроизводстве.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
	НаборЗаписейТоварыВПроизводстве.Цех = ШапкаДокумента.Цех;
	НаборЗаписейТоварыВПроизводстве.ДокументЗаказНаряд = ДокументОснование;
	НаборЗаписейТоварыВПроизводстве.ЕстьСтавкаНДС=Истина;
	НаборЗаписейТоварыВПроизводстве.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейТоварыВПроизводстве.Приход() ИЛИ Отказ;
	
	// Переместим в производство маркируемый товар
	НаборЗаписейМаркировкаТоваровВПроизводстве=Движения.МаркировкаТоваровВПроизводстве;
	НаборЗаписейМаркировкаТоваровВПроизводстве.РежимПроведения=Режим;
	НаборЗаписейМаркировкаТоваровВПроизводстве.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейМаркировкаТоваровВПроизводстве.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейМаркировкаТоваровВПроизводстве.ДокументЗаказНаряд=ШапкаДокумента.ДокументОснование;
	НаборЗаписейМаркировкаТоваровВПроизводстве.Цех=ШапкаДокумента.Цех;
	НаборЗаписейМаркировкаТоваровВПроизводстве.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейМаркировкаТоваровВПроизводстве.Приход() ИЛИ Отказ;
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	ПодразделениеЦех = обПолучитьБалансовоеПодразделение(Цех.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеЦех);

	// Доходы и расходы
	НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
	НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
	НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяОприходованияТМЦ;
	Если ВедетсяБалансПоПодразделению Тогда
		НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
	КонецЕсли;
	НаборЗаписейДиР.ВУпрВалюте				= Ложь;
	НаборЗаписейДиР.Доход					= СуммаДокумента;
	НаборЗаписейДиР.Расход					= 0;
	НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	
	//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
	Если ВедетсяБалансПоПодразделению и БалансовыеПодразделенияНеРавны Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДоходыИРасходы.Расход=СуммаДокумента;           
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.Цех.Подразделение;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДоходыИРасходы.Доход=СуммаДокумента;         
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности Производства
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказНарядБыл) Тогда		
		НаборЗаписейГраницыПроизводство = РегистрыСведений.ГраницыПроизводство.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПроизводство.ЗаказНаряд     		= ШапкаДокумента.ДокументОснование;
			НаборЗаписейГраницыПроизводство.МоментВремени  		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= ДополнительныеСвойства.ЗаказНарядБыл;
			НаборЗаписейГраницыПроизводство.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПроизводство.ЗаказНаряд			= ДополнительныеСвойства.ЗаказНарядБыл;
			НаборЗаписейГраницыПроизводство.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= Неопределено;
			НаборЗаписейГраницыПроизводство.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПроизводство.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПроизводство.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль документа ВводВЭксплуатацию

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение;
//  Результат - Соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
//
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары = Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("Актив",3);
	ОбязательныеТовары.Вставить("ТипЭксплуатации",1);
	ОбязательныеТовары.Вставить("БалансоваяСтоимость",1);
	ОбязательныеТовары.Вставить("МОЛ",2);
	

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеПолучатель",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("МОЛ",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
		
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",           КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ПодразделениеПолучатель", Ложь);
			КонецЕсли;
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			   							
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Товары Цикл
		Если НЕ (ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
				 ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
				 ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты) Тогда
			Если НЕ (ТекСтрока.Количество = 1) Тогда
				ТекСтрока.Количество = 1;
				#Если Клиент Тогда
				Сообщить("Табличная часть ""Активы"" строка " + ТекСтрока.НомерСтроки + ": количество актива """ + СокрЛП(ТекСтрока.Актив) + """ заменено на 1,000.", СтатусСообщения.Важное);
				#КонецЕсли
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()


// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВводВЭксплуатацию","Ввод в эксплуатацию",Истина);
		
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
// Режим - число, 0-возвращает излишки, 1-недостачи
//
// Параметры:
//  ШапкаДокумента - выборка по шапке документа.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента)
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВводВЭксплуатациюТовары.Номенклатура,
	               |	ВводВЭксплуатациюТовары.ХарактеристикаНоменклатуры,
	               |	ВводВЭксплуатациюТовары.Количество * ВводВЭксплуатациюТовары.Коэффициент КАК Количество,
	               |	0 КАК Резерв,
	               |	ВЫБОР
	               |		КОГДА ВводВЭксплуатациюТовары.МОЛ = &ПустойМОЛ
	               |			ТОГДА ВводВЭксплуатациюТовары.Ссылка.МОЛ
	               |		ИНАЧЕ ВводВЭксплуатациюТовары.МОЛ
	               |	КОНЕЦ КАК МОЛ,
	               |	ВводВЭксплуатациюТовары.Актив КАК Актив,
	               |	ВводВЭксплуатациюТовары.ТипЭксплуатации КАК ТипЭксплуатации,
	               |	ВводВЭксплуатациюТовары.БалансоваяСтоимость КАК БалансоваяСтоимость,
	               |	0 КАК Амортизация,
	               |	0 КАК СуммаОбслуживания,
				   |	ВводВЭксплуатациюТовары.Актив.ВидПрочегоАктива КАК ВидПрочегоАктива
	               |ИЗ
	               |	Документ.ВводВЭксплуатацию.Товары КАК ВводВЭксплуатациюТовары
	               |ГДЕ
	               |	ВводВЭксплуатациюТовары.Ссылка = &Ссылка";
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("ПустойМОЛ", Справочники.Сотрудники.ПустаяСсылка());
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// Возвращает суммы амортизации сгруппированные по
// статьям доходов и расходов из ТЧ Товары
//
// Возвращаемое значение:
// возвращает таблицу значений
Функция ПолучитьРезультатЗапросаПоСтатьям(ШапкаДокумента)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ВводВЭксплуатациюТовары.СтатьяРасходовПоАмортизации = &ПустаяСсылка
	               |			ТОГДА &Амортизация
	               |		ИНАЧЕ ВводВЭксплуатациюТовары.СтатьяРасходовПоАмортизации
	               |	КОНЕЦ КАК СтатьяРасходовПоАмортизации,
	               |	СУММА(ВводВЭксплуатациюТовары.БалансоваяСтоимость) КАК БалансоваяСтоимость
	               |ИЗ
	               |	Документ.ВводВЭксплуатацию.Товары КАК ВводВЭксплуатациюТовары
	               |ГДЕ
	               |	ВводВЭксплуатациюТовары.Ссылка = &Ссылка
	               |	И ВводВЭксплуатациюТовары.ТипЭксплуатации.СпособНачисленияАмортизации = &СпособНачисления
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА ВводВЭксплуатациюТовары.СтатьяРасходовПоАмортизации = &ПустаяСсылка
	               |			ТОГДА &Амортизация
	               |		ИНАЧЕ ВводВЭксплуатациюТовары.СтатьяРасходовПоАмортизации
	               |	КОНЕЦ";
	Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("СпособНачисления", Перечисления.СпособыНачисленияАмортизации.СтопроцентныйПриВводе);
	Запрос.УстановитьПараметр("ПустаяСсылка", Справочники.СтатьиДоходовИРасходов.ПустаяСсылка());
	Запрос.УстановитьПараметр("Амортизация", Справочники.СтатьиДоходовИРасходов.Амортизация);
	
	Возврат Запрос.Выполнить()
КонецФункции //ПолучитьРезультатЗапросаПоСтатьям()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все в порядке, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
	ПодразделениеПолучательБаланса = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеПолучатель);
	БалансовыеПодразделенияНеРавны = ПодразделениеСклад<>ПодразделениеПолучательБаланса;
	
	// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
	РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента);
	РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам.Выгрузить();
	РезультатЗапросаПоТоварамСвернутый = РезультатЗапросаПоТоварам.Скопировать();
	РезультатЗапросаПоТоварамСвернутый.Свернуть("Номенклатура, ХарактеристикаНоменклатуры", "Количество");
	
	// списываем с партий
	НаборЗаписейПартии                           = Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейПартии.ШапкаДокумента			 = ШапкаДокумента;
	НаборЗаписейПартии.СкладКомпании             = ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.СтатусПартии              = Перечисления.СтатусыПартий.ТоварКупленный;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамСвернутый;
	Отказ = НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	
	Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
		// БАЛАНС: Возможно что подразделения склада и получателя не равны в этом случае
		// уменьшаем доход на подразделении склада
		
		// Получим себестоимости по партиям
		ПартииТоваровКомпании = Движения.ПартииТоваровКомпании;
		СебестоимостьПартий = ПартииТоваровКомпании.Итог("СуммаУпр");
		// уменьшаем доход
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.Подразделение  = ШапкаДокумента.СкладКомпании.Подразделение;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		НаборЗаписейДиР.Доход = СебестоимостьПартий;
		Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
				
	Иначе
		// Доходы и расходы (по отклонению списания товаров от ввода активов)
		
		// БАЛАНС: Движение по отклонению списания товаров от ввода активов делаем 
		// в случае если способ ведения баланса - не ведётся по подразделениям 
		
		ИтогоБалансоваяСтоимость = РезультатЗапросаПоТоварам.Итог("БалансоваяСтоимость");
		ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		ИтогБалансоваяСтоимостьУпр = ?(ВалютаДокумента = ВалютаУпр, ИтогоБалансоваяСтоимость,
				обПересчет(ИтогоБалансоваяСтоимость, ВалютаДокумента, КурсДокумента, ВалютаУпр,
				?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), МоментВремени(), ЭтотОбъект.КурсВалютыУпр)));
		ИтогСписаннаяСтоимостьУпр = НаборЗаписейПартии.Итог("СуммаУпр");
		
		Если Окр(обМод(ИтогБалансоваяСтоимостьУпр - ИтогСписаннаяСтоимостьУпр), 2, РежимОкругления.Окр15как20) >= 0.01 Тогда
			НаборЗаписейДиР                = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.ОтклонениеСтоимостиАктиваПриВводеВЭксплуатацию;
			
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			
			НаборЗаписейДиР.ШапкаДокумента = ШапкаДокумента;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если ИтогБалансоваяСтоимостьУпр > ИтогСписаннаяСтоимостьУпр Тогда
				НаборЗаписейДиР.Доход  = ИтогБалансоваяСтоимостьУпр - ИтогСписаннаяСтоимостьУпр;
			Иначе
				НаборЗаписейДиР.Расход = ИтогСписаннаяСтоимостьУпр - ИтогБалансоваяСтоимостьУпр;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	//Все ОК
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// просматривает заполненность реквизитов тек. строки при попытке рассчитать балансовую стоимость
//
// Параметры:
//  ТекСтрока - текущая строка ТЧ "Товары"
// Возвращаемое значение:
//	Истина, если всё ОК
//
Функция ПроверитьРеквизитыСтроки(ТекСтрока) Экспорт
	//список проверяемых на заполненность реквизитов текущей строки
	СписокРеквизитовПроверки = Новый СписокЗначений;
	СписокРеквизитовПроверки.Добавить(СкладКомпании,          "Склад компании");
	СписокРеквизитовПроверки.Добавить(ТекСтрока.Номенклатура, "Номенклатура");
	СписокРеквизитовПроверки.Добавить(ТекСтрока.Количество,   "Количество");
	СписокРеквизитовПроверки.Добавить(ТекСтрока.Коэффициент,  "Коэффициент");
		
	//собственно проверка
	Для каждого ТекРеквизит Из СписокРеквизитовПроверки Цикл
		Если обЗначениеНеЗаполнено(ТекРеквизит.Значение) Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Истина;
КонецФункции // ПроверитьРеквизитыСтроки()

// проверяет активы в справочнике с отбором по номенклатуре
//
// Параметры:
// ВыбНоменклатура - Справочник.Ссылка "Номенклатура"
//
// Возвращаемое значение:
//  СписокАктивов - СписокЗначений - содержит активы, отобранные по номенклатуре.
//
Функция ПроверитьСуществующиеАктивы(ВыбНоменклатура) Экспорт
	СписокАктивов = Новый СписокЗначений;
	Если ВыбНоменклатура = Неопределено ИЛИ ВыбНоменклатура.Пустая() Тогда
		Возврат СписокАктивов;
	КонецЕсли;
		
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПрочиеАктивы.Ссылка КАК Актив
	|ИЗ
	|	Справочник.ПрочиеАктивы КАК ПрочиеАктивы
	|ГДЕ
	|	ПрочиеАктивы.Номенклатура = &Номенклатура";
	Запрос.УстановитьПараметр("Номенклатура", ВыбНоменклатура);
	
	ТаблАктивов = Запрос.Выполнить().Выгрузить();
	Для каждого ТекСтрокаАктива ИЗ ТаблАктивов Цикл
		СписокАктивов.Добавить(ТекСтрокаАктива.Актив);
	КонецЦикла;
	
	Возврат СписокАктивов;
КонецФункции // ПроверитьСуществующиеАктивы()

// рассчитывает балансовую стоимость актива исходя из партий товара (по умолчанию)
//
// Параметры
//  ТекСтрока – текущая строка ТЧ "Товары"
//
// Возвращаемое значение:
//   ВозвращаемоеЗначение – Структура(ОстатокТовара - остаток товара по которому была рассчитана балансовая стоимость,
//                                    БалансоваяСтоимость - рассчитанная балансовая стоимость).
//
Функция РассчитатьБалансовуюСтоимостьАктиваПоПартиям(ТекСтрока) Экспорт
	ВозвращаемоеЗначение = Новый Структура("ОстатокТовара, БалансоваяСтоимость, БалансоваяСтоимостьУпр", 0, 0, 0);
	
	//получим стратегию списания товаров
	СтратегияСписанияПартийТоваровПоДатам = Константы.СтратегияСписанияПартийТоваровПоДатам;
	//получаем таблицу партий для заданных параметров номенклатуры
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПартииТоваровКомпанииОстатки.Партия            КАК Партия,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток,0) КАК Количество,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаОстаток,0) КАК Сумма,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.СуммаУпрОстаток,0) КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент, Номенклатура = &Номенклатура" + ?(ТекСтрока.ХарактеристикаНоменклатуры.Пустая(), "", " И ХарактеристикаНоменклатуры = &Характеристика") + " И
	|				                                    СтатусПартии = &СтатусПартии И СкладКомпании = &Склад) КАК ПартииТоваровКомпанииОстатки
	|УПОРЯДОЧИТЬ ПО
	|	Партия.Дата";
	Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.ЛИФО Тогда
		Запрос.Текст = Запрос.Текст + " Убыв"
	КонецЕсли;
	Запрос.УстановитьПараметр("Момент", МоментВремени());
	Запрос.УстановитьПараметр("Номенклатура", ТекСтрока.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", ТекСтрока.ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("СтатусПартии", Перечисления.СтатусыПартий.ТоварКупленный);
	Запрос.УстановитьПараметр("Склад", СкладКомпании);
	ТаблПартий = Запрос.Выполнить().Выгрузить();
	
	Колич = Мин(ТаблПартий.Итог("Количество"), ТекСтрока.Количество * ТекСтрока.Коэффициент);
	ВозвращаемоеЗначение.ОстатокТовара = Колич;
	Если Колич = 0 Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли; 
	
	Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
		ВозвращаемоеЗначение.БалансоваяСтоимость = ?(ТаблПартий.Итог("Количество") = ТекСтрока.Количество * ТекСтрока.Коэффициент, ТаблПартий.Итог("Сумма"),
													ТаблПартий.Итог("Сумма") / ТаблПартий.Итог("Количество") * ТекСтрока.Количество * ТекСтрока.Коэффициент);
		ВозвращаемоеЗначение.БалансоваяСтоимостьУпр = ?(ТаблПартий.Итог("Количество") = ТекСтрока.Количество * ТекСтрока.Коэффициент, ТаблПартий.Итог("СуммаУпр"),
													ТаблПартий.Итог("СуммаУпр") / ТаблПартий.Итог("Количество") * ТекСтрока.Количество * ТекСтрока.Коэффициент);
	Иначе 
		БалансСтоим = 0;
		БалансСтоимУпр = 0;
		Для каждого ТекПартия Из ТаблПартий Цикл
			Если Колич <= 0 Тогда
				Прервать;		
			КонецЕсли;
			Стоим = ?(Колич < ТекПартия.Количество, ТекПартия.Сумма / ТекПартия.Количество * Колич, ТекПартия.Сумма);
			СтоимУпр = ?(Колич < ТекПартия.Количество, ТекПартия.СуммаУпр / ТекПартия.Количество * Колич, ТекПартия.СуммаУпр);
			БалансСтоим = БалансСтоим + Стоим;
			БалансСтоимУпр = БалансСтоимУпр + СтоимУпр;
			Колич = Колич - ТекПартия.Количество;
		КонецЦикла;
		ВозвращаемоеЗначение.БалансоваяСтоимость = БалансСтоим;
		ВозвращаемоеЗначение.БалансоваяСтоимостьУпр = БалансСтоимУпр;
	КонецЕсли;
	Возврат ВозвращаемоеЗначение;
КонецФункции // РассчитатьБалансовуюСтоимостьАктиваПоПартиям()

// Обработка реквизитов ТЧ для получения расчетной балансовой стоимости
//
// Параметры:
//  ТекСтрока  - строка табличного поля - текущая строка,
//  ТекКолонка - строка - имя текущей колонки.
//
Процедура ВывестиРасчетнуюБалансовуюСтоимостьАктива(ТекСтрока, ТекКолонка = Неопределено) Экспорт
	Если НЕ ПроверитьРеквизитыСтроки(ТекСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	//Если выбранный актив имеет ненулевую первоначальную стоимость, тогда берем оттуда, иначе рассчитываем по партиям
	ПервоначСтоим = 0; 
	Если ТекКолонка = "Количество" Тогда
	Иначе
		ПервоначСтоим = ТекСтрока.Актив.ПервоначальнаяСтоимость;
	КонецЕсли;
	Если ПервоначСтоим = 0 Тогда
		РасчетнаяБалансоваяСтоимость = РассчитатьБалансовуюСтоимостьАктиваПоПартиям(ТекСтрока);
		Если РасчетнаяБалансоваяСтоимость.ОстатокТовара = 0 Тогда
			ТекСтрока.БалансоваяСтоимость = 0;
			Возврат;
		КонецЕсли;
		ТекСтрока.Количество = РасчетнаяБалансоваяСтоимость.ОстатокТовара / ТекСтрока.Коэффициент;
		Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			РасчетнаяБалансоваяСтоимость = РасчетнаяБалансоваяСтоимость.БалансоваяСтоимость;
			ТребуетсяПересчет = Ложь;
		Иначе
			РасчетнаяБалансоваяСтоимость = РасчетнаяБалансоваяСтоимость.БалансоваяСтоимостьУпр;
			ТребуетсяПересчет = Истина;
		КонецЕсли; 
	Иначе
		РасчетнаяБалансоваяСтоимость = ПервоначСтоим;
		ТребуетсяПересчет = Истина;
	КонецЕсли;
		
	//пересчитаем балансовую стоимость в валюту документа
	Если ТребуетсяПересчет Тогда
		ВалютаИз = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		ТекСтрока.БалансоваяСтоимость = обПересчет(РасчетнаяБалансоваяСтоимость, ВалютаИз, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
	Иначе
		ТекСтрока.БалансоваяСтоимость = РасчетнаяБалансоваяСтоимость;
	КонецЕсли;
КонецПроцедуры // ВывестиРасчетнуюБалансовуюСтоимостьАктива()

// Расчет общей балансовой стоимости активов
//
// Возвращаемое значение:
//  СуммаВсего - число - итог по колонке "БалансоваяСтоимость".
//
Функция РассчитатьСуммуВсего() Экспорт
	СуммаВсего = Товары.Итог("БалансоваяСтоимость");
	Возврат СуммаВсего;
	//Возврат дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", тКэшСуммСписания);
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка	– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма		– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "СкладКомпании" Тогда
		Рез=ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Для Каждого стрТовары Из Товары Цикл
				Если НЕ обЗначениеНеЗаполнено(стрТовары.Номенклатура) Тогда
					стрТовары.Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.Номенклатура,СкладКомпании);
				Иначе
					стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
		Иначе 
			Для Каждого стрТовары Из Товары Цикл
				стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЦикла;
		КонецЕсли;
		Возврат Рез;
	ИначеЕсли Имя = "ПроверкаСкладаКомпании" тогда
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли СкладКомпании.Розничный Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранный склад: " + СкладКомпании + " не соответствует хоз. операции!
			|Выберите оптовый склад",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Актив" Тогда
		Если ТекСтрока.Актив.Пустая() Тогда
			//ничего не делаем
		Иначе
			ТекСтрока.Номенклатура    = ТекСтрока.Актив.Номенклатура;
			ТекСтрока.ТипЭксплуатации = ?(ТекСтрока.Актив.ОсновнойТипЭксплуатации.Пустая(), ТекСтрока.ТипЭксплуатации, ТекСтрока.Актив.ОсновнойТипЭксплуатации);
			ТекСтрока.СтатьяРасходовПоАмортизации = ТекСтрока.ТипЭксплуатации.СтатьяРасходовПоАмортизации;
			Если ТекСтрока.Актив.ПервоначальнаяСтоимость = 0 Тогда
			Иначе
				//пересчитаем первоначальную стоимость в валюту документа
				ВалютаИз = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
				ТекСтрока.БалансоваяСтоимость = обПересчет(ТекСтрока.Актив.ПервоначальнаяСтоимость,
					ВалютаИз, ?(КурсВалютыУпр = 0, Дата, КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			ОбработкаРеквизита("Товары.Номенклатура", ТекСтрока, ЭтаФорма, Истина);
		КонецЕсли;
		Возврат Истина;
	
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,Неопределено);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		
		//если указан ДопПараметры = Истина, то обработчик вызывается из обработчика изменения "Товары.Актив"
		Если ДопПараметры <> Неопределено И ТипЗнч(ДопПараметры)=Тип("Булево") И ДопПараметры Тогда
			ДопПараметры=Неопределено;
		Иначе	
			СписокСуществующихАктивов = ПроверитьСуществующиеАктивы(ТекСтрока.Номенклатура);
			Если СписокСуществующихАктивов.Количество() = 0 Тогда
				ТекСтрока.Актив = Справочники.ПрочиеАктивы.ПустаяСсылка();
			Иначе
				ТекСтрока.Актив = СписокСуществующихАктивов[0].Значение;
				Если НЕ (ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
						 ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
						 ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты) Тогда
					ТекСтрока.Количество = 1;
				КонецЕсли;				
			КонецЕсли;
			ТекСтрока.ТипЭксплуатации = ТекСтрока.Актив.ОсновнойТипЭксплуатации;
		КонецЕсли;
		
		//Заполнил МОЛа по умолчанию из шапки
		Если обЗначениеНеЗаполнено(ТекСтрока.МОЛ) Тогда
			ТекСтрока.МОЛ = МОЛ;
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("ПолучитьРасчетнуюСтоимостьАктива",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ТипЭксплуатации" Тогда
		Если ТекСтрока.ТипЭксплуатации.Пустая() ИЛИ НЕ (ТекСтрока.ТипЭксплуатации.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизации.СтопроцентныйПриВводе) Тогда
			ТекСтрока.СтатьяРасходовПоАмортизации = Справочники.СтатьиДоходовИРасходов.ПустаяСсылка();
		Иначе	
			ТекСтрока.СтатьяРасходовПоАмортизации = ТекСтрока.ТипЭксплуатации.СтатьяРасходовПоАмортизации;
		КонецЕсли;	
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПолучитьРасчетнуюСтоимостьАктива",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПолучитьРасчетнуюСтоимостьАктива",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ТекСтрока.Количество = 0 И ТекСтрока.Актив.Пустая() Тогда
			ТекСтрока.БалансоваяСтоимость = 0;
		КонецЕсли;
		Возврат ОбработкаРеквизита("ПолучитьРасчетнуюСтоимостьАктива",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ПолучитьРасчетнуюСтоимостьАктива" Тогда
		ИмяТекКолонки = ?(ЭтаФорма = Неопределено, Неопределено, ЭтаФорма.ЭлементыФормы.Товары.ТекущаяКолонка.Имя);
		ВывестиРасчетнуюБалансовуюСтоимостьАктива(ТекСтрока, ИмяТекКолонки);
		Возврат Истина;
				
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВводВЭксплуатацию(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВводВЭксплуатацию");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеПодразделения = спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
	ОбластьМакета.Параметры.ПредставлениеМОЛ = спПолучитьНаименование(МОЛ);
	ОбластьМакета.Параметры.ПредставлениеПодразделенияПолучателя = спПолучитьНаименование(ПодразделениеПолучатель);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,БалансоваяСтоимость",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
		ОбластьМакета.Параметры.Актив = СтрокаТабличнойЧасти.Актив;
		ОбластьМакета.Параметры.АктивНаименование = спПолучитьНаименование(СтрокаТабличнойЧасти.Актив);
		ОбластьМакета.Параметры.Код = СтрокаТабличнойЧасти.Актив.ИнвентарныйНомер;
		ОбластьМакета.Параметры.ТипЭксплуатации = СтрокаТабличнойЧасти.ТипЭксплуатации;
		ОбластьМакета.Параметры.БалансоваяСтоимость = Формат(СтрокаТабличнойЧасти.БалансоваяСтоимость,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Количество = СтрокаТабличнойЧасти.Количество;
		ОбластьМакета.Параметры.ЕдиницаИзмерения = СтрокаТабличнойЧасти.ЕдиницаИзмерения;
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,БалансоваяСтоимость",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги		
		
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("БалансоваяСтоимость");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВводВЭксплуатацию()

// Формирует печатную форму "ОС-1"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОС1(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ВводВЭксплуатациюТовары.Ссылка.Дата КАК ДатаВвода,
	|	ВводВЭксплуатациюТовары.Ссылка.МОЛ КАК Ответственный,
	|	ВводВЭксплуатациюТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеОрганизации,
	|	ВводВЭксплуатациюТовары.Ссылка.СкладКомпании КАК Склад,
	|
	|	ВводВЭксплуатациюТовары.Актив КАК Актив,
	|	ВводВЭксплуатациюТовары.Актив.ДатаВыбытия КАК ДатаВыбытия,
	|	ВводВЭксплуатациюТовары.Актив.ИнвентарныйНомер КАК ИнвНомер,
	|	ВводВЭксплуатациюТовары.Актив.ПервоначальнаяСтоимость КАК НачСтоимость,
	|	ВводВЭксплуатациюТовары.Актив.КоэффициентУскорения КАК КоэффициентУскоренияБУ,
	|	ВводВЭксплуатациюТовары.Актив.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ВводВЭксплуатациюТовары.БалансоваяСтоимость КАК БалансоваяСтоимость,
	|
	|	ВводВЭксплуатациюТовары.ТипЭксплуатации.СпособНачисленияАмортизации КАК СпособАмортизации,
	|	ВводВЭксплуатациюТовары.ТипЭксплуатации.АмортизационнаяГруппа КАК АмортизационнаяГруппа
	|ИЗ
	|	Документ.ВводВЭксплуатацию.Товары КАК ВводВЭксплуатациюТовары
	|ГДЕ
	|	ВводВЭксплуатациюТовары.Ссылка = &Ссылка";
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаАктивы = Запрос.Выполнить().Выбрать();
	ВыборкаАктивы.Следующий();
	
	ОрганизацияСдатчик    = обПолучитьЗначениеСвойства(ЭтотОбъект,"ОрганизацияСдатчик",ЭтотОбъект.Организация);
	ОрганизацияПолучатель = ЭтотОбъект.Организация;
	СдатчикНашаОрганизация = (ОрганизацияСдатчик = ОрганизацияПолучатель);
	//Чтение значение для адреса
	АдресОрганизацииСдатчика = киПолучитьПредставлениеКИ(ОрганизацияСдатчик, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	АдресЮридический = ?(АдресОрганизацииСдатчика = "", "", АдресОрганизацииСдатчика + ", ");
	АдресТелефоныСдатчика = АдресЮридический + "тел.:" + киПолучитьПредставлениеКИ(ОрганизацияСдатчик, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	Если СдатчикНашаОрганизация Тогда
		АдресТелефоныПолучателя = АдресТелефоныСдатчика;
	Иначе
		АдресОрганизацииПолучателя = киПолучитьПредставлениеКИ(ОрганизацияПолучатель, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		АдресЮридический = ?(АдресОрганизацииПолучателя = "", "", АдресОрганизацииПолучателя + ", ");
		АдресТелефоныПолучателя = АдресЮридический + "тел.:" + киПолучитьПредставлениеКИ(ОрганизацияПолучатель, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	КонецЕсли;
	
 	// Получим ответственных
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ГлавныйБухгалтер  = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	мМОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	ПодразделениеСдатчика = ?(СдатчикНашаОрганизация,ЭтотОбъект.ПодразделениеКомпании,"");
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил"); 
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	// Получим членов комиссии
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	
	Если ВыборкаАктивы.Количество() = 1 Тогда //актив один - печать ОС-1
        Макет = ПолучитьМакет("ОС1");
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		
		ОбластьМакета.Параметры.РуководительСдатчикПредставление     = ?(СдатчикНашаОрганизация,Руководитель.РуководительПредставление,"");
		ОбластьМакета.Параметры.РуководительСдатчикДолжность   		 = ?(СдатчикНашаОрганизация,Руководитель.РуководительДолжность,"");		
		ОбластьМакета.Параметры.РуководительСдатчик				     = ?(СдатчикНашаОрганизация,Руководитель.Руководитель,"");		
		ОбластьМакета.Параметры.РуководительПолучатель				 = Руководитель.Руководитель;		
		ОбластьМакета.Параметры.РуководительПолучательПредставление  = Руководитель.РуководительПредставление;
		ОбластьМакета.Параметры.РуководительПолучательДолжность	     = Руководитель.РуководительДолжность;
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчикаПредставление    = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтерПредставление,"");
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчика				         = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтер,"");		
		
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(мМОЛ);
		ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		ОбластьМакета.Параметры.Заполнить(Получил);
		
		//сдатчик номенклатуры
		ОбластьМакета.Параметры.ПредставлениеОрганизацииСдатчика	= спПолучитьНаименование(ОрганизацияСдатчик);
		ОбластьМакета.Параметры.ОрганизацияСдатчик					= ОрганизацияСдатчик;
		ОбластьМакета.Параметры.АдресСдатчика						= АдресТелефоныСдатчика;
		ОбластьМакета.Параметры.РеквизитыСдатчика					= Строка(ОрганизацияСдатчик.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияСдатчик.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеСдатчика				= ПодразделениеСдатчика;
		
		//получатель актива
		ОбластьМакета.Параметры.ПредставлениеОрганизацииПолучателя	= спПолучитьНаименование(ОрганизацияПолучатель);
		ОбластьМакета.Параметры.ОрганизацияПолучатель				= ОрганизацияПолучатель;
		ОбластьМакета.Параметры.АдресПолучателя						= АдресТелефоныПолучателя;
		ОбластьМакета.Параметры.РеквизитыПолучателя					= Строка(ОрганизацияПолучатель.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияПолучатель.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеПолучателя				= ПодразделениеПолучатель;
		
		ОбластьМакета.Параметры.КодПоОКПОСдатчик    = ОрганизацияСдатчик.КодПоОКПО;
		ОбластьМакета.Параметры.КодПоОКПОПолучатель = ОрганизацияПолучатель.КодПоОКПО;
		ОбластьМакета.Параметры.ДатаВвода    = ВыборкаАктивы.ДатаВвода;
		ОбластьМакета.Параметры.ДатаПередачи = обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаПередачи",ВыборкаАктивы.ДатаВыбытия);
		ОбластьМакета.Параметры.НомерГруппы		= ВыборкаАктивы.АмортизационнаяГруппа;
		ОбластьМакета.Параметры.ИнвНомер		= ВыборкаАктивы.ИнвНомер;
		ОбластьМакета.Параметры.НаименованиеОС	= спПолучитьНаименование(ВыборкаАктивы.Актив);
		ОбластьМакета.Параметры.КарточкаОС		= ВыборкаАктивы.Актив;
		ОбластьМакета.Параметры.НомерАкта = дкПолучитьНомерДляПечати(ЭтотОбъект);
		ОбластьМакета.Параметры.ДатаАкта  = Дата;
		ОбластьМакета.Параметры.ДатаДок   = Дата;
		
		ОбластьМакета.Параметры.СрокЭкспл            = 0;
		ОбластьМакета.Параметры.СрокПолезнИспПриПер  = 0;
		ОбластьМакета.Параметры.НачАмортизация       = Формат(0,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ОстСтоимость         = Формат(0,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ЦенаПродажи          = Формат(обПересчет(ВыборкаАктивы.БалансоваяСтоимость, ВалютаДокумента, КурсДокумента, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата),ФорматВыводаСуммы);
		ОбластьМакета.Параметры.НачСтоимость         = Формат(обПересчет(ВыборкаАктивы.НачСтоимость, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата),ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СрокПолезнИспПриПост = ВыборкаАктивы.СрокПолезногоИспользования;
		ОбластьМакета.Параметры.СпособАмортизации    = ВыборкаАктивы.СпособАмортизации;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
		
	Иначе //активов много - групповая печать (ОС-1б)
        Макет = ПолучитьМакет("ОС1б");
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка1");
		
		ОбластьМакета.Параметры.РуководительСдатчикПредставление     = ?(СдатчикНашаОрганизация,Руководитель.РуководительПредставление,"");
		ОбластьМакета.Параметры.РуководительСдатчикДолжность   		 = ?(СдатчикНашаОрганизация,Руководитель.РуководительДолжность,"");		
		ОбластьМакета.Параметры.РуководительСдатчик				     = ?(СдатчикНашаОрганизация,Руководитель.Руководитель,"");		
		ОбластьМакета.Параметры.РуководительПолучатель				 = Руководитель.Руководитель;		
		ОбластьМакета.Параметры.РуководительПолучательПредставление  = Руководитель.РуководительПредставление;
		ОбластьМакета.Параметры.РуководительПолучательДолжность	     = Руководитель.РуководительДолжность;
		
		
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(мМОЛ);
				
		//сдатчик номенклатуры
		ОбластьМакета.Параметры.ПредставлениеОрганизацииСдатчика	= спПолучитьНаименование(ОрганизацияСдатчик);
		ОбластьМакета.Параметры.ОрганизацияСдатчик					= ОрганизацияСдатчик;
		ОбластьМакета.Параметры.АдресСдатчика						= АдресТелефоныСдатчика;
		ОбластьМакета.Параметры.РеквизитыСдатчика					= Строка(ОрганизацияСдатчик.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияСдатчик.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеСдатчика				= ПодразделениеСдатчика;
		
		//получатель актива
		ОбластьМакета.Параметры.ПредставлениеОрганизацииПолучателя	= спПолучитьНаименование(ОрганизацияПолучатель);
		ОбластьМакета.Параметры.ОрганизацияПолучатель				= ОрганизацияПолучатель;
		ОбластьМакета.Параметры.АдресПолучателя						= АдресТелефоныПолучателя;
		ОбластьМакета.Параметры.РеквизитыПолучателя					= Строка(ОрганизацияПолучатель.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияПолучатель.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеПолучателя				= ПодразделениеПолучатель;
		
		
		ОбластьМакета.Параметры.КодОКПОСдатчик          = ОрганизацияСдатчик.КодПоОКПО;
		ОбластьМакета.Параметры.КодОКПОПолучатель       = ОрганизацияПолучатель.КодПоОКПО;		
		ОбластьМакета.Параметры.ДатаАктаВВода = ВыборкаАктивы.ДатаВвода;
		ОбластьМакета.Параметры.ДатаПередачи  = обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаПередачи",ВыборкаАктивы.ДатаВыбытия);
		ОбластьМакета.Параметры.НомерДок      = дкПолучитьНомерДляПечати(ЭтотОбъект);
		ОбластьМакета.Параметры.ДатаДок       = Дата;
		ТабДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Шапка2");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка2");
		НомСтр = 1;
		ВыборкаАктивы.Сбросить();
		Пока ВыборкаАктивы.Следующий() Цикл
			ОбластьМакета.Параметры.НомерСтроки		= НомСтр;
			ОбластьМакета.Параметры.НаименованиеОС	= спПолучитьНаименование(ВыборкаАктивы.Актив);
			ОбластьМакета.Параметры.КарточкаОС		= ВыборкаАктивы.Актив;
			ОбластьМакета.Параметры.НомерГруппы		= ВыборкаАктивы.АмортизационнаяГруппа;
			ОбластьМакета.Параметры.ИнвНомер		= ВыборкаАктивы.ИнвНомер;
			ОбластьМакета.Параметры.ДатаВвода		= ВыборкаАктивы.ДатаВвода;
			ТабДокумент.Вывести(ОбластьМакета);
			НомСтр = НомСтр + 1;
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка2П");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал2");
		ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
		
		ТабДокумент.Вывести(ОбластьМакета);
				
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка3");
		ТабДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Строка3");
		ИтогЦенаПродажи  = 0;
		ИтогНачСтоимость = 0;
		ВыборкаАктивы.Сбросить();
		Пока ВыборкаАктивы.Следующий() Цикл
			ЦенаПродажи  = обПересчет(ВыборкаАктивы.БалансоваяСтоимость, ВалютаДокумента, КурсДокумента, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
			НачСтоимость = обПересчет(ВыборкаАктивы.НачСтоимость, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
			
			ОбластьМакета.Параметры.НачАмортизация       = Формат(0,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.ОстСтоимость         = Формат(0,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.ЦенаПродажи          = Формат(ЦенаПродажи,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.НачСтоимость         = Формат(НачСтоимость,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СрокПолезнИспПриПост = ВыборкаАктивы.СрокПолезногоИспользования;
			ОбластьМакета.Параметры.СпособАмортизации    = ВыборкаАктивы.СпособАмортизации;
			ТабДокумент.Вывести(ОбластьМакета);
			
			ИтогЦенаПродажи  = ИтогЦенаПродажи  + ЦенаПродажи;
			ИтогНачСтоимость = ИтогНачСтоимость + НачСтоимость;
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка3П");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал3");
		ОбластьМакета.Параметры.ИтогЦенаПродажи  = Формат(ИтогЦенаПродажи,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ИтогНачСтоимость = Формат(ИтогНачСтоимость,ФорматВыводаСуммы); 
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчикаПредставление    = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтерПредставление,"");
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчика				 = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтер,"");		
		

		ТабДокумент.Вывести(ОбластьМакета);
		
        ОбластьМакета = Макет.ПолучитьОбласть("Шапка4");
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(мМОЛ);
		ОбластьМакета.Параметры.Заполнить(Получил);
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	КонецЕсли;
	
	//параметры табличного документа
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ОтображатьСетку     = Ложь;
	ТабДокумент.ТолькоПросмотр      = Истина;
			
	Возврат ТабДокумент;
КонецФункции // ПечатьОС1()

// Формирует печатную форму "М-11"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ11(ТабДокумент) Экспорт
	Если НЕ Проведен Тогда
		Сообщить("Документ не проведен в печатной форме не будет корректных данных!", СтатусСообщения.Информация);
	КонецЕсли;
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// Зададим параметры документа
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("М11");
	
	//получим списываемые товары
	ТекстЗапроса = "ВЫБРАТЬ
	|	ЕСТЬNULL(ПартииТоваровКомпанииОбороты.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ПартииТоваровКомпанииОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	<НоменклатурныйНомер> КАК НоменклатурныйНомер,
	|	ПартииТоваровКомпанииОбороты.Номенклатура.СтавкаНДС.Ставка КАК СтавкаНДС,
	|	ПартииТоваровКомпанииОбороты.КоличествоРасход КАК Количество,
	|	ПартииТоваровКомпанииОбороты.СуммаРасход - ПартииТоваровКомпанииОбороты.СуммаНДСРасход КАК Сумма,
	|	ВЫБОР 
	|   КОГДА ПартииТоваровКомпанииОбороты.КоличествоРасход = 0 ТОГДА 0
	|   ИНАЧЕ (ПартииТоваровКомпанииОбороты.СуммаРасход - ПартииТоваровКомпанииОбороты.СуммаНДСРасход)/
	|			ПартииТоваровКомпанииОбороты.КоличествоРасход 
	|	КОНЕЦ КАК Цена,
	|	ДокументТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ДокументТовары.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|
	|	ДокументТовары.Ссылка.СкладКомпании.МОЛ.Должность КАК МОЛДолжностьОтправитель,
	|	ДокументТовары.Ссылка.СкладКомпании.МОЛ КАК МОЛПредставлениеОтправитель,
	|	ДокументТовары.Ссылка.МОЛ.Должность КАК МОЛДолжностьПолучатель,
	|	ДокументТовары.Ссылка.МОЛ КАК МОЛПредставлениеПолучатель
	|
	|ИЗ Документ.ВводВЭксплуатацию.Товары КАК ДокументТовары
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Обороты(
	|		,
	|		,
	|		Регистратор,
	|		Номенклатура В (&СписокНоменклатуры)
	|			И ХарактеристикаНоменклатуры В (&СписокХарактеристик)
	|			И СкладКомпании = &Склад) КАК ПартииТоваровКомпанииОбороты
	|ПО ДокументТовары.Ссылка = ПартииТоваровКомпанииОбороты.Регистратор
	|	И ДокументТовары.Номенклатура = ПартииТоваровКомпанииОбороты.Номенклатура
	|	И ДокументТовары.ХарактеристикаНоменклатуры = ПартииТоваровКомпанииОбороты.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ДокументТовары.Ссылка = &Документ";
	
	РежимВыводаКодаВДокументах=обПраво("РежимВыводаКодаВДокументах",Права,,ЭтотОбъект);
	Если РежимВыводаКодаВДокументах=Перечисления.РежимыВыводаКодаВДокументах.Артикул Тогда
		СтрокаЗамены="ПартииТоваровКомпанииОбороты.Номенклатура.Артикул";
	ИначеЕсли РежимВыводаКодаВДокументах=Перечисления.РежимыВыводаКодаВДокументах.КодИАртикул Тогда
		СтрокаЗамены="ПартииТоваровКомпанииОбороты.Номенклатура.Код+"" / ""+ПартииТоваровКомпанииОбороты.Номенклатура.Артикул";
	Иначе
		СтрокаЗамены="ПартииТоваровКомпанииОбороты.Номенклатура.Код";
	КонецЕсли; 
	ТекстЗапроса=СтрЗаменить(ТекстЗапроса,"<НоменклатурныйНомер>",СтрокаЗамены);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СписокНоменклатуры", Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("СписокХарактеристик", Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.УстановитьПараметр("Склад", СкладКомпании);
	Запрос.УстановитьПараметр("Документ", Ссылка);
	ВыборкаСтрок = Запрос.Выполнить().Выгрузить();
		
	Затребовал = дкОтветственноеЛицо(ЭтотОбъект,"Затребовал");
	Разрешил   = дкОтветственноеЛицо(ЭтотОбъект,"Разрешил");
	
	// выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.Заполнить(Затребовал);
	ОбластьМакета.Параметры.Заполнить(Разрешил);
	ОбластьМакета.Параметры.Заголовок					= "Требование-накладная № " + дкПолучитьНомерДляПечати(ЭтотОбъект) + " от " + Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.Организация					= Организация;
	ОбластьМакета.Параметры.КодОКПО						= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ДатаСоставления				= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.Склад						= СкладКомпании;
	// выводим именно получателя, а не подразделение, откуда списали
	ОбластьМакета.Параметры.ПодразделениеКомпании		= ПодразделениеПолучатель;
	ТабДокумент.Вывести(ОбластьМакета); // конец шапки
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьРазделителя		= Макет.ПолучитьОбласть("Разделитель");
	ОбластьМакетаИтогоПоСтранице = ОбластьРазделителя;
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	мМОЛ = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.МОЛ);
	МолОтправитель = дкОтветственноеЛицо(ЭтотОбъект, "МОЛОтправитель");
	// выводим подвал                                
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Заполнить(мМОЛ);
	ОбластьПодвал.Параметры.Заполнить(МолОтправитель);
	ТабДокументПодвал = Новый ТабличныйДокумент;
	ТабДокументПодвал.Вывести(ОбластьПодвал);
	 
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	// выводим заголовок таблицы
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	
	// выводим многострочную часть документа
	Для каждого Строка Из ВыборкаСтрок Цикл
		Если обЗначениеНеЗаполнено(Строка.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		ОбластьСтрока.Параметры.Заполнить(Строка);
		ОбластьСтрока.Параметры.Количество = Формат(Строка.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена = Формат(Строка.Цена,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Сумма = Формат(Строка.Сумма,ФорматВыводаКоличества);
		
		ОбластьСтрока.Параметры.Счет					= "";
		Характеристика									= Строка(Строка.ХарактеристикаНоменклатуры);
 		ОбластьСтрока.Параметры.МатериалНаименование	= спПолучитьНаименование(Строка.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(Строка) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ТабДокументПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла; // конец многострочной части
	
	ТабДокумент.Вывести(ТабДокументПодвал); // конец подвала
	
	Возврат ТабДокумент;
КонецФункции // ПечатьМ11()

// Функция формирует печатную форму документа МБ-7
//
Функция ПечатьМБ7(ТабДокумент) Экспорт
		
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// Зададим параметры документа
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Макет = ПолучитьМакет("МБ7");
		
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВводВЭксплуатациюТовары.НомерСтроки КАК НомерПП,
	               |	ВводВЭксплуатациюТовары.Номенклатура.Код КАК НоменклатурныйНомер,
	               |	ВводВЭксплуатациюТовары.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	               |	ВводВЭксплуатациюТовары.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	               |	ВводВЭксплуатациюТовары.Количество,
	               |	ВводВЭксплуатациюТовары.МОЛ КАК Сотрудник,
	               |	ВводВЭксплуатациюТовары.Ссылка.Дата КАК ДатаДокумента,
	               |	ВводВЭксплуатациюТовары.Актив.СрокПолезногоИспользования КАК СрокСлужбы,
	               |	ВводВЭксплуатациюТовары.Актив,
	               |	ВводВЭксплуатациюТовары.МОЛ.ТабельныйНомер КАК ТабельныйНомер
	               |ИЗ
	               |	Документ.ВводВЭксплуатацию.Товары КАК ВводВЭксплуатациюТовары
	               |ГДЕ
	               |	ВводВЭксплуатациюТовары.Ссылка = &ТекущийДокумент
	               |	И ВводВЭксплуатациюТовары.Актив.ВидПрочегоАктива = &ВидПрочегоАктива
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерПП";
				   
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВидПрочегоАктива", Перечисления.ВидыПрочихАктивов.Спецодежда);				   
	ВыборкаСтрок = Запрос.Выполнить().Выгрузить();
	
	// выводим шапку

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.Заголовок					= "ВЕДОМОСТЬ № " + дкПолучитьНомерДляПечати(ЭтотОбъект) + " от " + Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.ПредставлениеОрганизации   = спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.ПредставлениеПодразделения = ПодразделениеКомпании;
	ОбластьМакета.Параметры.ОрганизацияПоОКПО          = Организация.КодПоОКПО; 	
	ОбластьМакета.Параметры.ДатаСоставления				= Формат(Дата, "ДФ = дд.ММ.гггг");
	ТабДокумент.Вывести(ОбластьМакета);//конец шапки
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьРазделителя		= Макет.ПолучитьОбласть("Разделитель");
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
    мМОЛ = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.МОЛ);
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.Руководитель);
	
	// выводим подвал                                
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Заполнить(мМОЛ);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	ТабДокументПодвал = Новый ТабличныйДокумент;
	ТабДокументПодвал.Вывести(ОбластьПодвал);	
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	// выводим заголовок таблицы
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
    ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;		
	
	СтуктураИтоговПоСтранице = Новый Структура;
	ИтогКоличествоПоСтранице = 0;
	// выводим многострочную часть документа
	Для Каждого Строка Из ВыборкаСтрок Цикл
		
		ОбластьСтрока.Параметры.Заполнить(Строка);
		ОбластьСтрока.Параметры.Количество = Формат(Строка.Количество,ФорматВыводаКоличества);
 		ОбластьСтрока.Параметры.ТоварНаименование	= Строка.Актив;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(Строка) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ТабДокументПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
        СтуктураИтоговПоСтранице.Вставить("ИтогКоличествоПоСтранице", ИтогКоличествоПоСтранице);
		ИтогКоличествоПоСтранице = ИтогКоличествоПоСтранице + Строка.Количество;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы,СтуктураИтоговПоСтранице , ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		

	КонецЦикла; //конец многострочной части
   
	ТабДокумент.Вывести(ТабДокументПодвал); // конец подвала
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьМБ7()

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
    // Документ не стандартный(имя реквизита для цены не "Цена", а "БалансоваяСтоимость").
    СтруктураПараметров=Новый Структура("ИмяРеквизитаЦена","БалансоваяСтоимость");
    дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Перем ВремЯчейка;
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(Основание) =  Тип("ДокументСсылка.ПеремещениеТоваров") тогда
		СкладКомпании = Основание.СкладПолучатель;
	КонецЕсли;
 	//из справочника ПрочиеАктивы
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ПрочиеАктивы") Тогда
		СтрокаТабличнойЧасти = Товары.Добавить();
		СтрокаТабличнойЧасти.Актив           = Основание.Ссылка;
		ОбработкаРеквизита("Товары.Актив", СтрокаТабличнойЧасти);
		ОбработкаРеквизита("Товары.Номенклатура", СтрокаТабличнойЧасти);
	//из складских документов
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ 
		ТипЗнч(Основание) = Тип("ДокументСсылка.АвансовыйОтчет") ИЛИ
		ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
    	ТаблицаОстатковПоступления = Новый ТаблицаЗначений;
		ТаблицаОстатковПоступления.Колонки.Добавить("Номенклатура");
		ТаблицаОстатковПоступления.Колонки.Добавить("Количество");
		ТаблицаОстатковПоступления.Колонки.Добавить("Актив");
		ТаблицаОстатковПоступления.Колонки.Добавить("Характеристика");
		ТаблицаОстатковПоступления.Колонки.Добавить("ЕдиницаИзмерения");
		ТаблицаОстатковПоступления.Колонки.Добавить("Коэффициент");
		Для каждого ТекСтрока Из Товары Цикл
			КоличествоОстаток = ТекСтрока.Количество - 1;
			ВремЯчейка = ТекСтрока.Ячейка;
			ОбработкаРеквизита("Товары.Номенклатура", ТекСтрока);
			ТекСтрока.Ячейка =ВремЯчейка;
			Если НЕ ТекСтрока.Актив.Пустая() И КоличествоОстаток > 0 Тогда
				Если НЕ (ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
						 ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
						 ТекСтрока.Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты) Тогда
					НоваяСтрока = ТаблицаОстатковПоступления.Добавить();
					НоваяСтрока.Номенклатура     = ТекСтрока.Номенклатура;
					НоваяСтрока.Количество       = КоличествоОстаток;
					НоваяСтрока.Актив            = ТекСТрока.Актив;
					НоваяСтрока.Характеристика   = ТекСтрока.ХарактеристикаНоменклатуры;
					НоваяСтрока.Коэффициент      = ТекСтрока.Коэффициент;
					НоваяСтрока.ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Для Каждого ТекСтрока Из ТаблицаОстатковПоступления Цикл
			ПоискСтроки = Товары.Найти(ТекСтрока.Актив);
			НоваяСтрока = Товары.Вставить(ПоискСтроки.НомерСтроки);
			НоваяСтрока.Номенклатура               = ТекСтрока.Номенклатура;
			НоваяСтрока.Количество                 = ТекСтрока.Количество;
			НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрока.Характеристика;
			НоваяСтрока.Коэффициент                = ТекСтрока.Коэффициент;
			НоваяСтрока.ЕдиницаИзмерения           = ТекСтрока.ЕдиницаИзмерения;			
		КонецЦикла;			
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Результат = Истина;
	Если НЕ ФлагПерерасчет Тогда
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда Возврат; КонецЕсли; 
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//проверим на корректность вида склада
	Если СкладКомпании.Розничный Тогда
		дкСообщитьРезультат("Реквизит <Склад компании> . Склад списания номенклатуры на перевод в актив не может быть розничным", "Важное&Прочие активы в эксплуатации:", ЭтотОбъект, СкладКомпании);
		Отказ = Истина;
	КонецЕсли;
	
	ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеПолучательБаланса = обПолучитьБалансовоеПодразделение(ПодразделениеПолучатель);
	БалансовыеПодразделенияНеРавны = ПодразделениеСклад<>ПодразделениеПолучательБаланса;
	
	// спишем товар со склада
	РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект).Выгрузить();
	РезультатЗапросаПоТоварамСвернутый = РезультатЗапросаПоТоварам.Скопировать();
	РезультатЗапросаПоТоварамСвернутый.Свернуть("Номенклатура, ХарактеристикаНоменклатуры", "Количество");
	НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании = СкладКомпании;
	НаборЗаписейОстатки.ДвиженияПоРознице=Ложь;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамСвернутый;
	Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// закинем в "Активы в эксплуатации"
	НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
	НаборЗаписейЭксплуатация.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейЭксплуатация.ПодразделениеКомпании     = ПодразделениеПолучатель;
	НаборЗаписейЭксплуатация.ЭтоПервыйВвод             = Истина;
	НаборЗаписейЭксплуатация.РезультатЗапросаПоАктивам = РезультатЗапросаПоТоварам;
	Отказ = НЕ НаборЗаписейЭксплуатация.Приход() ИЛИ Отказ;
	
	Если ВедетсяБалансПоПодразделению Тогда
		// БАЛАНС: Возможно что подразделения склада и получателя не равны в этом случае
		// увеличиваем доход на подразделении получателе
		Если БалансовыеПодразделенияНеРавны Тогда
			// Получим себестоимости по прочим активам
			ПрочиеАктивыВЭксплуатации=Движения.ПрочиеАктивыВЭксплуатации;
			СебестоимостьПрочиеАктивыВЭксплуатации = ПрочиеАктивыВЭксплуатации.Итог("БалансоваяСтоимостьУпр");
			// Увеличиваем доход
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ПодразделениеПолучатель;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьПрочиеАктивыВЭксплуатации;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	//<<Павличенко 12.01.18
	СуммаНДС=Движения.ПартииТоваровКомпании.Итог("СуммаНДС");
	//>>Павличенко 12.01.18
	
	// если тут же начисляется амортизация (100 процентный способ начисления), начисляем расходы
	Если НаборЗаписейЭксплуатация.СуммаАмортизацииУпр100 > 0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		РезультатЗапросПоСтатьям = ПолучитьРезультатЗапросаПоСтатьям(ЭтотОбъект).Выгрузить();
		Для Каждого Строка Из РезультатЗапросПоСтатьям Цикл
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Строка.СтатьяРасходовПоАмортизации;
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение      = ПодразделениеПолучатель;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте             = Ложь;
			НаборЗаписейДиР.Расход                 = Строка.БалансоваяСтоимость;
			//<<Павличенко 12.01.18
			НаборЗаписейДиР.РасходБезНДС           = Строка.БалансоваяСтоимость-СуммаНДС;
			//>>Павличенко 12.01.18
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ФлагПерерасчет = Истина;
		Записать();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ           

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// инициализация кэша валюты и курса документа
ВалютаКурсДок = Новый Структура("Валюта, Курс", ВалютаДокумента, КурсДокумента);
ФлагПерерасчет = Ложь;
// Модуль документа ЗаказКодовМаркировки

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя, ТекСтрока = Неопределено, ЭтаФорма = Неопределено, ДопПараметры = Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя = "Дата" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Возврат Рез;
	
	ИначеЕсли Имя = "ХозОперация" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя = "Организация" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя = "ПодразделениеКомпании" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя = "Контрагент" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		
		// Проверим номенклатуру
		Если ЗначениеЗаполнено(ТекСтрока.Номенклатура)
			И НЕ ТекСтрока.Номенклатура.ТипНоменклатуры.ВедетсяМаркировка Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру по которой не ведется учет по маркировке",5);
			#КонецЕсли
			ТекСтрока.Номенклатура = Неопределено;
		КонецЕсли;
		
		ТекСтрока.КодТНВЭД = ТекСтрока.Номенклатура.КодТНВЭД;
		
		// Заполним имя модели наименованием номенклатуры
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Модель)
			И ТоварнаяГруппа = Справочники.ТипыМаркировки.ШиныИАвтопокрышки
			И СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.МаркировкаОстатков Тогда
			ТекСтрока.Модель = ТекСтрока.Номенклатура.Наименование;
		КонецЕсли;
		
		ТекСтрока.GTIN = "";
		Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	ШтрихКоды.ШтрихКод КАК ШтрихКод
			               |ИЗ
			               |	РегистрСведений.ШтрихКоды КАК ШтрихКоды
			               |ГДЕ
			               |	ШтрихКоды.Объект = &Объект
			               |	И ШтрихКоды.GTIN";
			Запрос.УстановитьПараметр("Объект", ТекСтрока.Номенклатура);
			
			ВыборкаШК = Запрос.Выполнить().Выбрать();
			
			Если ВыборкаШК.Количество() = 1 И ВыборкаШК.Следующий() Тогда
				
				Штрихкод = ВыборкаШК.ШтрихКод;
				
				Пока СтрДлина(Штрихкод) < 14 Цикл
					Штрихкод = "0" + Штрихкод;
				КонецЦикла;
				
				Если СтрДлина(Штрихкод) > 14 Тогда
					Штрихкод = Лев(Штрихкод, 14);
				КонецЕсли;
				
				ТекСтрока.GTIN = Штрихкод;
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "Товары.ХарактеристикаНоменклатуры" Тогда
		
		// Заполним имя модели наименованием номенклатуры
		Если ТоварнаяГруппа = Справочники.ТипыМаркировки.ШиныИАвтопокрышки
			И СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.МаркировкаОстатков Тогда
			ТекСтрока.Модель = СтрШаблон("%1 %2", ТекСтрока.Номенклатура.Наименование, ТекСтрока.ХарактеристикаНоменклатуры.Наименование);
		КонецЕсли;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
//
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары = Новый Структура();
	ОбязательныеТовары.Вставить("Количество", 1);
	ОбязательныеТовары.Вставить("СпособЗаполненияСерийногоНомера", 1);
	
	Если СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.МаркировкаОстатков Тогда
		ОбязательныеТовары.Вставить("КодТНВЭД", 1);
		ОбязательныеТовары.Вставить("Модель", 1);
	Иначе
		ОбязательныеТовары.Вставить("GTIN", 3);
	КонецЕсли;
	ОбязательныеТовары.Вставить("Номенклатура", 1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("СпособВыпускаВОборот", 1);
	ОбязательныеРеквизиты.Вставить("Ответственный", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
	ОбязательныеРеквизиты.Вставить("Статус", 1);
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("ТоварнаяГруппа",1);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки = "", ДопРеквизиты = Неопределено, Заполнение = Истина, Уникальность  =Истина) Экспорт
	
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Органичения по отправке данных
	Если Товары.Количество() > 10 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(Ошибки, "Таблица <Товары>: допускается ввод не более 10 строк");
	КонецЕсли;
	
	ЭтоМаркировкаОстатков = (СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.МаркировкаОстатков);
	
	ПревышениеКоличества = Новый Массив;
	РазныйСпособФормированияНомеров = Ложь;
	НесоответствиеНомеров = Новый Массив;
	НекорректныйGTIN = Новый Массив;
	ДублированиеGTIN = Новый Массив;
	СписокGTIN  = Новый Массив;
	
	ТекущийСпособГенерацииСерийныхНомеров = Неопределено;
	
	// Проверим количество кодов маркировки в товарах
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если ТекущаяСтрока.Количество > 150000 Тогда
			ПревышениеКоличества.Добавить(ТекущаяСтрока.НомерСтроки);
		КонецЕсли;
		
		Если ТекущийСпособГенерацииСерийныхНомеров = Неопределено Тогда
			ТекущийСпособГенерацииСерийныхНомеров = ТекущаяСтрока.СпособЗаполненияСерийногоНомера;
		ИначеЕсли ТекущийСпособГенерацииСерийныхНомеров <> ТекущаяСтрока.СпособЗаполненияСерийногоНомера Тогда
			РазныйСпособФормированияНомеров = Истина;
		КонецЕсли;
		
		НайденныеСтроки = СерийныеНомераКодовМаркировки.НайтиСтроки(
			Новый Структура("ИдентификаторСтроки", ТекущаяСтрока.ИдентификаторСтроки));
		
		Если ТекущаяСтрока.СпособЗаполненияСерийногоНомера = Перечисления.СпособыЗаполненияСерийногоНомера.Ручной
			И ТекущаяСтрока.Количество <> НайденныеСтроки.Количество() Тогда
			НесоответствиеНомеров.Добавить(ТекущаяСтрока.НомерСтроки);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ТекущаяСтрока.GTIN) И СтрДлина(СокрЛП(ТекущаяСтрока.GTIN)) < 14 Тогда
			НекорректныйGTIN.Добавить(ТекущаяСтрока.НомерСтроки);
		КонецЕсли;
		
		Если ЭтоМаркировкаОстатков Тогда
			
			Если ЗначениеЗаполнено(ТекущаяСтрока.GTIN)
				И СписокGTIN.Найти(ТекущаяСтрока.GTIN) = Неопределено Тогда
				
				НайденныеСтроки = Товары.НайтиСтроки(Новый Структура("GTIN", ТекущаяСтрока.GTIN));
				
				Если НайденныеСтроки.Количество() > 1 Тогда
					Для Каждого СтрокаТовара Из НайденныеСтроки Цикл
						ДублированиеGTIN.Добавить(СтрокаТовара.НомерСтроки);
					КонецЦикла;
					СписокGTIN.Добавить(ТекущаяСтрока.GTIN);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПревышениеКоличества.Количество() > 0 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(
			Ошибки,
			"Таблица <Товары>: превышено допустимое количество в строке, указано больше 150000 кодов. Строки: "+СтрСоединить(ПревышениеКоличества, ", "));
	КонецЕсли;
	
	Если РазныйСпособФормированияНомеров Тогда
		Результат = Ложь;
		дкДобавитьОшибку(
			Ошибки,
			"Таблица <Товары>: указаны различные способы генерации серийных номеров. Необходимо привести к одному способу.");
	КонецЕсли;
	
	Если НесоответствиеНомеров.Количество() > 0 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(
			Ошибки,
			"Таблица <Товары>: несоответствие количества заказываемых кодов маркировки и количества серийных номеров. Строки: "+СтрСоединить(НесоответствиеНомеров, ", "));
	КонецЕсли;
	
	Если НекорректныйGTIN.Количество() > 0 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(
			Ошибки,
			"Таблица <Товары>: GTIN должен состоять из 14 символов. Дополните недостоющие символы нулями слева. Строки: "+СтрСоединить(НекорректныйGTIN, ", "));
	КонецЕсли;
	
	Если ДублированиеGTIN.Количество() > 0 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(
			Ошибки,
			"Таблица <Товары>: GTIN в строках не уникальны: "+СтрСоединить(ДублированиеGTIN, ", "));
	КонецЕсли;
	
	Результат = Результат И дкПроверитьСоответствиеНоменклатурыТоварнойГруппе(ЭтотОбъект,,, Ошибки);
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты = Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗаказКодовМаркировки","Заказ кодов маркировки",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати кодов маркировки
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьКодовМаркировки(ТабДокумент) Экспорт
	
	ФормаПечати = ПолучитьФорму("Обработка.ПечатьКодовМаркировки.Форма");
	ФормаПечати.ЗаказНаЭмиссию = Ссылка;
	ФормаПечати.Открыть();
	
КонецФункции // ПечатьКодовМаркировки()

//Функция печати этикеток и ценников
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	СтруктураПараметров=Неопределено;
	дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()


// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ЭтотОбъект.Автор.Сотрудник;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Статус) Тогда
		Статус = Перечисления.СтатусыЗаказаКодовМаркировки.Новый;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторПроизводственногоЗаказа) Тогда
		ИдентификаторПроизводственногоЗаказа = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		Товары.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		               |	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		               |ИЗ
		               |	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		               |ГДЕ
		               |	ЗаказПоставщикуТовары.Ссылка = &Ссылка
		               |	И ЗаказПоставщикуТовары.Номенклатура.ТипНоменклатуры.ВедетсяМаркировка";
		Запрос.УстановитьПараметр("Ссылка", Основание);
		
		ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
		
		Товары.Загрузить(ТаблицаТоваров);
		
		СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.ИмпортВРФ;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВводВОборотКодовМаркировки") Тогда
		
		Если Основание.СпособВводаВОборот = Перечисления.СпособыВводаВОборот.МаркировкаОстатков Тогда
			СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.МаркировкаОстатков;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Перемаркировка") Тогда
		
		СпособВыпускаВОборот = Перечисления.СпособыВыпускаВОборот.Перемаркировка;
		
		Товары.Очистить();
		
		Для Каждого ТекущаяСтрока Из Основание.Товары Цикл
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.КодМаркировкиНовый) Тогда
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = ТекущаяСтрока.НоменклатураНовая;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатурыНовая;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ИдентификаторСтроки) Тогда
			ТекущаяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.СпособЗаполненияСерийногоНомера) Тогда
			ТекущаяСтрока.СпособЗаполненияСерийногоНомера = Перечисления.СпособыЗаполненияСерийногоНомера.Автоматически;
		КонецЕсли;
		ОбработкаРеквизита("Товары.Номенклатура", ТекущаяСтрока);
	КонецЦикла;
	
	Если обЗначениеНеЗаполнено(ТоварнаяГруппа) Тогда
		ТоварнаяГруппа = дкТоварнаяГруппа(Товары.ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;
	
	// Если больше 10 строк номенклатур, то сформируем несколько заказов
	Если НЕ ДополнительныеСвойства.Свойство("НеПроверятьОграничениеСтрок")
		И Товары.Количество() > 10 Тогда
		УправлениеКодамиМаркировкиКлиентСервер.СформироватьЗаказыНаКодыМаркировки(ЭтотОбъект);
		ДокументОснование = Неопределено;
		дкОтменаВводаДокумента(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	Статус = Перечисления.СтатусыЗаказаКодовМаркировки.Новый;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Пока заполним серийные номера автоматически
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.СпособЗаполненияСерийногоНомера) Тогда
			ТекущаяСтрока.СпособЗаполненияСерийногоНомера = Перечисления.СпособыЗаполненияСерийногоНомера.Автоматически;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления
		И (Статус = Перечисления.СтатусыЗаказаКодовМаркировки.Отправлен
		ИЛИ Статус = Перечисления.СтатусыЗаказаКодовМаркировки.Выполнен) Тогда
		
		#Если Клиент Тогда
			Сообщить("Заказ на эмиссию кодов маркировки выполнен или отправлен. Данный заказ нельзя удалять.");
		#КонецЕсли
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		орЖурналСостояний(Ссылка, Статус);
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередУдалением()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ


// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

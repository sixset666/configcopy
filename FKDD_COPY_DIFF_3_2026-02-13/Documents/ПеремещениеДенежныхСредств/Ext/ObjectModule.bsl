// Модуль документа ПриходныйКассовыйОрдер

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("КассаКомпании",1);
	ОбязательныеРеквизиты.Вставить("КассаКомпанииПолучатель",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СуммаДокумента",1);
	ОбязательныеРеквизиты.Вставить("СтатьяДДС",1);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только в случае если
	// все нормально с остальными реквизитами
	// Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
		
		Если ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредств Тогда
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("КассаКомпании");
			КонтролируемыеРеквизитыШапки.Вставить("КассаКомпанииПолучатель");
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		ИначеЕсли ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствНаРасчетныйСчет Тогда
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("КассаКомпании");
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			КонтролируемыеРеквизитыБанковскиеСчета = Новый Структура();
			КонтролируемыеРеквизитыБанковскиеСчета.Вставить("КассаКомпанииПолучатель");
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыБанковскиеСчета",КонтролируемыеРеквизитыБанковскиеСчета);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		ИначеЕсли ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации Тогда
			КонтролируемыеРеквизитыБанковскиеСчета = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("КассаКомпании");
			КонтролируемыеРеквизитыБанковскиеСчета.Вставить("КассаКомпанииПолучатель");
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыБанковскиеСчета",КонтролируемыеРеквизитыБанковскиеСчета);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПеремещениеДенежныхСредств","Перемещение денежных средств",Истина);
	СписокХозОпераций.Добавить("ПеремещениеДенежныхСредствНаРасчетныйСчет","Перемещение денежных средств на расчетный счет");
	СписокХозОпераций.Добавить("ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации","Перемещение денежных средств между расчетными счетами организации");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="ВалютаДокумента" ИЛИ Имя="КурсДокумента" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		// Отобразим остаток денег в кассе
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				дкВывестиЗаголовокДенежныеСредства(ЭтаФорма,ЭтаФорма.ЭлементыФормы.тДенежныеСредства,КассаКомпании);
				дкВывестиЗаголовокДенежныеСредства(ЭтаФорма,ЭтаФорма.ЭлементыФормы.тДенежныеСредстваПолучатель,КассаКомпанииПолучатель);
			КонецЕсли;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="КассаКомпании" Тогда
		Если ТипЗнч(КассаКомпанииПолучатель) = Тип("СправочникСсылка.КассыКомпании") Тогда 
			Если НЕ(обЗначениеНеЗаполнено(КассаКомпании) ИЛИ КассаКомпании.МноговалютнаяКасса) Тогда
				// Если выбрана одновалютная касса
				Если ТипЗнч(КассаКомпанииПолучатель) = Тип("СправочникСсылка.КассыКомпании") Тогда
					Если НЕ (обЗначениеНеЗаполнено(КассаКомпанииПолучатель) ИЛИ КассаКомпанииПолучатель.МноговалютнаяКасса) Тогда
						// Касса-получатель тоже заполнена и одновалютная, проверим на соответствие валют
						Если КассаКомпании.ВалютаДенежныхСредств <> КассаКомпанииПолучатель.ВалютаДенежныхСредств Тогда
							Если ЭтаФорма <> Неопределено Тогда
								#Если Клиент Тогда
									Предупреждение("Валюта кассы компании <"+КассаКомпании.ВалютаДенежныхСредств+"> не соответствует валюте кассы-получателя <"+КассаКомпанииПолучатель.ВалютаДенежныхСредств+">!",10);
								#КонецЕсли
							КонецЕсли;
							КассаКомпании = Справочники.КассыКомпании.ПустаяСсылка();
						КонецЕсли;
					Иначе
						// Касса-получатель либо не заполнена либо многовалютная - значит нам рулить валютой документа
						ВалютаДокумента = КассаКомпании.ВалютаДенежныхСредств;
						дкОбработкаРеквизита("ВалютаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
						Попытка
							СтруктураКурса	= обКурсДляВалюты(ВалютаДокумента, ЭтотОбъект.Дата);
							КурсДокумента	= ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
							дкОбработкаРеквизита("КурсДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
						Исключение КонецПопытки;
					КонецЕсли;
					
				ИначеЕсли ТипЗнч(КассаКомпанииПолучатель) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
					
					Если НЕ обЗначениеНеЗаполнено(КассаКомпанииПолучатель) Тогда
						// Касса-получатель тоже заполнена и одновалютная, проверим на соответствие валют
						Если КассаКомпании.ВалютаДенежныхСредств <> КассаКомпанииПолучатель.ВалютаДенежныхСредств Тогда
							Если ЭтаФорма <> Неопределено Тогда
								#Если Клиент Тогда
									Предупреждение("Валюта кассы компании <"+КассаКомпании.ВалютаДенежныхСредств+"> не соответствует валюте расчетного счета <"+КассаКомпанииПолучатель.ВалютаДенежныхСредств+">!",10);
								#КонецЕсли
							КонецЕсли;
							КассаКомпании = Справочники.КассыКомпании.ПустаяСсылка();
						КонецЕсли;
					Иначе
						// Касса-получатель либо не заполнена либо многовалютная - значит нам рулить валютой документа
						ВалютаДокумента = КассаКомпании.ВалютаДенежныхСредств;
						дкОбработкаРеквизита("ВалютаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
						Попытка
							СтруктураКурса	= обКурсДляВалюты(ВалютаДокумента, ЭтотОбъект.Дата);
							КурсДокумента	= ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
							дкОбработкаРеквизита("КурсДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
						Исключение КонецПопытки;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(КассаКомпании) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
			
			Если НЕ обЗначениеНеЗаполнено(КассаКомпании) Тогда
				Если НЕ обЗначениеНеЗаполнено(КассаКомпанииПолучатель) Тогда
					// Расчетный счет получатель заполнен, проверим на соответствие валют
					Если КассаКомпании.ВалютаДенежныхСредств <> КассаКомпанииПолучатель.ВалютаДенежныхСредств Тогда
						Если ЭтаФорма <> Неопределено Тогда
							#Если Клиент Тогда
								Предупреждение("Валюта расчетного счета компании <" + КассаКомпании.ВалютаДенежныхСредств + "> не соответствует валюте расчетного счета получателя <" + КассаКомпанииПолучатель.ВалютаДенежныхСредств + ">!",10);
							#КонецЕсли
						КонецЕсли;
						КассаКомпании = Справочники.БанковскиеСчета.ПустаяСсылка();
					КонецЕсли;
				Иначе
					// Расчетный счет компании не заполнен - значит нам рулить валютой документа
					ВалютаДокумента = КассаКомпании.ВалютаДенежныхСредств;
					дкОбработкаРеквизита("ВалютаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
					Попытка
						СтруктураКурса	= обКурсДляВалюты(ВалютаДокумента, ЭтотОбъект.Дата);
						КурсДокумента	= ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
						дкОбработкаРеквизита("КурсДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
					Исключение КонецПопытки;
					
				КонецЕсли;
			КонецЕсли;
			
			//Проверим соответствие расчетного счета к организации
			Если КассаКомпании <> Справочники.БанковскиеСчета.ПустаяСсылка() Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	БанковскиеСчета.Ссылка
				|ИЗ
				|	Справочник.БанковскиеСчета КАК БанковскиеСчета
				|ГДЕ
				|	БанковскиеСчета.Владелец В
				|			(ВЫБРАТЬ
				|				ВложенныйЗапрос.Ссылка
				|			ИЗ
				|				(ВЫБРАТЬ
				|					Организации.Ссылка КАК Ссылка
				|				ИЗ
				|					Справочник.Организации КАК Организации
				|		
				|				ОБЪЕДИНИТЬ ВСЕ
				|		
				|				ВЫБРАТЬ
				|					ПодразделенияКомпании.Ссылка
				|				ИЗ
				|					Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
				|				) КАК ВложенныйЗапрос)";
				
				РезультатЗапроса = Запрос.Выполнить().Выгрузить();
				
				РасчетныйСчетОрганизаций = Новый Массив;
				РасчетныйСчетОрганизаций = РезультатЗапроса.ВыгрузитьКолонку("Ссылка");
				ПроверкаСчета = РасчетныйСчетОрганизаций.Найти(КассаКомпании);
				Если ПроверкаСчета = Неопределено Тогда
					Если ЭтаФорма <> Неопределено Тогда
						#Если Клиент Тогда
							Предупреждение("Расчетный счет <" + КассаКомпании + " > не относится к организации или подразделению" ,10);
						#КонецЕсли
					КонецЕсли;
					КассаКомпании = Справочники.БанковскиеСчета.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		// Отобразим остаток денег в кассе
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				дкВывестиЗаголовокДенежныеСредства(ЭтаФорма,ЭтаФорма.ЭлементыФормы.тДенежныеСредства,КассаКомпании);
				дкВывестиЗаголовокДенежныеСредства(ЭтаФорма,ЭтаФорма.ЭлементыФормы.тДенежныеСредстваПолучатель,КассаКомпанииПолучатель);
			КонецЕсли;
		#КонецЕсли
		
	ИначеЕсли Имя="КассаКомпанииПолучатель" Тогда
		Если ТипЗнч(КассаКомпанииПолучатель) = Тип("СправочникСсылка.КассыКомпании") Тогда
			Если НЕ(обЗначениеНеЗаполнено(КассаКомпанииПолучатель) ИЛИ КассаКомпанииПолучатель.МноговалютнаяКасса) Тогда
				// Если выбрана одновалютная касса
				Если НЕ (обЗначениеНеЗаполнено(КассаКомпании) ИЛИ КассаКомпании.МноговалютнаяКасса) Тогда
					// Касса компании тоже заполнена и одновалютная, проверим на соответствие валют
					Если КассаКомпании.ВалютаДенежныхСредств <> КассаКомпанииПолучатель.ВалютаДенежныхСредств Тогда
						Если ЭтаФорма <> Неопределено Тогда
							#Если Клиент Тогда
								Предупреждение("Валюта кассы-получателя <"+КассаКомпанииПолучатель.ВалютаДенежныхСредств+"> не соответствует валюте кассы компании <"+КассаКомпании.ВалютаДенежныхСредств+">!",10);
							#КонецЕсли
						КонецЕсли;
						КассаКомпанииПолучатель = Справочники.КассыКомпании.ПустаяСсылка();
					КонецЕсли;
				Иначе
					// Касса компании либо не заполнена либо многовалютная - значит нам рулить валютой документа
					ВалютаДокумента = КассаКомпанииПолучатель.ВалютаДенежныхСредств;
					дкОбработкаРеквизита("ВалютаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
					Попытка
						СтруктураКурса	= обКурсДляВалюты(ВалютаДокумента, ЭтотОбъект.Дата);
						КурсДокумента	= ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
						дкОбработкаРеквизита("КурсДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
					Исключение КонецПопытки;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(КассаКомпанииПолучатель) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
			Если НЕ обЗначениеНеЗаполнено(КассаКомпанииПолучатель) Тогда
				Если НЕ обЗначениеНеЗаполнено(КассаКомпании) Тогда
					// Расчетный счет получатель заполнен, проверим на соответствие валют
					Если КассаКомпании.ВалютаДенежныхСредств <> КассаКомпанииПолучатель.ВалютаДенежныхСредств Тогда
						Если ЭтаФорма <> Неопределено Тогда
							#Если Клиент Тогда
								Если ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации Тогда
									Предупреждение("Валюта расчетного счета компании получателя <" + КассаКомпанииПолучатель.ВалютаДенежныхСредств + "> не соответствует валюте расчетного счета <" + КассаКомпании.ВалютаДенежныхСредств + ">!",10);
								Иначе
									Предупреждение("Валюта расчетного счета компании <" + КассаКомпанииПолучатель.ВалютаДенежныхСредств + "> не соответствует валюте кассы компании <" + КассаКомпании.ВалютаДенежныхСредств + ">!",10);
								КонецЕсли;
							#КонецЕсли
						КонецЕсли;
						КассаКомпанииПолучатель = Справочники.БанковскиеСчета.ПустаяСсылка();
						
					КонецЕсли;
				Иначе
					// Расчетный счет компании не заполнен - значит нам рулить валютой документа
					ВалютаДокумента = КассаКомпанииПолучатель.ВалютаДенежныхСредств;
					дкОбработкаРеквизита("ВалютаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
					Попытка
						СтруктураКурса	= обКурсДляВалюты(ВалютаДокумента, ЭтотОбъект.Дата);
						КурсДокумента	= ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
						дкОбработкаРеквизита("КурсДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
					Исключение КонецПопытки;
					
				КонецЕсли;
			КонецЕсли;
			//Проверим соответствие расчетного счета к организации
			Если КассаКомпанииПолучатель <> Справочники.БанковскиеСчета.ПустаяСсылка() Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	БанковскиеСчета.Ссылка
				|ИЗ
				|	Справочник.БанковскиеСчета КАК БанковскиеСчета
				|ГДЕ
				|	БанковскиеСчета.Владелец В
				|			(ВЫБРАТЬ
				|				ВложенныйЗапрос.Ссылка
				|			ИЗ
				|				(ВЫБРАТЬ
				|					Организации.Ссылка КАК Ссылка
				|				ИЗ
				|					Справочник.Организации КАК Организации
				|		
				|				ОБЪЕДИНИТЬ ВСЕ
				|		
				|				ВЫБРАТЬ
				|					ПодразделенияКомпании.Ссылка
				|				ИЗ
				|					Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
				|				) КАК ВложенныйЗапрос)";
				
				РезультатЗапроса = Запрос.Выполнить().Выгрузить();
				
				РасчетныйСчетОрганизаций = Новый Массив;
				РасчетныйСчетОрганизаций = РезультатЗапроса.ВыгрузитьКолонку("Ссылка");
				ПроверкаСчета = РасчетныйСчетОрганизаций.Найти(КассаКомпанииПолучатель);
				Если ПроверкаСчета = Неопределено Тогда
					Если ЭтаФорма <> Неопределено Тогда
						#Если Клиент Тогда
							Предупреждение("Расчетный счет <" + КассаКомпанииПолучатель + " > не относится к организации или подразделению" ,10);
						#КонецЕсли
					КонецЕсли;
					КассаКомпанииПолучатель = Справочники.БанковскиеСчета.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		// Отобразим остаток денег в кассе
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				дкВывестиЗаголовокДенежныеСредства(ЭтаФорма,ЭтаФорма.ЭлементыФормы.тДенежныеСредства,КассаКомпании);
				дкВывестиЗаголовокДенежныеСредства(ЭтаФорма,ЭтаФорма.ЭлементыФормы.тДенежныеСредстваПолучатель,КассаКомпанииПолучатель);
			КонецЕсли;
		#КонецЕсли
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПеремещениеДенежныхСредств"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПеремещениеДенежныхСредств(ТабДокумент) Экспорт
	Если Не ХозОперация.Наименование = "Перемещение денежных средств" Тогда
		Сообщить("Печать <Перемещение денежных средств> не разрешена для хозяйственной операции <" + ХозОперация.Наименование + ">");
		Возврат Неопределено; 
	Иначе
		Макет = ПолучитьМакет("ПеремещениеДенежныхСредств");
		
		//вывод заголовка документа
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаДокумента");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
		ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
		ОбластьМакета.Параметры.КассаКомпании = спПолучитьНаименование(КассаКомпании);
		ОбластьМакета.Параметры.КассаПолучатель = спПолучитьНаименование(КассаКомпанииПолучатель);    
		
		// Получим остаток
		СтруктураОтбораДляКассыКомпании	            = Новый Структура("Валюта, СтруктурнаяЕдиница", ВалютаДокумента, КассаКомпании);
		СтруктураОтбораДляКассыКомпанииПолучателя	= Новый Структура("Валюта, СтруктурнаяЕдиница", ВалютаДокумента, КассаКомпанииПолучатель);
		ОстатокКомпании			                    = РегистрыНакопления.ДенежныеСредстваКомпании.Остатки(, СтруктураОтбораДляКассыКомпании,, "Сумма").Итог("Сумма");
		ОстатокКомпанииПолучателя			        = РегистрыНакопления.ДенежныеСредстваКомпании.Остатки(, СтруктураОтбораДляКассыКомпанииПолучателя,, "Сумма").Итог("Сумма");
		
		ОбластьМакета.Параметры.ОстатокВКассеКомпании           = Формат(ОстатокКомпании, "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(ВалютаДокумента.Наименование);
		ОбластьМакета.Параметры.ОстатокВКассеКомпанииПолучателя = Формат(ОстатокКомпанииПолучателя, "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(ВалютаДокумента.Наименование);
		
		ОбластьМакета.Параметры.Сумма       = Формат(СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(ВалютаДокумента.Наименование);
		ОбластьМакета.Параметры.СтатьяДДС   = СтатьяДДС.Наименование;
		ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(СуммаДокумента,ВалютаДокумента);
		
		//вывод свойств
		СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
		ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
		ТабДокумент.Вывести(ОбластьМакета);
		
		Возврат ТабДокумент;
	КонецЕсли;
	
	
КонецФункции // ПечатьПеремещениеДенежныхСредств()

// Формирует печатную форму "Перемещение денежных средств на расчетный счет"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПеремещениеДенежныхСредствНаРасчетныйСчет(ТабДокумент) Экспорт
	Если Не ХозОперация.Наименование = "Перемещение денежных средств на расчетный счет" Тогда
		Сообщить("Печать <Перемещение денежных средств на расчетный счет> не разрешена для хозяйственной операции <" + ХозОперация.Наименование + ">");
		Возврат Неопределено;
	Иначе
		Макет = Документы.РасходныйКассовыйОрдер.ПолучитьМакет("РасходныйКассовыйОрдер");
		
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		
		//Получаем область шапки документа
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ОбластьМакета.Параметры.СуммаДокумента = Формат(ЭтотОбъект.СуммаДокумента,ФорматВыводаСуммы);
		
		ОбластьМакета.Параметры.ОрганизацияПоОКПО	= ЭтотОбъект.Организация.КодПоОКПО;
		ОбластьМакета.Параметры.КодПодразделения	= ЭтотОбъект.ПодразделениеКомпании.Код;
		
		ОбластьМакета.Параметры.КредитСубСчет		= ?(ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),"50.1","50.2");
		ОбластьМакета.Параметры.ДебетСубСчет		= ЭтотОбъект.СтатьяДДС.КоррСчет.Код;
		
		ОбластьМакета.Параметры.СуммаПрописью		= обЧислоПрописью(ЭтотОбъект.СуммаДокумента,ЭтотОбъект.ВалютаДокумента);
		
		
		Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
		Кассир = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Кассир);
		ОбластьМакета.Параметры.Заполнить(Руководитель);
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(Кассир);
		
		ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
		ОбластьМакета.Параметры.Подразделение = ПодразделениеКомпании;
		ТабДокумент.Вывести(ОбластьМакета);
		
		Возврат ТабДокумент;
	КонецЕсли;
	
КонецФункции // ПечатьПеремещениеДенежныхСредствНаРасчетныйСчет()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		СуммаДокумента = Основание.СуммаДокумента;
	КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеДенежныхСредств Тогда
		КассаКомпанииПолучатель = Справочники.КассыКомпании.ПустаяСсылка();
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствНаРасчетныйСчет Тогда
		КассаКомпанииПолучатель = Справочники.БанковскиеСчета.ПустаяСсылка();
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации Тогда
		Если ТипЗнч(КассаКомпании) = Тип("СправочникСсылка.КассыКомпании") Тогда
			КассаКомпании = Справочники.БанковскиеСчета.ПустаяСсылка();
		КонецЕсли;
		КассаКомпанииПолучатель = Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// расходуем деньги из кассы
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДС.РежимПроведения=Режим;
	НаборЗаписейДС.СтруктурнаяЕдиница=КассаКомпании;
	НаборЗаписейДС.Валюта=Неопределено;
	НаборЗаписейДС.СтатьяДДС=СтатьяДДС;
	НаборЗаписейДС.Сумма=СуммаДокумента;
	Отказ=НЕ НаборЗаписейДС.Расход() ИЛИ Отказ;
	
	// приходуем деньги в кассу
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДС.РежимПроведения=Режим;
	НаборЗаписейДС.СтруктурнаяЕдиница=КассаКомпанииПолучатель;
	НаборЗаписейДС.Валюта=Неопределено;
	НаборЗаписейДС.СтатьяДДС=СтатьяДДС;
	НаборЗаписейДС.Сумма=СуммаДокумента;
	Отказ=НЕ НаборЗаписейДС.Приход() ИЛИ Отказ;
	
	// БАЛАНС: Возможно что подразделения отправителя и получателя не равны в этом случае
	// Уменьшаем доход на подразделении отправителе
	// Увеличиваем доход на подразделении получателе
	БалансВедетсяПоПодразделениям  = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	ПодразделениеОтправитель       = обПолучитьБалансовоеПодразделение(КассаКомпании.Подразделение);
	ПодразделениеПолучатель        = обПолучитьБалансовоеПодразделение(КассаКомпанииПолучатель.Подразделение);
	БалансовыеПодразделенияНеРавны = ПодразделениеОтправитель<>ПодразделениеПолучатель;
	
	Если БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны Тогда
		ТаблицаДвиженийДСК = Движения.ДенежныеСредстваКомпании.Выгрузить();
		ТаблицаДвиженийДСК.Свернуть("ВидДвижения","СуммаУпр");
				
		// Получим сумму расхода
		СтрокаТаблицаДвиженийДСК = ТаблицаДвиженийДСК.Найти(ВидДвиженияНакопления.Расход, "ВидДвижения");
		СуммаРасход = 0;
		Если СтрокаТаблицаДвиженийДСК <> Неопределено Тогда
			СуммаРасход = СтрокаТаблицаДвиженийДСК.СуммаУпр;
		КонецЕсли;
		
		// Получим сумму дохода
		СтрокаТаблицаДвиженийДСК = ТаблицаДвиженийДСК.Найти(ВидДвиженияНакопления.Приход, "ВидДвижения");
		СуммаДоход = 0;
		Если СтрокаТаблицаДвиженийДСК <> Неопределено Тогда
			СуммаДоход = СтрокаТаблицаДвиженийДСК.СуммаУпр;
		КонецЕсли;
				
		// Уменьшаем доход
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.Подразделение = КассаКомпании.Подразделение;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		НаборЗаписейДиР.Доход  = СуммаРасход;
		НаборЗаписейДиР.Расход = 0;
		Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		
		// Увеличиваем доход
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.Подразделение = КассаКомпанииПолучатель.Подразделение;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		НаборЗаписейДиР.Доход = СуммаДоход;
		НаборЗаписейДиР.Расход = 0;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	НаборЗаписей = Движения.ПлатежныйКалендарь;
	НаборЗаписей.ДокументОбъект = Ссылка;

	Отказ = Не НаборЗаписей.ФактПеремещение() ИЛИ Отказ;
	
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);

КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
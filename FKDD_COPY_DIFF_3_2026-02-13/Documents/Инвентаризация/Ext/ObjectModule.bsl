// Модуль документа Инвентаризация

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем РезультатЗапросаПоТоварамИзлишки; // Хранит результат выполнения запроса по излишкам
Перем РезультатЗапросаПоТоварамНедостачи; // Хранит результат выполнения запроса по недостачам
Перем ПолучатьКнижноеКоличествоИзПартий Экспорт; // Признак получения книжного значения количества из партий
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
		ОбязательныеТовары.Вставить("ДокументПередачи",3);
	КонецЕсли;
	Если обПраво("ВыборочноеСписаниеПартий", Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
		ОбязательныеТовары.Вставить("ГТД",2);
	КонецЕсли;

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
    Иначе
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СтатьяСписанияОбнаруженнойНедостачиТМЦ",1);
	ОбязательныеРеквизиты.Вставить("СтатьяОприходованияОбнаруженныхИзлишковТМЦ",1);
	ОбязательныеРеквизиты.Вставить("РежимКорректировкиКоличества",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации
	// будем только в случае если все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
		
		КонтролируемыеРеквизитыШапки = Новый Структура();
		Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов");
			КонтролируемыеРеквизитыТовары = Новый Структура();
			КонтролируемыеРеквизитыТовары.Вставить("ДокументПередачи");
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
		Иначе
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании");
			Если обПраво("ВыборочноеСписаниеПартий", Права,,ЭтотОбъект) Тогда
				КонтролируемыеРеквизитыТовары = Новый Структура();
				КонтролируемыеРеквизитыТовары.Вставить("Партия");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;	
		КонецЕсли;	
		
		КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран
	// "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты      = Новый Структура(); 			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
				Если БалансВедетсяПоОрганизации Тогда
					КонтролируемыеРеквизитыТовары = Новый Структура();
					КонтролируемыеРеквизитыТовары.Вставить("ДокументПередачи");
					ТабличныеЧасти = Новый Структура();
					ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
					КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
				КонецЕсли;
			Иначе
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
				
				Если БалансВедетсяПоОрганизации И обПраво("ВыборочноеСписаниеПартий", Права,,ЭтотОбъект) Тогда
					КонтролируемыеРеквизитыТовары = Новый Структура();
					КонтролируемыеРеквизитыТовары.Вставить("Партия");
					ТабличныеЧасти = Новый Структура();
					ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
					КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
				КонецЕсли;	
			КонецЕсли;	
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);  			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	дкПроверитьКорректностьРНПТ(ЭтотОбъект,,, "ГТДИзлишков");
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения характеристики номенклатуры
//                                                                
// Параметры:
//  СтрокаТовары - строка табличной части документа - ссылка на строку.
//  Ошибки       - строка - содержит описание ошибки.
//
// Возвращаемое значение:
//  Булево 
//
Функция ПроверитьЗаполнениеХарактеристики(СтрокаТовары, Ошибки="") Экспорт
	УчетВедется = СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ  СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2;	  
	РучноеСписаниеХарактеристик = СтрокаТовары.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ
												  			  СтрокаТовары.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик.Пустая();
	Приход = СтрокаТовары.Количество<СтрокаТовары.КоличествоУчет;
	Расход = СтрокаТовары.Количество>СтрокаТовары.КоличествоУчет;	
	Если УчетВедется И (Приход ИЛИ (Расход И РучноеСписаниеХарактеристик)) И обЗначениеНеЗаполнено(СтрокаТовары.ХарактеристикаНоменклатуры)  Тогда
	 	Возврат Ложь;
	КонецЕсли;	
	Возврат Истина;
КонецФункции // ПроверитьЗаполнениеХарактеристики()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ИнвентаризацияТоваров","Инвентаризация товаров",Истина);
	СписокХозОпераций.Добавить("ИнвентаризацияТоваровОтданныхНаКомиссию","Инвентаризация товаров отданных на комиссию");

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  ШапкаДокумента - ДокументСсылка или Выборка.
//  Режим          - Строка - указывает режим запроса.
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента,Режим="Излишки")
	//ВключаетНДС = ТипЦен.ЦенаВключаетНДС;
    Если Режим="Излишки" Тогда
        ТекстЗапроса="
        |ВЫБРАТЬ
        |	ИнвентаризацияТовары.Номенклатура,
        |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры,
        |	ИнвентаризацияТовары.ДокументПередачи КАК ДокументПередачи,";
        Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
            ТекстЗапроса=ТекстЗапроса+"
            |	ИнвентаризацияТовары.Партия КАК Партия,";
        КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|	ИнвентаризацияТовары.ГТДИзлишков КАК ГТД,";
        
		ТекстЗапроса=ТекстЗапроса+"
		|	0 КАК СуммаНДС,";
        
        Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
            Если НЕ обЗначениеНеЗаполнено(ШапкаДокумента.СкладКомпании) И ШапкаДокумента.СкладКомпании.Розничный Тогда
                ТекстЗапроса=ТекстЗапроса+"
                |	ИнвентаризацияТовары.СуммаОтпускная КАК СуммаРозничная,
                |	ИнвентаризацияТовары.ЦенаОтпускная КАК ЦенаРозничная,";
            Иначе
                ТекстЗапроса=ТекстЗапроса+"
                |	0 КАК СуммаРозничная,
                |	0 КАК ЦенаРозничная,";
            КонецЕсли;	
        Иначе
			//Если Не ВключаетНДС Тогда
			//	ТекстЗапроса=ТекстЗапроса+"
			//	|	(ИнвентаризацияТовары.Сумма + ИнвентаризацияТовары.Сумма*ИнвентаризацияТовары.Номенклатура.СтавкаНДС.Ставка/100) КАК СуммаВсего,";
			//Иначе
			//	ТекстЗапроса=ТекстЗапроса+"
			//	|	ИнвентаризацияТовары.Сумма КАК СуммаВсего,";
			//КонецЕсли;
        КонецЕсли;
		
        ТекстЗапроса=ТекстЗапроса+"
		|	ИнвентаризацияТовары.Сумма КАК СуммаВсего,
        |	ИнвентаризацияТовары.Количество*ИнвентаризацияТовары.Коэффициент КАК Количество
        |ИЗ
        |	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
        |ГДЕ
        |	  ИнвентаризацияТовары.Ссылка=&Ссылка
        |	И (
        |		ИнвентаризацияТовары.Количество>0
        //|    	ИЛИ ИнвентаризацияТовары.Сумма>0
        //|		ИЛИ ИнвентаризацияТовары.СуммаОтпускная>0
        |	)
        |";
        
        Запрос=Новый Запрос();
        Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
        Запрос.Текст=ТекстЗапроса;
        Возврат Запрос.Выполнить();
        
    Иначе
        // пройдемся по табличной части и сформируем таблицу значений
        Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
            ИмяРегистрПартий = "ПартииТоваровОтданные";
        Иначе
            ИмяРегистрПартий = "ПартииТоваровКомпании";
        КонецЕсли;
        
        ТекстЗапроса="		
        |ВЫБРАТЬ
        |	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
        |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
        |	ИнвентаризацияТовары.ДокументПередачи КАК ДокументПередачи,
        |	ИнвентаризацияТовары.Партия КАК Партия,
        |	ИнвентаризацияТовары.ГТД КАК ГТД,
        |	0 КАК СуммаНДС,";
        
        Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
            ТекстЗапроса=ТекстЗапроса+"
            |	ИнвентаризацияТовары.Сумма*(-1) КАК СуммаВсего,";
            Если НЕ обЗначениеНеЗаполнено(ШапкаДокумента.СкладКомпании) И ШапкаДокумента.СкладКомпании.Розничный Тогда
                ТекстЗапроса=ТекстЗапроса+"
                |	ИнвентаризацияТовары.СуммаОтпускная*(-1) КАК СуммаРозничная,
                |	ИнвентаризацияТовары.ЦенаОтпускная КАК ЦенаРозничная,";
            Иначе
                ТекстЗапроса=ТекстЗапроса+"
                |	0 КАК СуммаРозничная,
                |	0 КАК ЦенаРозничная,";
            КонецЕсли;	
        Иначе
            ТекстЗапроса=ТекстЗапроса+"
            |	ИнвентаризацияТовары.Сумма*(-1) КАК СуммаВсего,";
        КонецЕсли;
        ТекстЗапроса=ТекстЗапроса+"
        |	ИнвентаризацияТовары.Количество*(-1)*ИнвентаризацияТовары.Коэффициент КАК Количество
        |ИЗ
        |	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
        |ГДЕ
        |	  ИнвентаризацияТовары.Ссылка=&Ссылка
        |	И (
        |		ИнвентаризацияТовары.Количество*(-1)>0
        //|    	ИЛИ ИнвентаризацияТовары.Сумма*(-1)>0
        //|		ИЛИ ИнвентаризацияТовары.СуммаОтпускная*(-1)>0
        |	)
        |";
        
        Запрос=Новый Запрос();
        Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
        Запрос.УстановитьПараметр("ПустаяХарактеристика",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
        Запрос.УстановитьПараметр("ПустаяПартия",Неопределено);
        Запрос.Текст=ТекстЗапроса;
        Возврат Запрос.Выполнить(); 
    КонецЕсли;
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СтатьяСписанияОбнаруженнойНедостачиТМЦ КАК СтатьяСписанияОбнаруженнойНедостачиТМЦ,
	|	Док.СтатьяОприходованияОбнаруженныхИзлишковТМЦ КАК СтатьяОприходованияОбнаруженныхИзлишковТМЦ
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
	Если РезультатЗапросаПоТоварамИзлишки=Неопределено Тогда РезультатЗапросаПоТоварамИзлишки=ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента); КонецЕсли;
	Если РезультатЗапросаПоТоварамНедостачи=Неопределено Тогда РезультатЗапросаПоТоварамНедостачи=ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента,"Недостачи"); КонецЕсли;
    // определим вид проводки
	Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		
		// проверяем, присутствуют ли партии в табличной части
		ЕстьПартии = Ложь;
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// 2. Приходуем излишки
		Если НЕ РезультатЗапросаПоТоварамИзлишки.Пустой() Тогда
			НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейПартии.СкладКомпании             = ШапкаДокумента.СкладКомпании;
			НаборЗаписейПартии.ИмяРеквизитаДокумент      = Неопределено;
			НаборЗаписейПартии.СтатусПартии              = Перечисления.СтатусыПартий.ТоварКупленный;
			НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамИзлишки;
			НаборЗаписейПартии.ШапкаДокумента            = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		// 3. Списываем недостачи
		Если НЕ РезультатЗапросаПоТоварамНедостачи.Пустой() Тогда
			НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейПартии.СкладКомпании             = ШапкаДокумента.СкладКомпании;
			НаборЗаписейПартии.ИмяРеквизитаДокумент      = ?(ЕстьПартии, "Партия", "");
			НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамНедостачи;
			НаборЗаписейПартии.СтатусПартии              = Неопределено;
			НаборЗаписейПартии.ШапкаДокумента            = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
		КонецЕсли;
		
		// 4. Зачитываем принятые на реализацию товары, которые были списаны инвентаризацией
		НаборЗаписейРеализованныеТовары = Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Списание                  = Истина;
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамНедостачи;
		НаборЗаписейРеализованныеТовары.ШапкаДокумента            = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
		
		// 5. Доходы и расходы
		ТаблицаЗначений = НаборЗаписейПартии.Выгрузить();
		СуммаРасхода    = 0;
		СуммаДохода     = 0;
		Для Каждого СтрокаТаблицыЗначения Из ТаблицаЗначений Цикл
			Если СтрокаТаблицыЗначения.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
				СуммаРасхода = СуммаРасхода + СтрокаТаблицыЗначения.СуммаУпр;
			Иначе
				СуммаДохода = СуммаДохода + СтрокаТаблицыЗначения.СуммаУпр;
			КонецЕсли;
		КонецЦикла;                                                  
		
		ТаблицаЗначений.Свернуть("ВидДвижения");
		СтрокаТЗ = ТаблицаЗначений.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
		Если СтрокаТЗ<>Неопределено Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяСписанияОбнаруженнойНедостачиТМЦ;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Расход					= СуммаРасхода;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		
		СтрокаТЗ = ТаблицаЗначений.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
		Если СтрокаТЗ <> Неопределено Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда 
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяОприходованияОбнаруженныхИзлишковТМЦ;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= СуммаДохода;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
	Иначе
		// 1. Приходуем излишки
		НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект                = ЭтотОбъект;
		НаборЗаписейПартииОтданные.Контрагент                    = ШапкаДокумента.Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов         = ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам     = РезультатЗапросаПоТоварамИзлишки;
		НаборЗаписейПартииОтданные.ПередаватьНаКомиссиюВесьТовар = Истина;
		НаборЗаписейПартииОтданные.ШапкаДокумента                = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейПартииОтданные.Приход() ИЛИ Отказ;
		// 2. Списываем недостачи
		НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамНедостачи;
		НаборЗаписейПартииОтданные.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейПартииОтданные.ШапкаДокумента            = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейПартииОтданные.Расход() ИЛИ Отказ;
		
		ТаблицаЗначений = НаборЗаписейПартииОтданные.Выгрузить();
		ТаблицаЗначений.Свернуть("ВидДвижения","СуммаУпр,СуммаСебестоимостиУпр");
		
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(Константы.ВалютаУправленческогоУчетаКомпании.Получить(),Дата);
			КурсУпрВалюты  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпрВалюты = ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;	
		
		// 3. Взаиморасчеты на излишний товар
		СтрокаТЗ = ТаблицаЗначений.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
		Если СтрокаТЗ <> Неопределено Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда 
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяОприходованияОбнаруженныхИзлишковТМЦ;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= СтрокаТЗ.СуммаУпр;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		
		// 4. Взаиморасчеты на утраченный товар
		СтрокаТЗ = ТаблицаЗначений.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
		Если СтрокаТЗ <> Неопределено Тогда
			НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
			НаборЗаписейВзаиморасчеты.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.Контрагент                = ШапкаДокумента.Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов     = ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейВзаиморасчеты.Сделка                    = Неопределено;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
			НаборЗаписейВзаиморасчеты.Сумма                     = обПересчет(СтрокаТЗ.СуммаУпр,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),КурсУпрВалюты, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента);
			НаборЗаписейВзаиморасчеты.ШапкаДокумента            = ШапкаДокумента;
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
			Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			
			// Доходы и расходы по суммовым разницам.
			СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
				Если БалансВедетсяПоПодразделениям Тогда
					НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте             = Истина;
				НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
				Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
					НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
				Иначе
					НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
			
			//доходы и расходы
			//сумма себестоимости списанных товаров по недостачи
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = СтрокаТЗ.СуммаСебестоимостиУпр;	
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			
			//СуммаВсего - списанная сумма товарной задолженности
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
			НаборЗаписейДоходыИРасходы.Доход                  = СтрокаТЗ.СуммаУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;  			
	КонецЕсли;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка И ");
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Результат = Запрос.Выполнить(); ДоговорВзаиморасчетовБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			ДоговорВзаиморасчетовБыл = Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл",ДоговорВзаиморасчетовБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ШапкаДокумента.Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	
	// двигаем границу последовательности партий
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И 
			ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Товары.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

//Заполнение табличной части с учетом себестоимости
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму документа,
//  Таблица  - табличная часть документа,
//  Кнопка   - Кнопка -  определяет каким образом заполнить таблицу документа.
//
// Возвращаемое значение:
//  Возвращает таблицу значений - результат запроса.
//
Функция ЗаполнитьТабличнуюЧасть(ЭтаФорма,Таблица,Кнопка) Экспорт
	ИмяПодменю=Кнопка.Имя;
	// надо заполнить таблицу складскими остатками
	Если Найти(ИмяПодменю,"ЗаполнитьСкладскимиОстатками")>0 Тогда
		СкладКомпании=ЭтаФорма.СкладКомпании;
		Если обЗначениеНеЗаполнено(СкладКомпании) Тогда
			#Если Клиент Тогда
				Предупреждение("Не выбран склад !",10); Возврат ИмяПодменю;
			#КонецЕсли
		КонецЕсли;
		// спросим
		Если Таблица.Количество()>0 Тогда
			Если ИмяПодменю="ЗаполнитьСкладскимиОстатками" Тогда
				#Если Клиент Тогда
					Если Вопрос("Перед заполнением таблица будет очищена
						|Продолжить ?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет)<>КодВозвратаДиалога.Да Тогда
						Возврат ИмяПодменю;
					Иначе
						Таблица.Очистить();
					КонецЕсли;
				#Иначе
					Таблица.Очистить();
				#КонецЕсли
			ИначеЕсли ИмяПодменю="ЗаполнитьСкладскимиОстаткамиПоГруппе" Тогда
				#Если Клиент Тогда
					Если Вопрос("Перед заполнением очистить таблицу?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет)=КодВозвратаДиалога.Да Тогда
						Таблица.Очистить();
					КонецЕсли;
				#Иначе
					Таблица.Очистить();
				#КонецЕсли
			КонецЕсли;
		КонецЕсли;
		
		Запрос=Новый Запрос();
		ДопФильтр=" И (НЕ Номенклатура.ВидНоменклатуры = &Услуга) ";
		Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
		// если заполнение по группе номенклатуры, то выберем группу
		Если ИмяПодменю="ЗаполнитьСкладскимиОстаткамиПоГруппе" Тогда
			ФормаВыбораГруппыНоменклатуры=Справочники.Номенклатура.ПолучитьФормуВыбораГруппы();
			ВыбГруппа=ФормаВыбораГруппыНоменклатуры.ОткрытьМодально();
			Если ВыбГруппа=Неопределено Тогда Возврат ИмяПодменю; КонецЕсли;
			Если НЕ ВыбГруппа.ЭтоГруппа Тогда
				#Если Клиент Тогда
					Предупреждение("Необходимо выбрать группу номенклатуры !",10); Возврат ИмяПодменю;
				#Иначе
				#КонецЕсли
			КонецЕсли;
			ДопФильтр=" И Номенклатура В ИЕРАРХИИ(&ВыбГруппа)";
			Запрос.УстановитьПараметр("ВыбГруппа",ВыбГруппа);
		КонецЕсли;
		
		//ТипЦенНормативная = Справочники.ТипыЦен.НормативнаяЦена;
		Если ПолучатьКнижноеКоличествоИзПартий Тогда
			Если ЭтаФорма.флЗаполнятьБезНДС Тогда
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
				|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
				|	ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток КАК СебестоимостьРегл,
				|	ВЫБОР
				|		КОГДА ПартииТоваровКомпанииОстатки.СуммаОстаток <> 0 
				|			ТОГДА ПартииТоваровКомпанииОстатки.СуммаУпрОстаток - ПартииТоваровКомпанииОстатки.СуммаУпрОстаток * (ПартииТоваровКомпанииОстатки.СуммаНДСОстаток / ПартииТоваровКомпанииОстатки.СуммаОстаток)
				|		ИНАЧЕ ПартииТоваровКомпанииОстатки.СуммаУпрОстаток
				|	КОНЕЦ КАК Себестоимость
				|ИЗ
				|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент,СкладКомпании=&СкладКомпании "+ДопФильтр+") КАК ПартииТоваровКомпанииОстатки";
			Иначе
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
				|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
				|	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК СебестоимостьРегл,
				|	ПартииТоваровКомпанииОстатки.СуммаУпрОстаток КАК Себестоимость
				|ИЗ
				|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент,СкладКомпании=&СкладКомпании "+ДопФильтр+") КАК ПартииТоваровКомпанииОстатки";
			КонецЕсли;
		Иначе
			
			// Если расчетная цена получим базовую цену
			//РабочийТипЦен = ТипЦенНормативная;
			РабочийТипЦен = ТипЦен;
			ТекстЦена = "ТаблицаЦен.Цена";
			Счетчик = 1;
			ТекстСоединенийПроцентыСкидкиНаценки = "";
			Пока РабочийТипЦен.Рассчитывается Цикл
				Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
					ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
					ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
					|ЛЕВОЕ СОЕДИНЕНИЕ
					|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
					|ПО
					|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
					|	СрезПоследних.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
					
					ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
					
					Запрос.УстановитьПараметр("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
					
					Счетчик = Счетчик + 1;
				ИначеЕсли РабочийТипЦен.ПроцентСкидкиНаценки<>0 Тогда
					ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
				КонецЕсли;
				РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
			КонецЦикла;
			
			ТаблицаИерархииПодразделений = Новый ТаблицаЗначений;
			ТаблицаИерархииПодразделений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
			ТаблицаИерархииПодразделений.Колонки.Добавить("Уровень",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
			
			ТекущееПодразделение = ПодразделениеКомпании;
			Уровень = 0;
			Пока ЗначениеЗаполнено(ТекущееПодразделение) Цикл
				СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
				СтрокаПодразделения.Подразделение = ТекущееПодразделение;
				СтрокаПодразделения.Уровень = Уровень;
				// Получаем родителя подразделения
				ТекущееПодразделение = ТекущееПодразделение.Родитель;
				Уровень = Уровень + 1;
			КонецЦикла;
			СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
			СтрокаПодразделения.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
			СтрокаПодразделения.Уровень = Уровень;
			
			ВедетсяУчетПоЕдиницам        = (РабочийТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения);
			ВедетсяУчетПоХарактеристикам = (РабочийТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике);
			
			ТекстВыборкиЦен = "";
			ТекстСоединениеЦен = "";
			Если ВедетсяУчетПоЕдиницам Тогда
				
				ТекстВыборкиЦен = "ЕСТЬNULL(ТаблицаЦен.Цена, ЕСТЬNULL(ТаблицаЦенОбщ.Цена, 0))";
				
				ТекстСоединениеЦен = "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	ИтоговаяТаблицаЦен КАК ТаблицаЦен
				|ПО
				|	ОстаткиТоваров.Номенклатура = ТаблицаЦен.Номенклатура И 
				|	(НЕ ТаблицаЦен.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	ИтоговаяТаблицаЦен КАК ТаблицаЦенОбщ
				|ПО
				|	ОстаткиТоваров.Номенклатура = ТаблицаЦенОбщ.Номенклатура И
				|	ТаблицаЦенОбщ.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
				|";
			ИначеЕсли ВедетсяУчетПоХарактеристикам Тогда
				
				ТекстВыборкиЦен = "ЕСТЬNULL(ТаблицаЦен.Цена, ЕСТЬNULL(ТаблицаЦенОбщ.Цена, 0))";
				
				ТекстСоединениеЦен = "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	ИтоговаяТаблицаЦен КАК ТаблицаЦен
				|ПО
				|	ОстаткиТоваров.Номенклатура = ТаблицаЦен.Номенклатура И
				|	ОстаткиТоваров.ХарактеристикаНоменклатуры = ТаблицаЦен.ХарактеристикаНоменклатуры
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	ИтоговаяТаблицаЦен КАК ТаблицаЦенОбщ
				|ПО
				|	ОстаткиТоваров.Номенклатура = ТаблицаЦенОбщ.Номенклатура И 
				|	ТаблицаЦенОбщ.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
				|";
			Иначе
				
				ТекстВыборкиЦен = "ЕСТЬNULL(ТаблицаЦен.Цена, 0)";
				
				ТекстСоединениеЦен = "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	ИтоговаяТаблицаЦен КАК ТаблицаЦен
				|ПО
				|	ОстаткиТоваров.Номенклатура = ТаблицаЦен.Номенклатура
				|";
			КонецЕсли;
			
			ТекстЦена = ТекстЦена + "*ТабВалют.Курс";
			ВалютаИзНоменклатуры = РабочийТипЦен.ВВалютеУчета;
			
			ТекстЗапроса = "ВЫБРАТЬ
			|	ТаблицаПодразделений.Подразделение КАК Подразделение,
			|	ТаблицаПодразделений.Уровень КАК Уровень
			|ПОМЕСТИТЬ
			|	ТаблицаПодразделений
			|ИЗ
			|	&ТаблицаИерархииПодразделений КАК ТаблицаПодразделений
			|;
			|
			|ВЫБРАТЬ
			|	Валюты.Валюта КАК Валюта,
			|	ВЫБОР
			|		КОГДА Валюты.Кратность = 0 Тогда
			|			Валюты.Курс
			|		ИНАЧЕ
			|			(Валюты.Курс/Валюты.Кратность)/&КурсВалютыУпр
			|	КОНЕЦ КАК Курс
			|	
			|ПОМЕСТИТЬ
			|	ТабВалют
			|ИЗ
			|	РегистрСведений.КурсыВалют.СрезПоследних(&Момент, "+?(ВалютаИзНоменклатуры, "", "Валюта = &ВалютаЦен")+") КАК Валюты
			|ГДЕ
			|	Валюты.Курс > 0
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаЦен.Номенклатура КАК Номенклатура,
			|	ТаблицаЦен.Цена КАК Цена" + ?(ВедетсяУчетПоХарактеристикам, ",
			|	ТаблицаЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры", "") + ?(ВедетсяУчетПоЕдиницам, ",
			|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения", "") + ",
			|	ТаблицаПодразделений.Уровень
			|ПОМЕСТИТЬ
			|	ТаблицаЦен
			|ИЗ
			|	РегистрСведений.Цены.СрезПоследних(&Момент,
			|		ТипЦен = &ТипЦен И 
			|		Контрагент = &Контрагент" + ?(ВедетсяУчетПоХарактеристикам, "", " И 
			|		ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)") + ?(ВедетсяУчетПоЕдиницам, " И 
			|		(ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения ИЛИ 
			|		ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))", " И 
			|		ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)")+") КАК ТаблицаЦен
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	ТаблицаПодразделений КАК ТаблицаПодразделений
			|ПО
			|	ТаблицаЦен.ПодразделениеКомпании = ТаблицаПодразделений.Подразделение
			|ГДЕ
			|	ТаблицаЦен.Цена > 0
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|УНИЧТОЖИТЬ
			|	ТаблицаПодразделений
			|;
			|
			|ВЫБРАТЬ
			|	ТаблицаЦен.Номенклатура КАК Номенклатура" + ?(ВедетсяУчетПоХарактеристикам, ",
			|	ТаблицаЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры", "") + ?(ВедетсяУчетПоЕдиницам, ",
			|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения", "") + ",
			|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
			|ПОМЕСТИТЬ
			|	СрезПоследних
			|ИЗ
			|	ТаблицаЦен КАК ТаблицаЦен
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаЦен.Номенклатура" + ?(ВедетсяУчетПоХарактеристикам, ",
			|	ТаблицаЦен.ХарактеристикаНоменклатуры", "") + ?(ВедетсяУчетПоЕдиницам, ",
			|	ТаблицаЦен.ЕдиницаИзмерения", "") + "
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|ВЫБРАТЬ
			|	СрезПоследних.Номенклатура КАК Номенклатура" + ?(ВедетсяУчетПоЕдиницам, ",
			|	СрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения", "") + ?(ВедетсяУчетПоХарактеристикам, ",
			|	СрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры", "") + ",
			|	" + ТекстЦена + " КАК Цена
			|ПОМЕСТИТЬ
			|	ИтоговаяТаблицаЦен
			|ИЗ
			|	СрезПоследних КАК СрезПоследних
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	ТаблицаЦен КАК ТаблицаЦен
			|ПО
			|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура" + ?(ВедетсяУчетПоЕдиницам, " И 
			|	СрезПоследних.ЕдиницаИзмерения = ТаблицаЦен.ЕдиницаИзмерения", "") + ?(ВедетсяУчетПоХарактеристикам, " И 
			|	СрезПоследних.ХарактеристикаНоменклатуры = ТаблицаЦен.ХарактеристикаНоменклатуры", "") + " И 
			|	СрезПоследних.Уровень = ТаблицаЦен.Уровень
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	ТабВалют КАК ТабВалют
			|ПО
			|	" + ?(ВалютаИзНоменклатуры, "СрезПоследних.Номенклатура.ВалютаУчета = ТабВалют.Валюта", "ИСТИНА") + "
			|" + ТекстСоединенийПроцентыСкидкиНаценки + "
			|;
			|
			|УНИЧТОЖИТЬ
			|	СрезПоследних
			|;
			|
			|УНИЧТОЖИТЬ
			|	ТаблицаЦен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОстаткиТоваров.Номенклатура КАК Номенклатура,
			|	ОстаткиТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ОстаткиТоваров.КоличествоОстаток КАК Количество,
			|	(" + ТекстВыборкиЦен + ")*ОстаткиТоваров.КоличествоОстаток КАК Себестоимость
			|ИЗ
			|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Момент, СкладКомпании = &СкладКомпании"+ДопФильтр+") КАК ОстаткиТоваров " + ТекстСоединениеЦен;
			
			Запрос.Текст = ТекстЗапроса;
			
			Запрос.УстановитьПараметр("ТаблицаИерархииПодразделений",ТаблицаИерархииПодразделений);
			Запрос.УстановитьПараметр("ТипЦен",РабочийТипЦен);
			Запрос.УстановитьПараметр("Контрагент",Справочники.Контрагенты.ПустаяСсылка());
			Запрос.УстановитьПараметр("ВалютаЦен", РабочийТипЦен.ВалютаЦены);
			МоментВремени = ?(обЗначениеНеЗаполнено(ЭтаФорма.Ссылка),Новый МоментВремени(КонецДня(ЭтаФорма.Дата)),ЭтаФорма.МоментВремени());
			СтруктураКурса = обКурсДляВалюты(Константы.ВалютаУправленческогоУчетаКомпании.Получить(), МоментВремени);
			КурсВалютыУпр = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Запрос.УстановитьПараметр("КурсВалютыУпр", ?(КурсВалютыУпр = 0, 1, КурсВалютыУпр));
			
		КонецЕсли;
		
		МоментВремени = ?(обЗначениеНеЗаполнено(ЭтаФорма.Ссылка),Новый МоментВремени(КонецДня(ЭтаФорма.Дата)),ЭтаФорма.МоментВремени());
		Запрос.УстановитьПараметр("Момент",МоментВремени);
		Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
				
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#Иначе
		#КонецЕсли
		
		Возврат Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ИмяПодменю="ЗаполнитьПоДоговоруКомиссии" Тогда
		Если обЗначениеНеЗаполнено(ЭтаФорма.ДоговорВзаиморасчетов) Тогда
			#Если Клиент Тогда
				Предупреждение("Не указан договор !"); Возврат ИмяПодменю;
			#КонецЕсли
		КонецЕсли;
		// спросим
		Если Таблица.Количество()>0 Тогда
			#Если Клиент Тогда
				Если Вопрос("Перед заполнением таблица будет очищена
					|Продолжить ?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет)<>КодВозвратаДиалога.Да Тогда
					
					Возврат ИмяПодменю;
				Иначе
					Таблица.Очистить();
				КонецЕсли;
			#Иначе
				Таблица.Очистить();
			#КонецЕсли
		КонецЕсли;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ПартииТоваровОтданныеОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровОтданныеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданныеОстатки.ДокументПередачи КАК ДокументПередачи,
		|	ПартииТоваровОтданныеОстатки.КоличествоОстаток КАК Количество,
		|	ПартииТоваровОтданныеОстатки.СуммаУпрОстаток КАК Сумма,
		|	ПартииТоваровОтданныеОстатки.СуммаСебестоимостиУпрОстаток КАК Себестоимость
		|
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные.Остатки(&Момент, 
		|					Автомобиль = ЗНАЧЕНИЕ(Справочник.Автомобили.ПустаяСсылка) И 
		|					Контрагент = &Контрагент И 
		|					ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК ПартииТоваровОтданныеОстатки
		|ГДЕ
		|	ПартииТоваровОтданныеОстатки.КоличествоОстаток > 0
		|");

		МоментВремени=?(обЗначениеНеЗаполнено(ЭтаФорма.Ссылка),Новый МоментВремени(КонецДня(ЭтаФорма.Дата)),ЭтаФорма.МоментВремени());
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ЭтаФорма.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Контрагент",ЭтаФорма.Контрагент);
		Запрос.УстановитьПараметр("Момент",МоментВремени);
		#Если Клиент Тогда
		дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		Возврат Запрос.Выполнить().Выгрузить();
	КонецЕсли;
КонецФункции // ЗаполнитьТабличнуюЧасть()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		СменитьФлагПолучатьКнижноеКоличествоИзПартий();
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);

	ИначеЕсли Имя="СкладКомпании" Тогда
		ТЦ=ТипЦен; // сохраним тип цен от изменения
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТипЦен=ТЦ;
		Рез=ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Если Товары.Количество()>0 Тогда
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Если Вопрос("Произвести пересчет расчетного количества номенклатуры", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
						Для Каждого СтрокаТоваров Из Товары Цикл
							Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",СтрокаТоваров,ЭтаФорма,ДопПараметры) И Рез;
						КонецЦикла; 
					КонецЕсли; 
				Иначе
					Для Каждого СтрокаТоваров Из Товары Цикл
						Рез = ОбработкаРеквизита("ПолучитьРасчетноеКоличество", СтрокаТоваров, ЭтаФорма, ДопПараметры) И Рез;
					КонецЦикла; 
				КонецЕсли; 
			#Иначе
				Для Каждого СтрокаТоваров Из Товары Цикл
					Рез = ОбработкаРеквизита("ПолучитьРасчетноеКоличество", СтрокаТоваров, ЭтаФорма, ДопПараметры) И Рез;
				КонецЦикла;
			#КонецЕсли
			Если ЗначениеЗаполнено(СкладКомпании) И (СкладКомпании.Розничный) Тогда
				Для Каждого СтрокаТоваров Из Товары Цикл
					Рез = ОбработкаРеквизита("УстановитьОтпускнуюЦену", СтрокаТоваров, ЭтаФорма, ДопПараметры) И Рез;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании"
		Если СкладКомпании.Пустая() Тогда
		КонецЕсли;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		// отработаем глобально
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры=Новый Структура();
		КонецЕсли;
		ДопПараметры.Вставить("ПолучитьРасчетноеКоличество",Ложь);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ДопПараметры.Удалить("ПолучитьРасчетноеКоличество");
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		// если розничный склад, то получим розничную цену
		Рез = ОбработкаРеквизита("УстановитьОтпускнуюЦену", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		//если обработка заполнения ТЧ
		#Если Клиент Тогда
			//достанем доп. параметры
			Попытка флЗаполнятьПоСебестоимости = ДопПараметры.флЗаполнятьПоСебестоимости Исключение флЗаполнятьПоСебестоимости = Ложь; КонецПопытки;
			Попытка флЗаполнятьБезНДС = ДопПараметры.флЗаполнятьБезНДС Исключение флЗаполнятьБезНДС = Ложь; КонецПопытки;
			Попытка СтрокаТЧ = ДопПараметры.СтрокаТЧ; Исключение СтрокаТЧ = Неопределено КонецПопытки;
			//если заполнять по себестоимости, то из выборки получим себестоимость
			Если флЗаполнятьПоСебестоимости И СтрокаТЧ <> Неопределено Тогда
				ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
				ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
				Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
					СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
					КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
				Иначе
					КурсУпр = ЭтотОбъект.КурсВалютыУпр;
				КонецЕсли;
				
				Если ПолучатьКнижноеКоличествоИзПартий Тогда
					Если ВалютаДокумента = ВалютаРегл Тогда
						// kraviv 18.06.2021 проверка
						Себестоимость = СтрокаТЧ.СебестоимостьРегл;
						//Себестоимость = обПересчет(СтрокаТЧ.Себестоимость,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
						ТекСтрока.Цена = Себестоимость/?(СтрокаТЧ.Количество=0,1,СтрокаТЧ.Количество);
					ИначеЕсли ВалютаДокумента = ВалютаУпр Тогда
						Себестоимость = СтрокаТЧ.Себестоимость;
						ТекСтрока.Цена = Себестоимость/?(СтрокаТЧ.Количество=0,1,СтрокаТЧ.Количество);
					Иначе
						Себестоимость = обПересчет(СтрокаТЧ.Себестоимость,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
						ТекСтрока.Цена = Себестоимость/?(СтрокаТЧ.Количество=0,1,СтрокаТЧ.Количество);
					КонецЕсли; 
				Иначе
					Себестоимость = обПересчет(СтрокаТЧ.Себестоимость,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
					ТекСтрока.Цена = Себестоимость/?(СтрокаТЧ.Количество=0,1,СтрокаТЧ.Количество);
				КонецЕсли; 
			ИначеЕсли флЗаполнятьПоСебестоимости И СтрокаТЧ = Неопределено Тогда
				Себестоимость = 0;
				ТекСтрока.Цена = 0;
			КонецЕсли;
			//заполняем остальные реквизиты строки

			Если СтрокаТЧ<>Неопределено Тогда 
				ТекСтрока.КоличествоКнижн=?(ТекСтрока.Коэффициент=0,СтрокаТЧ.Количество,СтрокаТЧ.Количество/ТекСтрока.Коэффициент);
				Рез=ОбработкаРеквизита("Товары.КоличествоКнижн",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
				Если флЗаполнятьПоСебестоимости Тогда
					ТекСтрока.СуммаКнижн = Себестоимость;
				КонецЕсли;
				
				Если флЗаполнятьБезНДС И НЕ флЗаполнятьПоСебестоимости Тогда
					СтавкаНДС = СтрокаТЧ.Номенклатура.СтавкаНДС.Ставка;
					ТекСтрока.СуммаКнижн = ТекСтрока.СуммаКнижн - Окр(ТекСтрока.СуммаКнижн * СтавкаНДС / (100 + СтавкаНДС), 2);
				КонецЕсли;
				
				ТекСтрока.ХарактеристикаНоменклатуры=СтрокаТЧ.ХарактеристикаНоменклатуры;
				Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
					ТекСтрока.ДокументПередачи = СтрокаТЧ.ДокументПередачи;
				КонецЕсли;	
				Попытка
					ТекСтрока.ГТД=СтрокаТЧ.ГТД;
			    Исключение
			    КонецПопытки;
				Попытка 
					ТекСтрока.Партия=СтрокаТЧ.Партия;
					ОбработкаРеквизита("Товары.Партия",ТекСтрока,ЭтаФорма,ДопПараметры);
				Исключение
				КонецПопытки;
			Иначе
				// получим расчетное количество
				Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
			//расчет факт сумм
			//если Фактическое количество уже было заполнено из документа основания,
			//тогда его не меняем
			Попытка
				ТекСтрока.КоличествоФакт=ДопПараметры.КоличествоФакт;
			Исключение
				Если ТекСтрока.КоличествоФакт = 0 Тогда
					ТекСтрока.КоличествоФакт=ТекСтрока.КоличествоКнижн;
				КонецЕсли;
			КонецПопытки; 
			Рез=ОбработкаРеквизита("Товары.КоличествоФакт",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		#Иначе
			// получим расчетное количество
			Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		#КонецЕсли
	
		Возврат Рез;

	ИначеЕсли Имя="Товары.Количество" Тогда
		ТекСтрока.Количество=ТекСтрока.КоличествоФакт-ТекСтрока.КоличествоКнижн;
		ТекСтрока.Сумма=ТекСтрока.СуммаФакт-ТекСтрока.СуммаКнижн;
		ТекСтрока.СуммаОтпускная=ТекСтрока.СуммаОтпускнаяФакт-ТекСтрока.СуммаОтпускнаяКнижн;
		Возврат Рез;

	ИначеЕсли Имя="Товары.КоличествоКнижн" Тогда
		ТекСтрока.Количество=ТекСтрока.КоличествоФакт-ТекСтрока.КоличествоКнижн;
		ТекСтрока.СуммаКнижн=ТекСтрока.КоличествоКнижн*ТекСтрока.Цена*ТекСтрока.Коэффициент;
		Рез=ОбработкаРеквизита("Товары.СуммаКнижн",ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.СуммаОтпускнаяКнижн=ТекСтрока.КоличествоКнижн*ТекСтрока.ЦенаОтпускная*ТекСтрока.Коэффициент;
		Рез=ОбработкаРеквизита("Товары.СуммаОтпускнаяКнижн",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.СуммаКнижн" Тогда
		ТекСтрока.Сумма=ТекСтрока.СуммаФакт-ТекСтрока.СуммаКнижн;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.КоличествоФакт" Тогда
		ТекСтрока.Количество=ТекСтрока.КоличествоФакт-ТекСтрока.КоличествоКнижн;
		ТекСтрока.СуммаФакт=ТекСтрока.КоличествоФакт*ТекСтрока.Цена*ТекСтрока.Коэффициент;
		ТекСтрока.СуммаОтпускнаяФакт=ТекСтрока.КоличествоФакт*ТекСтрока.ЦенаОтпускная*ТекСтрока.Коэффициент;
		Если ТекСтрока.КоличествоФакт=ТекСтрока.КоличествоКнижн Тогда
			ТекСтрока.СуммаФакт=ТекСтрока.СуммаКнижн;
			ТекСтрока.СуммаОтпускнаяФакт=ТекСтрока.СуммаОтпускнаяКнижн;
		КонецЕсли;	
		Рез=ОбработкаРеквизита("Товары.СуммаФакт",ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("Товары.СуммаОтпускнаяФакт",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.СуммаФакт" Тогда
		ТекСтрока.Сумма=ТекСтрока.СуммаФакт-ТекСтрока.СуммаКнижн;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаОтпускнаяКнижн" Тогда
		ТекСтрока.СуммаОтпускная=ТекСтрока.СуммаОтпускнаяФакт-ТекСтрока.СуммаОтпускнаяКнижн;
		Возврат Истина;

	ИначеЕсли Имя="Товары.СуммаОтпускнаяФакт" Тогда
		ТекСтрока.СуммаОтпускная=ТекСтрока.СуммаОтпускнаяФакт-ТекСтрока.СуммаОтпускнаяКнижн;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		
		Если ТекСтрока.Коэффициент < ТекСтрока.ЕдиницаИзмерения.Коэффициент Тогда
			Текстрока.КоличествоФакт = Текстрока.КоличествоФакт * ТекСтрока.Коэффициент;
		ИначеЕсли ТекСтрока.Коэффициент > ТекСтрока.ЕдиницаИзмерения.Коэффициент Тогда
			Текстрока.КоличествоФакт = Текстрока.КоличествоФакт / ТекСтрока.ЕдиницаИзмерения.Коэффициент;
		КонецЕсли;
		
		ТекСтрока.Коэффициент=ТекСтрока.ЕдиницаИзмерения.Коэффициент;
		// получим расчетное количество
		Рез = ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.Цена=обПолучитьЦену(ТипЦен,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
		Рез = ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Рез = ОбработкаРеквизита("УстановитьОтпускнуюЦену", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		ТекСтрока.СуммаКнижн=ТекСтрока.Цена*ТекСтрока.КоличествоКнижн*ТекСтрока.Коэффициент;
		Рез=ОбработкаРеквизита("Товары.СуммаКнижн",ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.СуммаФакт=ТекСтрока.Цена*ТекСтрока.КоличествоФакт*ТекСтрока.Коэффициент;
		Рез=ОбработкаРеквизита("Товары.СуммаФакт",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЦенаОтпускная" Тогда
		// если склад НЕ розничный, то суммы не пересчитываем
		Если обЗначениеНеЗаполнено(СкладКомпании) ИЛИ НЕ СкладКомпании.Розничный Тогда Возврат Истина; КонецЕсли;
		ТекСтрока.СуммаОтпускнаяФакт=ТекСтрока.ЦенаОтпускная*ТекСтрока.КоличествоФакт*ТекСтрока.Коэффициент;
		Рез=ОбработкаРеквизита("Товары.СуммаОтпускнаяФакт",ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.СуммаОтпускнаяКнижн=ТекСтрока.ЦенаОтпускная*ТекСтрока.КоличествоКнижн*ТекСтрока.Коэффициент;
		Рез=ОбработкаРеквизита("Товары.СуммаОтпускнаяКнижн",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		Попытка
			ТекСтрока.КоличествоФакт=ДопПараметры.КоличествоФакт;
		Исключение
			ТекСтрока.КоличествоФакт=ТекСтрока.КоличествоКнижн;
		КонецПопытки; 
		
		Рез = ОбработкаРеквизита("Товары.КоличествоФакт", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Рез = ОбработкаРеквизита("УстановитьОтпускнуюЦену", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ДокументПередачи" Тогда
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Партия" ИЛИ Имя="Товары.ГТД" Тогда
		Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		Попытка
			ТекСтрока.КоличествоФакт=ДопПараметры.КоличествоФакт;
		Исключение
			ТекСтрока.КоличествоФакт=ТекСтрока.КоличествоКнижн;
		КонецПопытки; 
		Рез = ОбработкаРеквизита("Товары.КоличествоФакт",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	// ПРОЧИЕ ОБРАБОТЧИКИ
		
	ИначеЕсли Имя="ПолучитьРасчетноеКоличество" Тогда
		Рез=Истина;
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли;
		Попытка 
			ПолучитьРасчетноеКоличество = ДопПараметры.ПолучитьРасчетноеКоличество;	
		Исключение
			ПолучитьРасчетноеКоличество = Истина;
		КонецПопытки;
		Если НЕ ПолучитьРасчетноеКоличество Тогда
			Возврат Рез;
		КонецЕсли;
		
		// проверим
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда Возврат Ложь; КонецЕсли;
		Попытка флЗаполнятьПоСебестоимости = ДопПараметры.флЗаполнятьПоСебестоимости Исключение флЗаполнятьПоСебестоимости = Ложь; КонецПопытки;
		Попытка флЗаполнятьБезНДС = ДопПараметры.флЗаполнятьБезНДС Исключение флЗаполнятьБезНДС = Ложь; КонецПопытки;
		Попытка Ном=ТекСтрока.Номенклатура Исключение Возврат Ложь; КонецПопытки;
		Попытка Если ДопПараметры.НеСчитать Тогда Возврат Истина; КонецЕсли; Исключение КонецПопытки; // нужно для заполнения
		// получаем в зависимости от вида хоз. операции
		Если ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
	        СтруктураОтбора=Новый Структура("СкладКомпании,Номенклатура");
			СтруктураОтбора.СкладКомпании=СкладКомпании; СтруктураОтбора.Номенклатура=ТекСтрока.Номенклатура;
			Если ТекСтрока.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ НЕ обЗначениеНеЗаполнено(ТекСтрока.ХарактеристикаНоменклатуры) Тогда
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
			КонецЕсли;
			Если ПолучатьКнижноеКоличествоИзПартий И НЕ обЗначениеНеЗаполнено(ТекСтрока.Партия) Тогда
				СтруктураОтбора.Вставить("Партия",ТекСтрока.Партия);
			КонецЕсли;
			
			Если ДопПараметры.Свойство("ДатаОстатков") Тогда
				МоментВремени = Новый МоментВремени(ДопПараметры.ДатаОстатков);
			Иначе
				МоментВремени = ?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
			КонецЕсли;
			
			// Если в текущем узле не ведется партионный учет,
			// то остатки будем получать из регистра остатков, иначе из регистра партий.
			Если ПолучатьКнижноеКоличествоИзПартий Тогда
				ТаблицаОстатков = РегистрыНакопления.ПартииТоваровКомпании.Остатки(МоментВремени,СтруктураОтбора,,"Количество"+?(флЗаполнятьПоСебестоимости,",Сумма,СуммаУпр,СуммаНДС",""));
				Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ГТД) Тогда
					СтруктураОтбора.Вставить("ГТД",ТекСтрока.ГТД);
					ТаблицаОстатковГТД = РегистрыНакопления.ГТДПартийТоваровКомпании.Остатки(МоментВремени,СтруктураОтбора,,"Количество");
				КонецЕсли;
			Иначе
				ТаблицаОстатков = РегистрыНакопления.ОстаткиТоваровКомпании.Остатки(МоментВремени,СтруктураОтбора,,"Количество"+?(флЗаполнятьПоСебестоимости,",СуммаРозн",""));
			КонецЕсли;
			
			Если ТаблицаОстатков.Количество()>0 Тогда
				ВалютаРегл     = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
				ВалютаУпр	   = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
				СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
				КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность=0, 1, СтруктураКурса.Кратность);
				СуммаЗакупки   = 0; 
				КоличествоОстаток = ТаблицаОстатков.Итог("Количество");
				Попытка 
					Если ПолучатьКнижноеКоличествоИзПартий Тогда
						Если флЗаполнятьПоСебестоимости Тогда
							Если ВалютаДокумента = ВалютаРегл Тогда
								СуммаЗакупки = ТаблицаОстатков.Итог("Сумма") - ?(флЗаполнятьБезНДС, ТаблицаОстатков.Итог("СуммаНДС"), 0);
							ИначеЕсли ВалютаДокумента = ВалютаУпр Тогда
								СтавкаНДС = ?(ТаблицаОстатков.Итог("Сумма") = 0, 0, Окр(ТаблицаОстатков.Итог("СуммаНДС")/(ТаблицаОстатков.Итог("Сумма") - ТаблицаОстатков.Итог("СуммаНДС")) * 100, 2));
								СуммаЗакупки = ТаблицаОстатков.Итог("СуммаУпр") - ?(флЗаполнятьБезНДС, Окр(СтавкаНДС*ТаблицаОстатков.Итог("СуммаУпр") / (100 + СтавкаНДС), 2), 0);
							Иначе
								Если флЗаполнятьБезНДС Тогда
									СуммаОстатков = ТаблицаОстатков.Итог("Сумма") - ТаблицаОстатков.Итог("СуммаНДС");
								Иначе
									СуммаОстатков = ТаблицаОстатков.Итог("Сумма");
								КонецЕсли;
								СуммаЗакупки = обПересчет(СуммаОстатков,ВалютаРегл,КурсРегл,ВалютаДокумента,КурсДокумента);
							КонецЕсли;
						КонецЕсли; 
						Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ГТД) Тогда
							КоличествоГТД = ТаблицаОстатковГТД.Итог("Количество");
							Если КоличествоОстаток=0 Тогда
								СуммаЗакупки = 0;
							Иначе
								СуммаЗакупки = СуммаЗакупки*КоличествоГТД/КоличествоОстаток;
							КонецЕсли;
							КоличествоОстаток = КоличествоГТД; 
						КонецЕсли;
					Иначе
						Если флЗаполнятьПоСебестоимости Тогда
							Если флЗаполнятьБезНДС Тогда
								СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС.Ставка;
								СуммаОстатков = ТаблицаОстатков.Итог("СуммаРозн") - Окр(ТаблицаОстатков.Итог("СуммаРозн") * СтавкаНДС / (100 + СтавкаНДС));
							Иначе
								СуммаОстатков = ТаблицаОстатков.Итог("СуммаРозн");
							КонецЕсли;
							СуммаЗакупки = обПересчет(СуммаОстатков,ВалютаРегл,КурсРегл,ВалютаДокумента,КурсДокумента);
						КонецЕсли;
					КонецЕсли;
				Исключение 
				КонецПопытки;
				
				Попытка
					
					КоличествоПереданное = ДопПараметры.КоличествоКнижн;
					
					Если КоличествоПереданное <> 0 Тогда
						СуммаЗакупки = ?(КоличествоОстаток = 0, 0, СуммаЗакупки / КоличествоОстаток * КоличествоПереданное);
						КоличествоОстаток = КоличествоПереданное;
					КонецЕсли;
					
				Исключение
				КонецПопытки;
				
				Если обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
					КоличествоОстаток = КоличествоОстаток/ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				Иначе
					КоличествоОстаток = КоличествоОстаток/ТекСтрока.ЕдиницаИзмерения.Коэффициент;
				КонецЕсли; 
				ТекСтрока.Цена            = СуммаЗакупки/?(КоличествоОстаток=0,1,КоличествоОстаток);
				ТекСтрока.КоличествоКнижн = КоличествоОстаток;
				// пересчет
				Рез=ОбработкаРеквизита("Товары.КоличествоКнижн",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				ТекСтрока.СуммаКнижн      = СуммаЗакупки;
	        Иначе
				ТекСтрока.КоличествоКнижн = 0;
				// пересчет
				Рез=ОбработкаРеквизита("Товары.КоличествоКнижн",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		Иначе
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ПартииТоваровОтданныеОстатки.Контрагент,
			|	ПартииТоваровОтданныеОстатки.Номенклатура,
			|	ПартииТоваровОтданныеОстатки.ХарактеристикаНоменклатуры,
			|	ПартииТоваровОтданныеОстатки.ДоговорВзаиморасчетов,
			|	ПартииТоваровОтданныеОстатки.ДокументПередачи,
			|	ПартииТоваровОтданныеОстатки.Партия,
			|	ПартииТоваровОтданныеОстатки.ГТД,
			|	ПартииТоваровОтданныеОстатки.КоличествоОстаток КАК Количество,
			|	ПартииТоваровОтданныеОстатки.СуммаСебестоимостиУпрОстаток КАК СуммаСебестоимостиУпр
			|ИЗ
			|	РегистрНакопления.ПартииТоваровОтданные.Остатки(
			|			&МоментВремени,
			|				Автомобиль = ЗНАЧЕНИЕ(Справочник.Автомобили.ПустаяСсылка) И 
			|				Контрагент = &Контрагент И 
			|				Номенклатура = &Номенклатура И 
			|				ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры И 
			|				ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И 
			|				ДокументПередачи = &ДокументПередачи" + ?(обЗначениеНеЗаполнено(ТекСтрока.ГТД), "", " И 
			|				ГТД = &ГТД") + "
			|	) КАК ПартииТоваровОтданныеОстатки";
			Запрос.УстановитьПараметр("МоментВремени", ?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени()));
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Запрос.УстановитьПараметр("Номенклатура", ТекСтрока.Номенклатура);
			Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ТекСтрока.ХарактеристикаНоменклатуры);
			Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
			Запрос.УстановитьПараметр("ДокументПередачи", ТекСтрока.ДокументПередачи);
			Запрос.УстановитьПараметр("ГТД", ТекСтрока.ГТД);
			
			ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
						
			Если ТаблицаОстатков.Количество()>0 Тогда
				КоличествоОстаток=ТаблицаОстатков.Итог("Количество");
				Если обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
					КоличествоОстаток=КоличествоОстаток/ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				Иначе
					КоличествоОстаток=КоличествоОстаток/ТекСтрока.ЕдиницаИзмерения.Коэффициент;
				КонецЕсли; 
				ТекСтрока.КоличествоКнижн=КоличествоОстаток;
				ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
				Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
					СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
					КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
				Иначе
					КурсУпр = ЭтотОбъект.КурсВалютыУпр;
				КонецЕсли;
				Если флЗаполнятьБезНДС Тогда
					СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС.Ставка;
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаСебестоимостиУпр") - Окр(ТаблицаОстатков.Итог("СуммаСебестоимостиУпр") * СтавкаНДС/ (100 + СтавкаНДС), 2);
				Иначе
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаСебестоимостиУпр");
				КонецЕсли;
				ТекСтрока.СуммаКнижн=обПересчет(СуммаОстаток,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
				ТекСтрока.Цена=ТекСтрока.СуммаКнижн/?(КоличествоОстаток=0,1,КоличествоОстаток);
	        Иначе
				ТекСтрока.КоличествоКнижн=0;
				ТекСтрока.СуммаКнижн=0;
			КонецЕсли;
			// пересчет
			Рез=ОбработкаРеквизита("Товары.КоличествоКнижн",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.КоличествоФакт",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя = "УстановитьОтпускнуюЦену" Тогда
		
		//Заполним розничную цену (для розничного склада)
		Если ЗначениеЗаполнено(СкладКомпании) И (СкладКомпании.Розничный) Тогда
			ТекСтрока.ЦенаОтпускная = обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли, ТекСтрока.Номенклатура, ?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("Товары.ЦенаОтпускная", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

//Проверяет возможность проведения инвентаризации
//
// Возвращаемое значение:
//  Отказ - булево в зависимости от прохождения условий принимает значение Истина или Ложь.
//
Функция МожноПроводить()
	Отказ = Ложь;
	//проверим, нету ли в наличие проведенных чеков	
	Если ХозОперация = Справочники.ХозОперации.ИнвентаризацияТоваров И СкладКомпании.Розничный Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпании.Регистратор
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|ГДЕ
		|	ОстаткиТоваровКомпании.СкладКомпании = &СкладКомпании 
		|	И ОстаткиТоваровКомпании.Регистратор ССЫЛКА Документ.Чек
		|	И ОстаткиТоваровКомпании.Период МЕЖДУ &ДатаНачалоДня И &ДатаКонецДня
		|";
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаНачалоДня",НачалоДня(Дата));
		Запрос.УстановитьПараметр("ДатаКонецДня",КонецДня(Дата));
		Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
		Запрос.Текст = ТекстЗапроса;
		Результат = Запрос.Выполнить();
		Отказ = НЕ Результат.Пустой();
		Если Отказ Тогда
			дкСообщитьРезультат("Инвентаризацию можно проводить только после закрытия кассовой смены!", "Важное&Инвентаризация", ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	Возврат Отказ;
КонецФункции // МожноПроводить()

// Сменить флаг получать книжное количество из партий
//
Процедура СменитьФлагПолучатьКнижноеКоличествоИзПартий()
	
	РежимПроведенияПоПартиям = одОбменДанными.ПолучитьРежимПроведенияПоПартиям();
	
	Если РежимПроведенияПоПартиям=Перечисления.РежимыПроведенияПартий.ПервичнымиДокументамиДопроведение Тогда
		ПолучатьКнижноеКоличествоИзПартий = Истина;
	ИначеЕсли РежимПроведенияПоПартиям=Перечисления.РежимыПроведенияПартий.ПервичнымиДокументами Тогда
		ПолучатьКнижноеКоличествоИзПартий = Истина;
	Иначе
		ПолучатьКнижноеКоличествоИзПартий = Ложь;
	КонецЕсли;	
	
КонецПроцедуры // СменитьФлагПолучатьКнижноеКоличествоИзПартий()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Получение данных о прослеживаемых товаров
//
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	|	СУММА(ПартииТоваровКомпании.Количество) КАК КоличествоОсталось,
	|	СУММА(ПартииТоваровКомпании.Сумма - ПартииТоваровКомпании.СуммаНДС) КАК СуммаОсталось
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаТовараПоИнвентаризации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратУтерянногоТовара)
	|	КОНЕЦ КАК КодОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД КАК РНПТ,
	|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК КоличествоПрослеживаемости
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
	|ГДЕ
	|	ГТДПартийТоваровКомпании.Регистратор = &Ссылка
	|	И ГТДПартийТоваровКомпании.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпании.Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД,
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаТовараПоИнвентаризации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратУтерянногоТовара)
	|	КОНЕЦ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	// Проверим есть РНПТ у документа
	Если ПакетЗапросов[1].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаТоваров = ПакетЗапросов[0].Выгрузить();
	ТаблицаРНПТ = ПакетЗапросов[1].Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
	
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = Дата;
		
		// Получим сумму товара
		КоличествоРНПТ = ТекущаяСтрока.КоличествоПрослеживаемости;
		НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(Новый Структура("Номенклатура", ТекущаяСтрока.Номенклатура));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНоменклатуры = НайденныеСтроки[0];
		
		Если КоличествоРНПТ >= СтрокаНоменклатуры.КоличествоОсталось Тогда
			СуммаБезНДС = СтрокаНоменклатуры.СуммаОсталось;
		Иначе
			СуммаБезНДС = Окр(СтрокаНоменклатуры.СуммаОсталось /
				?(СтрокаНоменклатуры.КоличествоОсталось = 0, 1, СтрокаНоменклатуры.КоличествоОсталось)
				* КоличествоРНПТ, 2);
		КонецЕсли;
		НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
		
		СтрокаНоменклатуры.КоличествоОсталось = СтрокаНоменклатуры.КоличествоОсталось - КоличествоРНПТ;
		СтрокаНоменклатуры.СуммаОсталось = СтрокаНоменклатуры.СуммаОсталось - СуммаБезНДС;
		Если СтрокаНоменклатуры.КоличествоОсталось <= 0 Тогда
			ТаблицаТоваров.Удалить(СтрокаНоменклатуры);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	СтруктураПараметров=Неопределено;
	Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
		СтруктураПараметров=Новый Структура("ИмяРеквизитаКоличество,ИмяРеквизитаЦена,ТипЦен","КоличествоФакт","ЦенаОтпускная",СкладКомпании.ТипЦенРозничнойТорговли);
	КонецЕсли;
	дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ИнвентаризацияТоваровНаКомиссии"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИнвентаризацияТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИнвентаризацияТоваров");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьНаименование(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	СтруктураИтоговПоСтранице = Новый Структура();	
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		ОбластьМакета.Параметры.КоличествоКнижн = Формат(СтрокаТабличнойЧасти.КоличествоКнижн,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.КоличествоФакт = Формат(СтрокаТабличнойЧасти.КоличествоФакт,ФорматВыводаКоличества);
		
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьПодвал.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИнвентаризацияТоваровНаКомиссии()

// Формирует печатную форму "ИНВ3"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИНВ3(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ИНВ3");
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтрокНаСтранице			= 24;
	СтрокШапки				= 5;
	СтрокПодвала			= 5;
	НомерСтраницы			= 2;
	Ном						= 1;
	
	ИтогКоличество			= 0;
	ИтогСумма				= 0;
	ИтогКоличествоУчет		= 0;
	ИтогСуммаУчет			= 0;
	
	КолвоСтрокПоСтранице	= 0;
	КолвоПоСтранице			= 0;
	СуммаЛиста				= 0;
	
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	
	ЗаголовокТаблицы.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента;      
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();

	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// Определим, что показывать в колонке кода - код или артикул
	ИмяКодаСправочника = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,Права).Имя;
	
	// Выводим многострочную часть документа	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
	
		
		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда

			ОбластьИтоговПоСтранице                               = Макет.ПолучитьОбласть("ПодвалСтраницы");
			ОбластьИтоговПоСтранице.Параметры.ИтогоКоличество      = Формат(ИтогКоличество,ФорматВыводаКоличества);
			ОбластьИтоговПоСтранице.Параметры.ИтогоСумма          = Формат(ИтогСумма,ФорматВыводаСуммы);
			ОбластьИтоговПоСтранице.Параметры.ИтогоКоличествоУчет = Формат(ИтогКоличествоУчет,ФорматВыводаКоличества);
			ОбластьИтоговПоСтранице.Параметры.ИтогоСуммаУчет      = Формат(ИтогСуммаУчет,ФорматВыводаСуммы);

			ОбластьИтоговПоСтранице.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(КолвоСтрокПоСтранице, ,",,,,,,,,0");
			ОбластьИтоговПоСтранице.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(КолвоПоСтранице);
			ОбластьИтоговПоСтранице.Параметры.СуммаФактическиНаСтраницеПрописью                 = обЧислоПрописью(ИтогСуммаУчет,ЭтотОбъект.ВалютаДокумента);
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
			Ном =1;

			ИтогКоличество = 0;
			ИтогСумма      = 0;
			ИтогКоличествоУчет  = 0;
			ИтогСуммаУчет       = 0;

			КолвоСтрокПоСтранице = 0;
			КолвоПоСтранице      = 0;
			СуммаЛиста           = 0;

		КонецЕсли;

		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		Характеристика										= Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(СтрокаТовар.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					= дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКодаСправочника,Права);
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ	= СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		ОбластьМакета.Параметры.Номер						= СтрокаТовар.НомерСтроки;
		ОбластьМакета.Параметры.Цена						= Формат(СтрокаТовар.Цена,ФорматВыводаСуммы); //LIHV надо так. 		
		ОбластьМакета.Параметры.КоличествоУчет				= Формат(СтрокаТовар.КоличествоФакт,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.Количество   				= Формат(СтрокаТовар.КоличествоКнижн,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.СуммаУчет	    			= Формат(СтрокаТовар.СуммаФакт,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Сумма       				= Формат(СтрокаТовар.СуммаКнижн,ФорматВыводаСуммы);

		ТабДокумент.Вывести(ОбластьМакета);
		Ном = Ном + 1;

		ИтогКоличество     = ИтогКоличество     + СтрокаТовар.КоличествоКнижн;
		ИтогСумма          = ИтогСумма          + СтрокаТовар.СуммаКнижн;
		ИтогКоличествоУчет = ИтогКоличествоУчет + СтрокаТовар.КоличествоФакт;
		ИтогСуммаУчет      = ИтогСуммаУчет      + СтрокаТовар.СуммаФакт;

		КолвоСтрокПоСтранице = КолвоСтрокПоСтранице + 1;
		КолвоПоСтранице      = КолвоПоСтранице      + СтрокаТовар.КоличествоФакт;
		СуммаЛиста           = СуммаЛиста           + СтрокаТовар.СуммаФакт;

	КонецЦикла;

	// Выводим итоги по последней странице
	ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ПодвалСтраницы");
	ОбластьИтоговПоСтранице.Параметры.ИтогоКоличество     = Формат(ИтогКоличество,ФорматВыводаКоличества);
	ОбластьИтоговПоСтранице.Параметры.ИтогоСумма          = Формат(ИтогСумма,ФорматВыводаСуммы);
	ОбластьИтоговПоСтранице.Параметры.ИтогоКоличествоУчет = Формат(ИтогКоличествоУчет,ФорматВыводаКоличества);
	ОбластьИтоговПоСтранице.Параметры.ИтогоСуммаУчет      = Формат(ИтогСуммаУчет,ФорматВыводаСуммы);
	ОбластьИтоговПоСтранице.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(КолвоСтрокПоСтранице, ,",,,,,,,,0");
	ОбластьИтоговПоСтранице.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(КолвоПоСтранице);
	ОбластьИтоговПоСтранице.Параметры.СуммаФактическиНаСтраницеПрописью                 = обЧислоПрописью(ИтогСуммаУчет,ЭтотОбъект.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим подвал документа
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалОписи");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.НачальныйНомерПоПорядку = 1;
	ОбластьМакета.Параметры.НомерКонца              = ВыборкаСтрокТовары.Количество();
	ОбластьМакета.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(ВыборкаСтрокТовары.Итог("КоличествоФакт"));
	ОбластьМакета.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(ВыборкаСтрокТовары.Количество(), ,",,,,,,,,0");

	//если нет движений по регистрам то печатаем сумму документа
	СуммаНаПечать = ВыборкаСтрокТовары.Итог("СуммаФакт"); //LIHV надо так. ИтогСуммаУчет;
	ОбластьМакета.Параметры.СуммаФактическиНаСтраницеПрописью                 = обЧислоПрописью(СуммаНаПечать,ЭтотОбъект.ВалютаДокумента);
	
	// Заполним информацию о руководителях и ответственных
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	
	ПредседательКомиссии= дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ЧленКомиссии1		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ЧленКомиссии2		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИНВ3()

// Вывод итогов по странице печатной формы документа
//
// Параметры:
// ТабДокумент - табличный документ, печатная форма документа
// ОбластьПодвал - область табличного документа, область куда выводятся итоги
// СтруктураИтоговПоСтранице - структура, сравнит данные для вывода
// Права   - структура, права текущего пользователя или объект документа
//
Процедура ВывестиИтогиПоСтраницеИНВ19(ТабДокумент,ОбластьПодвал,СтруктураИтоговПоСтранице,Права) 
	Если ОбластьПодвал <> Неопределено Тогда
		Если СтруктураИтоговПоСтранице <> Неопределено Тогда
			Попытка
				ПраваПользователя=Права.Права;
				ОбъектПрава=Права;
			Исключение
				ПраваПользователя=Права;
				ОбъектПрава=Неопределено;
			КонецПопытки; 
			ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",ПраваПользователя,,ОбъектПрава);
			ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",ПраваПользователя,,ОбъектПрава);
			Для Каждого ЭлементИтога Из СтруктураИтоговПоСтранице Цикл
				Если Найти(ЭлементИтога.Ключ,"Сум") <> 0 Тогда
					ЗначениеПараметра = Формат(ЭлементИтога.Значение,ФорматВыводаСуммы);
				Иначе
					ЗначениеПараметра = Формат(ЭлементИтога.Значение,ФорматВыводаКоличества);
				КонецЕсли; 
				Попытка
					ОбластьПодвал.Параметры[ЭлементИтога.Ключ] = ЗначениеПараметра;
				Исключение
				КонецПопытки; 
			КонецЦикла;
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьПодвал);
	КонецЕсли;
КонецПроцедуры // дкДействияФормыСоздатьНапоминание()

// Функция проверяет попадание текущей области и области подвала на страницу
//
// Параметры:
// ТабДокумент 			 - табличный документ, в который выводится информация, подготовленная к печати
// Область 				 - текущая или массив текущих областей, предназначенных для вывода в данный момент
// ОбластьШапка 			 - область или массив областей, выступающих в качестве шапки и
// которые будут выводиться на каждой странице
// ОбластьПодвал 			 - область, которая будет выводиться в качестве подвала на каждой странице
// НомерСтраницы 			 - номер печатаемой страницы
// ТекстЗаголовка 			 - текст, выводимый как подпись к шапке очередного листа
// СтруктураИтоговПоСтранице - структура содержащая, накопленные итоги по текущей странице
//					 	 	 значение ключа структуры должно совпадать с идентификатором реквизита табличной части документа
// мсвДопОбластиПодвала  - подвальная часть макета. Проверка попадания последней строки таблицы на страницу
//							общего подвала Если = Неопределено, то проверка не осуществляется
// ВсегоСтраниц 			 - количество страниц выводимой
//
//Возвращаемое значение:
// Число - текущий номер страницы
//
Функция ВывестиГоризонтальнуюОбластьИНВ19(ТабДокумент, Область, ОбластьШапка = Неопределено, ОбластьПодвал = Неопределено, НомерСтраницы = 1,
	СтруктураИтоговПоСтранице = Неопределено, Права = Неопределено, мсвДопОбластиПодвала = Неопределено) 
	
	Попытка
		ПраваПользователя=Права.Права;
		ОбъектПрава=Права;
	Исключение
		ПраваПользователя=Права;
		ОбъектПрава=Неопределено;
	КонецПопытки; 
	
	ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы",ПраваПользователя,,ОбъектПрава);
	
	//если не разбиваем, выводим и все
	Если Не ИспользоватьРазбиениеНаСтраницы Тогда
		ТабДокумент.Вывести(Область);
		Возврат НомерСтраницы;
	КонецЕсли;
	
	//запишем их в один массив
	МассивОбластейДляПроверки = Новый Массив;
	МассивОбластейДляПроверки.Добавить(Область);

	Если ОбластьПодвал <> Неопределено Тогда
		МассивОбластейДляПроверки.Добавить(ОбластьПодвал);
	КонецЕсли;
	Если мсвДопОбластиПодвала <> Неопределено Тогда
		Для Каждого ТекОбласть Из мсвДопОбластиПодвала Цикл
			МассивОбластейДляПроверки.Добавить(ТекОбласть);
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		//проверим, помещаются ли на странице
		РезультатПроверки = ТабДокумент.ПроверитьВывод(МассивОбластейДляПроверки);
		
		//если не помещаются, то ...
		Если НЕ РезультатПроверки Тогда
			ВывестиИтогиПоСтраницеИНВ19(ТабДокумент,ОбластьПодвал,СтруктураИтоговПоСтранице,Права); //выводим итог по странице
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц(); //переходим на следующую страницу
			НомерСтраницы = НомерСтраницы + 1;
			Если ОбластьШапка <> Неопределено Тогда
				ОбластьШапка.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;				
				ТабДокумент.Вывести(ОбластьШапка); //выводим шапку таблицы
			КонецЕсли;
		КонецЕсли;
	Исключение 		
	КонецПопытки;
	
	//выводим текущую область
	ТабДокумент.Вывести(Область);
	
	Возврат НомерСтраницы;
КонецФункции // дкВывестиГоризонтальнуюОбласть()

// Формирует печатную форму "ИНВ19"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИНВ19(ТабДокумент) Экспорт
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ИНВ19");
	
	ФорматВыводаСуммы = "ЧДЦ=2";
	ФорматВыводаКоличества = "ЧН=0";
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				 = обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	 = ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление = спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтрокНаСтранице = 27;
	СтрокШапки      = 5;
	СтрокПодвала    = 5;
	НомерСтраницы   = 2;
	Ном             = 1;

	ИтогоРезультатИзлишекКолво   		= 0;
	ИтогоРезультатИзлишекСумма   		= 0;
	ИтогоРезультатНедостачаКолво 		= 0;
	ИтогоРезультатНедостачаСумма 		= 0;
	ИтогоПриходИзлишковКолво 	 		= 0;
	ИтогоПриходИзлишковСумма 	 		= 0;
	ИтогоСписаниеНедостачКолонка1Колво  = 0;
	ИтогоСписаниеНедостачКолонка1Сумма  = 0;
			
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента; 
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваров Тогда
		Если СкладКомпании.Розничный Тогда
			ВыборкаСтрокТовары = РегистрыНакопления.ОстаткиТоваровКомпании.СоздатьНаборЗаписей();
		Иначе
			ВыборкаСтрокТовары = РегистрыНакопления.ПартииТоваровКомпании.СоздатьНаборЗаписей();
		КонецЕсли; 
		ПростоеСписание = ИСТИНА;
	ИначеЕсли ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
		ВыборкаСтрокТовары = РегистрыНакопления.ПартииТоваровОтданные.СоздатьНаборЗаписей();
		ПростоеСписание = ЛОЖЬ;
	Иначе
		Сообщить("Для хоз операции "+ЭтотОбъект.ХозОперация.Наименование+" формирование ИНВ-19 не описано!");
		Возврат Неопределено;
	КонецЕсли; 
	ВыборкаСтрокТовары.Отбор.Регистратор.Значение = ЭтотОбъект.Ссылка;
	ВыборкаСтрокТовары.Прочитать();

	//определимся откуда будем брать многострочную часть документа
	//если движения есть, то из регистра, иначе - из табличной части документа
	ЕстьДвиженияПоРегистру = Ложь;
	Если ВыборкаСтрокТовары.Количество() > 0 Тогда
		ЕстьДвиженияПоРегистру = Истина;
	Иначе
		ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	КонецЕсли; 
	
	// Определим, что показывать в колонке кода - код или артикул
	ИмяКодаСправочника = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,Права).Имя;
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	// структура итогов по странице
	СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатИзлишекСумма,ИтогоРезультатНедостачаКолво,ИтогоРезультатНедостачаСумма,"+
	"ИтогоПриходИзлишковКолво,ИтогоПриходИзлишковСумма,ИтогоСписаниеНедостачКолонка1Колво,ИтогоСписаниеНедостачКолонка1Сумма",0,0,0,0,0,0,0,0);
	
 	ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоТаблицыИтогиПоСтранице");
	ОбластьИтоговТаблицы	= Макет.ПолучитьОбласть("ИтогоТаблицы");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьИтоговТаблицы);
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
	
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		Характеристика										 = Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			 = спПолучитьНаименование(СтрокаТовар.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					 = дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКодаСправочника,Права);
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ    = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		ОбластьМакета.Параметры.Номер = СтрокаТовар.НомерСтроки;

		Разница     = 0;
		РазницаСумм = 0;
		
		НеТребуетсяПересчетИзУпр = (ЭтотОбъект.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НеТребуетсяПересчетИзРегл = (ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
		
		Если ЕстьДвиженияПоРегистру Тогда
			Если ПростоеСписание Тогда
				Если СтрокаТовар.СкладКомпании.Розничный Тогда
					РазницаСумм = СтрокаТовар.СуммаРозн;
					Если Не НеТребуетсяПересчетИзРегл Тогда
						РазницаСумм = обПересчет(РазницаСумм, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
					КонецЕсли;
				Иначе
					Если НеТребуетсяПересчетИзРегл Тогда
						РазницаСумм = СтрокаТовар.Сумма;
					Иначе
						РазницаСумм = СтрокаТовар.СуммаУпр;
						Если НЕ НеТребуетсяПересчетИзУпр Тогда
							РазницаСумм = обПересчет(РазницаСумм, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если НеТребуетсяПересчетИзРегл Тогда
					РазницаСумм = СтрокаТовар.Сумма;
				Иначе
					РазницаСумм = СтрокаТовар.СуммаУпр;
					Если НЕ НеТребуетсяПересчетИзУпр Тогда
						РазницаСумм = обПересчет(РазницаСумм, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
			Разница = СтрокаТовар.Количество;
			Если СтрокаТовар.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
				Разница     = -Разница;
				РазницаСумм = -РазницаСумм;
			КонецЕсли; 
		Иначе
			Разница     = СтрокаТовар.КоличествоФакт - СтрокаТовар.КоличествоКнижн;
			РазницаСумм = СтрокаТовар.СуммаФакт - СтрокаТовар.СуммаКнижн;
		КонецЕсли;
		
		Если Разница = 0 Тогда
			Продолжить;
		КонецЕсли;

		Если Разница < 0 Тогда
			ОбластьМакета.Параметры.РезультатИзлишекКолво   = 0;
			ОбластьМакета.Параметры.РезультатИзлишекСумма   = 0;
			ОбластьМакета.Параметры.РезультатНедостачаКолво = Формат(-Разница,ФорматВыводаКоличества);
			ОбластьМакета.Параметры.РезультатНедостачаСумма = Формат(-РазницаСумм,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.ПриходИзлишковКолво = 0;
			ОбластьМакета.Параметры.ПриходИзлишковСумма = 0;
			ОбластьМакета.Параметры.СписаниеНедостачКолонка1Колво   = Формат(-Разница,ФорматВыводаКоличества);
			ОбластьМакета.Параметры.СписаниеНедостачКолонка1Сумма   = Формат(-РазницаСумм,ФорматВыводаСуммы);

			ИтогоРезультатИзлишекКолво   		= ИтогоРезультатИзлишекКолво   + 0;
			ИтогоРезультатИзлишекСумма   		= ИтогоРезультатИзлишекСумма   + 0;
			ИтогоРезультатНедостачаКолво 		= ИтогоРезультатНедостачаКолво + (-Разница);
			ИтогоРезультатНедостачаСумма 		= ИтогоРезультатНедостачаСумма + (-РазницаСумм);
			ИтогоПриходИзлишковКолво 	 		= ИтогоПриходИзлишковКолво 	+ 0;
			ИтогоПриходИзлишковСумма 	 		= ИтогоПриходИзлишковСумма + 0;
			ИтогоСписаниеНедостачКолонка1Колво  = ИтогоСписаниеНедостачКолонка1Колво   + (-Разница);
			ИтогоСписаниеНедостачКолонка1Сумма  = ИтогоСписаниеНедостачКолонка1Сумма   + (-РазницаСумм);
		Иначе
			ОбластьМакета.Параметры.РезультатИзлишекКолво   = Формат(Разница,ФорматВыводаКоличества);
			ОбластьМакета.Параметры.РезультатИзлишекСумма   = Формат(РазницаСумм,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.РезультатНедостачаКолво = 0;
			ОбластьМакета.Параметры.РезультатНедостачаСумма = 0;
			ОбластьМакета.Параметры.ПриходИзлишковКолво = Формат(Разница,ФорматВыводаКоличества);
			ОбластьМакета.Параметры.ПриходИзлишковСумма = Формат(РазницаСумм,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СписаниеНедостачКолонка1Колво   = 0;
			ОбластьМакета.Параметры.СписаниеНедостачКолонка1Сумма   = 0;
		
			ИтогоРезультатИзлишекКолво   		= ИтогоРезультатИзлишекКолво  + Разница;
			ИтогоРезультатИзлишекСумма  		= ИтогоРезультатИзлишекСумма  + РазницаСумм;
			ИтогоРезультатНедостачаКолво 		= ИтогоРезультатНедостачаКолво + 0;
			ИтогоРезультатНедостачаСумма		= ИтогоРезультатНедостачаСумма + 0;
			ИтогоПриходИзлишковКолво 	 		= ИтогоПриходИзлишковКолво + Разница;
			ИтогоПриходИзлишковСумма 	 		= ИтогоПриходИзлишковСумма + РазницаСумм;
			ИтогоСписаниеНедостачКолонка1Колво  = ИтогоСписаниеНедостачКолонка1Колво   + 0;
			ИтогоСписаниеНедостачКолонка1Сумма  = ИтогоСписаниеНедостачКолонка1Сумма   + 0;
		КонецЕсли; 
		
		Ном = Ном + 1;
	
		СтраницаДоВывода = НомерСтраницы;
		НомерСтраницы = ВывестиГоризонтальнуюОбластьИНВ19(ТабДокумент, ОбластьМакета, ЗаголовокТаблицы , ОбластьИтоговПоСтранице , НомерСтраницы, СтруктураИтогов, ЭтотОбъект, мсвДопОбластиПодвала);
		Если СтраницаДоВывода <> НомерСтраницы Тогда 
			СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатИзлишекСумма,ИтогоРезультатНедостачаКолво,ИтогоРезультатНедостачаСумма,"+
			"ИтогоПриходИзлишковКолво,ИтогоПриходИзлишковСумма,ИтогоСписаниеНедостачКолонка1Колво,ИтогоСписаниеНедостачКолонка1Сумма",0,0,0,0,0,0,0,0);
		КонецЕсли;

		Если Разница < 0 Тогда
			СтруктураИтогов.ИтогоРезультатИзлишекКолво   		= СтруктураИтогов.ИтогоРезультатИзлишекКолво + 0;
			СтруктураИтогов.ИтогоРезультатИзлишекСумма  		= СтруктураИтогов.ИтогоРезультатИзлишекСумма + 0;
			СтруктураИтогов.ИтогоРезультатНедостачаКолво 		= СтруктураИтогов.ИтогоРезультатНедостачаКолво + (-Разница);
			СтруктураИтогов.ИтогоРезультатНедостачаСумма 		= СтруктураИтогов.ИтогоРезультатНедостачаСумма + (-РазницаСумм);
			СтруктураИтогов.ИтогоПриходИзлишковКолво   			= СтруктураИтогов.ИтогоПриходИзлишковКолво + 0;
			СтруктураИтогов.ИтогоПриходИзлишковСумма   			= СтруктураИтогов.ИтогоПриходИзлишковСумма + 0;
			СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво  = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво + (-Разница);
			СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма  = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма + (-РазницаСумм);
		Иначе
			СтруктураИтогов.ИтогоРезультатИзлишекКолво   		= СтруктураИтогов.ИтогоРезультатИзлишекКолво + Разница;
			СтруктураИтогов.ИтогоРезультатИзлишекСумма  		= СтруктураИтогов.ИтогоРезультатИзлишекСумма + РазницаСумм;
			СтруктураИтогов.ИтогоРезультатНедостачаКолво 		= СтруктураИтогов.ИтогоРезультатНедостачаКолво + 0;
			СтруктураИтогов.ИтогоРезультатНедостачаСумма 		= СтруктураИтогов.ИтогоРезультатНедостачаСумма + 0;
			СтруктураИтогов.ИтогоПриходИзлишковКолво   			= СтруктураИтогов.ИтогоПриходИзлишковКолво + Разница;
			СтруктураИтогов.ИтогоПриходИзлишковСумма   			= СтруктураИтогов.ИтогоПриходИзлишковСумма + РазницаСумм;
			СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво  = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво + 0;
			СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма  = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма + 0;
		КонецЕсли;	
		
	КонецЦикла;

	// выведем область итогов по странице
	Если НомерСтраницы > 2 Тогда
		ВывестиИтогиПоСтраницеИНВ19(ТабДокумент,ОбластьИтоговПоСтранице,СтруктураИтогов,Права);
	КонецЕсли;
	
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатИзлишекКолво   		= Формат(ИтогоРезультатИзлишекКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатИзлишекСумма   		= Формат(ИтогоРезультатИзлишекСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатНедостачаКолво 		= Формат(ИтогоРезультатНедостачаКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатНедостачаСумма 		= Формат(ИтогоРезультатНедостачаСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоПриходИзлишковКолво   			= Формат(ИтогоПриходИзлишковКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоПриходИзлишковСумма   		 	= Формат(ИтогоПриходИзлишковСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоСписаниеНедостачКолонка1Колво 	= Формат(ИтогоСписаниеНедостачКолонка1Колво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоСписаниеНедостачКолонка1Сумма 	= Формат(ИтогоСписаниеНедостачКолонка1Сумма,ФорматВыводаСуммы);
	
	ТабДокумент.Вывести(ОбластьИтоговТаблицы);

	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	
	// Заполним информацию о руководителях и ответственных
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	ОбластьПодвал.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьПодвал.Параметры.МОЛДолжность	= ОбластьПодвал.Параметры.МОЛ.Должность;
	ОбластьПодвал.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьПодвал.Параметры.МОЛ);
	
	ТабДокумент.Вывести(ОбластьПодвал);

	Возврат ТабДокумент;
КонецФункции // ПечатьИНВ19()

// Формирует печатную форму "Пустографка"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИнвентаризация(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Инвентаризация");
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	
	//Настроим макет отчёта
	//Удаляем колонку "ЯчейкаХранения", если ни в одной из номенклатур этот реквизит не заполнен
	//Увеличиваем за её счёт колонку "Товар"
	ЕстьЯчейкиХранения=Ложь;
	Для Каждого СтрокаТЧ Из ВыборкаТабличнойЧасти Цикл
		Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.Ячейка) Тогда
			ЕстьЯчейкиХранения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьЯчейкиХранения Тогда
		ОбластьТовар = Макет.Область("Товар");
		ОбластьЯчейкаХранения = Макет.Область("ЯчейкаХранения");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЯчейкаХранения.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейкаХранения,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура();	
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		//В возвращаемой структуре нет ключа "Ячейка", создадим его
		СтруктураСтроки.Вставить("Ячейка");
		//Если ЯчейкаХранения не определена для данной номенклатуры, то печатаем пробел
		ЯчейкаДляПечати = СокрЛП(СтрокаТабличнойЧасти.Ячейка.Код);
		СтруктураСтроки.Ячейка 	= ?(обЗначениеНеЗаполнено(ЯчейкаДляПечати)," ",ЯчейкаДляПечати);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда			
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьПодвал.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИнвентаризация()

// Формирует печатную форму "Итоги по листам"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИтогиПоЛистам(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИтогиПоЛистам");
	
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);	

	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");	
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);	
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("СуммаЗакупки,СуммаПродажи",0,0);	
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	ТаблицаВременная=Новый ТаблицаЗначений();
	ТаблицаВременная.Колонки.Добавить("НомерЛиста");
	ТаблицаВременная.Колонки.Добавить("СуммаЗакупки");
	ТаблицаВременная.Колонки.Добавить("СуммаПродажи");
	
	Для Каждого СтрокаТовар Из Товары Цикл
		НоваяСтрока=ТаблицаВременная.Добавить();
		НоваяСтрока.НомерЛиста=СтрокаТовар.НомерЛиста;
		НоваяСтрока.СуммаЗакупки=СтрокаТовар.КоличествоФакт*СтрокаТовар.Цена;
		НоваяСтрока.СуммаПродажи=СтрокаТовар.КоличествоФакт*СтрокаТовар.ЦенаОтпускная;
	КонецЦикла;
	ТаблицаВременная.Свернуть("НомерЛиста","СуммаЗакупки,СуммаПродажи");
	ТаблицаВременная.Сортировать("НомерЛиста Возр");
	
	
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ТаблицаВременная Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);		
		ОбластьМакета.Параметры.СуммаЗакупки = Формат(СтрокаТабличнойЧасти.СуммаЗакупки,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаПродажи = Формат(СтрокаТабличнойЧасти.СуммаПродажи,ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ТаблицаВременная.Индекс(СтрокаТабличнойЧасти) = ТаблицаВременная.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда			
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;	
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);	
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги	
	СуммаЗакупки = ТаблицаВременная.Итог("СуммаЗакупки");
	ОбластьПодвал.Параметры.СуммаЗакупки = Формат(СуммаЗакупки,ФорматВыводаСуммы);
	
	СуммаПродажи = ТаблицаВременная.Итог("СуммаПродажи");
	ОбластьПодвал.Параметры.СуммаПродажи = Формат(СуммаПродажи,ФорматВыводаСуммы);	
	ТабДокумент.Вывести(ОбластьПодвал);			
	
	Возврат ТабДокумент;	
КонецФункции // ПечатьИтогиПоЛистам()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	// Могут быть повторяющиеся строки, свернем их
	ТаблицаТоваров = Товары.Выгрузить();	
	ТаблицаТоваров.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,Цена,ЦенаОтпускная,ХарактеристикаНоменклатуры,ДокументПередачи,Партия,ГТД,КоличествоКнижн,СуммаКнижн","КоличествоФакт,СуммаФакт");
	Если НЕ ТаблицаТоваров.Количество() = Товары.Количество() Тогда
		Товары.Загрузить(ТаблицаТоваров);
		Для Каждого СтрокаТовар Из Товары Цикл
			ОбработкаРеквизита("Товары.КоличествоКнижн",СтрокаТовар);
			ОбработкаРеквизита("Товары.КоличествоФакт",СтрокаТовар);
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ Основание = Неопределено Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если обЗначениеНеЗаполнено(Дата) Тогда
				Дата = ТекущаяДата();
			КонецЕсли;	
			Для каждого СтрокаТоваров Из Товары Цикл
				КоличествоФакт=СтрокаТоваров.КоличествоФакт;
				ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров,,Новый Структура("флЗаполнятьПоСебестоимости", Истина));
				СтрокаТоваров.КоличествоФакт=КоличествоФакт;
				ОбработкаРеквизита("Товары.КоличествоФакт",СтрокаТоваров);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(СтатьяСписанияОбнаруженнойНедостачиТМЦ) Тогда
		СтатьяСписанияОбнаруженнойНедостачиТМЦ = Справочники.СтатьиДоходовИРасходов.СписаниеОбнаруженнойНедостачиТМЦ;
	КонецЕсли; 
	Если обЗначениеНеЗаполнено(СтатьяОприходованияОбнаруженныхИзлишковТМЦ) Тогда
		СтатьяОприходованияОбнаруженныхИзлишковТМЦ = Справочники.СтатьиДоходовИРасходов.ОприходованиеОбнаруженныхИзлишковТМЦ;
	КонецЕсли; 
	Если обЗначениеНеЗаполнено(РежимКорректировкиКоличества) Тогда
		РежимКорректировкиКоличества = обПраво("КорректировкаКоличестваВИнвентаризации",Права,,ЭтотОбъект);
	КонецЕсли; 
	Если ЭтоНовый() Тогда
		ТипЦен=обПраво("ОсновнойТипЦенЗакупки",Права,,ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Комиссия=(ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию);
	
	// проверяем, присутствуют ли партии в табличной части
	ЕстьПартии = Ложь;
	Для Каждого СтрокаТовар Из Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
			ЕстьПартии = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		Если (НЕ Комиссия) И (НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ДокументПередачи)) Тогда
			СтрокаТЧ.ДокументПередачи=Неопределено;
		КонецЕсли;
		Если НЕ ((ЕстьПартии И НЕ Комиссия) И ПолучатьКнижноеКоличествоИзПартий) Тогда
			СтрокаТЧ.Партия=Неопределено;
			СтрокаТЧ.ГТД=Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// если мы проводим документ задним числом, то пересчитаем количество расчетное
	Если РежимКорректировкиКоличества=Перечисления.РежимКорректировкиКоличестваВИнвентаризации.КорректироватьКнижноеКоличество 
		И РежимЗаписи=РежимЗаписиДокумента.Проведение 
		И (НЕ ЭтоНовый()) Тогда
		Запрос = Новый Запрос();
		Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ПартииТоваровОтданныеОстатки.Номенклатура КАК Номенклатура,
			               |	ПартииТоваровОтданныеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			               |	ПартииТоваровОтданныеОстатки.ГТД КАК ГТД,
			               |	ПартииТоваровОтданныеОстатки.ДокументПередачи КАК ДокументПередачи,
			               |	СУММА(ПартииТоваровОтданныеОстатки.КоличествоОстаток) КАК Количество,
						   |	СУММА(ЕСТЬNULL(ПартииТоваровОтданныеОстатки.СуммаОстаток,0)) КАК Сумма
			               |ИЗ
			               |	РегистрНакопления.ПартииТоваровОтданные.Остатки(
			               |			&Момент,
			               |			Автомобиль = ЗНАЧЕНИЕ(Справочник.Автомобили.ПустаяСсылка)
			               |				И Номенклатура В (&Номенклатура)
			               |				И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
			               |				И Контрагент = &Контрагент
			               |				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК ПартииТоваровОтданныеОстатки
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	ПартииТоваровОтданныеОстатки.Номенклатура,
			               |	ПартииТоваровОтданныеОстатки.ХарактеристикаНоменклатуры,
			               |	ПартииТоваровОтданныеОстатки.ГТД,
			               |	ПартииТоваровОтданныеОстатки.ДокументПередачи";
		Иначе
			Если ПолучатьКнижноеКоличествоИзПартий Тогда
				ТекстЗапроса = "
				|ВЫБРАТЬ
				|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
				|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	ПартииТоваровКомпании.Партия КАК Партия,
				|	СУММА(ЕСТЬNULL(ПартииТоваровКомпании.КоличествоОстаток,0)) КАК Количество,
				|	СУММА(ЕСТЬNULL(ПартииТоваровКомпании.СуммаОстаток,0)) КАК Сумма
				|ИЗ
				|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент,Номенклатура В (&Номенклатура) И СкладКомпании=&СкладКомпании И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ПартииТоваровКомпании
				|СГРУППИРОВАТЬ ПО
				|	ПартииТоваровКомпании.Номенклатура,
				|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
				|	ПартииТоваровКомпании.Партия";
			Иначе
				ТекстЗапроса = 
				"ВЫБРАТЬ
				|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
				|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	СУММА(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток) КАК Количество,
				|	СУММА(ОстаткиТоваровКомпанииОстатки.СуммаРознОстаток) КАК Сумма
				|ИЗ
				|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
				|		&Момент,
				|		Номенклатура В (&Номенклатура)
				|			И СкладКомпании = &СкладКомпании
				|			И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки
				|
				|СГРУППИРОВАТЬ ПО
				|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
				|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
				|	ГТД
				|";
			КонецЕсли;
		КонецЕсли;
		
		Запрос.Текст=ТекстЗапроса;
		ГраницаРасчета=Новый Граница(Новый МоментВремени(Дата,Ссылка),ВидГраницы.Исключая);
		Запрос.УстановитьПараметр("Момент",?(ЭтоНовый(),КонецДня(ТекущаяДата()),ГраницаРасчета));
		Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
		Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		ТаблицаОстатковНоменклатуры = Запрос.Выполнить().Выгрузить();
		ЕстьГТД=(ТаблицаОстатковНоменклатуры.Колонки.Найти("ГТД")<>Неопределено);
		
		Для Каждого СтрокаТЧ Из Товары Цикл
			ТаблицаОстатковГТД = Неопределено;
			СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрокаТЧ.Номенклатура,СтрокаТЧ.ХарактеристикаНоменклатуры);
			Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию И НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ДокументПередачи) Тогда
				СтруктураОтбора.Вставить("ДокументПередачи", СтрокаТЧ.ДокументПередачи);
			КонецЕсли;
			Если ПолучатьКнижноеКоличествоИзПартий И (НЕ обЗначениеНеЗаполнено(СтрокаТЧ.Партия)) Тогда
				СтруктураОтбора.Вставить("Партия", СтрокаТЧ.Партия);
			КонецЕсли;
			Если ЕстьГТД И ПолучатьКнижноеКоличествоИзПартий И (НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ГТД)) Тогда
				СтруктураОтбора.Вставить("ГТД", СтрокаТЧ.ГТД);
			ИначеЕсли ПолучатьКнижноеКоличествоИзПартий И (НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ГТД)) Тогда
		        СтруктураОтбораГТД=Новый Структура("СкладКомпании,Номенклатура,Партия",СкладКомпании,СтрокаТЧ.Номенклатура,СтрокаТЧ.Партия);
				Если СтрокаТЧ.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ХарактеристикаНоменклатуры) Тогда
					СтруктураОтбораГТД.Вставить("ХарактеристикаНоменклатуры",СтрокаТЧ.ХарактеристикаНоменклатуры);
				КонецЕсли;
				СтруктураОтбораГТД.Вставить("ГТД",СтрокаТЧ.ГТД);
				ТаблицаОстатковГТД = РегистрыНакопления.ГТДПартийТоваровКомпании.Остатки(?(ЭтоНовый(),КонецДня(ТекущаяДата()),ГраницаРасчета),СтруктураОтбораГТД,,"Количество");
			КонецЕсли;
			МассивНайденныхСтрок = ТаблицаОстатковНоменклатуры.НайтиСтроки(СтруктураОтбора);
			КоличествоКнижн      = 0;
			СуммаКнижн           = 0;
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				СуммаКнижн      = СуммаКнижн + МассивНайденныхСтрок[Сч].Сумма;
				КоличествоКнижн = КоличествоКнижн + МассивНайденныхСтрок[Сч].Количество;
				Если ТаблицаОстатковГТД<>Неопределено Тогда
					СуммаКнижн=СуммаКнижн*ТаблицаОстатковГТД.Итог("Количество")/КоличествоКнижн;
					КоличествоКнижн = ТаблицаОстатковГТД.Итог("Количество"); 
				КонецЕсли;
				// удаляем за ненадобностью
				МассивНайденныхСтрок[Сч].Количество=МассивНайденныхСтрок[Сч].Количество-КоличествоКнижн;
				Если МассивНайденныхСтрок[Сч].Количество=0 Тогда
					ТаблицаОстатковНоменклатуры.Удалить(МассивНайденныхСтрок[Сч]);
				КонецЕсли;
			КонецЦикла;
			СтрокаТЧ.КоличествоКнижн = ?(СтрокаТЧ.Коэффициент=0, КоличествоКнижн, КоличествоКнижн/СтрокаТЧ.Коэффициент);
			ОбработкаРеквизита("Товары.КоличествоКнижн",СтрокаТЧ);
			СтрокаТЧ.СуммаКнижн = СуммаКнижн;
			ОбработкаРеквизита("Товары.СуммаКнижн",СтрокаТЧ);
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Заказ ИЗ РегистрНакопления.ЗаказыПокупателей ГДЕ Регистратор = &Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;

	СуммаДокумента=РассчитатьСуммуВсего();
КонецПроцедуры  //ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//для начала проверим можно ли проводить инвентаризацию,
	Отказ = МожноПроводить();
	
	// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
	РезультатЗапросаПоТоварамИзлишки   = ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект);
	РезультатЗапросаПоТоварамНедостачи = ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект,"Недостачи");
	
	// определим вид проводки
	Если ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
		НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
		// 1. Списываем недостачи
		Если НЕ РезультатЗапросаПоТоварамНедостачи.Пустой() Тогда
			НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
			НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамНедостачи;
			НаборЗаписейОстатки.ДвиженияПоРознице         = Истина;
			НаборЗаписейОстатки.ЕстьРозничнаяСумма        = Истина;
			НаборЗаписейОстатки.Резервировать             = Истина;
			Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
		КонецЕсли;
		Если Отказ Тогда
			// выведем сообщения об ошибках и предупреждениях
			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
			Возврат; // дальше смысла не имеет
		КонецЕсли;
		
		// Снимаем резервы по заказам (если таковые были)
		НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = НаборЗаписейОстатки.Выгрузить();
		НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.СнятиеРезервовЗаказовПокупателей() ИЛИ Отказ;
		
		// 2. Приходуем излишки
		Если НЕ РезультатЗапросаПоТоварамИзлишки.Пустой() Тогда
			НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
			НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамИзлишки;
			НаборЗаписейОстатки.ДвиженияПоРознице         = Истина;
			НаборЗаписейОстатки.ЕстьРозничнаяСумма        = Истина;
			НаборЗаписейОстатки.Резервировать             = Ложь;
			Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И 
					ХозОперация<>Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию И
					Движения.ЗаказыПокупателей.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	//// 2. Приходуем излишки
	//Если НЕ РезультатЗапросаПоТоварамИзлишки.Пустой() Тогда
	//	НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
	//	НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
	//	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамИзлишки;
	//	НаборЗаписейОстатки.ДвиженияПоРознице         = Истина;
	//	НаборЗаписейОстатки.ЕстьРозничнаяСумма        = Истина;
	//	Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	//КонецЕсли;
	
	// Проведем партии товаров.
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// Если инвентаризация на розничном складе, то установим измененные розничные цены.
	Если НЕ Отказ И СкладКомпании.Розничный И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда			
		// устанавливаем цены.
		НаборЗаписейЦены = Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент                = Неопределено;
		НаборЗаписейЦены.ТипЦен                    = СкладКомпании.ТипЦенРозничнойТорговли;
		НаборЗаписейЦены.РезультатЗапросаПоТоварам = Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаЦена          = "ЦенаОтпускная";
		НаборЗаписейЦены.ПодразделениеКомпании     = СкладКомпании.Подразделение;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// Проверим наличие прослеживаемых товаров, которые были реализованы
	Если НЕ Отказ Тогда
		Движения.ПартииТоваровКомпании.Записать();
		Движения.ГТДПартийТоваровКомпании.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// сбросим переменные
	РезультатЗапросаПоТоварамИзлишки   = Неопределено;
	РезультатЗапросаПоТоварамНедостачи = Неопределено;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Определим, учитываются ли в этом узле партии
СменитьФлагПолучатьКнижноеКоличествоИзПартий();


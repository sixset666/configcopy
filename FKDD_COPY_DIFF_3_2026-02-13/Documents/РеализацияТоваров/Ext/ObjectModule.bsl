// Модуль документа РеализацияТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем тКэшСуммСписания Экспорт;     // Переменная для кэширования результатов вычисления суммы списания
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Заполняет на основании заказа
//
// Параметры:
//  ЗаказПокупателя - ДокументСсылка.
//
Процедура ЗаполнитьРезервамиКонтрагента(ЗаказПокупателя) Экспорт
	
	Если обЗначениеНеЗаполнено(ЗаказПокупателя) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Номенклатура,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказыПокупателейОстатки.СкладКомпании = &СкладКомпании ТОГДА
		|			ЗаказыПокупателейОстатки.РезервОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК Количество,
		|	СУММА(ЗаказыПокупателейОстатки.СуммаУпрОстаток) КАК СуммаУпр,
		|	СУММА(ЗаказыПокупателейОстатки.СуммаОстаток) КАК Сумма,
		|	СУММА(ЗаказыПокупателейОстатки.ЗаказаноОстаток) КАК Заказано
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+",Контрагент = &Контрагент И (СкладКомпании = &СкладКомпании ИЛИ СкладКомпании = Заказ.СкладКомпании)) КАК ЗаказыПокупателейОстатки
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПокупателейОстатки.Номенклатура,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказыПокупателейОстатки.СкладКомпании = &СкладКомпании ТОГДА
		|			ЗаказыПокупателейОстатки.РезервОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) > 0 ");
		Запрос.УстановитьПараметр("НаМомент",МоментВремени());
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("СкладКомпании",?(обЗначениеНеЗаполнено(СкладКомпании), обПраво("ОсновнойСкладКомпании",Права,,ЭтотОбъект), СкладКомпании));
		
		Товары.Очистить();
		
		ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		
		ВалютаДокументаРегл = (ВалютаРегл = ВалютаДокумента);
		ВалютаДокументаУпр = (ВалютаУпр = ВалютаДокумента);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока=Товары.Добавить();
			НоваяСтрока.Номенклатура				= Выборка.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры	= Выборка.ХарактеристикаНоменклатуры;
			ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
			НоваяСтрока.Количество					= Выборка.Количество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
			ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
			Если Выборка.Количество = Выборка.Заказано Тогда
				НоваяСтрока.СуммаВсего = ?(ВалютаДокументаРегл, Выборка.Сумма, Выборка.СуммаУпр);
			Иначе
				НоваяСтрока.СуммаВсего = (?(ВалютаДокументаРегл, Выборка.Сумма, Выборка.СуммаУпр)/Выборка.Заказано)*Выборка.Количество;
			КонецЕсли;
			Если НЕ ВалютаДокументаРегл И НЕ ВалютаДокументаУпр Тогда
				НоваяСтрока.СуммаВсего = обПересчет(НоваяСтрока.СуммаВсего, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
		КонецЦикла;
	Иначе
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказыПокупателейОстатки.СкладКомпании = &СкладКомпании ТОГДА
		|			ЗаказыПокупателейОстатки.РезервОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК Количество,
		|	СУММА(ЗаказыПокупателейОстатки.ЗаказаноОстаток) КАК Заказано,
		|	СУММА(ЗаказыПокупателейОстатки.СуммаОстаток) КАК СуммаЗаказа,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.Количество * ЗаказПокупателяТовары.Коэффициент,1)) КАК КоличествоБазовое,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.ЕдиницаИзмерения,Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка))) КАК ЕдиницаИзмерения,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.Коэффициент,1)) КАК Коэффициент,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.СтавкаНДС, ЗаказыПокупателейОстатки.Номенклатура.СтавкаНДС)) КАК СтавкаНДС,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.НомерСтроки, 1000000) КАК НомерСтроки,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.СкидкаНаТовар, ЗНАЧЕНИЕ(Справочник.ТипыСкидок.ПустаяСсылка))) КАК СкидкаНаТовар,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.ПроцентСкидкиСтроки,0)) КАК ПроцентСкидкиСтроки,
		|	МАКСИМУМ(ЕСТЬNULL(ЗаказПокупателяТовары.ПроцентСкидки,0)) КАК ПроцентСкидки
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+",
		|	Контрагент = &Контрагент И 
		|	(СкладКомпании = &СкладКомпании ИЛИ СкладКомпании = &СкладЗаказа) И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
		|ПО 
		|	ЗаказыПокупателейОстатки.Заказ = ЗаказПокупателяТовары.Ссылка И 
		|	ЗаказыПокупателейОстатки.Номенклатура = ЗаказПокупателяТовары.Номенклатура И 
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказПокупателяТовары.ХарактеристикаНоменклатуры
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПокупателейОстатки.Номенклатура,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.НомерСтроки, 1000000)
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказыПокупателейОстатки.СкладКомпании = &СкладКомпании ТОГДА
		|			ЗаказыПокупателейОстатки.РезервОстаток
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) > 0 
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ
		|	СУММА(КоличествоБазовое),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(СуммаЗаказа),
		|	МАКСИМУМ(Заказано)
		|ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры");
		
		ВалютаЗаказа   = ЗаказПокупателя.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		СтруктураКурса = обКурсДляВалюты(ВалютаЗаказа,Дата);
		КурсЗаказа     = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		Запрос.УстановитьПараметр("НаМомент",МоментВремени());
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("Заказ",ЗаказПокупателя);
		Запрос.УстановитьПараметр("СкладКомпании",?(обЗначениеНеЗаполнено(СкладКомпании), обПраво("ОсновнойСкладКомпании",Права,,ЭтотОбъект), СкладКомпании));
		Запрос.УстановитьПараметр("СкладЗаказа", ЗаказПокупателя.СкладКомпании);
		
		СкидкаНаценка = ЗаказПокупателя.СкидкаНаценка;
		ОбработкаРеквизита("СкидкаНаценка");
		Товары.Очистить(); // нужные только скорректированные позиции
		ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатуры.Следующий() Цикл
			ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристик.Следующий() Цикл
				ВсегоОсталось = ВыборкаХарактеристик.Количество;
				КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
				СуммаОсталось = обПересчет(?(ВыборкаХарактеристик.Заказано <= ВыборкаХарактеристик.Количество,ВыборкаХарактеристик.СуммаЗаказа, ВыборкаХарактеристик.СуммаЗаказа/ВыборкаХарактеристик.Заказано*ВыборкаХарактеристик.Количество), ВалютаЗаказа, КурсЗаказа,ВалютаДокумента,КурсДокумента);
				ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
				НоваяСтрока   = Неопределено;
				Пока ВыборкаДетали.Следующий() Цикл
					Если ВсегоОсталось=0 Тогда
						Прервать;
					КонецЕсли;
					Если ВыборкаДетали.Количество = 0 Тогда
						Продолжить;
					КонецЕсли;
					Если КоличествоБазовоеПоЗаказу = 1 Тогда
						КоличествоСтроки = ВыборкаДетали.Количество;
					Иначе
						КоличествоСтроки = ВыборкаДетали.Количество*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
					КонецЕсли;
					ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
					НоваяСтрока=Товары.Добавить();
					НоваяСтрока.Номенклатура				= ВыборкаДетали.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры	= ВыборкаДетали.ХарактеристикаНоменклатуры;
					НоваяСтрока.ЕдиницаИзмерения			= ВыборкаДетали.ЕдиницаИзмерения;
					НоваяСтрока.Коэффициент					= ВыборкаДетали.Коэффициент;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
					НоваяСтрока.Количество					= ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);;
					НоваяСтрока.КоличествоБазовое			= ТекущееКоличество;
					НоваяСтрока.СтавкаНДС					= ВыборкаДетали.СтавкаНДС;
					ТекСумма = (СуммаОсталось/ВсегоОсталось)*ТекущееКоличество;
					СуммаОсталось = СуммаОсталось - ТекСумма;
					
					НоваяСтрока.СкидкаНаТовар = ВыборкаДетали.СкидкаНаТовар;
					НоваяСтрока.ПроцентСкидки = ВыборкаДетали.ПроцентСкидки;
					НоваяСтрока.ПроцентСкидкиСтроки = ВыборкаДетали.ПроцентСкидкиСтроки;
					
					НоваяСтрока.СуммаВсего = ТекСумма;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					
					ВсегоОсталось = ВсегоОсталось-ТекущееКоличество;
				КонецЦикла;
					
				Если ВсегоОсталось>0 ИЛИ СуммаОсталось>0 Тогда
					Если НЕ НоваяСтрока = Неопределено Тогда
						НоваяСтрока.Количество = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
						НоваяСтрока.КоличествоБазовое = НоваяСтрока.КоличествоБазовое + ВсегоОсталось;
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						НоваяСтрока.СуммаВсего = НоваяСтрока.СуммаВсего + СуммаОсталось;
					ИначеЕсли ВсегоОсталось>0 Тогда
						НоваяСтрока=Товары.Добавить();
						НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
						ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
						НоваяСтрока.Количество = ВсегоОсталось/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
						НоваяСтрока.КоличествоБазовое = НоваяСтрока.КоличествоБазовое + ВсегоОсталось;
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						НоваяСтрока.СуммаВсего = СуммаОсталось;
					КонецЕсли;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРезервамиКонтрагента()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Если ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг
		ИЛИ ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		Результат=Новый Соответствие();
		Для каждого ВидНоменклатуры Из Перечисления.ВидыНоменклатуры Цикл
			Если ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
				Результат.Вставить(ВидНоменклатуры,0);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
		ОбязательныеТовары.Вставить("ГТД",2);
	КонецЕсли;  
	
	//KamikadZe begin add 22.08.2018 14:31:32
	ОбязательныеТовары.Вставить("РабочийЛистКСО",2);
	//KamikadZe end add
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	Если ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		ОбязательныеРеквизиты.Вставить("Комитент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорКомитента",1);
	ИначеЕсли ХозОперация <> Справочники.ХозОперации.АктОбОказанииУслуг Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации И обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
				КонтролируемыеРеквизитыСостава = Новый Структура();
				КонтролируемыеРеквизитыСостава.Вставить("Партия");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыСостава);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;
	КонецЕсли;
	
	// проверим документ на повторение
	Если Результат И (НЕ обЗначениеНеЗаполнено(ДокументОснование)) И (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ДоговорПроведенияТехническогоОсмотра")) Тогда
		Если ХозОперация <> Справочники.ХозОперации.АктОбОказанииУслуг Тогда
			стрОшибка = "Для основания ""Договор проведения технического осмотра"" необходимо указать хоз. операцию ""Акт об оказании услуг"".";
			дкДобавитьОшибку(Ошибки,стрОшибка);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализацияТоваров.Ссылка
		|ИЗ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ГДЕ
		|	РеализацияТоваров.ДокументОснование = &ДокументОснование
		|	И РеализацияТоваров.Ссылка <> &ЭтаСсылка";
		Запрос.УстановитьПараметр("ДокументОснование" , ДокументОснование);
		Запрос.УстановитьПараметр("ЭтаСсылка"		  , Ссылка);
		РезЗапроса = Запрос.Выполнить().Выбрать();
		
		Если РезЗапроса.Количество() > 0 Тогда
			Результат = Ложь;
			РезЗапроса.Следующий();
			стрОшибка = "На основании документа <"+Строка(ДокументОснование)+"> уже ввен документ <"+Строка(РезЗапроса.Ссылка)+">.";
			дкДобавитьОшибку(Ошибки,стрОшибка);
		КонецЕсли;
	КонецЕсли;
	
	//Проверим ТЧ на наличие услуг при ХозОперации "РеализацияТоваровКомиссия"
	Если ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
		МассивСтрок = Новый Массив;
		НомераСтрок = " ";
		Флаг = Ложь;
		Для Каждого ТекСтрока Из ЭтотОбъект.Товары Цикл
			Если ТекСтрока.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
				НомераСтрок = НомераСтрок + ?(НомераСтрок = " ", " ", ", " ) + ТекСтрока.НомерСтроки;
				Флаг = Истина;
			КонецЕсли;
		КонецЦикла;
		Если Флаг Тогда
			Сообщить("Для хозяйственной операции <" + ХозОперация + "> не должно быть услуг в табличной части. Номера строк с услугами:" + НомераСтрок);
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РеализацияТоваров",			"Реализация товаров",Истина);
	СписокХозОпераций.Добавить("РеализацияТоваровКомиссия",	"Реализация товаров (комиссия)");
	СписокХозОпераций.Добавить("АктОбОказанииУслуг",		"Акт об оказании услуг");
	СписокХозОпераций.Добавить("РеализацияАгентскихУслуг",	"Реализация агентских услуг");
	
	Возврат СписокХозОпераций;
	
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Ссылка КАК ДокументПродажи,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.Комитент КАК Комитент,
	|	Док.ДоговорКомитента КАК ДоговорКомитента,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	ЕСТЬNULL(ДокТовары.СуммаВсего,0) КАК СуммаВсего
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(СуммаВсего) КАК СуммаВсего ИЗ Документ."+Метаданные().Имя+".Товары ГДЕ Ссылка=&Ссылка) КАК ДокТовары
	|	ПО Истина
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
		
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
		НаборЗаписейДоходыИРасходы.Доход = ШапкаДокумента.СуммаВсего;  
		
		//<<Павличенко 12.01.18
		НаборЗаписейДоходыИРасходы.ДоходБезНДС = ШапкаДокумента.СуммаВсего-ЭтотОбъект.Товары.Итог("СуммаНДС");
		//>>Павличенко 12.01.18
		
		НаборЗаписейДоходыИРасходы.Приход();
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// проверяем, присутствуют ли партии в табличной части	
	//ЕстьПартии = Ложь;
	//Для Каждого СтрокаТовар Из Товары Цикл
	//	Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
	//		ЕстьПартии = Истина;
	//		Прервать;
	//	КонецЕсли;
	//КонецЦикла;
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	
	ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	// проведем партии товаров проданные 
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	
	//НаборЗаписейПартии.ИмяРеквизитаДокумент=?(ЕстьПартии, "Партия", "");
	НаборЗаписейПартии.ИмяРеквизитаДокумент=?(обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект),"Партия","");
	
	НаборЗаписейПартии.ПоБазовомуКоличеству=Истина;
	// если и передаем товар на комиссию, то только собственный
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
		НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	КонецЕсли;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	// комиссия
	Если (ШапкаДокумента.ХозОперация=Справочники.ХозОперации.РеализацияТоваровКомиссия) Тогда
		// проведем партии товаров переданные на комиссию
		НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартииОтданные.Контрагент=ШапкаДокумента.Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.ПоБазовомуКоличеству=Истина;
		НаборЗаписейПартииОтданные.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейПартииОтданные.Приход() ИЛИ Отказ;
		
		//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
		Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
			СебестоимостьУпр=НаборЗаписейПартии.Итог("СуммаУпр");
			//сумма себестоимости списанных товаров
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.СкладКомпании.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Доход                  = СебестоимостьУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			
			НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
			СебестоимостьУпр=НаборЗаписейПартииОтданные.Итог("СуммаСебестоимостиУпр");
			//сумма себестоимости списанных товаров
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Доход                  = СебестоимостьУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
	Иначе
		// продажи
		Если НЕ Отказ Тогда
			НаборЗаписейПродажи=Движения.Продажи;
			НаборЗаписейПродажи.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейПродажи.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейПродажи.ДокументПродажи=ШапкаДокумента.ДокументПродажи;
			НаборЗаписейПродажи.Сторно=Ложь;
			НаборЗаписейПродажи.Покупатель=ШапкаДокумента.Контрагент;
			НаборЗаписейПродажи.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
			
			//НаборЗаписейПродажи.ИмяРеквизитаДокумент=?(ЕстьПартии, "Партия", "");
			НаборЗаписейПродажи.ИмяРеквизитаДокумент = ?(обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект),"Партия",""); 
			
			НаборЗаписейПродажи.Комиссия=Ложь;
			НаборЗаписейПродажи.ПоБазовомуКоличеству=Истина;
			НаборЗаписейПродажи.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// спишем реализованные комиссионные товары
	НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
	НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейРеализованныеТовары.Контрагент=ШапкаДокумента.Контрагент;
	НаборЗаписейРеализованныеТовары.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
	НаборЗаписейРеализованныеТовары.ПоБазовомуКоличеству=Истина;
	НаборЗаписейРеализованныеТовары.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
	
	// Доходы и расходы, если не комиссия
	Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда 
		// подготовим таблицу движений в разрезе подразделений только собственных списанных партий
		ТаблицаСписанийПартий = Движения.ПартииТоваровКомпании.Выгрузить();
		
		//<<Павличенко 12.01.18
		//ТаблицаСписанийПартий.Свернуть("СкладКомпании,СтатусПартии","СуммаУпр");
		ТаблицаСписанийПартий.Свернуть("СкладКомпании,СтатусПартии","СуммаУпр,СуммаНДС");
		//>>Павличенко 12.01.18
		
		СтруктураОтбора=Новый Структура("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		МассивНайденныхСтрок=ТаблицаСписанийПартий.НайтиСтроки(СтруктураОтбора);
		Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
			ТаблицаСписанийПартий.Удалить(МассивНайденныхСтрок[Сч]);
		КонецЦикла;
		
		СебестоимостьУпр = ТаблицаСписанийПартий.Итог("СуммаУпр");
		//<<Павличенко 12.01.18
		СуммаНДС = ТаблицаСписанийПартий.Итог("СуммаНДС");
		//>>Павличенко 12.01.18
		
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьУпр; 
		
		//<<Павличенко 12.01.18
		НаборЗаписейДоходыИРасходы.РасходБезНДС           = СебестоимостьУпр-СуммаНДС;
		//>>Павличенко 12.01.18
		
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		
		//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
		Если ВедетсяБалансПоПодразделению и БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.СкладКомпании.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Доход                  = СебестоимостьУпр;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Доход                  = СебестоимостьУпр;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		// подготовим таблицу движений в разрезе подразделений комиссионных товаров
		ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
		ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов", "СуммаУпр");
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
		ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение",ОписаниеТипов);
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
		КонецЦикла;
		ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
		
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
				НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
				НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЦикла;
		Иначе
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = ТаблицаСписанийПартийРеализованных.Итог("СуммаУпр");
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		//Доход от начисления дебиторской задолженности по отгруженному товару и оказанным услугам
		Запрос=Новый Запрос();
		ТекстЗапроса="
		|	ВЫБРАТЬ
		|		СУММА(РеализацияТоваровТовары.СуммаВсего) КАК СуммаУслуг,
		//<<Павличенко 12.01.18
		|		СУММА(РеализацияТоваровТовары.СуммаНДС) КАК СуммаНДС
		//>>Павличенко 12.01.18
		|	ИЗ
		|		Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|	ГДЕ
		|		РеализацияТоваровТовары.Ссылка = &ТекДок И РеализацияТоваровТовары.Номенклатура.ВидНоменклатуры=&Услуга
		|";
		Запрос.Текст=ТекстЗапроса;
		Запрос.УстановитьПараметр("ТекДок",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СуммаУслуг=Выборка.СуммаУслуг;  
			//<<Павличенко 12.01.18
			СуммаНДС=Выборка.СуммаНДС;
			//>>Павличенко 12.01.18
		КонецЦикла;
		
		СуммаТоваров=ШапкаДокумента.СуммаВсего-?(СуммаУслуг=NULL,0,СуммаУслуг); 
		
		//<<Павличенко 12.01.18
		СуммаТоваровБезНДС=ШапкаДокумента.СуммаВсего- ЭтотОбъект.Товары.Итог("СуммаНДС") - ?(СуммаУслуг=NULL,0,СуммаУслуг-СуммаНДС);
		//>>Павличенко 12.01.18
		
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=ВУпрВалюте;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
		НаборЗаписейДоходыИРасходы.Доход=СуммаТоваров;    
		
		//<<Павличенко 12.01.18
		НаборЗаписейДоходыИРасходы.ДоходБезНДС=СуммаТоваровБезНДС;
		//>>Павличенко 12.01.18  
		
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=ВУпрВалюте;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоУслугам;
		НаборЗаписейДоходыИРасходы.Доход=СуммаУслуг;  
		
		//<<Павличенко 12.01.18
		Если (НЕ СуммаУслуг=NULL) и (НЕ СуммаНДС=NULL) Тогда
			НаборЗаписейДоходыИРасходы.ДоходБезНДС=СуммаУслуг-СуммаНДС;
		КонецЕсли;
		//>>Павличенко 12.01.18
		
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// обработаем бонусы при изменении ТЧ
	Если Найти(Имя, "Товары.") > 0 И Имя <> "Товары.СуммаНДС" Тогда
		Для Каждого Строка Из Товары Цикл
			Если НЕ (Имя = "Товары.СуммаВсего" И ТекСтрока = Строка) Тогда
				Строка.СуммаВсего = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
			КонецЕсли;
			Строка.СуммаСкидкиБонусами = 0;
		КонецЦикла;
	КонецЕсли;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры	= Новый Структура();
			КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		КонецЕсли; 
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	ИначеЕсли Имя="ДокументОснование" Тогда
		#Если Клиент Тогда
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ДоговорПроведенияТехническогоОсмотра") Тогда
			ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг;
			Кнопка = ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Операция.Кнопки.Найти("АктОбОказанииУслуг");
			Если Кнопка <> Неопределено Тогда
				ЭтаФорма.ДействияФормыОперация(Кнопка);
			КонецЕсли;
		КонецЕсли;
		#КонецЕсли
	ИначеЕсли Имя="Комитент" Тогда
		
		Если ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
			
			ТекущийВидДоговора	= Перечисления.ВидыДоговоров.СКомитентом;
			
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры	= Новый Структура();
			КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",	ТекущийВидДоговора);
			
			ДоговорКомитента	= Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ЭтотОбъект.Комитент,ТекущийВидДоговора,ЭтотОбъект,ДопПараметры,Истина, Ложь);
		КонецЕсли; 
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании"
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли (ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия) И (СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранный склад: <" + СкладКомпании + "> является розничным.
			|На розничном складе невозможна реализация товаров на комиссию!",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
			Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
			Иначе
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя = "КоличествоКСписанию" Тогда
		
		// сравним бонусные баллы с суммой всего
		Если НЕ БонусныеПрограммыСервер.БонуснаяПрограммаАктивна(Карточка.БонуснаяПрограмма, Дата) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если ТипЗнч(ДопПараметры) = Тип("Структура") И ДопПараметры.Свойство("ПроверятьБлокировкуКарты")
			И БонусныеПрограммыСервер.ЗаблокированаКарточка(Карточка) Тогда
			КоличествоКСписанию = 0;
		КонецЕсли;
		
		КоличествоКСписанию = Мин(КоличествоКСписанию,
								  БонусныеПрограммыСервер.КоличествоБалловВСуммеДокумента(ЭтотОбъект, Карточка.БонуснаяПрограмма),
								  БонусныеПрограммыСервер.МаксимальноеКоличетсвоБоллов(ЭтотОбъект, Карточка.БонуснаяПрограмма));
		
		// расписываем сумму по строкам документа документам
		СводнаяТаблица = СформироватьСводнуюТаблицу();
		
		БонусныеПрограммыСервер.РаспределитьСуммуПоТаблице(СводнаяТаблица, БонусныеПрограммыСервер.БаллыВВалюту(КоличествоКСписанию, Карточка.БонуснаяПрограмма, ВалютаДокумента, Дата), "Сумма");
		
		Для Каждого Строка Из СводнаяТаблица Цикл
			Строка.Строка.СуммаВсего          = Строка.Сумма;
			Строка.Строка.СуммаНДС            = Строка.Строка.СуммаВсего*Строка.Строка.СтавкаНДС.Ставка/(100+Строка.Строка.СтавкаНДС.Ставка);
			Строка.Строка.СуммаСкидкиБонусами = Строка.РаспределенноеЗначение;
		КонецЦикла;
		
		БонусныеПрограммыСервер.РасчитатьБонусныеБаллыКНачислению(ЭтотОбъект, Карточка.БонуснаяПрограмма);
		
	   	СуммаСкидкиНаценки = Товары.Итог("СуммаСкидки");
		СуммаСкидкиНаценки = СуммаСкидкиНаценки + Товары.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиНаценки = СуммаСкидкиНаценки + Товары.Итог("СуммаСкидкиБонусами");
				
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если Рез И (НЕ ТекСтрока = Неопределено) И ЗначениеЗаполнено(ТекСтрока.Номенклатура) И 
				(ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг ИЛИ
				ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг) Тогда
			ТекСтрока.СтатьяДоходов = ТекСтрока.Номенклатура.СтатьяДопРасходов;
		КонецЕсли;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОбновитьПодборЗамен();
		КонецЕсли; 
		#КонецЕсли
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.Ячейка" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Возврат Истина;
		КонецЕсли;
		ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладКомпании);
		ТекСтрока.Ячейка=ЯчейкаСсылка;
		Возврат Истина;
	Иначе
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если Имя = "Дата" ИЛИ Имя = "ВалютаДокумента" ИЛИ Имя = "Карточка" Тогда
			ОбработкаРеквизита("КоличествоКСписанию", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

Функция СформироватьСводнуюТаблицу()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Строка");
	Таблица.Колонки.Добавить("Таблица");
	Таблица.Колонки.Добавить("Сумма");
	Таблица.Колонки.Добавить("РаспределенноеЗначение");
	
	Для Каждого Строка Из Товары Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Строка                 = Строка;
		НоваяСтрока.Таблица                = "Товары";
		НоваяСтрока.Сумма                  = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
		НоваяСтрока.РаспределенноеЗначение = 0;
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.РеализацияТоваров - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	ОсвобождентОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Товары.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.ГТД КАК РНПТ,
	|	СУММА(Продажи.Количество) КАК КоличествоПрослеживаемости,
	|	СУММА(Продажи.Сумма - Продажи.СуммаНДС) КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Регистратор = &Ссылка
	|	И Продажи.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Номенклатура,
	|	Продажи.ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваров.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваров.Дата, КВАРТАЛ) КАК ПериодОтчета,
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|				И РеализацияТоваров.Контрагент.СтранаРегистрации <> &СтранаРоссия
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВывозЗаПределыРФ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияТоваров)
	|	КОНЕЦ КАК КодОперации,
	|	РеализацияТоваров.Ссылка КАК Документ,
	|	РеализацияТоваров.Дата КАК ДатаДокумента,
	|	РеализацияТоваров.Номер КАК НомерДокумента,
	|	РеализацияТоваров.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	РеализацияТоваров.Контрагент.СтранаРегистрации КАК СтранаРегистрации
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	СтранаРоссия = Справочники.КлассификаторСтранМира.НайтиПоКоду("643");
	Если СтранаРоссия = Неопределено Тогда
		СтранаРоссия = Справочники.КлассификаторСтранМира.ПустаяСсылка();
	КонецЕсли;
	Запрос.УстановитьПараметр("СтранаРоссия", СтранаРоссия);

	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	// Проверим есть РНПТ у документа
	Если ПакетЗапросов[0].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ШапкаДокумента = ПакетЗапросов[1].Выбрать();
	ШапкаДокумента.Следующий();
	
	// Указываем для отчета только для контрагентов не из ЕАЭС
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(ШапкаДокумента.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	РНПТДокумента = ПакетЗапросов[0].Выгрузить();
	НомерДокумента = дкПолучитьНомерДляПечати(ШапкаДокумента.Документ);
	
	Для Каждого ТекущаяСтрока Из РНПТДокумента Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ШапкаДокумента);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.НомерДокумента = НомерДокумента;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

	// Формирует табличный документ 
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
	Функция ПечатьРеализацияТоваров(ТабДокумент) Экспорт
		Макет = ПолучитьМакет("РеализацияТоваров");
		
		//для начала настроим макет
		ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
		
		//bz++
		//ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
		//Если ВалютаПечатногоДокумента = Неопределено Тогда
		//	Возврат Неопределено;
		//КонецЕсли;
		//
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		//Если ПустаяСтрока(ФорматВыводаСуммы) Тогда
		//	ФорматВыводаСуммы = ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00";
		//КонецЕсли;
		
		//вывод заголовка документа
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		//ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
		//bz--
		Если ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг ИЛИ
			ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
			ТекстЗаголовка = ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy");
			ОбластьМакета.Параметры.Поставщик = "Исполнитель";
			ОбластьМакета.Параметры.Покупатель = "Заказчик";
		Иначе
			ОбластьМакета.Параметры.Поставщик = "Поставщик";
			ОбластьМакета.Параметры.Покупатель = "Покупатель";
			ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
		КонецЕсли; 
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		//bz++
		Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
			СтрокаПредставления = спПолучитьПредставление(Организация);
			ОбластьМакета.Параметры.ПредставлениеПоставщика = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
		Иначе	
			ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация);
		КонецЕсли;
		//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		//ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
		//bz--
		
		ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Контрагент);
		ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
		//вывод свойств
		СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
		ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
		ТабДокумент.Вывести(ОбластьМакета);
		НомерСтраницы = 2;
		НомерСтраницыПред = НомерСтраницы;
		//                        
		Если ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг ИЛИ
			ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
			ОбластьШапкаТаблицы.Параметры.Товар = "Услуга";
		Иначе
			ОбластьШапкаТаблицы.Параметры.Товар = "Товар";
		КонецЕсли;
		
		//теперь выводим шапку
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		//готовим области строки
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		
		//bz++
		//СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
		//bz--
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
		Если ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг ИЛИ
			ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
			ОбластьПодвал = Макет.ПолучитьОбласть("ПодвалАкта");
		Иначе
			ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		КонецЕсли;	
		
		//bz++
		//перебор строк
		ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
		//ВыборкаТабличнойЧасти=ЭтотОбъект.Товары.Выгрузить();
		//зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаПечатногоДокумента);
		//bz++
		
		Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;		
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				
				//bz++
				//СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
				//bz--
				
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
			
		КонецЦикла;
		
		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		
		//bz++
		ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
		//ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
		//bz--
		
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
		Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки")>0 ИЛИ ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки") > 0 ИЛИ ВыборкаТабличнойЧасти.Итог("СуммаСкидкиБонусами") > 0 Тогда
			СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки") + ВыборкаТабличнойЧасти.Итог("СуммаСкидкиБонусами");
			Попытка
				СкидкаВсего = СкидкаВсего + ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки");
			Исключение
			КонецПопытки;
			ОбластьПодвал.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
		КонецЕсли;
		
		//bz++
		//ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
		ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
		//bz--
		
		// сформируем информацию о бонусных баллах
		ТекстБаллы = "";
		Если ЭтотОбъект.КоличествоКСписанию > 0 Тогда
			ТекстБаллы = ТекстБаллы + "Для оплаты использованы бонусные баллы в количестве " + ЭтотОбъект.КоличествоКСписанию +
			//bz++
			//" на сумму "+(ВыборкаТабличнойЧасти.Итог("СуммаСкидкиБонусами")) + " " + ВалютаПечатногоДокумента +".";
			" на сумму "+(ЭтотОбъект.Товары.Итог("СуммаСкидкиБонусами")) + " " + ЭтотОбъект.ВалютаДокумента +".";
			//bz--
		КонецЕсли;
		
		Если ЭтотОбъект.КоличествоКНачислению > 0 Тогда
			ТекстБаллы = ТекстБаллы +?(ПустаяСтрока(ТекстБаллы), "", " ")+ "Было начислено "+ ЭтотОбъект.КоличествоКНачислению +" бонусных баллов на сумму в " +
			КоличествоКНачислению*Карточка.БонуснаяПрограмма.КратностьБонусов + " "+ Карточка.БонуснаяПрограмма.ВалютаБонуса+".";
		КонецЕсли;
		
		Если ПустаяСтрока(ТекстБаллы) Тогда
			ТекОбласть = ОбластьПодвал.Область(5, , 5, );
			ТекОбласть.АвтовысотаСтроки = Ложь;
			ТекОбласть.ВысотаСтроки = 1;
			ОбластьПодвал.Область(5, 2, 5, 2).Текст = "";
		Иначе
			ОбластьПодвал.Параметры.БонусныеБаллы = ТекстБаллы;
		КонецЕсли;
		
		// Выводим представления и расшифровки подписей
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
		Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
		ОбластьПодвал.Параметры.Заполнить(Отпустил);
		Получил  = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
		Получил.Вставить("ПолучилПредставление", ?(обЗначениеНеЗаполнено(Получил.ПолучилКонтрагент),"","/ "+ Получил.ПолучилКонтрагентПредставление));
		//Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
		ОбластьПодвал.Параметры.Заполнить(Получил);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьРеализацияТоваров()

	// Формирует табличный документ 
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
	Функция ПечатьПодборочныйЛист(ТабДокумент) Экспорт
		Макет = ПолучитьМакет("ПодборочныйЛист");
		
		ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
		
		//Настроим макет отчёта
		//Удаляем колонку "ЯчейкаХранения", если ни в одной из номенклатур этот реквизит не заполнен
		//Увеличиваем за её счёт колонку "Товар"
		ЕстьЯчейкиХранения=Ложь;
		Для Каждого СтрокаТЧ Из ВыборкаТабличнойЧасти Цикл
			Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.Ячейка) Тогда
				ЕстьЯчейкиХранения = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЕстьЯчейкиХранения Тогда
			ОбластьТовар = Макет.Область("Товар");
			ОбластьЯчейкаХранения = Макет.Область("ЯчейкаХранения");
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЯчейкаХранения.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьЯчейкаХранения,ТипСмещенияТабличногоДокумента.ПоВертикали);
		КонецЕсли;	
		
		//Настроим макет
		ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
		
		//вывод заголовка документа
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект, "Подборочный лист");
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		//bz++
		Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
			СтрокаПредставления = спПолучитьПредставление(Организация);
			ОбластьМакета.Параметры.ПредставлениеПоставщика = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
		Иначе	
			ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация);
		КонецЕсли;
		//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		//ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
		//bz--
		
		ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Контрагент);
		ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
		//вывод свойств
		СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
		ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
		ТабДокумент.Вывести(ОбластьМакета);
		НомерСтраницы = 2;
		НомерСтраницыПред = НомерСтраницы;
		
		//теперь выводим шапку
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		//готовим области строки
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		СтруктураИтоговПоСтранице = Новый Структура();
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		//перебор строк
		Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			//В возвращаемой структуре нет ключа "Ячейка", создадим его
			СтруктураСтроки.Вставить("Ячейка");
			//Если ЯчейкаХранения не определена для данной номенклатуры, то печатаем пробел
			ЯчейкаДляПечати = СокрЛП(СтрокаТабличнойЧасти.Ячейка.Код);
			СтруктураСтроки.Ячейка 	= ?(обЗначениеНеЗаполнено(ЯчейкаДляПечати)," ",ЯчейкаДляПечати);
			ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;		
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура();
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
			
		КонецЦикла;
		
		//Итогов по странице на этой печатной форме нет, но выводим для прорисовки завершающей чёрной линии снизу страницы
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//итоги
		
		// Выводим представления и расшифровки подписей
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
		Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
		ОбластьПодвал.Параметры.Заполнить(Отпустил);
		Получил  = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
		Получил.Вставить("ПолучилПредставление", ?(обЗначениеНеЗаполнено(Получил.ПолучилКонтрагент),"","/ "+ Получил.ПолучилКонтрагентПредставление));
		//Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
		ОбластьПодвал.Параметры.Заполнить(Получил);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьПодборочныйЛист()

	// Формирует печатную форму "ТОРГ-12"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
	Функция ПечатьТОРГ12(ТабДокумент) Экспорт
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
		
		// Получим валюту регламентированного учета для перерасчета цены и суммы
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		
		// Зададим параметры макета
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 0;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		Макет = ПолучитьОбщийМакет("ТОРГ12");
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		
		СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		
		// получим р/с из свойств документа
		РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
		
		// Правим, что автоматом не заполнилось, или заполнилось неправильно
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		
		//bz++
		ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.ПодразделениеКомпании,СтруктураПредставления2,Истина,РасчетныйСчетДокумента);
		//ДополнительныеПараметры.ДляПечати = Истина;
		//ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
		//ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		//	ОбластьМакета.Параметры.ПодразделениеКомпании,СтруктураПредставления2, ДополнительныеПараметры);
		//bz--
		
		ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикОрганизация",ЭтотОбъект.Организация);
		ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
		ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
		ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
		ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
		ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);
		
		//дальше заполнить Шапку
		ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
		ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
		ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
		
		ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
		ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
		ОбластьМакета.Параметры.ОснованиеДата = "";
		ОбластьМакета.Параметры.ОснованиеНомер = ""; 
		
		ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);	
		ТабДокумент.Вывести(ОбластьМакета);
		
		НомерСтраницы	= 2;
		НомерСтраницыПред = НомерСтраницы;
		КоличествоСтрокНаТекущейСтранице=0;
		
		ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
		ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
		//!!! TimR: В новой пф данное поле не выводится
		//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
		
		СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		
		
		ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
		ВыборкаСтрок = ЭтотОбъект.Товары;
		
		КоличествоСтрок = ВыборкаСтрок.Количество();
		
		// инициализация итогов по документу
		ИтогоКоличество = 0;
		ИтогоСумма      = 0;
		ИтогоНДС        = 0;
		ИтогоСуммаСНДС  = 0;
		
		СоответствиеИтоговПоставкамНДС = Новый Соответствие();
		
		//misha
		НДС01018=ложь;
		Запрос = Новый Запрос;
		Запрос.Текст =    "ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Значение
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Свойство.Код = ""02076""
		|	И ЗначенияСвойствОбъектов.Объект = &Объект";
		Запрос.УстановитьПараметр("Объект",ЭтотОбъект.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() и Выборка.Значение = Истина Тогда
			НДС01018=Истина;	
		КонецЕсли;
		//misha

		
		// Выводим многострочную часть документа
		Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,, ЭтотОбъект);
			ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
			ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.КоличествоБазовое,ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
			//ОбластьСтрока.Параметры.ТоварНаименование 			= СтруктураСтроки.ТоварНаименование + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "");
			
			//misha
			Если НДС01018 Тогда
				ОбластьСтрока.Параметры.СтавкаНДС ="0/10/18";	
			КонецЕсли;
			//misha
			
			// Пересчитаем сумму в соответствии с регламентированной валютой
			СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
			СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
			СуммаБезНДС = СуммаВсего - СуммаНДС;
			
			ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаНДС    = Формат(СуммаНДС, ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
			
			// Рассчитаем цену.
			ОбластьСтрока.Параметры.ЦенаБезНДС = Формат(СуммаБезНДС / (СтрокаТоваров.Количество * ?(СтрокаТоваров.Коэффициент = 0, 1, СтрокаТоваров.Коэффициент)), ФорматВыводаСуммы);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьВсего);
				//!!! TimR: В новой пф данное поле не выводится
				//мсвДопОбластиПодвала.Добавить(ОбластьВсегоСтавкаНДС);
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			Если КоличествоСтрокНаТекущейСтранице=0 Тогда
				ТабДокумент.Вывести(ОбластьСтрока);
			Иначе
				//выводим строку, делая проверку попадания на лист		
				НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			КонецЕсли; 
			
			// увеличим итоги по странице
			СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
			СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.КоличествоБазовое;
			СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
			СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
			СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
				НомерСтраницыПред = НомерСтраницы;
				КоличествоСтрокНаТекущейСтранице=0;
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			Иначе
				КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
			КонецЕсли;
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
			
			// итоги по ставкам НДС
			Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
				Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
					СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
				КонецЕсли;
				СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
			КонецЕсли;
			
			//Сформируем итоги по документу
			ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
			ИтогоНДС        = ИтогоНДС + СуммаНДС;
			ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
			
		КонецЦикла;
		
		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 1 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		// Выводим итоги по документу в целом
		ОбластьВсего.Параметры.ИтогКоличество = Формат(ВыборкаСтрок.Итог("КоличествоБазовое"), ФорматВыводаКоличества);
		ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма, ФорматВыводаСуммы);
		ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС, ФорматВыводаСуммы);
		ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьВсего);
		
		//!!! TimR: В новой пф данное поле не выводится
		//// выведем итоги по ставкам НДС
		//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
		//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "
		//	+дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
		//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
		//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьВсегоСтавкаНДС, , , НомерСтраницы,,ЭтотОбъект);
		//КонецЦикла;
		
		// Выводим подвал документа
		ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
		ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
		ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
		
		Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
		Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
			ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
			ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
			ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
			
			//bz++
			//ВладелецДоверенности = Доверенность.Владелец;
			//Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			//	 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			//	ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
			//ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			//	ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
			//КонецЕсли;
			//bz--
			
		КонецЕсли; 
		
		ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
		ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
		ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
		
		// Заполним информацию о руководителях организации
		Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
		ОбластьПодвал.Параметры.Заполнить(Руководитель);
		
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
		ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
		
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
		ОбластьПодвал.Параметры.Заполнить(Отпустил);
		
		Получил = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
		Получил.Вставить("ПолучилПредставление", ?(обЗначениеНеЗаполнено(Получил.ПолучилКонтрагент),"","/ "+ Получил.ПолучилКонтрагентПредставление));
		ОбластьПодвал.Параметры.Заполнить(Получил);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
		
		ТабДокумент.АвтоМасштаб = Истина;
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьТОРГ12()
	
// Формирует печатную форму Приложение №4 "Транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриложение4(ТабДокумент) Экспорт
	ТабДокумент = дкПечатьНовойТТН(ЭтотОбъект,ТабДокумент);
	Возврат ТабДокумент;
КонецФункции // ПечатьПриложение4()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

	// Формирует печатную форму "М-15"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
	Функция ПечатьМ15(ТабДокумент) Экспорт
		Макет = ПолучитьОбщийМакет("М15");
		
		// Зададим параметры макета
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 0;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
		
		ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
		
		// выводим шапку
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ОбластьМакета.Параметры.НомерДокумента = дкПолучитьНомерДляПечати(ЭтотОбъект);
		
		//bz++
		Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
			СтрокаПредставления = спПолучитьПредставление(Организация);
			ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
		Иначе	
			ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
		КонецЕсли;
		//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		//ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
		//bz--
		
		ОбластьМакета.Параметры.ОрганизацияПоОКПО = Организация.КодПоОКПО;
		ОбластьМакета.Параметры.ДатаСоставления = Формат(Дата, "ДФ = дд.ММ.гггг");
		ОбластьМакета.Параметры.ПодразделениеНаименование = спПолучитьНаименование(ПодразделениеКомпании);
		Если Не обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда 
			ОбластьМакета.Параметры.Основание = спПолучитьНаименование(ДоговорВзаиморасчетов);
		КонецЕсли;
		ОбластьМакета.Параметры.КонтрагентНаименование = спПолучитьНаименование(Контрагент);
		ОбластьМакета.Параметры.Получатель = обПолучитьЗначениеСвойства(ЭтотОбъект,"Получатель");
		ТабДокумент.Вывести(ОбластьМакета); // конец шапки
		
		ВыборкаСтрок = ЭтотОбъект.Товары;
		
		//подвал выводим во временный
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(ВыборкаСтрок.Количество(), ,",,,,,,,,0");
		ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ВыборкаСтрок.Итог("Сумма"), ВалютаДокумента);
		ОбластьПодвал.Параметры.ИтогНДС = обЧислоПрописью(ВыборкаСтрок.Итог("СуммаНДС"), ВалютаДокумента);
		Руководитель = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.Руководитель);
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект, "Отпустил");
		Получил = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
		Получил.Вставить("ПолучилПредставление", Получил.ПолучилКонтрагентПредставление);
		ОбластьПодвал.Параметры.Заполнить(Руководитель);
		ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьПодвал.Параметры.Заполнить(Отпустил);
		ОбластьПодвал.Параметры.Заполнить(Получил);
		ТабДокументПодвал = Новый ТабличныйДокумент;
		ТабДокументПодвал.Вывести(ОбластьПодвал);
		
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьРазделителя = Макет.ПолучитьОбласть("Разделитель");
		
		//сразу два, т.к. выводим на второй странице только
		НомерСтраницы = 2;
		НомерСтраницыПред = НомерСтраницы;
		
		// выводим заголовок таблицы
		ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
		ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		// Выводим многострочную часть документа
		Для каждого СтрокаТабличнойЧасти Из ВыборкаСтрок Цикл
			СтавкаНДС = СтрокаТабличнойЧасти.Номенклатура.СтавкаНДС.Ставка;
			
			ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
			ОбластьСтрока.Параметры.ТоварНаименование            = спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура);
			ОбластьСтрока.Параметры.ТоварКод					 = СтрокаТабличнойЧасти.Номенклатура.Код;
			ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод 		 = СтрокаТабличнойЧасти.Номенклатура.БазоваяЕдиницаИзмерения.Код;
			ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТабличнойЧасти.Номенклатура.БазоваяЕдиницаИзмерения;
			ОбластьСтрока.Параметры.Количество 					 = Формат(СтрокаТабличнойЧасти.Количество,ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.СуммаБезНДС 				 = Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.Цена 						 = Формат(СтрокаТабличнойЧасти.Сумма/СтрокаТабличнойЧасти.Количество,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаСНДС 					 = Формат(СтрокаТабличнойЧасти.СуммаВсего,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаНДС					 = Формат(СтрокаТабличнойЧасти.СуммаНДС,ФорматВыводаСуммы);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаСтрок.Индекс(СтрокаТабличнойЧасти) = ВыборкаСтрок.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ТабДокументПодвал);
			КонецЕсли;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьРазделителя,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				НомерСтраницыПред = НомерСтраницы;
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
		КонецЦикла;
		
		// выводим подвал
		ТабДокумент.Вывести(ТабДокументПодвал); // конец подвала
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьМ15()

// Формирует печатную форму "УПД"
// Возвращает сформированный табличный документ:
Функция ПечатьУПД(ТабДокумент) Экспорт
	
	//ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
	
	//Возврат ТабДокумент;  
	
	// Сомов ВС проверка с/ф
	Если НЕ ЗначениеЗаполнено(Документы.СчетФактураВыданный.НайтиПоРеквизиту("ДокументОснование", ЭтотОбъект.Ссылка)) Тогда
		Сообщить("Не заполнена Счет-Фактура!")
	Иначе
		ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
		Возврат ТабДокумент;
	КонецЕсли;	
	
КонецФункции //ПечатьУПД()

	// Формирует печатную форму "Акт об оказании услуг"
	// Возвращает сформированный табличный документ:
	Функция ПечатьАктОбОказанииУслуг(ТабДокумент) Экспорт
		
		Если ХозОперация <> Справочники.ХозОперации.АктОбОказанииУслуг
			И ХозОперация <> Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
			
			Сообщить(СтрШаблон(НСтр("ru = 'Печатная форма Акта об оказании услуг не доступна для Хоз.операции <%1>'"), ХозОперация));
			Возврат Неопределено;
			
		КонецЕсли;
		ДатаПечатная = Дата;
		
		Макет = ПолучитьМакет("АктОбОказанииУслуг");
		
		ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
		
		НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
		
		//зададим параметры макета
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
		Если ВалютаПечатногоДокумента=Неопределено Тогда Возврат Неопределено; КонецЕсли; 
		ТаблицаРабот=Товары.Выгрузить();
		зфПерерасчетТаблицыТоваров(ТаблицаРабот,ЭтотОбъект,ВалютаПечатногоДокумента,Истина);
		
		ЕстьСкидка = Истина;
		Если ТаблицаРабот.Итог("СуммаСкидки")=0 И ТаблицаРабот.Итог("СуммаСкидкиСтроки")=0 И ТаблицаРабот.Итог("СуммаСкидкиБонусами")=0 Тогда
			ЕстьСкидка = Ложь;
			ОбластьСкидка = Макет.Область("Скидка");
			ОбластьУслуга = Макет.Область("Услуга");
			
			ОбластьУслуга.ШиринаКолонки = ОбластьУслуга.ШиринаКолонки + ОбластьСкидка.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьСкидка, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
		КонецЕсли;
		
		ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаДок = Формат(ДатаПечатная, "ДФ = дд.ММ.гггг");
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.ДляПечати = Истина;
		ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(
		ЭтотОбъект.ПодразделениеКомпании, , ДополнительныеПараметры);
		ОбластьМакета.Параметры.ПредставлениеПолучателя = спПолучитьПредставление(ЭтотОбъект.Контрагент);
		СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
		ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
		ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;

		ТабДокумент.Вывести(ОбластьМакета);
		
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ДокументОснование");
			ОбластьМакета.Параметры.ДокументОснованиеПредставление = дкПолучитьПредставление(ДокументОснование);
			ОбластьМакета.Параметры.ДокументОснование=ДокументОснование;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабДокумент.Вывести(ОбластьШапка);

		НомерСтраницы = 2;
		НомерСтраницыПред = 2;
		
		ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Акт об оказании услуг № %1 от %2'"),НомерДляПечати, Формат(ДатаПечатная, "ДЛФ=D"));
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапка.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапка.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
		//Вывод табличной части работ
		
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, СуммаВсего, СуммаНДС, СуммаСкидки",ВалютаПечатногоДокумента, 0, 0, 0);
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьИтогПоСтранице = Макет.ПолучитьОбласть("ИтогиПоСтранице");
		
		ВыборкаТабличнойЧасти = ТаблицаРабот;
		Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			ОбластьСтрока.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
			ОбластьСтрока.Параметры.Работа = спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура);
			ОбластьСтрока.Параметры.Количество = Формат(СтрокаТабличнойЧасти.Количество, ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.Цена = Формат(СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы);
			Если ЕстьСкидка Тогда
				ОбластьСтрока.Параметры.СуммаСкидки = Формат(СтрокаТабличнойЧасти.СуммаСкидки+СтрокаТабличнойЧасти.СуммаСкидкиСтроки+СтрокаТабличнойЧасти.СуммаСкидкиБонусами, ФорматВыводаСуммы);
			КонецЕсли;
			ОбластьСтрока.Параметры.СуммаВсего = Формат(СтрокаТабличнойЧасти.СуммаВсего, ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаНДС = Формат(СтрокаТабличнойЧасти.СуммаНДС, ФорматВыводаСуммы);
			
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапка, ОбластьИтогПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, СуммаВсего, СуммаНДС, СуммаСкидки",ВалютаПечатногоДокумента , 0, 0, 0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапка.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			
			//обновим итоги по странице
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти, СтруктураИтоговПоСтранице);
		КонецЦикла; 
		
		//довыводим последний итог по странице, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьИтогПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
		КонецЕсли;
		
		СуммаВсего=ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		СуммаНДС=ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");
		Если ЕстьСкидка Тогда
			ОбластьМакета.Параметры.СуммаСкидки = Формат(ВыборкаТабличнойЧасти.Итог("СуммаСкидки")+ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки")+ВыборкаТабличнойЧасти.Итог("СуммаСкидкиБонусами"), ФорматВыводаСуммы);
		КонецЕсли;
		
		ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаНДС = Формат(СуммаНДС,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего услуг "+ВыборкаТабличнойЧасти.Количество()+" на сумму "+Формат(СуммаВсего,ФорматВыводаСуммы)+" "+ВалютаПечатногоДокумента+" (в т.ч. НДС "+Формат(СуммаНДС,ФорматВыводаСуммы)+" "+ВалютаПечатногоДокумента+")";
		ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		// Выводим представления и расшифровки подписей
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
		Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		Получил  = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
		Получил.Вставить("ПолучилПредставление", ?(обЗначениеНеЗаполнено(Получил.ПолучилКонтрагент),"","/ "+ Получил.ПолучилКонтрагентПредставление));
		ОбластьМакета.Параметры.Заполнить(Получил);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		Возврат ТабДокумент;
	КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
		БлокироватьПерерасчетСкидок	= Истина;
		//bz++
		//ХозОперация		= Справочники.ХозОперации.РеализацияАгентскихУслуг;
		ХозОперация		= Справочники.ХозОперации.АктОбОказанииУслуг;
		//bz--
	КонецЕсли;
	
	ОбработкаЗаполненияОтказ = НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание);
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда		
		
		//Если ТипЗнч(Основание.Страхователь)=Тип("СправочникСсылка.Контрагенты") Тогда
		//	Контрагент		= Основание.Страхователь;
		//	ОбработкаРеквизита("Контрагент");
		//КонецЕсли;  
		
		//misha
		Контрагент		= Основание.ВариантыСтрахования[0].Страховщик;
		ОбработкаРеквизита("Контрагент");
		//misha

		//СписокСтраховщиков	= Новый СписокЗначений();
		//Для Каждого СтрокаСтрахования Из Основание.ВариантыСтрахования Цикл
		//	Если СписокСтраховщиков.НайтиПоЗначению(СтрокаСтрахования.Страховщик)=Неопределено Тогда
		//		СписокСтраховщиков.Добавить(СтрокаСтрахования.Страховщик);
		//	КонецЕсли;
		//КонецЦикла;
		
		//Если СписокСтраховщиков	.Количество()=1 Тогда
		//	Комитент	= СписокСтраховщиков[0].Значение;
		//	ОбработкаРеквизита("Комитент");
		//ИначеЕсли СписокСтраховщиков.Количество()>1 Тогда						
		//	ФормаВыбора	= Справочники.Контрагенты.ПолучитьФорму("ФормаСписка");
		//	ФормаВыбора.РежимВыбора			= Истина;
		//	ФормаВыбора.ЗакрыватьПриВыборе	= Истина;
		//	ФормаВыбора.ЭлементыФормы.Список.ИзменятьИерархическийПросмотр	= Ложь;
		//	ФормаВыбора.ЭлементыФормы.Список.ИерархическийПросмотр	= Ложь;
		//	ФормаВыбора.Отбор.Ссылка.ВидСравнения	= ВидСравнения.ВСписке;
		//	ФормаВыбора.Отбор.Ссылка.Значение		= СписокСтраховщиков;
		//	ФормаВыбора.Отбор.Ссылка.Использование	= Истина;
		//	ФормаВыбора.Отбор.ЭтоГруппа.Установить(Ложь);
		//	
		//	Выбрано	= ФормаВыбора.ОткрытьМодально();
		//	
		//	Если ТипЗнч(Выбрано)=Тип("СправочникСсылка.Контрагенты") Тогда
		//		Комитент	= Выбрано;
		//		ОбработкаРеквизита("Комитент");
		//	КонецЕсли;
		//КонецЕсли;
			
		//Для Каждого ТекСтрока Из Основание.ВариантыСтрахования Цикл
		//	НоваяСтрока	= Товары.Добавить();
		//	НоваяСтрока.Номенклатура	= ТекСтрока.ПрограммаСтрахования.АгентскаяУслуга;
		//	ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);			
		//	НоваяСтрока.СуммаВсего		= ТекСтрока.СуммаПремии;
		//	ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
		//КонецЦикла;
		
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		
		КодыМаркировки.Очистить();
		Если Не обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
			ТаблТовары = Основание.Товары.Выгрузить();
			ТаблТовары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,Ячейка", "Количество");
			Товары.Загрузить(ТаблТовары);
		КонецЕсли;
		
		
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ДоговорПроведенияТехническогоОсмотра") Тогда		
		ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг;
		БлокироватьПерерасчетСкидок=Истина;
		Товары.Очистить();
		НоваяСтрока=Товары.Добавить();
		НоваяСтрока.Номенклатура=Основание.Номенклатура;
		НоваяСтрока.Количество=1;
		НоваяСтрока.ЕдиницаИзмерения=НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
		НоваяСтрока.Коэффициент=НоваяСтрока.ЕдиницаИзмерения.Коэффициент;
		НоваяСтрока.КоличествоБазовое=1;
		НоваяСтрока.Цена=Основание.Цена;
		НоваяСтрока.Сумма=Основание.СуммаДокумента;
		НоваяСтрока.СтавкаНДС=Основание.СтавкаНДС;
		НоваяСтрока.СуммаНДС=Основание.СуммаНДС;
		НоваяСтрока.СуммаВсего=Основание.СуммаДокумента;
		ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);   
		
	//БизТех Аляль 02.11.2025г. <<
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		Если Основание.Аукцион Тогда
			ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг;
			БлокироватьПерерасчетСкидок=Истина;
			Товары.Очистить();
			НоваяСтрока=Товары.Добавить();
			НоваяСтрока.Номенклатура=Справочники.Номенклатура.НайтиПоНаименованию("Лицензионный платеж ");  
			НоваяСтрока.СтатьяДоходов = Справочники.СтатьиДоходовИРасходов.Вознаграждение;
			НоваяСтрока.Количество=1;
			НоваяСтрока.ЕдиницаИзмерения=НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
			НоваяСтрока.Коэффициент=НоваяСтрока.ЕдиницаИзмерения.Коэффициент;
			НоваяСтрока.КоличествоБазовое=1;
			НоваяСтрока.Цена=50000;
			НоваяСтрока.Сумма=50000;
			НоваяСтрока.СтавкаНДС=Основание.СтавкаНДСНаАвтомобиль;
			//НоваяСтрока.СуммаНДС=Основание.СуммаНДС;
			НоваяСтрока.СуммаВсего=50000;
			ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока); 
		 КонецЕсли;
	КонецЕсли;
	//БизТех Аляль >>
	
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
			ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
		Иначе
			ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ОбработкаЗаполненияОтказ Тогда Возврат; КонецЕсли;
	
	Если НЕ Основание = Неопределено Тогда
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			Контрагент = Неопределено; ДоговорВзаиморасчетов = Неопределено;
			ТипЦенНовый = обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);	
			Если ТипЦен<>ТипЦенНовый Тогда
				ТипЦен=ТипЦенНовый;
				Если НЕ ТипЦен.ВВалютеУчета Тогда
					Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
						ВалютаДокумента=ТипЦен.ВалютаЦены;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		//Если ТипЗнч(Основание)<>Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
		//	ХозОперация = Справочники.ХозОперации.РеализацияТоваров;
		//КонецЕсли;
		Если Основание.ХозОперация = Справочники.ХозОперации.РасходныйСкладскойОрдер Тогда
			Если Не обЗначениеНеЗаполнено(Основание.ВладелецТовара) Тогда
				Если ТипЗнч(Основание.ВладелецТовара) = Тип("СправочникСсылка.Контрагенты") Тогда
					Контрагент = Основание.ВладелецТовара;
					ОбработкаРеквизита("Контрагент");
				КонецЕсли;
			КонецЕсли;
			Для Каждого СтрТовар Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура", СтрТовар);
			КонецЦикла;
			
			// заполняем ячейки
			Попытка
				Для Каждого стрТовар Из Товары Цикл
					Если стрТовар.Номенклатура = Основание.Товары[стрТовар.НомерСтроки-1].Номенклатура Тогда
						стрТовар.Ячейка = Основание.Товары[стрТовар.НомерСтроки-1].Ячейка;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
		ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров И НЕ обЗначениеНеЗаполнено(Основание.СкладПолучатель)Тогда
			Если Основание.СкладПолучатель.Розничный Тогда
				ТипЦен = Основание.СкладПолучатель.ТипЦенРозничнойТорговли;
				Для Каждого СтрТовар Из Товары Цикл
					ОбработкаРеквизита("Товары.Номенклатура", СтрТовар);
				КонецЦикла;
			КонецЕсли;
			
			// заполняем ячейки
			Попытка
				Для Каждого стрТовар Из Товары Цикл
					Если стрТовар.Номенклатура = Основание.Товары[стрТовар.НомерСтроки-1].Номенклатура Тогда
						стрТовар.Ячейка = Основание.Товары[стрТовар.НомерСтроки-1].ЯчейкаПолучатель;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
			ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковТоваров") ИЛИ
			ТипЗнч(Основание)=Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			Контрагент = Неопределено;
			ДоговорВзаиморасчетов = Неопределено;
			ТипЦенНовый = обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
			Если ТипЦен<>ТипЦенНовый Тогда
				ТипЦен=ТипЦенНовый;
				Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
					ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
				КонецЕсли; 
			КонецЕсли; 
			СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ВыборочноеСписаниеПартий=обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект);
			СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
			ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
			Если СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
				ПартияРеализации = ПартияТоваровОтрицательныхОстатков;
			Иначе
				ПартияРеализации = ДокументОснование;
				Запрос=Новый Запрос;
				Запрос.Текст="ВЫБРАТЬ
				             |	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
				             |	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				             |	СУММА(ПартииТоваровКомпанииОстатки.КоличествоОстаток) КАК Количество
				             |ИЗ
				             |	РегистрНакопления.ПартииТоваровКомпании.Остатки(
				             |			,
				             |			СкладКомпании = &СкладКомпании
				             |				И Партия = &Партия) КАК ПартииТоваровКомпанииОстатки
				             |
				             |СГРУППИРОВАТЬ ПО
				             |	ПартииТоваровКомпанииОстатки.Номенклатура,
				             |	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры";
				Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
				Запрос.УстановитьПараметр("Партия",ПартияРеализации);
				ОстаткиПартий=Запрос.Выполнить().Выгрузить();
				Запрос.Текст="ВЫБРАТЬ
				             |	ГТДПартийТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
				             |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				             |	ГТДПартийТоваровКомпанииОстатки.ГТД КАК ГТД,
				             |	СУММА(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток) КАК Количество
				             |ИЗ
				             |	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
				             |			,
				             |			СкладКомпании = &СкладКомпании
				             |				И Партия = &Партия) КАК ГТДПартийТоваровКомпанииОстатки
				             |
				             |СГРУППИРОВАТЬ ПО
				             |	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
				             |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
				             |	ГТДПартийТоваровКомпанииОстатки.ГТД";
				ОстаткиГТД=Запрос.Выполнить().Выгрузить();
			КонецЕсли;	
			Для Каждого СтрТовар Из Товары Цикл
				Если ВыборочноеСписаниеПартий Тогда
					СтрТовар.Партия = ПартияРеализации;
				Иначе
					СтрТовар.Партия=Неопределено;
					СтрТовар.ГТД=Неопределено;
				КонецЕсли;
				Если СтратегияСписанияПартийТоваровПоДатам<>Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
					СтруктураПоиска=Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрТовар.Номенклатура,СтрТовар.ХарактеристикаНоменклатуры);
					ОстаткиТекущейПартии=ОстаткиПартий.НайтиСтроки(СтруктураПоиска);
					Если обЗначениеНеЗаполнено(СтрТовар.ГТД) Тогда
						ОстаткиТекущейГТД=Неопределено;
					Иначе
						СтруктураПоиска.Вставить("ГТД",СтрТовар.ГТД);
						ОстаткиТекущейГТД=ОстаткиГТД.НайтиСтроки(СтруктураПоиска);
					КонецЕсли; 
					Если обЗначениеНеЗаполнено(СтрТовар.ГТД) Тогда
						Если ОстаткиТекущейПартии.Количество()>0 Тогда
							СтрТовар.Количество=Мин(ОстаткиТекущейПартии[0].Количество/СтрТовар.Коэффициент,СтрТовар.Количество);
							ОстаткиТекущейПартии[0].Количество=СтрТовар.Количество*СтрТовар.Коэффициент;
							Если ОстаткиТекущейПартии[0].Количество=0 Тогда
								ОстаткиПартий.Удалить(ОстаткиТекущейПартии[0]);
							КонецЕсли; 
						Иначе
							СтрТовар.Количество=0;
						КонецЕсли; 
					Иначе
						Если ОстаткиТекущейГТД.Количество()>0 Тогда
							СтрТовар.Количество=Мин(ОстаткиТекущейГТД[0].Количество/СтрТовар.Коэффициент,СтрТовар.Количество);
							ОстаткиТекущейГТД[0].Количество=СтрТовар.Количество*СтрТовар.Коэффициент;
							Если ОстаткиТекущейГТД[0].Количество=0 Тогда
								ОстаткиГТД.Удалить(ОстаткиТекущейГТД[0]);
							КонецЕсли; 
						Иначе
							СтрТовар.Количество=0;
						КонецЕсли; 
						Если ОстаткиТекущейПартии.Количество()>0 Тогда
							ОстаткиТекущейПартии[0].Количество=СтрТовар.Количество*СтрТовар.Коэффициент;
							Если ОстаткиТекущейПартии[0].Количество=0 Тогда
								ОстаткиПартий.Удалить(ОстаткиТекущейПартии[0]);
							КонецЕсли; 
						Иначе
							СтрТовар.Количество=0;
						КонецЕсли; 
					КонецЕсли;
					ОбработкаРеквизита("Товары.Количество",СтрТовар);
				КонецЕсли;
				СтрТовар.Цена = обПолучитьЦену(ТипЦен,СтрТовар.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, СтрТовар.ХарактеристикаНоменклатуры, СтрТовар.ЕдиницаИзмерения, ПодразделениеКомпании);
				ОбработкаРеквизита("Товары.Цена",СтрТовар);
			КонецЦикла;
			ПустыеСтроки=Товары.НайтиСтроки(Новый Структура("Количество",0));
			Для Сч=1 По ПустыеСтроки.Количество() Цикл
				Товары.Удалить(ПустыеСтроки[Сч-1]);
			КонецЦикла; 
			
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			//Для ввода на основании заказа покупателя будем отгружать товары,
			//зарезервированные под данный заказ на данном складе
			Товары.Очистить();
			ЗаполнитьРезервамиКонтрагента(Основание);
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
			ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг;
			
		//misha
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			Для Каждого СтрТовар Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура", СтрТовар);
			КонецЦикла;
		//misha
		КонецЕсли;

	КонецЕсли;
	
	// если ввели на основании, но при этом не заполнили ставки НДС, то исправимся
	// так же установим ячейки хранения по умолчанию
	Для Каждого СтрокаТовар Из Товары Цикл
		
		// Заполним доп. поля для товарной строки
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовар.Номенклатура) Тогда Продолжить; КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовар.СтавкаНДС) Тогда
			СтрокаТовар.СтавкаНДС = СтрокаТовар.Номенклатура.СтавкаНДС;
			Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.СтавкаНДС) Тогда 
				ОбработкаРеквизита("Товары.СтавкаНДС",СтрокаТовар);
			КонецЕсли;
		КонецЕсли;
				
		Если ЗначениеЗаполнено(СтрокаТовар.Ячейка) Тогда Продолжить; КонецЕсли;
		
		СтрокаТовар.Ячейка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(СтрокаТовар.Номенклатура, СкладКомпании);
	КонецЦикла;
	// если вводили документ на основании перемещения, то подправим склад
	Попытка СкладКомпании=Основание.СкладПолучатель Исключение КонецПопытки;
	
	ИдентификаторГосударственногоКонтракта = ?(ЗначениеЗаполнено(ИдентификаторГосударственногоКонтракта),
		ИдентификаторГосударственногоКонтракта,
		ДоговорВзаиморасчетов.ИдентификаторГосударственногоКонтракта);
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг ИЛИ
		 ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		 СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка();
	Иначе
		ПустаяСтатьяДоходов = Справочники.СтатьиДоходовИРасходов.ПустаяСсылка();
		Для Каждого ТекСтрока Из Товары Цикл
			ТекСтрока.СтатьяДоходов = ПустаяСтатьяДоходов;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		//Проверим корректность видов введенной номенклатуры
		БлокироватьВидыНоменклатуры=ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
		Если БлокироватьВидыНоменклатуры<>Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры)=Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
			Ошибки="";
			Для каждого СтрокаТоваров Из Товары Цикл
				ЕстьОшибка=(БлокироватьВидыНоменклатуры[СтрокаТоваров.Номенклатура.ВидНоменклатуры]<>Неопределено);
				Если ЕстьОшибка Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. В таблицу нельзя вводить номенклатуру вида """+СокрЛП(СтрокаТоваров.Номенклатура.ВидНоменклатуры)+""".";
					Отказ=Истина;
				КонецЕсли;
			КонецЦикла; 
			Если Отказ Тогда
				#Если Клиент Тогда
				// Выведем все накопившиеся ошибки 
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
				Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				#КонецЕсли
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст  = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		                      |	Заказы.Заказ КАК Заказ
		                      |ИЗ
		                      |	(ВЫБРАТЬ
		                      |		ЗаказыПокупателей.Заказ КАК Заказ
		                      |	ИЗ
		                      |		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		                      |	ГДЕ
		                      |		ЗаказыПокупателей.Регистратор = &Ссылка
		                      |	
		                      |	ОБЪЕДИНИТЬ
		                      |	
		                      |	ВЫБРАТЬ
		                      |		ЗаказыРаспределение.ЗаказПокупателя
		                      |	ИЗ
		                      |		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		                      |	ГДЕ
		                      |		ЗаказыРаспределение.Регистратор = &Ссылка
		                      |	
		                      |	ОБЪЕДИНИТЬ
		                      |	
		                      |	ВЫБРАТЬ
		                      |		ЗаказыРаспределение.ЗаказПоставщика
		                      |	ИЗ
		                      |		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		                      |	ГДЕ
		                      |		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	Если БонусныеПрограммыСервер.ЗаблокированаКарточка(Карточка)
		И (ДополнительныеСвойства.Свойство("ПроверятьБлокировкуКарты") ИЛИ (КоличествоКНачислению = 0 И ЗначениеЗаполнено(Ссылка))) Тогда
		КоличествоКНачислению = 0;
	Иначе
		БонусныеПрограммыСервер.РасчитатьБонусныеБаллыКНачислению(ЭтотОбъект, Карточка.БонуснаяПрограмма);
	КонецЕсли; 
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --
	
	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
		
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	//Удалим накопление сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.ОтменаПроведения();
	
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
	КонецЕсли;	
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	//begin taty 23.03.2021
	Если НЕ Отказ Тогда
		// получим долг
		СтруктураОтбора=Новый Структура();
		СтруктураОтбора.Вставить("Контрагент",            Контрагент);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		тзДолги = РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма");
		Долг    = тзДолги.Итог("Сумма");
		СуммаДокументаВВалютеДоговора = обПересчет(СуммаДокумента, ВалютаДокумента, КурсДокумента, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата);
		ОсталосьОплатить = СуммаДокументаВВалютеДоговора + Долг;
		ЭМ_ПодписалОплату = обПолучитьЗначениеСвойства(ЭтотОбъект, ПланыВидовХарактеристик.СвойстваОбъектов.ЭМ_ПодписалОплату,Справочники.Пользователи.ПустаяСсылка());
		Если ЗначениеЗаполнено(ЭМ_ПодписалОплату) Тогда
			ОплатаПодписана = Истина;
		Иначе
			ОплатаПодписана = Ложь;
		КонецЕсли; 
		//БИЗТЕХ МАМОНОВ 09.02.2026 ++ 
		//Добавил проверку на галочку в договоре
		Если ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита = Ложь Тогда 
			Если ОсталосьОплатить > 0 и Не ОплатаПодписана и не РольДоступна("ВосстПослед") Тогда
				ВалютаВзаиморасчетов = СокрЛП(ДоговорВзаиморасчетов.ВалютаВзаиморасчетов.Наименование);
				дкСообщитьРезультат(" Сумма долга по договору составляет "+Формат(ОсталосьОплатить,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
				| Необходимо подписать оплату.",СтатусСообщения.Важное,ЭтотОбъект);
				Отказ=Истина;
			КонецЕсли;
		КонецЕсли;
		//БИЗТЕХ МАМОНОВ 09.02.2026 --
	КонецЕсли; 
	//end taty 23.03.2021
	
	// проведем взаиморасчеты
	Если ХозОперация<>Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
		Иначе
			НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
		КонецЕсли; 
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		НаборЗаписейВзаиморасчеты.Сумма=Товары.Итог("СуммаВсего");
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	//Закрываем заказы покупателя по FIFO
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.КоличествоБазовое КАК Количество,
	|	ДокументТовары.Цена КАК ЦенаРозничная,
	|	0 КАК Резерв,
	|	ДокументТовары.СуммаВсего КАК Сумма
	|ИЗ
	|	Документ.РеализацияТоваров.Товары КАК ДокументТовары
	|ГДЕ
	|	  ДокументТовары.Ссылка=&Ссылка И ДокументТовары.Номенклатура.ВидНоменклатуры<>&Услуга
	|";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
	//Закрываем заказы
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
	НаборЗаписейЗаказыПокупателей.Контрагент = Контрагент;
	НаборЗаписейЗаказыПокупателей.Заказ = Неопределено;
	НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
	НаборЗаписейЗаказыПокупателей.Заказывать = Истина;
	НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
	НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству=Истина;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.ЗакрытиеЗаказовПокупателя() ИЛИ Отказ;
	//Если было списание резервов то таблица товаров содержит списанные резервы
	РезультатЗапросаПоТоварам=НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам;

	//Снимаем распределение заказов покупателя
	РезультатЗакрытияЗаказов=НаборЗаписейЗаказыПокупателей.Выгрузить();
	КолонкаКоличества=РезультатЗакрытияЗаказов.Колонки.Найти("Заказано");
	КолонкаКоличества.Имя="Количество"; КолонкаКоличества.Заголовок="Количество";
	РезультатЗакрытияЗаказов.Свернуть("Заказ,Контрагент,Номенклатура,ХарактеристикаНоменклатуры","Количество,Резерв");
	Для каждого СтрокаЗаказа Из РезультатЗакрытияЗаказов Цикл
		СтрокаЗаказа.Количество=-(СтрокаЗаказа.Количество-СтрокаЗаказа.Резерв);
	КонецЦикла; 
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗакрытияЗаказов;
	НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = "Заказ";
	НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
	НаборЗаписейРаспределениеЗаказов.Контрагент = Контрагент;
	НаборЗаписейРаспределениеЗаказов.ПоБазовомуКоличеству=Истина;
	Отказ=НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	
	//Делаем расход по складу
	Если ХозОперация=Справочники.ХозОперации.АктОбОказанииУслуг Тогда
		// проведем услуги по доходам и расходам
		Запрос=Новый Запрос("ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА РеализацияТоваровТовары.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка) ТОГДА
		|			РеализацияТоваровТовары.Номенклатура.СтатьяДопРасходов
		|		ИНАЧЕ
		|			РеализацияТоваровТовары.СтатьяДоходов
		|	КОНЕЦ КАК СтатьяРасходов,
		|	СУММА(РеализацияТоваровТовары.СуммаВсего) КАК СуммаУслуг,
		//<<Павличенко 12.01.18
		|	СУММА(РеализацияТоваровТовары.СуммаНДС) КАК СуммаНДС
		//>>Павличенко 12.01.18
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &ТекДок
		|	И РеализацияТоваровТовары.Номенклатура.ВидНоменклатуры = &Услуга
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА РеализацияТоваровТовары.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка) ТОГДА
		|			РеализацияТоваровТовары.Номенклатура.СтатьяДопРасходов
		|		ИНАЧЕ
		|			РеализацияТоваровТовары.СтатьяДоходов
		|	КОНЕЦ");
		Запрос.УстановитьПараметр("ТекДок", Ссылка);
		Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
		Выборка=Запрос.Выполнить().Выбрать();
		СуммаУслуг = 0;
		Пока Выборка.Следующий() Цикл
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Выборка.СтатьяРасходов;
			
			НаборЗаписейДоходыИРасходы.Доход                  = Выборка.СуммаУслуг;   
			НаборЗаписейДоходыИРасходы.ДоходБезНДС = Выборка.СуммаУслуг - Выборка.СуммаНДС;  
			
			СуммаУслуг = СуммаУслуг + Выборка.СуммаУслуг;
			Отказ = (НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ);
		КонецЦикла;

		ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
		ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеПодразделениеКомпании);
		
		//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
		Если ВедетсяБалансПоПодразделению и БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Доход=СуммаУслуг;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеКомпании;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Расход=СуммаУслуг;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		// продажи
		Если НЕ Отказ Тогда
				
			// проверяем, присутствуют ли партии в табличной части
			//ЕстьПартии = Ложь;
			//Для Каждого СтрокаТовар Из Товары Цикл
			//	Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
			//		ЕстьПартии = Истина;
			//		Прервать;
			//	КонецЕсли;
			//КонецЦикла;
			
			// получим шапку документа
			ШапкаДокумента=ПолучитьШапкуДокумента(Ссылка);
			НаборЗаписейПродажи=Движения.Продажи;
			НаборЗаписейПродажи.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейПродажи.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейПродажи.ДокументПродажи=ШапкаДокумента.ДокументПродажи;
			НаборЗаписейПродажи.Сторно=Ложь;
			НаборЗаписейПродажи.Покупатель=ШапкаДокумента.Контрагент;
			НаборЗаписейПродажи.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
			//bz++
			//НаборЗаписейПродажи.ИмяРеквизитаДокумент = ?(ЕстьПартии,"Партия","");
			НаборЗаписейПродажи.ИмяРеквизитаДокумент = ?(обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект),"Партия","");
			//bz++    
			НаборЗаписейПродажи.Комиссия=Ложь;
			НаборЗаписейПродажи.ПоБазовомуКоличеству=Истина;
			НаборЗаписейПродажи.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли;

	ИначеЕсли ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда		
		ШапкаДокумента					= ПолучитьШапкуДокумента(Ссылка);
		
		// проведем услуги по доходам и расходам
		Запрос=Новый Запрос("ВЫБРАТЬ
							|	ВЫБОР
							|		КОГДА РеализацияТоваровТовары.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка) ТОГДА
							|			РеализацияТоваровТовары.Номенклатура.СтатьяДопРасходов
							|		ИНАЧЕ
							|			РеализацияТоваровТовары.СтатьяДоходов
							|	КОНЕЦ КАК СтатьяРасходов,
							|	СУММА(РеализацияТоваровТовары.СуммаВсего) КАК СуммаУслуг,
							|	СУММА(РеализацияТоваровТовары.СуммаВсего+РеализацияТоваровТовары.СуммаСкидки+РеализацияТоваровТовары.СуммаСкидкиСтроки) КАК СуммаУслугСоСкидками
							|ИЗ
							|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
							|ГДЕ
							|	РеализацияТоваровТовары.Ссылка = &ТекДок
							|	И РеализацияТоваровТовары.Номенклатура.ВидНоменклатуры = &Услуга
							|СГРУППИРОВАТЬ ПО
							|	ВЫБОР
							|		КОГДА РеализацияТоваровТовары.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка) ТОГДА
							|			РеализацияТоваровТовары.Номенклатура.СтатьяДопРасходов
							|		ИНАЧЕ
							|			РеализацияТоваровТовары.СтатьяДоходов
							|	КОНЕЦ");
		Запрос.УстановитьПараметр("ТекДок", Ссылка);
		Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
		Выборка		= Запрос.Выполнить().Выбрать();
		ВУпрВалюте	= (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		СуммаУслуг 	= 0;
		Пока Выборка.Следующий() Цикл
			НаборЗаписейДоходыИРасходы							= Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте				= ВУпрВалюте;
			НаборЗаписейДоходыИРасходы.Доход					= Выборка.СуммаУслугСоСкидками;	
			НаборЗаписейДоходыИРасходы.ШапкаДокумента			= ШапкаДокумента;
			Отказ	= НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;			
			
			НаборЗаписейДоходыИРасходы							= Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте				= ВУпрВалюте;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов	= Выборка.СтатьяРасходов;
			НаборЗаписейДоходыИРасходы.Доход					= Выборка.СуммаУслуг;
			СуммаУслуг		= СуммаУслуг + Выборка.СуммаУслуг;
			Отказ = (НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ);
		КонецЦикла;

		ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
		ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеПодразделениеКомпании);
		
		//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
		Если ВедетсяБалансПоПодразделению и БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Доход=СуммаУслуг;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеКомпании;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Расход=СуммаУслуг;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;		
		
		// продажи
		НаборЗаписейПродажи							= Движения.Продажи;
		НаборЗаписейПродажи.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейПродажи.СкладКомпании			= ШапкаДокумента.СкладКомпании;
		НаборЗаписейПродажи.ДокументПродажи			= ШапкаДокумента.ДокументПродажи;
		НаборЗаписейПродажи.Сторно					= Ложь;
		НаборЗаписейПродажи.Покупатель				= ШапкаДокумента.Контрагент;
		НаборЗаписейПродажи.РезультатЗапросаПоТоварам	= Неопределено;
		НаборЗаписейПродажи.ДоговорВзаиморасчетов	= ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПродажи.ПодразделениеКомпании	= ШапкаДокумента.ПодразделениеКомпании;
		НаборЗаписейПродажи.ИмяРеквизитаДокумент	= "";
		НаборЗаписейПродажи.Комиссия				= Ложь;
		НаборЗаписейПродажи.ПоБазовомуКоличеству	= Истина;
		НаборЗаписейПродажи.ШапкаДокумента			= ШапкаДокумента;
		Отказ=НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;		
		
		// Реализованные товары		
		НаборЗаписейРеализованныеТовары	= Движения.РеализованныеТовары;
		
		ВалютаРегл		= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса	= обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
		КурсРегл		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ВалютаУпр		= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса	= обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
			КурсУпр			= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр			= ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;

		ТекстЗапросаПоУслугам	= "ВЫБРАТЬ
		|	ДокТовары.Номенклатура КАК Номенклатура,
		|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СУММА(ДокТовары.КоличествоБазовое) КАК Количество,
		|	СУММА(ДокТовары.СуммаСкидки) КАК СуммаСкидки,
		|	СУММА(ДокТовары.СуммаСкидкиСтроки) КАК СуммаСкидкиСтроки,
		|	СУММА(ДокТовары.СуммаВсего) КАК СуммаПродажи
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК ДокТовары
		|ГДЕ
		|	ДокТовары.Ссылка=&Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокТовары.Номенклатура,
		|	ДокТовары.ХарактеристикаНоменклатуры";
		ЗапросТЧ = Новый Запрос(ТекстЗапросаПоУслугам);
		ЗапросТЧ.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
		ТаблицаУслуги = ЗапросТЧ.Выполнить().Выгрузить();						
			
		Для Каждого ТекСтрока Из ТаблицаУслуги Цикл
			НоваяЗапись					= НаборЗаписейРеализованныеТовары.Добавить();
			НоваяЗапись.ВидДвижения		= ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период			= ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор		= ШапкаДокумента.Ссылка;
			НоваяЗапись.Номенклатура	= ТекСтрока.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры	= ТекСтрока.ХарактеристикаНоменклатуры;
			НоваяЗапись.Контрагент		= ШапкаДокумента.Комитент;
			НоваяЗапись.ДоговорВзаиморасчетов	= ШапкаДокумента.ДоговорКомитента;
			НоваяЗапись.ДокументПередачи		= ШапкаДокумента.Ссылка;

			НоваяЗапись.ХозОперация		= ШапкаДокумента.ХозОперация;
			НоваяЗапись.Количество		= ТекСтрока.Количество;
			
			СуммаПоступления			= ТекСтрока.СуммаПродажи + ТекСтрока.СуммаСкидки + ТекСтрока.СуммаСкидкиСтроки;
			НоваяЗапись.СуммаРегл		= Окр(обПересчет(СуммаПоступления,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл),2);
			НоваяЗапись.СуммаУпр		= Окр(обПересчет(СуммаПоступления,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр),2);
			
			НоваяЗапись.СуммаПродажиРегл = Окр(обПересчет(ТекСтрока.СуммаПродажи, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл), 2);
			НоваяЗапись.СуммаПродажи     = Окр(обПересчет(Текстрока.СуммаПродажи, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр), 2); 
		КонецЦикла;
	Иначе
		// для оказания услуг товары не списываем
		// проведем остатки товаров
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.Контрагент=Контрагент;
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
		НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
		НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная="Цена";
		НаборЗаписейОстатки.Резервировать=РезультатЗапросаПоТоварам<>Неопределено;
		Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
		Если Отказ Тогда
			// выведем сообщения об ошибках и предупреждениях
			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
			Возврат; // дальше смысла не имеет
		КонецЕсли;
		// Партии
		Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;		
	КонецЕсли;
	
	//Сформируем сумму накопления
	Если ХозОперация<>Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
		НаборЗаписейНакоплениеСумм.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейНакоплениеСумм.Контрагент             = Контрагент;
		НаборЗаписейНакоплениеСумм.КоличествоНоменклатуры = Товары.Итог("КоличествоБазовое");
		НаборЗаписейНакоплениеСумм.Карточка               = Карточка;
		НаборЗаписейНакоплениеСумм.Сторно                 = Ложь;
		Отказ=НЕ НаборЗаписейНакоплениеСумм.НакоплениеСуммы() ИЛИ Отказ;
	КонецЕсли;
	
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
	КонецЕсли;
	
	Если НЕ Отказ И (ХозОперация = Справочники.ХозОперации.РеализацияТоваров
		ИЛИ ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия) Тогда
		
		СостояниеКодаМаркировки = ?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо,
			Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа,
			Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику);
		
		// Изменим состояние маркировки
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.ПроверятьВыводИзОборота = Истина;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = СостояниеКодаМаркировки;
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
		
	КонецЕсли;
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// бонусные баллы
	НаборБонусныеПрограммы = Движения.БонусныеБаллы;
	Если КоличествоКНачислению > 0 Тогда
		НаборБонусныеПрограммы.Карта             = Карточка;
		НаборБонусныеПрограммы.БонуснаяПрограмма = Карточка.БонуснаяПрограмма;
		НаборБонусныеПрограммы.ХозОперация       = ХозОперация;
		НаборБонусныеПрограммы.КоличетсвоБаллов  = КоличествоКНачислению;
		НаборБонусныеПрограммы.Регистратор       = Ссылка;
		НаборБонусныеПрограммы.ДокДата           = Дата;
		
		Отказ = НЕ НаборБонусныеПрограммы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Если КоличествоКСписанию > 0 Тогда
		МаксимальноеКоличествоБаллов = БонусныеПрограммыСервер.МаксимальноеКоличетсвоБоллов(ЭтотОбъект, Карточка.БонуснаяПрограмма);
		Если КоличествоКСписанию > МаксимальноеКоличествоБаллов Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Максимальное количество бонусных баллов доступных к оплате %1 меньше указанных %2.'");
			ТекстСообщения = СтрЗаменить(СтрЗаменить(ТекстСообщения, "%1", МаксимальноеКоличествоБаллов), "%2", КоличествоКСписанию);
			Сообщить(ТекстСообщения, СтатусСообщения.Важное);
		КонецЕсли;
		
		НаборБонусныеПрограммы.Карта = Карточка;
		НаборБонусныеПрограммы.БонуснаяПрограмма = Карточка.БонуснаяПрограмма;
		НаборБонусныеПрограммы.ХозОперация       = ХозОперация;
		НаборБонусныеПрограммы.КоличетсвоБаллов  = КоличествоКСписанию;
		НаборБонусныеПрограммы.Регистратор       = Ссылка;
		НаборБонусныеПрограммы.ДокДата           = Дата;
		
		Отказ = НЕ НаборБонусныеПрограммы.Расход() ИЛИ Отказ;
		
	КонецЕсли;
	
	// Проверим налиие прослеживаемых товаров, которые были реализованы
	Если НЕ Отказ Тогда
		Движения.Продажи.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыПокупателей.Количество()>0 ИЛИ Движения.ЗаказыРаспределение.Количество()>0));
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Количество = тс.Количество;  
				
				Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли    

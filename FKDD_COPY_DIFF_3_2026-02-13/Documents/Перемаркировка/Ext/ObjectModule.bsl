// Модуль документа Перемаркировка


////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ЗаполнитьТабличнуюЧастьТоваровПоОснованию(Основание, ВидПервичногоДокумента, НаименованиеДокумента)
	
	Товары.Очистить();
	
	СтруктураПоиска = Новый Структура("ИдентификаторТовара");
	
	// Найдем ноенклатуру, у которой не отсканировали код
	Для Каждого ТекущаяСтрока Из Основание.Товары Цикл
		
		Если НЕ ТекущаяСтрока.Номенклатура.ТипНоменклатуры.ВедетсяМаркировка Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		
		Попытка
			НайденныеСтроки = Основание.КодыМаркировки.НайтиСтроки(СтруктураПоиска);
			КоличествоБезМаркировки = (ТекущаяСтрока.Количество * ТекущаяСтрока.Коэффициент - НайденныеСтроки.Количество());
		Исключение
			КоличествоБезМаркировки = 1;
		КонецПопытки;
		
		Пока КоличествоБезМаркировки > 0 Цикл
			
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.НоменклатураНовая = ТекущаяСтрока.Номенклатура;
			ОбработкаРеквизита("Товары.НоменклатураНовая", НоваяСтрока);
			НоваяСтрока.ХарактеристикаНоменклатурыНовая = ТекущаяСтрока.ХарактеристикаНоменклатуры;
			
			НоваяСтрока.ВидПервичногоДокумента = ВидПервичногоДокумента;
			НоваяСтрока.НомерПервичногоДокумента = Основание.Номер;
			НоваяСтрока.ДатаПервичногоДокумента = Основание.Дата;
			НоваяСтрока.НаименованиеПервичногоДокумента = НаименованиеДокумента;
			НоваяСтрока.ДатаПеремаркировки = Дата;
			
			КоличествоБезМаркировки = КоличествоБезМаркировки - 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("ДатаПеремаркировки",1);
	
	Если ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ОшибкаВОписании Тогда
		ОбязательныеТовары.Вставить("Номенклатура", 1);
		ОбязательныеТовары.Вставить("КодМаркировки", 3);
	КонецЕсли;
	ОбязательныеТовары.Вставить("КодТНВЭД", 1);
	
	ОбязательныеТовары.Вставить("НоменклатураНовая",1);
	
	Если НЕ ТоварнаяГруппа = Справочники.ТипыМаркировки.ШиныИАвтопокрышки Тогда
		ОбязательныеТовары.Вставить("СтранаПроизводства", 1);
	КонецЕсли;
	
	Если ТоварнаяГруппа = Справочники.ТипыМаркировки.ОбувныеТовары Тогда
		ОбязательныеТовары.Вставить("Цвет", 1);
		ОбязательныеТовары.Вставить("Размер", 1);
	КонецЕсли;
	
	Если Статус <> Перечисления.СтатусыПеремаркировки.Новый Тогда
		ОбязательныеТовары.Вставить("КодМаркировкиНовый",3);
	КонецЕсли;
	
	Если ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа Тогда
		ОбязательныеТовары.Вставить("Оплачен", 1);
		ОбязательныеТовары.Вставить("НаименованиеПервичногоДокумента", 1);
		ОбязательныеТовары.Вставить("ВидПервичногоДокумента", 1);
		ОбязательныеТовары.Вставить("ДатаПервичногоДокумента", 1);
		ОбязательныеТовары.Вставить("НомерПервичногоДокумента",1);
	КонецЕсли;
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("Статус",1);
	ОбязательныеРеквизиты.Вставить("ПричинаПеремаркировки",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("ТоварнаяГруппа",1);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверим статусы указанных кодов маркировки
	ТаблицаРазбораМаркировки = Новый ТаблицаЗначений;
	ТаблицаРазбораМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	ТаблицаРазбораМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	ТаблицаРазбораМаркировки.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)));
	ТаблицаРазбораМаркировки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	// Разберем маркировку на состовляющие для поиска
	Для Каждого ТекущийКодМаркировки Из Товары Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			дкДобавитьОшибку(Ошибки, "Код маркировки <"+СокрЛП(ТекущийКодМаркировки.КодМаркировки)+"> не разобран. Проверьте его корректность.");
			Результат = Ложь;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРазбораМаркировки.Добавить();
		НоваяСтрока.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		НоваяСтрока.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		НоваяСтрока.КодМаркировки = ТекущийКодМаркировки.КодМаркировки;
		НоваяСтрока.НомерСтроки = ТекущийКодМаркировки.НомерСтроки;
		
	КонецЦикла;
	
	ТекущиеСтатусы = РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(ТаблицаРазбораМаркировки, Дата);
	
	// Можем перемаркировкть только маркировку со статусом "Введен в оборот"
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.СостоянияКодовМаркировки.ВведенВОборот);
	СостоянияПроверки.Добавить(Перечисления.СостоянияКодовМаркировки.ВведенВОборотПриВозврате);
	СостоянияПроверки.Добавить(Перечисления.СостоянияКодовМаркировки.ВведенВОборотОжидаетПеремаркировки);
	СостоянияПроверки.Добавить(Перечисления.СостоянияКодовМаркировки.ВыведенИзОборота);
	СостоянияПроверки.Добавить(Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаПриУтратеИлиПовреждении);
	Для Каждого ТекущаяСтрока Из ТекущиеСтатусы Цикл
		
		Если СостоянияПроверки.Найти(ТекущаяСтрока.Состояние) = Неопределено Тогда
			дкДобавитьОшибку(Ошибки, "Код маркировки <"+СокрЛП(ТекущаяСтрока.КодМаркировки)+"> не введен в оборот.");
			Результат = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	// Получим состояния кодов для новых
	ТаблицаРазбораМаркировкиНовые = ТаблицаРазбораМаркировки.Скопировать();
	ТаблицаРазбораМаркировкиНовые.Очистить();
	
	// Разберем маркировку на состовляющие для поиска
	Для Каждого ТекущийКодМаркировки Из Товары Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировкиНовый) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировкиНовый);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			дкДобавитьОшибку(Ошибки, "Код маркировки <"+СокрЛП(ТекущийКодМаркировки.НоменклатураНовая)+"> не разобран. Проверьте его корректность.");
			Результат = Ложь;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРазбораМаркировкиНовые.Добавить();
		НоваяСтрока.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		НоваяСтрока.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		НоваяСтрока.КодМаркировки = ТекущийКодМаркировки.НоменклатураНовая;
		НоваяСтрока.НомерСтроки = ТекущийКодМаркировки.НомерСтроки;
		
	КонецЦикла;
	
	ТекущиеСтатусыНовая = РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(ТаблицаРазбораМаркировки, Дата);
	
	// Можем перемаркировкть только маркировку со статусом "Эмитирован"
	Для Каждого ТекущаяСтрока Из ТекущиеСтатусы Цикл
		
		Если ТекущаяСтрока.Состояние = Перечисления.СостоянияКодовМаркировки.Экспортирован Тогда
			дкДобавитьОшибку(Ошибки, "Код маркировки <"+СокрЛП(ТекущаяСтрока.КодМаркировки)+"> не эмитирован.");
			Результат = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	// Если перемартруем испорченную, то надо, чтоб КИ совпадал
	Если ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ИспорченаМаркировка Тогда
		Для Каждого ТекущаяСтрока Из ТаблицаРазбораМаркировки Цикл
			НайденнаяСтрока = ТаблицаРазбораМаркировкиНовые.Найти(ТекущаяСтрока.НомерСтроки, "НомерСтроки");
			Если НайденнаяСтрока <> Неопределено И НайденнаяСтрока.GTIN <> ТекущаяСтрока.GTIN Тогда
				дкДобавитьОшибку(Ошибки, "Товары. Строка "+ ТекущаяСтрока.НомерСтроки + ": Указанные коды маркировки имеют разный код товара.");
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЭтоРозничныйВозврат = (ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа);
	ЭтоДистанционныйВозврат = (ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиДистанционнаяПродажа);
	Если ЭтоДистанционныйВозврат Тогда
		
		Для Каждого ТекущаяСтрока Из Товары Цикл
			
			Если ТекущаяСтрока.Оплачен <> "Да" Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ВидПервичногоДокумента) Тогда
				дкДобавитьОшибку(Ошибки, "Товары. Строка "+ ТекущаяСтрока.НомерСтроки + ": Необходимо заполнить вид первичного документа.");
				Результат = Ложь;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ДатаПервичногоДокумента) Тогда
				дкДобавитьОшибку(Ошибки, "Товары. Строка "+ ТекущаяСтрока.НомерСтроки + ": Необходимо заполнить дату первичного документа.");
				Результат = Ложь;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.НомерПервичногоДокумента) Тогда
				дкДобавитьОшибку(Ошибки, "Товары. Строка "+ ТекущаяСтрока.НомерСтроки + ": Необходимо заполнить номер первичного документа.");
				Результат = Ложь;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.НаименованиеПервичногоДокумента) Тогда
				дкДобавитьОшибку(Ошибки, "Товары. Строка "+ ТекущаяСтрока.НомерСтроки + ": Необходимо заполнить наименование первичного документа.");
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Результат = Результат И дкПроверитьСоответствиеНоменклатурыТоварнойГруппе(ЭтотОбъект,,"Номенклатура,НоменклатураНовая", Ошибки);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Перемаркировка", "Перемаркировка", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	Если Имя = "Товары.НоменклатураНовая" Тогда
		Если НЕ ЗначениеЗаполнено(ТекСтрока.КодТНВЭД) Тогда
			ТекСтрока.КодТНВЭД = ТекСтрока.НоменклатураНовая.КодТНВЭД;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекСтрока.СтранаПроизводства)
			И НЕ ТоварнаяГруппа = Справочники.ТипыМаркировки.ШиныИАвтопокрышки Тогда
			ТекСтрока.СтранаПроизводства = ТекСтрока.НоменклатураНовая.СтранаПроисхождения;
		КонецЕсли;
	КонецЕсли;
	
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события установки нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Статус) Тогда
		Статус = Перечисления.СтатусыПеремаркировки.Новый;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Чек") Тогда
		
		Если Основание.Хозоперация <> Справочники.ХозОперации.ЧекНаВозврат Тогда
			#Если Клиент Тогда
				Сообщить("Перемаркировка доступна только при вводе на основании чека на возврат.", СтатусСообщения.Внимание);
			#КонецЕсли
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа;
		
		ЗаполнитьТабличнуюЧастьТоваровПоОснованию(
			Основание,
			Перечисления.ВидыПервичныхДокументов.КассовыйЧек,
			"Чек на возврат");
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
		
		ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа;
		
		ЗаполнитьТабличнуюЧастьТоваровПоОснованию(
			Основание,
			Перечисления.ВидыПервичныхДокументов.ТоварныйЧек,
			"Возврат от покупателя");
			
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СписаниеКодовМаркировки") Тогда
		
		Если Основание.Хозоперация <> Справочники.ХозОперации.СписаниеНанесенныхКодовМаркировки Тогда
			#Если Клиент Тогда
				Сообщить("Перемаркировка доступна только при вводе на основании списания КМ, нанесенных на товар.", СтатусСообщения.Внимание);
			#КонецЕсли
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ИспорченаМаркировка;
		
		ЗаполнитьТабличнуюЧастьТоваровПоОснованию(
			Основание,
			Перечисления.ВидыПервичныхДокументов.Прочее,
			"Списание кодов маркирвоки");
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа;
		
		Товары.Очистить();
		
		// Заполним ТЧ Товары только теми товарами, которые на уменьшение и не указаны коды
		СтруктураПоиска = Новый Структура("ИдентификаторТовара");
		СтруктураПоиска.Вставить("Возврат", Истина);
		Для Каждого ТекущаяСтрока Из Основание.Товары Цикл
			
			РазностьКоличества = -ТекущаяСтрока.КоличествоРазница;
			
			// Количество не было уменьшено
			Если РазностьКоличества <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Получим штучный товар
			РазностьКоличества = РазностьКоличества * ТекущаяСтрока.Коэффициент;
			
			// Проверим, что количество возвратов совпадает с количеством разницы
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
			НайденныеСтроки = Основание.КодыМаркировки.НайтиСтроки(СтруктураПоиска);
			
			// Уберем указанные маркировки к возврату
			РазностьКоличества = РазностьКоличества - НайденныеСтроки.Количество();
			
			Пока РазностьКоличества > 0 Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.НоменклатураНовая = ТекущаяСтрока.Номенклатура;
				ОбработкаРеквизита("Товары.НоменклатураНовая", НоваяСтрока);
				НоваяСтрока.ХарактеристикаНоменклатурыНовая = ТекущаяСтрока.ХарактеристикаНоменклатуры;
				
				НоваяСтрока.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументов.Прочее;
				НоваяСтрока.НомерПервичногоДокумента = Основание.Номер;
				НоваяСтрока.ДатаПервичногоДокумента = Основание.Дата;
				НоваяСтрока.НаименованиеПервичногоДокумента = "Корректировка реализации";
				НоваяСтрока.ДатаПеремаркировки = Дата;
				
				РазностьКоличества = РазностьКоличества - 1;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ТоварнаяГруппа) Тогда
		Номенклатура = Товары.ВыгрузитьКолонку("Номенклатура");
		НоменклатураНовая = Товары.ВыгрузитьКолонку("НоменклатураНовая");
		Для Каждого Элемент Из НоменклатураНовая Цикл
			Номенклатура.Добавить(Элемент);
		КонецЦикла;
		ТоварнаяГруппа = дкТоварнаяГруппа(Номенклатура);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Статус = Перечисления.СтатусыПеремаркировки.Новый;
	
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	Для Каждого Строка Из Товары Цикл
		ОбъектШК.РазобратьСтрокуШтрихкодаGS1(Строка.КодМаркировки, Истина);
		ОбъектШК.РазобратьСтрокуШтрихкодаGS1(Строка.КодМаркировкиНовый, Истина);
	КонецЦикла;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		орЖурналСостояний(Ссылка, Статус);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Обработчик события удаления проведения
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события проведения
//
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И Статус = Перечисления.СтатусыПеремаркировки.Выполнен Тогда
		
		// Изменим состояние маркировки
		ТаблицаМаркировок = Новый ТаблицаЗначений;
		ТаблицаМаркировок.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаМаркировок.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаМаркировок.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)));
		
		ТаблицаНовыхМаркировок = ТаблицаМаркировок.Скопировать();
		
		ЭтоВозврат = (ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиДистанционнаяПродажа
			ИЛИ ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировки.ВозвратБезМаркировкиРозничнаяПродажа);
		
		Для Каждого ТекущаяСтрока Из Товары Цикл
			
			Если НЕ ЭтоВозврат И ЗначениеЗаполнено(ТекущаяСтрока.КодМаркировки) Тогда
				НоваяСтрока = ТаблицаМаркировок.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			КонецЕсли;
			
			НоваяСтрока = ТаблицаНовыхМаркировок.Добавить();
			НоваяСтрока.Номенклатура = ТекущаяСтрока.НоменклатураНовая;
			НоваяСтрока.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатурыНовая;
			НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировкиНовый;
			
		КонецЦикла;
		
		// Для перемаркируемых поставим ссотояние выбытия из оборота
		ОчищатьЗаписи = Истина;
		УдалятьДвижения = Истина;
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		
		Если ТаблицаМаркировок.Количество() > 0 Тогда
			НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаПриПеремаркировке;
			НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаМаркировок;
			Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
			ОчищатьЗаписи = Ложь;
			УдалятьДвижения = Ложь;
		КонецЕсли;
		
		// Введем в оборот новые маркировки
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборот;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаНовыхМаркировок;
		НаборЗаписейСостоянияКодовМаркировки.ОчищатьЗаписи = ОчищатьЗаписи;
		НаборЗаписейСостоянияКодовМаркировки.УдалятьДвижения = УдалятьДвижения;
		Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
		
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

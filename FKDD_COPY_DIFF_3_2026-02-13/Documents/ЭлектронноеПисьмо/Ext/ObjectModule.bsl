// Модуль документа Электронная почта

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Функция возвращает строковое представление получателей письма.
//
// Параметры
//  КодГруппы - Перечисление.КодГруппыАдресаЭлектронногоПисьма - группа эл. письма (Кому, Копии, СкрытыеКопии)
//
// Возвращаемое значение:
//   Строка   – Представление получателей письма
//
Функция СформироватьПредставлениеПолучателейПисьма(КодГруппы) Экспорт
	
	ПредставлениеПолучателейПисьма = "";
	
	МассивСтрок = Получатели.НайтиСтроки(Новый Структура("КодГруппыАдреса",КодГруппы));
	ПредставлениеПолучателейПисьма = "";
	Для индекс = 0 По МассивСтрок.ВГраница() Цикл
		Если индекс > 0 Тогда
			ПредставлениеПолучателейПисьма = ПредставлениеПолучателейПисьма + ", ";
		КонецЕсли;
		Элемент = МассивСтрок[индекс];
		ПредставлениеПолучателейПисьма = ПредставлениеПолучателейПисьма + элСформироватьПредставлениеАдресаЭлектроннойПочты(Элемент.АдресЭлектроннойПочты, Элемент.Представление);
	КонецЦикла;	
	
	Возврат ПредставлениеПолучателейПисьма;	
	
КонецФункции // СформироватьПредставлениеПолучателейПисьма()

// Функция возвращает строковое представление всех получателей письма
//
// Возвращаемое значение:
//  Представление - строка - строковое представление всех получателей письма.
//
Функция ПредставлениеПолучателей()Экспорт
	
	Представление = СформироватьПредставлениеПолучателейПисьма(Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому);
	КопииПредставление = СформироватьПредставлениеПолучателейПисьма(Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии);
	СкрытыеКопииПредставление = СформироватьПредставлениеПолучателейПисьма(Перечисления.КодГруппыАдресаЭлектронногоПисьма.СкрытыеКопии);
	
	Если НЕ КопииПредставление = "" Тогда
		Представление = Представление+ " Копии: "+КопииПредставление;
	КонецЕсли;
	
	Если НЕ СкрытыеКопииПредставление = "" Тогда
		Представление = Представление+ " Скрытые копии: "+СкрытыеКопииПредставление; 
	КонецЕсли;

	Возврат Представление;
	
КонецФункции // ПредставлениеПолучателей()

// Процедура удаляет по коду группы старые строки ТЧ "Пользователи" и добавляет 
// новые переданные в нее
//
// Параметры:
//  ТаблицаЗначений - ТаблицаЗначений - Таблица электронных адресов и их представлений
//  КодГруппы 		- Число - код группы эл. письма (1 - "Кому", 2 - "Копии", 3 - "СкрытыеКопии")
// 
Процедура ОбновитьТабличнуюЧасть(ТаблицаЗначенийАдресов, КодГруппы) Экспорт
	
	МассивСтрок = Получатели.НайтиСтроки(Новый Структура("КодГруппыАдреса",КодГруппы));
	// удалим все строки с данный кодом
	Для Каждого Элемент Из МассивСтрок Цикл
		Получатели.Удалить(Элемент);
	КонецЦикла; 
	//добавим новые адреса
	Для Каждого Строка Из ТаблицаЗначенийАдресов Цикл
	  СтрокаТЧ = Получатели.Добавить();
	  СтрокаТЧ.АдресЭлектроннойПочты = Строка.АдресЭлектроннойПочты;
	  СтрокаТЧ.Представление = Строка.Представление;
	  СтрокаТЧ.КодГруппыАдреса = КодГруппы;
	КонецЦикла;  
	
КонецПроцедуры // ОбновитьТабличнуюЧасть()

 // Процедура заполняет реквизиты документа
 //
 // Параметры:
 //  Сообщение - ИнтернетПочтовоеСообщение, полученное электронное сообщение
 //  УчетнаяЗаписьПисьма - Ссылка,ссылка на элемент справочника учетные записи
 //
 Процедура СоздатьПолученноеПисьмо(Сообщение,УчетнаяЗаписьПисьма) Экспорт
	 	 
	 ЭтотОбъект.ОтправительПредставление = ?(СокрЛП(Сообщение.ИмяОтправителя) = "", "<", Сообщение.ИмяОтправителя + " <") + Сообщение.АдресОтправителя + ">" ;
	 
	 ЭтотОбъект.Тема = СокрЛП(Сообщение.Тема);
	 
	 ЭтотОбъект.ДатаОтправления = Сообщение.ДатаОтправления;
	 ЭтотОбъект.ДатаПолучения = Сообщение.ДатаПолучения;
	 
	 ЭтотОбъект.ТекстПисьма = Сообщение.Текст;
	 ЭтотОбъект.ФорматТекста = Сообщение.ТипТекста;
	 
	 ЭтотОбъект.ЕстьВложения = ?(Сообщение.КоличествоВложений>0,Истина,Ложь);
	 
	 ЭтотОбъект.ИдентификаторПисьма = Сообщение.ИдентификаторСообщения;
	 
	 // Заполним табличные части
	 Для Каждого Получатель Из Сообщение.Получатели Цикл
		 
		 НовыйПолучатель = ЭтотОбъект.Кому.Добавить();
		 НовыйПолучатель.Представление = "";//Получатель.Пользователь;
		 НовыйПолучатель.АдресЭлектроннойПочты = Получатель.Адрес;
		 
	 КонецЦикла; 
	 
	 Для Каждого Копия Из Сообщение.Копии Цикл
		 
		 НоваяКопия = ЭтотОбъект.Копии.Добавить();
		 НоваяКопия.Представление ="";// Копия.Пользователь;
		 НоваяКопия.АдресЭлектроннойПочты = Копия.Адрес;
		 
	 КонецЦикла; 
	 
	 ЭтотОбъект.ПолучателиПредставление = ЭтотОбъект.ПредставлениеПолучателей();
	 
	 ЭтотОбъект.УчетнаяЗапись = УчетнаяЗаписьПисьма;
	 ЭтотОбъект.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Входящие",Истина,,УчетнаяЗаписьПисьма);
	 ЭтотОбъект.СтатусПисьма = Перечисления.СтатусыСообщений.Входящее;
	 ЭтотОбъект.НеРассмотрено = Истина;
	 
	 ЭтотОбъект.Дата = ТекущаяДата();
	 ЭтотОбъект.Автор = ПараметрыСеанса.Пользователь;
	 ЭтотОбъект.Записать();
	 
	 ИмяКаталога = КаталогВременныхФайлов();
	 
	 
	 Если ЭтотОбъект.ЕстьВложения Тогда
		 ЭтотОбъект.Записать();
	 КонецЕсли;	 
	 
КонецПроцедуры // СоздатьПолученноеПисьмо()
 
// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Тема",1);
	Если НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтправитьВнешнимКлиентом") Тогда
		ОбязательныеРеквизиты.Вставить("ХозОперация",1);
		ОбязательныеРеквизиты.Вставить("УчетнаяЗапись",1);
		ОбязательныеРеквизиты.Вставить("ГруппаПисем",1);
	КонецЕсли;
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки

	Если (НЕ УчетнаяЗапись.ЭтоУчетнаяЗаписьНовостей) И ПустаяСтрока(ПолучателиПредставление) Тогда
	    Сообщить("Не указан ни один получатель (поле ""Кому""), отправка письма невозможна!", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	Если ПометкаУдаления Тогда
		Сообщить("Нельзя отправлять помеченное на удаление письмо.", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина); 
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЭлектронноеПисьмо","Электронное письмо");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Если НЕ обПраво("ПечатьБезСохранения",ЭтотОбъект.Права,,ЭтотОбъект) Тогда
		БылоИзмененоПравоПечатьБезСохранения = Истина;
		ПравоСсылка = обПолучитьСсылкуПВХПравИНастроек("ПечатьБезСохранения");
		ЭтотОбъект.Права.Вставить(ПравоСсылка, Истина);
	Иначе
		БылоИзмененоПравоПечатьБезСохранения = Ложь;
	КонецЕсли;
	ВозвратДкПечать = дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
	Если БылоИзмененоПравоПечатьБезСохранения Тогда
		ЭтотОбъект.Права.Вставить(ПравоСсылка, Ложь);
	КонецЕсли;
	Возврат ВозвратДкПечать;
КонецФункции // Печать()

// Процедура осуществляет печать электронного письма.
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЭлектронноеПисьмо(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("ЭлектронноеПисьмо");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаДокумента");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ГруппаПисемОбъекта = ЭтотОбъект.ГруппаПисем;
	ГруппаПисемВходящие = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Входящие", Истина, ,ЭтотОбъект.УчетнаяЗапись);
	Если (ГруппаПисемОбъекта = ГруппаПисемВходящие)
	 ИЛИ (ГруппаПисемОбъекта.ПринадлежитЭлементу(ГруппаПисемВходящие)) Тогда
	 	ОбластьМакета.Параметры.ДатаПисьма = ЭтотОбъект.ДатаПолучения;
	Иначе
		ОбластьМакета.Параметры.ДатаПисьма = ЭтотОбъект.ДатаОтправления;
	КонецЕсли;
	
	ОбластьМакета.Параметры.Получатели = СформироватьПредставлениеПолучателейПисьма(Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому);
	ОбластьМакета.Параметры.Копии      =  СформироватьПредставлениеПолучателейПисьма(Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТема");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТекстПисьма");
	Если ФорматТекста = Перечисления.ФорматТекста.ПростойТекст Тогда
		ОбластьМакета.Параметры.Текст = ТекстПисьма;			
	Иначе			
		ОбластьМакета.Параметры.Текст = обПолучитьТекст(ТекстПисьма);
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьЭлектронноеПисьмо()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Событие") Тогда
		Тема = Основание.Тема;
		ФорматТекста = Основание.ФорматТекста;
		Если ФорматТекста = Перечисления.ФорматТекста.HTML Тогда
			ТелоСодержания = обПолучитьТелоHTML(Основание.Содержание);
			Если НЕ ПустаяСтрока(ТелоСодержания) Тогда
				ТелоСодержания = "Содержание:<BR>" + ТелоСодержания;
			КонецЕсли;	
			ТелоРезультата = обПолучитьТелоHTML(Основание.Результат);
			Если НЕ ПустаяСтрока(ТелоРезультата) Тогда
				Если НЕ ПустаяСтрока(ТелоСодержания) Тогда
					ТелоСодержания = ТелоСодержания + "<BR><BR>";
				КонецЕсли;	
				ТелоСодержания = ТелоСодержания + "Результат:<BR>"+ТелоРезультата;
			КонецЕсли;
		Иначе
			ТелоСодержания = СокрЛП(Основание.Содержание);
			Если НЕ ПустаяСтрока(ТелоСодержания) Тогда
				ТелоСодержания = "Содержание:" + Символы.ПС + ТелоСодержания;
			КонецЕсли;	
			ТелоРезультата = СокрЛП(Основание.Результат);
			Если НЕ ПустаяСтрока(ТелоРезультата) Тогда
				Если НЕ ПустаяСтрока(ТелоСодержания) Тогда
					ТелоСодержания = ТелоСодержания + Символы.ПС + Символы.ПС;
				КонецЕсли;	
				ТелоСодержания = ТелоСодержания + "Результат:" + Символы.ПС + ТелоРезультата;
			КонецЕсли;
		КонецЕсли;
		ТекстПисьма = ТелоСодержания;
		ДокументОснование = Основание;
		Важность = Основание.Приоритет;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаБонусов") Тогда
		Получатели.Очистить();
		
		Тема = "Оповещение о бонусах";
		
		// заполним получателями из документа основания
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	КорректировкаБонусовБонусыКНачислению.Карточка.Объект КАК Контрагент
		|ПОМЕСТИТЬ Контрагенты
		|ИЗ
		|	Документ.КорректировкаБонусов.БонусыКНачислению КАК КорректировкаБонусовБонусыКНачислению
		|ГДЕ
		|	КорректировкаБонусовБонусыКНачислению.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактнаяИнформация.Объект,
		|	КонтактнаяИнформация.Представление
		|ПОМЕСТИТЬ КИ
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Вид = &Вид
		|
		|СГРУППИРОВАТЬ ПО
		|	КонтактнаяИнформация.Представление,
		|	КонтактнаяИнформация.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.Контрагент КАК Представление,
		|	Контрагенты.Контрагент КАК СсылкаНаОбъект,
		|	ЕСТЬNULL(КИ.Представление, """") КАК АдресЭлектроннойПочты
		|ИЗ
		|	Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ КИ КАК КИ
		|		ПО Контрагенты.Контрагент = КИ.Объект";
		Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний);
		Запрос.УстановитьПараметр("Ссылка", Основание);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Получатель = Получатели.Добавить();
			ЗаполнитьЗначенияСвойств(Получатель, Выборка);
			Получатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
		КонецЦикла;
		
	ИначеЕсли НЕ обЗначениеНеЗаполнено(Основание) Тогда
		Тема=Основание;
		Попытка
			Адресат=Основание.Заказчик;
		Исключение
			Попытка
				Адресат=Основание.Контрагент;
			Исключение
				Адресат=Неопределено;
			КонецПопытки;
		КонецПопытки;
		Если НЕ обЗначениеНеЗаполнено(Адресат) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	КонтактнаяИнформация.Представление
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Объект = &Объект
			|	И КонтактнаяИнформация.Тип = &Тип
			|	И КонтактнаяИнформация.ЗначениеПоУмолчанию = &ЗначениеПоУмолчанию";
			Запрос.УстановитьПараметр("Объект", Адресат);
			Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Запрос.УстановитьПараметр("ЗначениеПоУмолчанию", Истина);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СтрокаПолучатель = Получатели.Добавить();
				СтрокаПолучатель.АдресЭлектроннойПочты = Выборка.Представление;
				СтрокаПолучатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
				СтрокаПолучатель.Представление = Адресат.Наименование;
			КонецЕсли;	
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтправитьВнешнимКлиентом") Тогда
		ЭтотОбъект.ДополнительныеСвойства.Удалить("УспешнаяПроверкаПередЗаписью");
	КонецЕсли;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если ПрикрепленныеФайлы.Количество() > 0 Тогда
		ЕстьВложения = Истина;
	Иначе	
		ЕстьВложения = Ложь;
	КонецЕсли;
	
	ГруппаУдаленные = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Удаленные",Истина,,УчетнаяЗапись);
	Если ПометкаУдаления И (ГруппаПисем <> ГруппаУдаленные) И (НЕ ГруппаПисем.ПринадлежитЭлементу(ГруппаУдаленные)) Тогда
		ПометкаУдаления = Ложь;
	КонецЕсли; 
	
	// ЗАПОЛНЕНИЕ РЕКВИЗИТА ССЫЛКА НА ОБЪЕКТ.
	Если (СтатусПисьма=Перечисления.СтатусыСообщений.Входящее) И (НЕ УчетнаяЗапись.ЭтоУчетнаяЗаписьНовостей) Тогда
		// Вытаскиваем адрес.
		ВремТекст = СокрЛП(ОтправительПредставление);
		Индекс = Найти(ВремТекст, "<");
		Если Индекс = 0 Тогда
			ОтправительПредставление = Неопределено;
			Возврат;
		КонецЕсли;
		ВремТекст = Сред(ВремТекст, Индекс);
		ВремТекст = СтрЗаменить(ВремТекст, "<", "");
		ВремТекст = СтрЗаменить(ВремТекст, ">", "");
		Если ПустаяСтрока(ВремТекст) Тогда
			ОтправительПредставление = Неопределено;
			Возврат;
		КонецЕсли;
		
		// Адрес есть. Поищем его в контактной информации.
		ТекстЗапрос = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтактнаяИнформация.Объект КАК Объект,
		|	КонтактнаяИнформация.ЗначениеПоУмолчанию КАК ЗначениеПоУмолчанию
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип = &Тип
		|	И КонтактнаяИнформация.Объект ССЫЛКА Справочник.Контрагенты
		|	И КонтактнаяИнформация.Представление ПОДОБНО &Представление
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗначениеПоУмолчанию УБЫВ,
		|	Объект";
		Запрос = Новый Запрос(ТекстЗапрос);
		Запрос.УстановитьПараметр("Тип",           Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		Запрос.УстановитьПараметр("Представление", ВремТекст);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СсылкаНаОбъект = Выборка.Объект;		
		КонецЕсли;	
		
		// Теперь табличная часть.
		Для Каждого ТекСтрока Из Получатели Цикл  	
			ТекСтрока.СсылкаНаОбъект = Неопределено;	
		КонецЦикла;
	Иначе
		СсылкаНаОбъект = Неопределено;
		                  	
		// Табличная часть.
		ТекстЗапрос = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтактнаяИнформация.Объект КАК Объект,
		|	КонтактнаяИнформация.ЗначениеПоУмолчанию КАК ЗначениеПоУмолчанию
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип = &Тип
		|	И КонтактнаяИнформация.Объект ССЫЛКА Справочник.Контрагенты
		|	И КонтактнаяИнформация.Представление ПОДОБНО &Представление
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗначениеПоУмолчанию УБЫВ,
		|	Объект";		
		Запрос = Новый Запрос(ТекстЗапрос);
		Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
                                          		
		Для Каждого ТекСтрока Из Получатели Цикл  
			АдресЭлектроннойПочты = ТекСтрока.АдресЭлектроннойПочты;
			Если ПустаяСтрока(АдресЭлектроннойПочты) Тогда
				ТекСтрока.СсылкаНаОбъект = Неопределено;	
				Продолжить;
			КонецЕсли;
			
			// Адрес есть. Поищем его в контактной информации. 			
			Запрос.УстановитьПараметр("Представление", АдресЭлектроннойПочты);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ТекСтрока.СсылкаНаОбъект = Выборка.Объект;		
			КонецЕсли;			
		КонецЦикла;		
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтправитьВнешнимКлиентом") Тогда
		Если НЕ Отказ Тогда
			ЭтотОбъект.ДополнительныеСвойства.Вставить("УспешнаяПроверкаПередЗаписью", Истина);
		КонецЕсли;
		Отказ = Истина;
	КонецЕсли;
	
	дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	#Если Клиент Тогда
		Оповестить("ОбновитьДеревоСобытий", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли



// Модуль документа ПриходныйСкладскойОрдер

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Фундаментальный алгоритм размещения по ближайшим свободным ячейкам, стараемся идти вдоль линии маршрута.
// Параметры
//		КоличествоТоваров, число - указывает, для скольки товаров ищется маршрут;
//		ПроизвольнаяТочкаВхода, структура(X,Y,Z) - используется как произвольная точка входа, перекрывает заданную в складе	
//		СписокИсключаемыхЯчеек, список значений  - список дополнительных ячеек, исключаемых из  алгоритма поиска маршрута
// Результат
// 		Список ячеек маршрута  
Функция РазместитьПоБлижайшимЯчейкам(КоличествоТоваров, ПроизвольнаяТочкаВхода = Неопределено, СписокИсключаемыхЯчеек =Неопределено) Экспорт
	// параметр "ПроизвольнаяТочкаВхода" используется, когда нужен оптимальный маршрут
	// из произвольной точки(например в подборе) 
	// "Прямая маршрута" - прямая, проходящая через точки входа и выхода.
	// координаты точек входа и выхода склада
	Если ПроизвольнаяТочкаВхода = Неопределено Тогда
		ТочкаВхода  = Новый Структура("X,Y,Z", СкладКомпании.ВходX,СкладКомпании.ВходY,СкладКомпании.ВходУровень);
	Иначе	
		ТочкаВхода  = ПроизвольнаяТочкаВхода;
	КонецЕсли;
	ТочкаВыхода = Новый Структура("X,Y,Z",СкладКомпании.ВыходX, СкладКомпании.ВыходY, СкладКомпании.ВыходУровень);
	
	//Получим список пустых ячеек
	Запрос = Новый Запрос;
	
	ТекстЗапроса =   "ВЫБРАТЬ
	                 |	ЯчейкиХранения.Ссылка,
	                 |	ЯчейкиХранения.КоординатаX КАК КоординатаX,
	                 |	ЯчейкиХранения.КоординатаY КАК КоординатаY,
	                 |	ЯчейкиХранения.Уровень КАК КоординатаZ
	                 |ИЗ
	                 |	Справочник.ЯчейкиХранения КАК ЯчейкиХранения
	                 |ГДЕ
	                 |	ЯчейкиХранения.Владелец = &СкладКомпании
	                 |	И НЕ ЯчейкиХранения.ЭтоГруппа
	                 |	И НЕ ЯчейкиХранения.ПометкаУдаления
	                 |	И НЕ ЯчейкиХранения.Недоступна
	                 |	И НЕ ЯчейкиХранения.Ссылка В
	                 |				(ВЫБРАТЬ
	                 |					ОстаткиТоваровОрдерныйСкладОстатки.Ячейка
	                 |				ИЗ
	                 |					РегистрНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(&МоментВремени, Ячейка.Владелец = &СкладКомпании) КАК ОстаткиТоваровОрдерныйСкладОстатки)
					 |";
					 
	//// Если передан список исключаемых ячеек, исключим их из таблицы пустых ячеек
	Если СписокИсключаемыхЯчеек <> Неопределено Тогда
		Если ТипЗнч(СписокИсключаемыхЯчеек) = Тип("СписокЗначений") Тогда
			ТекстЗапроса = ТекстЗапроса + "	И НЕ ЯчейкиХранения.Ссылка В (&СписокИсключаемыхЯчеек)" + Символы.ПС;
			Запрос.УстановитьПараметр("СписокИсключаемыхЯчеек",СписокИсключаемыхЯчеек);
		КонецЕсли; 
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса + "УПОРЯДОЧИТЬ ПО
	                              |	КоординатаX,
	                              |	КоординатаY,
	                              |	КоординатаZ";
								  
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	МоментВремени=?(ЭтотОбъект.ЭтоНовый(), Новый МоментВремени(КонецДня(ЭтотОбъект.Дата)),ЭтотОбъект.МоментВремени());
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);

	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЯчеек = РезультатЗапроса.Выгрузить();
	
	СписокЯчеекМаршрута = Новый СписокЗначений;
	Если КоличествоТоваров > ТаблицаЯчеек.Количество() Тогда
		// Разместим сколько сможем, остальное пользователь разместит вручную
		Сообщить("На складе """ + СкладКомпании + """ нет нужного количества свободных ячеек. Часть товаров необходимо разместить вручную.", СтатусСообщения.Информация);
	    // Уменьшим число товаров
		КоличествоТоваров = ТаблицаЯчеек.Количество();
	КонецЕсли;
	
	// Основная идея (жадный алгоритм): на каждом шаге по списку товаров находим отрезок 
	// из последней найденной ячейки до выхода 
	// и делим его на количество частей, равное количество оставшихся нераспределенных товаров. Берем точки первого, ближайшего отрезка,
	// затем считаем для всех пустых ячеек оценочное расстояние как сумму расстояний до точек отрезка.
	// Берем ячейку с минимальным расстоянием.
	КвалификаторЧисла = Новый КвалификаторыЧисла(10,3);
	ТаблицаЯчеек.Колонки.Добавить("ОценочноеРасстояние", Новый ОписаниеТипов("Число",КвалификаторЧисла));
	ТаблицаЯчеек.Колонки.Добавить("РасстояниеДоНачалаОтрезка", Новый ОписаниеТипов("Число",КвалификаторЧисла));
	ТаблицаЯчеек.Колонки.Добавить("РасстояниеДоКонцаОтрезка", Новый ОписаниеТипов("Число",КвалификаторЧисла));
	ЧислоОтрезков = КоличествоТоваров;
	НачалоОтрезка = ТочкаВхода;
	КонецОтрезка  = Новый Структура("X,Y,Z", СкладКомпании.ВыходX,СкладКомпании.ВыходY,СкладКомпании.ВыходУровень);
	// пробегаем по ТЧ Товары
	Для Ин=1  По КоличествоТоваров Цикл
		КонецОтрезка.X = (ТочкаВыхода.X + (ЧислоОтрезков-1)*НачалоОтрезка.X) / ЧислоОтрезков;
		КонецОтрезка.Y = (ТочкаВыхода.Y + (ЧислоОтрезков-1)*НачалоОтрезка.Y) / ЧислоОтрезков;
		КонецОтрезка.Z = (ТочкаВыхода.Z + (ЧислоОтрезков-1)*НачалоОтрезка.Z) / ЧислоОтрезков;
		//ищем ячейку с минимальным оценочным расстоянием
		Для Каждого СтрокаТЗ Из ТаблицаЯчеек Цикл
			// считаем расстояние от каждой ячейки до начала отрезка по формуле расстояния между 2-мя точками									  
			СтрокаТЗ.РасстояниеДоНачалаОтрезка = Sqrt((НачалоОтрезка.X - СтрокаТЗ.КоординатаX)*(НачалоОтрезка.X - СтрокаТЗ.КоординатаX) + (НачалоОтрезка.Y-СтрокаТЗ.КоординатаY)*(НачалоОтрезка.Y-СтрокаТЗ.КоординатаY) + (НачалоОтрезка.Z-СтрокаТЗ.КоординатаZ)*(НачалоОтрезка.Z-СтрокаТЗ.КоординатаZ));
			// считаем расстояние до конца отрезка по формуле расстояния между 2-мя точками									  
			СтрокаТЗ.РасстояниеДоКонцаОтрезка  = Sqrt((КонецОтрезка.X - СтрокаТЗ.КоординатаX)*(КонецОтрезка.X - СтрокаТЗ.КоординатаX) + (КонецОтрезка.Y-СтрокаТЗ.КоординатаY)*(КонецОтрезка.Y-СтрокаТЗ.КоординатаY) + (КонецОтрезка.Z-СтрокаТЗ.КоординатаZ)*(КонецОтрезка.Z-СтрокаТЗ.КоординатаZ));
			//считаем оценочное расстояние
			СтрокаТЗ.ОценочноеРасстояние = СтрокаТЗ.РасстояниеДоНачалаОтрезка + СтрокаТЗ.РасстояниеДоКонцаОтрезка;
		КонецЦикла;                                                                                                
		ТаблицаЯчеек.Сортировать("ОценочноеРасстояние Возр");
		ПерваяСтрока = ТаблицаЯчеек.Получить(0);
		ОптимальнаяЯчейкаМаршрута = ТаблицаЯчеек.Получить(0).Ссылка;
		СписокЯчеекМаршрута.Добавить(ОптимальнаяЯчейкаМаршрута,Ин);
		ТаблицаЯчеек.Удалить(0);
		НачалоОтрезка.X = ОптимальнаяЯчейкаМаршрута.КоординатаX;
		НачалоОтрезка.Y = ОптимальнаяЯчейкаМаршрута.КоординатаY;
		НачалоОтрезка.Z = ОптимальнаяЯчейкаМаршрута.Уровень;
		ЧислоОтрезков = ЧислоОтрезков - 1; 
	КонецЦикла; 
	
	Возврат СписокЯчеекМаршрута;
КонецФункции // РазместитьПоБлижайшимЯчейкам()
               
// размещение товара по последним используемым ими ячейкам
// Результат - Таблица товаров и ячеек маршрута
//
Функция РазместитьПоРанееИспользуемымЯчейкам() Экспорт
	
	СписокТоваров = Новый СписокЗначений;
	СписокТоваров.ЗагрузитьЗначения(Товары.ВыгрузитьКолонку("Номенклатура"));
	//Получим список движений данных товаров по ячейкам
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиТоваровОрдерныйСклад.Номенклатура КАК Номенклатура,
	               |	ОстаткиТоваровОрдерныйСклад.Ячейка,
	               |	ОстаткиТоваровОрдерныйСклад.Период
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ОстаткиТоваровОрдерныйСклад.Номенклатура КАК Номенклатура,
	               |			МАКСИМУМ(ОстаткиТоваровОрдерныйСклад.Период) КАК Период
	               |		ИЗ
	               |			РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
	               |		ГДЕ
	               |			ОстаткиТоваровОрдерныйСклад.МоментВремени < &КонецПериода
	               |			И ОстаткиТоваровОрдерныйСклад.ВидДвижения = &ВидДвиженияПриход
	               |			И ОстаткиТоваровОрдерныйСклад.Ячейка.Владелец = &СкладКомпании
	               |			И ОстаткиТоваровОрдерныйСклад.Номенклатура В(&СписокТоваров)
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ОстаткиТоваровОрдерныйСклад.Номенклатура) КАК ВложенныйЗапрос
	               |		ПО ОстаткиТоваровОрдерныйСклад.Номенклатура = ВложенныйЗапрос.Номенклатура
	               |			И ОстаткиТоваровОрдерныйСклад.Период = ВложенныйЗапрос.Период
	               |ГДЕ
	               |	ОстаткиТоваровОрдерныйСклад.МоментВремени < &КонецПериода
	               |	И ОстаткиТоваровОрдерныйСклад.ВидДвижения = &ВидДвиженияПриход
	               |	И ОстаткиТоваровОрдерныйСклад.Ячейка.Владелец = &СкладКомпании
	               |	И НЕ ОстаткиТоваровОрдерныйСклад.Ячейка.ПометкаУдаления
	               |	И НЕ ОстаткиТоваровОрдерныйСклад.Ячейка.Недоступна
	               |	И ОстаткиТоваровОрдерныйСклад.Номенклатура В(&СписокТоваров)";
	
	
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	КонецПериода = ?(ЭтотОбъект.ЭтоНовый(), Новый МоментВремени(КонецДня(ЭтотОбъект.Дата)),ЭтотОбъект.МоментВремени());
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("СписокТоваров", СписокТоваров);

	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции // РазместитьПоРанееИспользуемымЯчейкам()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Если ХозОперация=Справочники.ХозОперации.ПриходныйСкладскойОрдерШин Тогда
		Для каждого ВидНоменклатуры Из Метаданные.Перечисления.ВидыНоменклатуры.ЗначенияПеречисления Цикл
			ВидНоменклатурыСсылка=Перечисления.ВидыНоменклатуры[ВидНоменклатуры.Имя];
			Если ВидНоменклатурыСсылка<>Перечисления.ВидыНоменклатуры.Шины Тогда
				Результат.Вставить(ВидНоменклатурыСсылка,0);
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	КонецЕсли; 
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Возврат Рез;
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПриходныйСкладскойОрдер", "Приходный складской ордер", Истина);
	СписокХозОпераций.Добавить("ПриходныйСкладскойОрдерШин", "Приходный складской ордер шин", Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый Тогда
		ОбязательныеТовары.Вставить("Ячейка",3);
	КонецЕсли; 

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	Если ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдерШин Тогда
		ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);  	
			   							
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
		
		Если ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдерШин Тогда
			Если обЗначениеНеЗаполнено(ДокументОснование) ИЛИ ТипЗнч(ДокументОснование)<>Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" должен быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли; 
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			               |	ПриходныйСкладскойОрдер.Ссылка КАК Ссылка
			               |ИЗ
			               |	Документ.ПриходныйСкладскойОрдер КАК ПриходныйСкладскойОрдер
			               |ГДЕ
			               |	ПриходныйСкладскойОрдер.ДокументОснование = &ДокументОснование
			               |	И ПриходныйСкладскойОрдер.Ссылка <> &Ссылка";
			Запрос.УстановитьПараметр("ДокументОснование",ДокументОснование);
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				дкДобавитьОшибку(Ошибки,"На основании <"+ДокументОснование+"> уже есть введенный документ <"+Выборка.Ссылка+">.");
				Результат=Ложь;
			КонецЕсли; 
		Иначе
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" не может быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.Чек") И НЕ ДокументОснование.ХозОперация = Справочники.ХозОперации.ЧекНаВозврат Тогда
		дкДобавитьОшибку(Ошибки, "Ввод приходного складского ордера на основании документа """+Метаданные.Документы.Чек+""" возможен только для хоз. операции ""Чек на возврат"".");
		Результат=Ложь;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ПриходныйСкладскойОрдер"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриходныйСкладскойОрдер(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПриходныйСкладскойОрдер");
	
	//для начала настроим макет
	// Произведем преобразование макета в зависимости от типа ячеистого склада
	ЭтоЯчеистыйСклад = (СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый);
	Если НЕ ЭтоЯчеистыйСклад Тогда
		ОбластьТовар				= Макет.Область("Товар");
		ОбластьЯчейка				= Макет.Область("Ячейка");
		ОбластьТовар.ШиринаКолонки	= ОбластьТовар.ШиринаКолонки + ОбластьЯчейка.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьВладелецТовара = Макет.Область("Владелец");
		Макет.УдалитьОбласть(ОбластьВладелецТовара,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьДокументОснование = Макет.Область("ДокументОснование");
		Макет.УдалитьОбласть(ОбластьДокументОснование,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//bz++
	//вывод заголовка документа
	//ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	//ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	//ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	//ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	//
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	//ОбластьМакета.Параметры.ПредставлениеСклада		 = СкладКомпании;
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;
	ОбластьМакета.Параметры.ПредставлениеСклада			= СкладКомпании;
	//bz--
	
	Если НЕ обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьМакета.Параметры.ПредставлениеВладельца	= спПолучитьПредставление(ВладелецТовара);
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета.Параметры.Заполнить(ДокументОснование);
		ОбластьМакета.Параметры.ПредставлениеДокументаОснования	= дкПолучитьПредставление(ДокументОснование);
		Попытка
			ОбластьМакета.Параметры.ПредставлениеКонтрагента		= спПолучитьПредставление(ДокументОснование.Контрагент);
			ОбластьМакета.Параметры.ПредставлениеДоговора			= ДокументОснование.ДоговорВзаиморасчетов;
		Исключение
        КонецПопытки; 
        ОбластьМакета.Параметры.ДокументОснование = ДокументОснование;
	КонецЕсли;
    	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);
		СтруктураСтроки.Вставить("КоличествоБазовое", Формат(СтрокаТабличнойЧасти.КоличествоБазовое,ФорматВыводаКоличества));
		СтруктураСтроки.Вставить("ЕдиницаИзмеренияБазовая", СтрокаТабличнойЧасти.Номенклатура.БазоваяЕдиницаИзмерения);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы,,ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПриходныйСкладскойОрдер()

// Формирует печатную форму "Приходный ордер М-4"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ4(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("М4");
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// Выводим общие реквизиты шапки
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО		= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим заголовок документа
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ДатаСоставления			= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.МестоПриемки			= СкладКомпании;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Попытка
			Контрагент										= ДокументОснование.Контрагент;
			ОбластьМакета.Параметры.ПоставщикНаименование	= спПолучитьНаименование(Контрагент);
			ОбластьМакета.Параметры.Поставщик				= Контрагент;
			ОбластьМакета.Параметры.ПоставщикКод			= Контрагент.Код;
		Исключение КонецПопытки;
	КонецЕсли;
	СтраховаяКомпания = дкОтветственноеЛицо(ЭтотОбъект,"СтраховаяКомпания");
	ОбластьМакета.Параметры.Заполнить(СтраховаяКомпания);
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.Валюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	СтрокНаСтранице = 23;
	СтрокШапки      = 9;
	СтрокПодвала    = 3;
	НомерСтраницы   = 1;

	КоличествоСтрок = ЭтотОбъект.Товары.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Определим, что показывать в колонке кода - код или артикул
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	// Инициализация итогов в документе
	ИтогоКоличествоПринято = 0;
	Ном = 0;
    ИтогоПоСтраницеКоличествоПринято = 0;
	
	ПодвалСтраницы=Макет.ПолучитьОбласть("ПодвалСтраницы");
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаСтрокТовары Цикл
		Если обЗначениеНеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;
		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 ИЛИ ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			НомерСтраницы = НомерСтраницы + 1;
			ИтогоПоСтранице=Макет.ПолучитьОбласть("ИтогоПоСтранице");
			ИтогоПоСтранице.Параметры.ИтогоКоличествоПринято = ИтогоПоСтраницеКоличествоПринято;
			ИтогоПоСтраницеКоличествоПринято=0;
			ТабДокумент.Вывести(ИтогоПоСтранице);
			ТабДокумент.Вывести(ПодвалСтраницы);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;

		КоличествоПринято = СтрокаТабличнойЧасти.Количество;
		ОбластьМакета.Параметры.КоличествоПринято = Формат(КоличествоПринято,ФорматВыводаКоличества);
		//КоличествоПоДокументу берем из основания, если оно есть
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			ТекНоменклатура = СтрокаТабличнойЧасти.Номенклатура;
			// получаем список строк с данным товаром (их может быть несколько с разными характеристиками)
			МассивНайденныхСтрок = ДокументОснование.Товары.НайтиСтроки(Новый Структура("Номенклатура",ТекНоменклатура));
			СписокСтрокТоварыОснования = Новый СписокЗначений; 
			КолвоИзОснования = 0;
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				ТекСтрока=МассивНайденныхСтрок[Сч];
				Если ТекСтрока.ХарактеристикаНоменклатуры = СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры Тогда
					КолвоИзОснования = ТекСтрока.Количество;
					Прервать;
				КонецЕсли; 
			КонецЦикла;
			ОбластьМакета.Параметры.КоличествоПоДокументу = Формат(КолвоИзОснования,ФорматВыводаКоличества);
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		Характеристика											= Строка(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод						= дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКод				= СтрокаТабличнойЧасти.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование	= СтрокаТабличнойЧасти.ЕдиницаИзмерения;
		ТабДокумент.Вывести(ОбластьМакета);
		ИтогоКоличествоПринято = ИтогоКоличествоПринято + КоличествоПринято;
		ИтогоПоСтраницеКоличествоПринято = ИтогоПоСтраницеКоличествоПринято + КоличествоПринято;
	КонецЦикла;

	//итоги по последней странице
	ИтогоПоСтранице=Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ИтогоПоСтранице.Параметры.ИтогоКоличествоПринято = Формат(ИтогоПоСтраницеКоличествоПринято,ФорматВыводаКоличества);
	ИтогоПоСтраницеКоличествоПринято=0;
	ТабДокумент.Вывести(ИтогоПоСтранице);
	
	// Выводим итоги по документу
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");

	ОбластьМакета.Параметры.ИтогоКоличествоПринято = Формат(ИтогоКоличествоПринято,ФорматВыводаКоличества);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим итоги по документу
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху         = 0;
	ТабДокумент.ПолеСлева          = 0;
	ТабДокумент.ПолеСнизу          = 0;
	ТабДокумент.ПолеСправа         = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
КонецФункции // ПечатьМ4()

// Печатает отчет "Карта склада"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКартаСклада(ТабДокумент) Экспорт
	ОтчетКартаСклада = Отчеты.КартаСклада.Создать();
	ОтчетКартаСклада.Склад = СкладКомпании;
	ОтчетКартаСклада.ИсточникМаршрута = ЭтотОбъект.Ссылка;
	ФормаКартаСклада = ОтчетКартаСклада.ПолучитьФорму();
	ФормаКартаСклада.РежимОтчетаПереданный = 1;
	ФормаКартаСклада.Открыть();
	ФормаКартаСклада.НарисоватьМаршрутДокумента(ЭтотОбъект);
	ТабДокумент = Неопределено;
	Возврат ТабДокумент;
КонецФункции // ПечатьКартаСклада()

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

//Функция печати Акт о возврате ТМЦ
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьМХ1(ТабДокумент) Экспорт

	Макет = ПолучитьОбщийМакет("МХ1");
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.Заполнить(ЭтотОбъект);
	ОбластьЗаголовок.Параметры.НомерДокумента                   = Номер;
	ОбластьЗаголовок.Параметры.ДатаДокумента                    = Дата;
	ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО                = Организация.КодПоОКПО;
	ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП = Организация.КодПоОКДП;
	ОбластьЗаголовок.Параметры.ПредставлениеАдресаХранителя     = СкладКомпании;
	
	//bz++
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;
	//bz--
	
	Если НЕ обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьЗаголовок.Параметры.ПредставлениеВладельца = спПолучитьПредставление(ВладелецТовара);
		Если Тип("СправочникСсылка.СкладыКомпании") = ТипЗнч(ВладелецТовара) Тогда
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ВладелецТовара.Организация.КодПоОКПО
		Иначе
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ВладелецТовара.КодПоОКПО;
		КонецЕсли;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьЗаголовок);
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьШапка);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ВыводитьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,ЭтотОбъект);
	ВыборкаСтрок = Товары.Выгрузить();
	НомерСтроки = 1;
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
		Если ВыводитьКод Тогда
			ОбластьСтрока.Параметры.Код = Строка(СтрокаТоваров.Номенклатура.Артикул);
		КонецЕсли;
		ОбластьСтрока.Параметры.Номенклатура     = Строка(СтрокаТоваров.Номенклатура);
		ОбластьСтрока.Параметры.Характеристика   = Строка(СтрокаТоваров.ХарактеристикаНоменклатуры);
		ОбластьСтрока.Параметры.ЕдиницаИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.КодОКЕИ          = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество       = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ТабДокумент.Вывести(ОбластьСтрока);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"),ФорматВыводаКоличества);
	ТабДокумент.Вывести(ОбластьИтого);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьПодвал);
	Возврат ТабДокумент;
КонецФункции // ПечатьМХ3(ТабДокумент)

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ВычетыТЧВыполнены = Ложь;
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			ЭтотОбъект.СкладКомпании = Основание.СкладПолучатель;
			// заполнение ячеек
			Для Каждого СтрТовары Из Основание.Товары Цикл
				Товары[СтрТовары.НомерСтроки-1].Ячейка=СтрТовары.ЯчейкаПолучатель;
			КонецЦикла;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Комплектация") Тогда
			Товары.Очистить();
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = Основание.Комплект;
			НоваяСтрока.ХарактеристикаНоменклатуры = Основание.ХарактеристикаКомплекта;
			НоваяСтрока.ЕдиницаИзмерения = Основание.Комплект.ОсновнаяЕдиницаИзмерения;
			НоваяСтрока.Коэффициент = Основание.Комплект.ОсновнаяЕдиницаИзмерения.Коэффициент;
			НоваяСтрока.Количество = Основание.КоличествоКомплектов;
			НоваяСтрока.КоличествоБазовое = Основание.КоличествоКомплектов;
			Если обЗначениеНеЗаполнено(Основание.Ячейка) Тогда
				Если НЕ обЗначениеНеЗаполнено(НоваяСтрока.Номенклатура) Тогда
					НоваяСтрока.Ячейка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(НоваяСтрока.Номенклатура,СкладКомпании);
				КонецЕсли;
			Иначе
				НоваяСтрока.Ячейка = Основание.Ячейка;
			КонецЕсли;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Разукомплектация") Тогда
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			             |	РазукомплектацияТовары.НомерСтроки КАК НомерСтроки,
			             |	РазукомплектацияТовары.Номенклатура,
			             |	РазукомплектацияТовары.ХарактеристикаНоменклатуры,
			             |	РазукомплектацияТовары.ЕдиницаИзмерения,
			             |	РазукомплектацияТовары.Коэффициент,
			             |	РазукомплектацияТовары.Количество * РазукомплектацияТовары.Ссылка.КоличествоКомплектов КАК Количество,
			             |	РазукомплектацияТовары.Количество * РазукомплектацияТовары.Ссылка.КоличествоКомплектов КАК КоличествоБазовое,
			             |	РазукомплектацияТовары.Ячейка
			             |ИЗ
			             |	Документ.Разукомплектация.Товары КАК РазукомплектацияТовары
			             |ГДЕ
			             |	РазукомплектацияТовары.Ссылка = &Ссылка
			             |
			             |УПОРЯДОЧИТЬ ПО
			             |	НомерСтроки";
			Запрос.УстановитьПараметр("Ссылка",Основание);
			Выборка=Запрос.Выполнить().Выбрать();
			Товары.Очистить();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = Выборка.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения  = Выборка.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент       = Выборка.Коэффициент;
				НоваяСтрока.Количество        = Выборка.Количество;
				НоваяСтрока.КоличествоБазовое = Выборка.КоличествоБазовое;
				НоваяСтрока.Ячейка            = Выборка.Ячейка;
			КонецЦикла;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
			Товары.Очистить();
			Для Каждого СтрТовар Из Основание.Товары Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = СтрТовар.Коэффициент;
				НоваяСтрока.Количество = СтрТовар.Количество;
				НоваяСтрока.КоличествоБазовое = СтрТовар.Количество;
				НоваяСтрока.Ячейка = СтрТовар.Ячейка;
			КонецЦикла;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
			ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдерШин;
			ХранениеШинКомплектами=обПраво("ХранениеШинКомплектами",Права,,ЭтотОбъект);
			Товары.Очистить();
			Для Каждого СтрТовар Из Основание.Шины Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = СтрТовар.Коэффициент;
				НоваяСтрока.Количество = СтрТовар.Количество;
				НоваяСтрока.КоличествоБазовое = СтрТовар.Количество;
				Если ХранениеШинКомплектами Тогда
					НоваяСтрока.Ячейка = Основание.Ячейка;
				Иначе
					НоваяСтрока.Ячейка = СтрТовар.Ячейка;
				КонецЕсли; 
			КонецЦикла;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РазукомплектацияАвтомобилей") Тогда
			Товары.Очистить();
			Для Каждого СтрТовар Из Основание.Товары Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = СтрТовар.Коэффициент;
				НоваяСтрока.Количество = СтрТовар.Количество;
				НоваяСтрока.КоличествоБазовое = СтрТовар.Количество;
				НоваяСтрока.Ячейка = СтрТовар.Ячейка;
			КонецЦикла;
			Для Каждого СтрТовар Из Основание.Опции Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = СтрТовар.Коэффициент;
				НоваяСтрока.Количество = СтрТовар.Количество;
				НоваяСтрока.КоличествоБазовое = СтрТовар.Количество;
				НоваяСтрока.Ячейка = СтрТовар.Ячейка;
			КонецЦикла;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			
			Товары.Очистить();
			Для Каждого СтрТовар Из Основание.Товары Цикл
				Если СтрТовар.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = СтрТовар.Коэффициент;
				НоваяСтрока.Количество = СтрТовар.Количество;
				НоваяСтрока.КоличествоБазовое = СтрТовар.Количество;
				Если НЕ обЗначениеНеЗаполнено(НоваяСтрока.Номенклатура) Тогда
					НоваяСтрока.Ячейка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(НоваяСтрока.Номенклатура,СкладКомпании);
				КонецЕсли;
				
			КонецЦикла;
			ВычетыТЧВыполнены = Истина;
			
		ИначеЕсли обЭтотТип(Основание, "ДокументСсылка.Чек") Тогда
			
			Если Основание.ХозОперация = Справочники.ХозОперации.ЧекНаВозврат Тогда
				
				Комментарий = "Введено на основании : " + Строка(Основание) + ".";
				
			Иначе
				
				Сообщить("Ввод приходного складского ордера возможен только для хоз. операции ""Чек на возврат""", СтатусСообщения.Важное);
				дкОтменаВводаДокумента(ЭтотОбъект);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Попытка
			Если НЕ обЗначениеНеЗаполнено(Основание.Контрагент) Тогда
				ВладелецТовара = Основание.Контрагент;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ ВычетыТЧВыполнены Тогда
		дкЗаполнитьТабличнуюЧастьПоОснованию(ЭтотОбъект, Основание, , , Ложь);
	КонецЕсли; 
	
	Для Каждого ТекСтрокаТЧ Из Товары Цикл
		Если ТекСтрокаТЧ.Коэффициент = 0 Тогда
			ТекСтрокаТЧ.Коэффициент = ?(ТекСтрокаТЧ.ЕдиницаИзмерения.Пустая(), 1, ТекСтрокаТЧ.ЕдиницаИзмерения.Коэффициент);
		КонецЕсли;	
		ТекСтрокаТЧ.КоличествоБазовое = ТекСтрокаТЧ.Количество * ТекСтрокаТЧ.Коэффициент;
	КонецЦикла;	
	
	//произведем свертку
	ТоварыВременная = Товары.Выгрузить();
	Товары.Очистить();
	Для Каждого ТекСтрока Из ТоварыВременная Цикл
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			Продолжить;	
		КонецЕсли;
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура", 				ТекСтрока.Номенклатура);
		ПараметрыОтбора.Вставить("ХарактеристикаНоменклатуры", 	ТекСтрока.ХарактеристикаНоменклатуры);
		ПараметрыОтбора.Вставить("Коэффициент",				 	ТекСтрока.Коэффициент);
		ПараметрыОтбора.Вставить("ЕдиницаИзмерения", 			ТекСтрока.ЕдиницаИзмерения);
		ПараметрыОтбора.Вставить("Ячейка", 						ТекСтрока.Ячейка);
		НайденныеСтроки = Товары.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество()=0 Тогда
			//еще нет такой строки
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока);
		Иначе
			//уже есть такая строка, посмотрим можно ли свернуть
			УчетПоХарактеристикам = Ложь;
			ТипНоменклатуры = ТекСтрока.Номенклатура.ТипНоменклатуры;
			Если НЕ обЗначениеНеЗаполнено(ТипНоменклатуры) Тогда
				УчетПоХарактеристикам = (ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ ТипНоменклатуры.ИспользованиеХарактеристик = 2);
			КонецЕсли;	
			Если УчетПоХарактеристикам И обЗначениеНеЗаполнено(ТекСтрока.ХарактеристикаНоменклатуры) Тогда
				//ведется учет по характеристикам - свернуть нельзя
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока);
			Иначе
				НайденнаяСтрока = НайденныеСтроки[0];
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + ТекСтрока.Количество;
				НайденнаяСтрока.КоличествоБазовое = НайденнаяСтрока.КоличествоБазовое + ТекСтрока.КоличествоБазовое;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Произведем свертку.
	//Товары.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент, Ячейка", "Количество, КоличествоБазовое");
	//Для Каждого СтрТовары Из Товары Цикл
	//	ОбработкаРеквизита("Товары.Ячейка",СтрТовары);
	//КонецЦикла;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		БылиДвижения = Ложь;
		
		// ордерный склад
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиТоваровОрдерныйСклад ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	// на всякий случай проверим тип склада, его могли поменять в самом справочнике
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Обычный Тогда
		Отказ = Истина;
		РезультатОбработки = "Склад документа имеет вид """ + СкладКомпании.ВидСклада + """. Проведение возможно только для ""Ордерного"" или ""Ячеистого"" склада!";
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.ПриходныйСкладскойОрдерШин Тогда
		ХранениеШинКомплектами=обПраво("ХранениеШинКомплектами",Права,,ЭтотОбъект);
		Если ХранениеШинКомплектами Тогда
			ТаблицаТоваров=Товары.Выгрузить();
			ТаблицаТоваров.Свернуть("Ячейка");
			Если ТаблицаТоваров.Количество()>1 Тогда
				Отказ = Истина;
				РезультатОбработки = "Хранение шин возможно только покомплектно. Все шины должны быть помещены в одну ячейку.";
				дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
				Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проведем остатки товаров по регистру ОстаткиТоваровОрдерныйСклад
	НаборЗаписейОстатки = Движения.ОстаткиТоваровОрдерныйСклад;
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.ДокументОбъект  = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании   = СкладКомпании;
	Если ХозОперация=Справочники.ХозОперации.ПриходныйСкладскойОрдерШин Тогда
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=ДокументОснование;	
	Иначе
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=Неопределено;	
	КонецЕсли; 
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = Неопределено;
	НаборЗаписейОстатки.Приходовать     = Истина;
	НаборЗаписейОстатки.ПоБазовомуКоличеству = Истина;
	Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	// двигаем границу последовательности ордерного склада
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		НаборЗаписейГраницыОрдерныйСклад = РегистрыСведений.ГраницыОрдерныйСклад.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= Дата;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыОрдерныйСклад.ДокументСсылка	= Ссылка;
		НаборЗаписейГраницыОрдерныйСклад.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем Режим;						// режим
Перем ТабСписок Экспорт;			// Таблица для переноса интервалов

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	// Обязательные поля таблицы товаров
	ОбязательныеСотрудники=Новый Структура();
	ОбязательныеСотрудники.Вставить("Сотрудник",3);
	ОбязательныеСотрудники.Вставить("ВидИнтервала",3);
	ОбязательныеСотрудники.Вставить("Дата",3);
	
	
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Цех",1);
	ОбязательныеРеквизиты.Вставить("ДатаТабеля",1);
	ОбязательныеРеквизиты.Вставить("Сотрудники",ОбязательныеСотрудники);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = Истина;
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	// Запретим наличие более одного документа в разрезе Дата-Цех
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Табель.Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		УРВ_Табель.Ссылка КАК Ссылка,
	|		НАЧАЛОПЕРИОДА(УРВ_Табель.Дата, ДЕНЬ) КАК ДатаДок
	|	ИЗ
	|		Документ.УРВ_Табель КАК УРВ_Табель
	|	ГДЕ
	|		УРВ_Табель.Цех = &Цех
	|		И УРВ_Табель.Ссылка <> &ЭтотДокумент) КАК Табель
	|ГДЕ
	|	Табель.ДатаДок = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)");
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Цех",Цех);
	Запрос.УстановитьПараметр("ЭтотДокумент",ЭтотОбъект.Ссылка);
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	Если ВыборкаДокументов.Следующий() Тогда
		дкДобавитьОшибку(Ошибки,"Дублирование документов по дате и цеху!. Существующий документ <" + ВыборкаДокументов.Ссылка+ ">");
		Результат = Ложь;
	КонецЕсли;           
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УРВ_ТабельУчетаРабочегоВремени","Табель учета рабочего времени",Истина);
	Возврат СписокХозОпераций;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ОБЪЕКТА
	Если Имя = "Сотрудник" Тогда
		Если НЕ ТекСтрока.Сотрудник.Пустая() Тогда
			ТекСтрока.ПродолжительностьСмены = 0;				
			ТекСтрока.ПричинаОтсутствия = Перечисления.УРВ_ПричиныОтсутствияНаРабочемМесте.ПустаяСсылка();
			ТекСтрока.НочнаяСмена = Ложь;
		КонецЕсли;
		Рез = Истина;
		Возврат Рез;
	ИначеЕсли Имя = "ПродолжительностьСмены" Тогда
		Если ТекСтрока.ПродолжительностьСмены <> 0 Тогда
			ТекСтрока.ПричинаОтсутствия = Перечисления.УРВ_ПричиныОтсутствияНаРабочемМесте.ПустаяСсылка();	
		Иначе
			ТекСтрока.НочнаяСмена = Ложь;	
		КонецЕсли;
		Рез = Истина;
		Возврат Рез;
	ИначеЕсли Имя = "ПричинаОтсутствия" Тогда
		Если НЕ ТекСтрока.ПричинаОтсутствия.Пустая() Тогда
			ТекСтрока.ПродолжительностьСмены = 0;				
		Иначе
			ТекСтрока.НочнаяСмена = Ложь;	
		КонецЕсли;
		Рез = Истина;
		Возврат Рез;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

//Выполняет проверку налиция табеля на дату
//
// Параметры:
// ВыдаватьПредупреждение	– Булево – Флаг разрешения выдачи предупреждений.
//
// Возвращаемое значение:
// Булево – Результат проверки.
//
Функция НаличиеТабеляЗаЭтотМесяц(ВыдаватьПредупреждение=Истина) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УРВ_ТабельноеВремя.Регистратор
	|ИЗ
	|	РегистрСведений.УРВ_ТабельноеВремя КАК УРВ_ТабельноеВремя
	|ГДЕ
	|	УРВ_ТабельноеВремя.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И УРВ_ТабельноеВремя.Цех = &Цех
	|	И УРВ_ТабельноеВремя.Регистратор <> &Регистратор";
	
	Запрос.УстановитьПараметр("ДатаНач" , НачалоМесяца(ДатаТабеля));
	Запрос.УстановитьПараметр("ДатаКон" , КонецМесяца(ДатаТабеля));
	Запрос.УстановитьПараметр("Цех"     , Цех);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если ВыдаватьПредупреждение Тогда
			#Если Клиент Тогда
				Предупреждение("За " + ПолучитьПредставлениеДаты(ДатаТабеля) + " уже введен табель """ + Строка(Выборка.Регистратор) + """. Проведение документа будет не возможно.", 15);
			#КонецЕсли
		Иначе
			дкСообщитьРезультат("За " + ПолучитьПредставлениеДаты(ДатаТабеля) + " уже введен табель """ + Строка(Выборка.Регистратор) + """.",,ЭтотОбъект);
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

//Преобразует переданную дату в формат представления
//
// Параметры:
// ДатаДляПреобразования	– Дата – Дата до преобразования.
//
// Возвращаемое значение:
// Строка – Строка, полученная в результате форматирования.
//
Функция ПолучитьПредставлениеДаты(ДатаДляПреобразования) Экспорт
	Возврат Формат(ДатаДляПреобразования, "ДФ='ММММ гггг'");
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
// Возвращает сформированный табличный документ:

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Отказ = НаличиеТабеляЗаЭтотМесяц(Ложь);
	
	// запишем таблицу в регистр
	Если НЕ Отказ Тогда
		НаборТабельноеВремя = Движения.УРВ_ТабельноеВремя;
		
		Для Каждого стрСотрудники Из Сотрудники Цикл
			НоваяЗапись              = НаборТабельноеВремя.Добавить();
			НоваяЗапись.Регистратор  = Ссылка;
			НоваяЗапись.Период       = стрСотрудники.Дата;
			НоваяЗапись.Сотрудник    = стрСотрудники.Сотрудник;
			НоваяЗапись.Цех          = Цех;
			НоваяЗапись.ВидИнтервала = стрСотрудники.ВидИнтервала;
			НоваяЗапись.Часы         = стрСотрудники.Часы;
		КонецЦикла;
	КонецЕсли;
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// создадим таблицу значений

ТабСписок = Новый ТаблицаЗначений;
ТабСписок.Колонки.Добавить("ВидИнтервала", Новый ОписаниеТипов("СправочникСсылка.ВидыИнтервалов"));
ТабСписок.Колонки.Добавить("Часов", Новый ОписаниеТипов("Число",,Новый КвалификаторыЧисла(3,1,ДопустимыйЗнак.Неотрицательный)));

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

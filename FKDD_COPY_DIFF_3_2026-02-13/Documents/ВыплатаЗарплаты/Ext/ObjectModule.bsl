// Модуль документа ВыплатаЗарплаты

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы сотрудников
	ОбязательныеСотрудники=Новый Структура();
	ОбязательныеСотрудники.Вставить("Контрагент",1);
	ОбязательныеСотрудники.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеСотрудники.Вставить("Сумма",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("КассаКомпании",1);	
	ОбязательныеРеквизиты.Вставить("СтатьяДДС",1);	
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СуммаДокумента",1);	
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Сотрудники",ОбязательныеСотрудники);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки = Новый Структура();
				КонтролируемыеРеквизитыШапки.Вставить("КассаКомпании");
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			КонецЕсли;
			
			КонтролируемыеРеквизитыСотрудники = Новый Структура();
			КонтролируемыеРеквизитыСотрудники.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Сотрудники",КонтролируемыеРеквизитыСотрудники);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);	
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВыплатаЗарплаты","Выплата заработной платы",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ОБЪЕКТА
	
	Если Имя = "Сотрудники.Контрагент" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Контрагент) Тогда
			ТекСтрока.ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
		Иначе 
			ДопПараметры = Новый Структура;
			ВидДоговора = Перечисления.ВидыДоговоров.Зарплата;
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.Зарплата);
			ДопПараметры.Вставить("ВалютаВзаиморасчетов", ВалютаДокумента);
			ТекСтрока.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ТекСтрока.Контрагент,ВидДоговора,ЭтотОбъект,ДопПараметры);
		КонецЕсли; 
	ИначеЕсли Имя = "Сотрудники.Сумма" Тогда 
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;		
КонецФункции // ОбработкаРеквизита()

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Сотрудники.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
// Формирует печатную форму "РасходныйКассовыйОрдер"
// Возвращает сформированный табличный документ:


// Формирует печатную форму "Выплата"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВыплата(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Выплата");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.Валюта = ВалютаДокумента;
	ОбластьМакета.Параметры.Организация = Организация;
	
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ТекстДатаНачала = Формат(ДокументОснование.ДатаНачала,"ДФ=dd.MM.yyyy"); 
		ТекстДатаКонца = Формат(ДокументОснование.ДатаКонца,"ДФ=dd.MM.yyyy"); 
		ОбластьМакета.Параметры.Период = "Период с " + ТекстДатаНачала + " по " + ТекстДатаКонца + ". Количество рабочих дней: " + ДокументОснование.КоличествоРабочихДней;
	КонецЕсли;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизация = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Итого");
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("Сумма", 0);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//Получаем область строки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ВыборкаТабличнойЧасти = ЭтотОбъект.Сотрудники;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.Сумма = Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("Сумма, ВалютаДокумента", 0, ВалютаДокумента);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Сумма = СуммаВсего;
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(СуммаВсего, ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВыплата()

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТ53(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("Т53");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);	
	
	//Получаем область шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");

	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление=спПолучитьПредставление(ОбластьМакета.Параметры.Организация,Новый Структура("Наименование",""));
	ОбластьМакета.Параметры.ПодразделениеКомпанииПредставление=спПолучитьНаименование(ОбластьМакета.Параметры.ПодразделениеКомпании);
	Если (НЕ обЗначениеНеЗаполнено(ДокументОснование)) И (ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.НачислениеЗарплаты")) Тогда
		ОбластьМакета.Параметры.ДатаНачала = ДокументОснование.ДатаНачала;
		ОбластьМакета.Параметры.ДатаКонца  = ДокументОснование.ДатаКонца;
	КонецЕсли;
	
  	//Чтение значения для директора
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	
	//Чтение значения для главбуха
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	ОбластьМакета.Параметры.ОрганизацияПоОКПО = ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.КоррСчет		= спПолучитьПредставление(ЭтотОбъект.Организация,Новый Структура("КоррСчет",""));
	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(ЭтотОбъект.СуммаДокумента,ЭтотОбъект.ВалютаДокумента);
	ОбластьМакета.Параметры.СуммаДокРублей = Цел(ЭтотОбъект.СуммаДокумента);
	ОбластьМакета.Параметры.СуммаДокКопеек = (ЭтотОбъект.СуммаДокумента - Цел(ЭтотОбъект.СуммаДокумента))*100; 
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.Валюта = ВалютаДокумента;
	СтрокаПараметров =  СтрЗаменить(ВалютаДокумента.ПараметрыПрописиНаРусском, ",", Символы.ПС);
	Коп = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 7));
	ОбластьМакета.Параметры.Коп = Коп;
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакетаШапка.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьМакетаШапка);
	
	//Таблица
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	ВыплатаЗарплатыСотрудники.НомерСтроки КАК НомерСтроки,
	                    |	ВыплатаЗарплатыСотрудники.Контрагент.Код КАК ТабельныйНомер,
	                    |	ВЫБОР
	                    |		КОГДА ВыплатаЗарплатыСотрудники.Контрагент.Сотрудник = &СотрудникПустаяСсылка
	                    |			ТОГДА ВыплатаЗарплатыСотрудники.Контрагент
	                    |		ИНАЧЕ ВыплатаЗарплатыСотрудники.Контрагент.Сотрудник
	                    |	КОНЕЦ КАК Физлицо,
	                    |	ЕСТЬNULL(ВыплатаЗарплатыСотрудники.Контрагент.Сотрудник.Наименование, ВыплатаЗарплатыСотрудники.Контрагент.Наименование) КАК ФизлицоПредставление,
	                    |	ВыплатаЗарплатыСотрудники.Сумма КАК Сумма
	                    |ИЗ
	                    |	Документ.ВыплатаЗарплаты.Сотрудники КАК ВыплатаЗарплатыСотрудники
	                    |ГДЕ
	                    |	ВыплатаЗарплатыСотрудники.Ссылка = &ДокументСсылка");
	Запрос.УстановитьПараметр("СотрудникПустаяСсылка", Справочники.Сотрудники.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
	РезультатЗапросаПоСотрудникам = Запрос.Выполнить();
	
	ОбластьМакетаСтрока 		= Макет.ПолучитьОбласть("Строка");
	ОбластьШапкаТаблицы			= Макет.ПолучитьОбласть("ШапкаТаблицы"); 
	ОбластьМакетаИтогПоСтранице = Макет.ПолучитьОбласть("ИтогПоЛисту");
	ОбластьМакетаПодвал			= Макет.ПолучитьОбласть("Подвал"); 
	
	НомерСтраницы = 1;
	НомерСтраницыПред = НомерСтраницы;
	СтруктураИтоговПоСтранице = Новый Структура("Сумма", 0);
    
	Если НЕ РезультатЗапросаПоСотрудникам.Пустой() Тогда
		Выборка = РезультатЗапросаПоСотрудникам.Выбрать();
		ВсегоСтрокДокумента = Выборка.Количество();
		НомерСтроки = 1;
		Пока Выборка.Следующий() Цикл
			ОбластьМакетаСтрока.Параметры.Заполнить(Выборка);
            ОбластьМакетаСтрока.Параметры.Сумма = Формат(Выборка.Сумма,ФорматВыводаСуммы);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если НомерСтроки = Выборка.Количество() Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьМакетаПодвал);
			КонецЕсли;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаСтрока, ОбластьШапкаТаблицы, ОбластьМакетаИтогПоСтранице,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("Сумма", 0);
				НомерСтраницыПред = НомерСтраницы;
			КонецЕсли;
			
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(Выборка, СтруктураИтоговПоСтранице);
			
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
	дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	
	ОбластьМакетаПодвал.Параметры.КоличествоЛистов = НомерСтраницы;
	//Чтение значения для главбуха
	ОбластьМакетаПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	//Чтение значения для кассира
	Кассир = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Кассир);
	ОбластьМакетаПодвал.Параметры.Заполнить(Кассир);
	ОбластьМакетаПодвал.Параметры.Валюта = ВалютаДокумента;
	ОбластьМакетаПодвал.Параметры.Коп = Коп;
	ТабДокумент.Вывести(ОбластьМакетаПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьТ53()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли; 
	Если Основание=Неопределено Тогда Возврат; КонецЕсли;
	ДокументОснование=Основание;
	//Вычислим сумму
	Сотрудники.Очистить();
	//выберем всю дебиторскую задолженность по договорам, которые есть в начислении зп
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	ВзаиморасчетыКомпанииОстатки.Контрагент,
	                    |	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов,
	                    |	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК Сумма,
	                    |	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
	                    |	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток, 0) КАК СуммаБаз
	                    |ИЗ
	                    |	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
	                    |			&МоментВремени,
	                    |			ДоговорВзаиморасчетов В
	                    |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	                    |					НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов
	                    |				ИЗ
	                    |					Документ.НачислениеЗарплаты.Сотрудники КАК НачислениеЗарплатыСотрудники
	                    |				ГДЕ
	                    |					НачислениеЗарплатыСотрудники.Ссылка = &ДокументОснование)) КАК ВзаиморасчетыКомпанииОстатки
	                    |ГДЕ
	                    |	ВзаиморасчетыКомпанииОстатки.СуммаОстаток < 0");
	Запрос.УстановитьПараметр("ДокументОснование",ДокументОснование);
	МоментВремени = ?(ТекущаяДата() > ДокументОснование.Дата, ТекущаяДата(), КонецДня(ДокументОснование.Дата));
	Запрос.УстановитьПараметр("МоментВремени",МоментВремени);
	РезультатЗапросаПоСотрудникам = Запрос.Выполнить();
	Если НЕ РезультатЗапросаПоСотрудникам.Пустой() Тогда
		Выборка = РезультатЗапросаПоСотрудникам.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Сотрудники.Добавить();
			НоваяСтрока.Контрагент = Выборка.Контрагент;
			НоваяСтрока.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			ВалютаДоговора = Выборка.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			Если ВалютаДокумента=ВалютаДоговора Тогда
				НоваяСтрока.Сумма = -Выборка.Сумма;
			ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				НоваяСтрока.Сумма = -Выборка.СуммаБаз; 
			ИначеЕсли ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
				НоваяСтрока.Сумма = -Выборка.СуммаУпр; 
			Иначе
				СтруктураКурса = обКурсДляВалюты(ВалютаДоговора,ДокументОснование.Дата);
				Курс           = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
				НоваяСтрока.Сумма = обПересчет(-Выборка.Сумма,ВалютаДоговора,Курс,ВалютаДокумента,ДокументОснование.Дата);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	СуммаДокумента=обПересчет(Основание.СуммаДокумента,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,Дата);
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// БАЛАНС: определяем, нужно ли будет вводить корректирующие движения
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);	
	ПодразделениеКассаКомпании    = обПолучитьБалансовоеПодразделение(КассаКомпании.Подразделение);
	
	// расходуем деньги из кассы
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.ДокументОбъект     = ЭтотОбъект;
	НаборЗаписейДС.РежимПроведения    = Режим;
	НаборЗаписейДС.СтруктурнаяЕдиница = КассаКомпании;
	НаборЗаписейДС.Валюта             = Неопределено;
	НаборЗаписейДС.СтатьяДДС          = СтатьяДДС;
	НаборЗаписейДС.Сумма              = СуммаДокумента;
	Отказ=НЕ НаборЗаписейДС.Расход() ИЛИ Отказ;
	
	// Таблица для накопления суммовых разниц.
	ТаблицаСуммовыхРазниц = Новый ТаблицаЗначений();
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
	
	// Таблица для накопления сумм по взаиморасчетам.
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений();
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
	
	Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ВыплатаЗарплатыСотрудники.Контрагент КАК Контрагент,
		|	ВыплатаЗарплатыСотрудники.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ВыплатаЗарплатыСотрудники.Сумма КАК Сумма
		|ИЗ
		|	Документ.ВыплатаЗарплаты.Сотрудники КАК ВыплатаЗарплатыСотрудники
		|ГДЕ
		|	ВыплатаЗарплатыСотрудники.Ссылка = &ДокументСсылка
		|	
		|");
	Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
	РезультатЗапросаПоСотрудникам = Запрос.Выполнить();
	Если НЕ РезультатЗапросаПоСотрудникам.Пустой() Тогда
		НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
		Выборка = РезультатЗапросаПоСотрудникам.Выбрать();
		Пока Выборка.Следующий() И НЕ Отказ Цикл
			// Проводим взаиморасчеты.
			НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.РежимПроведения       = Режим;
			НаборЗаписейВзаиморасчеты.Контрагент            = Выборка.Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок    = Истина;
			Если (НЕ обЗначениеНеЗаполнено(ДокументОснование)) И (ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.НачислениеЗарплаты")) Тогда
				НаборЗаписейВзаиморасчеты.Сделка = ДокументОснование;
			Иначе
				НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
			КонецЕсли;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем        = Истина;
			НаборЗаписейВзаиморасчеты.Сумма                            = Выборка.Сумма;
			НаборЗаписейВзаиморасчеты.Валюта                           = Неопределено;
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
			Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			
			СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			
			СтрокаСуммовыхРазниц = ТаблицаСуммовыхРазниц.Добавить();
			Если БалансВедетсяПоПодразделениям Тогда
				СтрокаСуммовыхРазниц.Подразделение = Выборка.ДоговорВзаиморасчетов.Подразделение;
			Иначе
				СтрокаСуммовыхРазниц.Подразделение = ПодразделениеКомпании;
			КонецЕсли;
			СтрокаСуммовыхРазниц.Сумма = СуммаДоходаРасходаСуммовыхРазниц;
				
			СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
			СтрокаВзаиморасчетов.Подразделение = Выборка.ДоговорВзаиморасчетов.Подразделение;
			СтрокаВзаиморасчетов.Сумма         = Выборка.Сумма;              			
		КонецЦикла;
	КонецЕсли;
	
	// Доходы и расходы по суммовым разницам.
	ТаблицаСуммовыхРазниц.Свернуть("Подразделение","Сумма");
	Для Каждого СтрокаСуммовыхРазниц Из ТаблицаСуммовыхРазниц Цикл
		Если СтрокаСуммовыхРазниц.Сумма<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение          = СтрокаСуммовыхРазниц.Подразделение;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СтрокаСуммовыхРазниц.Сумма<0 Тогда
				НаборЗаписейДиР.Расход = -СтрокаСуммовыхРазниц.Сумма;
			Иначе
				НаборЗаписейДиР.Доход = СтрокаСуммовыхРазниц.Сумма;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	
	
	// Корректирующие движения.
	ТаблицаВзаиморасчетов.Свернуть("Подразделение","Сумма");
	Для Каждого СтрокаВзаиморасчетов Из ТаблицаВзаиморасчетов Цикл
		ПодразделениеВзаиморасчета     = обПолучитьБалансовоеПодразделение(СтрокаВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = ПодразделениеКассаКомпании<>ПодразделениеВзаиморасчета;
		Если БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны И СтрокаВзаиморасчетов.Сумма<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение          = СтрокаВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Ложь;
			НаборЗаписейДиР.Доход                  = СтрокаВзаиморасчетов.Сумма;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение          = КассаКомпании.Подразделение;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Ложь;
			НаборЗаписейДиР.Доход                  = СтрокаВзаиморасчетов.Сумма;
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	
	// Возможно расхождение баланса на "копейки" из-за округления. 
	// Возникшую разницу необходимо списать на доходы и расходы.	
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	//проведение по регистру ПлатежныйКалендарь
	// Собственно само проведение
	НаборЗаписей = Движения.ПлатежныйКалендарь;
	НаборЗаписей.ДокументОбъект = Ссылка;
	
	Отказ = НЕ НаборЗаписей.ФактВыплата() ИЛИ Отказ;
	
	// Выведем сообщения об ошибках и предупреждениях.
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
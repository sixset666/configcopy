// Модуль документа РабочийЛист

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/атрикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем СписокВидовКонтактов Экспорт;				// Список видов контактов
Перем СписокКомуПокупает Экспорт;				// Список назначения покупки
Перем СписокСКемПришел Экспорт;					// Список сопровождающих клиента
Перем СписокСроков Экспорт;						// Список сроков сделки
Перем СписокСтатусов Экспорт;					// Список статусов рабочего листа
Перем ВводИзАРМРесепшен;						// Переменная хранит флаг, был ли введен данный документ из АРМ Ресепшен

Перем ОборудованиеПроизводителя Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	                     
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("Менеджер",1);
	ОбязательныеРеквизиты.Вставить("РуководительМенеджера",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("Пол",1);
	ОбязательныеРеквизиты.Вставить("ПредставлениеТелефона",1);
	ОбязательныеРеквизиты.Вставить("ВидКонтакта",1);
	ОбязательныеРеквизиты.Вставить("Реклама",1);
	ОбязательныеРеквизиты.Вставить("ПредполагаемыйСрок",1);
	ОбязательныеРеквизиты.Вставить("ВидОплаты",1);
	ОбязательныеРеквизиты.Вставить("Статус",1);
	
	ОбязательныеОпции=Новый Структура();
	ОбязательныеОпции.Вставить("Опция",      3);
	ОбязательныеОпции.Вставить("Количество", 1);
	ОбязательныеРеквизиты.Вставить("Опции",  ОбязательныеОпции);
	
	ОбязательныеВозражения=Новый Структура();
	ОбязательныеВозражения.Вставить("Возражение", 3);
	ОбязательныеРеквизиты.Вставить("Возражения",  ОбязательныеВозражения);	
	
	ОбязательныеПользователи=Новый Структура();
	ОбязательныеПользователи.Вставить("Пользователь",      3);
	ОбязательныеРеквизиты.Вставить("Пользователи",  ОбязательныеПользователи);	
	
	ОбязательныеДругиеВарианты=Новый Структура();
	ОбязательныеДругиеВарианты.Вставить("Модель",      1);
	ОбязательныеРеквизиты.Вставить("ДругиеВарианты",  ОбязательныеДругиеВарианты);	
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Возврат Результат;	
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	//Документ считаем корректно заполненным, независимо от заполненности табличных частей
	Возврат Истина;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РабочийЛист","Рабочий лист",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Контрагент" Тогда
		Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
			Результат	= дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);			
			Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
				Если Контрагент.ФормаСобственности	= Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
					ЮридическоеЛицо	= Истина;
				Иначе
					ЮридическоеЛицо	= Ложь;
				КонецЕсли;
				
				Если ЮридическоеЛицо Тогда
					Пол	= Перечисления.ПолФизическихЛиц.НеУказан;
				ИначеЕсли НЕ обЗначениеНеЗаполнено(Контрагент.Пол) Тогда
					Пол	= Контрагент.Пол;
				КонецЕсли;
				
				Если обЗначениеНеЗаполнено(Реклама) Тогда
					Реклама	= Контрагент.РекламныйИсточник;
				КонецЕсли;
			КонецЕсли;						
		КонецЕсли;
		
		#Если Клиент Тогда
		КодРегиона = "";
		КодГорода = "";
		НомерТелефона = "";
		ВнутреннийНомер = "";
		ПредставлениеТелефона = "";
		ВидТелефона = Неопределено;
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Телефоны = киПолучитьСписокТелефоновОбъекта(Контрагент);
			Если Телефоны<>Неопределено И Телефоны.Количество()>0 Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект,Телефоны[0].Значение);
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		
		Возврат Результат;
		
	ИначеЕсли Имя="ТипЦен" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		// Определение цены автомобиля
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаПродажи = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаПродажи	= обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;			
		СуммаПродажи	= ЦенаПродажи*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Возврат Результат;
		
	ИначеЕсли Имя="ТипЦенЗакупки" Тогда
		// Определение цены автомобиля
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаЗакупки = обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаЗакупки	= обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;			
		СуммаЗакупки	= ЦенаЗакупки*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаЗакупки",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Результат;
		
	ИначеЕсли Имя="Автомобиль" Тогда
		Результат = Истина;
		
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("НеИзменятьЦенуАвтомобиля",Истина);
			ДопПараметры.Вставить("ЗаполнятьКомплектациюАвтомобиля",Ложь);
			Модель				= Автомобиль.Модель;
			ВариантКомплектации	= Автомобиль.ВариантКомплектации;
			Результат			= ОбработкаРеквизита("Модель", ТекСтрока, ЭтаФорма, ДопПараметры);
			Результат			= ОбработкаРеквизита("ВариантКомплектации", ТекСтрока, ЭтаФорма, ДопПараметры);
			ДопПараметры.Удалить("НеИзменятьЦенуАвтомобиля");
			Цвет				= Автомобиль.Цвет;
			Интерьер			= Автомобиль.ТипСалона;
			#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				ЗаполнитьКомплектациюАвтомобиля();
				Для Каждого СтрокаТЧ Из Опции Цикл
					Результат = ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
				КонецЦикла;
			КонецЕсли;
			#КонецЕсли
		КонецЕсли;
		
		// Определение цены автомобиля
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаЗакупки = обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаЗакупки	= обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи	= обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;			
		СуммаЗакупки	= ЦенаЗакупки*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаЗакупки",ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаПродажи	= ЦенаПродажи*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
		#Если Клиент Тогда
			дкСообщитьОЗаметках(ЭтотОбъект,ЭтотОбъект["Автомобиль"],ЭтаФорма);
		#КонецЕсли
		
		Возврат Результат;
		
	ИначеЕсли Имя="СуммаЗакупки" Тогда
		Результат = Истина;
		Маржа	= СуммаПродажи-СуммаЗакупки;
		Возврат Результат;
		
	ИначеЕсли Имя="СуммаПродажи" Тогда
		Результат = Истина;
		Маржа	= СуммаПродажи-СуммаЗакупки;
		Возврат Результат;
		
	ИначеЕсли Имя="Модель" Тогда
		
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) И Автомобиль.Модель<>Модель Тогда
			Автомобиль	= Справочники.Автомобили.ПустаяСсылка();
			Результат	= ОбработкаРеквизита("Автомобиль", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Если ВариантКомплектации.Владелец<>Модель Тогда
			ВариантКомплектации	= Справочники.ВариантыКомплектации.ПустаяСсылка();
			Результат			= ОбработкаРеквизита("ВариантКомплектации",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		// Определение цены автомобиля
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаЗакупки = обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаЗакупки	= обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи	= обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;			
		СуммаЗакупки	= ЦенаЗакупки*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаЗакупки",ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаПродажи	= ЦенаПродажи*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;		
		
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантКомплектации" Тогда
		Результат	= Истина;
		
		Если ДопПараметры = Неопределено Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		
		Если Автомобиль.ВариантКомплектации<>ВариантКомплектации Тогда
			Автомобиль	= Справочники.Автомобили.ПустаяСсылка();
			Результат	= ОбработкаРеквизита("Автомобиль", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
			Если ВариантКомплектации.Владелец<>Модель Тогда
				Модель		= ВариантКомплектации.Владелец;
				Результат	= ОбработкаРеквизита("Модель",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			КонецЕсли;
		КонецЕсли;
		
		// Определение цены автомобиля
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаЗакупки = обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаЗакупки	= обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи	= обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;			
		СуммаЗакупки	= ЦенаЗакупки*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаЗакупки",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		СуммаПродажи	= ЦенаПродажи*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;		
		
		#Если Клиент Тогда
		ЗаполнятьКомплектациюАвтомобиля = Истина;
		ДопПараметры.Свойство("ЗаполнятьКомплектациюАвтомобиля",ЗаполнятьКомплектациюАвтомобиля);
		Если ЗаполнятьКомплектациюАвтомобиля = Неопределено Тогда
			ЗаполнятьКомплектациюАвтомобиля = Истина;
		КонецЕсли;
		Если ЗаполнятьКомплектациюАвтомобиля И ЭтаФорма<>Неопределено Тогда
			ЗаполнитьКомплектациюАвтомобиля();
			Для Каждого СтрокаТЧ Из Опции Цикл
				Результат = ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
			КонецЦикла;
		КонецЕсли; 
		#КонецЕсли
	
		Возврат Результат;
		
	ИначеЕсли Имя="КоличествоАвтомобилей" Тогда
		
		// Определение цены автомобиля
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаЗакупки = обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаЗакупки	= обПолучитьЦену(ТипЦенЗакупки,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			ЦенаПродажи	= обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;			
		СуммаЗакупки	= ЦенаЗакупки*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаЗакупки",ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаПродажи	= ЦенаПродажи*КоличествоАвтомобилей;
		Результат		= ОбработкаРеквизита("СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;		
		Возврат Результат;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ДОПОЛНИТЕЛЬНОЕ ОБОРУДОВАНИЕ"
	ИначеЕсли Имя="Опции.Опция" Тогда
		Результат	= Истина;
		Если ЭтаФорма<>Неопределено Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда
				 Если ОборудованиеПроизводителя.Найти(ТекСтрока.Опция,"Опция")=Неопределено Тогда
					#Если Клиент Тогда
					Если ЭтаФорма<>Неопределено Тогда
						Сообщить("Опция <"+ТекСтрока.Опция+"> недопустима для данного варианта комплектации.");
					КонецЕсли; 
					#КонецЕсли
					ТекСтрока.Опция=Неопределено;
					Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
				 КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		
		// проверим а не набор ли у нас...
		Если НЕ ТекСтрока.Опция.ПризнакНабора Тогда
			// установим количество
			Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
			// Заполним ставку НДС
			ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
			Если ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			// попытаемся получить цену
			ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации,Опция",Модель,ВариантКомплектации,ТекСтрока.Опция),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			Результат = ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			// имеем дело с набором, разложим его...
			Набор=ТекСтрока.Опция.СоставНабора;
			КоличествоНабора=ТекСтрока.Количество;
			Если КоличествоНабора=0 Тогда
				КоличествоНабора=1;
			КонецЕсли;
			// удалим строку из табличной части
			Опции.Удалить(ТекСтрока);
			// теперь будем рекурсивно добавлять состав набора
			Для Каждого ОборудованиеИзНабора Из Набор Цикл
				СтрокаТЧ=Опции.Найти(ОборудованиеИзНабора.Опция,"Опция");
				Если СтрокаТЧ=Неопределено Тогда
					СтрокаТЧ=Опции.Добавить();
					СтрокаТЧ.Опция=ОборудованиеИзНабора.Опция;
					СтрокаТЧ.Количество=ОборудованиеИзНабора.Количество*КоличествоНабора;
					ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры);
					Если обЗначениеНеЗаполнено(СтрокаТЧ.Опция) Тогда
						Опции.Удалить(СтрокаТЧ);
					КонецЕсли; 
				Иначе
					СтрокаТЧ.Количество=СтрокаТЧ.Количество+(ОборудованиеИзНабора.Количество*КоличествоНабора);
					ОбработкаРеквизита("Опции.Количество",СтрокаТЧ,ЭтаФорма,ДопПараметры);
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
		Возврат Результат;
		
	ИначеЕсли Имя="Опции.Количество" Тогда
		Результат=ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.Цена" Тогда
		Результат	= Истина;
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Результат=ОбработкаРеквизита("Опции.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Результат;
		
	ИначеЕсли Имя="Опции.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		Результат=ОбработкаРеквизита("Опции.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Результат;
		
	ИначеЕсли Имя="Опции.СтавкаНДС" Тогда
		СуммаВсегоБезСкидки=ТекСтрока.Сумма;
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Расчет НДС
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаНДС=Окр((СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС=Окр(СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли;
		// В любом случаее идем в секцию "СуммаНДС", т.к. в ней будем произведен расчет суммы всего.
		Результат=ЭтотОбъект.ОбработкаРеквизита("Опции.СуммаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Результат;
		
	ИначеЕсли Имя="Опции.СуммаНДС" Тогда
		Результат	= Истина;
		СуммаВсегоБезСкидки=ТекСтрока.Сумма;
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаВсего=СуммаВсегоБезСкидки;
		Иначе
			ТекСтрока.СуммаВсего=СуммаВсегоБезСкидки+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Результат;
		
	ИначеЕсли Имя="Опции.СуммаВсего" Тогда
		Результат	= истина;
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма=Окр(ТекСтрока.СуммаВсего/(1/100),2);
		Иначе
			Сумма=ТекСтрока.СуммаВсего*100/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.Сумма=Окр(Сумма/(1/100),2);
		КонецЕсли;
		// Пересчитываем цену.
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		// Рассчитываем новую сумму НДС. 		
		СуммаВсегоБезСкидки=ТекСтрока.Сумма;
		Если ЦенаВключаетНДС Тогда			
			ТекСтрока.СуммаНДС=Окр((СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС=Окр(СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли;
		Возврат Результат;
	ИначеЕсли Имя="Менеджер" Тогда
		Результат	= Истина;
		РуководительМенеджера = Справочники.Сотрудники.ПустаяСсылка();	
		Если НЕ обЗначениеНеЗаполнено(Менеджер) И НЕ обЗначениеНеЗаполнено(Менеджер.Подразделение) Тогда
			НазначениеПодразделения	= ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Справочник_ПодразделенияКомпании;
			СвойствоРуководитель	= зфЗащищенныеФункцииСервер.РабочийЛистНайтиСоздатьСвойство(НазначениеПодразделения, "Руководитель");	
			РуководительМенеджера	= зфЗащищенныеФункцииСервер.РабочийЛистПолучитьЗначениеСвойстваОбъекта(Менеджер.Подразделение, СвойствоРуководитель);
		КонецЕсли;
		Возврат Результат;
	Иначе	
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;		
КонецФункции // ОбработкаРеквизита()

// Функция возвращает сотрудника на основании переданного пользователя
//
Функция ПолучитьСотрудникаПоПользователю(ТекущийПользователь) Экспорт
	ТекущийСотрудник	= Справочники.Сотрудники.ПустаяСсылка();
	
	Если НЕ обЗначениеНеЗаполнено(ТекущийПользователь) И НЕ ПустаяСтрока(ТекущийПользователь.Наименование) Тогда
		Если обЗначениеНеЗаполнено(ТекущийПользователь.Сотрудник) Тогда
			ТекущийСотрудник	= Справочники.Сотрудники.НайтиПоНаименованию(ТекущийПользователь.Наименование, Истина);
			Если обЗначениеНеЗаполнено(ТекущийСотрудник) Тогда
				// Создадим нового сотрудника
				СотрудникОбъект	= Справочники.Сотрудники.СоздатьЭлемент();
				СотрудникОбъект.УстановитьНовыйКод();
				СотрудникОбъект.Наименование	= ТекущийПользователь.Наименование;
				СотрудникОбъект.Организация= ПараметрыСеанса.Организация;
				СотрудникОбъект.Подразделение	= ПараметрыСеанса.ПодразделениеКомпании;
				СотрудникОбъект.Должность		= Справочники.Должности.Неопределена;
				Попытка
					СотрудникОбъект.Записать();
					ТекущийСотрудник	= СотрудникОбъект.Ссылка;
				Исключение КонецПопытки;
			КонецЕсли;
		Иначе
			ТекущийСотрудник	= ТекущийПользователь.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущийСотрудник;
КонецФункции

#Если Клиент Тогда

// Функция возвращает данные о контактах с клиентом
//
Функция ПолучитьДанныеОКонтактах(Режим=1, ТолькоОднаЗапись=Истина,ВидСобытия=Неопределено) Экспорт
	Возврат зфРабочийЛистПолучитьДанныеОКонтактах(ЭтотОбъект,Режим,ТолькоОднаЗапись,ВидСобытия);
КонецФункции
	
// Расчет общей балансовой стоимости активов
//
// Возвращаемое значение:
//  СуммаВсего - число - итог по колонке "БалансоваяСтоимость".
//
Функция РассчитатьСуммуВсего() Экспорт
	// Если выбран авто - то “Сумма продажи” включает опции,
	// если выбрана “Модель+Вариант комплектации” - то “Сумма продажи” не включает сумму опций
	Если (НЕ обЗначениеНеЗаполнено(Модель)) И (НЕ обЗначениеНеЗаполнено(ВариантКомплектации)) Тогда
		СуммаВсего = СуммаПродажи+Опции.Итог("СуммаВсего");
	Иначе
		СуммаВсего = СуммаПродажи;
	КонецЕсли;
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Выводит сумму по табличной части документа
//
// Параметры:
// ЭтаФорма - форма, форма документа
//
Процедура ВывестиЗаголовокСуммаДокумента(ЭтаФорма) Экспорт
	
	Попытка
		ВремСумма = РассчитатьСуммуВсего();
	Исключение
		ВремСумма	= 0;
	КонецПопытки;
		
	// обновим надпись валюты и суммы документа
	ТекстВалюта = "";
	Попытка	
		ТекстВалюта = "Валюта: " + СокрЛП(ЭтаФорма.ВалютаДокумента) + " (" + Формат(ЭтаФорма.КурсДокумента,"ЧДЦ = 4") + ")";
		Попытка
			Если ВремСумма <> Неопределено Тогда
				ТекстСумма = " ИТОГО: " + Формат(ВремСумма,"ЧДЦ = 2; ЧН = 0,00");
			КонецЕсли; 
		Исключение
		КонецПопытки;
		ЭтаФорма.ЭлементыФормы.тСуммаДокумента.Заголовок = ТекстВалюта + ТекстСумма;
	Исключение 
	КонецПопытки;
	
	// здесь же установим подписи количества строк в заголовках страниц основной панели
	ТекстЦена = ""; ТекстСкидка = "";
	Попытка
		ТекстЦена = ?(обЗначениеНеЗаполнено(ЭтаФорма.ТипЦен)," < не указан > ",СокрЛП(ЭтаФорма.ТипЦен.Наименование));
	Исключение КонецПопытки;
	ТекстСкидка = "";
	
	Мет = ЭтаФорма.ЭтотОбъект.Метаданные();
	Для Каждого ТаблЧасть Из Мет.ТабличныеЧасти Цикл
		Попытка ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы[ТаблЧасть.Имя].Заголовок = СокрЛП(ТаблЧасть.Синоним) + " (" + СокрЛП(ЭтаФорма[ТаблЧасть.Имя].Количество()) + " поз.)"; Исключение КонецПопытки;
		// и тут же попытаемся установить текст подвала для колоники "Номенклатура"
		Попытка
			ЭтаФорма.ЭлементыФормы[ТаблЧасть.Имя].Колонки.Цена.ТекстПодвала = ТекстЦена;
		Исключение КонецПопытки;
		Попытка
			ЭтаФорма.ЭлементыФормы[ТаблЧасть.Имя].Колонки.Номенклатура.ТекстПодвала = ?(ПустаяСтрока(ТекстСкидка),"",ТекстСкидка);
		Исключение КонецПопытки;
	КонецЦикла; 
КонецПроцедуры // ВывестиЗаголовокСуммаДокумента()

#Если Клиент Тогда
// Проверить таблицу комплектации по автомобилю
// неподходящие опции и оборудование удаляем
Процедура ПроверитьКомплектациюАвтомобиля(ЭтаФорма = Неопределено) Экспорт
	// таблица комплектации
	Перем табКомплектация;
	
	Если Не ЗначениеЗаполнено(Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	// получим таблицу комплектации
	НайденыйАвтомобильОбъект = Автомобиль.ПолучитьОбъект();
	Рез = НайденыйАвтомобильОбъект.ПолучитьОпцииИОборудованиеАвтомобиля(табКомплектация);
	
	//если нет данных выйдем
	//Если НЕ Рез Тогда
	//	Возврат;
	//КонецЕсли;
	
	Флаг = Ложь;
	ТЗОпции = Опции.Выгрузить();
	
	Если Рез Тогда
		Для Каждого ТекСТрока Из Опции Цикл
			Строка = табКомплектация.Найти(ТекСТрока.Опция, "Номенклатура");
			Если Строка = Неопределено Тогда
				Флаг = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ТекКомплектация Из табКомплектация Цикл
		Если ТипЗнч(ТекКомплектация.Номенклатура) = Тип("СправочникСсылка.Опции") Тогда
			СтрокаОпции = ТЗОпции.Найти(ТекКомплектация.Номенклатура, "Опция");
			Если СтрокаОпции = Неопределено ИЛИ Не (ТекКомплектация.КоличествоОстаток = СтрокаОпции.Количество) Тогда
				Флаг = Истина;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Флаг Тогда 
		Ответ = Вопрос("В Заказе на автомобиль табличная часть ""Опции"" не соответствует комплектации автомобиля.
		|Перезаполнить согласно комплектации?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Опции.Очистить();
			Для Каждого ТекКомплектация Из табКомплектация Цикл
				Если ТипЗнч(ТекКомплектация.Номенклатура) = Тип("СправочникСсылка.Опции") Тогда
					НоваяСтрока = Опции.Добавить();
					НоваяСтрока.Опция = ТекКомплектация.Номенклатура;
					ОбработкаРеквизита("Опции.Опция", НоваяСтрока);
					//НоваяСтрока.Количество = ТекКомплектация.КоличествоОстаток;
					//НоваяСтрока.СуммаВсего = обПересчет(ТекКомплектация.СуммаУпрОстаток, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), Дата, КурсВалютыУпр),ВалютаДокумента,Дата,РежимОкругления.Окр15как20);
					//ОбработкаРеквизита("Опции.СуммаВсего", НоваяСтрока);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
	
КонецПроцедуры
#КонецЕсли

#Если Клиент Тогда
Процедура ПолучитьОборудованиеАвтомобиля(ЭтаФорма=Неопределено) Экспорт
	
	// Оборудование производителя
	Запрос = Новый Запрос;
	Если обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
		ЗапросТекст = "ВЫБРАТЬ
					  |	МоделиОпции.Опция КАК Опция,
					  |	1 КАК Количество
					  |ИЗ
					  |	Справочник.Модели.Опции КАК МоделиОпции
					  |ГДЕ
					  |	МоделиОпции.Ссылка = &Модель
					  |УПОРЯДОЧИТЬ ПО
					  |	Опция";
		Запрос.УстановитьПараметр("Модель",Модель);
	Иначе
		ЗапросТекст = "ВЫБРАТЬ
					  |	ОпцииВариантовКомплектации.Опция КАК Опция,
					  |	ОпцииВариантовКомплектации.Количество КАК Количество
					  |ИЗ
					  |	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
					  |ГДЕ
					  |	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
					  |	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения
					  |УПОРЯДОЧИТЬ ПО
					  |	Опция";
		Запрос.УстановитьПараметр("ВариантКомплектации",ВариантКомплектации);
		Запрос.УстановитьПараметр("ВидВключения",1);
	КонецЕсли; 
	Запрос.Текст = ЗапросТекст;
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() > 0 Тогда
		ОборудованиеПроизводителя = РезультатЗапроса;	
	Иначе
		ОборудованиеПроизводителя = Новый ТаблицаЗначений;
		ОборудованиеПроизводителя.Колонки.Добавить("Опция", Новый ОписаниеТипов("СправочникСсылка.Опции"));
	КонецЕсли;
	
	//неподходящие опции удаляем
	Если ОборудованиеПроизводителя.Количество()=0 Тогда
		Опции.Очистить();
	Иначе
		КоличествоСтрок = Опции.Количество()-1;
		Пока КоличествоСтрок>=0 Цикл
			ТекОпция = Опции[КоличествоСтрок];
			Если ОборудованиеПроизводителя.Найти(ТекОпция.Опция,"Опция")=Неопределено Тогда
				#Если Клиент Тогда
				Сообщить("Опция <"+ТекОпция.Опция+"> недопустима для данного варианта комплектации.");
				#КонецЕсли
				Опции.Удалить(ТекОпция);
			КонецЕсли;
			КоличествоСтрок = КоличествоСтрок-1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
#КонецЕсли

//Процедура формирует документы по табличной части шаблонов нового статуса рабочего листа 
//
Процедура СформироватьДокументыПоШаблонамСтатуса() Экспорт
	ОбработкаЗаполненияИзШаблона = Обработки.ВыгрузкаЗагрузкаШаблоновДокументов.Создать();
	
	ТаблицаШаблонов = Статус.ШаблоныСобытий.Выгрузить(,"Сдвиг,ШаблонСобытия");
	Если НЕ обПраво("АвтоматическоеСозданиеДокументовСобытие") И ТаблицаШаблонов.Количество() > 0 Тогда
		//Отредактируем состав создаваемых документов и их сдвиг
		Форма = ПолучитьФорму("ФормаВыбораСоставаСобытий");
		Форма.ШаблоныСобытий = ТаблицаШаблонов;
		ТаблицаШаблонов = Форма.ОткрытьМодально();
	КонецЕсли;
	
	Если ТипЗнч(ТаблицаШаблонов) <> Тип("Неопределено") Тогда
		Для Каждого СтрокаШаблона Из ТаблицаШаблонов Цикл
			НаборДанных = РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей();
			НаборДанных.Отбор.Идентификатор.Установить(СтрокаШаблона.ШаблонСобытия);
			НаборДанных.Прочитать();
			
			Для Каждого ЗаписьНабора Из НаборДанных Цикл
				ПараметрыИмени = Новый Структура("Объект,Идентификатор", ЗаписьНабора.Объект, ЗаписьНабора.Идентификатор);	
				Документ = ОбработкаЗаполненияИзШаблона.СоздатьИзШаблона(ПараметрыИмени);
				
				// Для создания документов на основании рабочего листа требуется заполнение документа-основания
				// будем использовать флаг отказа от создания
				ВыводимоеСообщение = "";
				ОтказОтСоздания = Ложь;
				
				//Заполнение документа
				Документ.ДокументОснование = ЭтотОбъект.Ссылка;
				Если ТипЗнч(Документ) = Тип("ДокументОбъект.Событие") Тогда
					Документ.Дата = ТекущаяДата();
					Если НЕ ЗначениеЗаполнено(Документ.Тема) Тогда
						Документ.Тема = Строка(ЭтотОбъект);
					КонецЕсли;
					Документ.Контрагент = ЭтотОбъект.Контрагент;
					Документ.ОбработкаРеквизита("Контрагент");
					Документ.Автор = ЭтотОбъект.Автор;
					Документ.ДатаНачала = ТекущаяДата() + СтрокаШаблона.Сдвиг * 3600 * 24;
					Документ.ДатаОкончания = Документ.ДатаНачала + Документ.Продолжительность * 3600;
					Документ.Менеджер = ЭтотОбъект.Менеджер;
					
					//Для некоторых документов возожность проведения отключена, отследим это
					РежимЗаписи = РежимЗаписиДокумента.Проведение;
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.ЖалобаКлиента") Тогда
					Документ.Дата = ТекущаяДата();
					Документ.Автор = ЭтотОбъект.Автор;
					Документ.Контрагент = ЭтотОбъект.Контрагент;
					Документ.ОбработкаРеквизита("Контрагент");
					Документ.Ответственный = ЭтотОбъект.Менеджер;
					
					Если ТипЗнч(ЭтотОбъект.Контрагент) = Тип("Строка") Тогда
						ВыводимоеСообщение = "Невозможно создать жалобу клиента, так как контрагент Рабочего листа не присутствует в справочнике";
						ОтказОтСоздания = Истина;
					КонецЕсли;
					//Документ.Договор = ЭтотОбъект.Договор;
					
					РежимЗаписи = РежимЗаписиДокумента.Запись;
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.SMS") Тогда
					/////Заполнение документа SMS
					НоваяСтрока = Документ.Получатели.Добавить();
					НоваяСтрока.Контрагент = ЭтотОбъект.Контрагент;
					НоваяСтрока.ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
					НоваяСтрока.КонтактнаяИнформация = кмПолучитьОсновнойМобильныйТелефонОбъекта(НоваяСтрока.Контрагент);
					НоваяСтрока.НомерТелефона = НоваяСтрока.КонтактнаяИнформация;		
					Документ.НачалоОтправки = ТекущаяДата() + СтрокаШаблона.Сдвиг * 3600 * 24;
					Документ.Автор = ЭтотОбъект.Автор;
					
					Если ТипЗнч(ЭтотОбъект.Контрагент) = Тип("Строка") Тогда 
						ВыводимоеСообщение = "Невозможно отправить SMS, так как контрагент Рабочего листа не присутствует в справочнике";
					ИначеЕсли НЕ ЗначениеЗаполнено(НоваяСтрока.НомерТелефона) Тогда
						ВыводимоеСообщение = "Невозможно отправитт SMS, так как у контрагента не заполнен Номер телефона";
					Иначе
						Попытка 
							Документ.Записать(РежимЗаписиДокумента.Запись);
							Документ.ОтправитьSMS();
						Исключение 
							Сообщить(ОписаниеОшибки());
						КонецПопытки;
					КонецЕсли;
					
					РежимЗаписи = РежимЗаписиДокумента.Запись;
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.ЭлектронноеПисьмо") Тогда
					//Заполнение Электронного пиьма
					Если НЕ ЗначениеЗаполнено(Документ.Тема) Тогда
						Документ.Тема = Строка(ЭтотОбъект);
					КонецЕсли;
					НаборЗаписейКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
					НаборЗаписейКИ.Отбор.Объект.Установить(ЭтотОбъект.Контрагент);
					НаборЗаписейКИ.Отбор.Тип.Установить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
					НаборЗаписейКИ.Отбор.ЗначениеПоУмолчанию.Установить(Истина);
					НаборЗаписейКИ.Прочитать();
					
					//Заполним получателей Электронного письма
					ЗаполненАдресЭП = Истина;
					Для Каждого ЗаписьКИ Из НаборЗаписейКИ Цикл
						СтрокаПолучатель = Документ.Получатели.Добавить();
						СтрокаПолучатель.АдресЭлектроннойПочты = ЗаписьКИ.Представление;
						Если НЕ ЗначениеЗаполнено(СтрокаПолучатель.АдресЭлектроннойПочты) Тогда
							ЗаполненАдресЭП = Ложь;
						КонецЕсли;
						СтрокаПолучатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
						СтрокаПолучатель.Представление = ЭтотОбъект.Контрагент.Наименование;
					КонецЦикла;
					Документ.УчетнаяЗапись = ЭпПолучитьУчетнуюЗаписьПоУмолчанию();
					
					Если НЕ ЗначениеЗаполнено(Документ.УчетнаяЗапись) Тогда
						ВыводимоеСообщение = "Невозможно отправить Электронное письмо, не заполнена учетная запись";
					ИначеЕсли НЕ ЗаполненАдресЭП Тогда
						ВыводимоеСообщение = "Невозможно отправить электронное письмо, у получателя не заполнен адрес электронной почты";
					Иначе
						Попытка
							Документ.Записать(РежимЗаписиДокумента.Запись);
							СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
							СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(Документ.УчетнаяЗапись, СтруктураРежимовОбмена);
							Если СоединениеСПочтовымСервером <> Неопределено Тогда
								Если эпОтправитьПисьмо(ЭтотОбъект, СоединениеСПочтовымСервером) Тогда
									эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
								Иначе
									эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
								КонецЕсли;	
							КонецЕсли;
						Исключение
							Сообщить(ОписаниеОшибки());
						КонецПопытки;
					КонецЕсли;
					
					РежимЗаписи = РежимЗаписиДокумента.Запись;
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.Опрос") Тогда
					Документ.Дата = ТекущаяДата() + 3600*24*СтрокаШаблона.Сдвиг;
					Документ.ОпрашиваемоеЛицо = Контрагент;
					Документ.Автор = ЭтотОбъект.Автор;
					
					РежимЗаписи = РежимЗаписиДокумента.Запись;
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.ТестДрайв") Тогда
					Документ.Автомобиль = ЭтотОбъект.Автомобиль;
					Разность = Документ.ДатаОкончания - Документ.ДатаНачала;
					Документ.ДатаНачала = ТекущаяДата() + 3600*24*СтрокаШаблона.Сдвиг;
					Документ.ДатаОкончания = Документ.ДатаНачала + Разность;
					Документ.Менеджер = ЭтотОбъект.Менеджер;
					
					РежимЗаписи = РежимЗаписиДокумента.Запись;
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.ЗаказНаАвтомобиль") Тогда
					//Заполнение заказа
					
					Документ.Заполнить(ЭтотОбъект.Ссылка);
					Документ.СрокПоставки = ТекущаяДата() + СтрокаШаблона.Сдвиг * 3600 * 24;
					
					Запрос = Новый Запрос("ВЫБРАТЬ
					|	ОстаткиАвтомобилейОстатки.Автомобиль
					|ИЗ
					|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&Период, Автомобиль = &Автомобиль) КАК ОстаткиАвтомобилейОстатки");
					Запрос.УстановитьПараметр("Период", ТекущаяДата());
					Запрос.УстановитьПараметр("Автомобиль", ЭтотОбъект.Автомобиль);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Документ.Резервировать = Истина
					Иначе
						Документ.Резервировать = Ложь;
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Автомобиль) Тогда
						РежимЗаписи = РежимЗаписиДокумента.Запись;
						ВыводимоеСообщение = "Автомобиль не заполнен, проведение заказа невозможно";
					Иначе
						РежимЗаписи = РежимЗаписиДокумента.Проведение;
					КонецЕсли;	
					
				ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.РеализацияАвтомобилей") Тогда
					//Заполнение реализации
					Документ.Заполнить(Неопределено);
					Документ.Автомобили.Очистить();
					Документ.Контрагент = ЭтотОбъект.Контрагент;
					Документ.ОбработкаРеквизита("Контрагент");
					Если ЗначениеЗаполнено(ЭтотОбъект.Автомобиль) Тогда
						СтрокаАвто = Документ.Автомобили.Добавить();
						СтрокаАвто.Автомобиль = ЭтотОбъект.Автомобиль;
						СтрокаАвто.ИдентификаторАвтомобиля = Новый УникальныйИдентификатор;
						Документ.ОбработкаРеквизита("Автомобили.Автомобиль", СтрокаАвто);
						РежимЗаписи = РежимЗаписиДокумента.Проведение;
						
						Запрос = Новый Запрос("ВЫБРАТЬ
						|	ЗаказыАвтомобилейОстатки.Заказ,
						|	ЗаказыАвтомобилейОстатки.Заказ.ДоговорВзаиморасчетов КАК Договор
						|ИЗ
						|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(&Период, Автомобиль = &Автомобиль) КАК ЗаказыАвтомобилейОстатки");
						Запрос.УстановитьПараметр("Автомобиль", ЭтотОбъект.Автомобиль);
						Запрос.УстановитьПараметр("Период", ТекущаяДата());
						Выборка = Запрос.Выполнить().Выбрать();
						Если Выборка.Следующий() Тогда
							СтрокаАвто.ЗаказНаАвтомобиль = Выборка.Заказ;
							Документ.ОбработкаРеквизита("Автомобили.ЗаказНаАвтомобиль", СтрокаАвто);
							Документ.ДоговорВзаиморасчетов = Выборка.Договор;
							Документ.ОбработкаРеквизита("ДоговорВзаиморасчетов");
						Иначе
							СтрокаАвто.АвтомобильБезЗаказа = Истина;
							Документ.ОбработкаРеквизита("Автомобили.АвтомобильБезЗаказа", СтрокаАвто);
						КонецЕсли;
					Иначе
						ВыводимоеСообщение = "Автомобиль не заполнен, проведение реализации невозможно";
						РежимЗаписи = РежимЗаписиДокумента.Запись;
					КонецЕсли;
				Иначе
					ОтказОтСоздания = Истина;
					ВыводимоеСообщение = "Документ вида " + Строка(ТипЗнч(Документ)) + " нельзя планировать в статусах рабочего листа. Документ не создан.";  
				КонецЕсли;                             
				
				//ЗаписьДокумента
				Если ЗначениеЗаполнено(ВыводимоеСообщение) Тогда
					#Если Клиент Тогда
						Сообщить(ВыводимоеСообщение);
					#КонецЕсли
				КонецЕсли;
				
				Если НЕ ОтказОтСоздания Тогда
					Попытка
						Документ.Записать(РежимЗаписи);
						#Если Клиент Тогда
							Сообщить("Создан документ " + Строка(Документ));
						#КонецЕсли
					Исключение
						#Если Клиент Тогда
							//Сообщить(ОписаниеОшибки());
						#КонецЕсли
						Попытка
							Документ.Записать(РежимЗаписиДокумента.Запись);
							#Если Клиент Тогда
								Сообщить(Строка(Документ) + " записан");
							#КонецЕсли
						Исключение
							Сообщить(Строка(Документ) + " не был записан! Данный вариант события невозможно выполнить.");
						КонецПопытки;
					КонецПопытки;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры //СформироватьДокументыПоШаблонамСтатуса()

//Функция предлагает пользователю отредактировать состав создаваемых событий и свиг каждого из них
//
Функция ПолучитьПараметрыСозданияСобытий(НаборДанныхШаблона)
	//Выбор настроек
	
	Возврат НаборДанныхШаблона;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "РабочийЛист"
// Возвращает сформированный табличный документ:
Функция ПечатьРабочийЛист(ТабДокумент) Экспорт
	// Предварительные обработки данных
	
	//валюта печати	
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	КоэффициентПересчета = обПересчет(1, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента, ВалютаПечатногоДокумента, ЭтотОбъект.Дата);
	
	// приступим к печати
	Макет = ПолучитьМакет("РабочийЛист");
	
	//зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	//ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//получим адрес электронной почты
	Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") И НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		НаборЗаписей = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей(); 	
		НаборЗаписей.Отбор.Объект.Значение      = Контрагент;
		НаборЗаписей.Отбор.Объект.Использование = Истина;
		НаборЗаписей.Прочитать();
		
		ТаблицаКИ	= НаборЗаписей.Выгрузить();
		МассивОсновныхТелефонов=ТаблицаКИ.НайтиСтроки(Новый Структура("Объект,Тип",Контрагент,Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты));
		Если МассивОсновныхТелефонов.Количество()>0 Тогда
			АдресЭП = МассивОсновныхТелефонов[0].Представление;
			Для Каждого ТекСтрока Из МассивОсновныхТелефонов Цикл
				Если ТекСтрока.ЗначениеПоУмолчанию Тогда
					АдресЭП = ТекСтрока.Представление;	
				КонецЕсли;
			КонецЦикла
		КонецЕсли;
	КонецЕсли;
	
	//вывод заголовка документа
	ОбластьМакета							= Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ВалютаДокумента	= ВалютаПечатногоДокумента;
	ОбластьМакета.Параметры.ТекстЗаголовка	= дкПолучитьПредставление(ЭтотОбъект);
	
	//фирма, адрес и телефон
	ОбластьМакета.Параметры.ФирмаПолноеНаименование = спПолучитьНаименование(Организация);
	ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.ФирмаАдресЮридический)) Тогда
		ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	КонецЕсли;
	ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.ФирмаТелефоны)) Тогда
		ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	КонецЕсли;	

	//Клиент
	ОбластьМакета.Параметры.Менеджер				= Строка(Менеджер);
	ОбластьМакета.Параметры.ДатаДокумента			= Формат(Дата, "ДЛФ=DD");
	ОбластьМакета.Параметры.ПредполагаемыйСрок		= ?(обЗначениеНеЗаполнено(ПредполагаемыйСрок),"неопределен",ПредполагаемыйСрок);
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОбластьМакета.Параметры.НаименованиеКлиента		= Контрагент.Наименование;
	ИначеЕсли ТипЗнч(Контрагент) = Тип("Строка") Тогда
		ОбластьМакета.Параметры.НаименованиеКлиента		= Контрагент;
	КонецЕсли;
	ОбластьМакета.Параметры.КонтактныйТелефон		= ПредставлениеТелефона;
	ОбластьМакета.Параметры.ЭлектроннаяПочта		= АдресЭП;
	ОбластьМакета.Параметры.ПредыдущееАвто 			= ПредыдущийАвтомобиль;
	
	//Автомобиль
	ОбластьМакета.Параметры.Заполнить(ВариантКомплектации);	
	ОбластьМакета.Параметры.Модель					= спПолучитьНаименование(Модель);
	ОбластьМакета.Параметры.ВариантКомплектации 	= спПолучитьНаименование(ВариантКомплектации);
	ОбластьМакета.Параметры.Цвет					= спПолучитьНаименование(Цвет);
	ОбластьМакета.Параметры.Интерьер				= спПолучитьНаименование(Интерьер);
	
	//ОбластьМакета.Параметры.СуммаПродажи			= Формат(СуммаПродажи * КоэффициентПересчета, ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОтображатьСуммуЗакупкиВРабочемЛисте	= обПраво("ОтображатьСуммуЗакупкиВРабочемЛисте",Права);
	Если ОтображатьСуммуЗакупкиВРабочемЛисте Тогда
		ОбластьМакета.Параметры.СуммаЗакупки		= Формат(СуммаЗакупки * КоэффициентПересчета, ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	КонецЕсли;
	
	//базовое оборудование автомобиля
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОпцииВариантовКомплектации.Опция КАК Опция,
	|	ОпцииВариантовКомплектации.Количество КАК Количество
	|ИЗ
	|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
	|ГДЕ
	|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
	|	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("ВидВключения", 2);
	
	Выборка = Запрос.Выполнить().Выбрать();	СтрокаДопОборудования = "";
	Пока Выборка.Следующий() Цикл
		СтрокаДопОборудования = СтрокаДопОборудования + спПолучитьНаименование(Выборка.Опция) + ?(Выборка.Количество = 0,"","("+Выборка.Количество+")") + " ,";
	КонецЦикла;
	ОбластьМакета.Параметры.СписокБазовогоОборудования = Лев(СтрокаДопОборудования, СтрДлина(СтрокаДопОборудования) - 2);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим представления и расшифровки подписей
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.Автомасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "РабочийЛистЗвонка"
// Возвращает сформированный табличный документ:
Функция ПечатьРабочийЛистЗвонка(ТабДокумент) Экспорт
		
	// приступим к печати
	Макет = ПолучитьМакет("РабочийЛистЗвонка");
		
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета							= Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.ДатаДокумента				= Формат(Дата, "ДЛФ=DDT");
	ДатаЧас	= Час(Дата);
	Если ДатаЧас>=12 И ДатаЧас<18 Тогда
		ВремяСуток	= "Добрый день!";
	ИначеЕсли ДатаЧас>=18 И ДатаЧас<24 Тогда
		ВремяСуток	= "Добрый вечер!";
	Иначе
		ВремяСуток	= "Доброе утро!";
	КонецЕсли;	
	ОбластьМакета.Параметры.ВремяСуток				= ВремяСуток;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим представления и расшифровки подписей
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "РабочийЛистВстречи"
// Возвращает сформированный табличный документ:
Функция ПечатьРабочийЛистВстречи(ТабДокумент) Экспорт
		
	// приступим к печати
	Макет = ПолучитьМакет("РабочийЛистВстречи");
		
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета							= Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.ДатаДокумента				= Формат(Дата, "ДЛФ=DD");
	ОбластьМакета.Параметры.ДатаВремя					= Формат(Дата, "ДЛФ=T");

	// Клиент
	Если обЗначениеНеЗаполнено(Контрагент) Тогда
		ОбластьМакета.Параметры.КонтрагентФамилия	= "";	
	Иначе
		Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			ОбластьМакета.Параметры.КонтрагентИмя		= Контрагент.Имя;
			ОбластьМакета.Параметры.КонтрагентФамилия	= Контрагент.Фамилия;
			ОбластьМакета.Параметры.КонтрагентОтчество	= Контрагент.Отчество;
			ОбластьМакета.Параметры.КонтрагентАдрес		= спПолучитьПредставление(Контрагент, Новый Структура("АдресФактический",""));
			ОбластьМакета.Параметры.КонтрагентТелефон	= спПолучитьПредставление(Контрагент, Новый Структура("ТелефонРабочий",""));
		ИначеЕсли ТипЗнч(Контрагент) = Тип("Строка") Тогда
			ОбластьМакета.Параметры.КонтрагентФамилия	= Контрагент;
		КонецЕсли;
	КонецЕсли;
	
	// Автомобиль
	ОбластьМакета.Параметры.Заполнить(ВариантКомплектации);	
	ОбластьМакета.Параметры.Модель					= спПолучитьНаименование(Модель);
	ОбластьМакета.Параметры.Цвет					= спПолучитьНаименование(Цвет);
	ОбластьМакета.Параметры.АвтомобильТипКузова		= спПолучитьНаименование(ВариантКомплектации.ТипКузова);
	ОбластьМакета.Параметры.АвтомобильТипДвигателя	= спПолучитьНаименование(ВариантКомплектации.ТипДвигателя);
	ОбластьМакета.Параметры.АвтомобильТипКПП		= спПолучитьНаименование(ВариантКомплектации.ТипКПП);
	
	Если НЕ обЗначениеНеЗаполнено(ВариантКомплектации.ТипДвигателя) И
		НЕ обЗначениеНеЗаполнено(ВариантКомплектации.ТипДвигателя.ОбъемДвигателя) Тогда
		ОбластьМакета.Параметры.АвтомобильДвигательОбъем	= Строка(ВариантКомплектации.ТипДвигателя.ОбъемДвигателя);
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим представления и расшифровки подписей
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "КоммерческоеПредложение"
// Возвращает сформированный табличный документ:
Функция ПечатьКоммерческоеПредложение(ТабДокумент) Экспорт
	// Предварительные обработки данных
	
	//валюта печати	
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	КоэффициентПересчета = обПересчет(1, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента, ВалютаПечатногоДокумента, ЭтотОбъект.Дата);
	
	// приступим к печати
	Макет = ПолучитьМакет("КоммерческоеПредложение");
	
	//для начала настроим макет
	ОбластьТовар = Макет.Область("Товар");
	
	//определяем есть ли скидки
	ОбластьСкидка = Макет.Область("Скидка");
	ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьСкидка.ШиринаКолонки;
	Макет.УдалитьОбласть(ОбластьСкидка,ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	ЕстьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,,ЭтотОбъект);
	//если код не выводим
	Если НЕ ЕстьКод Тогда
		ОбластьКод = Макет.Область("Код");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);		
	КонецЕсли;
			
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
	ТекстЗаголовка							= "Коммерческое предложение"; //дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка	= ТекстЗаголовка;
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(Организация);

	//контрагент
	ОбластьМакета.Параметры.КонтрагентПредставление	= спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредполагаемыйСрок		= ?(обЗначениеНеЗаполнено(ПредполагаемыйСрок),"неопределен",ПредполагаемыйСрок);
	
	//автомобиль
	ОбластьМакета.Параметры.Заполнить(ВариантКомплектации);	
	ОбластьМакета.Параметры.Модель					= спПолучитьНаименование(Модель);
	ОбластьМакета.Параметры.ВариантКомплектации 	= спПолучитьНаименование(ВариантКомплектации);
	ОбластьМакета.Параметры.Цвет					= спПолучитьНаименование(Цвет);
	ОбластьМакета.Параметры.АвтомобильТипКузова		= спПолучитьНаименование(ВариантКомплектации.ТипКузова);
	ОбластьМакета.Параметры.АвтомобильТипДвигателя	= спПолучитьНаименование(ВариантКомплектации.ТипДвигателя);
	ОбластьМакета.Параметры.АвтомобильТипКПП		= спПолучитьНаименование(ВариантКомплектации.ТипКПП);
	ОбластьМакета.Параметры.Интерьер				= спПолучитьНаименование(Интерьер);
	
	ОбластьМакета.Параметры.СуммаПродажи			= Формат(СуммаПродажи * КоэффициентПересчета, ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	
	//базовое оборудование автомобиля
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОпцииВариантовКомплектации.Опция КАК Опция,
	|	ОпцииВариантовКомплектации.Количество КАК Количество
	|ИЗ
	|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
	|ГДЕ
	|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
	|	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("ВидВключения", 2);
	
	Выборка = Запрос.Выполнить().Выбрать();	СтрокаДопОборудования = "";
	Пока Выборка.Следующий() Цикл
		СтрокаДопОборудования = СтрокаДопОборудования + спПолучитьНаименование(Выборка.Опция) + ?(Выборка.Количество = 0,"","("+Выборка.Количество+")") + " ,";
	КонецЦикла;
	ОбластьМакета.Параметры.СписокБазовогоОборудования = Лев(СтрокаДопОборудования, СтрДлина(СтрокаДопОборудования) - 2);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы		= 2;
	НомерСтраницыПред	= НомерСтраницы;
	
	// Если выбран авто - то “Сумма продажи” включает опции,
	// если выбрана “Модель+Вариант комплектации” - то “Сумма продажи” не включает сумму опций
	ОпцииБезЦен	= Истина;	
	Если (НЕ обЗначениеНеЗаполнено(Модель)) И (НЕ обЗначениеНеЗаполнено(ВариантКомплектации)) Тогда
		ОпцииБезЦен	= Ложь;
	КонецЕсли;	
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка"+?(ОпцииБезЦен,"БезЦены",""));
		
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0);
	
	//пересчитаем ТЧ
	ЭтотОбъектОпции = Опции.Выгрузить();
	зфПерерасчетТаблицыТоваров(ЭтотОбъектОпции, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	//дополнительные колонки для того чтобы замаскировать ТЧ Опции под ТЧ Товары
	ЭтотОбъектОпции.Колонки.Добавить("Номенклатура");
	ЭтотОбъектОпции.Колонки.Добавить("ЕдиницаИзмерения");
	ЭтотОбъектОпции.Колонки.Добавить("Коэффициент");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъектОпции;
	
	//теперь запишем параметры шапки
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы"+?(ОпцииБезЦен,"БезЦены",""));
	Если ЕстьКод Тогда
		КолонкаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиКод = КолонкаКода.Синоним;
	КонецЕсли;
	
	Если НЕ ОпцииБезЦен Тогда
		//заполняем заголовок колонки НДС по типу цен
		ОбластьШапкаТаблицы.Параметры.НДС = "НДС";
		Если ТипЦен.ЦенаВключаетНДС Тогда	// Если НДС включен
			Если НЕ (ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС) Тогда	// Однако не все организации - плательщики НДС
				ОбластьШапкаТаблицы.Параметры.НДС = "в т.ч. НДС";
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
		
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда 
		// ПЕЧАТЬ ТАБЛИЦЫ ОПЦИЙ
		//теперь выводим шапку
		ОбластьЗаголовокТаблицы.Параметры.ЗаголовокТаблицы = "Опции";
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиТовара = "Опции";
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	КонецЕсли;
	
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка
		СтрокаТабличнойЧасти.Номенклатура		= СтрокаТабличнойЧасти.Опция;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения	= "";
		СтрокаТабличнойЧасти.Коэффициент		= 1;
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
			НомерСтраницыПред							= НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы	= "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		Если НЕ ОпцииБезЦен Тогда
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		КонецЕсли;
	КонецЦикла;
	
	Если (ВыборкаТабличнойЧасти.Количество() > 0) И (НЕ ОпцииБезЦен) Тогда
		//довыводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьМакета							= Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.ВалютаДокумента	= ВалютаПечатногоДокумента;
		СуммаВсего								= ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьМакета.Параметры.СуммаВсего		= Формат(СуммаВсего,ФорматВыводаСуммы);
		НДСВсего								= ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьМакета.Параметры.НДСВсего		= Формат(НДСВсего,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаПрописью	= "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + НРег(обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента));
		
		НомерСтраницы	= дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим представления и расшифровки подписей
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	
	Если НЕ обЗначениеНеЗаполнено(Менеджер) Тогда
		ОбластьМакета.Параметры.МенеджерПредставление		= "/ " + Строка(Менеджер) + " /";
	КонецЕсли;
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		Если НЕ обЗначениеНеЗаполнено(Контрагент.Наименование) Тогда
			ОбластьМакета.Параметры.НаименованиеКлиента		= "/ " + СокрЛП(Контрагент.Наименование) + " /";
		КонецЕсли;
	ИначеЕсли ТипЗнч(Контрагент) = Тип("Строка") Тогда
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			ОбластьМакета.Параметры.НаименованиеКлиента		= "/ " + СокрЛП(Контрагент) + " /";
		КонецЕсли;
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

// Процедура заполнения табличной части по комплектации
//
Процедура ЗаполнитьКомплектациюАвтомобиля() Экспорт
	
	#Если Клиент Тогда
		ЕстьОпции = Опции.Количество() <> 0;
		Если ЕстьОпции Тогда
			ТекстВопроса = "Очистить табличную часть ""Опции"" перед заполнением по комплектации автомобиля?";	
			ТекстВопроса = ТекстВопроса + "
			|
			|	ДА - Очистить табличную часть;
			|	НЕТ - Не очищать табличную часть;
			|	Отмена - Отменить заполнение по комплектации автомобиля.";
			Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
				Если ЕстьОпции Тогда
					Опции.Очистить();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	#КонецЕсли
	
	#Если Клиент Тогда
		ПолучитьОборудованиеАвтомобиля();
		ПроверитьКомплектациюАвтомобиля();
	#КонецЕсли
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
// Обработчик события при установке новго номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание)=Тип("Структура") Тогда	
		ИмяСтруктуры = Неопределено;
		Основание.Свойство("Имя", ИмяСтруктуры);
		Если ИмяСтруктуры="РегистрацияЗвонковИКонтактов" Тогда	
			
			ВводИзАРМРесепшен = Истина;
			
			ВидКонтакта						= Основание.ВидКонтакта;
			ДатаПоследнегоКонтакта			= Основание.ДатаДень;
			СодержаниеПоследнегоКонтакта	= Основание.Примечание;
			//Менеджер						= Основание.Сотрудник;
			Менеджер						= Основание.Участник;
			Реклама							= Основание.Реклама;			
			Модель							= Основание.Модель;
			Контрагент						= Основание.Контрагент;
			КодРегиона 						= Основание.КодРегиона;
			КодГорода 						= Основание.КодГорода;
			НомерТелефона		 			= Основание.НомерТелефона;
			ВнутреннийНомер 				= Основание.ВнутреннийНомер;
			ПредставлениеТелефона 			= Основание.ПредставлениеТелефона;
			ВидТелефона 					= Основание.ВидТелефона;
			
			// найдем руководителя менеджера
			ОбработкаРеквизита("Менеджер");
			
			Если ЗначениеЗаполнено(Основание.Участник) Тогда
				СтрокаПользователь				= Пользователи.Добавить();
				СтрокаПользователь.Пользователь	= Справочники.Пользователи.НайтиПоРеквизиту("Сотрудник",Основание.Участник); 			
			КонецЕсли;
			
			Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
				Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
					НаименованиеКлиента			= Контрагент.Наименование;
				ИначеЕсли ТипЗнч(Контрагент) = Тип("Строка") Тогда
					НаименованиеКлиента = Контрагент;	
				КонецЕсли;
			КонецЕсли;
			
			ДокументОснование = Основание.ДокументСобытие;
			
		ИначеЕсли ИмяСтруктуры="Автомобиль" Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание);
			Опции.Загрузить(Основание.Опции);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ТипЦен) Тогда
    	ТипЦен=обПраво("ОсновнойТипЦенПродажиАвтомобилей", Права,,ЭтотОбъект)
	КонецЕсли; 
	
	Попытка
		УжеВводилось = ЭтотОбъект.ДополнительныеСвойства.УжеВводилось;
	Исключение
		УжеВводилось = Ложь;
	КонецПопытки;
	
	Если НЕ УжеВводилось Тогда
		Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
		
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.Событие") Тогда 	
			Пользователи.Загрузить(Основание.Пользователи.Выгрузить());
		КонецЕсли;
		
		#Если Клиент Тогда
			зфРабочийЛистОбработкаЗаполнения(ЭтотОбъект,Основание);
		#Иначе
			зфЗащищенныеФункцииСервер.РабочийЛистОбработкаЗаполнения(ЭтотОбъект,Основание);
		#КонецЕсли
		
		Если обЗначениеНеЗаполнено(Менеджер) Тогда
			ТекущийСотрудник = ПолучитьСотрудникаПоПользователю(ПараметрыСеанса.Пользователь);
			Менеджер = ТекущийСотрудник;
			ОбработкаРеквизита("Менеджер");
		КонецЕсли;
		
		Если НЕ ВводИзАРМРесепшен И Пользователи.Количество() = 0 Тогда
			СтрокаПользователь = Пользователи.Добавить();
			СтрокаПользователь.Пользователь = ПараметрыСеанса.Пользователь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.Событие") Тогда 	
		Если Основание.ВидСобытия = Перечисления.ВидыСобытий.ТелефонныйЗвонок Тогда
			ВидПервичногоКонтакта = Перечисления.ВидыПервичногоКонтакта.Телефон;
		ИначеЕсли Основание.ВидСобытия = Перечисления.ВидыСобытий.ЛичнаяВстреча Тогда
			ВидПервичногоКонтакта = Перечисления.ВидыПервичногоКонтакта.ЛичнаяВстреча;
		ИначеЕсли Основание.ВидСобытия = Перечисления.ВидыСобытий.ЭлектронноеПисьмо ИЛИ Основание.ВидСобытия = Перечисления.ВидыСобытий.ИнтернетЗапрос Тогда
			ВидПервичногоКонтакта = Перечисления.ВидыПервичногоКонтакта.ИнтернетЗапрос;
		КонецЕсли;
		
		// Получим Организацию и подразделение из пользователя
		Организация = ПараметрыСеанса.Организация;
		ОбработкаРеквизита("Организация");
		ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
		ОбработкаРеквизита("ПодразделениеКомпании");
		
		Реклама = Основание.ИсточникИнформации;
		
		ВидТелефона 				= Основание.ВидТелефона;
		КодРегиона					= Основание.КодРегиона;
		КодГорода					= Основание.КодГорода;
		НомерТелефона				= Основание.НомерТелефона;
		ВнутреннийНомер				= Основание.ВнутреннийНомер;
		ПредставлениеТелефона		= Основание.ПредставлениеТелефона;
		
		// Заполним данные, которые были введены в АРМ Ресепшен.
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	РегистрацияЗвонковИКонтактов.Модель,
		               |	РегистрацияЗвонковИКонтактов.Реклама
		               |ИЗ
		               |	РегистрСведений.РегистрацияЗвонковИКонтактов КАК РегистрацияЗвонковИКонтактов
		               |ГДЕ
		               |	РегистрацияЗвонковИКонтактов.ДокументСобытие = &ДокументСобытие";
		Запрос.УстановитьПараметр("ДокументСобытие", Основание);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Если НЕ ЗначениеЗаполнено(Реклама) Тогда
				Реклама = Выборка.Реклама;
			КонецЕсли;
			Модель = Выборка.Модель;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Синхронизация реквизита «Контрагент» в подчиненных документах
	// РабочийЛистОтделаСтрахования и РабочийЛистКредитногоОтдела
	
	ЗапросПодчиненныхЛистов			= Новый Запрос();
	ЗапросПодчиненныхЛистов.УстановитьПараметр("Ссылка", Ссылка);
	ЗапросПодчиненныхЛистов.УстановитьПараметр("Контрагент", Контрагент);
	ЗапросПодчиненныхЛистов.Текст	= "ВЫБРАТЬ
	                             	  |	РабочийЛистКредитногоОтдела.Ссылка КАК Ссылка
	                             	  |ИЗ
	                             	  |	Документ.РабочийЛистКредитногоОтдела КАК РабочийЛистКредитногоОтдела
	                             	  |ГДЕ
	                             	  |	РабочийЛистКредитногоОтдела.ДокументОснование = &Ссылка
	                             	  |	И РабочийЛистКредитногоОтдела.Клиент <> &Контрагент
	                             	  |
	                             	  |ОБЪЕДИНИТЬ ВСЕ
	                             	  |
	                             	  |ВЫБРАТЬ
	                             	  |	РабочийЛистОтделаСтрахования.Ссылка
	                             	  |ИЗ
	                             	  |	Документ.РабочийЛистОтделаСтрахования КАК РабочийЛистОтделаСтрахования
	                             	  |ГДЕ
	                             	  |	РабочийЛистОтделаСтрахования.ДокументОснование = &Ссылка
	                             	  |	И РабочийЛистОтделаСтрахования.Страхователь <> &Контрагент
									  |
	                             	  |ОБЪЕДИНИТЬ ВСЕ
	                             	  |
	                             	  |ВЫБРАТЬ
	                             	  |	Событие.Ссылка
	                             	  |ИЗ
	                             	  |	Документ.Событие КАК Событие
	                             	  |ГДЕ
	                             	  |	Событие.ДокументОснование = &Ссылка
									  |	И Событие.Контрагент<>&Контрагент";
									  
	тзПодчиненныхДокументов	= ЗапросПодчиненныхЛистов.Выполнить().Выгрузить();
	ОбновлятьКонтрагента	= Ложь;
	
	#Если Клиент Тогда
	Если (НЕ МонопольныйРежим()) И тзПодчиненныхДокументов.Количество()>0 И
		Вопрос("Синхронизировать контрагента в подчиненных документах?", РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
		ОбновлятьКонтрагента	= Истина;
	КонецЕсли;
	#КонецЕсли

	Если ОбновлятьКонтрагента Тогда
		Для Каждого ТекСтрока Из тзПодчиненныхДокументов Цикл
			Подчиненный	= ТекСтрока.Ссылка.ПолучитьОбъект();
			Если Подчиненный<>Неопределено Тогда
				Если ТипЗнч(Подчиненный)=Тип("ДокументОбъект.РабочийЛистКредитногоОтдела") Тогда
					Подчиненный.Клиент	= Контрагент;
				ИначеЕсли ТипЗнч(Подчиненный)=Тип("ДокументОбъект.РабочийЛистОтделаСтрахования") Тогда
					Подчиненный.Страхователь	= Контрагент;
				ИначеЕсли ТипЗнч(Подчиненный)=Тип("ДокументОбъект.Событие") Тогда
					Подчиненный.Контрагент	= Контрагент;
				Иначе
					Продолжить;
				КонецЕсли;
				Попытка
					Подчиненный.Записать();
				Исключение КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Обработчик события удаления проведения
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события проведения
//
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Проведение по регистру сведений Журнал событий
	Если НЕ Отказ Тогда
		Если НЕ орЖурналСостояний(Ссылка, Статус) Тогда
			Отказ=Истина;
			Возврат;
		КонецЕсли; 
	КонецЕсли;	
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// Вид контакта
СписокВидовКонтактов	= Новый СписокЗначений();
СписокВидовКонтактов.Добавить(Перечисления.ВидыСобытий.ЛичнаяВстреча, "Личная встреча",,БиблиотекаКартинок.СобытияВстреча16);
СписокВидовКонтактов.Добавить(Перечисления.ВидыСобытий.ТелефонныйЗвонок, "Телефонный звонок",,БиблиотекаКартинок.СобытияЗвонок16);
СписокВидовКонтактов.Добавить(Перечисления.ВидыСобытий.ЭлектронноеПисьмо, "Электронное письмо",,БиблиотекаКартинок.КонтактнаяИнформацияЭлектроннаяПочта);
СписокВидовКонтактов.Добавить(Перечисления.ВидыСобытий.ИнтернетЗапрос, "Интернет-запрос",,БиблиотекаКартинок.КонтактнаяИнформацияВебСтраница);
СписокВидовКонтактов.Добавить(Перечисления.ВидыСобытий.Прочее, "Прочее",,БиблиотекаКартинок.ПрочееСобытие);

// Кому покупает
СписокКомуПокупает	= Новый СписокЗначений();
СписокКомуПокупает.Добавить("Себе");
СписокКомуПокупает.Добавить("Супруге(у)");
СписокКомуПокупает.Добавить("Детям");
СписокКомуПокупает.Добавить("Родственникам");

// С кем пришел
СписокСКемПришел	= Новый СписокЗначений();
СписокСКемПришел.Добавить("Один");
СписокСКемПришел.Добавить("С супругой(ом)");
СписокСКемПришел.Добавить("С семьей");
СписокСКемПришел.Добавить("С другом");
СписокСКемПришел.Добавить("С родственником");

// Предполагаемый строк
СписокСроков	= Новый СписокЗначений();
СписокСроков.Добавить("Сегодня");
СписокСроков.Добавить("Неделя");
СписокСроков.Добавить("Месяц");
СписокСроков.Добавить("Полгода");
СписокСроков.Добавить("Год");

// Список статусов рабочего листа
СписокСтатусов	= Новый СписокЗначений;
ЗапросСтатусов		= Новый Запрос();
ЗапросСтатусов.Текст= "ВЫБРАТЬ
                      |	СтатусыРабочегоЛиста.Ссылка КАК Ссылка,
                      |	СтатусыРабочегоЛиста.Наименование КАК Наименование
                      |ИЗ
                      |	Справочник.СтатусыРабочегоЛиста КАК СтатусыРабочегоЛиста
                      |ГДЕ
                      |	СтатусыРабочегоЛиста.ИспользоватьВРабочемЛисте
                      |	И НЕ СтатусыРабочегоЛиста.ПометкаУдаления
                      |
                      |УПОРЯДОЧИТЬ ПО
                      |	СтатусыРабочегоЛиста.ПорядокВОтчете УБЫВ";
ВыборкаСтатусов	= ЗапросСтатусов.Выполнить().Выбрать();
Пока ВыборкаСтатусов.Следующий() Цикл
	СписокСтатусов.Добавить(ВыборкаСтатусов.Ссылка);	
КонецЦикла;

ВводИзАРМРесепшен = Ложь;

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

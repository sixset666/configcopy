// Модуль документа ВводВЭксплуатациюАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ВалютаРегл;                   // Валюта регламентированного учета
Перем ВалютаУпр;                    // Валюта управленческого учета
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка наличия других заказов на автомобиль с данным VINом
//
// Параметры
//
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль() Экспорт
	
	Отказ = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст="
	|ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	|	ЗаказыАвтомобилейОстатки.Заказ КАК Заказ,
	|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.РезервОстаток, 0) КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(
	|			&НаГраницу,
	|			Автомобиль В (&Автомобили)) КАК ЗаказыАвтомобилейОстатки
	|";
	Запрос.УстановитьПараметр("НаГраницу",  Новый Граница(МоментВремени(),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Автомобили", Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		дкСообщитьРезультат("Автомобиль: """+СокрЛП(Выборка.Автомобиль)+""" зарезервирован под заказ: <"+СокрЛП(Выборка.Заказ)+">.","Важное&Заказы автомобилей:", ЭтотОбъект);
		Отказ = Истина;
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
//
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеАвтомобили = Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("Актив",3);
	ОбязательныеАвтомобили.Вставить("ТипЭксплуатации",1);
	ОбязательныеАвтомобили.Вставить("БалансоваяСтоимость",1);
	ОбязательныеАвтомобили.Вставить("МОЛ",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеПолучатель",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("МОЛ",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента",Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ПодразделениеПолучатель", Ложь);
			КонецЕсли;
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			   							
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Если Результат Тогда
		// проверим на правильность активов
		Для Каждого СтрТЧ Из Автомобили Цикл
			Если СтрТЧ.Актив.ТипНоменклатуры <> Справочники.ТипыНоменклатуры.Автомобили Тогда
				Результат = Ложь;
				дкДобавитьОшибку(Ошибки,"Актив <"+СтрТЧ.Актив+"> имеет тип номеклатуры отличный от ""Автомобиль""");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВводВЭксплуатациюАвтомобилей","Ввод в эксплуатацию автомобилей",Истина);
	СписокХозОпераций.Добавить("ПереводАвтомобилейВТестДрайв","Перевод автомобилей в тест-драйв",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все в порядке, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	//Спишем оборудование автомобиля
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ВводВЭксплуатациюАвтомобилейАвтомобили.Автомобиль,
	             |	ВводВЭксплуатациюАвтомобилейАвтомобили.БалансоваяСтоимость
	             |ИЗ
	             |	Документ.ВводВЭксплуатациюАвтомобилей.Автомобили КАК ВводВЭксплуатациюАвтомобилейАвтомобили
	             |ГДЕ
	             |	ВводВЭксплуатациюАвтомобилейАвтомобили.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	ВыборкаАвтомобили=Запрос.Выполнить().Выгрузить();
	НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
	Для каждого СтрокаАвтомобиля Из ВыборкаАвтомобили Цикл
		//Спишем оборудование по документу
		НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейКомплектацияАвтомобилей.Автомобиль=СтрокаАвтомобиля.Автомобиль;
		НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
		НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
		НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
		//Спишем опции, установленные производителем
		НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейКомплектацияАвтомобилей.Автомобиль=СтрокаАвтомобиля.Автомобиль;
		НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
		НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
		НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
	КонецЦикла;
	Если НЕ Отказ Тогда
		НаборЗаписейКомплектацияАвтомобилей.Записать();
	КонецЕсли; 
	
	ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
	ПодразделениеПолучательБаланса = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеПолучатель);
	БалансовыеПодразделенияНеРавны = ПодразделениеСклад<>ПодразделениеПолучательБаланса;
	
	// Получим себестоимости по партиям
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
				 |	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
				 |ИЗ
				 |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				 |ГДЕ
				 |	ОстаткиАвтомобилей.Регистратор = &Регистратор
				 |
				 |ОБЪЕДИНИТЬ ВСЕ
				 |
				 |ВЫБРАТЬ
				 |	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0)
				 |ИЗ
				 |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
				 |ГДЕ
				 |	КомплектацияАвтомобилей.Регистратор = &Регистратор";
	Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
	Выборка=Запрос.Выполнить().Выгрузить();
	СебестоимостьПартий=Выборка.Итог("СуммаУпр");
	
	Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
			// БАЛАНС: Возможно что подразделения склада и получателя не равны в этом случае
		// уменьшаем доход на подразделении склада
			Если СебестоимостьПартий<>0 Тогда
				
				
				
				// уменьшаем доход
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				НаборЗаписейДиР.Доход = СебестоимостьПартий;
				Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
			КонецЕсли; 
			
	Иначе
		// БАЛАНС: Движение по отклонению списания товаров от ввода активов делаем 
		// в случае если способ ведения баланса - не ведётся по подразделениям 
		БалансоваяСтоимостьУпр = ?(ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ВыборкаАвтомобили.Итог("БалансоваяСтоимость"),
				обПересчет(ВыборкаАвтомобили.Итог("БалансоваяСтоимость"), ВалютаДокумента, МоментВремени(), Константы.ВалютаУправленческогоУчетаКомпании.Получить(),
				?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), МоментВремени(), ЭтотОбъект.КурсВалютыУпр)));
		
		Если Окр(обМод(БалансоваяСтоимостьУпр - СебестоимостьПартий), 2, РежимОкругления.Окр15как20) >= 0.01 Тогда
			НаборЗаписейДиР                = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.ОтклонениеСтоимостиАктиваПриВводеВЭксплуатацию;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ШапкаДокумента = ШапкаДокумента;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если БалансоваяСтоимостьУпр > СебестоимостьПартий Тогда
				НаборЗаписейДиР.Доход  = БалансоваяСтоимостьУпр - СебестоимостьПартий;
			Иначе
				НаборЗаписейДиР.Расход = СебестоимостьПартий - БалансоваяСтоимостьУпр;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	//Все ОК
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет общей балансовой стоимости активов
//
// Возвращаемое значение:
//  СуммаВсего - число - итог по колонке "БалансоваяСтоимость".
//
Функция РассчитатьСуммуВсего() Экспорт
	СуммаВсего = Автомобили.Итог("БалансоваяСтоимость");
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	Если Имя="Автомобили.Актив" Тогда
		Если НЕ ТекСтрока.Актив.Пустая() Тогда
			ТекСтрока.ТипЭксплуатации=?(ТекСтрока.Актив.ОсновнойТипЭксплуатации.Пустая(),ТекСтрока.ТипЭксплуатации,ТекСтрока.Актив.ОсновнойТипЭксплуатации);
		КонецЕсли;
		Возврат Истина;
	
	ИначеЕсли Имя = "Автомобили.Автомобиль" Тогда
		//Заполнил МОЛа по умолчанию из шапки
		Если обЗначениеНеЗаполнено(ТекСтрока.МОЛ) Тогда
			ТекСтрока.МОЛ = МОЛ;
		КонецЕсли;
		
		// заполним данные из модели
		Модель = ТекСтрока.Автомобиль.Модель;
		Если НЕ обЗначениеНеЗаполнено(Модель) Тогда
			// получим свойсва
			СтрПараметров = Справочники.Модели.ПолучитьНаборПараметровТестДрайва(Модель);
			ТекСтрока.МаксимальнаяПродолжительностьЭксплуатации = СтрПараметров.ПродолжительностьЭксплуатации;
			ТекСтрока.МаксимальныйПробег                        = СтрПараметров.МаксимальныйПробег;
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("ПолучитьРасчетнуюСтоимостьАктива",ТекСтрока,ЭтаФорма,ДопПараметры);
				
	ИначеЕсли Имя="ПолучитьРасчетнуюСтоимостьАктива" Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ЕСТЬNULL(СУММА(ОстаткиАвтомобилейОстатки.СуммаОстаток),0) КАК СуммаАвтомобиля,
		             |	ЕСТЬNULL(СУММА(ОстаткиАвтомобилейОстатки.СуммаУпрОстаток),0) КАК СуммаУпрАвтомобиля,
		             |	ЕСТЬNULL(СУММА(КомплектацияАвтомобилейОстатки.СуммаОстаток),0) КАК СуммаКомплектации,
		             |	ЕСТЬNULL(СУММА(КомплектацияАвтомобилейОстатки.СуммаУпрОстаток),0) КАК СуммаУпрКомплектации
		             |ИЗ
		             |	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&НаМомент, Автомобиль = &Автомобиль) КАК ОстаткиАвтомобилейОстатки
		             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Остатки(&НаМомент, Автомобиль = &Автомобиль) КАК КомплектацияАвтомобилейОстатки
		             |		ПО ОстаткиАвтомобилейОстатки.Автомобиль = КомплектацияАвтомобилейОстатки.Автомобиль";
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
		Запрос.УстановитьПараметр("Автомобиль",ТекСтрока.Автомобиль);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если ВалютаДокумента=ВалютаРегл Тогда
				БалансоваяСтоимость=Выборка.СуммаАвтомобиля+Выборка.СуммаКомплектации;
			Иначе
				БалансоваяСтоимость=Выборка.СуммаУпрАвтомобиля+Выборка.СуммаУпрКомплектации;
				Если ВалютаДокумента<>ВалютаУпр Тогда
					БалансоваяСтоимость=обПересчет(БалансоваяСтоимость,ВалютаУпр,?(обЗначениеНеЗаполнено(КурсВалютыУпр),Дата,КурсВалютыУпр),ВалютаДокумента,КурсДокумента);
				КонецЕсли;
			КонецЕсли;
		Иначе
			БалансоваяСтоимость=0;
		КонецЕсли; 
		ТекСтрока.БалансоваяСтоимость=БалансоваяСтоимость;
		Возврат Истина;
				
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВводВЭксплуатацию(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВводВЭксплуатацию");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеПодразделения = спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
	ОбластьМакета.Параметры.ПредставлениеМОЛ = спПолучитьНаименование(МОЛ);
	ОбластьМакета.Параметры.ПредставлениеПодразделенияПолучателя = спПолучитьНаименование(ПодразделениеПолучатель);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,БалансоваяСтоимость",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
		ОбластьМакета.Параметры.Актив = СтрокаТабличнойЧасти.Актив;
		ОбластьМакета.Параметры.АктивНаименование = спПолучитьНаименование(СтрокаТабличнойЧасти.Актив);
		ОбластьМакета.Параметры.Код = СтрокаТабличнойЧасти.Актив.ИнвентарныйНомер;
		ОбластьМакета.Параметры.ТипЭксплуатации = СтрокаТабличнойЧасти.ТипЭксплуатации;
		ОбластьМакета.Параметры.БалансоваяСтоимость = Формат(СтрокаТабличнойЧасти.БалансоваяСтоимость,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Количество = 1;
		ОбластьМакета.Параметры.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;

		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		//НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы,
		//ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,БалансоваяСтоимость",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги		
		
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("БалансоваяСтоимость");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВводВЭксплуатацию()

// Формирует печатную форму "ОС-1"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОС1(ТабДокумент) Экспорт
	ТекстЗапроса = "ВЫБРАТЬ
	|	ВводВЭксплуатациюАвтомобилей.Ссылка.Дата КАК ДатаВвода,
	|	ВводВЭксплуатациюАвтомобилей.Ссылка.МОЛ КАК Ответственный,
	|	ВводВЭксплуатациюАвтомобилей.Ссылка.ПодразделениеКомпании КАК ПодразделениеОрганизации,
	|	ВводВЭксплуатациюАвтомобилей.Ссылка.СкладКомпании КАК Склад,
	|
	|	ВводВЭксплуатациюАвтомобилей.Актив КАК Актив,
	|	ВводВЭксплуатациюАвтомобилей.Актив.ДатаВыбытия КАК ДатаВыбытия,
	|	ВводВЭксплуатациюАвтомобилей.Актив.ИнвентарныйНомер КАК ИнвНомер,
	|	ВводВЭксплуатациюАвтомобилей.Актив.ПервоначальнаяСтоимость КАК НачСтоимость,
	|	ВводВЭксплуатациюАвтомобилей.Актив.КоэффициентУскорения КАК КоэффициентУскоренияБУ,
	|	ВводВЭксплуатациюАвтомобилей.Актив.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ВводВЭксплуатациюАвтомобилей.БалансоваяСтоимость КАК БалансоваяСтоимость,
	|
	|	ВводВЭксплуатациюАвтомобилей.ТипЭксплуатации.СпособНачисленияАмортизации КАК СпособАмортизации,
	|	ВводВЭксплуатациюАвтомобилей.ТипЭксплуатации.АмортизационнаяГруппа КАК АмортизационнаяГруппа
	|ИЗ
	|	Документ.ВводВЭксплуатациюАвтомобилей.Автомобили КАК ВводВЭксплуатациюАвтомобилей
	|ГДЕ
	|	ВводВЭксплуатациюАвтомобилей.Ссылка = &Ссылка";
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаАктивы = Запрос.Выполнить().Выбрать();
	ВыборкаАктивы.Следующий();
	
	ОрганизацияСдатчик    = обПолучитьЗначениеСвойства(ЭтотОбъект,"ОрганизацияСдатчик",ЭтотОбъект.Организация);
	ОрганизацияПолучатель = ЭтотОбъект.Организация;
	СдатчикНашаОрганизация = (ОрганизацияСдатчик = ОрганизацияПолучатель);
	//Чтение значение для адреса
	АдресОрганизацииСдатчика = киПолучитьПредставлениеКИ(ОрганизацияСдатчик, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	АдресЮридический = ?(АдресОрганизацииСдатчика = "", "", АдресОрганизацииСдатчика + ", ");
	АдресТелефоныСдатчика = АдресЮридический + "тел.:" + киПолучитьПредставлениеКИ(ОрганизацияСдатчик, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	Если СдатчикНашаОрганизация Тогда
		АдресТелефоныПолучателя = АдресТелефоныСдатчика;
	Иначе
		АдресОрганизацииПолучателя = киПолучитьПредставлениеКИ(ОрганизацияПолучатель, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		АдресЮридический = ?(АдресОрганизацииПолучателя = "", "", АдресОрганизацииПолучателя + ", ");
		АдресТелефоныПолучателя = АдресЮридический + "тел.:" + киПолучитьПредставлениеКИ(ОрганизацияПолучатель, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	КонецЕсли;
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	// Получим ответственных
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ГлавныйБухгалтер  = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	мМОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	ПодразделениеСдатчика = ?(СдатчикНашаОрганизация,ЭтотОбъект.ПодразделениеКомпании,"");
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил"); 
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	// Получим членов комиссии
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	
	Если ВыборкаАктивы.Количество() = 1 Тогда //актив один - печать ОС-1
		Макет = ПолучитьМакет("ОС1");
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		
		ОбластьМакета.Параметры.РуководительСдатчикПредставление     = ?(СдатчикНашаОрганизация,Руководитель.РуководительПредставление,"");
		ОбластьМакета.Параметры.РуководительСдатчикДолжность   		 = ?(СдатчикНашаОрганизация,Руководитель.РуководительДолжность,"");		
		ОбластьМакета.Параметры.РуководительСдатчик				     = ?(СдатчикНашаОрганизация,Руководитель.Руководитель,"");		
		ОбластьМакета.Параметры.РуководительПолучатель				 = Руководитель.Руководитель;		
		ОбластьМакета.Параметры.РуководительПолучательПредставление  = Руководитель.РуководительПредставление;
		ОбластьМакета.Параметры.РуководительПолучательДолжность	     = Руководитель.РуководительДолжность;
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчикаПредставление    = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтерПредставление,"");
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчика				         = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтер,"");		
		
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(мМОЛ);
		ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		ОбластьМакета.Параметры.Заполнить(Получил);
		
		//сдатчик автомобиля
		ОбластьМакета.Параметры.ПредставлениеОрганизацииСдатчика	= спПолучитьНаименование(ОрганизацияСдатчик);
		ОбластьМакета.Параметры.ОрганизацияСдатчик					= ОрганизацияСдатчик;
		ОбластьМакета.Параметры.АдресСдатчика						= АдресТелефоныСдатчика;
		ОбластьМакета.Параметры.РеквизитыСдатчика					= Строка(ОрганизацияСдатчик.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияСдатчик.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеСдатчика				= ПодразделениеСдатчика;
		
		//получатель актива
		ОбластьМакета.Параметры.ПредставлениеОрганизацииПолучателя	= спПолучитьНаименование(ОрганизацияПолучатель);
		ОбластьМакета.Параметры.ОрганизацияПолучатель				= ОрганизацияПолучатель;
		ОбластьМакета.Параметры.АдресПолучателя						= АдресТелефоныПолучателя;
		ОбластьМакета.Параметры.РеквизитыПолучателя					= Строка(ОрганизацияПолучатель.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияПолучатель.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеПолучателя				= ПодразделениеПолучатель;
		
		ОбластьМакета.Параметры.КодПоОКПОСдатчик    = ОрганизацияСдатчик.КодПоОКПО;
		ОбластьМакета.Параметры.КодПоОКПОПолучатель = ОрганизацияПолучатель.КодПоОКПО;
		ОбластьМакета.Параметры.ДатаВвода    = ВыборкаАктивы.ДатаВвода;
		ОбластьМакета.Параметры.ДатаПередачи = обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаПередачи",ВыборкаАктивы.ДатаВыбытия);
		ОбластьМакета.Параметры.НомерГруппы		= ВыборкаАктивы.АмортизационнаяГруппа;
		ОбластьМакета.Параметры.ИнвНомер		= ВыборкаАктивы.ИнвНомер;
		ОбластьМакета.Параметры.НаименованиеОС	= спПолучитьНаименование(ВыборкаАктивы.Актив);
		ОбластьМакета.Параметры.КарточкаОС		= ВыборкаАктивы.Актив;
		ОбластьМакета.Параметры.НомерАкта = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаАкта  = Дата;
		ОбластьМакета.Параметры.ДатаДок   = Дата;
		
		ОбластьМакета.Параметры.СрокЭкспл            = 0;
		ОбластьМакета.Параметры.СрокПолезнИспПриПер  = 0;
		ОбластьМакета.Параметры.НачАмортизация       = 0;
		ОбластьМакета.Параметры.ОстСтоимость         = 0;
		ОбластьМакета.Параметры.ЦенаПродажи          = обПересчет(ВыборкаАктивы.БалансоваяСтоимость, ВалютаДокумента, КурсДокумента, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
		ОбластьМакета.Параметры.НачСтоимость         = обПересчет(ВыборкаАктивы.НачСтоимость, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
		ОбластьМакета.Параметры.СрокПолезнИспПриПост = ВыборкаАктивы.СрокПолезногоИспользования;
		ОбластьМакета.Параметры.СпособАмортизации    = ВыборкаАктивы.СпособАмортизации;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
		
	Иначе //активов много - групповая печать (ОС-1б)
		Макет = ПолучитьМакет("ОС1б");
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка1");
		
		ОбластьМакета.Параметры.РуководительСдатчикПредставление     = ?(СдатчикНашаОрганизация,Руководитель.РуководительПредставление,"");
		ОбластьМакета.Параметры.РуководительСдатчикДолжность   		 = ?(СдатчикНашаОрганизация,Руководитель.РуководительДолжность,"");		
		ОбластьМакета.Параметры.РуководительСдатчик				     = ?(СдатчикНашаОрганизация,Руководитель.Руководитель,"");		
		ОбластьМакета.Параметры.РуководительПолучатель				 = Руководитель.Руководитель;		
		ОбластьМакета.Параметры.РуководительПолучательПредставление  = Руководитель.РуководительПредставление;
		ОбластьМакета.Параметры.РуководительПолучательДолжность	     = Руководитель.РуководительДолжность;
		
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(мМОЛ);
				
		//сдатчик автомобилей
		ОбластьМакета.Параметры.ПредставлениеОрганизацииСдатчика	= спПолучитьНаименование(ОрганизацияСдатчик);
		ОбластьМакета.Параметры.ОрганизацияСдатчик					= ОрганизацияСдатчик;
		ОбластьМакета.Параметры.АдресСдатчика						= АдресТелефоныСдатчика;
		ОбластьМакета.Параметры.РеквизитыСдатчика					= Строка(ОрганизацияСдатчик.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияСдатчик.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеСдатчика				= ПодразделениеСдатчика;
		
		//получатель актива
		ОбластьМакета.Параметры.ПредставлениеОрганизацииПолучателя	= спПолучитьНаименование(ОрганизацияПолучатель);
		ОбластьМакета.Параметры.ОрганизацияПолучатель				= ОрганизацияПолучатель;
		ОбластьМакета.Параметры.АдресПолучателя						= АдресТелефоныПолучателя;
		ОбластьМакета.Параметры.РеквизитыПолучателя					= Строка(ОрганизацияПолучатель.ОсновнойБанковскийСчет.Банк) + " №" + ОрганизацияПолучатель.ОсновнойБанковскийСчет.НомерСчета;
		ОбластьМакета.Параметры.ПодразделениеПолучателя				= ПодразделениеПолучатель;
		
		ОбластьМакета.Параметры.КодОКПОСдатчик          = ОрганизацияСдатчик.КодПоОКПО;
		ОбластьМакета.Параметры.КодОКПОПолучатель       = ОрганизацияПолучатель.КодПоОКПО;		
		ОбластьМакета.Параметры.ДатаАктаВВода = ВыборкаАктивы.ДатаВвода;
		ОбластьМакета.Параметры.ДатаПередачи  = обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаПередачи",ВыборкаАктивы.ДатаВыбытия);
		ОбластьМакета.Параметры.НомерДок      = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаДок       = Дата;
		ТабДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Шапка2");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка2");
		НомСтр = 1;
		ВыборкаАктивы.Сбросить();
		Пока ВыборкаАктивы.Следующий() Цикл
			ОбластьМакета.Параметры.НомерСтроки		= НомСтр;
			ОбластьМакета.Параметры.НаименованиеОС	= спПолучитьНаименование(ВыборкаАктивы.Актив);
			ОбластьМакета.Параметры.КарточкаОС		= ВыборкаАктивы.Актив;
			ОбластьМакета.Параметры.НомерГруппы		= ВыборкаАктивы.АмортизационнаяГруппа;
			ОбластьМакета.Параметры.ИнвНомер		= ВыборкаАктивы.ИнвНомер;
			ОбластьМакета.Параметры.ДатаВвода		= ВыборкаАктивы.ДатаВвода;
			ТабДокумент.Вывести(ОбластьМакета);
			НомСтр = НомСтр + 1;
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка2П");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал2");
		ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
		
		ТабДокумент.Вывести(ОбластьМакета);
				
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка3");
		ТабДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Строка3");
		ИтогЦенаПродажи  = 0;
		ИтогНачСтоимость = 0;
		ВыборкаАктивы.Сбросить();
		Пока ВыборкаАктивы.Следующий() Цикл
			ЦенаПродажи  = обПересчет(ВыборкаАктивы.БалансоваяСтоимость, ВалютаДокумента, КурсДокумента, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
			НачСтоимость = обПересчет(ВыборкаАктивы.НачСтоимость, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
			
			ОбластьМакета.Параметры.НачАмортизация       = 0;
			ОбластьМакета.Параметры.ОстСтоимость         = 0;
			ОбластьМакета.Параметры.ЦенаПродажи          = ЦенаПродажи;
			ОбластьМакета.Параметры.НачСтоимость         = НачСтоимость;
			ОбластьМакета.Параметры.СрокПолезнИспПриПост = ВыборкаАктивы.СрокПолезногоИспользования;
			ОбластьМакета.Параметры.СпособАмортизации    = ВыборкаАктивы.СпособАмортизации;
			ТабДокумент.Вывести(ОбластьМакета);
			
			ИтогЦенаПродажи  = ИтогЦенаПродажи  + ЦенаПродажи;
			ИтогНачСтоимость = ИтогНачСтоимость + НачСтоимость;
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка3П");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал3");
		ОбластьМакета.Параметры.ИтогЦенаПродажи  = ИтогЦенаПродажи;
		ОбластьМакета.Параметры.ИтогНачСтоимость = ИтогНачСтоимость; 
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчикаПредставление    = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтерПредставление,"");
		ОбластьМакета.Параметры.ГлавныйБухгалтерСдатчика				 = ?(СдатчикНашаОрганизация,ГлавныйБухгалтер.ГлавныйБухгалтер,"");		
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка4");
		ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(мМОЛ);
		ОбластьМакета.Параметры.Заполнить(Получил);
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	КонецЕсли;
	
	//параметры табличного документа
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ОтображатьСетку     = Ложь;
	ТабДокумент.ТолькоПросмотр      = Истина;
			
	Возврат ТабДокумент;
КонецФункции // ПечатьОС1()

// Формирует печатную форму "М-11"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ11(ТабДокумент) Экспорт
	
	//печать основывается на движениях, непроведенный документ печатать запрещено
	Если НЕ ЭтотОбъект.Проведен Тогда
		Сообщить("Печать формы ""М-11"" запрещена для непроведенного документа.",СтатусСообщения.Внимание);
		Возврат Неопределено;
	КонецЕсли;

	// Зададим параметры документа
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("М11");
	
	//получим списываемые товары
	ТекстЗапроса = "ВЫБРАТЬ
	|	ДокументАвтомобили.Автомобиль КАК Номенклатура,
	|	ДокументАвтомобили.Автомобиль.VIN КАК НоменклатурныйНомер,
	|	1 КАК Количество,
	|	ЕСТЬNULL(ОстаткиАвтомобилейОбороты.СуммаРасход,0) - ЕСТЬNULL(ОстаткиАвтомобилейОбороты.СуммаНДСРасход,0) +
	|	ЕСТЬNULL(КомплектацияАвтомобилейОбороты.СуммаРасход,0) - ЕСТЬNULL(КомплектацияАвтомобилейОбороты.СуммаНДСРасход,0) КАК Сумма,
	|	ЕСТЬNULL(ОстаткиАвтомобилейОбороты.СуммаРасход,0) - ЕСТЬNULL(ОстаткиАвтомобилейОбороты.СуммаНДСРасход,0) +
	|	ЕСТЬNULL(КомплектацияАвтомобилейОбороты.СуммаРасход,0) - ЕСТЬNULL(КомплектацияАвтомобилейОбороты.СуммаНДСРасход,0) КАК Цена,
	|
	|	ДокументАвтомобили.Ссылка.СкладКомпании.МОЛ.Должность КАК МОЛДолжностьОтправитель,
	|	ДокументАвтомобили.Ссылка.СкладКомпании.МОЛ КАК МОЛПредставлениеОтправитель,
	|	ДокументАвтомобили.Ссылка.МОЛ.Должность КАК МОЛДолжностьПолучатель,
	|	ДокументАвтомобили.Ссылка.МОЛ КАК МОЛПредставлениеПолучатель
	|
	|ИЗ Документ.ВводВЭксплуатациюАвтомобилей.Автомобили КАК ДокументАвтомобили
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Обороты(
	|		,
	|		,
	|		Регистратор,
	|		Автомобиль В (&СписокАвтомобилей)
	|			И СкладКомпании = &Склад) КАК ОстаткиАвтомобилейОбороты
	|		ПО ДокументАвтомобили.Ссылка = ОстаткиАвтомобилейОбороты.Регистратор
	|		И ДокументАвтомобили.Автомобиль = ОстаткиАвтомобилейОбороты.Автомобиль
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Обороты(
	|		,
	|		,
	|		Регистратор,
	|		Автомобиль В (&СписокАвтомобилей)
	|			И СкладКомпании = &Склад) КАК КомплектацияАвтомобилейОбороты
	|ПО ДокументАвтомобили.Ссылка = КомплектацияАвтомобилейОбороты.Регистратор
	|	И ДокументАвтомобили.Автомобиль = КомплектацияАвтомобилейОбороты.Автомобиль
	|ГДЕ
	|	ДокументАвтомобили.Ссылка = &Документ";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СписокАвтомобилей", Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Запрос.УстановитьПараметр("Склад", СкладКомпании);
	Запрос.УстановитьПараметр("Документ", Ссылка);
	ВыборкаСтрок = Запрос.Выполнить().Выгрузить();
		
	Затребовал = дкОтветственноеЛицо(ЭтотОбъект,"Затребовал");
	Разрешил   = дкОтветственноеЛицо(ЭтотОбъект,"Разрешил");
	
	// выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.Заполнить(Затребовал);
	ОбластьМакета.Параметры.Заполнить(Разрешил);
	ОбластьМакета.Параметры.Заголовок					= "Требование-накладная № " + дкПолучитьНомерДляПечати(ЭтотОбъект) + " от " + Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.Организация					= Организация;
	ОбластьМакета.Параметры.КодОКПО						= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ДатаСоставления				= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.Склад						= СкладКомпании;
	ОбластьМакета.Параметры.ПодразделениеКомпании		= ПодразделениеКомпании;
	ТабДокумент.Вывести(ОбластьМакета); // конец шапки
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьРазделителя		= Макет.ПолучитьОбласть("Разделитель");
	ОбластьМакетаИтогоПоСтранице = ОбластьРазделителя;
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	мМОЛ = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.МОЛ);
	МолОтправитель = дкОтветственноеЛицо(ЭтотОбъект, "МОЛОтправитель");
	// выводим подвал                                
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Заполнить(мМОЛ);
	ОбластьПодвал.Параметры.Заполнить(МолОтправитель);
	ТабДокументПодвал = Новый ТабличныйДокумент;
	ТабДокументПодвал.Вывести(ОбластьПодвал);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	// выводим заголовок таблицы
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	
	// выводим многострочную часть документа
	ЕдИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;
	Для каждого Строка Из ВыборкаСтрок Цикл
		ОбластьСтрока.Параметры.Заполнить(Строка);
		ОбластьСтрока.Параметры.Счет					= "";
	 	ОбластьСтрока.Параметры.МатериалНаименование	= спПолучитьНаименование(Строка.Номенклатура);
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод		= ЕдИзмерения.Код;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование = ЕдИзмерения.Наименование;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(Строка) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ТабДокументПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла; // конец многострочной части
	
	ТабДокумент.Вывести(ТабДокументПодвал); // конец подвала
	
	Возврат ТабДокумент;
КонецФункции // ПечатьМ11()

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
    // Документ не стандартный(имя ТЧ не "Товары", а "Автомобили", имя реквизита для цены не "Цена",
	// а "БалансоваяСтоимость").
    СтруктураПараметров = Новый Структура("ИмяТабличнойЧасти,ИмяРеквизитаЦена","Автомобили","БалансоваяСтоимость");
    дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: 
//  НазваниеПечатнойФормы - строка, название макета, если пусто - то по умолчанию,
//  Документ              - сюда можно передать уже созданный табличный документ для заполнения
//  КоличествоЭкземпляров - число, необходимое количество копий на печать.
//  НаПринтер             - булево, Ложь - на экран, истина - на принтер.
// Возвращаемое значение:
//  Возвращает сформированный табличный документ.
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(Основание) =  Тип("ДокументСсылка.ПеремещениеАвтомобилей") тогда
		СкладКомпании = Основание.СкладПолучатель;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ПрочиеАктивы") Тогда
		СтрокаТабличнойЧасти = Автомобили.Добавить();
		СтрокаТабличнойЧасти.Актив = Основание.Ссылка;
		ОбработкаРеквизита("Автомобили.Актив", СтрокаТабличнойЧасти);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("СправочникСсылка.Автомобили") И (НЕ Основание.ЭтоГруппа) Тогда
		СтрокаТабличнойЧасти = Автомобили.Добавить();
		СтрокаТабличнойЧасти.Автомобиль = Основание.Ссылка;
		ОбработкаРеквизита("Автомобили.Автомобиль", СтрокаТабличнойЧасти);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеАвтомобилей") ИЛИ 
			  ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеАвтомобилей") Тогда
		Для каждого ТекСтрока Из Автомобили Цикл
			ОбработкаРеквизита("Автомобили.Автомобиль", ТекСтрока);
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Результат = Истина;
	Если НЕ ФлагПерерасчет Тогда
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда Возврат; КонецЕсли; 
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	////проверим на корректность вида склада
	//Если СкладКомпании.Розничный Тогда
	//	дкСообщитьРезультат("Реквизит <Склад компании> . Склад списания номенклатуры на перевод
	//	в актив не может быть розничным", "Важное&Прочие активы в эксплуатации:", ЭтотОбъект, СкладКомпании);
	//	Отказ = Истина;
	//КонецЕсли;
	
	ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеПолучательБаланса = обПолучитьБалансовоеПодразделение(ПодразделениеПолучатель);
	БалансовыеПодразделенияНеРавны = ПодразделениеСклад<>ПодразделениеПолучательБаланса;
	
	Отказ = ПроверитьЗаказыНаАвтомобиль() ИЛИ Отказ;
	
	//Спишем автомобиль со склада
	ТекстЗапроса="ВЫБРАТЬ
				 |	ДокументАвтомобили.Автомобиль КАК Автомобиль,
				 |	ДокументАвтомобили.Автомобиль КАК Номенклатура,
				 |	1 КАК Количество,
				 |	ВЫБОР
				 |		КОГДА ДокументАвтомобили.МОЛ = &ПустойМОЛ
				 |			ТОГДА ДокументАвтомобили.Ссылка.МОЛ
				 |		ИНАЧЕ ДокументАвтомобили.МОЛ
				 |	КОНЕЦ КАК МОЛ,
				 |	ДокументАвтомобили.Актив КАК Актив,
				 |	ДокументАвтомобили.ТипЭксплуатации КАК ТипЭксплуатации,
				 |	ДокументАвтомобили.БалансоваяСтоимость КАК БалансоваяСтоимость,
				 |	0 КАК Амортизация,
				 |	0 КАК СуммаОбслуживания
				 |ИЗ
				 |	Документ.ВводВЭксплуатациюАвтомобилей.Автомобили КАК ДокументАвтомобили
				 |ГДЕ
				 |	ДокументАвтомобили.Ссылка = &Ссылка
				 |";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("ПустойМОЛ",Справочники.Сотрудники.ПустаяСсылка());
	РезультатЗапросаПоАвтомобилям=Запрос.Выполнить().Выгрузить();
	НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
	НаборЗаписейОстаткиАвтомобилей.РежимПроведения=Режим;
	НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилям;
	НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
	//НаборЗаписейОстаткиАвтомобилей.Приходовать=Истина;
	//НаборЗаписейОстаткиАвтомобилей.Резервировать=Ложь;
	НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	Отказ=НЕ НаборЗаписейОстаткиАвтомобилей.Расход() ИЛИ Отказ;
	Если НЕ Отказ Тогда
		НаборЗаписейОстаткиАвтомобилей.Записать();
	КонецЕсли; 

	// проведем по Тест-Драйву
	Если НЕ Отказ И ХозОперация = Справочники.ХозОперации.ПереводАвтомобилейВТестДрайв Тогда
		НаборТестДрайв = Движения.АвтомобилиДляТестДрайва;
		НаборТестДрайв.ДокументОбъект = ЭтотОбъект;
		НаборТестДрайв.ПодразделениеКомпании = ПодразделениеПолучатель;
		НаборТестДрайв.РезультатЗапросаПоАвтомобилям = Автомобили.Выгрузить();
		НаборТестДрайв.Приход();
	КонецЕсли;
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// закинем в "Активы в эксплуатации"
	НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
	НаборЗаписейЭксплуатация.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейЭксплуатация.ПодразделениеКомпании     = ПодразделениеПолучатель;
	НаборЗаписейЭксплуатация.ЭтоПервыйВвод             = Истина;
	НаборЗаписейЭксплуатация.РезультатЗапросаПоАктивам = РезультатЗапросаПоАвтомобилям;
	Отказ = НЕ НаборЗаписейЭксплуатация.Приход() ИЛИ Отказ;
	
	Если ВедетсяБалансПоПодразделению Тогда
		// БАЛАНС: Возможно что подразделения склада и получателя не равны в этом случае
		// увеличиваем доход на подразделении получателе
		Если БалансовыеПодразделенияНеРавны Тогда
			// Получим себестоимости по прочим активам
			ПрочиеАктивыВЭксплуатации=Движения.ПрочиеАктивыВЭксплуатации;
			СебестоимостьПрочиеАктивыВЭксплуатации = ПрочиеАктивыВЭксплуатации.Итог("БалансоваяСтоимостьУпр");
			// Увеличиваем доход
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ПодразделениеПолучатель;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьПрочиеАктивыВЭксплуатации;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЕсли;
	
	// если тут же начисляется амортизация (100% способ начисления), начисляем расходы
	Если НаборЗаписейЭксплуатация.СуммаАмортизацииУпр100 > 0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Амортизация;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение      = ПодразделениеПолучатель;
		КонецЕсли;
		НаборЗаписейДиР.ВУпрВалюте             = Истина;
		НаборЗаписейДиР.Расход                 = НаборЗаписейЭксплуатация.СуммаАмортизацииУпр100;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ           

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();

// инициализация кэша валюты и курса документа
ВалютаКурсДок = Новый Структура("Валюта, Курс", ВалютаДокумента, КурсДокумента);
ФлагПерерасчет = Ложь;

// Модуль документа ИнвентаризацияАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем РезультатЗапросаПоАвтомобилямИзлишки;     // Результат выполнения запроса по излишкам автомобилей
Перем РезультатЗапросаПоАвтомобилямНедостачи;   // Результат выполнения запроса по недостачам автомобилей
Перем ВалютаУпр;                                // Валюта управленческого учета

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка наличия резервов на автомобиль
//
// Параметры
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль(СписокАвтомобилей=Неопределено)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("МетаданныеИмя",ЭтотОбъект.Метаданные().Имя);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	Если СписокАвтомобилей=Неопределено Тогда
		ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Иначе
		ДокументОбъектСтруктура.Вставить("Автомобили",СписокАвтомобилей);
	КонецЕсли; 
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
    Иначе
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СтатьяСписанияОбнаруженнойНедостачиТМЦ",1);
	ОбязательныеРеквизиты.Вставить("СтатьяОприходованияОбнаруженныхИзлишковТМЦ",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если  Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
		СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
		
		КонтролируемыеРеквизиты = Новый Структура();
		
		КонтролируемыеРеквизитыШапки = Новый Структура();
		Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
		Иначе
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
		КонецЕсли;	
		
		КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;	
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И Результат;
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект, "Автомобили", "Автомобиль", "ГТДИзлишков");
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ИнвентаризацияАвтомобилей","Инвентаризация автомобилей",Истина);
	СписокХозОпераций.Добавить("ИнвентаризацияАвтомобилейОтданныхНаКомиссию","Инвентаризация автомобилей у комиссионера");

	Возврат СписокХозОпераций;
КонецФункции

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по автомобилям
// Излишки - Булево, истина-возвращает излишки, ложь-недостачи
Функция ПолучитьРезультатЗапросаПоАвтомобилям(Излишки)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("ТипЦен",ТипЦен);
	ДокументОбъектСтруктура.Вставить("СкладКомпании",СкладКомпании);
	ДокументОбъектСтруктура.Вставить("ХозОперация",ХозОперация);
	ДокументОбъектСтруктура.Вставить("Контрагент",Контрагент);
	ДокументОбъектСтруктура.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
	Возврат зфЗащищенныеФункцииСервер.ИнвентаризацияАвтомобилейПолучитьРезультатЗапросаПоАвтомобилям(ДокументОбъектСтруктура,Излишки);
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СтатьяСписанияОбнаруженнойНедостачиТМЦ КАК СтатьяСписанияОбнаруженнойНедостачиТМЦ,
	|	Док.СтатьяОприходованияОбнаруженныхИзлишковТМЦ КАК СтатьяОприходованияОбнаруженныхИзлишковТМЦ
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	// определим вид проводки
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
		//Спишем оборудование автомобиля
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ОстаткиАвтомобилей.Автомобиль
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		|ГДЕ
		|	ОстаткиАвтомобилей.Регистратор = &Регистратор
		|	И ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРасход";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
		ВыборкаАвтомобилиРасход=Запрос.Выполнить().Выбрать();
		НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
		Пока ВыборкаАвтомобилиРасход.Следующий() Цикл
			//Спишем оборудование недостающего автомобиля
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ВыборкаАвтомобилиРасход.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
			//Спишем опции, установленное производителем
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ВыборкаАвтомобилиРасход.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
		КонецЦикла;
		Если НЕ Отказ Тогда
			НаборЗаписейКомплектацияАвтомобилей.Записать();
		КонецЕсли; 
		//Добавим стоимость комплектации реализованных автомобилей
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	РеализованныеАвтомобили.Контрагент КАК Контрагент,
					 |	РеализованныеАвтомобили.Автомобиль КАК Автомобиль,
					 |	РеализованныеАвтомобили.ГТД КАК ГТД,
					 |	РеализованныеАвтомобили.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
					 |	РеализованныеАвтомобили.ДокументПередачи КАК ДокументПередачи,
					 |	ЕСТЬNULL(КомплектацияАвтомобилей.СуммаУпр, 0) КАК СуммаУпр,
					 |	ЕСТЬNULL(КомплектацияАвтомобилей.СуммаУпр, 0) КАК СуммаПродажи,
					 |	ЕСТЬNULL(КомплектацияАвтомобилей.Сумма, 0)    КАК СуммаРегл,
					 |	ЕСТЬNULL(КомплектацияАвтомобилей.Сумма, 0)    КАК СуммаПродажиРегл
					 |ИЗ
					 |	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
					 |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					 |			КомплектацияАвтомобилей.Партия.Контрагент КАК Контрагент,
					 |			КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
					 |			КомплектацияАвтомобилей.Партия.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
					 |			КомплектацияАвтомобилей.Партия.Ссылка КАК ДокументПередачи,
					 |			СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр,
					 |			СУММА(КомплектацияАвтомобилей.Сумма) КАК Сумма
					 |		ИЗ
					 |			РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
					 |		ГДЕ
					 |			КомплектацияАвтомобилей.Регистратор = &Регистратор
					 |			И КомплектацияАвтомобилей.ВидДвижения = &ВидДвиженияРасход
					 |			И КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции
					 |		
					 |		СГРУППИРОВАТЬ ПО
					 |			КомплектацияАвтомобилей.Партия.Контрагент,
					 |			КомплектацияАвтомобилей.Автомобиль,
					 |			КомплектацияАвтомобилей.Партия.ДоговорВзаиморасчетов,
					 |			КомплектацияАвтомобилей.Партия.Ссылка) КАК КомплектацияАвтомобилей
					 |		ПО РеализованныеАвтомобили.Контрагент = КомплектацияАвтомобилей.Контрагент
					 |			И РеализованныеАвтомобили.Автомобиль = КомплектацияАвтомобилей.Автомобиль
					 |			И РеализованныеАвтомобили.ДоговорВзаиморасчетов = КомплектацияАвтомобилей.ДоговорВзаиморасчетов
					 |			И РеализованныеАвтомобили.ДокументПередачи = КомплектацияАвтомобилей.ДокументПередачи
					 |ГДЕ
					 |	РеализованныеАвтомобили.Регистратор = &Регистратор
					 |	И РеализованныеАвтомобили.ВидДвижения = &ВидДвиженияПриход";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияПриход",ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
		Выборка=Запрос.Выполнить().Выбрать();
		НаборЗаписейРеализованныеАвтомобили=Движения.РеализованныеАвтомобили;
		Пока Выборка.Следующий() Цикл
			Если Выборка.СуммаУпр<>0 ИЛИ Выборка.СуммаПродажи<>0 Тогда
				НоваяЗапись=НаборЗаписейРеализованныеАвтомобили.Добавить();
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период=ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗапись.Контрагент=Выборка.Контрагент;
				НоваяЗапись.Автомобиль=Выборка.Автомобиль;
				НоваяЗапись.ГТД=Выборка.ГТД;
				НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
				НоваяЗапись.ДокументПередачи=Выборка.ДокументПередачи;
				НоваяЗапись.Количество=0;
				НоваяЗапись.СуммаУпр=Выборка.СуммаУпр;
				НоваяЗапись.СуммаПродажи=Выборка.СуммаПродажи;
				НоваяЗапись.СуммаРегл=Выборка.СуммаРегл;
				НоваяЗапись.СуммаПродажиРегл=Выборка.СуммаПродажиРегл;
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗапись.Вознаграждение=0;
			КонецЕсли; 
		КонецЦикла;
		// Доходы и расходы
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	КомплектацияАвтомобилей.ВидДвижения,
		             |	СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр
		             |ИЗ
		             |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		             |ГДЕ
		             |	КомплектацияАвтомобилей.Регистратор = &Регистратор
		             |СГРУППИРОВАТЬ ПО
		             |	КомплектацияАвтомобилей.ВидДвижения";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Приход)) Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяОприходованияОбнаруженныхИзлишковТМЦ;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= Выборка.СуммаУпр;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Расход)) Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяСписанияОбнаруженнойНедостачиТМЦ;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Расход					= Выборка.СуммаУпр;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		// Доходы и расходы
		НаборЗаписейОстатки=Движения.ОстаткиАвтомобилей;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ОстаткиАвтомобилей.ВидДвижения,
		             |	СУММА(ОстаткиАвтомобилей.СуммаУпр) КАК СуммаУпр
		             |ИЗ
		             |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		             |ГДЕ
		             |	ОстаткиАвтомобилей.Регистратор = &Регистратор
		             |СГРУППИРОВАТЬ ПО
		             |	ОстаткиАвтомобилей.ВидДвижения";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Приход)) Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяОприходованияОбнаруженныхИзлишковТМЦ;
			КонецЕсли; 
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= Выборка.СуммаУпр;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Расход)) Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяСписанияОбнаруженнойНедостачиТМЦ;
			КонецЕсли; 
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Расход					= Выборка.СуммаУпр;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
	Иначе
		// Доходы и расходы
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	СУММА(АвтомобилиОтданные.СуммаУпр) КАК СуммаУпр,
		|	СУММА(АвтомобилиОтданные.СуммаСебестоимостиУпр) КАК СуммаСебестоимостиУпр
		|ИЗ
		|	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
		|ГДЕ
		|	АвтомобилиОтданные.Регистратор = &Регистратор
		|	И АвтомобилиОтданные.ВидДвижения = &ВидДвиженияРасход";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			//сумма себестоимости списанных товаров по недостачи
			НаборЗаписейДиР					= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект	= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяСписанияОбнаруженнойНедостачиТМЦ;
			КонецЕСли;
			НаборЗаписейДиР.ВУпрВалюте		= Истина;
			НаборЗаписейДиР.Расход			= Выборка.СуммаСебестоимостиУпр;	
			НаборЗаписейДиР.ШапкаДокумента	= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			//СуммаВсего - списанная сумма товарной задолженности
			НаборЗаписейДиР					= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект	= ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте		= Истина;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяОприходованияОбнаруженныхИзлишковТМЦ;
			КонецЕсли;
			НаборЗаписейДиР.Доход			= Выборка.СуммаУпр;
			НаборЗаписейДиР.ШапкаДокумента	= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); АвтомобильБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			АвтомобильБыл = Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("АвтомобильБыл",АвтомобильБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
		
	// двигаем границу последовательности комплектаций автомобилей
	ДвигаемГраницу = (ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей И Режим<>РежимПроведенияДокумента.Оперативный);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  = ШапкаДокумента.Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Автомобили.Итог("СуммаУчет");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
			Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
				Контрагент=Справочники.Контрагенты.ПустаяСсылка();
				Рез=ОбработкаРеквизита(Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		Иначе
			Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомиссионером И 
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбирать договор комиссии !",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Возврат Ложь;
			КонецЕсли; 
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании = Неопределено;
			КонецЕсли; 
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (Автомобили.Количество()>0) И (НЕ обЗначениеНеЗаполнено(СкладКомпании)) Тогда
			#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				Если Вопрос("Произвести пересчет расчетного количества автомобилей", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
					Для каждого СтрокаТоваров Из Автомобили Цикл
						Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",СтрокаТоваров,ЭтаФорма,ДопПараметры) И Рез;
					КонецЦикла; 
				КонецЕсли; 
			Иначе
				Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",СтрокаТоваров,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
			#Иначе
			Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			#КонецЕсли
		КонецЕсли; 
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		// получим расчетное количество
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Количество" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.КоличествоУчет" Тогда
		ТекСтрока.СуммаУчет=ТекСтрока.Цена*ТекСтрока.КоличествоУчет;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		ТекСтрока.СуммаУчет=ТекСтрока.Цена*ТекСтрока.КоличествоУчет;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаУчет" Тогда
		ТекСтрока.Цена=?(ТекСтрока.КоличествоУчет=0,0,ТекСтрока.СуммаУчет/ТекСтрока.КоличествоУчет);
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ПРОЧИЕ ОБРАБОТЧИКИ
	ИначеЕсли Имя="ПолучитьРасчетноеКоличество" Тогда
		// проверим
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда Возврат Ложь; КонецЕсли;
		Попытка флЗаполнятьБезНДС = ДопПараметры.флЗаполнятьБезНДС Исключение флЗаполнятьБезНДС = Ложь; КонецПопытки;
		// получаем в зависимости от вида хоз. операции
		Если ХозОперация<>Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
	        СтруктураОтбора=Новый Структура("СкладКомпании,Автомобиль");
			СтруктураОтбора.СкладКомпании=СкладКомпании; СтруктураОтбора.Автомобиль=ТекСтрока.Автомобиль;
			МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
			ТаблицаОстатков=РегистрыНакопления.ОстаткиАвтомобилей.Остатки(МоментВремени,СтруктураОтбора,,"СуммаУпр,Сумма,СуммаНДС,Количество");
			Если ТаблицаОстатков.Количество()>0 Тогда
				Если флЗаполнятьБезНДС Тогда
					СтавкаНДС = ?(ТаблицаОстатков.Итог("Сумма") = 0, 0, Окр(ТаблицаОстатков.Итог("СуммаНДС")/(ТаблицаОстатков.Итог("Сумма") - ТаблицаОстатков.Итог("СуммаНДС")) * 100, 2));
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаУпр") - Окр(ТаблицаОстатков.Итог("СуммаУпр") * СтавкаНДС / (100 + СтавкаНДС), 2);
				Иначе
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаУпр");
				КонецЕсли;
				ТекСтрока.Цена=обПересчет(СуммаОстаток,ВалютаУпр,Дата,ВалютаДокумента,КурсДокумента);
				ТекСтрока.Количество=ТаблицаОстатков.Итог("Количество");
			Иначе
				ТекСтрока.Цена=0;
				ТекСтрока.Количество=0;
			КонецЕсли;
			ТаблицаОстатков=РегистрыНакопления.КомплектацияАвтомобилей.Остатки(МоментВремени,СтруктураОтбора,,"СуммаУпр,Сумма,СуммаНДС,Количество");
			Если ТаблицаОстатков.Количество()>0 Тогда
				Если флЗаполнятьБезНДС Тогда
					СтавкаНДС = ?(ТаблицаОстатков.Итог("Сумма") = 0, 0, Окр(ТаблицаОстатков.Итог("СуммаНДС")/(ТаблицаОстатков.Итог("Сумма") - ТаблицаОстатков.Итог("СуммаНДС")) * 100, 2));
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаУпр") - Окр(ТаблицаОстатков.Итог("СуммаУпр") * СтавкаНДС / (100 + СтавкаНДС), 2);
				Иначе
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаУпр");
				КонецЕсли;
				ТекСтрока.Цена=ТекСтрока.Цена+обПересчет(СуммаОстаток,ВалютаУпр,Дата,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
		Иначе
	        СтруктураОтбора=Новый Структура("Контрагент,ДоговорВзаиморасчетов,Автомобиль");
			СтруктураОтбора.Контрагент=Контрагент; СтруктураОтбора.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
			СтруктураОтбора.Автомобиль=ТекСтрока.Автомобиль;
			МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
			ТаблицаОстатков=РегистрыНакопления.АвтомобилиОтданные.Остатки(МоментВремени,СтруктураОтбора,,"СуммаУпр,Сумма,СуммаНДС,Количество");
			Если ТаблицаОстатков.Количество()>0 Тогда
				Если флЗаполнятьБезНДС Тогда
					СтавкаНДС = ?(ТаблицаОстатков.Итог("Сумма") = 0, 0, Окр(ТаблицаОстатков.Итог("СуммаНДС")/(ТаблицаОстатков.Итог("Сумма") - ТаблицаОстатков.Итог("СуммаНДС")) * 100, 2));
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаУпр") - Окр(ТаблицаОстатков.Итог("СуммаУпр") * СтавкаНДС / (100 + СтавкаНДС), 2);
				Иначе
					СуммаОстаток = ТаблицаОстатков.Итог("СуммаУпр");
				КонецЕсли;
				ТекСтрока.Цена=обПересчет(СуммаОстаток,ВалютаУпр,Дата,ВалютаДокумента,КурсДокумента);
				ТекСтрока.Количество=ТаблицаОстатков.Итог("Количество");
	        Иначе
				ТекСтрока.Цена=0;
				ТекСтрока.Количество=0;
			КонецЕсли;
		КонецЕсли;
		// пересчет
		Рез=ОбработкаРеквизита("Автомобили.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТекСтрока.КоличествоУчет=0 Тогда ТекСтрока.КоличествоУчет=ТекСтрока.Количество; КонецЕсли;
		Возврат ОбработкаРеквизита("Автомобили.КоличествоУчет",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ИнвентаризацияАвтомобилей - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ВЫБОР
	|		КОГДА ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаТовараПоИнвентаризации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратУтерянногоТовара)
	|	КОНЕЦ КАК КодОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ОстаткиАвтомобилей.Автомобиль КАК Номенклатура,
	|	ОстаткиАвтомобилей.ГТД КАК РНПТ,
	|	ОстаткиАвтомобилей.Количество КАК КоличествоПрослеживаемости,
	|	ОстаткиАвтомобилей.Сумма - ОстаткиАвтомобилей.СуммаНДС КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &Ссылка
	|	И ОстаткиАвтомобилей.ГТД.РНПТ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим есть РНПТ у документа
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = РезультатЗапроса.Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
	
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = Дата;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ИнвентаризацияАвтомобилейНаКомиссии"
// Возвращает сформированный табличный документ:
Функция ПечатьИнвентаризацияАвтомобилейНаКомиссии(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИнвентаризацияАвтомобилейНаКомиссии");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);	
	
	//шапка печ. формы
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
		ОбластьМакета.Параметры.ТекстПокупатель = "Покупатель: ";
		ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(ЭтотОбъект.Контрагент);
		ОбластьМакета.Параметры.Покупатель = ЭтотОбъект.Контрагент;
	КонецЕсли;
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Количество,КоличествоУчет,КоличествоРазница,Сумма,СуммаУчет,СуммаРазница",ВалютаДокумента,0,0,0,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили;	
	//подготовим дополнительную таблицу
	СтруктураСтрокиТЧ = Новый Структура("НомерСтроки,Количество,КоличествоУчет,КоличествоРазница,Сумма,СуммаУчет,СуммаРазница,Цена");
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ЗаполнитьЗначенияСвойств(СтруктураСтрокиТЧ,СтрокаТабличнойЧасти);
		СтруктураСтрокиТЧ.КоличествоРазница = СтруктураСтрокиТЧ.КоличествоУчет - СтруктураСтрокиТЧ.Количество;
		СтруктураСтрокиТЧ.СуммаРазница = СтруктураСтрокиТЧ.СуммаУчет - СтруктураСтрокиТЧ.Сумма;		
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		ОбластьМакета.Параметры.КоличествоУчет = Формат(СтрокаТабличнойЧасти.КоличествоУчет,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.КоличествоРазница = Формат(СтруктураСтрокиТЧ.КоличествоРазница,ФорматВыводаКоличества);		
		ОбластьМакета.Параметры.СуммаУчет = Формат(СтрокаТабличнойЧасти.СуммаУчет,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаРазница = Формат(СтруктураСтрокиТЧ.СуммаРазница,ФорматВыводаСуммы);		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Количество,КоличествоУчет,КоличествоРазница,Сумма,СуммаУчет,СуммаРазница",ВалютаДокумента,0,0,0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиТЧ,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	КоличествоУчетВсего = ВыборкаТабличнойЧасти.Итог("КоличествоУчет");
	КоличествоВсего = ВыборкаТабличнойЧасти.Итог("Количество");
	СуммаУчетВсего = ВыборкаТабличнойЧасти.Итог("СуммаУчет");
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьМакета.Параметры.КоличествоУчетВсего = Формат(КоличествоУчетВсего,ФорматВыводаКоличества);
	ОбластьМакета.Параметры.КоличествоВсего = Формат(КоличествоВсего,ФорматВыводаКоличества);
	ОбластьМакета.Параметры.КоличествоРазницаВсего = Формат(КоличествоУчетВсего - КоличествоВсего,ФорматВыводаКоличества);	
	ОбластьМакета.Параметры.СуммаУчетВсего = Формат(СуммаУчетВсего,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаРазницаВсего = Формат(СуммаУчетВсего - СуммаВсего,ФорматВыводаСуммы);	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;	
КонецФункции

// Формирует печатную форму "ИНВ3"
// Возвращает сформированный табличный документ:
Функция ПечатьИНВ3(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ИНВ3");
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	ОбластьМакета.Параметры.Номер			= дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

	СтрокНаСтранице			= 24;
	СтрокШапки				= 5;
	СтрокПодвала			= 5;
	НомерСтраницы			= 2;
	Ном						= 1;
	
	ИтогКоличество			= 0;
	ИтогСумма				= 0;
	ИтогКоличествоУчет		= 0;
	ИтогСуммаУчет			= 0;
	
	КолвоСтрокПоСтранице	= 0;
	КолвоПоСтранице			= 0;
	СуммаЛиста				= 0;

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента;      
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Автомобили;
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();

	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда

			ОбластьИтоговПоСтранице                               = Макет.ПолучитьОбласть("ПодвалСтраницы");
			ОбластьИтоговПоСтранице.Параметры.ИтогоКоличество      = ИтогКоличество;
			ОбластьИтоговПоСтранице.Параметры.ИтогоСумма          = ИтогСумма;
			ОбластьИтоговПоСтранице.Параметры.ИтогоКоличествоУчет = ИтогКоличествоУчет;
			ОбластьИтоговПоСтранице.Параметры.ИтогоСуммаУчет      = ИтогСуммаУчет;

			ОбластьИтоговПоСтранице.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(КолвоСтрокПоСтранице, ,",,,,,,,,0");
			ОбластьИтоговПоСтранице.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(КолвоПоСтранице);
			ОбластьИтоговПоСтранице.Параметры.СуммаФактическиНаСтраницеПрописью                 = обЧислоПрописью(ИтогСуммаУчет,ЭтотОбъект.ВалютаДокумента);
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
			Ном =1;

			ИтогКоличество = 0;
			ИтогСумма      = 0;
			ИтогКоличествоУчет  = 0;
			ИтогСуммаУчет       = 0;

			КолвоСтрокПоСтранице = 0;
			КолвоПоСтранице      = 0;
			СуммаЛиста           = 0;

		КонецЕсли;

		ОбластьМакета.Параметры.Номер = Ном;
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		ОбластьМакета.Параметры.ТоварНаименование = спПолучитьНаименование(СтрокаТовар.Автомобиль);
		ОбластьМакета.Параметры.Номенклатура = СтрокаТовар.Автомобиль;
		ОбластьМакета.Параметры.ТоварКод = СтрокаТовар.Автомобиль.VIN;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = Справочники.КлассификаторЕдиницИзмерения.шт;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = 796;
		ОбластьМакета.Параметры.Номер=СтрокаТовар.НомерСтроки;
		ОбластьМакета.Параметры.Цена=СтрокаТовар.Цена;
		ОбластьМакета.Параметры.КоличествоУчет = СтрокаТовар.КоличествоУчет;
		ОбластьМакета.Параметры.Количество = СтрокаТовар.Количество;
		ОбластьМакета.Параметры.СуммаУчет = СтрокаТовар.СуммаУчет;
		ОбластьМакета.Параметры.Сумма = СтрокаТовар.Сумма;

		ТабДокумент.Вывести(ОбластьМакета);
		Ном = Ном + 1;

		ИтогКоличество     = ИтогКоличество     + СтрокаТовар.Количество;
		ИтогСумма          = ИтогСумма          + СтрокаТовар.Сумма;
		ИтогКоличествоУчет = ИтогКоличествоУчет + СтрокаТовар.КоличествоУчет;
		ИтогСуммаУчет      = ИтогСуммаУчет      + СтрокаТовар.СуммаУчет;

		КолвоСтрокПоСтранице = КолвоСтрокПоСтранице + 1;
		КолвоПоСтранице      = КолвоПоСтранице      + СтрокаТовар.КоличествоУчет;
		СуммаЛиста           = СуммаЛиста           + СтрокаТовар.СуммаУчет;

	КонецЦикла;

	// Выводим итоги по последней странице
	ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ПодвалСтраницы");
	ОбластьИтоговПоСтранице.Параметры.ИтогоКоличество     = ИтогКоличество;
	ОбластьИтоговПоСтранице.Параметры.ИтогоСумма          = ИтогСумма;
	ОбластьИтоговПоСтранице.Параметры.ИтогоКоличествоУчет = ИтогКоличествоУчет;
	ОбластьИтоговПоСтранице.Параметры.ИтогоСуммаУчет      = ИтогСуммаУчет;
	ОбластьИтоговПоСтранице.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(КолвоСтрокПоСтранице, ,",,,,,,,,0");
	ОбластьИтоговПоСтранице.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(КолвоПоСтранице);
	ОбластьИтоговПоСтранице.Параметры.СуммаФактическиНаСтраницеПрописью                 = обЧислоПрописью(ИтогСуммаУчет,ЭтотОбъект.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим подвал документа
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалОписи");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.НачальныйНомерПоПорядку = 1;
	ОбластьМакета.Параметры.НомерКонца              = ВыборкаСтрокТовары.Количество();
	ОбластьМакета.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(ВыборкаСтрокТовары.Итог("КоличествоУчет"));
	ОбластьМакета.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(ВыборкаСтрокТовары.Количество(), ,",,,,,,,,0");

	//если нет движений по регистрам то печатаем сумму документа
	СуммаНаПечать = ВыборкаСтрокТовары.Итог("СуммаУчет");
	ОбластьМакета.Параметры.СуммаФактическиНаСтраницеПрописью                 = обЧислоПрописью(СуммаНаПечать,ЭтотОбъект.ВалютаДокумента);
	
	// Заполним информацию о руководителях и ответственных
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	
	ПредседательКомиссии= дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ЧленКомиссии1		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ЧленКомиссии2		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ИНВ19"
// Возвращает сформированный табличный документ:
Функция ПечатьИНВ19(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ИНВ19");
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	ОбластьМакета.Параметры.Номер			= дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

	НомерСтраницы   = 2;

	// Инициализация итогов по документу
	ИтогоРезультатИзлишекКолво   = 0;
	ИтогоРезультатИзлишекСумма   = 0;
	ИтогоРезультатНедостачаКолво = 0;
	ИтогоРезультатНедостачаСумма = 0;

	// Инициализация итогов по странице
	ИтогоРезультатИзлишекКолвоПоСтранице   = 0;
	ИтогоРезультатИзлишекСуммаПоСтранице   = 0;
	ИтогоРезультатНедостачаКолвоПоСтранице = 0;
	ИтогоРезультатНедостачаСуммаПоСтранице = 0;
	
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента; 
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
		ВыборкаСтрокТовары = РегистрыНакопления.ОстаткиАвтомобилей.СоздатьНаборЗаписей();
		ПростоеСписание = ИСТИНА;
	ИначеЕсли ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
		ВыборкаСтрокТовары = РегистрыНакопления.АвтомобилиОтданные.СоздатьНаборЗаписей();
		ПростоеСписание = ЛОЖЬ;
	Иначе
		Сообщить("Для хоз операции "+ЭтотОбъект.ХозОперация.Наименование+" формирование ИНВ-19 не описано!");
		Возврат Неопределено;
	КонецЕсли; 
	ВыборкаСтрокТовары.Отбор.Регистратор.Значение = ЭтотОбъект.Ссылка;
	ВыборкаСтрокТовары.Прочитать();

	//определимся откуда будем брать многострочную часть документа
	//если движения есть, то из регистра иначе из табличной части документа
	ЕстьДвиженияПоРегистру = Ложь;
	Если ВыборкаСтрокТовары.Количество() > 0 Тогда
		ЕстьДвиженияПоРегистру = Истина;
	Иначе
		ВыборкаСтрокТовары = ЭтотОбъект.Автомобили;
	КонецЕсли; 
	КоличествоСтрок  = ВыборкаСтрокТовары.Количество();
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоТаблицыИтогиПоСтранице");
	ОбластьМакетаИтого = Макет.ПолучитьОбласть("ИтогоТаблицы");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		// Проверим, помещаются ли нужные области на странице
		Если СтрокаТовар.НомерСтроки = КоличествоСтрок Тогда
			// только на последнем шаге, когда нужно вывести все
			МассивОбластей = Новый Массив();
			МассивОбластей.Добавить(ОбластьМакета);
			МассивОбластей.Добавить(ОбластьМакетаИтогоПоСтранице);
			МассивОбластей.Добавить(ОбластьМакетаИтого);
			МассивОбластей.Добавить(ОбластьМакетаПодвал);			
		Иначе
			МассивОбластей = Новый Массив();			
			МассивОбластей.Добавить(ОбластьМакета);
			МассивОбластей.Добавить(ОбластьМакетаИтогоПоСтранице);
		КонецЕсли; 			
		
		Попытка
			Если Не ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
				ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолвоПоСтранице;
				ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатНедостачаСумма = ИтогоРезультатНедостачаСуммаПоСтранице;
				ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолвоПоСтранице;
				ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатИзлишекСумма   = ИтогоРезультатИзлишекСуммаПоСтранице;

				ИтогоРезультатНедостачаКолвоПоСтранице = 0;
				ИтогоРезультатНедостачаСуммаПоСтранице = 0;
				ИтогоРезультатИзлишекКолвоПоСтранице = 0;
				ИтогоРезультатИзлишекСуммаПоСтранице = 0;
				
				ТабДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);

				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		ОбластьМакета.Параметры.Номенклатура = СтрокаТовар.Автомобиль;
		ОбластьМакета.Параметры.ТоварНаименование = спПолучитьНаименование(СтрокаТовар.Автомобиль);
		ОбластьМакета.Параметры.ТоварКод = СтрокаТовар.Автомобиль.VIN;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = Справочники.КлассификаторЕдиницИзмерения.шт;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = 796;
		ОбластьМакета.Параметры.Номер = СтрокаТовар.НомерСтроки;

		Разница     = 0;
		РазницаСумм = 0;
		
		НеТребуетсяПересчетИзУпр = (ЭтотОбъект.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НеТребуетсяПересчетИзРегл = (ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
		
		Если ЕстьДвиженияПоРегистру Тогда
			Если ПростоеСписание Тогда
				Если СтрокаТовар.СкладКомпании.Розничный Тогда
					РазницаСумм = СтрокаТовар.СуммаРозн;
					Если Не НеТребуетсяПересчетИзРегл Тогда
						РазницаСумм = обПересчет(РазницаСумм, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
					КонецЕсли;
				Иначе
					Если НеТребуетсяПересчетИзРегл Тогда
						РазницаСумм = СтрокаТовар.Сумма;
					Иначе
						РазницаСумм = СтрокаТовар.СуммаУпр;
						Если НЕ НеТребуетсяПересчетИзУпр Тогда
							РазницаСумм = обПересчет(РазницаСумм, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если НеТребуетсяПересчетИзРегл Тогда
					РазницаСумм = СтрокаТовар.Сумма;
				Иначе
					РазницаСумм = СтрокаТовар.СуммаУпр;
					Если НЕ НеТребуетсяПересчетИзУпр Тогда
						РазницаСумм = обПересчет(РазницаСумм, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
			Разница = СтрокаТовар.Количество;
			Если СтрокаТовар.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
				Разница     = -Разница;
				РазницаСумм = -РазницаСумм;
			КонецЕсли; 
		Иначе
			Разница     = СтрокаТовар.КоличествоУчет - СтрокаТовар.Количество;
			РазницаСумм = СтрокаТовар.СуммаУчет - СтрокаТовар.Сумма;
		КонецЕсли;
		
		Если Разница = 0 Тогда
			Продолжить;
		КонецЕсли;

		Если Разница < 0 Тогда
			ОбластьМакета.Параметры.РезультатНедостачаКолво = -Разница;
			ОбластьМакета.Параметры.РезультатНедостачаСумма = -РазницаСумм;
			ОбластьМакета.Параметры.РезультатИзлишекКолво   = 0;
			ОбластьМакета.Параметры.РезультатИзлишекСумма   = 0;

			ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолво + (-Разница);
			ИтогоРезультатНедостачаСумма = ИтогоРезультатНедостачаСумма + (-РазницаСумм);
			ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолво   + 0;
			ИтогоРезультатИзлишекСумма   = ИтогоРезультатИзлишекСумма   + 0;
			
			ИтогоРезультатНедостачаКолвоПоСтранице = ИтогоРезультатНедостачаКолвоПоСтранице + (-Разница);
			ИтогоРезультатНедостачаСуммаПоСтранице = ИтогоРезультатНедостачаСуммаПоСтранице + (-РазницаСумм);
			ИтогоРезультатИзлишекКолвоПоСтранице   = ИтогоРезультатИзлишекКолвоПоСтранице   + 0;
			ИтогоРезультатИзлишекСуммаПоСтранице   = ИтогоРезультатИзлишекСуммаПоСтранице   + 0;
		Иначе
			ОбластьМакета.Параметры.РезультатНедостачаКолво = 0;
			ОбластьМакета.Параметры.РезультатНедостачаСумма = 0;
			ОбластьМакета.Параметры.РезультатИзлишекКолво   = Разница;
			ОбластьМакета.Параметры.РезультатИзлишекСумма   = РазницаСумм;

			ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолво + 0;
			ИтогоРезультатНедостачаСумма = ИтогоРезультатНедостачаСумма + 0;
			ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолво   + Разница;
			ИтогоРезультатИзлишекСумма   = ИтогоРезультатИзлишекСумма   + РазницаСумм;
			
			ИтогоРезультатНедостачаКолвоПоСтранице = ИтогоРезультатНедостачаКолвоПоСтранице + 0;
			ИтогоРезультатНедостачаСуммаПоСтранице = ИтогоРезультатНедостачаСуммаПоСтранице + 0;
			ИтогоРезультатИзлишекКолвоПоСтранице   = ИтогоРезультатИзлишекКолвоПоСтранице   + Разница;
			ИтогоРезультатИзлишекСуммаПоСтранице   = ИтогоРезультатИзлишекСуммаПоСтранице   + РазницаСумм;
		КонецЕсли; 

		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	Если НомерСтраницы > 2 Тогда
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолвоПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатИзлишекСумма   = ИтогоРезультатИзлишекСуммаПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолвоПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоРезультатНедостачаСумма = ИтогоРезультатНедостачаСуммаПоСтранице;
		ТабДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);
		ОбластьМакетаИтого = Макет.ПолучитьОбласть("ИтогоТаблицы2");
	КонецЕсли;
	
	ОбластьМакетаИтого.Параметры.ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолво;
	ОбластьМакетаИтого.Параметры.ИтогоРезультатИзлишекСумма   = ИтогоРезультатИзлишекСумма;
	ОбластьМакетаИтого.Параметры.ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолво;
	ОбластьМакетаИтого.Параметры.ИтогоРезультатНедостачаСумма = ИтогоРезультатНедостачаСумма;
	ТабДокумент.Вывести(ОбластьМакетаИтого);

	ОбластьМакета = ОбластьМакетаПодвал;
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	// Заполним информацию о руководителях и ответственных
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "Пустографка"
// Возвращает сформированный табличный документ:
Функция ПечатьИнвентаризацияАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИнвентаризацияАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Организация);
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
		ОбластьМакета.Параметры.ТекстКонтрагентСклад="Склад:";
		ОбластьМакета.Параметры.ПредставлениеКонтрагентаСклада = спПолучитьНаименование(СкладКомпании);
		ОбластьМакета.Параметры.КонтрагентСклад = СкладКомпании;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
		ОбластьМакета.Параметры.ТекстКонтрагентСклад="Комиссионер:";
		ОбластьМакета.Параметры.ПредставлениеКонтрагентаСклада = спПолучитьПредставление(ЭтотОбъект.Контрагент);
		ОбластьМакета.Параметры.КонтрагентСклад = ЭтотОбъект.Контрагент;
	КонецЕсли; 
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы,,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт

	//Если НЕ ЭтотОбъект.Проведен Тогда
	//	Сообщить("Печать непроведенного документа невозможна.");
	//	Возврат Неопределено;
	//КонецЕсли;
	
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ТипЦен=обПраво("ОсновнойТипЦенЗакупкиАвтомобилей",Права,,ЭтотОбъект);
	КонецЕсли;
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если (ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию) И 
		 (НЕ обЗначениеНеЗаполнено(СкладКомпании)) Тогда
		 СкладКомпании=Неопределено;
	КонецЕсли; 
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	// если мы проводим документ задним числом, то пересчитаем количество расчетное
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Запрос=Новый Запрос;
		Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
			ТекстЗапроса="
			|ВЫБРАТЬ
			|	ВЫБОР 
			|		КОГДА СУММА(ОстаткиАвтомобилей.КоличествоОстаток) ЕСТЬ NULL
			|		ТОГДА 0
			|		ИНАЧЕ СУММА(ОстаткиАвтомобилей.КоличествоОстаток)
			|		КОНЕЦ
			|	КАК Количество
			|ИЗ
			|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&Момент,Автомобиль=&Автомобиль И СкладКомпании=&СкладКомпании) КАК ОстаткиАвтомобилей
			|";
			Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
		Иначе
			ТекстЗапроса="
			|ВЫБРАТЬ
			|	ВЫБОР 
			|		КОГДА СУММА(АвтомобилиОтданные.КоличествоОстаток) ЕСТЬ NULL
			|		ТОГДА 0
			|		ИНАЧЕ СУММА(АвтомобилиОтданные.КоличествоОстаток)
			|		КОНЕЦ
			|	КАК Количество
			|ИЗ
			|	РегистрНакопления.АвтомобилиОтданные.Остатки(&Момент,Контрагент=&Контрагент И Автомобиль=&Автомобиль И ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов) КАК АвтомобилиОтданные
			|";
			Запрос.УстановитьПараметр("Контрагент",Контрагент);
			Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		КонецЕсли;
		Запрос.Текст=ТекстЗапроса;
		ГраницаРасчета=Новый Граница(МоментВремени(),ВидГраницы.Исключая);
		Запрос.УстановитьПараметр("Момент",?(ЭтоНовый(),КонецДня(ТекущаяДата()),ГраницаРасчета));
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			Запрос.УстановитьПараметр("Автомобиль",СтрокаТЧ.Автомобиль);
			Результат=Запрос.Выполнить();
			Если Результат.Пустой() Тогда
				СтрокаТЧ.Количество=0;
			Иначе
				СтрокаТЧ.Количество=Результат.Выгрузить().Получить(0).Количество;
			КонецЕсли;
			ОбработкаРеквизита("Автомобили.Количество",СтрокаТЧ);
		КонецЦикла;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	СуммаДокумента=РассчитатьСуммуВсего();
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//проверим, что автомобиль отсутствует на остатках по различным регистрам
	Если НЕ РегистрыНакопления.ОстаткиАвтомобилей.ПроверитьОстаткиАвтомобилей(ЭтотОбъект,Отказ) Тогда 
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);		
		Возврат; 
	КонецЕсли;
	
	// у нас не стандартная таблица, поэтому сформирует свой результат запроса по автомобилям
	РезультатЗапросаПоАвтомобилямИзлишки=ПолучитьРезультатЗапросаПоАвтомобилям(Истина);
	РезультатЗапросаПоАвтомобилямНедостачи=ПолучитьРезультатЗапросаПоАвтомобилям(Ложь);
	
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
		Отказ = ПроверитьЗаказыНаАвтомобиль(РезультатЗапросаПоАвтомобилямНедостачи.Выгрузить().ВыгрузитьКолонку("Автомобиль")) ИЛИ Отказ;
	КонецЕсли;
	
	// определим вид проводки
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей Тогда
		// 1. Приходуем излишки
		НаборЗаписейОстатки=Движения.ОстаткиАвтомобилей;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилямИзлишки;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
		НаборЗаписейОстатки.Партия=Ссылка;
		//НаборЗаписейОстатки.Приходовать=Истина;
		//НаборЗаписейОстатки.Резервировать=Ложь;
		Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
		
		// 2. Списываем недостачи
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилямНедостачи;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.СтатусПартии=Новый Массив;
		НаборЗаписейОстатки.СтатусПартии.Добавить(Перечисления.СтатусыПартий.ТоварКупленный);
		НаборЗаписейОстатки.СтатусПартии.Добавить(Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		НаборЗаписейОстатки.Партия=Ссылка;
		//НаборЗаписейОстатки.Приходовать=Истина;
		//НаборЗаписейОстатки.Резервировать=Ложь;
		Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
		// 3. Зачитываем принятые на реализацию автомобили, которые были списаны инвентаризацией
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеАвтомобили;
		НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Списание=Истина;
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилямНедостачи;
		Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
		
		Если НЕ Отказ Тогда
			НаборЗаписейОстатки.Записать();
			НаборЗаписейРеализованныеТовары.Записать();
		КонецЕсли; 
		
	Иначе
		// 1. Приходуем излишки
		НаборЗаписейАвтомобилиОтданные=Движения.АвтомобилиОтданные;
		НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
		НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		НаборЗаписейАвтомобилиОтданные.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилямИзлишки;
		НаборЗаписейАвтомобилиОтданные.ПередаватьНаКомиссиюВсеАвтомобили=Истина;
		Отказ=НЕ НаборЗаписейАвтомобилиОтданные.Приход() ИЛИ Отказ;
		// 2. Списываем недостачи
		НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
		НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		НаборЗаписейАвтомобилиОтданные.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилямНедостачи;
		НаборЗаписейАвтомобилиОтданные.ПередаватьНаКомиссиюВсеАвтомобили=Истина;
		Отказ=НЕ НаборЗаписейАвтомобилиОтданные.Расход() ИЛИ Отказ;
		
		Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,МоментВремени());
			КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр        = КурсВалютыУпр;
		КонецЕсли;
		тзАвтомобилиОтданные = НаборЗаписейАвтомобилиОтданные.Выгрузить();
		тзАвтомобилиОтданные.Свернуть("ВидДвижения","СуммаУпр,СуммаСебестоимостиУпр");
		
		// 3. Взаиморасчеты за излишний товар.
		СтрокаТЗ = тзАвтомобилиОтданные.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
		Если СтрокаТЗ <> Неопределено Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
			Если БалансВедетсяПоПодразделениям Тогда 
				НаборЗаписейДиР.Подразделение = СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= СтатьяОприходованияОбнаруженныхИзлишковТМЦ;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= СтрокаТЗ.СуммаУпр;
			//НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		// 4. Взаиморасчеты за утраченный товар.
		СтрокаТЗ = тзАвтомобилиОтданные.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
		Если СтрокаТЗ <> Неопределено Тогда
			НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
			НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
			НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
			НаборЗаписейВзаиморасчеты.Сумма=обПересчет(СтрокаТЗ.СуммаУпр,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
			Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			
			// доходы и расходы по суммовым разницам
			СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
					НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
				Иначе
					НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			НаборЗаписейАвтомобилиОтданные.Записать();
		КонецЕсли; 
		
	КонецЕсли;
	
	// Проверим наличие прослеживаемых товаров, которые были реализованы
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// двигаем границу последовательности автомобилей
	ДвигаемГраницу = (ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилей И Режим<>РежимПроведенияДокумента.Оперативный);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// сбросим переменные
	РезультатЗапросаПоАвтомобилямИзлишки=Неопределено;
	РезультатЗапросаПоАвтомобилямНедостачи=Неопределено;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();

// Модуль документа КорректировкаЗаказаПокупателя

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
									 
									 
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ВидОплаты",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
		
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты      = Новый Структура();			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДокументОснование", Ложь);
			КонецЕсли;
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки", КонтролируемыеРеквизитыШапки);  			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("КорректировкаЗаказаПокупателя","Корректировка заказа покупателя",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "ДокументОснование" Тогда
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Контрагент = ?(обЗначениеНеЗаполнено(ДокументОснование), Неопределено, ДокументОснование.ПодразделениеПолучатель);
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
			ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Истина;
			СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
			Для Каждого ТекСтрока Из Товары Цикл
				ТекСтрока.СкидкаНаТовар = СкидкаНаценка;
				//ТекСтрока.СтавкаНДС     = Справочники.СтавкиНДС.БезНДС;
				ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
				ОбработкаРеквизита("Товары.СтавкаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
			КонецЦикла;
			ОбработкаРеквизита("РассчитатьСкидки", , ЭтаФорма, ДопПараметры);
		Иначе
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				Контрагент    = ДокументОснование.Контрагент;
				СкидкаНаценка = ДокументОснование.СкидкаНаценка;
			КонецЕсли;
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Ложь;
			ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОбновитьПодборЗамен();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.СтавкаНДС" Тогда
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") И ЗначениеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Заполняет табличную часть "Товары" по остаткам
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьТоварыПоОстаткам(ЗаказПокупателя) Экспорт
	
	Товары.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказыПокупателейОстатки.Номенклатура,
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	|	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК Заказано,
	|	ЗаказыПокупателейОстатки.СуммаОстаток    КАК Сумма,
	|	ЗаказыПокупателейОстатки.СуммаУпрОстаток КАК СуммаУпр,
	|	ЗаказыПокупателейОстатки.РезервОстаток   КАК Резерв
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+", ) КАК ЗаказыПокупателейОстатки
	|ГДЕ
	|	ЗаказыПокупателейОстатки.Заказ = &Заказ";
	
	ГраницаВремени = ?(Проведен, Новый Граница(МоментВремени(), ВидГраницы.Исключая), МоментВремени());
	Запрос.УстановитьПараметр("НаМомент",   ГраницаВремени);
	Запрос.УстановитьПараметр("Заказ",      ЗаказПокупателя);
	ТабОстатковЗаказов = Запрос.Выполнить().Выгрузить();
	
	ОснованиеЗаказПокупателя = (ТипЗнч(ЗаказПокупателя) = Тип("ДокументСсылка.ЗаказПокупателя"));
	
	Если ОснованиеЗаказПокупателя Тогда
		ТзТоваров    = ЗаказПокупателя.Товары.Выгрузить();
		ВалютаЗаказа = ЗаказПокупателя.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	КонецЕсли;
	
	Для Каждого стрОстатков Из ТабОстатковЗаказов Цикл
		Если ОснованиеЗаказПокупателя Тогда
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",стрОстатков.Номенклатура,стрОстатков.ХарактеристикаНоменклатуры);
			
			МассивСтрок = ТзТоваров.НайтиСтроки(СтруктураПоиска);
			Если МассивСтрок.Количество() = 1 Тогда
				ПроцентСкидкиШапки  = МассивСтрок[0].ПроцентСкидки;
				ПроцентСкидкиСтроки = МассивСтрок[0].ПроцентСкидкиСтроки;
				СкидкаСтроки        = МассивСтрок[0].СкидкаНаТовар;
			ИначеЕсли МассивСтрок.Количество() > 1 Тогда
				СкидкаСтроки = Справочники.ТипыСкидок.ПустаяСсылка();
				СуммаСкидкиСтрокиВсего = 0;
				СуммаСкидкиШапкиВсего = 0;
				СуммаВсего = 0;
				Для Каждого стрМассива Из МассивСтрок Цикл
					Если обЗначениеНеЗаполнено(СкидкаСтроки) И НЕ обЗначениеНеЗаполнено(стрМассива.СкидкаНаТовар) Тогда
						СкидкаСтроки = стрМассива.СкидкаНаТовар;
					КонецЕсли;
					
					СуммаВсего             = СуммаВсего + стрМассива.СуммаВсего;
					СуммаСкидкиШапкиВсего  = СуммаСкидкиШапкиВсего + стрМассива.СуммаСкидки;
					СуммаСкидкиСтрокиВсего = СуммаСкидкиСтрокиВсего + стрМассива.СуммаСкидкиСтроки;
				КонецЦикла;
				
				СуммаСоСкидкой = СуммаВсего + СуммаСкидкиШапкиВсего + СуммаСкидкиСтрокиВсего;
				
				Если СуммаСоСкидкой = 0 Тогда
					ПроцентСкидкиШапки  = 0;
					ПроцентСкидкиСтроки = 0;
				Иначе
					ПроцентСкидкиШапки  = (СуммаСкидкиШапкиВсего*100)/СуммаСоСкидкой;
					ПроцентСкидкиСтроки = (СуммаСкидкиСтрокиВсего*100)/СуммаСоСкидкой;
				КонецЕсли;
			Иначе
				ПроцентСкидкиШапки  = 0;
				ПроцентСкидкиСтроки = 0;
				СкидкаСтроки        = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура               = стрОстатков.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = стрОстатков.ХарактеристикаНоменклатуры;
		ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
		Если (НЕ НоваяСтрока.Коэффициент = 0) И (НЕ НоваяСтрока.Коэффициент = 1) Тогда
			НоваяСтрока.Количество = Окр(стрОстатков.Заказано/НоваяСтрока.Коэффициент, 3);
		Иначе
			НоваяСтрока.Количество = стрОстатков.Заказано;
		КонецЕсли;
		ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
		
		Если ОснованиеЗаказПокупателя Тогда
			НоваяСтрока.СкидкаНаТовар       = СкидкаСтроки;
			НоваяСтрока.ПроцентСкидки       = ПроцентСкидкиШапки;
			НоваяСтрока.ПроцентСкидкиСтроки = ПроцентСкидкиСтроки;
			ОбработкаРеквизита("РассчитатьСкидки",НоваяСтрока);
		КонецЕсли;
		
		Если ОснованиеЗаказПокупателя И ВалютаЗаказа = ВалютаДокумента Тогда
			НоваяСтрока.СуммаВсего = стрОстатков.Сумма;
		Иначе
			НоваяСтрока.СуммаВсего = обПересчет(стрОстатков.СуммаУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), Дата, КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьТоварыПоОстаткам()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "КорректировкаЗаказаПокупателя"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКорректировкаЗаказаПокупателя(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("КорректировкаЗаказаПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод образца заполнения платёжного поручения
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета");
	ОбластьМакета.Параметры.БанкПолучателя						= Организация.ОсновнойБанковскийСчет.Банк;
	ОбластьМакета.Параметры.БанкПолучателяПредставление			= спПолучитьПредставление(Организация,Новый Структура("Банк"));
	ОбластьМакета.Параметры.БИКБанкаПолучателя					= спПолучитьПредставление(Организация,Новый Структура("БИК"));
	ОбластьМакета.Параметры.СчетБанкаПолучателя					= спПолучитьПредставление(Организация,Новый Структура("КоррСчет"));
	ОбластьМакета.Параметры.ИНН									= спПолучитьПредставление(Организация,Новый Структура("ИНН"));
	ОбластьМакета.Параметры.КПП									= спПолучитьКППДляПечати(Организация, ПодразделениеКомпании);
	ОбластьМакета.Параметры.Получатель							= Организация;
	ОбластьМакета.Параметры.ПолучательПредставление				= спПолучитьПредставление(Организация,Новый Структура("Наименование"));
	ОбластьМакета.Параметры.СчетПолучателя						= спПолучитьПредставление(Организация,Новый Структура("БанковскийСчет"));
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтруктураПредставления=Новый Структура(
		"ИНН,КПП,Наименование,АдресЮридический,ТелефонРабочий",
		"ИНН ","КПП ","","","тел.: "
	);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация,
		СтруктураПредставления, ДополнительныеПараметры);
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = дкПолучитьПредставление(ДокументОснование);
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета.Параметры.ДокументОснованиеПредставление	= "<заказ не выбран>"
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ОбластьМакета.Параметры.Контрагент = ДокументОснование.Контрагент;
		ОбластьМакета.Параметры.КонтрагентПредставление	= спПолучитьПредставление(ДокументОснование.Контрагент,СтруктураПредставления);
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		ОбластьМакета.Параметры.Контрагент = ДокументОснование.ПодразделениеПолучатель;
		ОбластьМакета.Параметры.КонтрагентПредставление	= спПолучитьНаименование(ДокументОснование.ПодразделениеПолучатель);
	КонецЕсли;

	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") > 0 Тогда
		СуммаСкидки = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СуммаСкидки,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительОрганизация");
	Исполнитель.ИсполнительОрганизацияПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительОрганизация),"","/ "+ Исполнитель.ИсполнительОрганизацияПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик    = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикКонтрагент");
	Заказчик.ЗаказчикКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикКонтрагент),"","/ "+ Заказчик.ЗаказчикКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьКорректировкаЗаказаПокупателя()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,,ТипЗнч(ДокументОснование)<>Тип("ДокументСсылка.ЗаказПокупателя")) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Ложь;
		ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Ложь;
		
		ЗаказПокупателя = Основание;
		Если Основание.Проведен Тогда
			// Заполним табличные части неотгруженными товарами по заказу покупателя.
			ЗаполнитьТоварыПоОстаткам(ЗаказПокупателя);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		Контрагент = Основание.ПодразделениеПолучатель;
		ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
		ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Истина;
	КонецЕсли;

КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПоставщика
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Корректируем заказ покупателя
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.СкладКомпании = Справочники.СкладыКомпании.ПустаяСсылка();
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Неопределено;
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		//Если это заказ покупателя
		НаборЗаписейЗаказыПокупателей.Контрагент = ДокументОснование.Контрагент;
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		//Если это внутренний заказ
		НаборЗаписейЗаказыПокупателей.Контрагент = ДокументОснование.ПодразделениеПолучатель;
	Иначе
		дкСообщитьРезультат("Недопустимый документ-основание для корректировки заказа.",,ЭтотОбъект);
		Отказ=Истина;
	КонецЕсли; 
	НаборЗаписейЗаказыПокупателей.Заказ = ДокументОснование;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.КорректировкаЗаказаПокупателя() ИЛИ Отказ;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		СуммаКорректировки = НаборЗаписейЗаказыПокупателей.Итог("Сумма");
		ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, ДокументОснование, СуммаКорректировки);
		Если ТаблицаДолгов.Количество() > 0 Тогда
			СтрокаДолга = ТаблицаДолгов[0];
			СуммаЗаказа       = Формат(СтрокаДолга.СуммаЗаказа,       "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(СтрокаДолга.Валюта);
			Предоплата        = Формат(СтрокаДолга.Предоплата,        "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(СтрокаДолга.Валюта);
			ПроцентПредоплаты = Формат(СтрокаДолга.ПроцентПредоплаты, "ЧЦ=15; ЧДЦ=2; ЧН=0") + "%";
			
			Сообщить("Стоимость заказа после корректировки составит: " + СуммаЗаказа + ", внесена предоплата " + Предоплата + ", установленный процент предоплаты " + ПроцентПредоплаты + ".",СтатусСообщения.Информация);
			
		КонецЕсли;
	КонецЕсли;
	
	//Снимаем распределение заказов покупателя
	РезультатЗакрытияЗаказов = НаборЗаписейЗаказыПокупателей.Выгрузить();
	РезультатЗакрытияЗаказов.Свернуть("Номенклатура, ХарактеристикаНоменклатуры", "Заказано");
	
	КолонкаКоличества = РезультатЗакрытияЗаказов.Колонки.Найти("Заказано");
	КолонкаКоличества.Имя = "Количество";
	
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗакрытияЗаказов;
	НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = ДокументОснование;
	НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
	Отказ=НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		//Корректировка распределения других заказов, на внутреннем заказе
		НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
		НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
		НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗакрытияЗаказов;
		НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = Неопределено;
		НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = ДокументОснование;
		Отказ=НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки = Истина;
#КонецЕсли 
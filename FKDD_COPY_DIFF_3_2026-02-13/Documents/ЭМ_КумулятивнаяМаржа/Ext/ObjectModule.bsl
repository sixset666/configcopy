// Модуль документа ПланФакт

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЭМ_КумулятивнаяМаржа","Кумулятивная маржа",Истина);
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Показатели.Показатель" Тогда
		
		Если ТекСтрока <> Неопределено
			И ТекСтрока.Показатель.АвтоматическийРасчет Тогда
			
			Если ТекСтрока.Показатель = Справочники.ЭМ_ПоказателиКумулятивнойМаржи.МаржаАвтомобиля Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ОстаткиАвтомобилейОстатки.Партия КАК Партия
				|ПОМЕСТИТЬ ВТПартияАвтомобиля
				|ИЗ
				|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&ДатаОстатка, ) КАК ОстаткиАвтомобилейОстатки
				|ГДЕ
				|	ОстаткиАвтомобилейОстатки.Автомобиль = &Автомобиль
				|	И ОстаткиАвтомобилейОстатки.СкладКомпании.Организация = &Организация
				|	И ОстаткиАвтомобилейОстатки.КоличествоОстаток = 1
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВЫБОР
				|		КОГДА ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				|			ТОГДА -ОстаткиАвтомобилей.Сумма
				|		ИНАЧЕ ОстаткиАвтомобилей.Сумма
				|	КОНЕЦ КАК Сумма,
				|	ВЫБОР
				|		КОГДА ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				|			ТОГДА -ОстаткиАвтомобилей.СуммаНДС
				|		ИНАЧЕ ОстаткиАвтомобилей.СуммаНДС
				|	КОНЕЦ КАК СуммаНДС
				|ПОМЕСТИТЬ ВТМаржа
				|ИЗ
				|	ВТПартияАвтомобиля КАК ПартияАвтомобиля
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				|		ПО ПартияАвтомобиля.Партия = ОстаткиАвтомобилей.Партия
				|			И (ОстаткиАвтомобилей.Автомобиль = &Автомобиль)
				|			И (ОстаткиАвтомобилей.СкладКомпании.Организация = &Организация)
				|			И (ОстаткиАвтомобилей.Регистратор <> &РеализацияАвтомобилей)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	РеализацияАвтомобилейАвтомобили.СуммаВсего,
				|	РеализацияАвтомобилейАвтомобили.СуммаНДС
				|ИЗ
				|	ВТПартияАвтомобиля КАК ПартияАвтомобиля
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
				|		ПО (РеализацияАвтомобилейАвтомобили.Ссылка = &РеализацияАвтомобилей)
				|			И (РеализацияАвтомобилейАвтомобили.Автомобиль = &Автомобиль)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЕСТЬNULL(СУММА(Маржа.Сумма), 0) КАК Сумма,
				|	ЕСТЬNULL(СУММА(Маржа.СуммаНДС), 0) КАК СуммаНДС
				|ИЗ
				|	ВТМаржа КАК Маржа";
				
				Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
				Запрос.УстановитьПараметр("Организация", Организация);
				Запрос.УстановитьПараметр("ДатаОстатка", Новый Граница(ДокументОснование.МоментВремени(), ВидГраницы.Исключая));
				Запрос.УстановитьПараметр("РеализацияАвтомобилей", ДокументОснование);
				Результат = Запрос.Выполнить();
				Если НЕ Результат.Пустой() Тогда
					Выборка = Результат.Выбрать();
					Выборка.Следующий();
					Если ТекСтрока.Сумма <> Выборка.Сумма Тогда
						ТекСтрока.Сумма = Выборка.Сумма;
					КонецЕсли;
					Если ТекСтрока.СуммаНДС <> Выборка.СуммаНДС Тогда
						ТекСтрока.СуммаНДС = Выборка.СуммаНДС;
					КонецЕсли;
				Иначе
					Если ТекСтрока.Сумма <> 0 Тогда
						ТекСтрока.Сумма = 0;
					КонецЕсли;
					Если ТекСтрока.СуммаНДС <> 0 Тогда
						ТекСтрока.СуммаНДС = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ТекСтрока.СуммаБезНДС <> ТекСтрока.Сумма - ТекСтрока.СуммаНДС Тогда
					ТекСтрока.СуммаБезНДС = ТекСтрока.Сумма - ТекСтрока.СуммаНДС;
				КонецЕсли;
				
			ИначеЕсли ТекСтрока.Показатель = Справочники.ЭМ_ПоказателиКумулятивнойМаржи.МаржаДопоборудования Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ОстаткиАвтомобилейОстатки.Партия КАК Партия
				|ПОМЕСТИТЬ ВТПартияАвтомобиля
				|ИЗ
				|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&ДатаОстатка, ) КАК ОстаткиАвтомобилейОстатки
				|ГДЕ
				|	ОстаткиАвтомобилейОстатки.Автомобиль = &Автомобиль
				|	И ОстаткиАвтомобилейОстатки.СкладКомпании.Организация = &Организация
				|	И ОстаткиАвтомобилейОстатки.КоличествоОстаток = 1
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	МАКСИМУМ(ОстаткиАвтомобилей.Период) КАК Период
				|ПОМЕСТИТЬ ВТНачалоПериода
				|ИЗ
				|	ВТПартияАвтомобиля КАК ПартияАвтомобиля
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				|		ПО ПартияАвтомобиля.Партия = ОстаткиАвтомобилей.Регистратор
				|			И (ОстаткиАвтомобилей.Автомобиль = &Автомобиль)
				|			И (ОстаткиАвтомобилей.СкладКомпании.Организация = &Организация)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ОстаткиАвтомобилей.Регистратор КАК Регистратор
				|ПОМЕСТИТЬ ВТСписокЗаказНаряды
				|ИЗ
				|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачалоПериода КАК НачалоПериода
				|		ПО ОстаткиАвтомобилей.Период > НачалоПериода.Период
				|			И (ОстаткиАвтомобилей.Период <= &ДатаОстатка)
				|ГДЕ
				|	ОстаткиАвтомобилей.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказНаряд)
				|	И ОстаткиАвтомобилей.Автомобиль = &Автомобиль
				|	И ОстаткиАвтомобилей.СкладКомпании.Организация = &Организация
				|	И ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Продажи.Сумма КАК Сумма,
				|	Продажи.СуммаНДС КАК СуммаНДС,
				|	Продажи.Себестоимость КАК Себестоимость,
				|	Продажи.СуммаНДСВходящий КАК СебестоимостьНДС
				|ПОМЕСТИТЬ ВТЗаказНаряды
				|ИЗ
				|	РегистрНакопления.Продажи КАК Продажи
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачалоПериода КАК НачалоПериода
				|		ПО Продажи.Период > НачалоПериода.Период
				|			И (Продажи.Период <= &ДатаОстатка)
				|ГДЕ
				|	Продажи.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказНаряд)
				|	И Продажи.ДокументПродажи ССЫЛКА Документ.ЗаказНаряд
				|	И ВЫРАЗИТЬ(Продажи.ДокументПродажи КАК Документ.ЗаказНаряд).ВидРемонта = ЗНАЧЕНИЕ(Справочник.ВидыРемонта.ДополнительноеОборудование)
				|	И ВЫРАЗИТЬ(Продажи.ДокументПродажи КАК Документ.ЗаказНаряд).Автомобиль = &Автомобиль
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ОстаткиАвтомобилей.Сумма,
				|	ОстаткиАвтомобилей.СуммаНДС,
				|	0,
				|	0
				|ИЗ
				|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				|ГДЕ
				|	ОстаткиАвтомобилей.Регистратор В
				|			(ВЫБРАТЬ
				|				ЗаказНаряды.Регистратор
				|			ИЗ
				|				ВТСписокЗаказНаряды КАК ЗаказНаряды)
				|	И ОстаткиАвтомобилей.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказНаряд)
				|	И ОстаткиАвтомобилей.Автомобиль = &Автомобиль
				|	И ОстаткиАвтомобилей.СкладКомпании.Организация = &Организация
				|	И ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	0,
				|	0,
				|	ТоварыВПроизводстве.Сумма,
				|	ТоварыВПроизводстве.СуммаНДС
				|ИЗ
				|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
				|ГДЕ
				|	ТоварыВПроизводстве.ЗаказНаряд В
				|			(ВЫБРАТЬ
				|				ЗаказНаряды.Регистратор
				|			ИЗ
				|				ВТСписокЗаказНаряды КАК ЗаказНаряды)
				|	И ТоварыВПроизводстве.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказНаряд)
				|	И ТоварыВПроизводстве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	0,
				|	0,
				|	КомплектацияАвтомобилей.Сумма,
				|	КомплектацияАвтомобилей.СуммаНДС
				|ИЗ
				|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачалоПериода КАК НачалоПериода
				|		ПО КомплектацияАвтомобилей.Период > НачалоПериода.Период
				|			И (КомплектацияАвтомобилей.Период <= &ДатаОстатка)
				|ГДЕ
				|	КомплектацияАвтомобилей.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказНаряд)
				|	И КомплектацияАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				|	И КомплектацияАвтомобилей.Автомобиль = &Автомобиль
				|	И КомплектацияАвтомобилей.СкладКомпании.Организация = &Организация
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	РеализацияАвтомобилейТовары.СуммаВсего,
				|	РеализацияАвтомобилейТовары.СуммаНДС,
				|	0,
				|	0
				|ИЗ
				|	Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Товары КАК РеализацияАвтомобилейТовары
				|		ПО РеализацияАвтомобилейАвтомобили.ИдентификаторАвтомобиля = РеализацияАвтомобилейТовары.ИдентификаторАвтомобиля
				|ГДЕ
				|	РеализацияАвтомобилейАвтомобили.Автомобиль = &Автомобиль
				|	И РеализацияАвтомобилейАвтомобили.Ссылка = &РеализацияАвтомобилей
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	0,
				|	0,
				|	СубподрядОбороты.СебестоимостьОборот,
				|	СубподрядОбороты.СуммаНДСВходящийОборот
				|ИЗ
				|	РегистрНакопления.Субподряд.Обороты(, &ДатаРеализации, , ) КАК СубподрядОбороты
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачалоПериода КАК ВТНачалоПериода
				|		ПО СубподрядОбороты.ЗаказНаряд.Дата > ВТНачалоПериода.Период
				|ГДЕ
				|	СубподрядОбороты.ЗаказНаряд.Автомобиль = &Автомобиль
				|	И (СубподрядОбороты.ЗаказНаряд.ВидРемонта.Код = ""ЦБ000017""
				|			ИЛИ СубподрядОбороты.ЗаказНаряд.ВидРемонта.Код = ""00000001"")
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	СУММА(ЗаказНаряды.Сумма) - СУММА(ЗаказНаряды.Себестоимость) КАК Сумма,
				|	СУММА(ЗаказНаряды.СуммаНДС) - СУММА(ЗаказНаряды.СебестоимостьНДС) КАК СуммаНДС
				|ИЗ
				|	ВТЗаказНаряды КАК ЗаказНаряды";
				
				Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
				Запрос.УстановитьПараметр("Организация", Организация);
				Запрос.УстановитьПараметр("ДатаОстатка", ДокументОснование.Дата);
				Запрос.УстановитьПараметр("РеализацияАвтомобилей", ДокументОснование);
				Запрос.УстановитьПараметр("ДатаРеализации", ДокументОснование.Дата-1);  

				Результат = Запрос.Выполнить();
				Если НЕ Результат.Пустой() Тогда
					Выборка = Результат.Выбрать();
					Выборка.Следующий();
					Если ТекСтрока.Сумма <> Выборка.Сумма Тогда
						ТекСтрока.Сумма = Выборка.Сумма;
					КонецЕсли;
					Если ТекСтрока.СуммаНДС <> Выборка.СуммаНДС Тогда
						ТекСтрока.СуммаНДС = Выборка.СуммаНДС;
					КонецЕсли;
				Иначе
					Если ТекСтрока.Сумма <> 0 Тогда
						ТекСтрока.Сумма = 0;
					КонецЕсли;
					Если ТекСтрока.СуммаНДС <> 0 Тогда
						ТекСтрока.СуммаНДС = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ТекСтрока.СуммаБезНДС <> ТекСтрока.Сумма - ТекСтрока.СуммаНДС Тогда
					ТекСтрока.СуммаБезНДС = ТекСтрока.Сумма - ТекСтрока.СуммаНДС;
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			ОбработкаРеквизита("Показатели.Сумма", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="Показатели.Сумма" Тогда
		
		Если ТекСтрока <> Неопределено Тогда
			Если НЕ ТекСтрока.Показатель.АвтоматическийРасчет Тогда
				
				Если ТекСтрока.Показатель.ВключаетНДС Тогда    
					
					Если Дата<Дата(2026,1,1) Тогда
						СтавкаНДС = Справочники.СтавкиНДС.НайтиПоНаименованию("20%").Ставка;
					Иначе	
						СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС.Ставка;
					КонецЕсли;
					
					НоваяСуммаНДС = Окр(ТекСтрока.Сумма / (100 + СтавкаНДС)* СтавкаНДС, 2, РежимОкругления.Окр15как20); 
					
					Если ТекСтрока.СуммаНДС <> НоваяСуммаНДС Тогда
						ТекСтрока.СуммаНДС = НоваяСуммаНДС;
					КонецЕсли;
				ИначеЕсли ТекСтрока.СуммаНДС <> 0 Тогда
					ТекСтрока.СуммаНДС = 0;
				КонецЕсли;
			
			КонецЕсли;
			
			Если ТекСтрока.СуммаБезНДС <> ТекСтрока.Сумма - ТекСтрока.СуммаНДС Тогда
				ТекСтрока.СуммаБезНДС = ТекСтрока.Сумма - ТекСтрока.СуммаНДС;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
Функция ЗаполнитьПоДокументуОснованию(ЭтаФорма, Отказ) Экспорт
		
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Сообщить("Вводить документ ""Кумулятивная маржа"" можно только на основании других документов!", СтатусСообщения.Внимание);
		Отказ = Истина;
		Возврат Ложь;
	ИначеЕсли обЗначениеНеЗаполнено(Автомобиль) Тогда
		
		Автомобили = ДокументОснование.Автомобили.ВыгрузитьКолонку("Автомобиль");
		КоличествоАвтомобилей = Автомобили.Количество();
		Если КоличествоАвтомобилей = 0 Тогда
			Сообщить("В документе основании """ + ДокументОснование + """ не указаны автомобили!", СтатусСообщения.Внимание);
			Отказ = Истина;
			Возврат Ложь;
		ИначеЕсли КоличествоАвтомобилей = 1 Тогда
			Автомобиль = Автомобили[0];
		Иначе
			Список = Новый СписокЗначений;
			Список.ЗагрузитьЗначения(Автомобили);
			
			ФормаВыбора = Справочники.Автомобили.ПолучитьФормуВыбора(,,ЭтаФорма);
			ФормаВыбора.Заголовок = "Выберите автомобиль";
			ФормаВыбора.Отбор.Ссылка.ВидСравнения = ВидСравнения.ВСписке;
			ФормаВыбора.Отбор.Ссылка.Значение = Список;
			ФормаВыбора.Отбор.Ссылка.Использование = Истина;
			ФормаВыбора.ЭлементыФормы.Список.НастройкаОтбора.Ссылка.Доступность = Ложь;
			Автомобиль = ФормаВыбора.ОткрытьМодально();
			
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(Автомобиль) Тогда
			Сообщить("Не заполнен автомобиль!", СтатусСообщения.Внимание);
			Отказ = Истина;
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КумулятивнаяМаржа.Ссылка КАК Документ
	|ИЗ
	|	Документ.ЭМ_КумулятивнаяМаржа КАК КумулятивнаяМаржа
	|ГДЕ
	|	НЕ КумулятивнаяМаржа.ПометкаУдаления
	|	И КумулятивнаяМаржа.ДокументОснование = &ДокументОснование
	|	И КумулятивнаяМаржа.Автомобиль = &Автомобиль";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Сообщить("По документу " + ДокументОснование + " для автомобиля " + Автомобиль + " уже введен документ " + Выборка.Документ, СтатусСообщения.Информация);
		ОткрытьЗначение(Выборка.Документ);
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Дата = ДокументОснование.Дата;
	ОбновитьТаблицуПоказателей(Неопределено);
	
	Возврат Истина;
КонецФункции

//Обновляет таблицу показателей в соответствии с справочником "Кумулятивная маржа"
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму документа.
//
Процедура ОбновитьТаблицуПоказателей(ЭтаФорма) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Показатели.Показатель КАК Справочник.ЭМ_ПоказателиКумулятивнойМаржи) КАК Показатель,
	|	Показатели.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТПоказателиДо
	|ИЗ
	|	&Показатели КАК Показатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникПоказатели.Ссылка КАК Показатель,
	|	0 КАК Сумма,
	|	СправочникПоказатели.Код КАК Код
	|ПОМЕСТИТЬ ВТПоказателиГрязно
	|ИЗ
	|	Справочник.ЭМ_ПоказателиКумулятивнойМаржи КАК СправочникПоказатели
	|ГДЕ
	|	НЕ СправочникПоказатели.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоказателиДо.Показатель,
	|	ПоказателиДо.Сумма,
	|	ПоказателиДо.Показатель.Код
	|ИЗ
	|	ВТПоказателиДо КАК ПоказателиДо
	|ГДЕ
	|	ПоказателиДо.Сумма > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Показатели.Показатель КАК Показатель,
	|	СУММА(Показатели.Сумма) КАК Сумма
	|ИЗ
	|	ВТПоказателиГрязно КАК Показатели
	|
	|СГРУППИРОВАТЬ ПО
	|	Показатели.Показатель,
	|	Показатели.Код
	|
	|УПОРЯДОЧИТЬ ПО
	|	Показатели.Код";
	
	Запрос.УстановитьПараметр("Показатели", Показатели);
	Показатели.Загрузить(Запрос.Выполнить().Выгрузить());
	Для Каждого ТекСтрока Из Показатели Цикл
		ОбработкаРеквизита("Показатели.Показатель", ТекСтрока, ЭтаФорма);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоказатели()
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	//МАМОНОВ 19.12.2025 ++ коммент
	//Если ЗначениеЗаполнено(ДокументОснование)
	//	И ДокументОснование.Проведен Тогда
	//	
	//	ДокументОснование.ПолучитьОбъект().Записать(РежимЗаписиДокумента.ОтменаПроведения);
	//	
	//#Если Клиент Тогда
	//	Сообщить("Отменено проведение документа """ + ДокументОснование + """!", СтатусСообщения.Информация);
	//#КонецЕсли
	//	
	//КонецЕсли;
	//МАМОНОВ 19.12.2025 --
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КумулятивнаяМаржа.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ЭМ_КумулятивнаяМаржа.Обороты(
		|			,
		|			,
		|			Регистратор,
		|			РеализацияАвтомобиля = &ДокументОснование
		|				И Автомобиль = &Автомобиль) КАК КумулятивнаяМаржа";
	
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		дкСообщитьРезультат("Для документа """ + ДокументОснование + """ уже создан и проведен документ " + Выборка.Регистратор, "Важное&Кумулятивная маржа:", ЭтотОбъект, ДокументОснование);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		НаборЗаписей = Движения.ЭМ_КумулятивнаяМаржа;
		
		Для Каждого Строка Из Показатели Цикл
			
			Если Строка.Сумма = 0
				И Строка.СуммаНДС = 0 Тогда
					Продолжить;
			КонецЕсли;
				
			НоваяЗапись							= НаборЗаписей.Добавить();
			НоваяЗапись.Период					= ДокументОснование.Дата;
			НоваяЗапись.Регистратор				= Ссылка;
			НоваяЗапись.Автомобиль				= Автомобиль;
			НоваяЗапись.Показатель				= Строка.Показатель;
			НоваяЗапись.Сумма					= Строка.Сумма;
			НоваяЗапись.СуммаНДС				= Строка.СуммаНДС;
			НоваяЗапись.РеализацияАвтомобиля 	= ДокументОснование;
			
		КонецЦикла;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	//<<ЧалапАЮ БизТех 03.02.2024
	ЗапросПремий = Новый Запрос( "ВЫБРАТЬ
	|	БТ_ПроцентыПремийКМСрезПоследних.Период КАК Период,
	|	БТ_ПроцентыПремийКМСрезПоследних.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	БТ_ПроцентыПремийКМСрезПоследних.ПроцентДляВсех КАК ПроцентДляВсех,
	|	БТ_ПроцентыПремийКМСрезПоследних.ПроцентКС КАК ПроцентКС
	|ИЗ
	|	РегистрСведений.БТ_ПроцентыПремийКМ.СрезПоследних(&Дата, ) КАК БТ_ПроцентыПремийКМСрезПоследних
	|ГДЕ
	|	БТ_ПроцентыПремийКМСрезПоследних.ПодразделениеКомпании = &ПодразделениеКомпании");
	ЗапросПремий.УстановитьПараметр("Дата",Конецдня(ЭтотОбъект.Дата));
	ЗапросПремий.УстановитьПараметр("ПодразделениеКомпании",ЭтотОбъект.ПодразделениеКомпании);
	
	Результат = ЗапросПремий.Выполнить().Выбрать();
	
	Если Результат.Следующий() тогда   
		Если ЭтотОбъект.Автомобиль.БТ_КрасныйСток тогда
			ЭтотОбъект.БТ_ПроцентПремии = результат.ПроцентКС
		иначе
			ЭтотОбъект.БТ_ПроцентПремии = результат.ПроцентДляВсех
		КонецЕсли;
	КонецЕсли;
	ЭтотОбъект.Записать(); 
	//ЧалапАЮ БизТех 03.02.2024>>
	
	//БИЗТЕХ КОВАЛЬ 13.02.2025 ++
	//Выгрузка куммаржи в CME
	Если Не Отказ И Справочники.БТ_СписокКонстантныхЗначений.ЕстьЛиЗначениеВТаблице("CMExpert_ВыгрузкаКуммаржи","Значение",ПодразделениеКомпании) Тогда
		ОбрCME = Обработки.БТ_ОбменCMExpert.Создать();
		ОбрCME.УстановитьПоказателиКуммаржиАвтомобиля(Ссылка);
	КонецЕсли;
	//БИЗТЕХ КОВАЛЬ 13.02.2025 --

КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Модуль документа Взаимозачет

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Процедура отрабатывает выбор сделки в табличной части.
//
// Параметры:
//  ДебиторКредитор – "Строка" - Дебитор, либо Кредитор.
//
//  ТекСтрока – "СтрокаТабличнойЧасти" - Текущая строка табличной части "Состав".
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура ОбработатьВыборСделки(ДебиторКредитор="Дебитор", ТекСтрока)
	
	ТекущийДоговор   = ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор];
	ТекущаяСделка    = ТекСтрока["Сделка" + ДебиторКредитор]; 
	МетаданныеСделка = ТекущаяСделка.Метаданные();
	
	Если МетаданныеСделка.Реквизиты.Найти("ДоговорВзаиморасчетов")<>Неопределено Тогда
		Если ТекущийДоговор.Пустая() Тогда
			ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор] = ТекущаяСделка.ДоговорВзаиморасчетов;
		ИначеЕсли ТекущийДоговор<>ТекущаяСделка.ДоговорВзаиморасчетов Тогда
			Сообщить("Договор взаиморасчетов выбранной сделки не соответствует текущему договору <" + ТекущийДоговор + ">", СтатусСообщения.Важное);	
		КонецЕсли;
	ИначеЕсли (МетаданныеСделка.ТабличныеЧасти.Найти("Состав")<>Неопределено) Тогда
		Если МетаданныеСделка.ТабличныеЧасти.Состав.Реквизиты.Найти("ДоговорВзаиморасчетов")<>Неопределено Тогда				
			// Значит реквизит "ДоговорВзаиморасчетов" находится в табличной части "Состав" документа-сделки.
			Если ТекущийДоговор.Пустая() Тогда
				// Ищем первый подходящий договор в табличной части.
				Для Каждого ТекСтрокаТЧСостав Из ТекущаяСделка.Состав Цикл  	
					Если ТекСтрокаТЧСостав.ДоговорВзаиморасчетов.Владелец=ЭтотОбъект[ДебиторКредитор] Тогда
						ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор] = ТекСтрокаТЧСостав.ДоговорВзаиморасчетов;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ТекущаяСделка.Состав.Найти(ТекущийДоговор, "ДоговорВзаиморасчетов")=Неопределено Тогда 			
				Сообщить("В табличной части <Состав> выбранной сделки не найдено договора <" + ТекущийДоговор + ">.
				|Выберите другую сделку, либо другой договор. ", СтатусСообщения.Важное);					
			КонецЕсли;
			
		ИначеЕсли МетаданныеСделка.ТабличныеЧасти.Состав.Реквизиты.Найти("ДоговорВзаиморасчетовДебитор")<>Неопределено Тогда
			// Значит в качестве сделки выступает документ "Взаимозачет".
			Если ТекущийДоговор.Пустая() Тогда
				// Ищем первый подходящий договор в табличной части.
				Для Каждого ТекСтрокаТЧСостав Из ТекущаяСделка.Состав Цикл  	
					Если ТекСтрокаТЧСостав.ДоговорВзаиморасчетовДебитор.Владелец=ЭтотОбъект[ДебиторКредитор] Тогда
						ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор] = ТекСтрокаТЧСостав.ДоговорВзаиморасчетовДебитор;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				// Если не нашли договор в дебиторах, тогда ищем в кредиторах
				Если ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор].Пустая() Тогда
					Для Каждого ТекСтрокаТЧСостав Из ТекущаяСделка.Состав Цикл  	
						Если ТекСтрокаТЧСостав.ДоговорВзаиморасчетовКредитор.Владелец=ЭтотОбъект[ДебиторКредитор] Тогда
							ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор] = ТекСтрокаТЧСостав.ДоговорВзаиморасчетовКредитор;
							Прервать;
						КонецЕсли;
					КонецЦикла;  					
				КонецЕсли;
				
			ИначеЕсли (ТекущаяСделка.Состав.Найти(ТекущийДоговор, "ДоговорВзаиморасчетовДебитор")=Неопределено) И (ТекущаяСделка.Состав.Найти(ТекущийДоговор, "ДоговорВзаиморасчетовКредитор")=Неопределено) Тогда
				Сообщить("В табличной части <Состав> выбранной сделки не найдено договора <" + ТекущийДоговор + ">.
				|Выберите другую сделку, либо другой договор. ", СтатусСообщения.Важное);					
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборСделки() 

// Процедура отрабатывает выбор сделки в табличной части.
//
// Параметры:
//  ДебиторКредитор – "Строка" - Дебитор, либо Кредитор.
//
//  ТекСтрока – "СтрокаТабличнойЧасти" - Текущая строка табличной части "Состав".
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура ОбработатьВыборДоговора(ДебиторКредитор, ТекСтрока)
	
	ТекущийДоговор   = ТекСтрока["ДоговорВзаиморасчетов" + ДебиторКредитор];
	ТекущаяСделка    = ТекСтрока["Сделка" + ДебиторКредитор]; 
	МетаданныеСделка = ТекущаяСделка.Метаданные();
	
	Если МетаданныеСделка.Реквизиты.Найти("ДоговорВзаиморасчетов")<>Неопределено Тогда
		Если ТекущийДоговор<>ТекущаяСделка.ДоговорВзаиморасчетов Тогда
			ТекСтрока["Сделка" + ДебиторКредитор] = Неопределено;
		КонецЕсли;
	ИначеЕсли (МетаданныеСделка.ТабличныеЧасти.Найти("Состав")<>Неопределено) Тогда
		Если МетаданныеСделка.ТабличныеЧасти.Состав.Реквизиты.Найти("ДоговорВзаиморасчетов")<>Неопределено Тогда				
			// Значит реквизит "ДоговорВзаиморасчетов" находится в табличной части "Состав" документа-сделки.
			Если ТекущаяСделка.Состав.Найти(ТекущийДоговор, "ДоговорВзаиморасчетов")=Неопределено Тогда 			
				ТекСтрока["Сделка" + ДебиторКредитор] = Неопределено;					
			КонецЕсли;
			
		ИначеЕсли МетаданныеСделка.ТабличныеЧасти.Состав.Реквизиты.Найти("ДоговорВзаиморасчетовДебитор")<>Неопределено Тогда
			// Значит в качестве сделки выступает документ "Взаимозачет".
			Если (ТекущаяСделка.Состав.Найти(ТекущийДоговор, "ДоговорВзаиморасчетовДебитор")=Неопределено) И (ТекущаяСделка.Состав.Найти(ТекущийДоговор, "ДоговорВзаиморасчетовКредитор")=Неопределено) Тогда
				ТекСтрока["Сделка" + ДебиторКредитор] = Неопределено;						
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборДоговора() 

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеСостав=Новый Структура();
	ОбязательныеСостав.Вставить("ДоговорВзаиморасчетовДебитор",3);
	ОбязательныеСостав.Вставить("ДоговорВзаиморасчетовКредитор",3);
	
	//anton
	//ОбязательныеСостав.Вставить("СделкаДебитор",3);
	//ОбязательныеСостав.Вставить("СделкаКредитор",3);
	//anton
	
	ОбязательныеСостав.Вставить("Сумма",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Дебитор",1);
	ОбязательныеРеквизиты.Вставить("Кредитор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Состав",ОбязательныеСостав);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
			
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			КонтролируемыеРеквизитыСостава.Вставить("ДоговорВзаиморасчетовДебитор",  КонтрольПоПодразделению);
			КонтролируемыеРеквизитыСостава.Вставить("ДоговорВзаиморасчетовКредитор", КонтрольПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Состав",КонтролируемыеРеквизитыСостава);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти); 			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Взаимозачет","Взаимозачет",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего() Экспорт
	//пересчет суммы документа с учетом валюты в строке и валюты в шапке документа
	СуммаДокументаВрем=0;
	Для Каждого СтрокаТЧ Из Состав Цикл
		СуммаДокументаВрем=СуммаДокументаВрем+обПересчет(СтрокаТЧ.Сумма,СтрокаТЧ.ДоговорВзаиморасчетовДебитор.ВалютаВзаиморасчетов,Дата,ВалютаДокумента,КурсДокумента);
	КонецЦикла;
	Возврат СуммаДокументаВрем;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Дебитор" Тогда
		Если НЕ обЗначениеНеЗаполнено(Дебитор) Тогда
			Для Каждого СтрокаТаблицы Из Состав Цикл
				Если СтрокаТаблицы.ДоговорВзаиморасчетовДебитор.Владелец <> Дебитор Тогда
					СтрокаТаблицы.ДоговорВзаиморасчетовДебитор	= Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
					СтрокаТаблицы.СделкаДебитор					= Неопределено;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя="Кредитор" Тогда
		Если НЕ обЗначениеНеЗаполнено(Кредитор) Тогда
			Для Каждого СтрокаТаблицы Из Состав Цикл
				Если СтрокаТаблицы.ДоговорВзаиморасчетовКредитор.Владелец <> Кредитор Тогда 
					СтрокаТаблицы.ДоговорВзаиморасчетовКредитор	= Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
					СтрокаТаблицы.СделкаКредитор				= Неопределено;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя = "Состав.ДоговорВзаиморасчетовДебитор" Тогда
		Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.СделкаДебитор)) И (НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетовДебитор)) Тогда
			ОбработатьВыборДоговора("Дебитор", ТекСтрока);
		КонецЕсли;
		ТекСтрока.КурсВалютыДоговораДебитора = 0;
		
	ИначеЕсли Имя = "Состав.ДоговорВзаиморасчетовКредитор" Тогда
		Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.СделкаКредитор)) И (НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетовКредитор)) Тогда
			ОбработатьВыборДоговора("Кредитор", ТекСтрока); 
		КонецЕсли;
		ТекСтрока.КурсВалютыДоговораКредитора = 0;
		
	ИначеЕсли Имя = "Состав.СделкаДебитор" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.СделкаДебитор) Тогда
			ОбработатьВыборСделки("Дебитор", ТекСтрока);	 			
		КонецЕсли;
		
		ОбработкаРеквизита("Состав.Сделка", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "Состав.СделкаКредитор" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.СделкаКредитор) Тогда
			ОбработатьВыборСделки("Кредитор", ТекСтрока);
		КонецЕсли;
		
		ОбработкаРеквизита("Состав.Сделка", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "Состав.Сделка" Тогда
		// Проверим можно ли и нужно ли получить сумму взаимозачета
		Если (ТекСтрока.Сумма <> 0)
				ИЛИ обЗначениеНеЗаполнено(ТекСтрока.СделкаДебитор)
				ИЛИ обЗначениеНеЗаполнено(ТекСтрока.СделкаКредитор) Тогда
			Возврат ЛОЖЬ;
		КонецЕсли;
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток КАК СуммаУпр,
		|	ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток КАК СуммаБаз
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
		|		&Дата,
		|		Контрагент = &Дебитор
		|		    И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетовДебитор
		|		    И Сделка = &СделкаДебитор) КАК ВзаиморасчетыКомпанииОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	-ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток,
		|	-ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
		|		&Дата,
		|		Контрагент = &Кредитор
		|		    И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетовКредитор
		|		    И Сделка = &СделкаКредитор) КАК ВзаиморасчетыКомпанииОстатки
		|ИТОГИ
		|	МИНИМУМ(СуммаУпр),
		|	МИНИМУМ(СуммаБаз)
		|ПО
		|	ОБЩИЕ";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("Дебитор", Дебитор);
		Запрос.УстановитьПараметр("СделкаДебитор", ТекСтрока.СделкаДебитор);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетовДебитор", ТекСтрока.ДоговорВзаиморасчетовДебитор);
		Запрос.УстановитьПараметр("Кредитор", Кредитор);
		Запрос.УстановитьПараметр("СделкаКредитор", ТекСтрока.СделкаКредитор);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетовКредитор", ТекСтрока.ДоговорВзаиморасчетовКредитор);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				ТекСтрока.Сумма = Выборка.СуммаБаз;
			Иначе
				ТекСтрока.Сумма = обПересчет(Выборка.СуммаУпр,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),Дата,ВалютаДокумента,Дата);
			КонецЕсли; 
		КонецЕсли;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

//<< ГорковенкоПА (БизТех) 01.09.2025
Процедура ЗаполнитьПоСделкеКредитора()Экспорт
	//проверка заполнения табличной части в строке сделка кредитора
	 ЕстьДанные = Ложь;
	 Для Каждого Строка Из Состав Цикл
		 Если Строка.СделкаКредитор <> Неопределено Тогда
			 ЕстьДанные = Истина;
			 Прервать;
		 КонецЕсли;
	 КонецЦикла;
	 //вывод сообщения о наличии записей в строке сделка кредитора 
	 Если ЕстьДанные Тогда
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		Ответ = Вопрос("Ранее введенные данные по сделке кредитора будут очищены. Продолжить автоматическое заполнение?", Режим, 0);	 
		//получение значения реквизита "Сделка кредитор"
		СделкаКредитора = ЭтотОбъект.СделкаКредитора;    
		Если Ответ =КодВозвратаДиалога.Да Тогда
		    //очистка поля сделка кредитора в табличной части
			 Для Каждого Строка Из ЭтотОбъект.Состав Цикл 
				 Строка.СделкаКредитор = СделкаКредитора;
			 КонецЦикла;   
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	Иначе  //заполняем пустые сделки кредитора 
		Для каждого Строка из ЭтотОбъект.Состав Цикл
			Если не ЗначениеЗаполнено(Строка.СделкаКредитор) Тогда
				Строка.СделкаКредитор = СделкаКредитора;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;	 	 
КонецПроцедуры // ГорковенкоПА (БизТех) 01.09.2025>>


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Взаимозачет"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВзаимозачет(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Взаимозачет");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);	
	
	//определяем шапку ТЧ, которая будет повторяться на всех страницах документа
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//реквизиты шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;	
	ОбластьМакета.Параметры.Организация = Организация;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизация = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.Дебитор = Дебитор;
	ОбластьМакета.Параметры.ДебиторПредставление = спПолучитьПредставление(Дебитор) ;
	ОбластьМакета.Параметры.Кредитор = Кредитор;
	ОбластьМакета.Параметры.КредиторПредставление = спПолучитьПредставление(Кредитор) ;
	
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	//шапка
	//ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	//ТабДокумент.Вывести(ОбластьМакета);
	//шапка_
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//Итоги по странице
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("Сумма",0);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	//Подвал
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 	
	
	//строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Состав;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.Сумма = Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
		
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			СтруктураИтоговПоСтранице = Новый Структура("Сумма",0);
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;	

	//итоги
	
	Сумма = Состав.Итог("Сумма");
	ОбластьПодвал.Параметры.ИтогоСумма = Формат(Сумма,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(Состав.Итог("Сумма"),ЭтотОбъект.ВалютаДокумента);
	// Выводим представления и расшифровки подписей
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/ "+ ГлавныйБухгалтер.ГлавныйБухгалтерПредставление);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	ТабДокумент.Вывести(ОбластьПодвал);
															   
	//зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
	Возврат ТабДокумент;
КонецФункции // ПечатьВзаимозачет()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

Функция ПолучитьСписокЗаказовНаАвтомобиль(ЗаказНаАвтомобиль)
	
	ОтмененныйЗаказ = ЗаказНаАвтомобиль.ДокументОснование;
	Если ТипЗнч(ОтмененныйЗаказ) = Тип("ДокументСсылка.ЗаказНаАвтомобиль") И ЗначениеЗаполнено(ОтмененныйЗаказ) Тогда
		МассивЗаказов = ПолучитьСписокЗаказовНаАвтомобиль(ОтмененныйЗаказ);
		МассивЗаказов.Добавить(ОтмененныйЗаказ);
	Иначе
		МассивЗаказов = Новый Массив;
	КонецЕсли;
	
	Возврат МассивЗаказов;
	
КонецФункции

Функция ПолучитьСписокЗаказовНаАвтомобильПоставщику(ЗаказНаАвтомобиль)
	
	ОтмененныйЗаказ = ЗаказНаАвтомобиль.ДокументОснование;
	Если ТипЗнч(ОтмененныйЗаказ) = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") И ЗначениеЗаполнено(ОтмененныйЗаказ) Тогда
		МассивЗаказов = ПолучитьСписокЗаказовНаАвтомобильПоставщику(ОтмененныйЗаказ);
		МассивЗаказов.Добавить(ОтмененныйЗаказ);
	Иначе
		МассивЗаказов = Новый Массив;
	КонецЕсли;
	
	Возврат МассивЗаказов;
	
КонецФункции

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		
		МассивЗаказов = ПолучитьСписокЗаказовНаАвтомобиль(Основание);
		
		Если МассивЗаказов.Количество()>0 Тогда
			
			ПересчетСуммы = Ложь;
			Если ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
				ИмяРесурсаСуммы = "СуммаУпр";
			ИначеЕсли ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				ИмяРесурсаСуммы = "СуммаБаз";
			Иначе
				ИмяРесурсаСуммы = "Сумма";
				ПересчетСуммы   = Истина;
			КонецЕсли;
			
			Запрос = Новый Запрос("ВЫБРАТЬ
			|	ВзаиморасчетыКомпанииОстатки.Контрагент,
			|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов,
			|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК Валюта,
			|	ВзаиморасчетыКомпанииОстатки.Сделка,
			|	ВзаиморасчетыКомпанииОстатки." + ИмяРесурсаСуммы + "Остаток КАК Сумма
			|ИЗ
			|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(, Сделка В (&МассивЗаказов)) КАК ВзаиморасчетыКомпанииОстатки
			|ГДЕ
			|	ВзаиморасчетыКомпанииОстатки." + ИмяРесурсаСуммы + "Остаток < 0
			|ИТОГИ ПО
			|	Контрагент
			|");
			Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
			ВыборкаКонтрагент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Дебитор = Основание.Контрагент;
			Договор  = Основание.ДоговорВзаиморасчетов;
			Валюта   = Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			стрКурс = обКурсДляВалюты(Валюта, Дата);
			Попытка
				КурсВалютыДоговораДебитора = стрКурс.Курс/?(стрКурс.Кратность=0,1,стрКурс.Кратность);
			Исключение
			КонецПопытки;
			Если ВыборкаКонтрагент.Следующий() Тогда
				Кредитор = ВыборкаКонтрагент.Контрагент;
				Выборка = ВыборкаКонтрагент.Выбрать();
				Пока Выборка.Следующий() Цикл
					НоваяСтрока = Состав.Добавить();
					НоваяСтрока.ДоговорВзаиморасчетовКредитор = Выборка.ДоговорВзаиморасчетов;
					НоваяСтрока.СделкаКредитор                = Выборка.Сделка;
					стрКурс = обКурсДляВалюты(Выборка.Валюта, Дата);
					Попытка
						НоваяСтрока.КурсВалютыДоговораКредитора = стрКурс.Курс/?(стрКурс.Кратность=0,1,стрКурс.Кратность);
					Исключение
					КонецПопытки;
					НоваяСтрока.Сумма                        = -Выборка.Сумма;
					НоваяСтрока.ДоговорВзаиморасчетовДебитор = Договор;
					НоваяСтрока.СделкаДебитор                = Основание;
					НоваяСтрока.КурсВалютыДоговораДебитора   = КурсВалютыДоговораДебитора;
					
					Если ПересчетСуммы И (НЕ ВалютаДокумента = Выборка.Валюта) Тогда
						НоваяСтрока.Сумма = обПересчет(-Выборка.Сумма, Выборка.Валюта, НоваяСтрока.КурсВалютыДоговораКредитора, ВалютаДокумента, Дата);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		
		МассивЗаказов = ПолучитьСписокЗаказовНаАвтомобильПоставщику(Основание);
		
		Если МассивЗаказов.Количество()>0 Тогда
			
			ПересчетСуммы = Ложь;
			Если ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
				ИмяРесурсаСуммы = "СуммаУпр";
			ИначеЕсли ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				ИмяРесурсаСуммы = "СуммаБаз";
			Иначе
				ИмяРесурсаСуммы = "Сумма";
				ПересчетСуммы   = Истина;
			КонецЕсли;
			
			Запрос = Новый Запрос("ВЫБРАТЬ
			|	ВзаиморасчетыКомпанииОстатки.Контрагент,
			|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов,
			|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК Валюта,
			|	ВзаиморасчетыКомпанииОстатки.Сделка,
			|	ВзаиморасчетыКомпанииОстатки." + ИмяРесурсаСуммы + "Остаток КАК Сумма
			|ИЗ
			|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(, Сделка В (&МассивЗаказов)) КАК ВзаиморасчетыКомпанииОстатки
			|ГДЕ
			|	ВзаиморасчетыКомпанииОстатки." + ИмяРесурсаСуммы + "Остаток > 0
			|ИТОГИ ПО
			|	Контрагент
			|");
			Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
			ВыборкаКонтрагент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Кредитор = Основание.Контрагент;
			Договор  = Основание.ДоговорВзаиморасчетов;
			Валюта   = Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			стрКурс = обКурсДляВалюты(Валюта, Дата);
			Попытка
				КурсВалютыДоговораКредитора = стрКурс.Курс/?(стрКурс.Кратность=0,1,стрКурс.Кратность);
			Исключение
			КонецПопытки;
			Если ВыборкаКонтрагент.Следующий() Тогда
				Дебитор = ВыборкаКонтрагент.Контрагент;
				Выборка = ВыборкаКонтрагент.Выбрать();
				Пока Выборка.Следующий() Цикл
					НоваяСтрока = Состав.Добавить();
					НоваяСтрока.ДоговорВзаиморасчетовДебитор  = Выборка.ДоговорВзаиморасчетов;
					НоваяСтрока.СделкаДебитор                 = Выборка.Сделка;
					стрКурс = обКурсДляВалюты(Выборка.Валюта, Дата);
					Попытка
						НоваяСтрока.КурсВалютыДоговораДебитора = стрКурс.Курс/?(стрКурс.Кратность=0,1,стрКурс.Кратность);
					Исключение
					КонецПопытки;
					НоваяСтрока.Сумма                         = Выборка.Сумма;
					НоваяСтрока.ДоговорВзаиморасчетовКредитор = Договор;
					НоваяСтрока.СделкаКредитор                = Основание;
					НоваяСтрока.КурсВалютыДоговораКредитора   = КурсВалютыДоговораКредитора;
					
					Если ПересчетСуммы И (НЕ ВалютаДокумента = Выборка.Валюта) Тогда
						НоваяСтрока.Сумма = обПересчет(Выборка.Сумма, Выборка.Валюта, НоваяСтрока.КурсВалютыДоговораДебитора, ВалютаДокумента,Дата);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли; 
		
		//<<ЧалапАЮ (БизТех) 18.08.2023
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Соллерс_АктДистрибьютораПоГарантии") Тогда  
		Кредитор = Основание.Контрагент;
		Дебитор = Кредитор;
		Для каждого СтрЗН из Основание.ТаблицаЗаказНарядов цикл
			//СтруктураСтрЗн = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрЗН);  
			НоваяСтрока = Состав.Добавить();
			НоваяСтрока.ДоговорВзаиморасчетовДебитор = СтрЗН.ЗаказНаряд.ДоговорВзаиморасчетов;
			НоваяСтрока.СделкаДебитор = СтрЗн.ЗаказНаряд.Ссылка;
			НоваяСтрока.Сумма = СтрЗН.СуммаПодтвержденная; 
			стрКурс = обКурсДляВалюты(СтрЗН.ЗаказНаряд.ВалютаДокумента, Дата);
			Попытка
				НоваяСтрока.КурсВалютыДоговораДебитора = стрКурс.Курс/?(стрКурс.Кратность=0,1,стрКурс.Кратность);
			Исключение
			КонецПопытки;
		КонецЦикла;   //ЧалапАЮ (БизТех) 18.08.2023 >>
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	// проводим
	НаборЗаписейВзаиморасчетыКомпании=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчетыКомпании.РежимПроведения=Режим;
	
	ТипыСделокПокупателя=орПолучитьТипыСделок(Истина);
	ТипыСделокПоставщика=орПолучитьТипыСделок(Ложь);
	
	// БАЛАНС: Подразделения договор кредитора и дебитора могут находиться в различных балансовых "ветках".
	// Поэтому, в случае ведения баланса по подразделениям возникнет балансовый разрыв.
	// Необходимо добавить корректирующие движения по регистру "Доходы и расходы".
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	Если БалансВедетсяПоПодразделениям Тогда
		ТаблицаВзаиморасчетов = Новый ТаблицаЗначений;
		ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
		ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрДоход",  Новый ОписаниеТипов("Число"));
		ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрРасход", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	// Суммовые разницы возникают в рамках договора, поэтому их нельзя списывать одним движением.
	ТаблицаСуммовыхРазниц = Новый ТаблицаЗначений;
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("СуммаУпр",      Новый ОписаниеТипов("Число"));
      	       	
	Доход  = 0; 
	Расход = 0;
	Для Каждого СтрокаТЧ Из Состав Цикл
		СуммоваяРазницаПоДебитору  = 0;
		СуммоваяРазницаПоКредитору = 0;

		НаборЗаписейВзаиморасчетыКомпании.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейВзаиморасчетыКомпании.Контрагент            = Дебитор;
		НаборЗаписейВзаиморасчетыКомпании.ДоговорВзаиморасчетов = СтрокаТЧ.ДоговорВзаиморасчетовДебитор;
		НаборЗаписейВзаиморасчетыКомпании.КурсВзаиморасчетов    = ?(СтрокаТЧ.КурсВалютыДоговораДебитора=0, Неопределено, СтрокаТЧ.КурсВалютыДоговораДебитора);
		НаборЗаписейВзаиморасчетыКомпании.Сделка=СтрокаТЧ.СделкаДебитор;
		НаборЗаписейВзаиморасчетыКомпании.АвтоЗакрытиеСделок	= Ложь;
		НаборЗаписейВзаиморасчетыКомпании.СписатьНераспределеннуюСуммуПоСделке = Истина;
		Если ТипыСделокПокупателя.СодержитТип(ТипЗнч(СтрокаТЧ.СделкаДебитор)) Тогда
			НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Истина;
		ИначеЕсли ТипыСделокПоставщика.СодержитТип(ТипЗнч(СтрокаТЧ.СделкаДебитор)) Тогда
			НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Ложь;
		Иначе
			НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Неопределено;
		КонецЕсли; 
		НаборЗаписейВзаиморасчетыКомпании.Сумма = СтрокаТЧ.Сумма;
		НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц = 0;
		Отказ = НЕ НаборЗаписейВзаиморасчетыКомпании.Расход() ИЛИ Отказ;
		Доход = Доход + СтрокаТЧ.Сумма;
		СуммоваяРазницаПоДебитору = НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц;

		НаборЗаписейВзаиморасчетыКомпании.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейВзаиморасчетыКомпании.Контрагент            = Кредитор;
		НаборЗаписейВзаиморасчетыКомпании.ДоговорВзаиморасчетов = СтрокаТЧ.ДоговорВзаиморасчетовКредитор;
		НаборЗаписейВзаиморасчетыКомпании.КурсВзаиморасчетов    = ?(СтрокаТЧ.КурсВалютыДоговораКредитора=0, Неопределено, СтрокаТЧ.КурсВалютыДоговораКредитора);
		НаборЗаписейВзаиморасчетыКомпании.Сделка                = СтрокаТЧ.СделкаКредитор;
		НаборЗаписейВзаиморасчетыКомпании.АвтоЗакрытиеСделок 	= Ложь;
		НаборЗаписейВзаиморасчетыКомпании.СписатьНераспределеннуюСуммуПоСделке = Истина;
		Если ТипыСделокПокупателя.СодержитТип(ТипЗнч(СтрокаТЧ.СделкаКредитор)) Тогда
			НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем = Истина;
		ИначеЕсли ТипыСделокПоставщика.СодержитТип(ТипЗнч(СтрокаТЧ.СделкаКредитор)) Тогда
			НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем = Ложь;
		Иначе
			НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем = Неопределено;
		КонецЕсли; 
		НаборЗаписейВзаиморасчетыКомпании.Сумма = СтрокаТЧ.Сумма;
		НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц = 0;
		Отказ  = НЕ НаборЗаписейВзаиморасчетыКомпании.Приход() ИЛИ Отказ;
		Расход = Расход + СтрокаТЧ.Сумма;
		СуммоваяРазницаПоКредитору = НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц;
		
		// Заполняем таблицу суммовых разниц. Отдельно по кредиту и дебету.
		Если СуммоваяРазницаПоДебитору<>0 Тогда
			НоваяСтрока = ТаблицаСуммовыхРазниц.Добавить();
			Если БалансВедетсяПоПодразделениям Тогда
				НоваяСтрока.Подразделение = СтрокаТЧ.ДоговорВзаиморасчетовДебитор.Подразделение;
			Иначе
				НоваяСтрока.Подразделение = ПодразделениеКомпании;  
			КонецЕсли;
			НоваяСтрока.СуммаУпр      = СуммоваяРазницаПоДебитору;
		КонецЕсли;
		
		Если СуммоваяРазницаПоКредитору<>0 Тогда
			НоваяСтрока = ТаблицаСуммовыхРазниц.Добавить();
			Если БалансВедетсяПоПодразделениям Тогда
				НоваяСтрока.Подразделение = СтрокаТЧ.ДоговорВзаиморасчетовДебитор.Подразделение;
			Иначе
				НоваяСтрока.Подразделение = ПодразделениеКомпании;  
			КонецЕсли; 
			НоваяСтрока.СуммаУпр      = СуммоваяРазницаПоКредитору;
		КонецЕсли;
		
		// БАЛАНС: проверяем, находятся ли подразделения в различных балансовых ветках.
		Если БалансВедетсяПоПодразделениям Тогда
			ПодразделениеДебитора  = обПолучитьБалансовоеПодразделение(СтрокаТЧ.ДоговорВзаиморасчетовДебитор.Подразделение);		
			ПодразделениеКредитора = обПолучитьБалансовоеПодразделение(СтрокаТЧ.ДоговорВзаиморасчетовКредитор.Подразделение);	
			Если ПодразделениеДебитора<>ПодразделениеКредитора Тогда
				// Увеличение кредиторской задолженности
				НоваяСтрока = ТаблицаВзаиморасчетов.Добавить();
				НоваяСтрока.Подразделение  = СтрокаТЧ.ДоговорВзаиморасчетовДебитор.Подразделение;
				НоваяСтрока.СуммаУпрРасход = СтрокаТЧ.Сумма;
				
				// Увеличение дебиторской задолженности
				НоваяСтрока = ТаблицаВзаиморасчетов.Добавить();
				НоваяСтрока.Подразделение  = СтрокаТЧ.ДоговорВзаиморасчетовКредитор.Подразделение;
				НоваяСтрока.СуммаУпрДоход  = СтрокаТЧ.Сумма;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
	
	// БАЛАНС: создаем корректирующие движения.
	Если БалансВедетсяПоПодразделениям Тогда
		ТаблицаВзаиморасчетов.Свернуть("Подразделение", "СуммаУпрРасход, СуммаУпрДоход");
		Для Каждого ТекСтрока Из ТаблицаВзаиморасчетов  Цикл 
			Если ТекСтрока.СуммаУпрРасход<>0 Тогда
				// Корректируем движения взаиморасчетов.
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение  = ТекСтрока.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте     = Ложь;
				НаборЗаписейДиР.Расход         = ТекСтрока.СуммаУпрРасход;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
			КонецЕсли;
			Если ТекСтрока.СуммаУпрДоход<>0 Тогда
				// Корректируем движения взаиморасчетов.
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение  = ТекСтрока.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте     = Ложь;
				НаборЗаписейДиР.Доход          = ТекСтрока.СуммаУпрДоход;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Доходы и расходы по суммовым разницам.
	ТаблицаСуммовыхРазниц.Свернуть("Подразделение", "СуммаУпр");
	Для Каждого ТекСтрока Из ТаблицаСуммовыхРазниц Цикл
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.Подразделение          = ТекСтрока.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте             = Истина;
		Если ТекСтрока.СуммаУпр<0 Тогда
			НаборЗаписейДиР.Расход = -ТекСтрока.СуммаУпр;
		Иначе
			НаборЗаписейДиР.Доход = ТекСтрока.СуммаУпр;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;		
	КонецЦикла;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
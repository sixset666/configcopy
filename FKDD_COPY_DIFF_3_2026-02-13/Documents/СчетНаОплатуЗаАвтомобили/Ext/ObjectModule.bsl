// Модуль документа СчетНаОплатуЗаАвтомобили

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаРасчетСкидокАвтомобили Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.

Перем КешСебестоимостиАвтомобилейКупленныхУФизЛица; // Переменная для хранения значения валюты регламентированного учета

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция возвращает положительную себестоимость автомобиля в валюте документа если 
// этот автомобиль был приобретен у физического лица
//
// Параметры:
//  СтрокаТЧ       - строка табличной части Товары
//
// Возвращаемое значение:
//  Себестоимость - Число себестоимость автомобиля в валюте документа
// 
Функция ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(СтрокаТЧ)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("ХозОперация",Справочники.ХозОперации.РеализацияАвтомобилей);
	ДокументОбъектСтруктура.Вставить("ВалютаДокумента",ВалютаДокумента);
	ДокументОбъектСтруктура.Вставить("КурсДокумента",КурсДокумента);
	Если КешСебестоимостиАвтомобилейКупленныхУФизЛица=Неопределено Тогда
		ДокументОбъектСтруктура.Вставить("КешСебестоимостиАвтомобилейКупленныхУФизЛица",Неопределено);
	Иначе
		ДокументОбъектСтруктура.Вставить("КешСебестоимостиАвтомобилейКупленныхУФизЛица",КешСебестоимостиАвтомобилейКупленныхУФизЛица.Скопировать());
	КонецЕсли; 
	ДокументОбъектСтрока=Новый Структура();
	Для каждого РеквизитТабличнойЧасти Из ЭтотОбъект.Метаданные().ТабличныеЧасти.Автомобили.Реквизиты Цикл
		ДокументОбъектСтрока.Вставить(РеквизитТабличнойЧасти.Имя,СтрокаТЧ[РеквизитТабличнойЧасти.Имя]);
	КонецЦикла; 
	Себестоимость=зфЗащищенныеФункцииСервер.РеализацияАвтомобилейПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ДокументОбъектСтруктура,ДокументОбъектСтрока);
	СтрокаТЧ.СебестоимостьАвтомобиля=Себестоимость;
	Если ДокументОбъектСтруктура.КешСебестоимостиАвтомобилейКупленныхУФизЛица<>Неопределено Тогда
		КешСебестоимостиАвтомобилейКупленныхУФизЛица=ДокументОбъектСтруктура.КешСебестоимостиАвтомобилейКупленныхУФизЛица.Скопировать();
	КонецЕсли; 
	Возврат Себестоимость;
КонецФункции // ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица()

// Получение суммы по строке документа без скидки
//
// ЭтотОбъект	– ДокументОбъект	– Документ, для которого требуется получить сумму строки.
// ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа.
//
// Возвращаемое значение:
// Число – Сумма строки документа.
//
Функция ПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока)
	Сумма = 0;
	
	// Включает ли цена НДС?
	Попытка
		ЦенаВключаетНДС = ЭтотОбъект.ТипЦен.Пустая() ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
	Исключение
		ЦенаВключаетНДС = Истина;
	КонецПопытки;
	// Пробуем получить ставку НДС. Если ее нет в табличной части, то считаем что ставка = 0.
	Попытка
		СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
	Исключение
		СтавкаНДС = 0;
	КонецПопытки; 
	
	Себестоимость = ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ТекСтрока);
	
	Сумма = ТекСтрока.Сумма;
	Если НЕ ЦенаВключаетНДС Тогда
		Если Себестоимость = 0 Тогда
			Сумма = Сумма + Окр(Сумма*СтавкаНДС/100,2);
		Иначе
			Сумма = Сумма + Окр((Сумма-Себестоимость)*СтавкаНДС/100,2);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Сумма;
КонецФункции // ПолучитьСуммуСтрокиБезСкидки()

Функция ПолучитьСуммуДокументаБезСкидкиАвтомобилей()
	
	СуммаДокументаБезСкидки = ЭтотОбъект.Автомобили.Итог("Сумма");
	ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
	Если НЕ ЦенаВключаетНДС Тогда
		Для каждого ТекСтрока Из ЭтотОбъект.Автомобили Цикл
			Себестоимость = ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ТекСтрока);
			СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ((ТекСтрока.Сумма - Себестоимость) * ТекСтрока.СтавкаНДС.Ставка)/100;
		КонецЦикла;
	КонецЕсли;
	Возврат СуммаДокументаБезСкидки;
	
КонецФункции // ПолучитьСуммуДокументаБезСкидкиАвтомобилей()

// Функция перерассчитывает проценты скидок в табличной части документа.
//
// Параметры:
//  ДокументОбъект – "ДокументОбъект" - Произвольный документ, для которого производится поиск подходящей скидки.
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура ПрименитьСкидку(ТекущаяСкидкаНаценка, ТекущаяСуммаСкидки, ТекущееЗначениеСкидки, СуммаДляРасчетаШапочнойСкидки)
	// подготовим данные для сравнения
	РедактируемПроценты = обПраво("СпособВыбораСкидки",Права,, ЭтотОбъект) = Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов;
	Попытка
		СкидкаШапкиДок = СкидкаНаценка;
	Исключение
		СкидкаШапкиДок = Неопределено;
	КонецПопытки;
	
	Если Автомобили.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
		
	СкидкаОтносительная = ТекущаяСкидкаНаценка.Пустая() Или (ТекущаяСкидкаНаценка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная);
	СкидкаШапкиВытесняющая = ТекущаяСкидкаНаценка.ФлагВытеснения;
	
	Если СкидкаОтносительная Тогда
		// Скидки более 100% недопустимы.
		ПроцентСкидки = Мин(ТекущееЗначениеСкидки, 100); 		
	Иначе
		// Скидки более 100% недопустимы.
		КоэффициентСкидки = ?(Автомобили.Итог("Сумма")=0, 0, ТекущаяСуммаСкидки / СуммаДляРасчетаШапочнойСкидки);
		КоэффициентСкидки = ?(КоэффициентСкидки>1, 1, КоэффициентСкидки);
	КонецЕсли;
	
	// Проходимся по табличной части. За одно найдем строку с максимальной суммой.
	СтрокаСМаксимальнойСуммой = Автомобили[0];
	СуммаСтрокиСМаксимальнойСуммой = ПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,СтрокаСМаксимальнойСуммой);
	//СуммаВытеснения = 0;
	Для Каждого ТекСтрока Из Автомобили Цикл
		Сумма = ПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока);
		Если СкидкаОтносительная Тогда
			Если НЕ (РедактируемПроценты И НЕ обЗначениеНеЗаполнено(ТекСтрока.ПроцентСкидки) И (СкидкаШапкиДок = ТекущаяСкидкаНаценка))Тогда
				ТекСтрока.ПроцентСкидки = ПроцентСкидки;
			КонецЕсли;
			Если ТекущаяСкидкаНаценка.Пустая() И РедактируемПроценты Тогда
				ТекСтрока.ПроцентСкидки = 0;
			КонецЕсли;
			ТекСтрока.СуммаСкидки	 = ТекСтрока.ПроцентСкидки / 100 * Сумма;
			ТекСтрока.СуммаСкидки	 = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,ТекущаяСкидкаНаценка);
		Иначе
			Если ТекущаяСкидкаНаценка.Пустая() И РедактируемПроценты Тогда
				ТекСтрока.СуммаСкидки = 0;
			Иначе
				ТекСтрока.СуммаСкидки = КоэффициентСкидки * Сумма;
				ТекСтрока.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,ТекущаяСкидкаНаценка);
			КонецЕсли;
			ТекСтрока.ПроцентСкидки	 = ?(ТекСтрока.СуммаСкидки=0, 0, КоэффициентСкидки * 100);			
		КонецЕсли;
		
		СкидкаНеВытесняется = Истина;
		//СуммаСкидкиСтрокиВрем = ТекСтрока.СуммаСкидки;
		
		Если СкидкаНеВытесняется И Сумма>СуммаСтрокиСМаксимальнойСуммой Тогда
			// Ищем строку с максимальной суммой.
			СтрокаСМаксимальнойСуммой = ТекСтрока;
			СуммаСтрокиСМаксимальнойСуммой = Сумма;
		КонецЕсли;
		
		// Рассчитываем сумму НДС и сумму всего.
		ОбработкаРеквизита("Автомобили.СтавкаНДС", ТекСтрока);
	КонецЦикла;
	
	Если РедактируемПроценты Тогда
		ОстатокСкидки = 0;
	Иначе
		//ived Была ошибка исправляемая данным кодом исправили сейчас не воспроизводится
		//ОстатокСкидки = ТекущаяСуммаСкидки - ТабличнаяЧасть.Итог("СуммаСкидки") - СуммаВытеснения;
		ОстатокСкидки = ТекущаяСуммаСкидки - Автомобили.Итог("СуммаСкидки");
	КонецЕсли;
	Если ОстатокСкидки<>0 Тогда
		СтрокаСМаксимальнойСуммой.СуммаСкидки   = СтрокаСМаксимальнойСуммой.СуммаСкидки + ОстатокСкидки;
		СтрокаСМаксимальнойСуммой.ПроцентСкидки	= ?(СуммаСтрокиСМаксимальнойСуммой=0, 0, 100 * СтрокаСМаксимальнойСуммой.СуммаСкидки / СуммаСтрокиСМаксимальнойСуммой);
		
		ОбработкаРеквизита("Автомобили.СтавкаНДС", СтрокаСМаксимальнойСуммой);
	КонецЕсли;
	
КонецПроцедуры // ПрименитьСкидкуШапки() 

Процедура ПересчетСкидокАвтомобилей()
	
	ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
	
	//Пересчитаем скидки 
	Если НЕ ЦенаВключаетНДС И НЕ ОбработкаРасчетСкидокАвтомобили.КэшСкидок.Количество() = 0 Тогда
		
		ОбработкаРасчетСкидокАвтомобили.СуммаДляРасчетаШапочнойСкидки = ПолучитьСуммуДокументаБезСкидкиАвтомобилей();
		КоэффициентПересчетаВВалютуДокумента = ?((КурсДокумента=0) ИЛИ (ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()), 1, КурсДокумента);
		СтруктураВыборки = Новый Структура("Объект,Свойство",Неопределено,Неопределено);
		
		Для Каждого ТекСтрокаКэша Из ОбработкаРасчетСкидокАвтомобили.КэшСкидок Цикл
			ОтказПоНесовпадениюСвойства = Ложь;
			Если ЗначениеЗаполнено(ТекСтрокаКэша.ВидСвойства) И ЗначениеЗаполнено(ТекСтрокаКэша.Свойство) Тогда
				Если ЭтотОбъект.ЭтоНовый() Тогда
					ОтказПоНесовпадениюСвойства = Истина;
				Иначе
					СтруктураВыборки.Объект = Ссылка;
					СтруктураВыборки.Свойство = ТекСтрокаКэша.ВидСвойства;
					СтрСвойств = РегистрыСведений.ЗначенияСвойствОбъектов.Получить(СтруктураВыборки);
					Если СтрСвойств.Значение <> ТекСтрокаКэша.Свойство Тогда
						ОтказПоНесовпадениюСвойства = Истина
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ОтСуммыЧекаВВалютеДокумента = ТекСтрокаКэша.ОтСуммыЧека / КоэффициентПересчетаВВалютуДокумента;
			
			Если ОтСуммыЧекаВВалютеДокумента>ОбработкаРасчетСкидокАвтомобили.СуммаДляРасчетаШапочнойСкидки Тогда // Сумма документа недостаточна для скидки
				ТекСтрокаКэша.РассчитаннаяСуммаСкидки = 0;	
				ТекСтрокаКэша.НеПодходит             = Истина;
			ИначеЕсли ОтказПоНесовпадениюСвойства Тогда
				ТекСтрокаКэша.РассчитаннаяСуммаСкидки = 0;
				ТекСтрокаКэша.НеПодходит             = Истина;
			Иначе
				ТекСтрокаКэша.НеПодходит = Ложь;
				// Раcсчитываем сумму скидки.
				Если ТекСтрокаКэша.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ТекСтрокаКэша.РассчитаннаяСуммаСкидки = ОбработкаРасчетСкидокАвтомобили.СуммаДляРасчетаШапочнойСкидки * ТекСтрокаКэша.ЗначениеСкидки / 100;
				Иначе
					// Если скидка абсолютная, то ее значение - это сумма в валюте рег. учета.
					// Выполним пересчет в валюту документа.
					ТекСтрокаКэша.РассчитаннаяСуммаСкидки = ТекСтрокаКэша.ЗначениеСкидки / КоэффициентПересчетаВВалютуДокумента;
					
					// Если сумма абсолютной скидки превышает значение базы для расчета скидки (т.е больше суммы документа), 
					// то итоговая сумма уйдет в минус, поэтому такая скидка не подойдет.
					Если ТекСтрокаКэша.РассчитаннаяСуммаСкидки>ОбработкаРасчетСкидокАвтомобили.СуммаДляРасчетаШапочнойСкидки Тогда
						ТекСтрокаКэша.НеПодходит = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Сортируем по убыванию.
		ОбработкаРасчетСкидокАвтомобили.КэшСкидок.Сортировать("НеПодходит ВОЗР, ЭтоПодарок УБЫВ, ФлагВытеснения УБЫВ, РассчитаннаяСуммаСкидки УБЫВ, ЗначениеСкидки УБЫВ");
		
		Если Не ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].НеПодходит Тогда
			// Подходящая скидка найдена, сразу заполним итоговую сумму скидки на документ.
			НайденноеЗначениеСкидки = ?(ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная, ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].РассчитаннаяСуммаСкидки, ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].ЗначениеСкидки);
			ТекущаяСуммаСкидки = ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].РассчитаннаяСуммаСкидки;
			Если (СкидкаНаценкаАвтомобили<>ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].Скидка) ИЛИ (ЗначениеСкидкиНаценкиАвтомобили<>НайденноеЗначениеСкидки) Тогда
				ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка  = ОбработкаРасчетСкидокАвтомобили.КэшСкидок[0].Скидка;
				ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки = НайденноеЗначениеСкидки;
			КонецЕсли;
		Иначе 
			Если Не СкидкаНаценкаАвтомобили.РучнаяСкидка Тогда
				СкидкаНаценкаАвтомобили = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецЕсли;			
			Если ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки<>0 Тогда
				ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки = 0;
				ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки    = 0; 
			КонецЕсли;			
		КонецЕсли;
		
		ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки=дкОкруглитьСуммуСкидки(ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки,ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка);
		
		ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСкидкаНаценка] = ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка;
		ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаЗначениеСкидкиНаценки] = ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки;
		ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСуммаСкидкиНаценки] = ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки;
		
		// Пересчитаем скидки для документа
		ПрименитьСкидку(ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка, ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки, ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки, ОбработкаРасчетСкидокАвтомобили.СуммаДляРасчетаШапочнойСкидки);
	КонецЕсли;
	
КонецПроцедуры

// Функция поиска заказа по автомобилю
Функция НайтиЗаказАвтомобиля(Автомобиль)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("Контрагент",Контрагент);
	ДокументОбъектСтруктура.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
	Возврат зфЗащищенныеФункцииСервер.РеализацияАвтомобилейНайтиЗаказАвтомобиля(ДокументОбъектСтруктура,Автомобиль);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	//ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	//ОбязательныеАвтомобили.Вставить("Автомобиль",1);
	//ОбязательныеАвтомобили.Вставить("ВариантКомплектации",2);
	ОбязательныеАвтомобили.Вставить("ИдентификаторАвтомобиля",1);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("ИдентификаторАвтомобиля",3);
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Заказчик",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;     	
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СчетНаОплатуЗаАвтомобили","Счет на оплату за автомобили",Истина);

	Возврат СписокХозОпераций;
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Автомобили.Итог("СуммаВсего")+Товары.Итог("СуммаВсего");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для Каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Результат=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Результат = ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиАвтомобили", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;

	ИначеЕсли Имя="Организация" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Организация может является плательщиком НДС, а может и нет.. надо обработать ТЧ
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли; 
			Результат=ОбработкаРеквизита("Автомобили.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла;
		Если НЕ Организация.Пустая() Тогда
			//БизТех 15.05.2023г. для некоторых подразделений требуется брать банковский счет из самого спр. Подразделния <<
			Если ЗначениеЗаполнено(ПодразделениеКомпании.ОсновнойБанковскийСчет) Тогда
				РасчетныйСчетОрганизации = ПодразделениеКомпании.ОсновнойБанковскийСчет; 
				// БизТех >>
			ИначеЕсли (РасчетныйСчетОрганизации.Пустая()) ИЛИ (НЕ РасчетныйСчетОрганизации.Владелец = Организация) Тогда
				РасчетныйСчетОрганизации = Организация.ОсновнойБанковскийСчет;	
			КонецЕсли;
		Иначе
			РасчетныйСчетОрганизации = Справочники.БанковскиеСчета.ПустаяСсылка();
		КонецЕсли
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Подразделение может является плательщиком НДС, а может и нет.. надо обработать ТЧ
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли; 
			Результат=ОбработкаРеквизита("Автомобили.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла;
		Результат=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиАвтомобили", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		КонецЕсли; 
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если обЗначениеНеЗаполнено(Заказчик) Тогда
			Если (НЕ обЗначениеНеЗаполнено(Контрагент)) И (Заказчик<>Контрагент) Тогда
				Заказчик=Контрагент;
				Результат=ОбработкаРеквизита("Заказчик",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			КонецЕсли; 
		КонецЕсли;
	ИначеЕсли Имя="Заказчик" Тогда
		Если (НЕ обЗначениеНеЗаполнено(Заказчик)) И (обЗначениеНеЗаполнено(Контрагент)) Тогда
			Контрагент=Заказчик;
			Результат=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		Иначе
			НеверныйДоговор=Ложь;
			Если ХозОперация=Справочники.ХозОперации.РеализацияАвтомобилей Тогда
				Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Продажа И
					 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
					 НеверныйДоговор=Истина;
				КонецЕсли; 
			ИначеЕсли ХозОперация=Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
				Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомиссионером И
					 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
					 НеверныйДоговор=Истина;
				КонецЕсли; 
			КонецЕсли;
			Если НеверныйДоговор Тогда
				#Если Клиент Тогда
				Предупреждение("Неправильный вид договора !");
				#КонецЕсли
				ДоговорВзаиморасчетов=Неопределено;
				ОбработкаРеквизита("ДоговорВзаиморасчетов",,ЭтаФорма);
				Результат=Ложь;
			КонецЕсли; 
		КонецЕсли;
		// Если у документа есть реквизит СкидкаНаценка, заполним его из Договора 
		Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов.ТипСкидкиНаценки)) И 
			 (СкидкаНаценкаАвтомобили<>ДоговорВзаиморасчетов.ТипСкидкиНаценки) Тогда
			 СкидкаНаценкаАвтомобили=ДоговорВзаиморасчетов.ТипСкидкиНаценки;
			 Результат=ОбработкаРеквизита("СкидкаНаценкаАвтомобили",,ЭтаФорма,ДопПараметры);
		КонецЕсли;		
		
	ИначеЕсли Имя="Карточка" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	ИначеЕсли Имя="СкидкаНаценкаАвтомобили" Тогда
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	//ОБРАБОТКА ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		//Проставим количество
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
		// Теперь получаем подставим значение скидки
		Попытка // документ может не поддерживать скидки
			Если (НЕ ЭтотОбъект.СкидкаНаценкаАвтомобили.Пустая()) И (ЭтотОбъект.СкидкаНаценкаАвтомобили.СпособВычисления = Перечисления.СкидкиСпособВычисления.Относительная) Тогда
				ТекСтрока.ПроцентСкидки=ЭтотОбъект.ЗначениеСкидкиНаценкиАвтомобили;
			Иначе
				ТекСтрока.ПроцентСкидки=0;
			КонецЕсли;
		Исключение КонецПопытки;
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
			Если ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли; 
		КонецЕсли; 
		//А теперь обработаем сам автомобиль
		Если ТипЗнч(ТекСтрока.Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			ТекСтрока.ВариантКомплектации=ТекСтрока.Автомобиль.ВариантКомплектации;
			ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации",ТекСтрока.Автомобиль,Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			             |	КомплектацияАвтомобилейОстатки.Номенклатура КАК Опция,
			             |	КомплектацияАвтомобилейОстатки.КоличествоОстаток КАК Количество
			             |ИЗ
			             |	РегистрНакопления.КомплектацияАвтомобилей.Остатки(&НаМомент,Автомобиль=&Автомобиль) КАК КомплектацияАвтомобилейОстатки
			             |ГДЕ
			             |	КомплектацияАвтомобилейОстатки.Автомобиль = &Автомобиль
			             |	И КомплектацияАвтомобилейОстатки.Номенклатура ССЫЛКА Справочник.Опции";
			Запрос.УстановитьПараметр("НаМомент",Дата);
			Запрос.УстановитьПараметр("Автомобиль",ТекСтрока.Автомобиль);
			Выборка=Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ТекСтрока.Цена=ТекСтрока.Цена+(обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации,Опция",ТекСтрока.Автомобиль.Модель,ТекСтрока.Автомобиль.ВариантКомплектации,Выборка.Опция),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента)*Выборка.Количество);
			КонецЦикла;
			Результат = ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		ИначеЕсли ТипЗнч(ТекСтрока.Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
			Если ТекСтрока.ВариантКомплектации.Владелец<>ТекСтрока.Автомобиль Тогда
				ТекСтрока.ВариантКомплектации=Неопределено;
			КонецЕсли; 
			ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации",ТекСтрока.Автомобиль,ТекСтрока.ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		#Если Клиент Тогда
			дкСообщитьОЗаметках(ЭтотОбъект,ТекСтрока.Автомобиль,ЭтаФорма);
		#КонецЕсли
		
		Результат=ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.ВариантКомплектации" Тогда
		ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации",ТекСтрока.Автомобиль,ТекСтрока.ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Результат=ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Количество" ИЛИ Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.ПроцентСкидки" Тогда
		// Скидка не может быть выше 100%
 		Если ТекСтрока.ПроцентСкидки>100 Тогда
			ТекСтрока.ПроцентСкидки = 100;
		КонецЕсли;
		
		Себестоимость = ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ТекСтрока);
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
		
		Если НЕ Себестоимость = 0 И НЕ ЦенаВключаетНДС Тогда
			СуммаРасчетная = Макс(ТекСтрока.Сумма-Себестоимость, 0);
			ТекСтрока.СуммаСкидки = Окр((ТекСтрока.Сумма + (СуммаРасчетная)*СтавкаНДС/100) * ТекСтрока.ПроцентСкидки / 100, 2);
		Иначе
			ТекСтрока.СуммаСкидки = Окр(?(ЦенаВключаетНДС, ТекСтрока.Сумма, ТекСтрока.Сумма+ТекСтрока.Сумма*(СтавкаНДС/100)) * ТекСтрока.ПроцентСкидки / 100, 2);
		КонецЕсли;
		ТекСтрока.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценкаАвтомобили);
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитыватьПроцент", Ложь);
		РезультатОбработки = ЭтотОбъект.ОбработкаРеквизита("Автомобили.СуммаСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаСкидки" Тогда
		// Выполним расчет итоговой суммы скидки на документ.
		Попытка	// Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцент = ДопПараметры.ПересчитыватьПроцент;
		Исключение 
			ПересчитыватьПроцент = Истина;	
		КонецПопытки;
		СуммаИсходная = 0;
		Если ТипЦен.ЦенаВключаетНДС Тогда
			СуммаИсходная = ТекСтрока.СуммаВсего + ТекСтрока.СуммаСкидки;
		Иначе
			СуммаИсходная = (ТекСтрока.СуммаВсего + ТекСтрока.СуммаСкидки - ТекСтрока.СуммаНДС);
			СуммаИсходная = СуммаИсходная + (СуммаИсходная*ТекСтрока.СтавкаНДС.Ставка/100);
		КонецЕсли;
		Если ПересчитыватьПроцент Тогда
			ТекСтрока.ПроцентСкидки = ?(СуммаИсходная=0, 0, Окр(ТекСтрока.СуммаСкидки * 100 /СуммаИсходная, 2));
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиАвтомобили = Автомобили.Итог("СуммаСкидки");
		РезультатОбработки = ОбработкаРеквизита("Автомобили.СтавкаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		
		Если ТипЗнч(ДопПараметры) = Тип("Структура")
			И ДопПараметры.Свойство("НеПересчитыватьСтавкуНДС") Тогда
			Возврат Истина;
		КонецЕсли;
		
		// Расчет НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
		
		// Рассчитываем новую сумму НДС.
		СуммаВсегоБезСкидки = ?(ЦенаВключаетНДС, ТекСтрока.Сумма, ТекСтрока.Сумма*(1+СтавкаНДС/100)) - ТекСтрока.СуммаСкидки;
		
		// Расчет НДС
		Себестоимость = ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ТекСтрока);
		Если НЕ Себестоимость = 0 И НЕ ЦенаВключаетНДС Тогда
			СуммаВсегоБезСкидки = ТекСтрока.Сумма - ТекСтрока.СуммаСкидки;
			СуммаБезСкидки = Макс(СуммаВсегоБезСкидки - Себестоимость, 0);
			ТекСтрока.СуммаНДС = Окр(СтавкаНДС/100*СуммаБезСкидки,2);
			ТекСтрока.СуммаВсего = СуммаВсегоБезСкидки + ТекСтрока.СуммаНДС;
		Иначе
			ТекСтрока.СуммаВсего = СуммаВсегоБезСкидки;
			Если НЕ Себестоимость = 0 Тогда
				СуммаВсегоБезСкидки = Макс(СуммаВсегоБезСкидки - Себестоимость, 0);
			КонецЕсли;
			ТекСтрока.СуммаНДС = Окр((СуммаВсегоБезСкидки*СтавкаНДС)/(100+СтавкаНДС),2);
		КонецЕсли;
		// В любом случаее идем в секцию "СуммаНДС", т.к. в ней будем произведен расчет суммы всего.
		РезультатОбработки = ЭтотОбъект.ОбработкаРеквизита("Автомобили.СуммаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаНДС" Тогда
		// Вычисляем сумму с учетом скидок.
		СуммаВсегоБезСкидки = ТекСтрока.Сумма - ТекСтрока.СуммаСкидки;
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда			
			ТекСтрока.СуммаВсего = СуммаВсегоБезСкидки;			
		Иначе
			ТекСтрока.СуммаВсего = СуммаВсегоБезСкидки + ТекСтрока.СуммаНДС;			
		КонецЕсли;
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
		СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
		
		Себестоимость = ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ТекСтрока);
		
		Если СкидкаНаценкаАвтомобили.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная Тогда
			СуммаБезСкидки = ТекСтрока.СуммаВсего + ТекСтрока.СуммаСкидки;
		Иначе
			СуммаБезСкидки = ТекСтрока.СуммаВсего*100/(100-ТекСтрока.ПроцентСкидки)
		КонецЕсли;
		
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если ЦенаВключаетНДС Тогда
			РасчетнаяСумма = СуммаБезСкидки;
		Иначе
			Если Себестоимость = 0 Тогда
				РасчетнаяСумма = СуммаБезСкидки - Окр((СуммаБезСкидки * СтавкаНДС)/(100 + СтавкаНДС),2);
			Иначе
				СуммаНДС       = Окр(((СуммаБезСкидки-Себестоимость)*СтавкаНДС)/(100+СтавкаНДС),2);
				РасчетнаяСумма = Макс(СуммаБезСкидки - СуммаНДС, 0);
			КонецЕсли;
		КонецЕсли;
		ТекСтрока.Сумма = РасчетнаяСумма;
		// Пересчитываем цену.
		ТекСтрока.Цена = ?(ТекСтрока.Количество=0,ТекСтрока.Сумма,Окр(ТекСтрока.Сумма/ТекСтрока.Количество,2));
		// Вычисляем новую сумму "шапочной" скидки приходящейся на текущую строку.
		ТекСтрока.СуммаСкидки = Окр(ТекСтрока.Сумма * ТекСтрока.ПроцентСкидки / 100, 2);
		ТекСтрока.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценкаАвтомобили);
		// Рассчитываем новую сумму НДС.
		СуммаВсегоБезСкидки = ?(ЦенаВключаетНДС, ТекСтрока.Сумма - ТекСтрока.СуммаСкидки, ТекСтрока.Сумма*(1+СтавкаНДС/100));
		Если НЕ Себестоимость = 0 И НЕ ЦенаВключаетНДС Тогда
			СуммаВсегоБезСкидки = ТекСтрока.Сумма - ТекСтрока.СуммаСкидки;
			СуммаБезСкидки = Макс(СуммаВсегоБезСкидки - Себестоимость, 0);
			ТекСтрока.СуммаНДС = Окр(СтавкаНДС/100*СуммаБезСкидки,2);
		Иначе
			Если НЕ Себестоимость = 0 Тогда
				СуммаВсегоБезСкидки = Макс(СуммаВсегоБезСкидки - Себестоимость, 0);
			КонецЕсли;
			ТекСтрока.СуммаНДС = Окр((СуммаВсегоБезСкидки*СтавкаНДС)/(100+СтавкаНДС),2);
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиАвтомобили = Автомобили.Итог("СуммаСкидки");
		
	ИначеЕсли Имя="РассчитатьСкидкиАвтомобили" Тогда	
		Попытка	
			НеРассчитыватьСкидки = ДопПараметры.НеРассчитыватьСкидки;
		Исключение 
			НеРассчитыватьСкидки = Ложь;
		КонецПопытки;
		
		Попытка
			БлокироватьПерерасчетСкидок = ЭтотОбъект.БлокироватьПерерасчетСкидок;
		Исключение
			БлокироватьПерерасчетСкидок = Ложь;
		КонецПопытки;
		
		ОбработкаРасчетСкидокАвтомобили.СкидкаНаценка=СкидкаНаценкаАвтомобили;
		// Подбираем подходящую скидку. Если скидка не изменилась, то функция вернет Ложь.
		
		Если БлокироватьПерерасчетСкидок Тогда
			Попытка
				Если обЗначениеНеЗаполнено(ЭтотОбъект.СкидкаНаценкаАвтомобили) Тогда
					Для Каждого стрДок Из ЭтотОбъект.Автомобили Цикл
						стрДок.ПроцентСкидки = 0;
						ЭтотОбъект.ОбработкаРеквизита("Автомобили.ПроцентСкидки", стрДок, ЭтаФорма);
					КонецЦикла;
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			Если ТекСтрока = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ЭтотОбъект.ОбработкаРеквизита("Автомобили.ПроцентСкидки", ТекСтрока, ЭтаФорма);
			Возврат Истина;
		ИначеЕсли (НЕ НеРассчитыватьСкидки) И ОбработкаРасчетСкидокАвтомобили.ПодобратьСкидку(ЭтотОбъект) Тогда
			ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСкидкаНаценка] = ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка;
			ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаЗначениеСкидкиНаценки] = ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки;
			ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСуммаСкидкиНаценки] = ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки;
			// Подобралась другая "шапочная" скидка, значит, возможно, придется заблокировать работу с некоторыми колонками 
			// в зависимости от типа скидки.
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					дкУправлениеДоступностьюКолонокСкидок(ЭтаФорма, ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка, ОбработкаРасчетСкидокАвтомобили.ИмяТабличнойЧасти);	
				КонецЕсли;
			#КонецЕсли
			// Применяем скидку к табличной части. Пересчитываем строчные скидки.
			ОбработкаРасчетСкидокАвтомобили.ПрименитьСкидку(ЭтотОбъект);
			ПересчетСкидокАвтомобилей();
			
			#Если Клиент Тогда					
				Если ЭтаФорма<>Неопределено Тогда
					дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);		
				КонецЕсли;  
			#КонецЕсли 
		Иначе // Просто пересчитываем значение суммы скидки для текущей строки.
			Если ТекСтрока<>Неопределено Тогда									
				// Рассчитываем "шапочную" скидку для текущей строки. За одно рассчитаем общую сумму скидки на документ.
				Если СкидкаНаценкаАвтомобили.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ОбработкаРеквизита("Автомобили.ПроцентСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);				
				Иначе
					ОбработкаРеквизита("Автомобили.СуммаСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);				
				КонецЕсли;
			Иначе
				// Если произошла смена типа скидки, но значение скидки не изменилась, то пересчитывать ТЧ нет необходимости, но
				// сохранить новый тип скидки нужно.
				СкидкаНаценкаАвтомобили = ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка;
				СуммаСкидкиНаценкиАвтомобили = Автомобили.Итог("СуммаСкидки");					
			КонецЕсли;			
		КонецЕсли;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		//А вот тут мы загрузим те цены, которые указаны в регистре комплектации
		СтрокаАвтомобиля=Автомобили.Найти(ТекСтрока.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаАвтомобиля=Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		Автомобиль=СтрокаАвтомобиля.Автомобиль;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	СУММА(КомплектацияАвтомобилейОстатки.КоличествоОстаток) КАК Количество,
		             |	СУММА(КомплектацияАвтомобилейОстатки.СуммаПродажиУпрОстаток) КАК СуммаПродажиУпр
		             |ИЗ
		             |	РегистрНакопления.КомплектацияАвтомобилей.Остатки(
		             |		&НаДату,
		             |		Автомобиль = &Автомобиль
		             |		    И Номенклатура = &Номенклатура
		             |		    И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК КомплектацияАвтомобилейОстатки";
		Запрос.УстановитьПараметр("НаДату",?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),Новый Граница(Дата,ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("Автомобиль",Автомобиль);
		Запрос.УстановитьПараметр("Номенклатура",ТекСтрока.Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
		ОстаткиОборудования=Запрос.Выполнить().Выгрузить();
		ТекСтрока.Количество=ОстаткиОборудования.Итог("Количество");
		Если обЗначениеНеЗаполнено(ТекСтрока.Количество) Тогда ТекСтрока.Количество=0; КонецЕсли; 
		ОстатокСуммы=ОстаткиОборудования.Итог("СуммаПродажиУпр");
		Если обЗначениеНеЗаполнено(ОстатокСуммы) Тогда ОстатокСуммы=0; КонецЕсли; 
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
		Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
			КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр        = КурсВалютыУпр;
		КонецЕсли;
		ОстатокСуммы=обПересчет(ОстатокСуммы,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если НЕ ЦенаВключаетНДС Тогда
			//НДС в цену не включен
			ОстатокСуммы=(100*ОстатокСуммы)/(100+ТекСтрока.СтавкаНДС.Ставка);
		КонецЕсли; 
		ТекСтрока.Сумма=ОстатокСуммы;
		Результат=ОбработкаРеквизита("Товары.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	Иначе 
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "СчетНаОплатуЗаАвтомобили"
// Возвращает сформированный табличный документ:
Функция ПечатьСчетНаОплатуЗаАвтомобили(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("СчетНаОплатуЗаАвтомобили");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод образца заполнения платёжного поручения
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета"); 
	ОбластьМакета.Параметры.БанкПолучателя						= РасчетныйСчетОрганизации.Банк;
	ОбластьМакета.Параметры.БанкПолучателяПредставление			= спПолучитьНаименование(РасчетныйСчетОрганизации.Банк);
	ОбластьМакета.Параметры.БИКБанкаПолучателя					= Символы.НПП+СокрЛП(РасчетныйСчетОрганизации.Банк.Код);
	ОбластьМакета.Параметры.СчетБанкаПолучателя					= Символы.НПП+СокрЛП(РасчетныйСчетОрганизации.Банк.КоррСчет);
	ОбластьМакета.Параметры.ИНН									= Символы.НПП+спПолучитьПредставление(Организация,Новый Структура("ИНН"));
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.КПП									= Символы.НПП+спПолучитьПредставление(Организация,Новый Структура("КПП"), ДополнительныеПараметры);
	ОбластьМакета.Параметры.Получатель							= Организация;
	ОбластьМакета.Параметры.ПолучательПредставление				= спПолучитьПредставление(Организация,Новый Структура("Наименование"));
	ОбластьМакета.Параметры.СчетПолучателя						= Символы.НПП+СокрЛП(РасчетныйСчетОрганизации.НомерСчета);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект,"Счет на оплату");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтруктураПредставления = Новый Структура("ИНН,КПП,Наименование,АдресЮридический,ТелефонРабочий","ИНН ","КПП ","","","тел.: ");
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация,СтруктураПредставления, ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Контрагент, СтруктураПредставления);
	ОбластьМакета.Параметры.ПредставлениеДоговорВзаиморасчетов = Строка(ДоговорВзаиморасчетов);
	ОбластьМакета.Параметры.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;

	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод документа основания
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ДокументОснование");
		ОбластьМакета.Параметры.ДокументОснованиеПредставление=дкПолучитьПредставление(ДокументОснование);
		ОбластьМакета.Параметры.ДокументОснование=ДокументОснование;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 
	
	//вывод срока действия счета
	Если ЗначениеЗаполнено(ДействителенДо) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ДействителенДо");
		ОбластьМакета.Параметры.ДействителенДо = Формат(ДействителенДо,"ДФ = dd.MM.yyyy");
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//подготовим дополнительную таблицу
	ТаблицаТоваровПоАвтомобилям = ЭтотОбъект.Товары.Выгрузить();
	ТаблицаТоваровПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаСкидки,СуммаВсего");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиТовары = ТаблицаТоваровПоАвтомобилям.Найти(СтрокаТабличнойЧасти.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиТовары <> Неопределено Тогда
			СтрокаТабличнойЧасти.Сумма		= СтрокаТабличнойЧасти.Сумма		+ СтрокаТабличнойЧастиТовары.Сумма;
			СтрокаТабличнойЧасти.СуммаНДС	= СтрокаТабличнойЧасти.СуммаНДС		+ СтрокаТабличнойЧастиТовары.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаВсего	= СтрокаТабличнойЧасти.СуммаВсего	+ СтрокаТабличнойЧастиТовары.СуммаВсего;
		КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтрокаКомментарий=СокрЛП(СтрокаТабличнойЧасти.Комментарий);
		Если НЕ ПустаяСтрока(СтрокаКомментарий) Тогда
			СтруктураСтроки.АвтомобильНаименование = СокрЛП(СтрокаКомментарий);
		КонецЕсли;
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") > 0 Тогда
		СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
		ОбластьМакета.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	Руководитель.РуководительПредставление = ?(обЗначениеНеЗаполнено(Руководитель.Руководитель),"","/"+Руководитель.РуководительПредставление+"/");
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/"+ГлавныйБухгалтер.ГлавныйБухгалтерПредставление+"/");
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ДопПараметры = Новый Структура;
	
	// Ставку НДС для документа была сформирована ранее
	Если ЗначениеЗаполнено(Основание) 
		И ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		ДопПараметры.Вставить("НеПересчитыватьСтавкуНДС");
	КонецЕсли;
	
	ОбработкаРеквизита("Организация",,,ДопПараметры);
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОбработкаРасчетСкидок=Обработки.РасчетСкидок.Создать();
//ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки=Истина;

КешСебестоимостиАвтомобилейКупленныхУФизЛица = Неопределено;

ОбработкаРасчетСкидокАвтомобили=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСкидкаНаценка="СкидкаНаценкаАвтомобили";
ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаЗначениеСкидкиНаценки="ЗначениеСкидкиНаценкиАвтомобили";
ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСуммаСкидкиНаценки="СуммаСкидкиНаценкиАвтомобили";
ОбработкаРасчетСкидокАвтомобили.НеРассчитыватьСтрочныеСкидки=Истина;
ОбработкаРасчетСкидокАвтомобили.ИмяТабличнойЧасти="Автомобили";
ОбработкаРасчетСкидокАвтомобили.НеРассчитыватьАвтоматическиеСкидки=Истина;

// Модуль документа БюджетДоходовИРасходов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ВалютаУпр;					// Валюта управленческого учета
Перем ВалютаРег;					// Валюта регламентированного учета 
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеРеквизитыТабЧасти = Новый Структура();
	ОбязательныеРеквизитыТабЧасти.Вставить("СтатьяРасходовИДоходов", 3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("СценарийПланирования", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);  	
	ОбязательныеРеквизиты.Вставить("РасходыИДоходы", ОбязательныеРеквизитыТабЧасти);
		
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки = "", ДопРеквизиты = Неопределено, Заполнение = Истина, Уникальность = Истина) Экспорт
	
	Результат = Истина;	
	
	// Запретим запись с невыбранным сценарием планирования
	Если обЗначениеНеЗаполнено(СценарийПланирования) ИЛИ обЗначениеНеЗаполнено(СценарийПланирования.Периодичность) Тогда
		дкДобавитьОшибку(Ошибки, "Не выбран сценарий планирования, либо у выбранного сценария не выбрана периодичность планирования.");		
		Результат = Ложь;		
	КонецЕсли;
	
	// проверим правильность введенного подразделения хоз.операции.
	Результат = плПроверитьКорректностьВыбораПодразделенияИХозОперации(ХозОперация, ПодразделениеКомпании) И Результат;
	Если НЕ Результат Тогда
		дкДобавитьОшибку(Ошибки, "Для данного подразделения нельзя вводить данный вид бюджета.");		
	КонецЕсли;
	
	Результат = (дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И Результат);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты = Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя, ТекСтрока = Неопределено, ЭтаФорма = Неопределено, ДопПараметры = Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "СценарийПланирования" Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		плПолучитьДатыПланируемогоПериода(Параметры); 	
		ДатаПланирования = Параметры.ДатаНачала;  
		
		// ТАБЛИЧНАЯ ЧАСТЬ
	ИначеЕсли Имя = "РасходыИДоходы.СтатьяРасходовИДоходов" Тогда
		Если ТекСтрока.СтатьяРасходовИДоходов.ВидСтатьи = Перечисления.ВидыСтатейДоходовИРасходов.ВыручкаОтОсновногоВидаДеятельности
			или ТекСтрока.СтатьяРасходовИДоходов.ВидСтатьи = Перечисления.ВидыСтатейДоходовИРасходов.НакладныеДоходы Тогда 
			ТекСтрока.СуммаРасход = 0;		
		Иначе
			ТекСтрока.СуммаДоход = 0;
		КонецЕсли; 		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);		
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("БюджетДоходовИРасходов"   ,"Бюджет расходов и доходов", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда  	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// ПечатьБюджетДоходовИРасходов - Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьБюджетДоходовИРасходов(ТабДокумент) Экспорт
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	
	// Получаем макет
	Макет = ПолучитьМакет("БюджетДоходовИРасходов");      
	
	// Заголовок
	Параметры = Новый Структура;
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	Параметры.Вставить("ДатаИзПериода", ДатаПланирования);                                                 		
	ПредставлениеПериода = плПолучитьДатыПланируемогоПериода(Параметры);
	
	Параметры.Очистить(); 	 
	Параметры.Вставить("Область", "Заголовок");		
	Параметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	Параметры.Вставить("СценарийПланирования",  СценарийПланирования);
	Параметры.Вставить("ТекстЗаголовка",        ХозОперация.Наименование + " № " + дкПолучитьНомерДляПечати(ЭтотОбъект) + " от " + Формат(Дата,"ДФ=dd.MM.yyyy"));
	Параметры.Вставить("ПериодПланирования",    ПредставлениеПериода);     	
	Параметры.Вставить("Список",                Новый СписокЗначений);
	
	ОбластьМакета = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	ТабДокумент.Вывести(ОбластьМакета);

	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;

	// Готовим и выводим шапку
	Параметры.Очистить();
	Параметры.Вставить("Область",   "ШапкаТаблицы");	
	Параметры.Вставить("ТипСтатей", "Статьи доходов и расходов");
	Параметры.Вставить("Список",    Новый СписокЗначений);
	
	ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	// Подготовим шапку для дальнейшего использования, если строки не влезут на страницу
	Параметры.Вставить("ТекстЗаголовка", ХозОперация.Наименование + " № " + дкПолучитьНомерДляПечати(ЭтотОбъект) + " от " + Формат(Дата, "ДФ=dd.MM.yyyy"));
	ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// Подготовим подвал
	Параметры.Вставить("Список",           Новый СписокЗначений);
	Параметры.Вставить("Область",          "Подвал");
	Параметры.Вставить("Автор",            Автор);
	Параметры.Вставить("ВалютаДокумента",  Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	Параметры.Вставить("ИтогоСуммаДоход",  Формат(РасходыИДоходы.Итог("СуммаДоход"),  ФорматВыводаСуммы) );
	Параметры.Вставить("ИтогоСуммаРасход", Формат(РасходыИДоходы.Итог("СуммаРасход"), ФорматВыводаСуммы) );
	ОбластьПодвалаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// Ну теперь строки пойдут.
	Параметры.Очистить();
	
	ИтогиНаСтранице = Новый Структура;
	ИтогиНаСтранице.Вставить("СуммаДоход", 0);
	ИтогиНаСтранице.Вставить("СуммаРасход", 0);
	
	НомерСтроки = 1;	
	Для Каждого ТекСтрока Из РасходыИДоходы Цикл
		
		// Подготавливаем список
		Параметры.Вставить("Область", "Строка");
		Параметры.Вставить("Список",          Новый СписокЗначений); 
		Параметры.Вставить("СтатьяРасходов",  ТекСтрока.СтатьяРасходовИДоходов);
		Параметры.Вставить("СуммаДоход",      Формат(ТекСтрока.СуммаДоход, ФорматВыводаСуммы) );
		Параметры.Вставить("СуммаРасход",     Формат(ТекСтрока.СуммаРасход,ФорматВыводаСуммы) );
		Параметры.Вставить("НомерСтроки",     НомерСтроки);  		
		ОбластьСтрокиТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);
		
		// Сформируем итоговую область (Подвал страницы)
		Параметры.Вставить("Область",  "ПодвалСтраницы");
		Параметры.Вставить("СуммаДоходИтогоНаСтранице",  Формат(ИтогиНаСтранице.СуммаДоход, ФорматВыводаСуммы) );
		Параметры.Вставить("СуммаРасходИтогоНаСтранице", Формат(ИтогиНаСтранице.СуммаРасход, ФорматВыводаСуммы) );  
		ОбластьПодвалаСтраницыТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры); 
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если НомерСтроки = РасходыИДоходы.Количество() Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвалаТаблицы);
		КонецЕсли;
		
		НомерСтраницы = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокиТаблицы, ОбластьШапкаТаблицы, ОбластьПодвалаСтраницыТаблицы,
			НомерСтраницы, Права, мсвДопОбластиПодвала);		
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			ИтогиНаСтранице = Новый Структура;
			ИтогиНаСтранице.Вставить("СуммаДоход", 0);
			ИтогиНаСтранице.Вставить("СуммаРасход", 0);
			
			НомерСтраницыПред = НомерСтраницы;
			
			Параметры.Вставить("ТекстЗаголовка", ХозОперация.Наименование + " № " + дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy"));
			ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	

		КонецЕсли;
		
		//добавляем итоги
		ИтогиНаСтранице.СуммаДоход = ИтогиНаСтранице.СуммаДоход + ТекСтрока.СуммаДоход;
		ИтогиНаСтранице.СуммаРасход = ИтогиНаСтранице.СуммаРасход + ТекСтрока.СуммаРасход;
										  		
		НомерСтроки = НомерСтроки + 1; 	
	КонецЦикла;
	
	Если НомерСтраницы > 2 Тогда
		ТабДокумент.Вывести(ОбластьПодвалаСтраницыТаблицы);
	КонецЕсли;
	
	// Выводим подвал
	НомерСтраницы = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалаТаблицы, , , НомерСтраницы, Права);
		
	Возврат ТабДокумент; 
КонецФункции // ПечатьБюджетДоходовИРасходов()

// СформироватьОбластьТабличногоДокумента - Формирует область табличного документа.
//
// Параметры:
//  Макет     - макет, переданный для формирования табличного документа.
//  Параметры - структура, передает параметры заполнения области табличного документа.
//
// Возвращаемое значение:
//  ТабДок - Возвращает табличный документ.
//
Функция СформироватьОбластьТабличногоДокумента(Знач Макет, Знач Параметры)
	ТабДок = Новый ТабличныйДокумент;
	ТекОблПериод = Макет.ПолучитьОбласть("" + Параметры.Область + "|СтатьяРасходов"); 	
	ЗаполнитьЗначенияСвойств(ТекОблПериод.Параметры, Параметры);		
	ТабДок.Вывести(ТекОблПериод);
	
	Для Каждого ТекПериод Из Параметры.Список Цикл
		ТекОблПериод = Макет.ПолучитьОбласть("" + Параметры.Область + "|Период");
		ЗаполнитьЗначенияСвойств(ТекОблПериод.Параметры, ТекПериод);
		ТабДок.Присоединить(ТекОблПериод);
	КонецЦикла;
	
	ТабДок.Присоединить(Макет.ПолучитьОбласть("" + Параметры.Область + "|Граница"));
	
	Возврат ТабДок;
КонецФункции // СформироватьОбластьТабличногоДокумента()

// ВывестиГоризонтальнуюОбласть - выводит переданные области табличного документа
// 
// Параметры:
//  ТабличныйДокумент       - табличный документ.
//  ОбластьСтроки           - табличный документ, содержит область строки,
//  ОбластьШапкиСтраницы    - табличный документ, содержит область Шапки,
//  ОбластьПодвалаСтраницы  - табличный документ, содержит область Подвала,
//  НомерСтраницы           - число номер текущей страницы,
//  Право                   - переменная, в которой кэшируются права пользователя
//  мсвДопОбластиПодвала    - массив.
//
//  Возвращаемое значение:
//   НомерСтраницы          - число, номер страницы.
//
Функция ВывестиГоризонтальнуюОбласть(ТабличныйДокумент, ОбластьСтроки, ОбластьШапкиСтраницы, ОбластьПодвалаСтраницы=Неопределено, НомерСтраницы, Право, мсвДопОбластиПодвала=Неопределено)
	ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы", Права,,ЭтотОбъект);
	
	//если не разбиваем, выводим и все
	Если НЕ ИспользоватьРазбиениеНаСтраницы Тогда
		ТабличныйДокумент.Вывести(ОбластьСтроки);
		Возврат НомерСтраницы;
	КонецЕсли;
	
	МассивОбластейДляПроверки = Новый Массив;
	МассивОбластейДляПроверки.Добавить(ОбластьСтроки);
	Если ОбластьПодвалаСтраницы <> Неопределено Тогда
		МассивОбластейДляПроверки.Добавить(ОбластьПодвалаСтраницы);
	КонецЕсли;
	
	Попытка
		РезультатПроверки = ТабличныйДокумент.ПроверитьВывод(МассивОбластейДляПроверки);
		Если НЕ РезультатПроверки Тогда
			Если ОбластьПодвалаСтраницы <> Неопределено Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвалаСтраницы);
			КонецЕсли;
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			НомерСтраницы = НомерСтраницы + 1;
			
			Если ОбластьШапкиСтраницы <> Неопределено Тогда
				ТабличныйДокумент.Вывести(ОбластьШапкиСтраницы);
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ТабличныйДокумент.Вывести(ОбластьСтроки);
		
	Возврат НомерСтраницы;   	
КонецФункции // ВывестиГоризонтальнуюОбласть()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы = "", КоличествоЭкземпляров = 0, НаПринтер = Ложь, Документ = Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)  	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	        	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , , ,"10111") Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)   	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли; 
	
	// Собственно само проведение
	НаборЗаписей = Движения.БюджетДоходовИРасходов;
	НаборЗаписей.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписей.ДокументОбъект        = Ссылка;
	НаборЗаписей.РежимПроведения       = Режим;
	
	Отказ = НЕ НаборЗаписей.Приход();
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;  
	
	дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);  	
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
ВалютаРег = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
   
// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Модуль документа ПереоценкаАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// пересчитывает количество, старую цену и старую сумму.
// ТекСтрока - строка табличной части, если не определена, то пересчитывается вся табличная часть
Процедура ПересчитатьСтроку(ТекСтрока=Неопределено) Экспорт
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("ХозОперация",ХозОперация);
	ДокументОбъектСтруктура.Вставить("ВалютаДокумента",ВалютаДокумента);
	ДокументОбъектСтруктура.Вставить("КурсДокумента",КурсДокумента);
	ДокументОбъектСтруктура.Вставить("КурсВалютыУпр",КурсВалютыУпр);
	ДокументОбъектСтруктура.Вставить("СкладКомпании",СкладКомпании);
	ДокументОбъектСтруктура.Вставить("Контрагент",Контрагент);
	ДокументОбъектСтруктура.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.Выгрузить());
	Если ТекСтрока=Неопределено Тогда
		ДокументОбъектСтрока=Неопределено;
	Иначе
		ДокументОбъектСтрока=Новый Структура();
		Для каждого РеквизитТабличнойЧасти Из ЭтотОбъект.Метаданные().ТабличныеЧасти.Автомобили.Реквизиты Цикл
			ДокументОбъектСтрока.Вставить(РеквизитТабличнойЧасти.Имя,ТекСтрока[РеквизитТабличнойЧасти.Имя]);
		КонецЦикла; 
	КонецЕсли; 
	зфЗащищенныеФункцииСервер.ПереоценкаАвтомобилейПересчитатьСтроку(ДокументОбъектСтруктура,ДокументОбъектСтрока);
	Если ТекСтрока=Неопределено Тогда
		Автомобили.Загрузить(ДокументОбъектСтруктура.Автомобили);
	Иначе
		Для каждого РеквизитТабличнойЧасти Из ДокументОбъектСтрока Цикл
			ТекСтрока[РеквизитТабличнойЧасти.Ключ]=РеквизитТабличнойЧасти.Значение;
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("ДокументПередачи",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейПринятыхНаКомиссию ИЛИ
		 ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейНаОтветственномХранении Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Если ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейПринятыхНаКомиссию ИЛИ
				 ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейНаОтветственномХранении Тогда
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;	
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыАвтомобили = Новый Структура();
				КонтролируемыеРеквизитыАвтомобили.Вставить("ДокументПередачи");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Автомобили",КонтролируемыеРеквизитыАвтомобили);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И Результат;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПереоценкаАвтомобилейПринятыхНаКомиссию","Переоценка автомобилей принятых на комиссию",Истина);
	СписокХозОпераций.Добавить("ПереоценкаАвтомобилейОтданныхНаКомиссию","Переоценка автомобилей отданных на комиссию",Ложь);
	СписокХозОпераций.Добавить("ПереоценкаАвтомобилейНаОтветственномХранении","Переоценка автомобилей на ответственном хранении",Ложь);
	Возврат СписокХозОпераций;
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Автомобили.Итог("Сумма");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
				ПересчитатьСтроку(СтрокаТЧ);
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ПереоценкаАвтомобилейОтданныхНаКомиссию Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		ИначеЕсли ХозОперация = Справочники.ХозОперации.ПереоценкаАвтомобилейПринятыхНаКомиссию Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании = Неопределено;
			КонецЕсли; 
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Для Каждого СтрокаАвтомобилей Из Автомобили Цикл
			ПересчитатьСтроку(СтрокаАвтомобилей);
		КонецЦикла;
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		// установим количество
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
		ПересчитатьСтроку(ТекСтрока);
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.ДокументПередачи" Тогда
		ПересчитатьСтроку(ТекСтрока);
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Цена" Тогда	
		ТекСтрока.Сумма=(ТекСтрока.Цена-ТекСтрока.ЦенаСтарая)*ТекСтрока.Количество;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда	
		ТекСтрока.Сумма=(ТекСтрока.Цена-ТекСтрока.ЦенаСтарая)*ТекСтрока.Количество;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПереоценкаАвтомобилей"
// Возвращает сформированный табличный документ:
Функция ПечатьПереоценкаАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПереоценкаАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("ЦенаСтарая",Формат(СтрокаТабличнойЧасти.ЦенаСтарая,ФорматВыводаСуммы));
		ПроцентНаценки = ?(СтрокаТабличнойЧасти.ЦенаСтарая=0,0,100*(СтрокаТабличнойЧасти.Цена-СтрокаТабличнойЧасти.ЦенаСтарая)/СтрокаТабличнойЧасти.ЦенаСтарая);
		СтруктураСтроки.Вставить("ПроцентНаценки",Формат(ПроцентНаценки,"ЧЦ=6; ЧДЦ=2"));
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;	
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если (ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейОтданныхНаКомиссию) И 
		 (НЕ обЗначениеНеЗаполнено(СкладКомпании)) Тогда
		 СкладКомпании=Неопределено;
	КонецЕсли; 
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 	
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	ПересчитатьСтроку();
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейПринятыхНаКомиссию ИЛИ
		 ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейНаОтветственномХранении Тогда
		// переоцениваем регистр "ОстаткиАвтомобилей"
		НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
		НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
		Если ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейНаОтветственномХранении Тогда
			НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварОтветственноеХранение;
		Иначе
			НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
		КонецЕсли; 
		НаборЗаписейОстаткиАвтомобилей.СкладПолучатель=Неопределено;
		НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям=Неопределено;
		НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
		Отказ = НЕ НаборЗаписейОстаткиАвтомобилей.Переоценка() ИЛИ Отказ;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейОтданныхНаКомиссию Тогда
		// переоцениваем регистр "АвтомобилиОтданные"
		НаборЗаписейАвтомобилиОтданные=Движения.АвтомобилиОтданные;
		НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейАвтомобилиОтданные.Сторно=Ложь;
		НаборЗаписейАвтомобилиОтданные.РезультатЗапросаПоАвтомобилям=Неопределено;
		НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
		НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Отказ = НЕ НаборЗаписейАвтомобилиОтданные.Переоценка() ИЛИ Отказ;
		
		//// получим доходы и расходы
		//ТаблицаДиР=НаборЗаписейАвтомобилиОтданные.Выгрузить();
		//ТаблицаДиР.Свернуть("ВидДвижения","СуммаУпр");
		//СтрокаПриход = ТаблицаДиР.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
		//СтрокаРасход = ТаблицаДиР.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
		//ПриходСумма=0;
		//Если СтрокаПриход<>Неопределено Тогда ПриходСумма = СтрокаПриход.СуммаУпр; КонецЕсли; 
		//РасходСумма=0;
		//Если СтрокаРасход<>Неопределено Тогда РасходСумма = СтрокаРасход.СуммаУпр; КонецЕсли; 
		//НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
		//НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
		//НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.ТорговаяНаценка;
		//НаборЗаписейДиР.ВУпрВалюте				= Истина;
		//НаборЗаписейДиР.Доход=ПриходСумма-РасходСумма;
		//Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	
	КонецЕсли;
	
	// двигаем границу последовательности автомобилей
	ДвигаемГраницу = ((ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейПринятыхНаКомиссию ИЛИ 
					   ХозОперация=Справочники.ХозОперации.ПереоценкаАвтомобилейНаОтветственномХранении) И 
					   Режим<>РежимПроведенияДокумента.Оперативный И 
					   Движения.ОстаткиАвтомобилей.Количество()>0
					  );
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

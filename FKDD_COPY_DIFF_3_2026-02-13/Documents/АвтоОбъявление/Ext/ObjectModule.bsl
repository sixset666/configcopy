// Модуль документа АвтоОбъявление
   
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ОбработкаАвтоОбъявления Экспорт; // Переменная для хранения обработки-объекта АвтоОбъявления

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация"          ,1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор"                ,1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента"      ,1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента"        ,1);
	ОбязательныеРеквизиты.Вставить("ТипЦен"               ,1);
	ОбязательныеРеквизиты.Вставить("ХозОперация"          ,1);
	ОбязательныеРеквизиты.Вставить("Состояние"            ,1);
	ОбязательныеРеквизиты.Вставить("Сервис"               ,1);
	ОбязательныеРеквизиты.Вставить("Автомобиль"           ,1);
		
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("АвтоОбъявление","Объявление о продаже автомобиля",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Автомобиль" Тогда
		Рез=Истина;
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			ЦенаАвтомобиля = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(), Дата, МоментВремени()),,ВалютаДокумента,КурсДокумента);
		Иначе
			ЦенаАвтомобиля = 0;
		КонецЕсли;	
		Возврат Рез;
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;             
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// инициализирует обработку авто-объявлений
Процедура ИнициализацияОбработкиАвтоОбъявления() Экспорт
	Если ОбработкаАвтоОбъявления = Неопределено Тогда
		ОбработкаАвтоОбъявления = Обработки.АвтоОбъявления.Создать();
	КонецЕсли;
КонецПроцедуры	

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Авто-объявление"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьАвтоОбъявление(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("АвтоОбъявление");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок"); 
	
	ОбластьМакета.Параметры.ТекстЗаголовка = Ссылка;
	
	ОбластьМакета.Параметры.Модель       = Автомобиль.Модель;
	ОбластьМакета.Параметры.Комплектация = ?(Автомобиль.ВариантКомплектации.Пустая(), "<Не указана>", Автомобиль.ВариантКомплектации);
	ОбластьМакета.Параметры.VIN          = Автомобиль.VIN;
	ОбластьМакета.Параметры.ТипКузова    = ?(Автомобиль.ВариантКомплектации.Пустая(), Автомобиль.ТипКузова, Автомобиль.ВариантКомплектации.ТипКузова);
	ОбластьМакета.Параметры.ТипДвигателя = ?(Автомобиль.ВариантКомплектации.Пустая(), Автомобиль.ТипДвигателя, Автомобиль.ВариантКомплектации.ТипДвигателя);
	ОбластьМакета.Параметры.ТипКПП       = ?(Автомобиль.ВариантКомплектации.Пустая(), Автомобиль.ТипКПП, Автомобиль.ВариантКомплектации.ТипКПП);
	ОбластьМакета.Параметры.ГодВыпуска   = Формат(Автомобиль.ГодВыпуска, "ДФ=yyyy");
	ОбластьМакета.Параметры.Пробег       = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег);
	ОбластьМакета.Параметры.Цвет         = Автомобиль.Цвет;
	
	ОбластьМакета.Параметры.Стоимость    = Формат(ЦенаАвтомобиля, "ЧЦ=15; ЧДЦ=2");
	
	ОбластьМакета.Параметры.Сервис       = Сервис;
	ОбластьМакета.Параметры.Состояние    = Состояние;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы"); 
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	//Для Каждого ТекСтрока Из карти Цикл
	//	ОбластьМакета.Параметры.Заполнить(ТекСтрока);
	//	ТабДокумент.Вывести(ОбластьМакета);
	//КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал"); 
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАвтоОбъявление()	

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли; 	
	Если НЕ ЗначениеЗаполнено(Состояние) Тогда
		Состояние = Перечисления.СостоянияАвтоОбъявлений.ВРаботе;
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Сервис) Тогда
		Сервис = Перечисления.СервисыАвтоОбъявлений.Автоклауд;
	КонецЕсли; 
	
	ТипЦенНовый = обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
	Если ТипЦен<>ТипЦенНовый Тогда
		ТипЦен=ТипЦенНовый;
		Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
			ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
		КонецЕсли; 
	КонецЕсли; 
	
	Если    ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеАвтомобилей") 
		Или ТипЗнч(Основание) = Тип("ДокументСсылка.ВводОстатковАвтомобилей") 
		Тогда					
		Автомобиль = Основание.Автомобили[0].Автомобиль;
		ОбработкаРеквизита("Автомобиль");
	ИначеЕсли ТипЗнч(Основание) = Тип("СправочникСсылка.Автомобили") Тогда 
		Автомобиль = Основание;
		ОбработкаРеквизита("Автомобиль");
	КонецЕсли;   	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Идентификатор = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	СуммаДокумента = ЦенаАвтомобиля;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОбработкаАвтоОбъявления = Неопределено;

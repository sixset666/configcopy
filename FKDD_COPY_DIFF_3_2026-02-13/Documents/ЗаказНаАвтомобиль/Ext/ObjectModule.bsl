// Модуль документа

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаРасчетСкидокНаАвтомобиль Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа
Перем НеЗаполнятьКонтрагента Экспорт;	// Отказ от заполнения контрагента значением по умолчанию

Перем ОборудованиеБазовое Экспорт;
Перем ОборудованиеПроизводителя Экспорт;
Перем ОборудованиеПродавца Экспорт;

Перем КешСебестоимостиАвтомобилейКупленныхУФизЛица; // Переменная для хранения значения валюты регламентированного учета

//Перем ДокументыДляПерепроведения;     // Список документов для проведения

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


// Функция возвращает положительную себестоимость автомобиля в валюте документа если 
// этот автомобиль был приобретен у физического лица
//
// Параметры:
//  СтрокаТЧ       - строка табличной части Товары
//
// Возвращаемое значение:
//  Себестоимость - Число себестоимость автомобиля в валюте документа
// 
Функция ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ЭтаФорма)
	
	Себестоимость = 0;
	
	// Проверим есть ли автомобиль на складе
	Если НЕ ПроверитьОстаткиАвтомобилей() Тогда
		СебестоимостьАвтомобиля = 0;
	Иначе
		
		ДокументОбъектСтруктура=Новый Структура();
		ДокументОбъектСтруктура.Вставить("Дата",Дата);
		ДокументОбъектСтруктура.Вставить("ХозОперация",Справочники.ХозОперации.РеализацияАвтомобилей);
		ДокументОбъектСтруктура.Вставить("ВалютаДокумента",ВалютаДокумента);
		ДокументОбъектСтруктура.Вставить("КурсДокумента",КурсДокумента);
		Если КешСебестоимостиАвтомобилейКупленныхУФизЛица=Неопределено Тогда
			ДокументОбъектСтруктура.Вставить("КешСебестоимостиАвтомобилейКупленныхУФизЛица",Неопределено);
		Иначе
			ДокументОбъектСтруктура.Вставить("КешСебестоимостиАвтомобилейКупленныхУФизЛица",КешСебестоимостиАвтомобилейКупленныхУФизЛица.Скопировать());
		КонецЕсли; 
		ДокументОбъектСтрока=Новый Структура();
		ДокументОбъектСтрока.Вставить("Автомобиль", Автомобиль);
		ДокументОбъектСтрока.Вставить("СтавкаНДС", СтавкаНДСНаАвтомобиль);
		
		Себестоимость=зфЗащищенныеФункцииСервер.РеализацияАвтомобилейПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ДокументОбъектСтруктура,ДокументОбъектСтрока);    
		
		//<<ЧалапАЮ БизТех 14.03.2025 000000784 - для авто которые между селект и оптимой были перепроданы по комиссии
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ПоступлениеАвтомобилейАвтомобили.Ссылка КАК Ссылка,
		                      |	ПоступлениеАвтомобилейАвтомобили.Ссылка.Контрагент.ФормаСобственности КАК КонтрагентФормаСобственности,
		                      |	ПоступлениеАвтомобилейАвтомобили.Сумма КАК Сумма
		                      |ИЗ
		                      |	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
		                      |ГДЕ
		                      |	ПоступлениеАвтомобилейАвтомобили.Ссылка.Проведен
		                      |	И ПоступлениеАвтомобилейАвтомобили.Ссылка.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеАвтомобилейКомиссия)
		                      |	И ПоступлениеАвтомобилейАвтомобили.Ссылка.Контрагент.ФормаСобственности = ЗНАЧЕНИЕ(Перечисление.ФормыСобственности.ЧастноеЛицо)
		                      |	И ПоступлениеАвтомобилейАвтомобили.Автомобиль = &Автомобиль
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	ПоступлениеАвтомобилейАвтомобили.Ссылка.Дата УБЫВ");      //найдем самое последнее поступление (не комиссия)    
		Запрос.УстановитьПараметр("Автомобиль",автомобиль); 
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() тогда     //если найдено поступление не комиссия от ФЛ, то переопределим СС
			Себестоимость = Результат[0].Сумма;	
		КонецЕсли;
		//ЧалапАЮ БизТех 14.03.2025>>

		СебестоимостьАвтомобиля=Себестоимость;
		
		Если ДокументОбъектСтруктура.КешСебестоимостиАвтомобилейКупленныхУФизЛица<>Неопределено Тогда
			КешСебестоимостиАвтомобилейКупленныхУФизЛица=ДокументОбъектСтруктура.КешСебестоимостиАвтомобилейКупленныхУФизЛица.Скопировать();
		КонецЕсли;
		
	КонецЕсли;
	
	#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.тСебестоимостьАвтомобиля.Заголовок = "Себестоимость: " + Строка(Формат(СебестоимостьАвтомобиля, "ЧЦ=15; ЧДЦ=2; ЧН=0,00"));
		КонецЕсли;
	#КонецЕсли
	
	Возврат Себестоимость;
КонецФункции // ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица()


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	Если НЕ ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобиль Тогда
		ОбязательныеРеквизиты.Вставить("ДокументОснование", 1);
	КонецЕсли;
	
	Если НЕ ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобильОтмена Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",   1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов", 1);
		ОбязательныеРеквизиты.Вставить("Заказчик",     1);
		ОбязательныеРеквизиты.Вставить("ВидОплаты",    1);
		//anton
		//ОбязательныеРеквизиты.Вставить("СрокПоставки", 1);
		//anton
	КонецЕсли;
		
	ОбязательныеРеквизиты.Вставить("Модель",   1);
	//ОбязательныеРеквизиты.Вставить("Автомобиль",   1);
	
	ОбязательныеОпции=Новый Структура();
	ОбязательныеОпции.Вставить("Опция",      3);
	ОбязательныеОпции.Вставить("Количество", 1);
	ОбязательныеРеквизиты.Вставить("Опции",  ОбязательныеОпции);
	
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура", 3);
	ОбязательныеТовары.Вставить("Количество",   1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения", 3);
	ОбязательныеТовары.Вставить("Коэффициент", 1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры", 2);
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	//Если ДопРеквизиты=Неопределено Тогда
	//	ДопРеквизиты = Новый Структура;
	//КонецЕсли; 
	//ДопРеквизиты.Вставить("Автомобиль",1);
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	//Документ считаем корректно заполненным, независимо от заполненности табличных частей
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	Если ЗначениеЗаполнено(ДокументОснование) И ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		СписокХозОпераций.Добавить("ЗаказНаАвтомобильИзменение", "Изменение автомобиля", Истина);
		СписокХозОпераций.Добавить("ЗаказНаАвтомобильПереуступка", "Переуступка заказа на автомобиль", Ложь);
		СписокХозОпераций.Добавить("ЗаказНаАвтомобильОтмена", "Отмена заказа на автомобиль", Ложь);
	Иначе
		СписокХозОпераций.Добавить("ЗаказНаАвтомобиль", "Заказ клиента на автомобиль", Истина);
	КонецЕсли;
	
	Возврат СписокХозОпераций;
	
КонецФункции

// пересчет цены на авто при изменении флага цена включает НДС
Процедура ПересчитатьЦеныДляНовогоТипаЦен() Экспорт
	ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(Неопределено);
	ОбработкаРеквизита("ЦенаАвтомобиля");
КонецПроцедуры

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат СуммаВсегоНаАвтомобиль+Опции.Итог("СуммаВсего")+Товары.Итог("СуммаВсего");
КонецФункции

// Расчет суммы НДС
Функция РассчитатьСуммуНДСВсего() Экспорт
	Возврат СуммаНДСНаАвтомобиль+Опции.Итог("СуммаНДС")+Товары.Итог("СуммаНДС");
КонецФункции

// Получение суммы без скидки для расчета новой скидки
//
// Параметры
//  ОбработкаРасчетСкидок	– Обработка.РасчетСкидок – Обработка расчета скидок
//
// Возвращаемое значение:
//   Число   – Сумма автомобиля и опций без скидки
//
Функция ПолучитьБазуДляРасчетаСкидки(ОбработкаРасчетСкидок) Экспорт
	БазаДляРасчетаСкидки=Неопределено;
	
	Если ОбработкаРасчетСкидок.ИмяТабличнойЧасти="Опции" Тогда
		БазаДляРасчетаСкидки = орПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, "Автомобиль");
	КонецЕсли;
	
	Возврат БазаДляРасчетаСкидки;
КонецФункции // ПолучитьБазуДляРасчетаСкидки() 

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	Если Имя = "ХозОперация" Тогда
		
	ИначеЕсли Имя = "Дата" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Результат = ОбработкаРеквизита("РассчитатьСкидкиНаАвтомобиль", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
		
	ИначеЕсли Имя = "ТипЦен" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТребуетсяПересчет = Истина;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет",ТребуетсяПересчет) Тогда
				ТребуетсяПересчет = Истина;
			КонецЕсли;
		КонецЕсли;
		Если ТребуетсяПересчет Тогда
			//АвтомобильИзСправочника=Справочники.Автомобили.НайтиПоРеквизиту("VIN",VIN);	
			Если ЗначениеЗаполнено(Автомобиль) Тогда
				ЦенаАвтомобиля = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			Иначе
				ЦенаАвтомобиля = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ЭтаФорма);
			Результат = ОбработкаРеквизита("ЦенаАвтомобиля",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			Для Каждого СтрокаТЧ Из Опции Цикл
				Результат = ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
			КонецЦикла;
		КонецЕсли;
		ТекущаяВалютаДокумента = ВалютаДокумента;
		ТекущийКурсДокумента   = КурсДокумента;
		Возврат Результат;
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет = ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет = Ложь;
		КонецПопытки; 
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			ЦенаАвтомобиля=обПересчет(ЦенаАвтомобиля,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
			ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ЭтаФорма);
			ОбработкаРеквизита("ЦенаАвтомобиля",ТекСтрока,ЭтаФорма,ДопПараметры);
			Для каждого СтрокаТЧ Из Опции Цикл
				СтрокаТЧ.Цена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Результат=ОбработкаРеквизита("Опции.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
			КонецЦикла;
			Результат=ОбработкаРеквизита("ПроцентПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		КонецЕсли;
		ТекущаяВалютаДокумента = ВалютаДокумента;
		ТекущийКурсДокумента   = КурсДокумента;
		Результат = ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Результат = ОбработкаРеквизита("ОтображениеВалютыПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	ИначеЕсли Имя="Организация" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		// Организация может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат=ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат=ОбработкаРеквизита("Опции.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла; 
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Подразделение может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат=ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат=ОбработкаРеквизита("Опции.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла; 
		// Изменение подразделения приводит к необходимости пересчета скидок документа (как "шапочных", так и товарных).
		Результат=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (НЕ обЗначениеНеЗаполнено(Контрагент)) И обЗначениеНеЗаполнено(Заказчик) Тогда
			Заказчик  = Контрагент;
			Результат = ОбработкаРеквизита("Заказчик",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			ОбработкаРеквизита("ОтображениеСрокаСнятияРезерва",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		
		//bz++
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.кнПараметрыЛизинга.Видимость=Ложь;
			Если ЗначениеЗаполнено(Контрагент) Тогда
				Если Контрагент.ЛизинговаяКомпания Тогда
					ЭтаФорма.ЭлементыФормы.кнПараметрыЛизинга.Видимость=Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//bz--
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Если у документа есть реквизит СкидкаНаценка, заполним его из Договора 
		Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов.ТипСкидкиНаценки)) И 
			(СкидкаНаценкаНаАвтомобиль<>ДоговорВзаиморасчетов.ТипСкидкиНаценки) Тогда
			СкидкаНаценкаНаАвтомобиль=ДоговорВзаиморасчетов.ТипСкидкиНаценки;
			Результат=ОбработкаРеквизита("СкидкаНаценкаНаАвтомобиль",,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		Если ДоговорВзаиморасчетов<>Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка() Тогда
			ПроцентПредоплатыНовый=?(ДоговорВзаиморасчетов.ПроцентПредоплаты=0,обПраво("МинимальныйПроцентПредоплаты",Права,,ЭтотОбъект),ДоговорВзаиморасчетов.ПроцентПредоплаты);
			Если ПроцентПредоплаты<>ПроцентПредоплатыНовый Тогда
				ПроцентПредоплаты=ПроцентПредоплатыНовый;
				Результат = ОбработкаРеквизита("ПроцентПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ВидОплаты) ИЛИ ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
				ВидОплаты=ДоговорВзаиморасчетов.ВидОплаты;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя="Карточка" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Результат = ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	ИначеЕсли Имя="Заказчик" Тогда
		Если НЕ обЗначениеНеЗаполнено(Заказчик) И (Заказчик<>Контрагент) Тогда
			Контрагент=Заказчик;
			Результат=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		КонецЕсли; 
		
	ИначеЕсли Имя="ПроцентПредоплаты" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаСоСкидкой=РассчитатьСуммуВсего();
		СуммаПредоплаты=(СуммаСоСкидкой*ПроцентПредоплаты)/100;
		
	ИначеЕсли Имя="СуммаПредоплаты" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаСоСкидкой=РассчитатьСуммуВсего();
		Если СуммаСоСкидкой=0 Тогда ПроцентПредоплаты=0;
		Иначе
			ВремПроцентПредоплаты = (СуммаПредоплаты*100)/СуммаСоСкидкой;
			ВремПроцентПредоплаты = Цел(ВремПроцентПредоплаты*100)/100;
			ПроцентПредоплаты = ВремПроцентПредоплаты;
		КонецЕсли; 
		
	ИначеЕсли Имя="СрокПоставки" Тогда
		Результат=ОбработкаРеквизита("ОтображениеСрокаПоставки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СрокСнятияРезерва" Тогда
		Возврат ОбработкаРеквизита("ОтображениеСрокаСнятияРезерва",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ОтображениеВалютыПредоплаты" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.тВалютаПредоплаты.Заголовок=СокрЛП(ВалютаДокумента.Наименование)+":";
		КонецЕсли; 
		#КонецЕсли
		
	ИначеЕсли Имя="ОтображениеСрокаПоставки" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(СрокПоставки) Тогда
				ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="срок не определен";
			Иначе
				Если Ссылка.Пустая() ИЛИ НЕ Ссылка.Проведен Тогда
					ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
				Иначе
					Запрос = Новый Запрос("ВЫБРАТЬ
					|	МАКСИМУМ(ЗаказыАвтомобилей.Период) КАК Период,
					|	СУММА(ВЫБОР
					|		КОГДА ЗаказыАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			ЗаказыАвтомобилей.Количество
					|		ИНАЧЕ
					|			-ЗаказыАвтомобилей.Количество
					|	КОНЕЦ) КАК Количество
					|ИЗ
					|	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
					|ГДЕ
					|	ЗаказыАвтомобилей.Заказ = &Заказ");
					Запрос.УстановитьПараметр("Заказ", Ссылка);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Если Выборка.Период = NULL Тогда
							ПросроченоДней = 0;
						ИначеЕсли Выборка.Количество > 0 Тогда
							ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
						Иначе
							ПросроченоДней = 0;
						КонецЕсли;
					Иначе
						ПросроченоДней = 0;
					КонецЕсли;
				КонецЕсли;
					
				КолДней = Макс(ПросроченоДней, -ПросроченоДней); 
				ОстатокОтДеления = КолДней%10; 
				Если ОстатокОтДеления = 0 ИЛИ ОстатокОтДеления >=5 И ОстатокОтДеления <=9 
					ИЛИ КолДней%100 >= 10 И КолДней%100 <= 19 Тогда
					НадписьДень =  "дней";	
				ИначеЕсли ОстатокОтДеления = 1 Тогда
					НадписьДень = "день";
				ИначеЕсли ОстатокОтДеления >=2 И ОстатокОтДеления <=4 Тогда
					НадписьДень = "дня";
				КонецЕсли;		
				
				Если ПросроченоДней > 0 Тогда
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок = "просрочено на " + СокрЛП(ПросроченоДней) + " " + НадписьДень;
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(128,0,0);
				Иначе
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок = "";
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(0,0,128);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		#КонецЕсли
		
	ИначеЕсли Имя="ОтображениеСрокаСнятияРезерва" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(СрокСнятияРезерва) Тогда
				ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.Заголовок="срок не определен";
				ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.ЦветТекста=Новый Цвет(0,0,128);
			Иначе
				
				Если Ссылка.Пустая() ИЛИ НЕ Ссылка.Проведен Тогда
					ПросроченоДней = Цел((ТекущаяДата()-СрокСнятияРезерва)/86400);
				Иначе
					Запрос = Новый Запрос("ВЫБРАТЬ
					                      |	МАКСИМУМ(ЗаказыАвтомобилей.Период) КАК Период,
					                      |	СУММА(ВЫБОР
					                      |			КОГДА ЗаказыАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
					                      |				ТОГДА ЗаказыАвтомобилей.Количество
					                      |			ИНАЧЕ -ЗаказыАвтомобилей.Количество
					                      |		КОНЕЦ) КАК Количество
					                      |ИЗ
					                      |	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
					                      |ГДЕ
					                      |	ЗаказыАвтомобилей.Заказ = &Заказ");
					Запрос.УстановитьПараметр("Заказ", Ссылка);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Если Выборка.Период = NULL Тогда
							ПросроченоДней = 0;
						ИначеЕсли Выборка.Количество > 0 Тогда
							ПросроченоДней = Цел((ТекущаяДата()-СрокСнятияРезерва)/86400);
						Иначе
							ПросроченоДней = 0;
						КонецЕсли;
					Иначе
						ПросроченоДней = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ПросроченоДней>0 Тогда
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.Заголовок="срок снятия резерва истек " + СокрЛП(ПросроченоДней)+ " " + обПолучитьПредставлениеДня(ПросроченоДней) + " назад";
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.ЦветТекста=Новый Цвет(128,0,0);
				Иначе
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.Заголовок="";
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.ЦветТекста=Новый Цвет(0,128,0);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Возврат Истина;		
		
	// ОБРАБОТКА РЕКВИЗИТОВ АВТОМОБИЛЯ
	ИначеЕсли Имя="Автомобиль" Тогда
		Результат = Истина;
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("НеИзменятьЦенуАвтомобиля",Истина);
			ДопПараметры.Вставить("ЗаполнятьКомплектациюАвтомобиля",Ложь);
			Модель = Автомобиль.Модель;
			ВариантКомплектации = Автомобиль.ВариантКомплектации;
			Результат = ОбработкаРеквизита("Модель", ТекСтрока, ЭтаФорма, ДопПараметры);
			Результат = ОбработкаРеквизита("ВариантКомплектации", ТекСтрока, ЭтаФорма, ДопПараметры);
			ДопПараметры.Удалить("НеИзменятьЦенуАвтомобиля");
			Цвет      = Автомобиль.Цвет;
			ЦветКод   = Автомобиль.ЦветКод;
			ТипСалона = Автомобиль.ТипСалона;
			НоваяЦенаАвтомобиля = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(), Дата, МоментВремени()),,ВалютаДокумента,КурсДокумента);
			#Если Клиент Тогда
			Если НоваяЦенаАвтомобиля<>ЦенаАвтомобиля Тогда
				Если ЭтаФорма<>Неопределено Тогда
					ТекстВопроса="Цена выбранного автомобиля составляет "+Формат(НоваяЦенаАвтомобиля,"ЧДЦ=2; ЧН=0,00")+" "+СокрЛП(ВалютаДокумента)+".
						|Цена автомобиля по заказу "+Формат(ЦенаАвтомобиля,"ЧДЦ=2; ЧН=0,00")+" "+СокрЛП(ВалютаДокумента)+".
						|
						|Изменить цену автомобиля в заказе?";
					Ответ=Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да,"Изменение цены");
					Если Ответ<>КодВозвратаДиалога.Да Тогда
						НоваяЦенаАвтомобиля=ЦенаАвтомобиля;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
			Если ЭтаФорма<>Неопределено Тогда
				ЗаполнитьКомплектациюАвтомобиля(ЭтаФорма);
			КонецЕсли;
			Для Каждого СтрокаТЧ Из Опции Цикл
				Результат = ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
			КонецЦикла; 
			#КонецЕсли
			ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ЭтаФорма);
			Если НоваяЦенаАвтомобиля<>ЦенаАвтомобиля Тогда
				ЦенаАвтомобиля = НоваяЦенаАвтомобиля;
				Результат = ОбработкаРеквизита("ЦенаАвтомобиля", ТекСтрока, ЭтаФорма, ДопПараметры);
			КонецЕсли;
			Резервировать = ПроверитьОстаткиАвтомобилей();
		Иначе
			ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ЭтаФорма);
			ОбработкаРеквизита("СтавкаНДСНаАвтомобиль", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		#Если Клиент Тогда
			дкСообщитьОЗаметках(ЭтотОбъект,ЭтотОбъект["Автомобиль"],ЭтаФорма);
		#КонецЕсли

		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.УправлениеДиалогом();
		КонецЕсли; 
		#КонецЕсли
		
	ИначеЕсли Имя="Модель" Тогда
		ВариантКомплектацииИзменен=Ложь;
		Если ВариантКомплектации.Владелец<>Модель Тогда
			ВариантКомплектации=Неопределено;
			ВариантКомплектацииИзменен=Истина;
		КонецЕсли;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОтключитьОбработчикИзмененияДанных("АвтомобильЗаказа");
			АвтомобильИзменен=Ложь;
			Если Модель<>ЭтаФорма.АвтомобильЗаказа.Модель Тогда
				ЭтаФорма.АвтомобильЗаказа.Модель=Модель;
				АвтомобильИзменен=Истина;
			КонецЕсли; 
			Если ВариантКомплектации<>ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации Тогда
				ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации=ВариантКомплектации;
				АвтомобильИзменен=Истина;
			КонецЕсли;
			Если АвтомобильИзменен Тогда
				ЭтаФорма.АвтомобильЗаказа.ФормированиеНаименованияАвтомобиля("");
			КонецЕсли; 
			ЭтаФорма.ПодключитьОбработчикИзмененияДанных("АвтомобильЗаказа","АвтомобильЗаказаПриИзменении",Истина);
		КонецЕсли; 
		#КонецЕсли
		Если ВариантКомплектацииИзменен Тогда
			Результат=ОбработкаРеквизита("ВариантКомплектации",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="ВариантКомплектации" Тогда
		//Если Автомобиль.ВариантКомплектации<>ВариантКомплектации Тогда
		//	Автомобиль = Неопределено;
		//	Результат = ОбработкаРеквизита("Автомобиль", ТекСтрока, ЭтаФорма, ДопПараметры);
		//КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
			Если ВариантКомплектации.Владелец<>Модель Тогда
				Модель=ВариантКомплектации.Владелец;
				Результат=ОбработкаРеквизита("Модель",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			КонецЕсли;
		КонецЕсли;
		НеИзменятьЦенуАвтомобиля = Ложь;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("НеИзменятьЦенуАвтомобиля",НеИзменятьЦенуАвтомобиля) Тогда
				НеИзменятьЦенуАвтомобиля=Ложь;
			КонецЕсли;
		КонецЕсли;
		Если НЕ НеИзменятьЦенуАвтомобиля Тогда
			Если ЗначениеЗаполнено(Автомобиль) Тогда
				ЦенаАвтомобиля = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			Иначе
				ЦенаАвтомобиля = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации", Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ЭтаФорма);
			Результат = ОбработкаРеквизита("ЦенаАвтомобиля",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		КонецЕсли;
		
		#Если Клиент Тогда
		ЗаполнятьКомплектациюАвтомобиля = Истина;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ЗаполнятьКомплектациюАвтомобиля",ЗаполнятьКомплектациюАвтомобиля) Тогда
				ЗаполнятьКомплектациюАвтомобиля=Истина;
			КонецЕсли;
		КонецЕсли;
		Если ЭтаФорма<>Неопределено Тогда
			Если ЗаполнятьКомплектациюАвтомобиля Тогда
				ЗаполнитьКомплектациюАвтомобиля(ЭтаФорма);
			КонецЕсли;
			Для Каждого СтрокаТЧ Из Опции Цикл
				Результат = ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
			КонецЦикла; 
			Если ВариантКомплектации<>ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации Тогда
				ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации=ВариантКомплектации;
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
	
		Результат = ОбработкаРеквизита("ПроцентПредоплаты",,ЭтаФорма) И Результат;
		
	ИначеЕсли Имя="Цвет" ИЛИ Имя="ЦветКод" ИЛИ Имя="ТипСалона" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если ЭтотОбъект[Имя]<>ЭтаФорма.АвтомобильЗаказа[Имя] Тогда
				ЭтаФорма.АвтомобильЗаказа[Имя]=ЭтотОбъект[Имя];
			КонецЕсли;
			Если Имя="Цвет" Тогда
				ЭтаФорма.ОтключитьОбработчикИзмененияДанных("АвтомобильЗаказа");
				ЭтаФорма.АвтомобильЗаказа.ФормированиеНаименованияАвтомобиля("");
				ЭтаФорма.ПодключитьОбработчикИзмененияДанных("АвтомобильЗаказа","АвтомобильЗаказаПриИзменении",Истина);
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
	    Результат = Истина;
		
	ИначеЕсли Имя="ЦенаАвтомобиля" Тогда
		Результат=ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СебестоимостьАвтомобиля" Тогда
		Результат=ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СкидкаНаценкаНаАвтомобиль" Тогда
		Результат=ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ПроцентСкидкиНаАвтомобиль" Тогда
		// Скидка не может быть выше 100%
		Если ПроцентСкидкиНаАвтомобиль>100 Тогда
			ПроцентСкидкиНаАвтомобиль=100;	
		КонецЕсли;
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		СуммаБезСкидки  = ЦенаАвтомобиля*?(ЦенаВключаетНДС,1,1+СтавкаНДСНаАвтомобиль.Ставка/100);
		СуммаСкидки     = Окр(ПроцентСкидкиНаАвтомобиль*СуммаБезСкидки/100,2);
		СуммаСкидки		= дкОкруглитьСуммуСкидки(СуммаСкидки,СкидкаНаценкаНаАвтомобиль);
		Если СуммаСкидки<>СуммаСкидкиНаАвтомобиль Тогда // Сумма скидки изменилась
			СуммаСкидкиНаАвтомобиль=СуммаСкидки;
			Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
				ДопПараметры.Вставить("ПересчитыватьПроцентСкидки",Ложь);
			Иначе
				ДопПараметры = Новый Структура("ПересчитыватьПроцентСкидки",Ложь);
			КонецЕсли;
			Результат=ОбработкаРеквизита("СуммаСкидкиНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
			Результат=ОбработкаРеквизита("ПроцентПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Иначе
			Результат=ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		КонецЕсли; 
		
	ИначеЕсли Имя="СуммаСкидкиНаАвтомобиль" Тогда
		Попытка // Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцентСкидки=ДопПараметры.ПересчитыватьПроцентСкидки;
		Исключение 
			ПересчитыватьПроцентСкидки=Истина;
		КонецПопытки;
		Если ПересчитыватьПроцентСкидки Тогда
			Если НЕ БлокироватьПерерасчетСкидок ТОгда
				БазаДляРасчетаСкидки = ОбработкаРасчетСкидокНаАвтомобиль.БазаДляРасчетаСкидки;
			Иначе
				ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
				Если НЕ ЦенаВключаетНДС Тогда
					БазаДляРасчетаСкидки = ЦенаАвтомобиля + ((ЦенаАвтомобиля - СебестоимостьАвтомобиля) * СтавкаНДСНаАвтомобиль.Ставка)/100;
				Иначе
					БазаДляРасчетаСкидки = ЦенаАвтомобиля;
				КонецЕсли;
			КонецЕсли;
			ПроцентСкидкиНаАвтомобиль = ?(БазаДляРасчетаСкидки=0, 0, Окр((СуммаСкидкиНаАвтомобиль/БазаДляРасчетаСкидки)*100, 2));
		КонецЕсли;
		// Ничего делать не будем - весь расчет от НДС зависит (включено или нет)
		Результат=ОбработкаРеквизита("СтавкаНДСНаАвтомобиль", ТекСтрока, ЭтаФорма, "СкидкаИзменена");
		
	ИначеЕсли Имя="СтавкаНДСНаАвтомобиль" Тогда
		СуммаАвтомобиляБезСкидки=?(ТипЦен.ЦенаВключаетНДС,ЦенаАвтомобиля,ЦенаАвтомобиля*(1+СтавкаНДСНаАвтомобиль.Ставка/100))-СуммаСкидкиНаАвтомобиль;
		СуммаВсегоНаАвтомобиль=СуммаАвтомобиляБезСкидки;
		Если СебестоимостьАвтомобиля >= СуммаАвтомобиляБезСкидки Тогда
			СуммаНДСНаАвтомобиль = 0;
		Иначе
			СуммаНДСНаАвтомобиль=Окр(((СуммаАвтомобиляБезСкидки-СебестоимостьАвтомобиля)*СтавкаНДСНаАвтомобиль.Ставка)/(100+СтавкаНДСНаАвтомобиль.Ставка),2);
		КонецЕсли;
		Результат=ЭтотОбъект.ОбработкаРеквизита("СуммаНДСНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СуммаНДСНаАвтомобиль" Тогда
		//СуммаАвтомобиляБезСкидки=?(ТипЦен.ЦенаВключаетНДС,ЦенаАвтомобиля,ЦенаАвтомобиля*(1+СтавкаНДСНаАвтомобиль.Ставка/100))-СуммаСкидкиНаАвтомобиль;
		//СуммаВсегоНаАвтомобиль=СуммаАвтомобиляБезСкидки;
		Возврат Результат;
		
	ИначеЕсли Имя="СуммаВсегоНаАвтомобиль" Тогда
		СкидкаНаАвтомобильАбсолютная = (СкидкаНаценкаНаАвтомобиль.СпособВычисления=Перечисления.СкидкиСпособВычисления.Абсолютная);
		Если СкидкаНаАвтомобильАбсолютная Тогда
			ЗначениеСкидкиНаАвтомобиль = СуммаСкидкиНаАвтомобиль;
		Иначе
			ЗначениеСкидкиНаАвтомобиль = ПроцентСкидкиНаАвтомобиль;
		КонецЕсли;
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			Если СкидкаНаАвтомобильАбсолютная Тогда
				ЦенаАвтомобиля = Окр((СуммаВсегоНаАвтомобиль-ЗначениеСкидкиНаАвтомобиль)/(1-ПроцентСкидкиНаАвтомобиль/100),2);
			Иначе
				ЦенаАвтомобиля = Окр(СуммаВсегоНаАвтомобиль/(1-ПроцентСкидкиНаАвтомобиль/100-ЗначениеСкидкиНаАвтомобиль/100),2);
			КонецЕсли;
		Иначе
			Если СкидкаНаАвтомобильАбсолютная Тогда
				Сумма = СуммаВсегоНаАвтомобиль*100/(100+СтавкаНДСНаАвтомобиль.Ставка)+ЗначениеСкидкиНаАвтомобиль;
				ЦенаАвтомобиля = Окр(Сумма/(1-ПроцентСкидкиНаАвтомобиль/100),2);
			Иначе
				Сумма=СуммаВсегоНаАвтомобиль/(1-ПроцентСкидкиНаАвтомобиль/100-ЗначениеСкидкиНаАвтомобиль/100);
				ЦенаАвтомобиля=Окр(Сумма*100/(СтавкаНДСНаАвтомобиль.Ставка+100),2); // Учитываем, что НДС сверху.
			КонецЕсли;
		КонецЕсли;
		СуммаСкидкиНаАвтомобиль=Окр(ЦенаАвтомобиля*ПроцентСкидкиНаАвтомобиль/100,2);
		// Рассчитываем новую сумму НДС.
		СуммаАвтомобиляБезСкидки=ЦенаАвтомобиля-СуммаСкидкиНаАвтомобиль;
		СуммаРасчетаНДС = ?(СебестоимостьАвтомобиля > СуммаАвтомобиляБезСкидки, 0, СуммаАвтомобиляБезСкидки - СебестоимостьАвтомобиля);
		Если ЦенаВключаетНДС Тогда
			СуммаНДСНаАвтомобиль = Окр((СуммаРасчетаНДС*СтавкаНДСНаАвтомобиль.Ставка)/(100+СтавкаНДСНаАвтомобиль.Ставка),2);
		Иначе
			СуммаНДСНаАвтомобиль =Окр(СуммаРасчетаНДС*СтавкаНДСНаАвтомобиль.Ставка/100,2);
		КонецЕсли;
		
	ИначеЕсли Имя="МаркетинговаяПрограмма" Тогда
		Если НЕ обЗначениеНеЗаполнено(МаркетинговаяПрограмма.СкидкаНаценка) Тогда
			СкидкаНаценкаНаАвтомобиль=МаркетинговаяПрограмма.СкидкаНаценка;
			Результат=ОбработкаРеквизита("СкидкаНаценкаНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ДОПОЛНИТЕЛЬНОЕ ОБОРУДОВАНИЕ"
	ИначеЕсли Имя="Опции.Опция" Тогда
		
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда
				Если ОборудованиеПроизводителя = Неопределено ИЛИ ОборудованиеПроизводителя.колонки.Количество() = 0 Тогда
					зфЗаказНаАвтомобильПолучитьОборудованиеАвтомобиля(ЭтотОбъект);
				КонецЕсли;
				Если ОборудованиеПроизводителя.Найти(ТекСтрока.Опция,"Опция")=Неопределено Тогда
					Если ЭтаФорма<>Неопределено Тогда
						Сообщить("Опция <"+ТекСтрока.Опция+"> недопустима для данного варианта комплектации.");
					КонецЕсли; 
					ТекСтрока.Опция=Неопределено;
					Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		#КонецЕсли
		
		НеИзменятьЦеныОпций = Ложь;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("НеИзменятьЦеныОпций",НеИзменятьЦеныОпций) Тогда
				НеИзменятьЦеныОпций = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ НеИзменятьЦеныОпций Тогда
		
			Если (НЕ СкидкаНаценкаНаАвтомобиль.Пустая()) И (СкидкаНаценкаНаАвтомобиль.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная) Тогда
				ТекСтрока.ПроцентСкидки = ЗначениеСкидкиНаценкиНаАвтомобиль;
			Иначе	
				ТекСтрока.ПроцентСкидки = 0;
			КонецЕсли;
			
			// почистим суммы
			Попытка
				ТекСтрока.СуммаВсего = 0;
				ТекСтрока.СуммаНДС = 0;
			Исключение КонецПопытки;
			
			// Сбросим значения скидки строки, если таковые имеются.
			Попытка	// В ТЧ может не быть реквизитов строчных скидок...
				ТекСтрока.СкидкаНаТовар       = Справочники.ТипыСкидок.ПустаяСсылка(); 
				ТекСтрока.ПроцентСкидкиСтроки = 0;
				ТекСтрока.СуммаСкидкиСтроки   = 0;
			Исключение КонецПопытки;
			// проверим а не набор ли у нас...
			Если НЕ ТекСтрока.Опция.ПризнакНабора Тогда
				// установим количество
				Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
				// Заполним ставку НДС
				ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
				Если ОсвобожденОтНДС Тогда
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				ИначеЕсли ТипЗнч(ТекСтрока.Опция)=Тип("СправочникСсылка.Опции") Тогда
					ТекСтрока.СтавкаНДС = ТекСтрока.Опция.СтавкаНДС; 
				Иначе
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС; 
				КонецЕсли;
				// попытаемся получить цену
				ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации,Опция",Модель,ВариантКомплектации,ТекСтрока.Опция),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
				Результат = ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
			Иначе
				// имеем дело с набором, разложим его...
				Набор=ТекСтрока.Опция.СоставНабора;
				КоличествоНабора=ТекСтрока.Количество;
				Если КоличествоНабора=0 Тогда
					КоличествоНабора=1;
				КонецЕсли;
				// удалим строку из табличной части
				Опции.Удалить(ТекСтрока);
				// теперь будем рекурсивно добавлять состав набора
				Для Каждого ОборудованиеИзНабора Из Набор Цикл
					СтрокаТЧ=Опции.Найти(ОборудованиеИзНабора.Опция,"Опция");
					Если СтрокаТЧ=Неопределено Тогда
						СтрокаТЧ=Опции.Добавить();
						СтрокаТЧ.Опция=ОборудованиеИзНабора.Опция;
						СтрокаТЧ.Количество=ОборудованиеИзНабора.Количество*КоличествоНабора;
						ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры);
						Если обЗначениеНеЗаполнено(СтрокаТЧ.Опция) Тогда
							Опции.Удалить(СтрокаТЧ);
						КонецЕсли; 
					Иначе
						СтрокаТЧ.Количество=СтрокаТЧ.Количество+(ОборудованиеИзНабора.Количество*КоличествоНабора);
						ОбработкаРеквизита("Опции.Количество",СтрокаТЧ,ЭтаФорма,ДопПараметры);
					КонецЕсли; 
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя="Опции.Количество" Тогда
		Результат=ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("НеПерерасчитыватьЦену",Истина);
		ОбработкаРеквизита("Опции.Сумма", ТекСтрока, ЭтаФорма, ДопПараметры);
		Результат=ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Опции.Сумма" Тогда
		
		НеПерерасчитыватьЦену = Ложь;
		Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			ДопПараметры.Свойство("НеПерерасчитыватьЦену",НеПерерасчитыватьЦену);
			Если НеПерерасчитыватьЦену = Неопределено Тогда
				НеПерерасчитыватьЦену = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НеПерерасчитыватьЦену Тогда
			ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		КонецЕсли;
			
		Результат=ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.ПроцентСкидки" Тогда
		// Скидка не может быть выше 100%
		Если ТекСтрока.ПроцентСкидки>100 Тогда
			ТекСтрока.ПроцентСкидки = 100;	
		КонецЕсли;
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
		ТекСтрока_СуммаБезСкидки=Окр(ТекСтрока.Сумма*?(ЦенаВключаетНДС,1,1+ТекСтрока.СтавкаНДС.Ставка/100),2);
		СуммаСкидки=Окр(ТекСтрока.ПроцентСкидки*ТекСтрока_СуммаБезСкидки/100,2);
		СуммаСкидки=дкОкруглитьСуммуСкидки(СуммаСкидки,СкидкаНаценка);
		Если СуммаСкидки<>ТекСтрока.СуммаСкидки Тогда // Сумма скидки изменилась
			ТекСтрока.СуммаСкидки=СуммаСкидки;
		КонецЕсли; 
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитыватьПроцентСкидки",Ложь);
		Результат=ОбработкаРеквизита("Опции.СуммаСкидки",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	ИначеЕсли Имя="Опции.СуммаСкидки" Тогда
		Попытка	// Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцентСкидки=ДопПараметры.ПересчитыватьПроцентСкидки;
		Исключение 
			ПересчитыватьПроцентСкидки=Истина;
		КонецПопытки;
		Если ПересчитыватьПроцентСкидки Тогда
			ТекСтрока.ПроцентСкидки=?(ТекСтрока.Сумма=0,0,Окр(ТекСтрока.СуммаСкидки*100/ТекСтрока.Сумма,2));
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиНаАвтомобиль=СуммаСкидкиНаАвтомобиль+Опции.Итог("СуммаСкидки");
		// Ничего делать не будем - весь расчет от НДС зависит (включено или нет)
		Результат=ОбработкаРеквизита("Опции.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.СтавкаНДС" Тогда
		
		
		
		СуммаВсегоБезСкидки = ?(ТипЦен.ЦенаВключаетНДС,ТекСтрока.Сумма,ТекСтрока.Сумма*(1+ТекСтрока.СтавкаНДС.Ставка/100))-ТекСтрока.СуммаСкидки; // База для расчета НДС
		ТекСтрока.СуммаНДС  = Окр((СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		
		ТекСтрока.СуммаВсего = ?(ТипЦен.ЦенаВключаетНДС,ТекСтрока.Сумма,ТекСтрока.Сумма*(1+ТекСтрока.СтавкаНДС.Ставка/100))-ТекСтрока.СуммаСкидки;
		
		Результат=ЭтотОбъект.ОбработкаРеквизита("Опции.СуммаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.СуммаНДС" Тогда
		
		
	ИначеЕсли Имя="Опции.СуммаВсего" Тогда
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма=Окр(ТекСтрока.СуммаВсего/(1-ТекСтрока.ПроцентСкидки/100),2);
		Иначе
			Сумма=ТекСтрока.СуммаВсего*100/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.Сумма=Окр(Сумма/(1-ТекСтрока.ПроцентСкидки/100),2);
		КонецЕсли;
		// Пересчитываем цену.
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		// Вычисляем новую сумму "шапочной" скидки приходящейся на текущую строку.
		ТекСтрока.СуммаСкидки=Окр(ТекСтрока.Сумма*ТекСтрока.ПроцентСкидки/100,2);
		ТекСтрока.СуммаСкидки=дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценка);
		// Рассчитываем новую сумму НДС. 		
		СуммаВсегоБезСкидки=ТекСтрока.Сумма-ТекСтрока.СуммаСкидки;
		Если ЦенаВключаетНДС Тогда			
			ТекСтрока.СуммаНДС=Окр((СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС=Окр(СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="РассчитатьСкидкуНаАвтомобиль" Тогда
		Попытка	
			НеРассчитыватьСкидки = ДопПараметры.НеРассчитыватьСкидки;
		Исключение
			НеРассчитыватьСкидки = Ложь;
		КонецПопытки;
		Попытка
			БлокироватьПерерасчетСкидок = ЭтотОбъект.БлокироватьПерерасчетСкидок;
		Исключение                  
			БлокироватьПерерасчетСкидок = Ложь;
		КонецПопытки;
		ОбработкаРасчетСкидокНаАвтомобиль.Дата = Дата;
		ОбработкаРасчетСкидокНаАвтомобиль.ПодразделениеКомпании = ПодразделениеКомпании;
		ОбработкаРасчетСкидокНаАвтомобиль.Карточка = Карточка;
		ОбработкаРасчетСкидокНаАвтомобиль.СкидкаНаценка = СкидкаНаценкаНаАвтомобиль;
		ОбработкаРасчетСкидокНаАвтомобиль.СкладКомпании = Неопределено;
		// Подбираем подходящую скидку. Если скидка не изменилась, то функция вернет Ложь.
		Если БлокироватьПерерасчетСкидок Тогда
			Попытка
				Если обЗначениеНеЗаполнено(ЭтотОбъект.СкидкаНаценкаНаАвтомобиль) Тогда
					ЭтотОбъект.ПроцентСкидкиНаАвтомобиль=0;
					ЭтотОбъект.ОбработкаРеквизита("ПроцентСкидкиНаАвтомобиль",, ЭтаФорма);
				КонецЕсли;
			Исключение
			КонецПопытки;
			ЭтотОбъект.ОбработкаРеквизита("ПроцентСкидкиНаАвтомобиль",, ЭтаФорма);
			Если ТекСтрока <> Неопределено Тогда
				Если СкидкаНаценкаНаАвтомобиль.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ОбработкаРеквизита("Опции.ПроцентСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
				Иначе
					ОбработкаРеквизита("Опции.СуммаСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			КонецЕсли;
			Возврат Истина;
		ИначеЕсли (НЕ НеРассчитыватьСкидки) И ОбработкаРасчетСкидокНаАвтомобиль.ПодобратьСкидку(ЭтотОбъект) Тогда
			СкидкаНаценкаНаАвтомобиль=ОбработкаРасчетСкидокНаАвтомобиль.ТекущаяСкидкаНаценка;
			ЗначениеСкидкиНаценкиНаАвтомобиль=ОбработкаРасчетСкидокНаАвтомобиль.ТекущееЗначениеСкидки;
			СуммаСкидкиНаценкиНаАвтомобиль=ОбработкаРасчетСкидокНаАвтомобиль.ТекущаяСуммаСкидки;
			// Применяем скидку к табличной части. Пересчитываем строчные скидки.
			ОбработкаРасчетСкидокНаАвтомобиль.ПрименитьСкидку(ЭтотОбъект);
		Иначе // Просто пересчитываем значение суммы скидки для текущей строки.
			Если ТекСтрока<>Неопределено Тогда
				// Рассчитываем "шапочную" скидку для текущей строки. За одно рассчитаем общую сумму скидки на документ.
				Если СкидкаНаценкаНаАвтомобиль.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ОбработкаРеквизита("Опции.ПроцентСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
				Иначе
					ОбработкаРеквизита("Опции.СуммаСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			Иначе
				// Рассчитываем "шапочную" скидку для автомобиля. За одно рассчитаем общую сумму скидки на документ.
				Если СкидкаНаценкаНаАвтомобиль.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ОбработкаРеквизита("ПроцентСкидкиНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
				Иначе
					ОбработкаРеквизита("СуммаСкидкиНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
				// Если произошла смена типа скидки, но значение скидки не изменилась, то пересчитывать ТЧ нет необходимости, но
				// сохранить новый тип скидки нужно.
				СкидкаНаценкаНаАвтомобиль=ОбработкаРасчетСкидокНаАвтомобиль.ТекущаяСкидкаНаценка;
				СуммаСкидкиНаценкиНаАвтомобиль=СуммаСкидкиНаАвтомобиль+Опции.Итог("СуммаСкидки");
			КонецЕсли;
		КонецЕсли;
		
	Иначе 
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

// Функция возвращает документ поступления автомобиля
Функция ПолучитьTradeIn() Экспорт
	
	Если Ссылка=Документы.ЗаказНаАвтомобиль.ПустаяСсылка() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
	             |	ЗначенияСвойствОбъектов.Объект КАК ПоступлениеАвтомобилей
	             |ИЗ
	             |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	             |ГДЕ
	             |	ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ПоступлениеАвтомобилей
	             |	И ЗначенияСвойствОбъектов.Свойство = &Свойство
	             |	И ЗначенияСвойствОбъектов.Значение = &Значение";
	Запрос.УстановитьПараметр("Свойство",ПланыВидовХарактеристик.СвойстваОбъектов.ЗаказНаАвтомобиль);
	Запрос.УстановитьПараметр("Значение",Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПоступлениеАвтомобилей = Выборка.ПоступлениеАвтомобилей;
	Иначе
		ПоступлениеАвтомобилей = Неопределено;
	КонецЕсли;
	
	Возврат ПоступлениеАвтомобилей;
	
КонецФункции

//Создание документа поступления автомобиля по Trade-In
Процедура СоздатьTradeIn() Экспорт
	
	Если Ссылка=Документы.ЗаказНаАвтомобиль.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	ПоступлениеАвтомобилей = ПолучитьTradeIn();
	Если ПоступлениеАвтомобилей = Неопределено Тогда
		ПоступлениеАвтомобилей = Документы.ПоступлениеАвтомобилей.СоздатьДокумент();
		ПоступлениеАвтомобилей.Заполнить(Ссылка);
		ПоступлениеАвтомобилей.Автомобили.Очистить();
		ПоступлениеАвтомобилей.Опции.Очистить();
		ПоступлениеАвтомобилей.Контрагент     = Контрагент;
		ПоступлениеАвтомобилей.ОбработкаРеквизита("Контрагент");
		ПоступлениеАвтомобилей.СуммаДокумента = ПоступлениеАвтомобилей.РассчитатьСуммуВсего();
		ПоступлениеАвтомобилейФорма = ПоступлениеАвтомобилей.ПолучитьФорму();
		ПоступлениеАвтомобилейФорма.ЗаказНаАвтомобиль = Ссылка;
		//ПоступлениеАвтомобилейФорма.ВидАвтомобиля     = Перечисления.ВидАвтомобиля.АвтомобильСПробегом;
		ПоступлениеАвтомобилейФорма.Открыть(); 
		//<<ЧалапАЮ БизТех 21.12.2023
	иначе  
		//Если Вопрос("По данному заказу уже создано поступление Trade-In. Создать новое поступление?",РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда   //почему-то при проведении ОТмены заказа, ругаетс яна Режимдиалога - убрала вопрос
			ПоступлениеАвтомобилей = Документы.ПоступлениеАвтомобилей.СоздатьДокумент();
			ПоступлениеАвтомобилей.Заполнить(Ссылка);
			ПоступлениеАвтомобилей.Автомобили.Очистить();
			ПоступлениеАвтомобилей.Опции.Очистить();
			ПоступлениеАвтомобилей.Контрагент     = Контрагент;
			ПоступлениеАвтомобилей.ОбработкаРеквизита("Контрагент");
			ПоступлениеАвтомобилей.СуммаДокумента = ПоступлениеАвтомобилей.РассчитатьСуммуВсего();  
			ПоступлениеАвтомобилейФорма = ПоступлениеАвтомобилей.ПолучитьФорму();
			ПоступлениеАвтомобилейФорма.ЗаказНаАвтомобиль = Ссылка;
			//ПоступлениеАвтомобилейФорма.ВидАвтомобиля     = Перечисления.ВидАвтомобиля.АвтомобильСПробегом;
			ПоступлениеАвтомобилейФорма.Открыть(); 
		//КонецЕсли; 
		//ЧалапАЮ БизТех 21.12.2023>>
	КонецЕсли;  
	
	//ПоступлениеАвтомобилейФорма = ПоступлениеАвтомобилей.ПолучитьФорму();
	//ПоступлениеАвтомобилейФорма.ЗаказНаАвтомобиль = Ссылка;
	////ПоступлениеАвтомобилейФорма.ВидАвтомобиля     = Перечисления.ВидАвтомобиля.АвтомобильСПробегом;
	//ПоступлениеАвтомобилейФорма.Открыть();  
КонецПроцедуры 

//Открывает отчет по оборудованию
Процедура ВывестиОтчетОборудования() Экспорт
	
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		Автомобиль.ПолучитьОбъект().ОтчетОборудованиеАвтомобиля();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу комплектации по автомобилю
Процедура ЗаполнитьКомплектациюАвтомобиля(ЭтаФорма = Неопределено) Экспорт
	
	#Если Клиент Тогда
		ЕстьОпции = Опции.Количество() <> 0;
		ЕстьОборудование = Товары.Количество() <> 0;
		Если ЕстьОпции ИЛИ ЕстьОборудование Тогда
			Если НЕ ЕстьОборудование Тогда
				ТекстВопроса = "Очистить табличную часть ""Опции"" перед заполнением по комплектации автомобиля?";	
			ИначеЕсли НЕ ЕстьОпции Тогда
				ТекстВопроса = "Очистить табличную часть ""Оборудование"" перед заполнением по комплектации автомобиля?";	
			Иначе
				ТекстВопроса = "Очистить табличные части ""Опции"" и ""Оборудование"" перед заполнением по комплектации автомобиля?";	
			КонецЕсли;
			ТекстВопроса = ТекстВопроса + "
			|
			|	ДА - Очистить табличную часть;
			|	НЕТ - Не очищать табличную часть;
			|	Отмена - Отменить заполнение по комплектации автомобиля.";
			Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
				Если ЕстьОпции Тогда
					Опции.Очистить();
				КонецЕсли;
				Если ЕстьОборудование Тогда
					Товары.Очистить();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	#КонецЕсли
	
	#Если Клиент Тогда
		ПолучитьОборудованиеАвтомобиля(ЭтаФорма);
		ПроверитьКомплектациюАвтомобиля();
	#КонецЕсли

	//Для Каждого СтрокаТЧ Из Опции Цикл
	//	ОбработкаРеквизита("Опции.Опция",СтрокаТЧ);
	//КонецЦикла; 
	
	//// получим таблицу комплектации
	//НайденыйАвтомобильОбъект = Автомобиль.ПолучитьОбъект();
	//Рез = НайденыйАвтомобильОбъект.ПолучитьОпцииИОборудованиеАвтомобиля(табКомплектация);
	//
	////если нет данных выйдем
	//Если НЕ Рез Тогда
	//	Возврат;
	//КонецЕсли;
	//
	//// заполнияем ТЧ
	//Для Каждого стрКомплектации Из табКомплектация Цикл
	//	Если ТипЗнч(стрКомплектации.Номенклатура) = Тип("СправочникСсылка.Опции") Тогда
	//		// добавление в опции
	//		НоваяСтрока = Опции.Найти(стрКомплектации.Номенклатура,"Опция"); 
	//		Если НоваяСтрока = Неопределено тогда
	//			НоваяСтрока = Опции.Добавить();
	//			НоваяСтрока.Опция = стрКомплектации.Номенклатура;
	//			ОбработкаРеквизита("Опции.Опция", НоваяСтрока);
	//			НоваяСтрока.Количество = 0;
	//		КонецЕсли;
	//		НоваяСтрока.Количество = НоваяСтрока.Количество + стрКомплектации.КоличествоОстаток;
	//		НоваяСтрока.СуммаВсего = обПересчет(стрКомплектации.СуммаУпрОстаток, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), Дата, КурсВалютыУпр),ВалютаДокумента,Дата,РежимОкругления.Окр15как20);
	//		ОбработкаРеквизита("Опции.СуммаВсего", НоваяСтрока);
	//	Иначе
	//		// добавление в товары
	//		НоваяСтрока = Товары.Найти(стрКомплектации.Номенклатура,"Номенклатура"); 
	//		Если НоваяСтрока = Неопределено тогда
	//			НоваяСтрока = Товары.Добавить();
	//			НоваяСтрока.Номенклатура = стрКомплектации.Номенклатура;
	//			ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
	//			НоваяСтрока.Количество = 0;
	//		КонецЕсли;
	//		НоваяСтрока.Количество = НоваяСтрока.Количество + стрКомплектации.КоличествоОстаток;
	//		НоваяСтрока.СуммаВсего = обПересчет(стрКомплектации.СуммаПродажиУпрОстаток, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), Дата, КурсВалютыУпр),ВалютаДокумента,Дата,РежимОкругления.Окр15как20);
	//		ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
	//	КонецЕсли;
	//КонецЦикла;
	
КонецПроцедуры

#Если Клиент Тогда
// Проверить таблицу комплектации по автомобилю
// неподходящие опции и оборудование удаляем
Процедура ПроверитьКомплектациюАвтомобиля(ЭтаФорма = Неопределено) Экспорт
	// таблица комплектации
	Перем табКомплектация;
	
	Если Не ЗначениеЗаполнено(Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	// получим таблицу комплектации
	НайденыйАвтомобильОбъект = Автомобиль.ПолучитьОбъект();
	Рез = НайденыйАвтомобильОбъект.ПолучитьОпцииИОборудованиеАвтомобиля(табКомплектация);
	
	//если нет данных выйдем
	//Если НЕ Рез Тогда
	//	Возврат;
	//КонецЕсли;
	
	Флаг = Ложь;
	ФлагОпции = Ложь;
	ФлагТовары = Ложь;
	ТЗОпции = Опции.Выгрузить();
	ТЗОборудование = Товары.Выгрузить();
	
	Если Рез Тогда
		Для Каждого ТекСТрока Из Опции Цикл
			Строка = табКомплектация.Найти(ТекСТрока.Опция, "Номенклатура");
			Если Строка = Неопределено Тогда
				Флаг = Истина;
				ФлагОпции = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ТекСТрока Из Товары Цикл
		Строка = табКомплектация.Найти(ТекСТрока.Номенклатура, "Номенклатура");
		СтрокаХарактеристикаНоменклатуры = табКомплектация.Найти(ТекСТрока.ХарактеристикаНоменклатуры, "ХарактеристикаНоменклатуры");
		Если Строка = Неопределено Или СтрокаХарактеристикаНоменклатуры = Неопределено Тогда
			Флаг = Истина;
			ФлагТовары = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекКомплектация Из табКомплектация Цикл
		Если ТипЗнч(ТекКомплектация.Номенклатура) = Тип("СправочникСсылка.Опции") Тогда
			СтрокаОпции = ТЗОпции.Найти(ТекКомплектация.Номенклатура, "Опция");
			Если СтрокаОпции = Неопределено ИЛИ Не (ТекКомплектация.КоличествоОстаток = СтрокаОпции.Количество) Тогда
				ФлагОпции = Истина;
				Флаг = Истина;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если ТипЗнч(ТекКомплектация.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			СтрокаОборудование = ТЗОборудование.Найти(ТекКомплектация.Номенклатура, "Номенклатура");
			СтрокаОборудованиеХарактеристикаНоменклатуры = ТЗОборудование.Найти(ТекКомплектация.ХарактеристикаНоменклатуры, "ХарактеристикаНоменклатуры");
			Если СтрокаОборудование = Неопределено Или Не (ТекКомплектация.КоличествоОстаток = СтрокаОборудование.Количество) Или СтрокаОборудованиеХарактеристикаНоменклатуры = Неопределено Тогда 
				ФлагТовары = Истина;
				Флаг = Истина;
				Продолжить
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Флаг Тогда 
		Ответ = Вопрос("В Заказе на автомобиль табличная часть "+?(ФлагОпции,"""Опции""", "")+""+?((ФлагТовары И ФлагОпции)," и ", "" )+""+?(ФлагТовары,"""Оборудование""", "")+" не соответствует комплектации автомобиля.
		|Перезаполнить согласно комплектации?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Если ФлагОпции Тогда
				Опции.Очистить();
			КонецЕсли;
			Если ФлагТовары Тогда
				Товары.Очистить();
			КонецЕсли;
			Для Каждого ТекКомплектация Из табКомплектация Цикл
				Если ТипЗнч(ТекКомплектация.Номенклатура) = Тип("СправочникСсылка.Опции") И ФлагОпции Тогда
					НоваяСтрока = Опции.Добавить();
					НоваяСтрока.Опция = ТекКомплектация.Номенклатура;
					ОбработкаРеквизита("Опции.Опция", НоваяСтрока);
					//НоваяСтрока.Количество = ТекКомплектация.КоличествоОстаток;
					//НоваяСтрока.СуммаВсего = обПересчет(ТекКомплектация.СуммаУпрОстаток, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), Дата, КурсВалютыУпр),ВалютаДокумента,Дата,РежимОкругления.Окр15как20);
					//ОбработкаРеквизита("Опции.СуммаВсего", НоваяСтрока);
				ИначеЕсли ТипЗнч(ТекКомплектация.Номенклатура) = Тип("СправочникСсылка.Номенклатура") И ФлагТовары Тогда
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.Номенклатура = ТекКомплектация.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры = ТекКомплектация.ХарактеристикаНоменклатуры;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
					НоваяСтрока.Количество = ТекКомплектация.КоличествоОстаток;
					НоваяСтрока.СуммаВсего = обПересчет(ТекКомплектация.СуммаПродажиУпрОстаток, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), Дата, КурсВалютыУпр),ВалютаДокумента,Дата,РежимОкругления.Окр15как20);
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
	
КонецПроцедуры
#КонецЕсли

#Если Клиент Тогда
Процедура ПолучитьОборудованиеАвтомобиля(ЭтаФорма=Неопределено) Экспорт
	//получим опции и оборудование согласно выбранному варианту комплектации 
	зфЗаказНаАвтомобильПолучитьОборудованиеАвтомобиля(ЭтотОбъект);
	Если ЭтаФорма <> Неопределено Тогда
		ЭтаФорма.ОборудованиеБазовое = ЭтотОбъект.ОборудованиеБазовое;
	КонецЕсли;
	
	//неподходящие опции удаляем
	Если ОборудованиеПроизводителя.Количество()=0 Тогда
		Опции.Очистить();
	Иначе
		КоличествоСтрок = Опции.Количество()-1;
		Пока КоличествоСтрок>=0 Цикл
			ТекОпция = Опции[КоличествоСтрок];
			Если ОборудованиеПроизводителя.Найти(ТекОпция.Опция,"Опция")=Неопределено Тогда
				#Если Клиент Тогда
				Сообщить("Опция <"+ТекОпция.Опция+"> недопустима для данного варианта комплектации.");
				#КонецЕсли
				Опции.Удалить(ТекОпция);
			КонецЕсли;
			КоличествоСтрок = КоличествоСтрок-1;
		КонецЦикла;
	КонецЕсли;
	
	//неподходящее оборудование удаляем
	Если ОборудованиеПродавца.Количество()>0 Тогда
		КоличествоСтрок = Товары.Количество()-1;
		Пока КоличествоСтрок>=0 Цикл
			ТекОборудование = Товары[КоличествоСтрок];
			Если ОборудованиеПродавца.Найти(ТекОборудование.Номенклатура,"Опция")=Неопределено Тогда
				#Если Клиент Тогда
				Сообщить("Оборудование <"+ТекОборудование.Номенклатура+"> недопустимо для данного варианта комплектации.");
				#КонецЕсли
				Товары.Удалить(ТекОборудование);
			КонецЕсли;
			КоличествоСтрок = КоличествоСтрок-1;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ЗаказНаАвтомобиль"
// Возвращает сформированный табличный документ:
Функция ПечатьЗаказНаАвтомобиль(ТабДокумент) Экспорт
	// Предварительные обработки данных
	
	//валюта печати
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	КоэффициентПересчета = обПересчет(1, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента, ВалютаПечатногоДокумента, ЭтотОбъект.Дата);
	
	// приступим к печати
	Макет = ПолучитьМакет("ЗаказНаАвтомобиль");
	
	//для начала настроим макет
	ОбластьТовар = Макет.Область("Товар");
	//определяем есть ли скидки
	Если Товары.Итог("СуммаСкидки")=0 И Опции.Итог("СуммаСкидки")=0 Тогда
		ОбластьСкидка = Макет.Область("Скидка");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьСкидка.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьСкидка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
		
	ЕстьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,,ЭтотОбъект);
	//если код не выводим
	Если НЕ ЕстьКод Тогда
		ОбластьКод = Макет.Область("Код");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);		
	КонецЕсли;
		
	//теперь запишем параметры шапки
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	Если ЕстьКод Тогда
		КолонкаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиКод = КолонкаКода.Синоним;
	КонецЕсли;
	
	//заполняем заголовок колонки НДС по типу цен
	ОбластьШапкаТаблицы.Параметры.НДС = "НДС";
	Если ТипЦен.ЦенаВключаетНДС Тогда	// Если НДС включен
		Если НЕ (ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС) Тогда	// Однако не все организации - плательщики НДС
			ОбластьШапкаТаблицы.Параметры.НДС = "в т.ч. НДС";
		КонецЕсли; 
	КонецЕсли;
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(Организация);
	//заказчик	
	ОбластьМакета.Параметры.ЗаказчикПредставление	= спПолучитьНаименование(Заказчик);
	ОбластьМакета.Параметры.ЗаказчикПочтовыйАдрес	= спПолучитьПредставление(Заказчик, Новый Структура("АдресФактический",""));
	ОбластьМакета.Параметры.ЗаказчикТелефоны		= спПолучитьПредставление(Заказчик, Новый Структура("ТелефонРабочий",""));
	//контрагент
	ОбластьМакета.Параметры.КонтрагентПредставление	= спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.СрокПоставки			= Формат(СрокПоставки,"ДФ='dd.ММММ.yyyy'; ДП=""Неопределен""");
	ОбластьМакета.Параметры.СуммаПредоплаты			= Формат(СуммаПредоплаты * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	//автомобиль
	ОбластьМакета.Параметры.Заполнить(ВариантКомплектации);	
	ОбластьМакета.Параметры.АвтомобильМодель		= спПолучитьНаименование(Модель);
	ОбластьМакета.Параметры.АвтомобильВариантКомплектации = спПолучитьНаименование(ВариантКомплектации);
	ОбластьМакета.Параметры.АвтомобильЦвет			= спПолучитьНаименование(Цвет);
	ОбластьМакета.Параметры.АвтомобильЦветКод		= ЦветКод;
	ОбластьМакета.Параметры.АвтомобильТипКузова		= спПолучитьНаименование(ВариантКомплектации.ТипКузова);
	ОбластьМакета.Параметры.АвтомобильТипДвигателя	= спПолучитьНаименование(ВариантКомплектации.ТипДвигателя);
	ОбластьМакета.Параметры.АвтомобильТипКПП		= спПолучитьНаименование(ВариантКомплектации.ТипКПП);
	ОбластьМакета.Параметры.АвтомобильТипСалона		= спПолучитьНаименование(ТипСалона);
	
	ОбластьМакета.Параметры.АвтомобильЦена			= Формат(ЦенаАвтомобиля * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.ПредставлениеНДС		= ?(ТипЦен.ЦенаВключаетНДС,"В т.ч. ","")+"НДС" + " (" + СтавкаНДСНаАвтомобиль.Ставка + "%)" + ": "+Формат(СуммаНДСНаАвтомобиль * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.ПредставлениеСкидки		= ?(СкидкаНаценкаНаАвтомобиль.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная, "", " (" + Формат(СкидкаНаценкаНаАвтомобиль.ЗначениеСкидки,"ЧДЦ=2; ЧН=0,00") + "%): ") + Формат(СуммаСкидкиНаАвтомобиль * КоэффициентПересчета, ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.СуммаВсегоНаАвтомобиль	= Формат(СуммаВсегоНаАвтомобиль * КоэффициентПересчета, ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	
	//базовое оборудование автомобиля
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОпцииВариантовКомплектации.Опция КАК Опция,
	|	ОпцииВариантовКомплектации.Количество КАК Количество
	|ИЗ
	|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
	|ГДЕ
	|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
	|	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("ВидВключения", 2);
	
	Выборка = Запрос.Выполнить().Выбрать();	СтрокаДопОборудования = "";
	Пока Выборка.Следующий() Цикл
		СтрокаДопОборудования = СтрокаДопОборудования + спПолучитьНаименование(Выборка.Опция) + ?(Выборка.Количество = 0,"","("+Выборка.Количество+")") + " ,";
	КонецЦикла;
	ОбластьМакета.Параметры.СписокБазовогоОборудования = Лев(СтрокаДопОборудования, СтрДлина(СтрокаДопОборудования) - 2);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
	
	//пересчитаем ТЧ
	ЭтотОбъектОпции = Опции.Выгрузить();
	зфПерерасчетТаблицыТоваров(ЭтотОбъектОпции, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	//дополнительные колонки для того чтобы замаскировать ТЧ Опции под ТЧ Товары
	ЭтотОбъектОпции.Колонки.Добавить("Номенклатура");
	ЭтотОбъектОпции.Колонки.Добавить("ЕдиницаИзмерения");
	ЭтотОбъектОпции.Колонки.Добавить("Коэффициент");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъектОпции;
	
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда 
		// ПЕЧАТЬ ТАБЛИЦЫ ОПЦИЙ
		//теперь выводим шапку
		ОбластьЗаголовокТаблицы.Параметры.ЗаголовокТаблицы = "Опции";
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиТовара = "Опции";
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	КонецЕсли;
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка
		СтрокаТабличнойЧасти.Номенклатура		= СтрокаТабличнойЧасти.Опция;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения	= "";
		СтрокаТабличнойЧасти.Коэффициент		= 1;
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда
		//довыводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
		Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") > 0 Тогда
			СуммаСкидки = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
			ОбластьМакета.Параметры.СкидкаВсего = Формат(СуммаСкидки,ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	// ПЕЧАТЬ ТАБЛИЦЫ ТОВАРОВ	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
	
	//пересчитаем ТЧ
	ЭтотОбъектТовары = Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ЭтотОбъектТовары, ЭтотОбъект, ВалютаПечатногоДокумента);

	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъектТовары;
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка= "";
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы	= "";
	
	Если ВыборкаТабличнойЧасти.Количество()>0 Тогда
		ОбластьЗаголовокТаблицы.Параметры.ЗаголовокТаблицы = "Оборудование";
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		//выводим шапку
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиТовара = "Оборудование";
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	КонецЕсли;
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	Если ВыборкаТабличнойЧасти.Количество()>0 Тогда
		//довыводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
		Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") > 0 Тогда
			СуммаСкидки = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
			ОбластьМакета.Параметры.СкидкаВсего = Формат(СуммаСкидки,ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим представления и расшифровки подписей
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	
	ОбластьМакета.Параметры.ВалютаДокумента	= ВалютаПечатногоДокумента;
	ОбластьМакета.Параметры.СуммаДокумента	= Формат(СуммаДокумента * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.СуммаДокументаПрописью = обЧислоПрописью(СуммаДокумента*КоэффициентПересчета,ВалютаПечатногоДокумента);
	СуммаНДС = СуммаНДСНаАвтомобиль + Опции.Итог("СуммаНДС") + Товары.Итог("СуммаНДС");
	ОбластьМакета.Параметры.СуммаНДС		= Формат(СуммаНДС * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"Исполнитель");
	Исполнитель.ИсполнительПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.Исполнитель),"","/ "+ Исполнитель.ИсполнительПредставление);
	ОбластьМакета.Параметры.Заполнить(Исполнитель);
	ОЛЗаказчик	= дкОтветственноеЛицо(ЭтотОбъект,"Заказчик");
	ОЛЗаказчик.ЗаказчикПредставление = ?(обЗначениеНеЗаполнено(ОЛЗаказчик.Заказчик),"","/ "+ ОЛЗаказчик.ЗаказчикПредставление);
	ОбластьМакета.Параметры.Заполнить(ОЛЗаказчик);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Функция печати договора продажи
Функция ПечатьДоговорПродажи(ТабДокумент) Экспорт
	//валюта печати
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда Возврат Неопределено; КонецЕсли;
	
	//пересчитаем ТЧ
	ТаблицаОборудования = Опции.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаОборудования, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	ТаблицаТоваров = Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаТоваров, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	//макет
	Макет = ПолучитьМакет("ДоговорПродажи");
	//заголовок документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.НомерДоговора = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДоговора = Формат(Дата,"ДФ=dd.MM.yyyy");
	
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьНаименование(ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ФирмаАдрес = киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	
	ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
	ОбластьМакета.Параметры.ЗаказчикПочтовыйАдрес = киПолучитьПредставлениеКИ(Заказчик,Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
	ОбластьМакета.Параметры.ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Заказчик,Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "	
		|ВЫБРАТЬ
		|	ПодтверждающиеДокументы.Ссылка КАК Документ,
		|	ПодтверждающиеДокументы.КемВыдан КАК ДокументКемВыдан,
		|	ПодтверждающиеДокументы.ДатаВыдачи КАК ДокументДатаВыдачи,
		|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента КАК ВидДокумента
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Владелец
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|";
	Запрос.УстановитьПараметр("Владелец", ЭтотОбъект.Заказчик);		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	Если ВыборкаДокументов.Количество()=0 Тогда
		ОбластьМакета.Параметры.ЗаказчикДокумент				= "_________________";
		ОбластьМакета.Параметры.ЗаказчикДокументКемВыдан		= "___________";
		ОбластьМакета.Параметры.ЗаказчикДокументДатаВыдачи		= "___________";
	Иначе 
		ВыборкаДокументов.Следующий();
		ОбластьМакета.Параметры.ЗаказчикДокумент				= Строка(ВыборкаДокументов.ВидДокумента) + ": " + ВыборкаДокументов.Документ;
		ОбластьМакета.Параметры.ЗаказчикДокументКемВыдан		= ВыборкаДокументов.ДокументКемВыдан;
		ОбластьМакета.Параметры.ЗаказчикДокументДатаВыдачи		= Формат(ВыборкаДокументов.ДокументДатаВыдачи,"ДФ='dd MMMM yyyy'");
	КонецЕсли;
	
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.РуководительДолжность 		= Руководитель.РуководительДолжность;
	ОбластьМакета.Параметры.РуководительПредставление 	= Руководитель.РуководительПредставление;
	
	СуммаПредоплатыПечать=обПересчет(СуммаПредоплаты,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ОбластьМакета.Параметры.СуммаПредоплаты 		= СуммаПредоплатыПечать;
	ОбластьМакета.Параметры.СуммаПредоплатыПрописью = обЧислоПрописью(СуммаПредоплатыПечать,ВалютаПечатногоДокумента);
	//ОбластьМакета.Параметры.ВалютаДокумента 		= ВалютаПечатногоДокумента;
	ОбластьМакета.Параметры.СрокПоставки    		= Формат(СрокПоставки,"ДФ=dd MMMM yyyy");
	ОбластьМакета.Параметры.ПредставлениеАвтомобиля = " модель """+спПолучитьНаименование(Модель)+?(обЗначениеНеЗаполнено(ВариантКомплектации),"",""" в комплектации """+спПолучитьНаименование(ВариантКомплектации)+"""");
	
	СуммаВсегоНаАвтомобильПечать = РассчитатьСуммуВсего();
	СуммаВсегоНаАвтомобильПечать = обПересчет(СуммаВсегоНаАвтомобильПечать,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ОбластьМакета.Параметры.СуммаВсегоНаАвтомобиль 			= СуммаВсегоНаАвтомобильПечать;
	ОбластьМакета.Параметры.СуммаВсегоНаАвтомобильПрописью 	= обЧислоПрописью(СуммаВсегоНаАвтомобильПечать,ВалютаПечатногоДокумента);
	
	ОбластьМакета.Параметры.СтавкаНДСНаАвтомобиль 			= СтавкаНДСНаАвтомобиль.Ставка;
	СуммаНДСНаАвтомобильПечать = обПересчет(РассчитатьСуммуНДСВсего(),ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ОбластьМакета.Параметры.СуммаНДСНаАвтомобиль 			= СуммаНДСНаАвтомобильПечать;
	ОбластьМакета.Параметры.СуммаНДСНаАвтомобильПрописью 	= обЧислоПрописью(СуммаНДСНаАвтомобильПечать,ВалютаПечатногоДокумента);
	
	ОбластьМакета.Параметры.ПроцентПредоплаты 				= ПроцентПредоплаты;
	ОбластьМакета.Параметры.ПроцентПредоплатыПрописью 		= ЧислоПрописью(ПроцентПредоплаты);
	СуммаПредоплатыПечать = обПересчет(СуммаПредоплаты,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ОбластьМакета.Параметры.СуммаПредоплаты   				= СуммаПредоплатыПечать;
	ОбластьМакета.Параметры.СуммаПредоплатыПрописью   		= обЧислоПрописью(СуммаПредоплатыПечать,ВалютаПечатногоДокумента);
	СуммаНДСПредоплатыПечать = обПересчет(РассчитатьСуммуНДСВсего()*ПроцентПредоплаты/100,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ОбластьМакета.Параметры.СуммаНДСПредоплаты   			= СуммаНДСПредоплатыПечать;
	ОбластьМакета.Параметры.СуммаНДСПредоплатыПрописью   	= обЧислоПрописью(СуммаНДСПредоплатыПечать,ВалютаПечатногоДокумента);
	
	ПроцентДоплаты = 100 - ПроцентПредоплаты;
	СуммаДоплаты = СуммаВсегоНаАвтомобильПечать - СуммаПредоплатыПечать;
	ОбластьМакета.Параметры.ПроцентДоплаты 					= ПроцентДоплаты;
	ОбластьМакета.Параметры.ПроцентДоплатыПрописью 			= ЧислоПрописью(ПроцентДоплаты);
	ОбластьМакета.Параметры.СуммаДоплаты   					= СуммаДоплаты;
	ОбластьМакета.Параметры.СуммаДоплатыПрописью   			= обЧислоПрописью(СуммаДоплаты,ВалютаПечатногоДокумента);
	СуммаНДСВсегоПечать = Окр(обПересчет(РассчитатьСуммуНДСВсего(),ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20),2,РежимОкругления.Окр15как20);
	СуммаНДСДоплаты = СуммаНДСВсегоПечать - СуммаНДСПредоплатыПечать;
	ОбластьМакета.Параметры.СуммаНДСДоплаты		   			= СуммаНДСДоплаты;
	ОбластьМакета.Параметры.СуммаНДСДоплатыПрописью   		= обЧислоПрописью(СуммаНДСДоплаты,ВалютаПечатногоДокумента);
	
	ОбластьМакета.Параметры.ИННКПП 							= "ИНН " + Организация.ИНН + " / КПП " + Организация.КПП;
	
	ОбластьМакета.Параметры.НомерСчетаПолучателя	= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.НомерСчета;
	ОбластьМакета.Параметры.БанкПолучателя			= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк;
	ОбластьМакета.Параметры.БИКБанкаПолучателя		= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.Код;
	ОбластьМакета.Параметры.СчетБанкаПолучателя		= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.КоррСчет;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПриложениеШапка");
	ОбластьМакета.Параметры.НомерДоговора = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДоговора = Формат(Дата,"ДФ=dd.MM.yyyy");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если ТаблицаОборудования.Количество() > 0 Тогда
		//Шапка таблицы
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьМакета.Параметры.ТаблицаСпецификации = "Оборудование производителя";
		ТабДокумент.Вывести(ОбластьМакета);
		//Таблица
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		Для каждого СтрокаОборудования Из ТаблицаОборудования Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаОборудования);
			ОбластьМакета.Параметры.ОборудованиеНаименование = спПолучитьНаименование(СтрокаОборудования.Опция);
			ОбластьМакета.Параметры.Оборудование = СтрокаОборудования.Опция;
			ОбластьМакета.Параметры.Цена = СтрокаОборудования.СуммаВсего/СтрокаОборудования.Количество;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.СуммаИтого = ТаблицаОборудования.Итог("СуммаВсего");		
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	Если ТаблицаТоваров.Количество() > 0 Тогда
		//Шапка таблицы
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьМакета.Параметры.ТаблицаСпецификации = "Товары продавца";		
		ТабДокумент.Вывести(ОбластьМакета);
		//Таблица
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		Для каждого СтрокаТоваров Из ТаблицаТоваров Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаТоваров);
			ОбластьМакета.Параметры.ОборудованиеНаименование = спПолучитьНаименование(СтрокаТоваров.Номенклатура);
			ОбластьМакета.Параметры.Оборудование = СтрокаТоваров.Номенклатура;
			Если СтрокаТоваров.Количество <> 0 Тогда
				ОбластьМакета.Параметры.Цена = СтрокаТоваров.СуммаВсего/СтрокаТоваров.Количество;
			Иначе
				ОбластьМакета.Параметры.Цена = 0;
			КонецЕсли;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.СуммаИтого = ТаблицаТоваров.Итог("СуммаВсего");		
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	//базовое доп. оборудование
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОпцииВариантовКомплектации.Опция КАК Опция
	|ИЗ
	|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
	|ГДЕ
	|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
	|	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения";
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("ВидВключения", 2);
	СписокДопОборудования = Запрос.Выполнить().Выгрузить();
	
	Если СписокДопОборудования.Количество() > 0 Тогда
		//Шапка таблицы
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицыОборудование");
		ТабДокумент.Вывести(ОбластьМакета);
		//Таблица
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаОборудование");
		НомерСтроки = 1;
		Для каждого ТекДопОб Из СписокДопОборудования Цикл
			ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
			ОбластьМакета.Параметры.ОборудованиеНаименование = спПолучитьНаименование(ТекДопОб.Опция);
			ОбластьМакета.Параметры.Оборудование = ТекДопОб.Опция;
			НомерСтроки = НомерСтроки + 1;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли; 

	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "Акт приема"
// Возвращает сформированный табличный документ:
Функция ПечатьАктПриема(ТабДокумент) Экспорт
	
	СвойствоМодель = обПолучитьЗначениеСвойства(ЭтотОбъект,"Модель");
	
	Если НЕ ЗначениеЗаполнено(СвойствоМодель) Тогда
		Предупреждение("Акт осмотра автомобиля может быть напечатан, если указана модель автомобиля для поступления по Trade-In!");		
		Возврат Неопределено;
	КонецЕсли;
	
	Макет = Документы.ПоступлениеАвтомобилей.ПолучитьМакет("АктПриема");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект,Истина);
	
	ОбластьМакета.Параметры.ЗаголовокПФ = "осмотра автомобиля с пробегом";
	ОбластьМакета.Параметры.Номер = НомерДляПечати;
	ОбластьМакета.Параметры.Дата = Формат(ЭтотОбъект.Дата,"ДЛФ=DD");
	ОбластьМакета.Параметры.МодельНаименование = спПолучитьНаименование(СвойствоМодель);
	ОбластьМакета.Параметры.Модель = СвойствоМодель;
	ОбластьМакета.Параметры.ГодВыпуска = Формат(обПолучитьЗначениеСвойства(ЭтотОбъект,"ГодВыпуска"),"ДФ=гггг");
	ОбластьМакета.Параметры.Цвет = обПолучитьЗначениеСвойства(ЭтотОбъект,"Цвет");
	ОбластьМакета.Параметры.Пробег = ?(ЗначениеЗаполнено(обПолучитьЗначениеСвойства(ЭтотОбъект,"Пробег")),СокрЛП(обПолучитьЗначениеСвойства(ЭтотОбъект,"Пробег")),"");
	
	Комплектация = обПолучитьЗначениеСвойства(ЭтотОбъект,"ВариантКомплектации");
	ОбластьМакета.Параметры.Комплектация = Комплектация;
	Если ЗначениеЗаполнено(Комплектация) Тогда 
		ОбластьМакета.Параметры.ТипКПП = Комплектация.ТипКПП;
		Если НЕ Комплектация.ТипДвигателя.Пустая() Тогда
			ОбластьМакета.Параметры.ОбъемДвигателя = Комплектация.ТипДвигателя.ОбъемДвигателя;
		КонецЕсли;
	КонецЕсли;	
	
	ОбластьМакета.Параметры.ОписаниеКомитента = Контрагент.НаименованиеПолное;
	ОбластьМакета.Параметры.ОписаниеКомиссионера = Организация.НаименованиеПолное;
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	Возврат ТабДокумент;
	
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

//ШАБЛОНЫ ОФИСНЫХ ДОКУМЕНТОВ

// Формирует структуру с данными объекта, которые необходимы для заполнения шаблона
Функция ПолучитьДанныеОбъекта() Экспорт
		
	//валюта печати
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда Возврат Неопределено; КонецЕсли;
	
	//пересчитаем ТЧ
	ТаблицаОборудования = Опции.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаОборудования, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	ТаблицаТоваров = Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаТоваров, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ДанныеОбъекта = Новый Структура;
	
	ДанныеОбъекта.Вставить("НомерДоговора", НомерДляПечати);
	ДанныеОбъекта.Вставить("ДатаДоговора", Формат(Дата,"ДФ=dd.MM.yyyy"));
	ДанныеОбъекта.Вставить("ПредставлениеОрганизации", спПолучитьНаименование(ЭтотОбъект.Организация));
	ДанныеОбъекта.Вставить("ФирмаАдрес", киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический));
	ДанныеОбъекта.Вставить("ФирмаТелефоны", киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий));
	ДанныеОбъекта.Вставить("ЗаказчикПолноеНаименование", спПолучитьНаименование(Заказчик));
	ДанныеОбъекта.Вставить("ЗаказчикПочтовыйАдрес", киПолучитьПредставлениеКИ(Заказчик,Справочники.ВидыКонтактнойИнформации.АдресПочтовый));
	ДанныеОбъекта.Вставить("ЗаказчикТелефоны", киПолучитьПредставлениеКИ(Заказчик,Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "	
		|ВЫБРАТЬ
		|	ПодтверждающиеДокументы.Ссылка КАК Документ,
		|	ПодтверждающиеДокументы.КемВыдан КАК ДокументКемВыдан,
		|	ПодтверждающиеДокументы.ДатаВыдачи КАК ДокументДатаВыдачи,
		|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента КАК ВидДокумента
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Владелец
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|";
	Запрос.УстановитьПараметр("Владелец", ЭтотОбъект.Заказчик);		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	Если ВыборкаДокументов.Количество()=0 Тогда
		ДанныеОбъекта.Вставить("ЗаказчикДокумент", "_________________");
		ДанныеОбъекта.Вставить("ЗаказчикДокументКемВыдан", "___________");
		ДанныеОбъекта.Вставить("ЗаказчикДокументДатаВыдачи", "___________");
	Иначе 
		ВыборкаДокументов.Следующий();
		ДанныеОбъекта.Вставить("ЗаказчикДокумент", Строка(ВыборкаДокументов.ВидДокумента) + ": " + ВыборкаДокументов.Документ);
		ДанныеОбъекта.Вставить("ЗаказчикДокументКемВыдан", ВыборкаДокументов.ДокументКемВыдан);
		ДанныеОбъекта.Вставить("ЗаказчикДокументДатаВыдачи", Формат(ВыборкаДокументов.ДокументДатаВыдачи,"ДФ='dd MMMM yyyy'"));
	КонецЕсли;
	
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ДанныеОбъекта.Вставить("РуководительДолжность", Руководитель.РуководительДолжность);
	ДанныеОбъекта.Вставить("РуководительПредставление", Руководитель.РуководительПредставление);
	
	СуммаПредоплатыПечать=обПересчет(СуммаПредоплаты,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ДанныеОбъекта.Вставить("СуммаПредоплаты", СуммаПредоплатыПечать);
	ДанныеОбъекта.Вставить("СуммаПредоплатыПрописью", обЧислоПрописью(СуммаПредоплатыПечать,ВалютаПечатногоДокумента));
	ДанныеОбъекта.Вставить("СрокПоставки", Формат(СрокПоставки,"ДФ=dd MMMM yyyy"));
	ДанныеОбъекта.Вставить("ПредставлениеАвтомобиля", " модель """+спПолучитьНаименование(Модель)+?(обЗначениеНеЗаполнено(ВариантКомплектации),"",""" в комплектации """+спПолучитьНаименование(ВариантКомплектации)+""""));
	
	СуммаВсегоНаАвтомобильПечать = РассчитатьСуммуВсего();
	СуммаВсегоНаАвтомобильПечать = обПересчет(СуммаВсегоНаАвтомобильПечать,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ДанныеОбъекта.Вставить("СуммаВсегоНаАвтомобиль", СуммаВсегоНаАвтомобильПечать);
	ДанныеОбъекта.Вставить("СуммаВсегоНаАвтомобильПрописью", обЧислоПрописью(СуммаВсегоНаАвтомобильПечать,ВалютаПечатногоДокумента));	
	
	ДанныеОбъекта.Вставить("СтавкаНДСНаАвтомобиль", СтавкаНДСНаАвтомобиль.Ставка);
	СуммаНДСНаАвтомобильПечать = обПересчет(РассчитатьСуммуНДСВсего(),ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ДанныеОбъекта.Вставить("СуммаНДСНаАвтомобиль", СуммаНДСНаАвтомобильПечать);
	ДанныеОбъекта.Вставить("СуммаНДСНаАвтомобильПрописью", обЧислоПрописью(СуммаНДСНаАвтомобильПечать,ВалютаПечатногоДокумента));
	
	ДанныеОбъекта.Вставить("ПроцентПредоплаты", ПроцентПредоплаты);
	ДанныеОбъекта.Вставить("ПроцентПредоплатыПрописью", ЧислоПрописью(ПроцентПредоплаты));
	
	СуммаПредоплатыПечать = обПересчет(СуммаПредоплаты,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ДанныеОбъекта.Вставить("СуммаПредоплаты", СуммаПредоплатыПечать);
	ДанныеОбъекта.Вставить("СуммаПредоплатыПрописью", обЧислоПрописью(СуммаПредоплатыПечать,ВалютаПечатногоДокумента));
	
	СуммаНДСПредоплатыПечать = обПересчет(РассчитатьСуммуНДСВсего()*ПроцентПредоплаты/100,ВалютаДокумента,Дата,ВалютаПечатногоДокумента,Дата,РежимОкругления.Окр15как20);
	ДанныеОбъекта.Вставить("СуммаНДСПредоплаты", СуммаНДСПредоплатыПечать);
	ДанныеОбъекта.Вставить("СуммаНДСПредоплатыПрописью", обЧислоПрописью(СуммаНДСПредоплатыПечать,ВалютаПечатногоДокумента));
	
	ПроцентДоплаты = 100 - ПроцентПредоплаты;
	СуммаДоплаты = СуммаВсегоНаАвтомобильПечать - СуммаПредоплатыПечать;
	ДанныеОбъекта.Вставить("ПроцентДоплаты", ПроцентДоплаты);
	ДанныеОбъекта.Вставить("ПроцентДоплатыПрописью", ЧислоПрописью(ПроцентДоплаты));
	ДанныеОбъекта.Вставить("СуммаДоплаты", СуммаДоплаты);
	ДанныеОбъекта.Вставить("СуммаДоплатыПрописью", обЧислоПрописью(СуммаДоплаты,ВалютаПечатногоДокумента));
	
	СуммаНДСДоплаты = СуммаНДСНаАвтомобильПечать - СуммаНДСПредоплатыПечать;
	ДанныеОбъекта.Вставить("СуммаНДСДоплаты", СуммаНДСДоплаты);
	ДанныеОбъекта.Вставить("СуммаНДСДоплатыПрописью", обЧислоПрописью(СуммаНДСДоплаты,ВалютаПечатногоДокумента));
	
	ДанныеОбъекта.Вставить("ИННКПП", "ИНН " + Организация.ИНН + " / КПП " + Организация.КПП);
	
	ДанныеОбъекта.Вставить("НомерСчетаПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.НомерСчета);
	ДанныеОбъекта.Вставить("БанкПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк);
	ДанныеОбъекта.Вставить("БИКБанкаПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.Код);
	ДанныеОбъекта.Вставить("СчетБанкаПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.КоррСчет);
	
	//заголовок--
	
	//ДанныеОбъекта.Вставить("НомерДоговора", НомерДляПечати);
	//ДанныеОбъекта.Вставить("ДатаДоговора", Формат(Дата,"ДФ=dd.MM.yyyy");
	
	ДанныеОбъекта.Вставить("ТаблицаОборудования", Новый Структура);		
	Если ТаблицаОборудования.Количество() > 0 Тогда
		//Шапка таблицы
		ДанныеОбъекта.ТаблицаОборудования.Вставить("ТаблицаСпецификации", "Оборудование производителя");		
		//Таблица
		ДанныеОбъекта.ТаблицаОборудования.Вставить("Строки", Новый Массив);		
		
		НомерСтрокиПоПорядку = 0;
		
		Для каждого СтрокаОборудования Из ТаблицаОборудования Цикл
			
			НомерСтрокиПоПорядку = НомерСтрокиПоПорядку + 1;
			
			СтрокаТаблицы = Новый Структура;
			СтрокаТаблицы.Вставить("НомерСтроки",		НомерСтрокиПоПорядку);
			СтрокаТаблицы.Вставить("Оборудование",	СтрокаОборудования.Опция);
			СтрокаТаблицы.Вставить("ОборудованиеНаименование",	спПолучитьНаименование(СтрокаОборудования.Опция));
			СтрокаТаблицы.Вставить("СуммаВсего",		Формат(СтрокаОборудования.СуммаВсего,"ЧДЦ=2; ЧН=0,00"));
			СтрокаТаблицы.Вставить("Количество",	СтрокаОборудования.Количество);
			СтрокаТаблицы.Вставить("Цена",		Формат((СтрокаОборудования.СуммаВсего/СтрокаОборудования.Количество),"ЧДЦ=2; ЧН=0,00"));
			СтрокаТаблицы.Вставить("Количество",	СтрокаОборудования.Количество);			
			
			ДанныеОбъекта.ТаблицаОборудования.Строки.Добавить(СтрокаТаблицы);
		КонецЦикла;
		ДанныеОбъекта.ТаблицаОборудования.Вставить("СуммаИтого", Формат(ТаблицаОборудования.Итог("СуммаВсего"),"ЧДЦ=2; ЧН=0,00"));		
	КонецЕсли;
	
	
	ДанныеОбъекта.Вставить("ТаблицаТоваров", Новый Структура);		
	Если ТаблицаТоваров.Количество() > 0 Тогда
		//Шапка таблицы
		ДанныеОбъекта.ТаблицаТоваров.Вставить("ТаблицаСпецификации", "Товары продавца");		
		//Таблица
		ДанныеОбъекта.ТаблицаТоваров.Вставить("Строки", Новый Массив);		
		
		НомерСтрокиПоПорядку = 0;
		
		Для каждого СтрокаТоваров Из ТаблицаТоваров Цикл
			
			НомерСтрокиПоПорядку = НомерСтрокиПоПорядку + 1;
			
			СтрокаТаблицы = Новый Структура;
			СтрокаТаблицы.Вставить("НомерСтроки",		НомерСтрокиПоПорядку);
			СтрокаТаблицы.Вставить("Оборудование",	СтрокаТоваров);
			СтрокаТаблицы.Вставить("ОборудованиеНаименование",	спПолучитьНаименование(СтрокаТоваров.Номенклатура));
			СтрокаТаблицы.Вставить("СуммаВсего",	Формат(СтрокаТоваров.СуммаВсего,"ЧДЦ=2; ЧН=0,00"));
			СтрокаТаблицы.Вставить("Количество",	СтрокаТоваров.Количество);
			Если СтрокаТоваров.Количество <> 0 Тогда
				СтрокаТаблицы.Вставить("Цена",	Формат((СтрокаТоваров.СуммаВсего/СтрокаТоваров.Количество),"ЧДЦ=2; ЧН=0,00"));
			Иначе
				СтрокаТаблицы.Вставить("Цена",	Формат(0,"ЧДЦ=2; ЧН=0,00"));
			КонецЕсли;
			
			ДанныеОбъекта.ТаблицаТоваров.Строки.Добавить(СтрокаТаблицы);
		КонецЦикла;
		ДанныеОбъекта.ТаблицаТоваров.Вставить("СуммаИтого", Формат(ТаблицаТоваров.Итог("СуммаВсего"),"ЧДЦ=2; ЧН=0,00"));		
	КонецЕсли;
	
	
	//базовое доп. оборудование
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОпцииВариантовКомплектации.Опция КАК Опция
	|ИЗ
	|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
	|ГДЕ
	|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
	|	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения";
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("ВидВключения", 2);
	СписокДопОборудования = Запрос.Выполнить().Выгрузить();

	
	ДанныеОбъекта.Вставить("ТаблицаДопОборудования", Новый Структура);		
	Если СписокДопОборудования.Количество() > 0 Тогда

		//Шапка таблицы
		
		//Таблица
		ДанныеОбъекта.ТаблицаДопОборудования.Вставить("Строки", Новый Массив);		
		
		НомерСтрокиПоПорядку = 0;
		
		Для каждого СтрокаДопОб Из СписокДопОборудования Цикл
			НомерСтрокиПоПорядку = НомерСтрокиПоПорядку + 1;
			СтрокаТаблицы = Новый Структура;
			СтрокаТаблицы.Вставить("НомерСтроки",		НомерСтрокиПоПорядку);
			СтрокаТаблицы.Вставить("ОборудованиеНаименование",		спПолучитьНаименование(СтрокаДопОб.Опция));
			СтрокаТаблицы.Вставить("Оборудование",		СтрокаДопОб.Опция);
			
			ДанныеОбъекта.ТаблицаДопОборудования.Строки.Добавить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли; 
	
	Возврат ДанныеОбъекта;
	
КонецФункции

// Формирует структуру с описанием областей шаблона
Функция ПолучитьОписаниеОбластейМакетаОфисногоДокумента() Экспорт
	
	ОписаниеОбластей = Новый Структура;
	
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ВерхнийКолонтитул",	"ВерхнийКолонтитул");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "НижнийКолонтитул",		"НижнийКолонтитул");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "Заголовок",	"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "Приложение",		"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаТаблицы",		"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "Строка",	"СтрокаТаблицы");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаТаблицыОборудование",		"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаОборудование",			"СтрокаТаблицы");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал",	"Общая");
		
	Возврат ОписаниеОбластей;
	
КонецФункции

// Формирует печатную форму "ЗаказНаряд"
// Возвращает сформированный табличный документ:
Функция ПечатьШаблонаЗаказНаАвтомобильДоговорПродажи(ТабДокумент,ДвоичныеДанныеМакета,ИмяМакета) Экспорт
	
	Области = ПолучитьОписаниеОбластейМакетаОфисногоДокумента();
	ДанныеОбъекта = ПолучитьДанныеОбъекта();

	Попытка
		
		Если пчПроверитьCOMСоединениеMSWord() Тогда
			ТипМакета = "MS";
		ИначеЕсли пчПроверитьCOMСоединениеOOWriter() Тогда
			ТипМакета = "OO";
		Иначе
			Сообщить("Ошибка при попытке установки соединения. 
			|Для вывода печатных форм требуется, чтобы на компьютере был установлен пакет Microsoft Office или Open Office.");
			Возврат Ложь;	
		КонецЕсли;
		
		//Если Вопрос("WORD - ДА, OpenOffice - НЕТ",РежимДиалогаВопрос.ДаНет,5,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
		//	ТипМакета = "MS";
		//Иначе
		//	ТипМакета = "OO";
		//КонецЕсли;
		
		Макет		   = пчИнициализироватьМакет(ДвоичныеДанныеМакета, ТипМакета);
 		ПечатнаяФорма  = пчИнициализироватьПечатнуюФорму(ТипМакета, Макет.НастройкиСтраницыМакета);
		
		Если ПечатнаяФорма <> Неопределено И Макет <> Неопределено Тогда
			
			// Вывод колонтитулов документа.
			Область = пчПолучитьОбласть(Макет, Области["ВерхнийКолонтитул"]);
			пчПрисоединитьОбласть(ПечатнаяФорма, Область);
			
			Область = пчПолучитьОбласть(Макет, Области["НижнийКолонтитул"]);
			пчПрисоединитьОбласть(ПечатнаяФорма, Область);
			
			Область = пчПолучитьОбласть(Макет, Области["Заголовок"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта);
			
			Область = пчПолучитьОбласть(Макет, Области["Приложение"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ДанныеОбъекта.ТаблицаОборудования.Количество() > 0 Тогда
				Область = пчПолучитьОбласть(Макет, Области["ШапкаТаблицы"]);
				пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаОборудования, Ложь);
				
				Область = пчПолучитьОбласть(Макет, Области["Строка"]);
				пчПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаОборудования.Строки, Ложь);
				
				Область = пчПолучитьОбласть(Макет, Области["Подвал"]);
				пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаОборудования, Истина, Истина);
				
			КонецЕсли;	
				
			Если ДанныеОбъекта.ТаблицаТоваров.Количество() > 0 Тогда
				Область = пчПолучитьОбласть(Макет, Области["ШапкаТаблицы"]);
				пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаТоваров, Ложь);
				
				Область = пчПолучитьОбласть(Макет, Области["Строка"]);
				пчПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаТоваров.Строки, Ложь);
	
				Область = пчПолучитьОбласть(Макет, Области["Подвал"]);
				пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаТоваров, Истина, Истина);
			
			КонецЕсли;	
			
			Если ДанныеОбъекта.ТаблицаДопОборудования.Количество() > 0 Тогда
				Область = пчПолучитьОбласть(Макет, Области["ШапкаТаблицыОборудование"]);
				пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаДопОборудования, Ложь, Истина);
			
				Область = пчПолучитьОбласть(Макет, Области["СтрокаОборудование"]);
				пчПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаДопОборудования.Строки, Ложь);
			КонецЕсли;	
			
			пчПоказатьДокумент(ПечатнаяФорма);
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Сообщить(ИнформацияОбОшибке);
		пчОчиститьСсылки(ПечатнаяФорма,Ложь);
		пчОчиститьСсылки(Макет);
		Возврат Ложь;
	КонецПопытки;
	
	пчОчиститьСсылки(ПечатнаяФорма,Ложь);
	пчОчиститьСсылки(Макет);
	
	Возврат ТабДокумент;
	
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание) Экспорт
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") И ЗначениеЗаполнено(ДокументОснование) Тогда
		Если ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобиль Тогда
			ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобильИзменение;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.РабочийЛист") Тогда
		РабочийЛист	= Основание;
		ДокументОснование	= Основание;
		Контрагент			= Основание.Контрагент;
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ТипЦенПродажи", 			Основание.ТипЦен);
		ДопПараметры.Вставить("ВалютаВзаиморасчетов",   Основание.ВалютаДокумента);
		
		ОбработкаРеквизита("Контрагент",,,ДопПараметры);
		Заказчик			= Основание.Контрагент;
		ХозОперация			= Справочники.ХозОперации.ЗаказНаАвтомобиль;
		ТипСалона			= Основание.Интерьер;
		
		КоличествоАвто	= ?(Основание.КоличествоАвтомобилей=0,1,Основание.КоличествоАвтомобилей);
		ЦенаАвтомобиля	= Окр(Основание.СуммаПродажи/КоличествоАвто,2);
		
		Если обЗначениеНеЗаполнено(СтавкаНДСНаАвтомобиль) Тогда
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			ОбработкаРеквизита("СтавкаНДСНаАвтомобиль");
		КонецЕсли;
		
		ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(Неопределено);
		ОбработкаРеквизита("ЦенаАвтомобиля");
		
		//при создании Заказа в АРМ Подбор опции переносятся из Рабочего листа без изменений
		АРМПодборАвтомобилей = Неопределено;
		ЭтотОбъект.ДополнительныеСвойства.Свойство("АРМПодборАвтомобилей",АРМПодборАвтомобилей);	
		Если АРМПодборАвтомобилей = Неопределено Тогда
			ЗаполнитьКомплектациюАвтомобиля();
			ДопПараметры = Новый Структура("НеИзменятьЦеныОпций",Истина);
			Для Каждого СтрокаТЧ Из Опции Цикл
				ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,, ДопПараметры);
			КонецЦикла; 
		КонецЕсли;
		
		ОбработкаРеквизита("ПроцентПредоплаты");
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.Автомобили") И (НЕ Основание.ЭтоГруппа) Тогда
		
		Автомобиль = Основание;
		ОбработкаРеквизита("Автомобиль");
		Если обЗначениеНеЗаполнено(СтавкаНДСНаАвтомобиль) Тогда
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			ОбработкаРеквизита("СтавкаНДСНаАвтомобиль");
		КонецЕсли; 
		
	КонецЕсли;
	
	Если (НЕ ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильОтмена) И обЗначениеНеЗаполнено(СрокПоставки) Тогда
		СрокПоставкиДней = обПраво("СрокПоставкиПокупателюПоУмолчанию",Права,,ЭтотОбъект);
		СрокПоставки     = НачалоДня(ТекущаяДата())+СрокПоставкиДней*60*60*24;
	КонецЕсли;
	
	Если НеЗаполнятьКонтрагента Тогда
		Заказчик	= Справочники.Контрагенты.ПустаяСсылка();
		Контрагент	= Справочники.Контрагенты.ПустаяСсылка();
		ДоговорВзаиморасчетов	= Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();		
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Основание) Тогда
		Если обЗначениеНеЗаполнено(СтавкаНДСНаАвтомобиль) Тогда
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			ОбработкаРеквизита("СтавкаНДСНаАвтомобиль");
		КонецЕсли; 
		НоваяСуммаДокумента = РассчитатьСуммуВсего();
		Если СуммаДокумента<>НоваяСуммаДокумента Тогда
			СуммаДокумента = НоваяСуммаДокумента;
		КонецЕсли; 
		//Заполнение предоплаты
		Если ДоговорВзаиморасчетов.ПроцентПредоплаты = 0 Тогда
			ПроцентПредоплаты = обПраво("МинимальныйПроцентПредоплаты", Права,,ЭтотОбъект);
		Иначе
			ПроцентПредоплаты = ДоговорВзаиморасчетов.ПроцентПредоплаты;
		КонецЕсли;
		ОбработкаРеквизита("ПроцентПредоплаты");
	КонецЕсли;
	
	Резервировать = ПроверитьОстаткиАвтомобилей();
	
	ТекущаяВалютаДокумента = ВалютаДокумента;
	ТекущийКурсДокумента   = КурсДокумента;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		Автомобиль=Справочники.Автомобили.ПустаяСсылка();
		ОбработкаРеквизита("Автомобиль");
	КонецЕсли;  
	
	//<<ЧалапАЮ БизТех 06.10.2023    
	//Если ЗначениеЗаполнено(ОбъектКопирования.Контрагент.БТ_ПризнакЛояльности)  тогда          
	//	//Сообщить("Контрагент " + ОбъектКопирования.Контрагент.Наименование +" имеет признак " + ОбъектКопирования.Контрагент.БТ_ПризнакЛояльности +", дальнейшие работы с ним запрещены, необходимо обратиться к руководителю отдела.");	
	//	//Контрагент = Справочники.Контрагенты.ПустаяСсылка(); 
	//	//Заказчик = Справочники.Контрагенты.ПустаяСсылка();   
	//	Предупреждение("Контрагент " + ОбъектКопирования.Контрагент.Наименование +" имеет признак " + ОбъектКопирования.Контрагент.БТ_ПризнакЛояльности); //ЧалапАю БизТех 10.01.25
	//КонецЕсли; 
	//ЧалапАЮ БизТех 06.10.2023>>

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим сумму предоплаты
	Если НЕ ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильОтмена Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение И СуммаДокумента<СуммаПредоплаты Тогда
			Сообщить("Сумма предоплаты не может быть больше суммы документа");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И НЕ Отказ И Резервировать Тогда
	 	Отказ = (НЕ ПроверитьОстаткиАвтомобилей());
		Если Отказ Тогда
			Сообщить("Автомобиль: """+СокрЛП(Автомобиль)+""" отсутствует. Резервирование невозможено.",  СтатусСообщения.Важное);
		КонецЕсли;
	КонецЕсли;  
	
	////БизТех 17.04.2023г. нельзя отменять проведение документа, если есть подчиненные документы вида ЗаказНаАвтомобиль <<
	//Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения и НЕ Отказ Тогда  
	//	Запрос = Новый Запрос;
	//	Запрос.Текст =  "ВЫБРАТЬ
	//	                |	ЗаказыАвтомобилей.Регистратор КАК Регистратор
	//	                |ИЗ
	//	                |	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
	//	                |ГДЕ
	//	                |	ЗаказыАвтомобилей.Заказ = &Заказ
	//	                |	И (ТИПЗНАЧЕНИЯ(ЗаказыАвтомобилей.Регистратор) = ТИП(Документ.ЗаказНаАвтомобиль)
	//	                |			ИЛИ ТИПЗНАЧЕНИЯ(ЗаказыАвтомобилей.Регистратор) = ТИП(Документ.РеализацияАвтомобилей))";
	//	Запрос.УстановитьПараметр("Заказ", ЭтотОбъект.Ссылка);
	//	Рез = Запрос.Выполнить().Выгрузить();
	//	Если Рез.Количество()>1 и не РольДоступна("ПолныеПрава") Тогда   
	//		Сообщить("Существуют подчиненные документы! Нельзя отменить проведение.");
	//		Отказ = Истина;
	//	КонецЕсли;
	//КонецЕсли;
	//// >>	
	
	//ДокументыДляПерепроведения = Неопределено;
	//Если (НЕ Отказ) И (НЕ Ссылка.Пустая()) Тогда
	//	//Поищем резервирование автомобиля со старым VIN, 
	//	//осуществленным после момента документа
	//	 Запрос = Новый Запрос;
	//	Запрос.Текст = "ВЫБРАТЬ
	//				 |	ЗаказыАвтомобилей.Регистратор КАК Регистратор
	//				 |ИЗ
	//				 |	РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыАвтомобилей
	//				 |ГДЕ
	//				 |	ЗаказыАвтомобилей.Регистратор <> &Ссылка И 
	//				 |	ЗаказыАвтомобилей.Заказ = &Ссылка И 
	//				 |	ЗаказыАвтомобилей.Автомобиль <> &Автомобиль И 
	//				 |	ЗаказыАвтомобилей.Период >= &Дата
	//				 |УПОРЯДОЧИТЬ ПО
	//				 |	Регистратор";
	//	Запрос.УстановитьПараметр("Ссылка",     Ссылка);
	//	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	//	Запрос.УстановитьПараметр("Дата",       Ссылка.Дата);
	//	
	//	ДокументыДляПерепроведения = Запрос.Выполнить().Выгрузить();
	//КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		// автомобили
		//Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		//Результат = Запрос.Выполнить();
		//Если НЕ Результат.Пустой() Тогда
		//	БылиДвижения = Истина;
		//	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		//КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	#Если Клиент Тогда
		Оповестить("ОбновитьДеревоСобытий", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	//Если НЕ Отказ Тогда
	//	//Выведем сообщения о необходимости перепроведения будущих документов
	//	СообщитьОПерепроведении();
	//КонецЕсли;
	
КонецПроцедуры

// Проверка наличия других заказов на автомобиль
//
// Параметры
//  VINАвтомобиля  – <Строка> – VIN автомобиля, заказы на который проверяются
//  ОбъектПроверки  – <ДокументОбъект> – Документ, который проверяет наличие заказов
//
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль(АвтомобильПроверки,ОбъектПроверки)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.ЗаказНаАвтомобильПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,АвтомобильПроверки,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.ЗаказНаАвтомобильПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,АвтомобильПроверки,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Проверка наличия автомобиля на складах
//
// Параметры
//
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьОстаткиАвтомобилей() Экспорт
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("Автомобиль",Автомобиль);
	Возврат зфЗащищенныеФункцииСервер.ЗаказНаАвтомобильПроверитьОстаткиАвтомобилей(ДокументОбъектСтруктура);
КонецФункции // ПроверитьОстаткиАвтомобилей()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	#Если Клиент Тогда
    //БизТех Коваль А.Д. 03.07.2024 ++
	//Отказ записи если у контрагента присутствует флаг "телефон неактуален"
	Если Контрагент.ТелефонКонтрНеАктуален Тогда
		Предупреждение("Телефон данного контрагента не актуален. Обновите телефон контрагента перед закрытием документа.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	//БизТех Коваль А.Д. 03.07.2024 --
	#КонецЕсли
	
	
	//Bizteh++ 05.08.2024 убираем обмен с Оптима МК, т.к. с 01.10.2022 КСО в базе ФКДД
	////->Diginova _Настройка базы_ 05.01.2022
	//Если Не Отказ и Константы.DN_ЗапуститьОбмен.Получить() тогда
	//	УИД = Ссылка.УникальныйИдентификатор();
	//	Отправлен = DN_HTTP_Обмен.ОтправитьОбъект(Ссылка,УИД);
	//КонецЕсли;
	////<-Diginova _FlaiM_ 
	//Bizteh++ 05.08.2024 убираем обмен с Оптима МК, т.к. с 01.10.2022 КСО в базе ФКДД

	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим процент предоплаты по документу
	Если ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобиль ИЛИ 
		ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильИзменение ИЛИ 
		ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильПереуступка Тогда
		
		////Проверим процент предоплаты по документу
		//Если ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильИзменение ИЛИ 
		//	ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильПереуступка Тогда
		
		//	//Проверим сумму предоплаты по заказу
		//	Запрос = Новый Запрос;
		//	Запрос.Текст = "ВЫБРАТЬ
		//	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаРасход КАК Сумма,
		//	|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрРасход КАК СуммаУпр
		//	|ИЗ
		//	|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		//	|	ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		//	|	Контрагент = &Контрагент И
		//	|	(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)) КАК ВзаиморасчетыКомпанииОстаткиИОбороты";
		//	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		//	Запрос.УстановитьПараметр("Контрагент",            Контрагент);
		//	Запрос.УстановитьПараметр("Сделка",                Ссылка);
		//	тзОплаты = Запрос.Выполнить().Выгрузить();
		//	Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
		//		Оплачено = обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,Дата,ВалютаДокумента,Дата,РежимОкругления.Окр15как20);
		//		Если СуммаПредоплаты>Оплачено Тогда
		//			дкСообщитьРезультат("   Сумма предоплаты по заказу составляет "+Формат(СуммаПредоплаты,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(ВалютаДокумента.Наименование)+".
		//			|   Оплачено "+Формат(Оплачено,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(ВалютаДокумента.Наименование)+".
		//			|   Для проведения заказа необходимо оплатить "+Формат(СуммаПредоплаты-Оплачено,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(ВалютаДокумента.Наименование)+".",,ЭтотОбъект);
		//			Отказ = Истина;
		//			дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		//			Возврат;
		//		КонецЕсли;
		//	Иначе
		//		ОплаченоУпр = тзОплаты.Итог("СуммаУпр");
		//		СуммаПредоплатыУпр = обПересчет(СуммаПредоплаты,ВалютаДокумента,Дата,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр),РежимОкругления.Окр15как20);
		//		Если СуммаПредоплатыУпр>ОплаченоУпр Тогда
		//			дкСообщитьРезультат("   Сумма предоплаты по заказу составляет "+Формат(СуммаПредоплатыУпр,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование)+".
		//			|   Оплачено "+Формат(ОплаченоУпр,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование)+".
		//			|   Для проведения заказа необходимо оплатить "+Формат(СуммаПредоплатыУпр-ОплаченоУпр,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование)+".",,ЭтотОбъект);
		//			Отказ = Истина;
		//			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		//			Возврат;
		//		КонецЕсли;
		//	КонецЕсли;
		//КонецЕсли;
		
		Если обПраво("ПроверкаМаксимальногоПревышенияКредитаКонтрагента",Права,,ЭтотОбъект) И НЕ ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита Тогда
			// получим долг
			СтруктураОтбора=Новый Структура();
			СтруктураОтбора.Вставить("Контрагент",            Контрагент);
			СтруктураОтбора.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
			//получаем текущие остатки, т.к. оплата может быть позже заказа
			тзДолги = РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма,СуммаУпр");
			Долг    = тзДолги.Итог("Сумма");
			СуммаДокументаВВалютеДоговора = обПересчет(СуммаДокумента, ВалютаДокумента, КурсДокумента, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата);
			Если (Долг+СуммаДокументаВВалютеДоговора)>ДоговорВзаиморасчетов.МаксимальныйКредит Тогда
				ВалютаВзаиморасчетов = СокрЛП(ДоговорВзаиморасчетов.ВалютаВзаиморасчетов.Наименование);
				дкСообщитьРезультат("   Сумма долга по договору составляет "+Формат(Долг,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
									|   Сумма заказа составляет "+Формат(СуммаДокументаВВалютеДоговора,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
									|   Итого "+Формат(Долг+СуммаДокументаВВалютеДоговора,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
									|   А максимальный кредит по договору "+Формат(ДоговорВзаиморасчетов.МаксимальныйКредит,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".",,ЭтотОбъект);
				//Отказ=Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
	//Проверим, а нет ли заказа на этот автомобиль по заказу документа основания
	//Сформируем таблицы документов оснований
	МассивДокументовОснований=Новый Массив;
	ТаблицаДокументовОснований=Новый ТаблицаЗначений;
	ТаблицаДокументовОснований.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказНаАвтомобиль"));
	ДокОснования=ДокументОснование;
	Пока ЗначениеЗаполнено(ДокОснования) Цикл
		Если ТипЗнч(ДокОснования)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
			МассивДокументовОснований.Добавить(ДокОснования);
			НоваяСтрока=ТаблицаДокументовОснований.Добавить();
			НоваяСтрока.Заказ=ДокОснования;
		Иначе
			Прервать;
		КонецЕсли;
		ДокОснования=ДокОснования.ДокументОснование;
	КонецЦикла; 
	//Получим остатки заказов-оснований
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	               |	ЗаказыАвтомобилейОстатки.Заказ КАК Заказ,
	               |	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0) КАК Количество,
	               |	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.РезервОстаток, 0) КАК Резерв,
	               |	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.СуммаОстаток, 0) КАК Сумма,
	               |	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр
	               |ИЗ
	               |	РегистрНакопления.ЗаказыАвтомобилей.Остатки(&НаДату, Заказ В (&Заказ)) КАК ЗаказыАвтомобилейОстатки";
	Запрос.УстановитьПараметр("Заказ",  МассивДокументовОснований);
	Запрос.УстановитьПараметр("НаДату", МоментВремени());
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыАвтомобилей");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата));
//	ЗначенияБлокировки.Вставить("Заказ", ДокументОснование);
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаДокументовОснований);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Заказ","Заказ");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И (Выборка.Количество<>0 ИЛИ Выборка.Резерв<>0) Тогда
		//Снимаем заказ документа основания
		НаборЗаписейЗаказы=Движения.ЗаказыАвтомобилей;
		НоваяЗапись = НаборЗаписейЗаказы.Добавить();
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.Автомобиль  = Выборка.Автомобиль;
		НоваяЗапись.Заказ       = Выборка.Заказ;
		НоваяЗапись.Количество  = -Выборка.Количество;
		НоваяЗапись.Резерв      = -Выборка.Резерв;
		НоваяЗапись.Сумма       = -Выборка.Сумма;
		НоваяЗапись.СуммаУпр    = -Выборка.СуммаУпр;
		НоваяЗапись.ХозОперация = ХозОперация;
		Количество = Выборка.Количество;
		//Резерв     = Выборка.Резерв;
		Движения.ЗаказыАвтомобилей.Записать();
	Иначе
		Количество = 1;
		//Резерв     = 0;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобиль ИЛИ 
		ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильИзменение Тогда
		
		// Проверим нет ли другого заказа на этот автомобиль
		Если ПроверитьЗаказыНаАвтомобиль(Автомобиль, ЭтотОбъект) Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если (НЕ Отказ) И (НЕ ХозОперация = Справочники.ХозОперации.ЗаказНаАвтомобильОтмена) Тогда
		//Теперь зарегистрируем наш заказ
		НаборЗаписейЗаказы = Движения.ЗаказыАвтомобилей;
		НоваяЗапись = НаборЗаписейЗаказы.Добавить();
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.Автомобиль  = Автомобиль;
		НоваяЗапись.Заказ       = Ссылка;
		НоваяЗапись.Количество  = Количество;
		Если Резервировать Тогда
			//ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, Ссылка);
			//Если ТаблицаДолгов.Количество()>0 Тогда
			//	СтрокаДолга = ТаблицаДолгов[0];
			//	Сообщить("Автомобиль """+СокрЛП(Автомобиль)+""" нельзя зарезервировать по заказу """+СокрЛП(Ссылка)+""". Долг по предоплате составляет: "+Формат(СтрокаДолга.Долг, "ЧЦ=15; ЧДЦ=2")+" "+СокрЛП(СтрокаДолга.Валюта)+".", СтатусСообщения.Внимание);
			//Иначе
			НоваяЗапись.Резерв  = 1;
			//КонецЕсли;
		КонецЕсли;
		
		СуммаЗаказа = СуммаВсегоНаАвтомобиль + Опции.Итог("СуммаВсего") + Товары.Итог("СуммаВсего");
		НоваяЗапись.Сумма       = Окр(обПересчет(СуммаЗаказа, ВалютаДокумента, КурсДокумента, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата), 2);
		НоваяЗапись.СуммаУпр    = Окр(обПересчет(СуммаЗаказа, ВалютаДокумента, КурсДокумента, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата), 2);
		НоваяЗапись.ХозОперация = ХозОперация;
	КонецЕсли;
	
	//// двигаем границу последовательности автомобилей
	//ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ОстаткиАвтомобилей.Количество()>0);
	//Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
	//	// двигаем границу последовательности автомобилей
	//	НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
	//	Если ДвигаемГраницу Тогда
	//		ТаблицаСкладов = Движения.ОстаткиАвтомобилей.Выгрузить();
	//		ТаблицаСкладов.Свернуть("СкладКомпании","");
	//		НаборЗаписейГраницыАвтомобили.СкладКомпании		= ТаблицаСкладов.ВыгрузитьКолонку("СкладКомпании");
	//		НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
	//		НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
	//		НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
	//	Иначе
	//		НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
	//		НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
	//		НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
	//		НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
	//	КонецЕсли;
	//	НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
	//	НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	//КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	//Если НЕ Отказ Тогда
	//	//Выведем сообщения о необходимости перепроведения будущих документов
	//	СообщитьОПерепроведении();
	//КонецЕсли;
	
	//bizteh++
	Если ПодразделениеКомпании.Код <> "ЦБ000164" Тогда //ТрейдИн
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СУММА(тт_СкидкиНаАвтомобиль.СуммаСкидки) КАК СуммаСкидки,
		|	СУММА(тт_СкидкиНаАвтомобиль.СуммаВозмещения) КАК СуммаВозмещения
		|ИЗ
		|	РегистрСведений.тт_СкидкиНаАвтомобиль КАК тт_СкидкиНаАвтомобиль
		|ГДЕ
		|	тт_СкидкиНаАвтомобиль.Объект = &Объект";
		Запрос.УстановитьПараметр("Объект", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Сообщить("Не указана суммма скидки и сумма возмещения.");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ВыборкаДетальныеЗаписи.СуммаСкидки = 0 или НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СуммаСкидки) Тогда
				Сообщить("Проведение документа не возможно. Не выбрано ни одной скидки.");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
			Если ВыборкаДетальныеЗаписи.СуммаВозмещения = 0 или НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СуммаВозмещения) Тогда
				Сообщить("Проведение документа не возможно. Не указана сумма возмещения.");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;  
	
	//bizteh(САА 23.12.2021)++
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	тт_РабочийЛистТрейдИн.Ссылка КАК Ссылка,
	|	тт_РабочийЛистТрейдИн.Состояние КАК Состояние
	|ИЗ
	|	Документ.тт_РабочийЛистТрейдИн КАК тт_РабочийЛистТрейдИн
	|ГДЕ
	|	тт_РабочийЛистТрейдИн.Автомобиль = &Автомобиль
	|	И НЕ тт_РабочийЛистТрейдИн.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекСостояние = Перечисления.БТ_ТрейдИнСостояние.ВРезерве;
		Если ВыборкаДетальныеЗаписи.Состояние <> ТекСостояние и ВыборкаДетальныеЗаписи.Состояние <> Перечисления.БТ_ТрейдИнСостояние.Продан Тогда
			отт_РабочийЛистТрейдИн = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			отт_РабочийЛистТрейдИн.Состояние = ТекСостояние; 
			отт_РабочийЛистТрейдИн.Записать(РежимЗаписиДокумента.Запись);
			
			НоваяЗапись = РегистрыСведений.БТ_РабочийЛистТрейдИнИсторияСостояний.СоздатьМенеджерЗаписи();
			НоваяЗапись.тт_РабочийЛистТрейдИн = ВыборкаДетальныеЗаписи.Ссылка;
			НоваяЗапись.ДатаИзменения = ТекущаяДата();
			НоваяЗапись.Состояние = ТекСостояние;
			НоваяЗапись.Пользователь = ПараметрыСеанса.Пользователь;
			НоваяЗапись.Записать();	
			
		КонецЕсли;
	КонецЦикла; 
	//bizteh(САА 23.12.2021)--
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОбработкаРасчетСкидок=Обработки.РасчетСкидок.Создать();
//ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки=Истина;

ОбработкаРасчетСкидокНаАвтомобиль=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидокНаАвтомобиль.ИмяРеквизитаСкидкаНаценка="СкидкаНаценкаНаАвтомобиль";
ОбработкаРасчетСкидокНаАвтомобиль.ИмяРеквизитаЗначениеСкидкиНаценки="ЗначениеСкидкиНаценкиНаАвтомобиль";
ОбработкаРасчетСкидокНаАвтомобиль.ИмяРеквизитаСуммаСкидкиНаценки="СуммаСкидкиНаценкиНаАвтомобиль";
ОбработкаРасчетСкидокНаАвтомобиль.НеРассчитыватьСтрочныеСкидки=Истина;
ОбработкаРасчетСкидокНаАвтомобиль.ИмяТабличнойЧасти="Опции";
ОбработкаРасчетСкидокНаАвтомобиль.НеРассчитыватьАвтоматическиеСкидки=Истина;

НеЗаполнятьКонтрагента	= Ложь;

КешСебестоимостиАвтомобилейКупленныхУФизЛица = Неопределено;

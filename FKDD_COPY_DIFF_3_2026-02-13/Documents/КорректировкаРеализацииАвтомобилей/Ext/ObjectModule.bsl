// Модуль документа Корректировка реализации автомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;                            // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;               // Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ТекущаяВалютаДокумента Экспорт;           // Текущая валюта документа перед изменением
Перем ТекущийКурсДокумента Экспорт;             //Текущий курс документа перед изменением

Перем ИмяРеквизитаКода Экспорт;                 // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Функция ПолучитьСписокОснований() Экспорт
	
	Основания = Новый Массив;
	ТекОснование = ДокументОснование;
	
	Если ТекОснование = Неопределено Тогда
		Возврат Основания;
	КонецЕсли;
	
	Пока ТипЗнч(ТекОснование) <> Тип("ДокументСсылка.РеализацияАвтомобилей") Цикл
		Основания.Добавить(ТекОснование);
		ТекОснование = ТекОснование.ДокументОснование;
	КонецЦикла;
	
	Основания.Добавить(ТекОснование);
	
	Возврат Основания;
	
КонецФункции

Функция ПолучитьСделку(СписокОснований = Неопределено) Экспорт
	
	Если СписокОснований = Неопределено Тогда
		СписокОснований = ПолучитьСписокОснований();
	КонецЕсли;
	
	Для Каждого Основание Из СписокОснований Цикл
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
			Возврат Основание;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Документы.РеализацияАвтомобилей.ПустаяСсылка();
	
КонецФункции

Функция ПолучитьКомиссионныеАвтомобили()
	
	// получим коммисионные автомобили
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОстаткиАвтомобилей.Автомобиль
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &Регистратор
	|	И ОстаткиАвтомобилей.СтатусПартии = &СтатусПартии";
	Запрос.УстановитьПараметр("Регистратор"  , Сделка);
	Запрос.УстановитьПараметр("СтатусПартии" , Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница КАК СуммаНДСРазница,
	|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница КАК СуммаВсегоРазница,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеализованныеАвтомобилиОстатки.КоличествоОстаток, 0) = 0
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОстаток,
	|	КорректировкаРеализацииАвтомобилейАвтомобили.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеАвтомобили.Остатки(&МоментВремени, Автомобиль В (&Автомобили)) КАК РеализованныеАвтомобилиОстатки
	|		ПО КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль = РеализованныеАвтомобилиОстатки.Автомобиль
	|ГДЕ
	|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница <> 0
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница <> 0
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль В(&Автомобили)
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница = 0";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Автомобили", РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьКомиссионныеАвтомобили()

// Возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку.
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.Проект КАК Проект,
	|	Док.Организация КАК Организация,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СкладКомпании КАК СкладКомпании,
	|	&СуммаВсегоРазница КАК СуммаДокумента,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.Сделка КАК Сделка
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("СуммаВсегоРазница",Автомобили.Итог("СуммаВсегоРазница"));
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// Обработчик события заполнения объекта как при создании нового, так и при вводе на основании существующего.
//
// Параметры:
//  ДанныеЗаполнения     - Произвольный - Значение, которое используется как основание для заполнения.
//  ТекстЗаполнения      - Строка       - Текст, используемый для заполнения объекта.
//  СтандартнаяОбработка - Булево       - В данный параметр передается признак выполнения системной обработки события.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения = "", СтандартнаяОбработка = Истина) Экспорт

	Если Не ВозможенВВодНаОсновании(ДанныеЗаполнения) Тогда
		ДополнительныеСвойства.Вставить("ВводНаОснованииЗапрещен", Истина);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		Если НЕ ДанныеЗаполнения.Проведен Тогда
			Сообщить(Нстр("ru = 'Корректировка реализации автомобилей вводится только на основании проведенного документа.'"));
			ДополнительныеСвойства.Вставить("ВводНаОснованииЗапрещен", Истина);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		Контрагент = Неопределено;
		ДоговорВзаиморасчетов = Неопределено;
	КонецЕсли;
	
	// Вызываем общий обработчик события
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Сообщить(НСтр("ru = 'Ввод корректировки реализации автомобилей возможен только на основании.'"));
		Возврат;
	КонецЕсли;
	
	Сделка = ПолучитьСделку();
	
	Основания = ПолучитьСписокОснований();
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРег = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// заполняем суммы автомобилей
	Если Сделка.ХозОперация <> Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
		ТаблицаСумм = ПолучитьСуммуПродажиАвтомобиля(Автомобили.ВыгрузитьКолонку("Автомобиль"), Основания).Строки;
	Иначе
		// заполним суммами передачи
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистраторы" , Основания);
		Запрос.УстановитьПараметр("Автомобили"   , Автомобили.ВыгрузитьКолонку("Автомобиль"));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	АвтомобилиОтданные.Автомобиль,
		|	СУММА(АвтомобилиОтданные.Сумма) КАК Сумма,
		|	СУММА(АвтомобилиОтданные.СуммаУпр) КАК СуммаУпр,
		|	СУММА(АвтомобилиОтданные.СуммаНДС) КАК СуммаНДС
		|ИЗ
		|	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
		|ГДЕ
		|	АвтомобилиОтданные.Регистратор В(&Регистраторы)
		|	И АвтомобилиОтданные.Автомобиль В(&Автомобили)
		|
		|СГРУППИРОВАТЬ ПО
		|	АвтомобилиОтданные.Автомобиль";
		
		ТаблицаСумм = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	// заполняем суммы автомобилей
	Для Каждого Автомобиль Из Автомобили Цикл
		
		Автомобиль.VIN = Автомобиль.Автомобиль.VIN;
		
		// получим сумму для авто
		Суммы = ТаблицаСумм.Найти(Автомобиль.Автомобиль, "Автомобиль");
		Если Суммы = Неопределено Тогда
			Суммы = Новый Структура("СуммаУпр,СуммаНДС,Сумма", 0,0,0);
		КонецЕсли;
		
		Если ВалютаРег = ВалютаДокумента Тогда
			Автомобиль.СуммаВсего = Суммы.Сумма;
		Иначе
			Автомобиль.СуммаВсего = обПересчет(Суммы.СуммаУпр, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		ОбработкаРеквизита("Автомобили.СуммаВсего", Автомобиль);
		
		Автомобиль.СуммаНДС = обПересчет(Суммы.СуммаНДС, ВалютаРег, Дата, ВалютаДокумента, КурсДокумента);
		
		// заполним доп реквизиты
		Автомобиль.СуммаВсегоПоДокументуРеализации = Автомобиль.СуммаВсего;
		Автомобиль.СуммаПоДокументуРеализации      = Автомобиль.Сумма;
		Автомобиль.СуммаНДСПоДокументуРеализации   = Автомобиль.СуммаНДС;
		Автомобиль.СтавкаНДСПоДокументуРеализации  = Автомобиль.СтавкаНДС;
		Автомобиль.КоличествоПоДокументуРеализации = Автомобиль.Количество;
		Автомобиль.ИзДокументаОснования            = Истина;
		
		ОбработкаРеквизита("РассчитатьРазницу", Автомобиль);
	КонецЦикла;
	
	// Подсчет новой суммы документа назначения
	СуммаДокумента = Автомобили.Итог("СуммаВсего");
	
	ИдентификаторГосударственногоКонтракта = ?(ЗначениеЗаполнено(ИдентификаторГосударственногоКонтракта),
		ИдентификаторГосударственногоКонтракта,
		ДоговорВзаиморасчетов.ИдентификаторГосударственногоКонтракта);
	
	Если
		ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.КорректировкаРеализацииАвтомобилейИсправлениеВПервичныхДокументах")
	Тогда
		НомерИсправления = 1;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Функция ВозможенВВодНаОсновании(ДанныеЗаполнения)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения)
		И (ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияАвтомобилей")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КорректировкаРеализацииАвтомобилей")) Тогда
		
		Если ДанныеЗаполнения = Ссылка Тогда
			Сообщить("Ввод корректировки реализации автомобилей на основании самой себя запрещен.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если НЕ ДанныеЗаполнения.Проведен Тогда
			Сообщить("Ввод корректировки реализации автомобилей возможен только на основании проведенного документа.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		ХозОперацияОснования = ДанныеЗаполнения.ХозОперация;
		Если ХозОперацияОснования = Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
			Сообщить("Ввод корректировки реализации автомобилей не возможен на основании реализации автомобилей комиссия.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если Документы.КорректировкаРеализацииАвтомобилей.КорректировкаНеДоступна(ДанныеЗаполнения) Тогда
			Сообщить("На основании """ + ДанныеЗаполнения + """ введен возврат автомобилей. Ввод корректировки реализации автомобилей невозможен.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ВыполнитьДвиженияПоВозвратуАвтомобилей(РежимПроведения, ДокументСсылка, СписокОснований, Отказ)
	
	РеализацияАвтомобилейКомиссия = (Сделка.ХозОперация = Справочники.ХозОперации.РеализацияАвтомобилейКомиссия);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль
	               |ПОМЕСТИТЬ СписокАвтомобилейКВозврату
	               |ИЗ
	               |	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
	               |ГДЕ
	               |	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
	               |	И КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиАвтомобилей.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПродажиАвтомобилей.Автомобиль КАК Автомобиль,
	               |	ПродажиАвтомобилей.ДокументПродажи КАК ДокументПродажи,
	               |	ПродажиАвтомобилей.Поставщик КАК Поставщик,
	               |	ПродажиАвтомобилей.Покупатель КАК Покупатель,
	               |	ПродажиАвтомобилей.СтатусПартии КАК СтатусПартии,
	               |	ПродажиАвтомобилей.ХозОперация КАК ХозОперация,
	               |	ПродажиАвтомобилей.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПродажиАвтомобилей.СкладКомпании КАК СкладКомпании,
	               |	ПродажиАвтомобилей.СтавкаНДС КАК СтавкаНДС,
	               |	ПродажиАвтомобилей.Партия КАК Партия,
	               |	ПродажиАвтомобилей.ГТД КАК ГТД,
	               |	СУММА(-ПродажиАвтомобилей.Количество) КАК Количество,
	               |	СУММА(-ПродажиАвтомобилей.Сумма) КАК Сумма,
	               |	СУММА(-ПродажиАвтомобилей.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(-ПродажиАвтомобилей.СуммаСкидки) КАК СуммаСкидки,
	               |	СУММА(-ПродажиАвтомобилей.СуммаУпр) КАК СуммаУпр,
	               |	СУММА(-ПродажиАвтомобилей.Себестоимость) КАК Себестоимость,
	               |	СУММА(-ПродажиАвтомобилей.СуммаНДСВходящий) КАК СуммаНДСВходящий,
	               |	СУММА(-ПродажиАвтомобилей.СебестоимостьУпр) КАК СебестоимостьУпр
	               |ИЗ
	               |	РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
	               |ГДЕ
	               |	ПродажиАвтомобилей.Регистратор В(&СписокОснований)
	               |	И ПродажиАвтомобилей.Автомобиль В
	               |			(ВЫБРАТЬ
	               |				СписокАвтомобилейКВозврату.Автомобиль КАК Автомобиль
	               |			ИЗ
	               |				СписокАвтомобилейКВозврату КАК СписокАвтомобилейКВозврату)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиАвтомобилей.СтавкаНДС,
	               |	ПродажиАвтомобилей.СкладКомпании,
	               |	ПродажиАвтомобилей.Партия,
	               |	ПродажиАвтомобилей.ГТД,
	               |	ПродажиАвтомобилей.СтатусПартии,
	               |	ПродажиАвтомобилей.ХозОперация,
	               |	ПродажиАвтомобилей.Автомобиль,
	               |	ПродажиАвтомобилей.ДокументПродажи,
	               |	ПродажиАвтомобилей.Покупатель,
	               |	ПродажиАвтомобилей.Поставщик,
	               |	ПродажиАвтомобилей.ПодразделениеКомпании,
	               |	ПродажиАвтомобилей.ДоговорВзаиморасчетов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
	               |	ОстаткиАвтомобилей.СкладКомпании КАК СкладКомпании,
	               |	ОстаткиАвтомобилей.СтатусПартии КАК СтатусПартии,
	               |	ОстаткиАвтомобилей.Партия КАК Партия,
	               |	ОстаткиАвтомобилей.ГТД КАК ГТД,
	               |	СУММА(-ОстаткиАвтомобилей.Количество) КАК Количество,
	               |	СУММА(-ОстаткиАвтомобилей.Сумма) КАК Сумма,
	               |	СУММА(-ОстаткиАвтомобилей.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(-ОстаткиАвтомобилей.СуммаУпр) КАК СуммаУпр
	               |ИЗ
	               |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	               |ГДЕ
	               |	ОстаткиАвтомобилей.Регистратор = &Регистратор
	               |	И ОстаткиАвтомобилей.Автомобиль В
	               |			(ВЫБРАТЬ
	               |				СписокАвтомобилейКВозврату.Автомобиль КАК Автомобиль
	               |			ИЗ
	               |				СписокАвтомобилейКВозврату КАК СписокАвтомобилейКВозврату)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстаткиАвтомобилей.СкладКомпании,
	               |	ОстаткиАвтомобилей.СтатусПартии,
	               |	ОстаткиАвтомобилей.Партия,
	               |	ОстаткиАвтомобилей.ГТД,
	               |	ОстаткиАвтомобилей.Автомобиль
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
	               |	КомплектацияАвтомобилей.СкладКомпании КАК СкладКомпании,
	               |	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
	               |	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	КомплектацияАвтомобилей.СтатусПартии КАК СтатусПартии,
	               |	КомплектацияАвтомобилей.ГТД КАК ГТД,
	               |	КомплектацияАвтомобилей.Партия КАК Партия,
	               |	СУММА(-КомплектацияАвтомобилей.Количество) КАК Количество,
	               |	СУММА(-КомплектацияАвтомобилей.Сумма) КАК Сумма,
	               |	СУММА(-КомплектацияАвтомобилей.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(-КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр,
	               |	СУММА(-КомплектацияАвтомобилей.СуммаПродажи) КАК СуммаПродажи,
	               |	СУММА(-КомплектацияАвтомобилей.СуммаПродажиУпр) КАК СуммаПродажиУпр
	               |ИЗ
	               |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	               |ГДЕ
	               |	КомплектацияАвтомобилей.Регистратор = &Регистратор
	               |	И КомплектацияАвтомобилей.Автомобиль В
	               |			(ВЫБРАТЬ
	               |				СписокАвтомобилейКВозврату.Автомобиль КАК Автомобиль
	               |			ИЗ
	               |				СписокАвтомобилейКВозврату КАК СписокАвтомобилейКВозврату)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	КомплектацияАвтомобилей.СкладКомпании,
	               |	КомплектацияАвтомобилей.Номенклатура,
	               |	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
	               |	КомплектацияАвтомобилей.Партия,
	               |	КомплектацияАвтомобилей.Автомобиль,
	               |	КомплектацияАвтомобилей.СтатусПартии,
	               |	КомплектацияАвтомобилей.ГТД
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокАвтомобилейКВозврату.Автомобиль КАК Автомобиль,
	               |	РеализацияАвтомобилейАвтомобили.СуммаВсего КАК СуммаВсегоАвтомобиль,
	               |	РеализацияАвтомобилейАвтомобили.ИдентификаторАвтомобиля КАК ИдентификаторАвтомобиля
	               |ПОМЕСТИТЬ СписокАвтомобилей
	               |ИЗ
	               |	СписокАвтомобилейКВозврату КАК СписокАвтомобилейКВозврату
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
	               |		ПО СписокАвтомобилейКВозврату.Автомобиль = РеализацияАвтомобилейАвтомобили.Автомобиль
	               |			И (РеализацияАвтомобилейАвтомобили.Ссылка = &Регистратор)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(СписокАвтомобилей.СуммаВсегоАвтомобиль) КАК СуммаВсегоАвтомобиль,
	               |	СУММА(ЕСТЬNULL(РеализацияАвтомобилейТовары.СуммаВсего, 0)) КАК СуммаВсегоКомплектации
	               |ИЗ
	               |	СписокАвтомобилей КАК СписокАвтомобилей
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Товары КАК РеализацияАвтомобилейТовары
	               |		ПО СписокАвтомобилей.ИдентификаторАвтомобиля = РеализацияАвтомобилейТовары.ИдентификаторАвтомобиля
	               |			И (РеализацияАвтомобилейТовары.Ссылка = &Регистратор)";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Регистратор", Сделка);
	Запрос.УстановитьПараметр("СписокОснований", СписокОснований);
	
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	Если ПакетЗапроса[5].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	
	// Регистр ПродажиАвтомобилей
	ПродажиАвтомобилей = ПакетЗапроса[1].Выбрать();
	
	Пока ПродажиАвтомобилей.Следующий() Цикл
		НоваяЗапись = Движения.ПродажиАвтомобилей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ПродажиАвтомобилей);
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ХозОперация = ХозОперация;
	КонецЦикла;
	
	// Регистр ОстаткиАвтомобилей
	ОстаткиАвтомобилей = ПакетЗапроса[2].Выбрать();
	
	Пока ОстаткиАвтомобилей.Следующий() Цикл
		НоваяЗапись = Движения.ОстаткиАвтомобилей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ОстаткиАвтомобилей);
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ХозОперация = ХозОперация;
	КонецЦикла;
	
	Движения.ОстаткиАвтомобилей.Записать();
	
	Если НЕ РеализацияАвтомобилейКомиссия Тогда
		// Если среди возвращаемых автомобилей есть комиссионные, то отсторнируем реализованные автомобили.
		НаборЗаписейРеализованныеАвтомобили=Движения.РеализованныеАвтомобили;
		НаборЗаписейРеализованныеАвтомобили.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеАвтомобили.Сторно=Истина;
		Отказ=НЕ НаборЗаписейРеализованныеАвтомобили.Приход() ИЛИ Отказ;
	Иначе
		// автомобили возвращает комиссионер. отсторнируем передачу на комиссию
		НаборЗаписейАвтомобилиОтданные=Движения.АвтомобилиОтданные;
		НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейАвтомобилиОтданные.Сторно=Истина;
		НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
		НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		
		ЗапросПоАвтомобилям = Новый Запрос;
		ЗапросПоАвтомобилям.Текст = "ВЫБРАТЬ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		|	&ДокументРеализацияАвтомобилей КАК ДокументПередачи,
		|	СУММА(КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСПоДокументуРеализации) КАК СуммаНДС,
		|	СУММА(КорректировкаРеализацииАвтомобилейАвтомобили.СуммаПоДокументуРеализации) КАК Сумма,
		|	СУММА(КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоПоДокументуРеализации) КАК СуммаВсего,
		|	СУММА(1) КАК Количество
		|ИЗ
		|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница = -1
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль";
		ЗапросПоАвтомобилям.УстановитьПараметр("Ссылка", Ссылка);
		ЗапросПоАвтомобилям.УстановитьПараметр("ДокументРеализацияАвтомобилей", Сделка);
		
		НаборЗаписейАвтомобилиОтданные.РезультатЗапросаПоАвтомобилям = ЗапросПоАвтомобилям.Выполнить();
		Отказ=НЕ НаборЗаписейАвтомобилиОтданные.Приход() ИЛИ отказ;
	КонецЕсли;
	
	СписокАвтомобилей =  ПакетЗапроса[5].Выбрать();
	СписокАвтомобилей.Следующий();
	СуммаАвтомобилей  = СписокАвтомобилей.СуммаВсегоАвтомобиль;
	СуммаДопОборудования = СписокАвтомобилей.СуммаВсегоКомплектации;
	
	
	// Регистр КомплектацияАвтомобилей
	КомплектацияАвтомобилей = ПакетЗапроса[3].Выбрать();
	
	Пока КомплектацияАвтомобилей.Следующий() Цикл
		НоваяЗапись = Движения.КомплектацияАвтомобилей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, КомплектацияАвтомобилей);
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ХозОперация = ХозОперация;
	КонецЦикла;
	
	Движения.КомплектацияАвтомобилей.Записать();
	
	Если НЕ РеализацияАвтомобилейКомиссия Тогда
		// Если среди возвращенного товара есть комиссионный, то отсторнируем реализованные товары.
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	КомплектацияАвтомобилей.Автомобиль,
		|	КомплектацияАвтомобилей.СкладКомпании,
		|	КомплектацияАвтомобилей.Номенклатура,
		|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
		|	КомплектацияАвтомобилей.СтатусПартии,
		|	КомплектацияАвтомобилей.Партия,
		|	КомплектацияАвтомобилей.ГТД,
		|	КомплектацияАвтомобилей.Количество,
		|	КомплектацияАвтомобилей.Сумма,
		|	КомплектацияАвтомобилей.СуммаНДС,
		|	КомплектацияАвтомобилей.СуммаУпр,
		|	КомплектацияАвтомобилей.СуммаПродажи,
		|	КомплектацияАвтомобилей.СуммаПродажиУпр,
		|	КомплектацияАвтомобилей.ХозОперация,
		|	КомплектацияАвтомобилей.СтавкаНДС
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор
		|	И КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура";
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Сторно=Истина;
		НаборЗаписейРеализованныеТовары.ЕстьАвтомобиль = Истина;
		НаборЗаписейРеализованныеТовары.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоПартиям=Запрос.Выполнить().Выгрузить();
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоГТД=НаборЗаписейРеализованныеТовары.РезультатЗапросаПоПартиям.Скопировать();
		НаборЗаписейРеализованныеТовары.ШапкаДокумента=ЭтотОбъект;
		Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
		// продажи
		Если НЕ Отказ Тогда
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	КомплектацияАвтомобилей.ВидДвижения,
			|	КомплектацияАвтомобилей.Автомобиль,
			|	КомплектацияАвтомобилей.Номенклатура,
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
			|	КомплектацияАвтомобилей.СтатусПартии,
			|	КомплектацияАвтомобилей.Партия,
			|	КомплектацияАвтомобилей.ГТД,
			|	КомплектацияАвтомобилей.СтавкаНДС,
			|	КомплектацияАвтомобилей.Количество,
			|	КомплектацияАвтомобилей.Сумма,
			|	КомплектацияАвтомобилей.СуммаНДС,
			|	КомплектацияАвтомобилей.СуммаУпр
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Регистратор = &Регистратор
			|	И КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура";
			Запрос.УстановитьПараметр("Регистратор",Ссылка);
			НаборЗаписейПродажи = Движения.Продажи;
			НаборЗаписейПродажи.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейПродажи.РезультатЗапросаПоПартиям = Запрос.Выполнить().Выгрузить();
			НаборЗаписейПродажи.СкладКомпании             = СкладКомпании;
			НаборЗаписейПродажи.Сторно                    = Истина;
			НаборЗаписейПродажи.ПоБазовомуКоличеству      = Ложь;
			НаборЗаписейПродажи.Покупатель                = Контрагент;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов     = ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании     = ПодразделениеКомпании;
			НаборЗаписейПродажи.Комиссия                  = Ложь;
			НаборЗаписейПродажи.ШапкаДокумента            = ЭтотОбъект;
			НаборЗаписейПродажи.ЕстьАвтомобиль            = Истина;
			Запрос = Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль
			|ПОМЕСТИТЬ СписокАвтомобилей
			|ИЗ
			|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
			|ГДЕ
			|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
			|	И КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РеализацияАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
			|	РеализацияАвтомобилейТовары.Номенклатура КАК Номенклатура,
			|	РеализацияАвтомобилейТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	СУММА(РеализацияАвтомобилейТовары.СуммаНДС) КАК СуммаНДС,
			|	СУММА(РеализацияАвтомобилейТовары.СуммаВсего) КАК СуммаВсего,
			|	СУММА(РеализацияАвтомобилейТовары.Количество * РеализацияАвтомобилейТовары.Коэффициент) КАК Количество,
			|	СУММА(РеализацияАвтомобилейТовары.Сумма) КАК Сумма,
			|	РеализацияАвтомобилейТовары.Номенклатура.ВидНоменклатуры КАК НоменклатураВидНоменклатуры,
			|	СУММА(РеализацияАвтомобилейТовары.СуммаСкидки) КАК СуммаСкидки,
			|	РеализацияАвтомобилейТовары.СтавкаНДС КАК СтавкаНДС
			|ИЗ
			|	Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Товары КАК РеализацияАвтомобилейТовары
			|		ПО РеализацияАвтомобилейАвтомобили.Ссылка = РеализацияАвтомобилейТовары.Ссылка
			|			И РеализацияАвтомобилейАвтомобили.ИдентификаторАвтомобиля = РеализацияАвтомобилейТовары.ИдентификаторАвтомобиля
			|			И (РеализацияАвтомобилейАвтомобили.Ссылка = &РеализацияАвтомобилей)
			|ГДЕ
			|	РеализацияАвтомобилейАвтомобили.Автомобиль В
			|			(ВЫБРАТЬ
			|				СписокАвтомобилей.Автомобиль КАК Автомобиль
			|			ИЗ
			|				СписокАвтомобилей КАК СписокАвтомобилей)
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализацияАвтомобилейТовары.ХарактеристикаНоменклатуры,
			|	РеализацияАвтомобилейАвтомобили.Автомобиль,
			|	РеализацияАвтомобилейТовары.Номенклатура,
			|	РеализацияАвтомобилейТовары.СтавкаНДС,
			|	РеализацияАвтомобилейТовары.Номенклатура.ВидНоменклатуры";
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("РеализацияАвтомобилей", Сделка);
			НаборЗаписейПродажи.РезультатЗапросаПоТоварам = Запрос.Выполнить().Выгрузить();
			Запрос=Новый Запрос("
			|ВЫБРАТЬ
			|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
			|	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	КомплектацияАвтомобилей.Партия КАК Партия,
			|	КомплектацияАвтомобилей.ГТД КАК ГТД,
			|	СУММА(КомплектацияАвтомобилей.Сумма) КАК Сумма,
			|	СУММА(КомплектацияАвтомобилей.СуммаНДС) КАК СуммаНДС,
			|	СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Регистратор = &РеализацияТоваров И 
			|	КомплектацияАвтомобилей.Автомобиль                 В (&Автомобили) И 
			|	КомплектацияАвтомобилей.Партия                     В (&Партии) И 
			|	КомплектацияАвтомобилей.СтатусПартии               = &СтатусПартии И 
			|	КомплектацияАвтомобилей.Номенклатура               В (&Номенклатура) И 
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры В (&ХарактеристикиНоменклатуры) И 
			|	КомплектацияАвтомобилей.ВидДвижения = &ВидДвижения
			|СГРУППИРОВАТЬ ПО
			|	КомплектацияАвтомобилей.Автомобиль,
			|	КомплектацияАвтомобилей.Номенклатура,
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
			|	КомплектацияАвтомобилей.Партия,
			|	КомплектацияАвтомобилей.ГТД,
			|	КомплектацияАвтомобилей.Регистратор");
			Запрос.УстановитьПараметр("ВидДвижения",                ВидДвиженияНакопления.Расход);
			Запрос.УстановитьПараметр("СтатусПартии",               Перечисления.СтатусыПартий.ТоварКупленный);
			Запрос.УстановитьПараметр("РеализацияТоваров",          Сделка);
			Запрос.УстановитьПараметр("Ссылка",                     Ссылка);
			Запрос.УстановитьПараметр("Автомобили",                 НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("Автомобиль"));
			Запрос.УстановитьПараметр("Партии",                     НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("Партия"));
			Запрос.УстановитьПараметр("Номенклатура",               НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("Номенклатура"));
			Запрос.УстановитьПараметр("ХарактеристикиНоменклатуры", НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
			НаборЗаписейПродажи.КэшСебестоимостиПриКомиссии = Запрос.Выполнить().Выгрузить();
			НаборЗаписейПродажи.ДокументПродажи = Сделка;
			Отказ = НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли;
	Иначе
		// товар возвращает комиссионер. отсторнируем передачу на комиссию оборудования
		НаборЗаписейПартииОтданные = Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейПартииОтданные.Сторно                = Истина;
		НаборЗаписейПартииОтданные.Контрагент            = Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.ШапкаДокумента        = ШапкаДокумента;
		НаборЗаписейПартииОтданные.ПоБазовомуКоличеству  = Ложь;
		НаборЗаписейПартииОтданные.ЕстьАвтомобиль        = Истина;
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ СписокАвтомобилей
		|ИЗ
		|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		|	РеализацияАвтомобилейТовары.Номенклатура КАК Номенклатура,
		|	РеализацияАвтомобилейТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	РеализацияАвтомобилейТовары.Ссылка КАК ДокументПередачи,
		|	РеализацияАвтомобилейТовары.СуммаНДС КАК СуммаНДС,
		|	РеализацияАвтомобилейТовары.СуммаВсего КАК СуммаВсего,
		|	РеализацияАвтомобилейТовары.Количество * РеализацияАвтомобилейТовары.Коэффициент КАК Количество,
		|	РеализацияАвтомобилейАвтомобили.Сумма КАК Сумма
		|ИЗ
		|	Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Товары КАК РеализацияАвтомобилейТовары
		|		ПО РеализацияАвтомобилейАвтомобили.Ссылка = РеализацияАвтомобилейТовары.Ссылка
		|			И РеализацияАвтомобилейАвтомобили.ИдентификаторАвтомобиля = РеализацияАвтомобилейТовары.ИдентификаторАвтомобиля
		|			И (РеализацияАвтомобилейАвтомобили.Ссылка = &РеализацияАвтомобилей)
		|ГДЕ
		|	РеализацияАвтомобилейАвтомобили.Автомобиль В
		|			(ВЫБРАТЬ
		|				СписокАвтомобилей.Автомобиль КАК Автомобиль
		|			ИЗ
		|				СписокАвтомобилей КАК СписокАвтомобилей)";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("РеализацияАвтомобилей", Сделка);
		НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам=Запрос.Выполнить();
		Отказ=НЕ НаборЗаписейПартииОтданные.Приход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейПартииОтданные.Записать();
		КонецЕсли; 
		// Товар возвращает комиссионер. отсторнируем передачу на комиссию опций, установленных производителем.
		Запрос=Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ ТаблицаАвтомобилей
		|ИЗ
		|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваровОтданные.Автомобиль КАК Автомобиль,
		|	ПартииТоваровОтданные.Контрагент КАК Контрагент,
		|	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданные.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ПартииТоваровОтданные.ДокументПередачи КАК ДокументПередачи,
		|	ПартииТоваровОтданные.Партия КАК Партия,
		|	ПартииТоваровОтданные.ГТД КАК ГТД,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваровОтданные.Количество
		|			ИНАЧЕ -ПартииТоваровОтданные.Количество
		|		КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваровОтданные.Сумма
		|			ИНАЧЕ -ПартииТоваровОтданные.Сумма
		|		КОНЕЦ) КАК Сумма,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваровОтданные.СуммаУпр
		|			ИНАЧЕ -ПартииТоваровОтданные.СуммаУпр
		|		КОНЕЦ) КАК СуммаУпр,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваровОтданные.СуммаНДС
		|			ИНАЧЕ -ПартииТоваровОтданные.СуммаНДС
		|		КОНЕЦ) КАК СуммаНДС,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваровОтданные.СуммаСебестоимостиУпр
		|			ИНАЧЕ -ПартииТоваровОтданные.СуммаСебестоимостиУпр
		|		КОНЕЦ) КАК СуммаСебестоимостиУпр,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваровОтданные.СуммаСебестоимостиРегл
		|			ИНАЧЕ -ПартииТоваровОтданные.СуммаСебестоимостиРегл
		|		КОНЕЦ) КАК СуммаСебестоимостиРегл
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
		|ГДЕ
		|	ПартииТоваровОтданные.Период <= &НаДату
		|	И ПартииТоваровОтданные.Номенклатура = &ОпцияПроизводителя
		|	И ПартииТоваровОтданные.Регистратор = &РеализацияАвтомобилей
		|	И ПартииТоваровОтданные.Автомобиль В
		|			(ВЫБРАТЬ
		|				ТаблицаАвтомобилей.Автомобиль
		|			ИЗ
		|				ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровОтданные.Автомобиль,
		|	ПартииТоваровОтданные.Контрагент,
		|	ПартииТоваровОтданные.Номенклатура,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданные.ДоговорВзаиморасчетов,
		|	ПартииТоваровОтданные.ДокументПередачи,
		|	ПартииТоваровОтданные.Партия,
		|	ПартииТоваровОтданные.ГТД";
		Запрос.УстановитьПараметр("НаДату", МоментВремени().Дата);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ОпцияПроизводителя",Справочники.Номенклатура.ОпцияПроизводителя);
		Запрос.УстановитьПараметр("РеализацияАвтомобилей", Сделка);
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
			НоваяЗапись=НаборЗаписейПартииОтданные.Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=Дата;
			НоваяЗапись.Регистратор=Ссылка;
			НоваяЗапись.Автомобиль=Выборка.Автомобиль;
			НоваяЗапись.Контрагент=Выборка.Контрагент;
			НоваяЗапись.Номенклатура=Выборка.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры=Выборка.ХарактеристикаНоменклатуры;
			НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.ДокументПередачи=Выборка.ДокументПередачи;
			НоваяЗапись.Партия=Выборка.Партия;
			НоваяЗапись.ГТД=Выборка.ГТД;
			НоваяЗапись.Количество=-Выборка.Количество;
			НоваяЗапись.Сумма=-Выборка.Сумма;
			НоваяЗапись.СуммаУпр=-Выборка.СуммаУпр;
			НоваяЗапись.СуммаНДС=-Выборка.СуммаНДС;
			НоваяЗапись.СуммаСебестоимостиУпр=-Выборка.СуммаСебестоимостиУпр;
			НоваяЗапись.СуммаСебестоимостиРегл=-Выборка.СуммаСебестоимостиРегл;
			НоваяЗапись.ХозОперация=ХозОперация;
		КонецЦикла;
	КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// доходы и расходы
	// получим себестоимость
	Если НЕ РеализацияАвтомобилейКомиссия Тогда
		СебестоимостьАвтомобилейУпр  = 0;
		СебестоимостьОборудованияУпр = 0;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		|ГДЕ
		|	ОстаткиАвтомобилей.Регистратор = &Регистратор
		|	И (НЕ ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварПринятыйКомиссия))";
		Запрос.УстановитьПараметр("Регистратор",Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СебестоимостьАвтомобилейУпр = -Выборка.СуммаУпр;
		КонецЕсли; 
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор
		|	И (НЕ КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварПринятыйКомиссия))";
		Запрос.УстановитьПараметр("Регистратор",Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СебестоимостьОборудованияУпр = - Выборка.СуммаУпр;
		КонецЕсли; 
		// Итоговая Себестоимость автомобилей
		Если СебестоимостьАвтомобилейУпр <> 0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
			НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьАвтомобилейУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		
		Если СебестоимостьОборудованияУпр <> 0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьОборудованияУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		// Итог СуммаВсего
		Если СуммаАвтомобилей<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоАвтомобилям;
			НаборЗаписейДоходыИРасходы.Доход                  = СуммаАвтомобилей;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		Если СуммаДопОборудования<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
			НаборЗаписейДоходыИРасходы.Доход                  = СуммаДопОборудования;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		
		// доходы и расходы
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.Записать();
		НаборЗаписейРеализованныеАвтомобили.Записать();
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль
		             |ПОМЕСТИТЬ СписокАвтомобилей
		             |ИЗ
		             |	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		             |ГДЕ
		             |	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Регистратор
		             |	И КорректировкаРеализацииАвтомобилейАвтомобили.Количество = 0
		             |;
		             |
		             |////////////////////////////////////////////////////////////////////////////////
		             |ВЫБРАТЬ
		             |	ОбъединенныйЗапрос.Подразделение КАК Подразделение,
		             |	-СУММА(ОбъединенныйЗапрос.СуммаАвтомобилейУпр) КАК СуммаАвтомобилейУпр,
		             |	-СУММА(ОбъединенныйЗапрос.СуммаОборудованияУпр) КАК СуммаОборудованияУпр
		             |ИЗ
		             |	(ВЫБРАТЬ
		             |		РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
		             |		ЕСТЬNULL(РеализованныеАвтомобили.СуммаУпр, 0) КАК СуммаАвтомобилейУпр,
		             |		0 КАК СуммаОборудованияУпр
		             |	ИЗ
		             |		РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
		             |	ГДЕ
		             |		РеализованныеАвтомобили.Регистратор = &Регистратор
		             |		И РеализованныеАвтомобили.Автомобиль В
		             |				(ВЫБРАТЬ
		             |					СписокАвтомобилей.Автомобиль КАК Автомобиль
		             |				ИЗ
		             |					СписокАвтомобилей КАК СписокАвтомобилей)
		             |	
		             |	ОБЪЕДИНИТЬ ВСЕ
		             |	
		             |	ВЫБРАТЬ
		             |		РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение,
		             |		0,
		             |		ЕСТЬNULL(РеализованныеТовары.СуммаУпр, 0)
		             |	ИЗ
		             |		РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
		             |	ГДЕ
		             |		РеализованныеТовары.Регистратор = &Регистратор
		             |		И РеализованныеТовары.Автомобиль В
		             |				(ВЫБРАТЬ
		             |					СписокАвтомобилей.Автомобиль КАК Автомобиль
		             |				ИЗ
		             |					СписокАвтомобилей КАК СписокАвтомобилей)) КАК ОбъединенныйЗапрос
		             |
		             |СГРУППИРОВАТЬ ПО
		             |	ОбъединенныйЗапрос.Подразделение";
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		// В случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции.
		Если БалансВедетсяПоПодразделениям Тогда
			Выборка=Запрос.Выполнить().Выбрать();
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			Пока Выборка.Следующий() Цикл
				СуммаАвтомобилейУпр  = Выборка.СуммаАвтомобилейУпр;
				СуммаОборудованияУпр = Выборка.СуммаОборудованияУпр;
				Если СуммаАвтомобилейУпр <> 0 Тогда
					НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.Подразделение  = Выборка.Подразделение;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыИРасходы.Расход                 = СуммаАвтомобилейУпр;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
				КонецЕсли;
				Если СуммаОборудованияУпр <> 0 Тогда
					НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.Подразделение  = Выборка.Подразделение;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыИРасходы.Расход                 = СуммаОборудованияУпр;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
				КонецЕсли;
			КонецЦикла; 
		Иначе
			ТаблицаСебестоимости = Запрос.Выполнить().Выгрузить();
			СуммаАвтомобилейУпр = ТаблицаСебестоимости.Итог("СуммаАвтомобилейУпр");
			СуммаОборудованияУпр = ТаблицаСебестоимости.Итог("СуммаОборудованияУпр");
			Если СуммаАвтомобилейУпр <> 0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
				НаборЗаписейДоходыИРасходы.Расход                 = СуммаАвтомобилейУпр;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			КонецЕсли;
			Если СуммаОборудованияУпр <> 0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
				НаборЗаписейДоходыИРасходы.Расход                 = СуммаОборудованияУпр;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		// Возможно, что возврат производится на склад, на котором подразделение не совпадает с подразделением договора.
		ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
		ПодразделениеДоговор = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговор<>ПодразделениеСклад);
		Если БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны Тогда
			// получим себестоимость
			ОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
			КомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
			СебестоимостьАвтомобилей = -(ОстаткиАвтомобилей.Итог("СуммаУпр")+КомплектацияАвтомобилей.Итог("СуммаУпр"));
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = СкладКомпании.Подразделение;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьАвтомобилей;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			АвтомобилиОтданные=Движения.АвтомобилиОтданные;
			ПартииТоваровОтданные=Движения.ПартииТоваровОтданные;
			СебестоимостьАвтомобилейОтданные = -(АвтомобилиОтданные.Итог("СуммаСебестоимостиУпр")+ПартииТоваровОтданные.Итог("СуммаСебестоимостиУпр"));
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьАвтомобилейОтданные;
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// Двигаем границу последовательности комплектации автомобилей.
	Если РежимПроведения<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); АвтомобильБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				АвтомобильБыл = Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("АвтомобильБыл",АвтомобильБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если РежимПроведения<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  = ШапкаДокумента.Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	// Двигаем границу последовательности автомобилей.
	Если РежимПроведения<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если РежимПроведения<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьДвиженияПоВозвратуАвтомобилей()

Процедура ВыполнитьДвиженияПоРеализованнымАвтомобилям(СписокОснований, КомиссионныеАвтомобили, Отказ)
	
	Если КомиссионныеАвтомобили = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РеализованныеАвтомобили.Контрагент,
	|	РеализованныеАвтомобили.Автомобиль,
	|	РеализованныеАвтомобили.ДоговорВзаиморасчетов,
	|	РеализованныеАвтомобили.ДокументПередачи,
	|	РеализованныеАвтомобили.ГТД
	|ИЗ
	|	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
	|ГДЕ
	|	РеализованныеАвтомобили.Регистратор В(&Регистраторы)
	|	И РеализованныеАвтомобили.Автомобиль В(&Автомобили)";
	Запрос.УстановитьПараметр("Регистраторы" , СписокОснований);
	Запрос.УстановитьПараметр("Автомобили" , Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	ВыборкаОтданных = Запрос.Выполнить().Выбрать();
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	Для Каждого ВыборкаДокумента Из КомиссионныеАвтомобили Цикл
		ВыборкаОтданных.Сбросить();
		Если НЕ ВыборкаОтданных.НайтиСледующий(ВыборкаДокумента.Автомобиль, "Автомобиль") Тогда
			дкСообщитьРезультат("Для автомобиля <"+ВыборкаДокумента.Автомобиль+"> не найдена запись в регистре отданных автомобилей.",,ЭтотОбъект);
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = Движения.РеализованныеАвтомобили.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаОтданных);
		
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		
		// запишем суммы
		НоваяЗапись.СуммаПродажиРегл = обПересчет(ВыборкаДокумента.СуммаВсегоРазница, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
		НоваяЗапись.СуммаПродажи     = обПересчет(ВыборкаДокумента.СуммаВсегоРазница, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр);
		НоваяЗапись.ХозОперация = ХозОперация;
		
		//СебестоимостьРавнаПродаже
		Если НЕ НоваяЗапись.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж Тогда
			НоваяЗапись.СуммаРегл      = НоваяЗапись.СуммаПродажиРегл;
			НоваяЗапись.СуммаНДС       = Окр((НоваяЗапись.СуммаРегл * ВыборкаДокумента.СтавкаНДС.Ставка)/(100 + ВыборкаДокумента.СтавкаНДС.Ставка),2);
			НоваяЗапись.СуммаУпр       = НоваяЗапись.СуммаПродажи;

		КонецЕсли;
		
		Если НЕ ВыборкаДокумента.ЕстьОстаток Тогда
			ВтораяЗапись = Движения.РеализованныеАвтомобили.Добавить();
			ЗаполнитьЗначенияСвойств(ВтораяЗапись, НоваяЗапись);
			ВтораяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Движения.РеализованныеАвтомобили.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполнитьДвиженияПоПродажам(СписокОснований, КомиссионныеАвтомобили, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПродажиАвтомобилей.ПодразделениеКомпании,
	|	ПродажиАвтомобилей.Автомобиль,
	|	ПродажиАвтомобилей.ДокументПродажи,
	|	ПродажиАвтомобилей.Поставщик,
	|	ПродажиАвтомобилей.Покупатель,
	|	ПродажиАвтомобилей.СтатусПартии,
	|	ПродажиАвтомобилей.ХозОперация,
	|	ПродажиАвтомобилей.ДоговорВзаиморасчетов,
	|	ПродажиАвтомобилей.СкладКомпании,
	|	ПродажиАвтомобилей.СтавкаНДС,
	|	ПродажиАвтомобилей.Партия,
	|	ПродажиАвтомобилей.ГТД,
	|	ПродажиАвтомобилей.СуммаНДСВходящий,
	|	ПродажиАвтомобилей.Себестоимость
	|ИЗ
	|	РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
	|ГДЕ
	|	ПродажиАвтомобилей.Регистратор В(&Регистраторы)
	|	И ПродажиАвтомобилей.Автомобиль В(&Автомобили)";
	Запрос.УстановитьПараметр("Регистраторы" , СписокОснований);
	Запрос.УстановитьПараметр("Автомобили"   , Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	СуммаДоходов  = 0;
	СуммаРасходов = 0;
	
	Для Каждого Строка Из Автомобили Цикл
		
		Если Строка.СуммаВсегоРазница = 0
			ИЛИ Строка.СуммаНДСРазница = 0
			ИЛИ Строка.КоличествоРазница <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("Автомобиль", Строка.Автомобиль); Выборка.Сбросить();
		Если НЕ Выборка.НайтиСледующий(Отбор) Тогда
			дкСообщитьРезультат("Для автомобиля <"+Строка.Автомобиль+"> не найдена запись в регистре продаж.",,ЭтотОбъект);
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = Движения.ПродажиАвтомобилей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка,, "Себестоимость,СуммаНДСВходящий");
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.ХозОперация = ХозОперация;
		
		// запишем суммы
		НоваяЗапись.Сумма    = обПересчет(Строка.СуммаВсегоРазница, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
		НоваяЗапись.СуммаНДС = обПересчет(Строка.СуммаНДСРазница, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
		НоваяЗапись.СуммаУпр = обПересчет(Строка.СуммаВсегоРазница, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр);
		
		// Проверим что еще не отчитались за автомобиль
		Если НЕ КомиссионныеАвтомобили = Неопределено Тогда
			КомиссионныйАвтомобиль = КомиссионныеАвтомобили.Найти(Строка.Автомобиль, "Автомобиль");
			
			Если КомиссионныйАвтомобиль <> Неопределено И НЕ КомиссионныйАвтомобиль.ЕстьОстаток Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Обработаем ситуацию когда себестоимость равна сумме продажи.
		Попытка
			
			Если НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия
				И НЕ НоваяЗапись.Партия.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж
			Тогда
			
				СтавкаНДСВходящее   = Окр(Выборка.СуммаНДСВходящий / (Выборка.Себестоимость-Выборка.СуммаНДСВходящий), 2);
				СебестоимостьБезНДС = НоваяЗапись.Сумма - НоваяЗапись.СуммаНДС;
				
				НоваяЗапись.Себестоимость    = НоваяЗапись.Сумма;
				НоваяЗапись.СебестоимостьУпр = НоваяЗапись.СуммаУпр;
				НоваяЗапись.СуммаНДСВходящий = Окр(СебестоимостьБезНДС*СтавкаНДСВходящее, 2);
				
				СуммаРасходов = СуммаРасходов + НоваяЗапись.СебестоимостьУпр;
				
			КонецЕсли;
			
		Исключение
			
			// TODO: Обработка исключения.
			
		КонецПопытки;
		
		СуммаДоходов = СуммаДоходов + НоваяЗапись.СуммаУпр;

	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Движения.ПродажиАвтомобилей.Записать();
	КонецЕсли;
	
	Если СуммаДоходов <> 0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоАвтомобилям;
		НаборЗаписейДиР.ВУпрВалюте             = Истина;
		
		НаборЗаписейДиР.Доход = СуммаДоходов;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Если СуммаРасходов <> 0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
		НаборЗаписейДиР.ВУпрВалюте             = Истина;
		
		НаборЗаписейДиР.Расход = СуммаРасходов;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
КонецФункции

// Обработчик события объекта возникает в момент, когда выполняется установка нового номера.
//
// Параметры:
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//  Префикс              - Строка - Префикс, который будет использоваться для генерации номера.
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения объекта копированием.
//
// Параметры:
//  ОбъектКопирования - ДокументОбъект - Исходный объект, который является источником копирования.
//
Процедура ПриКопировании(ОбъектКопирования)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

// Обработчик события возникающего после начала транзакции записи, но до начала записи объекта.
//
// Параметры:
//  Отказ           - Булево                   - Признак отказа от совершения операции.
//  РежимЗаписи     - РежимЗаписиДокумента     - Текущий режим записи документа.
//  РежимПроведения - РежимПроведенияДокумента - Текущий режим проведения.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// заполним сделку
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Сделка = ПолучитьСделку();
	КонецЕсли;
	
	// Проверим хоз. операции
	// составим список документов движений.
	Если ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииАвтомобилейКорректировкаПоСогласованиюСторон Тогда
		
		// заполним значение до корректировки
		Для Каждого Строка Из Автомобили Цикл
			Строка.СуммаВсегоДоКорректировки = Строка.СуммаВсегоПоДокументуРеализации;
			Строка.СуммаНДСДоКорректировки   = Строка.СуммаНДСПоДокументуРеализации;
		КонецЦикла;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииАвтомобилейИсправлениеВПервичныхДокументах Тогда
		
		// проверим введенные на основании корректировки
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодчиненныеДокументы.Ссылка КАК Документ
		|ИЗ
		|	КритерийОтбора.ПодчиненныеДокументы(&Основание) КАК ПодчиненныеДокументы
		|ГДЕ
		|	ПодчиненныеДокументы.Ссылка ССЫЛКА Документ.КорректировкаРеализацииАвтомобилей
		|	И НЕ ПодчиненныеДокументы.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.КорректировкаРеализацииАвтомобилейКорректировкаПоСогласованиюСторон)
		|	И НЕ ПодчиненныеДокументы.Ссылка = &Ссылка";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Основание", ДокументОснование);
		Запрос.УстановитьПараметр("Ссылка",    Ссылка);
		ВыполнениеЗапроса = Запрос.Выполнить();
		
		Если НЕ ВыполнениеЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'К документу """+Строка(ДокументОснование)+""" введено больше одного корректировочного документа с хоз. операцей ""Исправление первичных документов"". 
								|Каждую последующую корректировку следует вводить на основании предыдущей.'");
			Сообщить(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	// Вызываем общий обработчик события
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка корректности типа цен.
	Если ТипЦен.Рассчитывается Тогда
		
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Использование расчетных типов цен запрещено.'"));
		
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	РассчитатьСуммуВсего();
	
КонецПроцедуры

// Обработчик события возникающего после записи объекта в базу данных, но до окончания транзакции записи.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ПриЗаписи(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

// Обработчик события возникающего в транзакции удаления перед непосредственным удалением объекта из базы данных.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ПередУдалением(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

// Обработчик события возникающего при отмене проведения документа. Выполняется в транзакции записи.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события возникающего в транзакции записи для формирования движений документу по подчиненным регистрам.
//
// Параметры:
//  Отказ           - Булево                   - Признак отказа от совершения операции.
//  РежимПроведения - РежимПроведенияДокумента - Текущий режим проведения.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Вызываем общий обработчик события
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	Ошибки = "";

	Если НЕ ВозможенВВодНаОсновании(Сделка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	// Проверим, что автомобиль отсутствует на остатках по различным регистрам
	Если НЕ РегистрыНакопления.ОстаткиАвтомобилей.ПроверитьОстаткиАвтомобилей(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	СписокОснований = ПолучитьСписокОснований();
	
	Если Сделка.ХозОперация <> Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
		КомиссионныеАвтомобили = ПолучитьКомиссионныеАвтомобили();
		ВыполнитьДвиженияПоПродажам(СписокОснований, КомиссионныеАвтомобили, Отказ);
		ВыполнитьДвиженияПоВзаиморасчетам(Отказ, РежимПроведения);
		ВыполнитьДвиженияПоРеализованнымАвтомобилям(СписокОснований, КомиссионныеАвтомобили, Отказ);
	Иначе
		ВыполнитьДвиженияПоРегистрамРеализации(СписокОснований, Отказ);
	КонецЕсли;
	
	ВыполнитьДвиженияПоВозвратуАвтомобилей(РежимПроведения, Ссылка, СписокОснований, Отказ);
	
	// Проверим налиие прослеживаемых товаров, которые были реализованы
	ТаблицаПрослеживаемыхТоваров = Документы.КорректировкаРеализацииАвтомобилей.ОперацииСПрослеживаемымиТоварами(
		ЭтотОбъект);
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ТаблицаПрослеживаемыхТоваров;
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	ДвигаемГраницу = (РежимПроведения<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);     
	
	//<<ЧалапАю БизТех 15.07.2025  000000930
	
	Если не Отказ тогда //и ТипЗнч(ДокументОснование) = тип("ДокументСсылка.РеализацияАвтомобилей") тогда 
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Автомобили.Автомобиль КАК Автомобиль
		|ПОМЕСТИТЬ ВТАвтомобили
		|ИЗ
		|	&Автомобили КАК Автомобили
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЭМ_КумулятивнаяМаржа.Ссылка КАК Документ,
		|	ЭМ_КумулятивнаяМаржа.Проведен КАК Проведен
		|ИЗ
		|	Документ.ЭМ_КумулятивнаяМаржа КАК ЭМ_КумулятивнаяМаржа
		|ГДЕ
		|	НЕ ЭМ_КумулятивнаяМаржа.ПометкаУдаления
		|	И ЭМ_КумулятивнаяМаржа.Автомобиль В
		|			(ВЫБРАТЬ
		|				ВТАвтомобили.Автомобиль КАК Автомобиль
		|			ИЗ
		|				ВТАвтомобили КАК ВТАвтомобили
		|			СГРУППИРОВАТЬ ПО
		|				ВТАвтомобили.Автомобиль)
		|	И ЭМ_КумулятивнаяМаржа.ДокументОснование = &ДокументОснование";
		Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
		Запрос.УстановитьПараметр("Автомобили", Автомобили);  

		Результат = Запрос.Выполнить();
		Если Результат.Пустой() тогда
			Для Каждого Автомобиль Из Автомобили Цикл  
				Если Автомобиль.КоличествоРазница<0 тогда       
					
					ТЧОснований = Новый ТаблицаЗначений;  
					ТЧОснований.Колонки.Добавить("Основание");
					
					ДокПроверки = ДокументОснование; 
					Пока не типЗНЧ(ДокПроверки) = тип("ДокументСсылка.РеализацияАвтомобилей")  цикл
						стр = ТЧОснований.Добавить();
						Стр.Основание = ДокПроверки;    
						ДокПроверки = ДокПроверки.ДокументОснование;
					КонецЦикла;
					стр = ТЧОснований.Добавить();
					Стр.Основание = ДокПроверки; 
					
						
					ЗапросКМОснований = Новый Запрос;
					ЗапросКМОснований.Текст =
					"ВЫБРАТЬ
					|	Автомобили.Автомобиль КАК Автомобиль
					|ПОМЕСТИТЬ ВТАвтомобили
					|ИЗ
					|	&Автомобили КАК Автомобили
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	МАКСИМУМ(ЭМ_КумулятивнаяМаржа.Ссылка) КАК Документ
					|ИЗ
					|	Документ.ЭМ_КумулятивнаяМаржа КАК ЭМ_КумулятивнаяМаржа
					|ГДЕ
					|	НЕ ЭМ_КумулятивнаяМаржа.ПометкаУдаления
					|	И ЭМ_КумулятивнаяМаржа.Автомобиль В
					|			(ВЫБРАТЬ
					|				ВТАвтомобили.Автомобиль КАК Автомобиль
					|			ИЗ
					|				ВТАвтомобили КАК ВТАвтомобили
					|			СГРУППИРОВАТЬ ПО
					|				ВТАвтомобили.Автомобиль)
					|	И ЭМ_КумулятивнаяМаржа.ДокументОснование В(&ДокументОснование)
					|	И ЭМ_КумулятивнаяМаржа.Проведен";
					ЗапросКМОснований.УстановитьПараметр("ДокументОснование", ТЧОснований);
					ЗапросКМОснований.УстановитьПараметр("Автомобили", Автомобили);  
					
					РезультатКМОснований = ЗапросКМОснований.Выполнить().Выгрузить();
					//ПредыдущаяКМ = Документы.ЭМ_КумулятивнаяМаржа.НайтиПоРеквизиту("ДокументОснование",ДокументОснование); 
					//ПредыдущаяКМ = 
					Если РезультатКМОснований.Количество()>0 тогда
						НовыйКМ = РезультатКМОснований[0].Документ.Скопировать();
						НовыйКМ.Дата = Ссылка.Дата;
						НовыйКМ.ДокументОснование = Ссылка;
						Для каждого СтрокаПоказателя из НовыйКМ.Показатели цикл
							СтрокаПоказателя.Сумма = -СтрокаПоказателя.Сумма;
							СтрокаПоказателя.СуммаБезНДС = -СтрокаПоказателя.СуммаБезНДС;
							СтрокаПоказателя.СуммаНДС = -СтрокаПоказателя.СуммаНДС;
						КонецЦикла;
						НовыйКМ.Записать(РежимЗаписиДокумента.Проведение);  
						Сообщить("Создан и проведен документ """ + НовыйКМ + """ для автомобиля " + Автомобиль, СтатусСообщения.Информация);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
	//ЧалапАю БизТех 15.07.2025  000000930	>>  
	
КонецПроцедуры // ОбработкаПроведения

Процедура ВыполнитьДвиженияПоРегистрамРеализации(СписокОснований, Отказ)
	
	// получим таблицу авто с остатками
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница КАК СуммаНДСРазница,
	|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница КАК СуммаВсегоРазница,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АвтомобилиОтданныеОстатки.КоличествоОстаток, 2) = 2
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОстаток
	|ИЗ
	|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АвтомобилиОтданные.Остатки(
	|				,
	|				Контрагент = &Контрагент
	|					И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК АвтомобилиОтданныеОстатки
	|		ПО КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль = АвтомобилиОтданныеОстатки.Автомобиль
	|ГДЕ
	|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница <> 0
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница <> 0
	|	И КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница = 0";
	
	//Запрос.УстановитьПараметр("Момент"                , МоментВремени());
	Запрос.УстановитьПараметр("Контрагент"            , Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов" , ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Ссылка"                , Ссылка);
	
	ВыборкаДокумента = Запрос.Выполнить().Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	АвтомобилиОтданные.Контрагент,
	|	АвтомобилиОтданные.Автомобиль,
	|	АвтомобилиОтданные.ДоговорВзаиморасчетов,
	|	АвтомобилиОтданные.ДокументПередачи,
	|	АвтомобилиОтданные.Партия,
	|	АвтомобилиОтданные.ГТД
	|ИЗ
	|	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
	|ГДЕ
	|	АвтомобилиОтданные.Регистратор В(&Регистраторы)
	|	И АвтомобилиОтданные.Автомобиль В(&Автомобили)";
	Запрос.УстановитьПараметр("Регистраторы" , СписокОснований);
	Запрос.УстановитьПараметр("Автомобили"   , Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	ВыборкаОтданных = Запрос.Выполнить().Выбрать();
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	Пока ВыборкаДокумента.Следующий() Цикл
		ВыборкаОтданных.Сбросить();
		Если НЕ ВыборкаОтданных.НайтиСледующий(ВыборкаДокумента.Автомобиль, "Автомобиль") Тогда
			дкСообщитьРезультат("Для автомобиля <"+ВыборкаДокумента.Автомобиль+"> не найдена запись в регистре отданных автомобилей.",,ЭтотОбъект);
			Отказ = Истина;
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = Движения.АвтомобилиОтданные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаОтданных);
		
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.ХозОперация = ХозОперация;
		// запишем суммы
		НоваяЗапись.Сумма    = обПересчет(ВыборкаДокумента.СуммаВсегоРазница, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
		НоваяЗапись.СуммаНДС = обПересчет(ВыборкаДокумента.СуммаНДСРазница, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
		НоваяЗапись.СуммаУпр = обПересчет(ВыборкаДокумента.СуммаВсегоРазница, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр);
		
		Если НЕ ВыборкаДокумента.ЕстьОстаток Тогда
			ВтораяЗапись = Движения.АвтомобилиОтданные.Добавить();
			ЗаполнитьЗначенияСвойств(ВтораяЗапись, НоваяЗапись);
			ВтораяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Движения.АвтомобилиОтданные.Записать();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПолучитьСуммуПродажиАвтомобиля(Знач Автомобиль = Неопределено, Основания = Неопределено) Экспорт
	
	Если Автомобиль = Неопределено Тогда
		Автомобиль = Автомобили.ВыгрузитьКолонки("Автомобиль");
	ИначеЕсли ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
		АвтомобильМассив = Новый Массив;
		АвтомобильМассив.Добавить(Автомобиль);
		Автомобиль = АвтомобильМассив;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Основания) Тогда
		Основания = ПолучитьСписокОснований();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Автомобили"   , Автомобиль);
	Запрос.УстановитьПараметр("Регистраторы" , Основания);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПродажиАвтомобилей.Автомобиль КАК Автомобиль,
	|	ПродажиАвтомобилей.Сумма КАК Сумма,
	|	ПродажиАвтомобилей.СуммаНДС КАК СуммаНДС,
	|	ПродажиАвтомобилей.СуммаУпр КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
	|ГДЕ
	|	ПродажиАвтомобилей.Автомобиль В(&Автомобили)
	|	И ПродажиАвтомобилей.Регистратор В(&Регистраторы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Продажи.Автомобиль,
	|	Продажи.Сумма,
	|	Продажи.СуммаНДС,
	|	Продажи.СуммаУпр
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Регистратор В(&Регистраторы)
	|	И Продажи.Автомобиль В(&Автомобили)
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС),
	|	СУММА(СуммаУпр)
	|ПО
	|	Автомобиль";
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

Процедура ВыполнитьДвиженияПоВзаиморасчетам(Отказ, Режим)
	// получим сумму различий
	СуммаЗаписи = Автомобили.Итог("СуммаВсегоРазница");
	
	Если СуммаЗаписи = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписейВзаиморасчеты                       = Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения       = Режим;
	НаборЗаписейВзаиморасчеты.Контрагент            = Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.Сделка                = Сделка;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
	
	Если СуммаЗаписи > 0 Тогда
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Ложь;
		НаборЗаписейВзаиморасчеты.Сумма                     = СуммаЗаписи;
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
	Иначе
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
		НаборЗаписейВзаиморасчеты.Сумма                     = -СуммаЗаписи;
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	КонецЕсли;
	
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц <> 0 Тогда
		НаборЗаписейДиР                        = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте             = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
		
КонецПроцедуры


// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	ВремСуммаДокумента=Автомобили.Итог("СуммаВсего");
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа.
//	Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если ДопПараметры=Неопределено Тогда
		ДопПараметры=Новый Структура;
	КонецЕсли;

	Если ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
		ДопПараметры.Вставить("НеРассчитыватьСкидки",Истина);
	КонецЕсли; 
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ДокументОснование" Тогда
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			Если ДокументОснование = Ссылка Тогда
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Сообщить("Ввод корректировки реализации автомобилей на основании самой себя запрещен.",СтатусСообщения.Внимание);
				Возврат Истина;
			КонецЕсли;

			Если Документы.КорректировкаРеализацииАвтомобилей.КорректировкаНеДоступна(ДокументОснование) Тогда
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				ТекстСообщения = НСтр("ru = 'На основании """ + ДокументОснование + """ введен возврат автомобилей. Ввод корректировки реализации автомобилей невозможен.'");
				Сообщить(ТекстСообщения,СтатусСообщения.Внимание);
				Возврат Истина;
			КонецЕсли;
			
			Если НЕ ДокументОснование.Проведен Тогда
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Сообщить("Ввод корректировки реализации автомобилей возможен только на основании проведенного документа.",СтатусСообщения.Внимание);
				Возврат Истина;
			КонецЕсли;

			Если ДокументОснование.ХозОперация = Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				ТекстСообщения = НСтр("ru = 'Ввод корректировки реализации автомобилей невозможен на основании реализации автомобилей комиссия.'");
				Сообщить(ТекстСообщения,СтатусСообщения.Внимание);
				Возврат Истина;
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КорректировкаРеализацииАвтомобилей.Ссылка
			|ИЗ
			|	Документ.КорректировкаРеализацииАвтомобилей КАК КорректировкаРеализацииАвтомобилей
			|ГДЕ
			|	КорректировкаРеализацииАвтомобилей.ДокументОснование = &ДокументОснование
			|	И КорректировкаРеализацииАвтомобилей.Ссылка <> &Ссылка";
			Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			РезЗапроса = Запрос.Выполнить();
			Если НЕ РезЗапроса.Пустой() Тогда
				Выборка = РезЗапроса.Выбрать();
				Выборка.Следующий();
				#Если Клиент Тогда
					Предупреждение("На основании документа " + ДокументОснование + " уже введен" + Выборка.Ссылка);
				#КонецЕсли
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
			
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда 
					Если Вопрос("Заполнить по документу-основания?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
						ЭтотОбъект.Заполнить(ДокументОснование);
						ЭтаФорма.УправлениеДиалогом();
						дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
					КонецЕсли;
				КонецЕсли; 
			#КонецЕсли
			Возврат Ложь;
		КонецЕсли;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "Автомобили"

	ИначеЕсли Имя = "Автомобили.Автомобиль" Тогда
		// подставим VIN
		ТекСтрока.VIN = ТекСтрока.Автомобиль.VIN;
		Если НЕ ЗначениеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Возврат Истина;

	ИначеЕсли Имя = "Автомобили.Количество" Тогда
		ОбработкаРеквизита("Автомобили.Сумма", ТекСтрока, ЭтаФорма);
		Возврат Истина;

	ИначеЕсли Имя = "Автомобили.Цена" Тогда
		ТекСтрока.Сумма = ТекСтрока.Цена;
		ОбработкаРеквизита("Автомобили.Сумма", ТекСтрока, ЭтаФорма);
		Возврат Истина;

	ИначеЕсли Имя = "Автомобили.Сумма" Тогда
		Если ТекСтрока.Количество = 0 Тогда
			ТекСтрока.Сумма = 0;
		КонецЕсли;
		
		ТекСтрока.Цена = ТекСтрока.Сумма;
		
		// расчитаем сумму всего
		Если ?(ЗначениеЗаполнено(ТипЦен),ТипЦен.ЦенаВключаетНДС,Истина) Тогда
			ТекСтрока.СуммаВсего = ТекСтрока.Сумма;
			ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсего - (100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
		Иначе
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;

	ИначеЕсли Имя = "Автомобили.СуммаВсего" Тогда
		Если ТекСтрока.Количество = 0 Тогда
			ТекСтрока.СуммаВсего = 0;
		КонецЕсли;
		
		//Получим новую сумму НДС как процент от суммы всего
		Если ?(ЗначениеЗаполнено(ТипЦен),ТипЦен.ЦенаВключаетНДС,Истина)Тогда
			//Цена уже содержит НДС
			ТекСтрока.Сумма    = ТекСтрока.СуммаВсего;
			ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсего - (100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
		Иначе
			//НДС в цену не включен
			ТекСтрока.Сумма    = (100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсего - ТекСтрока.Сумма;
		КонецЕсли; 
		
		ТекСтрока.Цена = ТекСтрока.Сумма;
		Возврат Истина;

	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаНДС" Тогда
		Возврат Истина;

	ИначеЕсли Имя="РассчитатьРазницу" Тогда
		ТекСтрока.СуммаРазница      = ТекСтрока.Сумма - ТекСтрока.СуммаПоДокументуРеализации;
		ТекСтрока.СуммаНДСРазница   = ТекСтрока.СуммаНДС - ТекСтрока.СуммаНДСПоДокументуРеализации;
		ТекСтрока.СуммаВсегоРазница = ТекСтрока.СуммаВсего - ТекСтрока.СуммаВсегоПоДокументуРеализации;
		ТекСтрока.КоличествоРазница = ТекСтрока.Количество - ТекСтрока.КоличествоПоДокументуРеализации;

	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//Зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	
	// Обязательные поля таблицы товаров
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили); 
		
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности договора и склада документа. 
	// Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("КорректировкаРеализацииАвтомобилейИсправлениеВПервичныхДокументах","Исправление в первичных документах",Истина);
	СписокХозОпераций.Добавить("КорректировкаРеализацииАвтомобилейКорректировкаПоСогласованиюСторон","Корректировка по согласованию сторон",Ложь);
	Возврат СписокХозОпераций;
КонецФункции

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.КорректировкаРеализацииАвтомобилей - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	ОсвобождентОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Автомобили.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииАвтомобилейИсправлениеВПервичныхДокументах Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПродажиАвтомобилей.Автомобиль КАК Автомобиль,
		|	ПродажиАвтомобилей.ГТД КАК ГТД
		|ПОМЕСТИТЬ ГТДАвтомобилей
		|ИЗ
		|	РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
		|ГДЕ
		|	ПродажиАвтомобилей.Регистратор = &Сделка
		|	И ПродажиАвтомобилей.ГТД.РНПТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УПД) КАК ВидДокумента,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияТоваров) КАК КодОперации,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Количество КАК КоличествоПрослеживаемости,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсего - КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДС КАК СуммаБезНДС
		|ПОМЕСТИТЬ ТЧАвтомобили
		|ИЗ
		|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДАвтомобилей.Автомобиль КАК Номенклатура,
		|	ГТДАвтомобилей.ГТД КАК РНПТ,
		|	ТЧАвтомобили.ОтчетностьОперации КАК ОтчетностьОперации,
		|	ТЧАвтомобили.ВидДокумента КАК ВидДокумента,
		|	ТЧАвтомобили.КодОперации КАК КодОперации,
		|	ТЧАвтомобили.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ТЧАвтомобили.СуммаБезНДС КАК СуммаБезНДС
		|ИЗ
		|	ГТДАвтомобилей КАК ГТДАвтомобилей
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТЧАвтомобили КАК ТЧАвтомобили
		|		ПО ГТДАвтомобилей.Автомобиль = ТЧАвтомобили.Номенклатура";
		Документ = Сделка;
		Если ЗначениеЗаполнено(Документ) Тогда
			ПериодОтчета = НачалоКвартала(Документ.Дата);
			НомерДокумента = дкПолучитьНомерДляПечати(Документ);
			ДатаДокумента = Документ.Дата;
		Иначе
			ПериодОтчета = НачалоКвартала(Дата);
		КонецЕсли;
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПродажиАвтомобилей.Автомобиль КАК Автомобиль,
		|	ПродажиАвтомобилей.ГТД КАК ГТД
		|ПОМЕСТИТЬ ГТДАвтомобилей
		|ИЗ
		|	РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
		|ГДЕ
		|	ПродажиАвтомобилей.Регистратор = &Ссылка
		|	И ПродажиАвтомобилей.ГТД.РНПТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УКД) КАК ВидДокумента,
		|	ВЫБОР
		|		КОГДА КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУменьшение)
		|	КОНЕЦ КАК КодОперации,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	ГТДАвтомобилей.ГТД КАК РНПТ,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница * ВЫБОР
		|		КОГДА КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница < 0
		|			ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК КоличествоПрослеживаемости,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница * ВЫБОР
		|		КОГДА КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница < 0
		|			ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ - КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница * ВЫБОР
		|		КОГДА КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница < 0
		|			ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК СуммаБезНДС
		|ИЗ
		|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГТДАвтомобилей КАК ГТДАвтомобилей
		|		ПО КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль = ГТДАвтомобилей.Автомобиль
		|ГДЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница <> 0
		|	И НЕ ГТДАвтомобилей.ГТД ЕСТЬ NULL";
		Документ = Ссылка;
		ПериодОтчета = НачалоКвартала(Дата);
		НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
		ДатаДокумента = Дата;
	КонецЕсли;
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Сделка", Сделка);
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим есть РНПТ у документа
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = РезультатЗапроса.Выгрузить();
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Документ;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = ДатаДокумента;
		НоваяСтрока.Контрагент = Контрагент;
		
		// Пересчет суммы
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(обПересчет(НоваяСтрока.СуммаБезНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,Дата), 2);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда

//////////////////////////////////////////////////////////////////////////////////
//// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА


// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

// Формирует печатную форму "Накладная"
// Возвращает сформированный табличный документ:
Функция ПечатьРеализацияАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РеализацияАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтруктураПараметров = Новый Структура("Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,ОКПО",
		"", "ИНН ", "КПП ", "", "тел.: ", "Код по ОКПО ");
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация,
		СтруктураПараметров, ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Контрагент, СтруктураПараметров);
	ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();
	
	КоличествоПозиций = ВыборкаТабличнойЧасти.Количество();
	
	//подготовим дополнительную таблицу
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		
		Если СтрокаТабличнойЧасти.Количество = 0 Тогда
			КоличествоПозиций = КоличествоПозиций - 1;
			Продолжить;
		КонецЕсли;
		
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);	
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+КоличествоПозиций+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "УПД"
// Возвращает сформированный табличный документ:
Функция ПечатьУПД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
	
	Возврат ТабДокумент;	
	
КонецФункции //ПечатьУПД()

// Формирует печатную форму "УКД"
// Возвращает сформированный табличный документ:
Функция ПечатьУКД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУКД(ЭтотОбъект, ТабДокумент);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьУКД

// Функция получения данных для УКД.
//
Функция ПолучитьДанныеДляПечатиУКД() Экспорт
	
	ДокументФактура = Документы.СчетФактураВыданный.НайтиПоРеквизиту("ДокументОснование", ЭтотОбъект.Ссылка);
	ДокументОбъект = ДокументФактура;
	ДанныеОбъекта = Новый Структура();
	
	// Сформируем статус документа
	Если ДокументФактура <> Документы.СчетФактураВыданный.ПустаяСсылка() И НЕ ДокументФактура.ПометкаУдаления Тогда
		Статус = 1;
		ДокументОбъект = ДокументФактура;
	Иначе
		Статус = 2;
		ДокументОбъект = ЭтотОбъект;
	КонецЕсли;
	
	Если Дата < Дата('20210701') Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Цена,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Сумма,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаРазница,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СтавкаНДС,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДС,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСРазница,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсего,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоРазница,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Количество КАК Количество,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоРазница КАК КоличествоРазница,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСПоДокументуРеализации,
		|	КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоПоДокументуРеализации
		|ИЗ
		|	Документ.КорректировкаРеализацииАвтомобилей.Автомобили КАК КорректировкаРеализацииАвтомобилейАвтомобили
		|ГДЕ
		|	КорректировкаРеализацииАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И (КорректировкаРеализацииАвтомобилейАвтомобили.Количество <> КорректировкаРеализацииАвтомобилейАвтомобили.КоличествоПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииАвтомобилейАвтомобили.Сумма <> КорректировкаРеализацииАвтомобилейАвтомобили.СуммаПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииАвтомобилейАвтомобили.СтавкаНДС <> КорректировкаРеализацииАвтомобилейАвтомобили.СтавкаНДСПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДС <> КорректировкаРеализацииАвтомобилейАвтомобили.СуммаНДСПоДокументуРеализации
		|			ИЛИ КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсего <> КорректировкаРеализацииАвтомобилейАвтомобили.СуммаВсегоПоДокументуРеализации)";
		
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
		ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
		
	Иначе
		
		ТаблицаТоваров = Документы.СчетФактураВыданный.ПолучитьДанныеДляЗаполненияПФКорректировки(ЭтотОбъект);
		
	КонецЕсли;
	
	// данные документа
	ДанныеОбъекта.Вставить("Дата"                   	, ДокументОбъект.Дата);
	ДанныеОбъекта.Вставить("Номер"                  	, дкПолучитьНомерДляПечати(ДокументОбъект));
	ДанныеОбъекта.Вставить("ХозОперация"            	, ДокументОбъект.ХозОперация);
	ДанныеОбъекта.Вставить("ДокументОснование"      	, ДокументОбъект.ДокументОснование);
	ДанныеОбъекта.Вставить("ВалютаДокумента"      		, ЭтотОбъект.ВалютаДокумента);
	ДанныеОбъекта.Вставить("Поставщик"              	, ДокументОбъект.Организация);
	ДанныеОбъекта.Вставить("ПодразделениеКомпании"  	, ДокументОбъект.ПодразделениеКомпании);
	ДанныеОбъекта.Вставить("Покупатель"              	, ДокументОбъект.Контрагент);
	ДанныеОбъекта.Вставить("Организация"              	, ЭтотОбъект.Организация);
	ДанныеОбъекта.Вставить("ДоговорВзаиморасчетов"     	, ?(ЗначениеЗаполнено(ДокументОбъект.ДоговорВзаиморасчетов), ЭтотОбъект.ДоговорВзаиморасчетов, "--"));
	ДанныеОбъекта.Вставить("Товары"              		, ТаблицаТоваров);
	ДанныеОбъекта.Вставить("Статус"              		, Статус);
	ДанныеОбъекта.Вставить("Ссылка"              		, ДокументОбъект.Ссылка);
	ДанныеОбъекта.Вставить("ИдентификаторГосударственногоКонтракта", ЭтотОбъект.ИдентификаторГосударственногоКонтракта);
	ДанныеОбъекта.Вставить("ДатаОтгрузки", Неопределено);
	
	Если Статус = 1 Тогда
		ДанныеОбъекта.Вставить("НомерИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.НомерИсходногоДокумента),
				дкПолучитьНомерДляПечати(ДокументОбъект,,"НомерИсходногоДокумента"),
				"--"));
		ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.ДатаИсходногоДокумента), ДокументОбъект.ДатаИсходногоДокумента, "--"));
		ДанныеОбъекта.Вставить("ПлатежноРасчетныеДокументы" , ДокументОбъект.ПлатежноРасчетныеДокументы);
	КонецЕсли;
		
	// Свойства
	ДанныеОбъекта.Вставить("Руководитель"     		, дкОтветственноеЛицо(ДокументОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.Руководитель));
	ДанныеОбъекта.Вставить("ГлавныйБухгалтер" 		, дкОтветственноеЛицо(ДокументОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер));
	ДанныеОбъекта.Вставить("Менеджер" 				, дкОтветственноеЛицо(ДокументОбъект, "Менеджер"));
	
	Возврат ДанныеОбъекта;
		
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
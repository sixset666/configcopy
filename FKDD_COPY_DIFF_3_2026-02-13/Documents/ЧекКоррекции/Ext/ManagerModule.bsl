// Модуль менеджера документа "Чек коррекции"

#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//


// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()

#КонецОбласти

#Область Сервисныефункции

Процедура ПроизвестиРаспределениеСуммыОплаты(СуммаДокумента, Товары) Экспорт
	
	Если СуммаДокумента = 0 ИЛИ Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаибольшаяСуммаВсего = 0;
	НомерСтроки = 0;
	
	КоэффициентРаспределения = ?(Товары.Итог("СуммаВсего") = 0, 0, СуммаДокумента / Товары.Итог("СуммаВсего"));
	
	Для Каждого Строка ИЗ Товары Цикл
		Строка.СуммаОплаты = Окр(Строка.СуммаВсего * КоэффициентРаспределения, 2);
		Если Строка.СуммаВсего > НаибольшаяСуммаВсего Тогда
			НаибольшаяСуммаВсего = Строка.СуммаВсего;
			НомерСтроки = Строка.НомерСтроки;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерСтроки > 0 Тогда
		ОшибкаОкругления = СуммаДокумента - Товары.Итог("СуммаОплаты");
		СтрокаТаблицы = Товары.Найти(НомерСтроки, "НомерСтроки");
		//СтрокаТаблицы.СуммаОплаты = СтрокаТаблицы.СуммаОплаты + ?(ОшибкаОкругления <= 0, ОшибкаОкругления, - ОшибкаОкругления);
		СтрокаТаблицы.СуммаОплаты = СтрокаТаблицы.СуммаОплаты + ОшибкаОкругления;
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьВозможностьСозданияНаОсновании(ДанныеЗаполнения, ВызовИзОбработки = Истина, Объект = Неопределено) Экспорт
	
	ТекстОшибки = "";
	ТипДокументаОснования = ТипЗнч(ДанныеЗаполнения);
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат ТекстОшибки;
	КонецЕсли;
	
	ДокументПробит = ЗначениеЗаполнено(ДанныеЗаполнения.ДатаФР);
	
	//проверим, а есть ли уже введенные чеки коррекции для даннного документа.
	Если ЕстьЧекКоррекции(ДанныеЗаполнения) ИЛИ ДокументПробит И ЕстьСторнирующийЧекКоррекции(ДанныеЗаполнения, Объект) Тогда
		ТекстОшибки = НСтр("ru = 'Для данного документа уже есть введенный чек коррекции. Повторная операция невозможна.'");
		Возврат ТекстОшибки;
	КонецЕсли;
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.Выписка") ИЛИ
		 ТипДокументаОснования = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") ИЛИ
		 ТипДокументаОснования = Тип("ДокументСсылка.РасходныйКассовыйОрдер") ИЛИ
		 ТипДокументаОснования = Тип("ДокументСсылка.ЧекНаОплату") Тогда
		
		Если НЕ ДанныеЗаполнения.ДляПробитияНаФР Тогда
			ТекстОшибки = НСтр("ru = 'Запрещено вводить документ на основаниий документа, не предназначенного для пробития на фискальном регистраторе. Установите признак документа ""Для пробития на фискальном регистраторе"".'");
			Возврат ТекстОшибки;
		КонецЕсли;
	КонецЕсли;
	Если ТипДокументаОснования = Тип("ДокументСсылка.ЧекКоррекции") Тогда
		
		Если ДанныеЗаполнения.ЭтоСторно Тогда
			Возврат НСтр("ru = 'Запрещено вводить документ на основании сторнирующего чека коррекции.'");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

// Проверка наличия подчиненного документа чек коррекции.
//
// Параметры:
//  Основание - ДокументСсылка - документ, для которого производится обработка.
//  Объект - ДокументСсылка.ЧекКоррекции - (необязательное) объект, из которого происходит вызов.
//
// Возвращаемое значение:
//  Булево - Истина, если есть введенный сторнирующий чек коррекции на основании переданного документа.
//
Функция ЕстьЧекКоррекции(Основание, Объект = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЧекКоррекции.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекКоррекции КАК ЧекКоррекции
	|ГДЕ
	|	ЧекКоррекции.ДокументОснование = &Основание
	|	И НЕ ЧекКоррекции.ПометкаУдаления
	|	И НЕ ЧекКоррекции.ЭтоСторно";
	
	Если ЗначениеЗаполнено(Объект) Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ЧекКоррекции.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Объект);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// Проверка наличия подчиненного сторнирующего документа чек коррекции.
//
// Параметры:
//  Основание - ДокументСсылка - документ, для которого производится обработка.
//  Объект - ДокументСсылка.ЧекКоррекции - (необязательное) объект, из которого происходит вызов.
//
// Возвращаемое значение:
//  Булево - Истина, если есть введенный сторнирующий чек коррекции на основании переданного документа.
//
Функция ЕстьСторнирующийЧекКоррекции(Основание, Объект = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЧекКоррекции.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекКоррекции КАК ЧекКоррекции
	|ГДЕ
	|	ЧекКоррекции.ДокументОснование = &Основание
	|	И НЕ ЧекКоррекции.ПометкаУдаления
	|	И ЧекКоррекции.ЭтоСторно";
	
	Если ЗначениеЗаполнено(Объект) Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ЧекКоррекции.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Объект);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти


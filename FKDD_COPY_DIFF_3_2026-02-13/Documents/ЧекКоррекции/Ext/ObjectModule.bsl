// Модуль документа "Чек коррекции"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;                              // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;                 // Переменная для накопления результатов проведения по модулям наборов записей
Перем СообщениеОбОшибке Экспорт;                  // Переменная для накопления результатов записи. Если = неопределено, то вывод будет на экран, иначе тут сообщения об ошибках.
Перем ИмяРеквизитаКода Экспорт;                   // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОтключитьПроверкиПередЗаписью Экспорт;      // Флаг отключения проверок (используется при программном формировании документа)
Перем КонтролироватьЗаполнениеОплат;              // Вспомогательная переменная определяющая необходимость выполнения контроля оплат документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт;   // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт;              // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
Перем ОтменитьПроверкуДоступностиСкладов Экспорт; // Булево - отменяет проверку доступности склада в дкДокументы.дкПроверитьДоступностьСкладовКомпании()
                                                  // свойства и методы для расчета скидок документа.

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные реквизиты документа.
	ОбязательныеРеквизиты = Новый Структура();
	
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента", 1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента", 1);
	Если ЗначениеЗаполнено(ДокументОснование) И ЗначениеЗаполнено(ДокументОснование.Контрагент) Тогда 
		ОбязательныеРеквизиты.Вставить("Контрагент", 1);
		Попытка
			Если ЗначениеЗаполнено(ДокументОснование.ДоговорВзаиморасчетов) Тогда
				ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов", 1);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("ТипЦен", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
	ОбязательныеРеквизиты.Вставить("СуммаДокумента", 1);
	ОбязательныеРеквизиты.Вставить("ПризнакСпособаРасчета", 1);
	ОбязательныеРеквизиты.Вставить("ТипРасчета", 1);
	ОбязательныеРеквизиты.Вставить("ТипКоррекции", 1);
	ОбязательныеРеквизиты.Вставить("ДатаКоррекции", 1);
	ОбязательныеРеквизиты.Вставить("КассаККМ", 1);
	Если ТипКоррекции = Перечисления.ТипыЧековКоррекции.ПоПредписанию Тогда
		ОбязательныеРеквизиты.Вставить("НомерПредписания", 1);
	КонецЕсли;
	
	// Обязательные поля таблицы "Товары"
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура", 3);
	ОбязательныеТовары.Вставить("Количество", 1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения", 3);
	ОбязательныеТовары.Вставить("Коэффициент", 1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры", 2);
	ОбязательныеТовары.Вставить("ГТД", 2);
	ОбязательныеТовары.Вставить("ДоговорВзаиморасчетов", 2);
	Если обПраво("ЗапретитьПодарки", Права, , ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("СуммаВсего", 1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	
	// Обязательные поля таблицы "Оплаты"
	ОбязательныеОплаты=Новый Структура();
	ОбязательныеОплаты.Вставить("ТипОплаты", 3);
	ОбязательныеОплаты.Вставить("Сумма", 1);
	ОбязательныеОплаты.Вставить("ТипПлатежнойКарты", 2);
	ОбязательныеОплаты.Вставить("Карточка", 2);
	ОбязательныеОплаты.Вставить("ДоговорВзаиморасчетов", 2);
	ОбязательныеРеквизиты.Вставить("Оплаты", ОбязательныеОплаты);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки", 3);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаККМ");
			КонецЕсли; 			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	// Если задача контроля заполнения не стоит, выходим
	Если НЕ Заполнение Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Проконтролируем корректность заполнения фискальных реквизитов документа
	Если НЕ(обЗначениеНеЗаполнено(ФР.КлассОборудования) ИЛИ ФР.КлассОборудования=Перечисления.КлассыОборудования.ФР) Тогда
		дкДобавитьОшибку(Ошибки, "Реквизит <ФР> класс оборудования должен быть фискальным регистратором !");
		Результат = ЛОЖЬ;
	КонецЕсли;
	
	// Проводим инициализацию основных 
	ОплатыВсего = Оплаты.Итог("Сумма");
	ОплатыТалон = 0;
	ОплатыБезнал= 0;
	СтрокаТалон = Неопределено;
	
	// Выполним проверку корректности заполнения реквизит таблицы оплат
	Для Каждого ТекОплата Из Оплаты Цикл
		Если НЕ ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Наличные Тогда
			ОплатыБезнал = ОплатыБезнал + ТекОплата.Сумма;
		КонецЕсли;
		Если ЭтоСторно Тогда
			Продолжить;
		КонецЕсли;
		Если ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Безналичные Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.ТипПлатежнойКарты) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Тип платежной карты> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Кредит Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			ИначеЕсли обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			ИначеЕсли НЕ ТекОплата.ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.Кредит Тогда
				дкДобавитьОшибку(Ошибки,"Таблица <Оплаты> вид договора оплаты не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Талон Тогда
			Если НЕ ТекОплата.Карточка.ВидКарточки=Перечисления.ВидыКарточек.Талон Тогда
				дкДобавитьОшибку(Ошибки,"Таблица <Оплаты> вид карточки не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			ОплатыТалон = ОплатыТалон + ТекОплата.Сумма;
			СтрокаТалон = ТекОплата;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.КлубнаяКарта Тогда
			Если НЕ ТекОплата.Карточка.ВидКарточки=Перечисления.ВидыКарточек.КлубнаяКарта Тогда
				дкДобавитьОшибку(Ошибки,"Таблица <Оплаты> вид карточки не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			ИначеЕсли обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Проводим контроль сумм оплат
	Если НЕ КонтролироватьЗаполнениеОплат Тогда
		// Проверку корректности сумм оплат производить сейчас не нужно
		
	ИначеЕсли СуммаДокумента>ОплатыВсего Тогда
		дкДобавитьОшибку(Ошибки, "Общая сумма оплат не может быть менее суммы документа !");
		Результат = ЛОЖЬ;
		
	ИначеЕсли СуммаДокумента<ОплатыВсего Тогда
		// Определим причину превышения оплаты
		Если ОплатыВсего=ОплатыТалон Тогда
			Если (ОплатыВсего-СуммаДокумента)>=СтрокаТалон.Карточка.Номинал Тогда
				дкДобавитьОшибку(Ошибки, "Сумма оплаты талонами, превышает необходимую !");
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли (НЕ ОплатыБезнал=ОплатыТалон) И СуммаДокумента<ОплатыБезнал Тогда
			дкДобавитьОшибку(Ошибки, "Сумма безналичной оплаты не может превышать сумму документа !");
			Результат = ЛОЖЬ;
			
		Иначе
			дкДобавитьОшибку(Ошибки, "Общая сумма оплат не может быть больше суммы документа !");
			Результат = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
		дкДобавитьОшибку(Ошибки, ТекстОшибки);
		Результат = Ложь;
	КонецЕсли;
 
	Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
		дкДобавитьОшибку(Ошибки, ТекстОшибки);
		Результат = Ложь;
	КонецЕсли;
	
	// Проверим количество маркировок и количество товара в строке
	Если ХозОперация = Справочники.ХозОперации.ЧекНаОплату
		ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаВозврат Тогда
		// Отключим некоторые проверки
		РеализацияТовара = ХозОперация = Справочники.ХозОперации.ЧекКоррекции
			И (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")
			ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров"));
		Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки, НЕ РеализацияТовара);
	КонецЕсли;
	
	// Возвращаем результат проверки
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЧекКоррекции","Чек коррекции",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет общей суммы амортизации активов
// 
// Возвращаемое значение:
//  Возвращает итог по колонке "СуммаВсего".
//
Функция РассчитатьСуммуВсего() Экспорт
	НеПересчитыватьСуммуДокумента=Ложь;
	Если НЕ ДополнительныеСвойства.Свойство("НеПересчитыватьСуммуДокумента",НеПересчитыватьСуммуДокумента) Тогда
		НеПересчитыватьСуммуДокумента=Ложь;
	КонецЕсли; 
	
	Возврат ?(НеПересчитыватьСуммуДокумента, СуммаДокумента, Товары.Итог("СуммаВсего"));
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Карточка" Тогда
		Если НЕ Карточка.Пустая() Тогда
			Если Карточка.ВидКарточки<>Перечисления.ВидыКарточек.ДисконтнаяКарта Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранная карточка не является дисконтной!");
				#КонецЕсли
				Карточка=Неопределено;
				ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(Карточка.Объект) Тогда
				Если Контрагент<>Карточка.Объект Тогда
					//В документе выбран другой контрагент - заменяем на владельца карточки
					Контрагент=Карточка.Объект;
					Возврат ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (НЕ Контрагент.Пустая()) И (НЕ Карточка.Пустая()) И ((Карточка.Объект<>Неопределено) И (НЕ Карточка.Объект.Пустая())) И (Контрагент<>Карточка.Объект)  Тогда
			Карточка=Неопределено;
			Возврат ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="КассаККМ" Тогда
		Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
			Возврат Истина;
		КонецЕсли;
		#Если Клиент Тогда
		Рез=Истина;
		Если ЭтаФорма<>Неопределено Тогда
			Если КассаККМ.Организация<>Организация Тогда
				Если Вопрос("Организация кассы отлична от текущей организации документа. Установить организацию документа в соответствие с организацией кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					Организация=КассаККМ.Организация;
					Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.Подразделение<>ПодразделениеКомпании Тогда
				Если Вопрос("Подразделение кассы отлично от текущего подразделения документа. Установить подразделение документа в соответствии с подразделением кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ПодразделениеКомпании=КассаККМ.Подразделение;
					Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента Тогда
				Если Вопрос("Валюта кассы отлична от текущей валюты документа. Установить валюту документа в соответствие с валютой кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
					Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли;
					Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЕсли; 
		Иначе
			Организация=КассаККМ.Организация;
			Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
			ПодразделениеКомпании=КассаККМ.Подразделение;
			Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
			ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
			Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		КонецЕсли; 
		#Иначе
		Организация=КассаККМ.Организация;
		Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
		ПодразделениеКомпании=КассаККМ.Подразделение;
		Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
		ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
		Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ФР" Тогда
		Если НЕ обЗначениеНеЗаполнено(ФР) Тогда
			Если ФР.КлассОборудования<>Перечисления.КлассыОборудования.ФР Тогда
				#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Предупреждение("Выбранное оборудование: " + ФР + " не является фискальным регистратором!
					|Выберите фискальный регистратор",10);
				КонецЕсли; 
				#КонецЕсли
				ФР=Неопределено;
				Возврат ОбработкаРеквизита("ФР",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="Сделка" Тогда
		Если ТипЗнч(Сделка) = Тип("ДокументСсылка.СчетНаОплату") Тогда
			ДоговорВзаиморасчетов = Сделка.ДоговорВзаиморасчетов;
			СуммаДокумента = обПересчет(Сделка.СуммаДокумента,Сделка.ВалютаДокумента,Дата,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		Если ЗначениеЗаполнено(Сделка) Тогда
			дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, Сделка, СуммаДокумента);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя = "СкидкаНаценка"
		  ИЛИ Имя = "ТипЦен" 
		  ИЛИ Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		СуммаДокумента = РассчитатьСуммуВсего();
		
		Если Имя = "Товары.Номенклатура" Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
				ТекСтрока.ПризнакПредметаРасчета = ТекСтрока.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
			Иначе
				ТекСтрока.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		Возврат Рез И ОбработкаРеквизита("СуммаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СуммаДокумента" Тогда
		Если НЕ СтавкаНДС.Пустая() Тогда
			Если СуммаДокумента = Товары.Итог("СуммаВсего") Тогда
				СуммаНДС = Товары.Итог("СуммаНДС");	
			Иначе
				СуммаНДС = Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СтавкаНДС" Тогда
		Если НЕ СтавкаНДС.Пустая() Тогда
			СуммаНДС = Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Найти(Имя, "Товары.") > 0 Тогда 
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаСкидкиНаценки = Товары.Итог("СуммаСкидки");
		СуммаСкидкиНаценки = СуммаСкидкиНаценки + Товары.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиНаценки = СуммаСкидкиНаценки + Товары.Итог("СуммаСкидкиБонусами");
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ОплатаЧерезКассу" Тогда
		#Если Клиент Тогда
			Если ЭтотОбъект.ЭтоНовый() ИЛИ ЭтотОбъект.Модифицированность() Тогда
				Предупреждение("Перед выполнением операции необходимо записать документ!");
				РезультатОбработки = ЛОЖЬ;
			Иначе
				Обработки.ФронтКассира.Создать().ОплатитьЧекКоррекции(ЭтотОбъект.Ссылка);
			КонецЕсли;
		#КонецЕсли
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Проверим наличие марировок в документ-основание
Функция ВедетсяУчетКодовМаркировок() Экспорт
	
	// Проверим наличие ТЧ Маркировки
	Возврат (НЕ ЗначениеЗаполнено(ДокументОснование))
		ИЛИ ((НЕ ЗначениеЗаполнено(ДокументОснование)
		ИЛИ (ЗначениеЗаполнено(ДокументОснование)
		И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.Чек")
		И НЕ ЗначениеЗаполнено(ДокументОснование.ДокументОснование))));
	
КонецФункции

Процедура ЗаполнитьЕдиницуИзмеренияДляАвтомобилей(Строка)
	
	ИспользованиеЕдиницИзмерения = Строка.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения;
	Если ЗначениеЗаполнено(Строка.ЕдиницаИзмерения) Тогда
		Если (ИспользованиеЕдиницИзмерения = 1 И Строка.ЕдиницаИзмерения.Владелец <> Строка.Номенклатура.ТипНоменклатуры)
			ИЛИ (ИспользованиеЕдиницИзмерения = 2 И Строка.ЕдиницаИзмерения.Владелец <> Строка.Номенклатура) Тогда
			Строка.ЕдиницаИзмерения = Строка.Номенклатура.ОсновнаяЕдиницаИзмерения;
		КонецЕсли;
	Иначе
		Строка.ЕдиницаИзмерения = Строка.Номенклатура.ОсновнаяЕдиницаИзмерения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьРеквизитыФискальногоРегистратора()
	
	НомерЧека=Неопределено;
	НомерДокумента=Неопределено;
	НомерПредписания=Неопределено;
	ДатаФР=Неопределено;
	НомерСмены=Неопределено;
	ФискальныйПризнак=Неопределено;
	ПробитЧек=ЛОЖЬ;
	
КонецПроцедуры

Функция ПолучитьСистемуНалогообложения(ДаннаяОрганизация, Знач ДанноеПодразделение)
	Результат = Неопределено;
	Если ЗначениеЗаполнено(ДанноеПодразделение) Тогда
		Пока НЕ ЗначениеЗаполнено(Результат) Цикл
			Результат=ДанноеПодразделение.Организация.СистемаНалогообложения;
			ДанноеПодразделение = ДанноеПодразделение.Родитель;
			Если Не ЗначениеЗаполнено(ДанноеПодразделение) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Результат = ДаннаяОрганизация.Результат;
	КонецЕсли;
	
	Если Результат = Перечисления.СистемыНалогообложения.Общая Тогда
		Результат = Перечисления.ТипыСистемНалогообложенияККТ.ОСН;
	ИначеЕсли Результат = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		Результат = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	// Установим значение вспомогательного реквизита (используется для отбора документов в журнале документов на оплату)
	ДляПробитияНаФР = ИСТИНА;
	ПробитЧек = ЛОЖЬ;
	
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Если Основание.Свойство("ДокументОснование") Тогда
			ДокументОснование = Основание.ДокументОснование;
			Если Основание.Свойство("ЭтоСторно") Тогда
				ЭтоСторно = Основание.ЭтоСторно;
			КонецЕсли;
			Основание = ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	
	ТекстСообщения = Документы.ЧекКоррекции.ПроверитьВозможностьСозданияНаОсновании(Основание);
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
		Сообщить(ТекстСообщения,СтатусСообщения.Внимание);
		дкОтменаВводаДокумента(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	// Вызываем стандартный обработчик
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ТипЗнч(Основание);
	
	Если ТипОснования = Тип("ДокументСсылка.Выписка") Тогда
		ОбработкаЗаполнения_Выписка(Основание);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		ОбработкаЗаполнения_ПриходныйКассовыйОрдер(Основание);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		ОбработкаЗаполнения_РасходныйКассовыйОрдер(Основание);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.Чек") Тогда
		ОбработкаЗаполнения_Чек(Основание);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЧекКоррекции") Тогда
		ОбработкаЗаполнения_ЧекКоррекции(Основание);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЧекНаОплату") Тогда
		ОбработкаЗаполнения_ЧекНаОплату(Основание);
	КонецЕсли;
	
	СистемаНалогообложения = ПолучитьСистемуНалогообложения(Организация, ПодразделениеКомпании);
	ОчиститьРеквизитыФискальногоРегистратора();
	
	Если ЗначениеЗаполнено(Основание) Тогда
		Попытка
			ДатаКоррекции = Основание.ДатаФР;
		Исключение
		КонецПопытки;
		Если НЕ ЗначениеЗаполнено(ДатаКоррекции) Тогда
			ДатаКоррекции = Основание.Дата;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаКоррекции) Тогда
		ДатаКоррекции = Дата;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипКоррекции) Тогда
		ТипКоррекции = Перечисления.ТипыЧековКоррекции.Самостоятельно;
	КонецЕсли;
	
	Попытка
		ДополнительныйРеквизитЧека = Основание.ФискальныйПризнак;
	Исключение
	КонецПопытки;
	
	Если ЭтоСторно Тогда
		ТипРасчета = ОбратнаяОперацияТипаРасчета(ТипРасчета);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) И ЗначениеЗаполнено(Основание.КассаККМ) Тогда
		КассаККМ = Основание.КассаККМ;
	КонецЕсли;
	
	// Производим установку реквизита "Касса ККМ" в значение по умолчанию
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		КассаККМ = ПараметрыСеанса.Компьютер.КассаККМ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) И ЗначениеЗаполнено(Основание.ФР) Тогда
		ФР = Основание.ФР;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Производит заполнение объекта на основании документа "Выписка"
//
// Параметры:
//  Основание     - ДокументСсылка - Ссылка на документ для заполнения.
//
// Возвращаемое значение:
//  Булево - Признак возможности дальнейшей обработки события.
//
Функция ОбработкаЗаполнения_Выписка(Основание) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Товары.Количество() = 0 Тогда
		ОснованиеОбъект = Основание.ПолучитьОбъект();
		дкПодготовитьТаблицуТоваровСервер(ОснованиеОбъект);
		Для Каждого Строка Из ОснованиеОбъект.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ПризнакПредметаРасчета = НоваяСтрока.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
			НоваяСтрока.СуммаВсего = НоваяСтрока.СуммаОплаты;
			ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
		КонецЦикла;
		Для Каждого Строка Из ОснованиеОбъект.КодыМаркировки Цикл
			НоваяСтрока = КодыМаркировки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Строка Из Товары Цикл
		ЗаполнитьЕдиницуИзмеренияДляАвтомобилей(Строка);
	КонецЦикла;
	
	СуммаДокумента = Макс(Основание.СуммаДокументаПриход - Основание.СуммаДокументаРасход, -(Основание.СуммаДокументаПриход - Основание.СуммаДокументаРасход));
	СуммаНДС = Основание.СуммаНДС;
	
	// Заполним оплату
	НоваяСтрока = Оплаты.Добавить();
	НоваяСтрока.Сумма = СуммаДокумента;
	НоваяСтрока.ТипОплаты = Справочники.ТипыОплат.Безналичные;
	
	// Определим признак способа расчета
	ПризнакСпособаРасчета = Основание.ТипСпособаРасчетаККТ;
	
	// Получена сумма оплаты, пересчет не требуется
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Возвращаем признак возможности дальнейшей обработки события
	Возврат Истина;
	
КонецФункции // ОбработкаЗаполнения_Выписка()

// Производит заполнение объекта на основании документа "Приходный кассовый ордер"
//
// Параметры:
//  Основание     - ДокументСсылка - Ссылка на документ для заполнения.
//
// Возвращаемое значение:
//  Булево - Признак возможности дальнейшей обработки события.
//
Функция ОбработкаЗаполнения_ПриходныйКассовыйОрдер(Основание) Экспорт
	
	// Если данные заполнения не указаны, значит производиться проверка существования обработчика заполнения у объекта.
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Товары.Количество() = 0 Тогда
		ОснованиеОбъект = Основание.ПолучитьОбъект();
		дкПодготовитьТаблицуТоваровСервер(ОснованиеОбъект);
		Для Каждого Строка Из ОснованиеОбъект.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		Для Каждого Строка Из ОснованиеОбъект.КодыМаркировки Цикл
			НоваяСтрока = КодыМаркировки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;

	СуммаДокумента = Основание.СуммаДокумента;
	СуммаНДС = Основание.СуммаНДС;
	
	Если НЕ ЗначениеЗаполнено(ТипРасчета) Тогда
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	КонецЕсли;
	
	// Заполним оплату
	НоваяСтрока = Оплаты.Добавить();
	НоваяСтрока.Сумма = СуммаДокумента;
	НоваяСтрока.ТипОплаты = Справочники.ТипыОплат.Наличные;
	
	// Определим признак способа расчета
	ПризнакСпособаРасчета = Основание.ТипСпособаРасчетаККТ;
	
	// Получена сумма оплаты, пересчет не требуется
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Возвращаем признак возможности дальнейшей обработки события
	Возврат Истина;
	
КонецФункции // ОбработкаЗаполнения_ПриходныйКассовыйОрдер()

// Производит заполнение объекта на основании документа "Расходный кассовый ордер"
//
// Параметры:
//  Основание     - ДокументСсылка - Ссылка на документ для заполнения.
//
// Возвращаемое значение:
//  Булево - Признак возможности дальнейшей обработки события.
//
Функция ОбработкаЗаполнения_РасходныйКассовыйОрдер(Основание) Экспорт
	
	// Если данные заполнения не указаны, значит производиться проверка существования обработчика заполнения у объекта.
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Товары.Количество() = 0 Тогда
		ОснованиеОбъект = Основание.ПолучитьОбъект();
		дкПодготовитьТаблицуТоваровСервер(ОснованиеОбъект);
		Для Каждого Строка Из ОснованиеОбъект.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		Для Каждого Строка Из ОснованиеОбъект.КодыМаркировки Цикл
			НоваяСтрока = КодыМаркировки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	
	СуммаДокумента = Основание.СуммаДокумента;
	СуммаНДС = Основание.СуммаНДС;
	
	// Заполним оплату
	НоваяСтрока = Оплаты.Добавить();
	НоваяСтрока.Сумма = СуммаДокумента;
	НоваяСтрока.ТипОплаты = Справочники.ТипыОплат.Наличные;
	
	// Определим признак способа расчета
	ПризнакСпособаРасчета = Основание.ТипСпособаРасчетаККТ;
	
	// Получена сумма оплаты, пересчет не требуется
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Возвращаем признак возможности дальнейшей обработки события
	Возврат Истина;
	
КонецФункции // ОбработкаЗаполнения_РасходныйКассовыйОрдер()

// Производит заполнение объекта на основании документа "Чек"
//
// Параметры:
//  Основание     - ДокументСсылка - Ссылка на документ для заполнения.
//
// Возвращаемое значение:
//  Булево - Признак возможности дальнейшей обработки события.
//
Функция ОбработкаЗаполнения_Чек(Основание) Экспорт
	
	// Если данные заполнения не указаны, значит производиться проверка существования обработчика заполнения у объекта.
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Перезаполним ТЧ Товары из документа-основания
	Товары.Загрузить(Основание.Товары.Выгрузить());
	ТипРасчета = ТипРасчетаЧека(Основание);
	
	СуммаДокумента = Основание.СуммаДокумента;
	СуммаНДС = Товары.Итог("СуммаНДС");
	
	// Определим признак способа расчета
	ПризнакСпособаРасчета = Перечисления.ТипыСпособаРасчетаККТ.Передача;
	
	// Получена сумма оплаты, пересчет не требуется
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Возвращаем признак возможности дальнейшей обработки события
	Возврат Истина;
	
КонецФункции // ОбработкаЗаполнения_Чек()

// Производит заполнение объекта на основании документа "Чек коррекции"
//
// Параметры:
//  Основание     - ДокументСсылка - Ссылка на документ для заполнения.
//
// Возвращаемое значение:
//  Булево - Признак возможности дальнейшей обработки события.
//
Функция ОбработкаЗаполнения_ЧекКоррекции(Основание) Экспорт
	
	// Если данные заполнения не указаны, значит производиться проверка существования обработчика заполнения у объекта.
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Основание.ЭтоСторно Тогда
		Сообщить(НСтр("ru = 'Запрещено вводить Чек коррекции на основании сторнирующего чека коррекции '"),СтатусСообщения.Внимание);
		дкОтменаВводаДокумента(ЭтотОбъект);
		Возврат Ложь;
	КонецЕсли;
	
	СуммаДокумента = Основание.СуммаДокумента;
	СуммаНДС = Основание.СуммаНДС;
	
	// Определим признак способа расчета
	ПризнакСпособаРасчета = Основание.ПризнакСпособаРасчета;
	
	// Получена сумма оплаты, пересчет не требуется
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Возвращаем признак возможности дальнейшей обработки события
	Возврат Истина;
	
КонецФункции // ОбработкаЗаполнения_ЧекКоррекции()

// Производит заполнение объекта на основании документа "Чек на оплату"
//
// Параметры:
//  Основание     - ДокументСсылка - Ссылка на документ для заполнения.
//
// Возвращаемое значение:
//  Булево - Признак возможности дальнейшей обработки события.
//
Функция ОбработкаЗаполнения_ЧекНаОплату(Основание) Экспорт
	
	// Если данные заполнения не указаны, значит производится проверка существования обработчика заполнения у объекта.
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ТипРасчета = ТипРасчетаЧека(Основание);
	
	СуммаДокумента = Основание.СуммаДокумента;
	СуммаНДС = Основание.СуммаНДС;
	
	// Определим признак способа расчета
	ПризнакСпособаРасчета = ДокументОснование.ТипСпособаРасчетаККТ;
	
	// Получена сумма оплаты, пересчет не требуется
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Возвращаем признак возможности дальнейшей обработки события
	Возврат Истина;
	
КонецФункции // ОбработкаЗаполнения_ЧекНаОплату()

// Получение типа расчета денежными средствами
//
// Параметры:
//  Объект - ДокументСсылка   - Объект, для которого выполняется получение типа расчета.
//
Функция ТипРасчетаЧека(Объект)
	
	ХозОперацияОбъект = Объект.ХозОперация;
	
	// У документа установлен тип расчета.
	Если дкДокументЕстьРеквизитШапки(Объект, "ТипРасчета") И ЗначениеЗаполнено(Объект.ТипРасчета) Тогда
		ТипРасчетаОбъект = Объект.ТипРасчета;
	ИначеЕсли ХозОперацияОбъект = ПредопределенноеЗначение("Справочник.ХозОперации.ЧекНаОплатуПокупкиВозврат") Тогда
		ТипРасчетаОбъект = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств");
	ИначеЕсли ХозОперацияОбъект = ПредопределенноеЗначение("Справочник.ХозОперации.ЧекНаОплатуВозврат")
		ИЛИ ХозОперацияОбъект = ПредопределенноеЗначение("Справочник.ХозОперации.ЧекНаВозврат")
		ИЛИ ХозОперацияОбъект = ПредопределенноеЗначение("Справочник.ХозОперации.ВозвратТоваровОтПокупателя") Тогда
		ТипРасчетаОбъект = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств");
	ИначеЕсли ХозОперацияОбъект = ПредопределенноеЗначение("Справочник.ХозОперации.ЧекНаОплатуПокупки") Тогда
		ТипРасчетаОбъект = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств");
	Иначе
		ТипРасчетаОбъект = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств");
	КонецЕсли;
	
	Возврат ТипРасчетаОбъект;
	
КонецФункции

Функция ОбратнаяОперацияТипаРасчета(Операция)
	
	Если Операция = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда
		Возврат Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств;
	ИначеЕсли Операция = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств Тогда
		Возврат Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств;
	КонецЕсли;
	
	Возврат Операция;
	
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	
	// Установим значение вспомогательного реквизита (используется для отбора документов в журнале документов на оплату)
	ДляПробитияНаФР = ИСТИНА;
	ПробитЧек = ЛОЖЬ;
	
	// Вызываем стандартный обработчик
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьРеквизитыФискальногоРегистратора();
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Вызываем общий обработчик события
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	СуммаНаличными = 0;
	Для Каждого СтрокаОплат Из Оплаты Цикл
		Если СтрокаОплат.ТипОплаты = Перечисления.ТипыОплатыККТ.Наличные Тогда
			СуммаНаличными = СуммаНаличными + СтрокаОплат.Сумма;
		КонецЕсли;
	КонецЦикла;
	СуммаПрочие = Оплаты.Итог("Сумма") - СуммаНаличными;
	//
	//ТекстСообщения = "";
	//
	Если НЕ ЭтоСторно Тогда
	//	
	//	ПризнакСпособаРасчетаПоТабличнойЧасти = Перечисления.ПризнакиСпособаРасчета.ПолучитьПризнакСпособаРасчета(
	//		ЭтотОбъект,
	//		Товары,
	//		ПризнакСпособаРасчета,
	//		ТекстСообщения
	//	);
	//	Если НЕ ПризнакСпособаРасчета = ПризнакСпособаРасчетаПоТабличнойЧасти Тогда
	//		ТекстСообщения = НСтр("ru='Неверно заполнен ""Способ расчета"".'") + " " + ТекстСообщения;
	//		ВывестиСообщение(ТекстСообщения, , "ПризнакСпособаРасчета", , Отказ);
	//	КонецЕсли;
	//	
	//	Если ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой
	//		ИЛИ ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСЧастичнойОплатой
	//		ИЛИ ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаБезОплаты Тогда
	//			
	//		Если СуммаДокумента <> Товары.Итог("СуммаВсего") Тогда
	//			ВывестиСообщение(НСтр("ru='Сумма товарных позиций больше суммы оплат'"), , , , Отказ);
	//		ИначеЕсли СуммаДокумента < СуммаПрочие Тогда
	//			ВывестиСообщение(
	//				НСтр("ru = 'Неверно заполнена колонка ""Сумма"" в табличной части оплаты, сумма безналичных платежей больше суммы оплаты'"),
	//				, , ,
	//				Отказ
	//			);
	//		ИначеЕсли СуммаДокумента > Оплаты.Итог("Сумма") Тогда
	//			ВывестиСообщение(НСтр("ru='Неверно заполнена колонка ""Сумма"" в табличной части оплаты'"), , , , Отказ);
	//		КонецЕсли;
	//		
	//	КонецЕсли;
	//	
		Если СуммаДокумента > 0 И Товары.Итог("СуммаОплаты") = 0 И Товары.Количество() > 0 Тогда
			Сообщить(
				НСтр("ru='Необходимо произвести пропорциональное распределение суммы вносимой оплаты между предметами платежа'"));
				Отказ = Истина;
		ИначеЕсли СуммаДокумента <> Товары.Итог("СуммаОплаты") И Товары.Количество() > 0 Тогда
			Сообщить(
				НСтр("ru='Неверно заполнена колонка ""Сумма оплаты"" в табличной части товары, необходимо распределить сумму оплаты'"));
				Отказ = Истина;
		//ИначеЕсли СуммаДокумента <> (Оплаты.Итог("Сумма") - Оплаты.Итог("Сдача")) Тогда
		//	Сообщить(НСтр("ru='Неверно заполнена колонка ""Сумма"" в табличной части оплаты'"));
		//	Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	//// Очистим результаты проверки кодов маркировки
	//МаркировкаТоваровСервер.ОчиститьДанныеПроверкиКодовМаркировки(ЭтотОбъект);
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)

	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
ОтключитьПроверкиПередЗаписью = Ложь;
КонтролироватьЗаполнениеОплат = ИСТИНА;
#Если Клиент Тогда
	Права = глПрава;
	ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
	ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
#КонецЕсли 

СообщениеОбОшибке=Неопределено;

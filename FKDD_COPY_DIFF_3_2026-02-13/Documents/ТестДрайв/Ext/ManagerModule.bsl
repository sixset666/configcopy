// Формирование набора записей регистра "График работы ресурсов" по тест-драйву
// Параметры
//   Ссылка	  – Ссылка на документ тест-драйв.
// Возвращаемое значение:
//   Булево   – Результат записи набора записей в регистр
Функция ЗаполнитьГрафикРаботыАвтомобилей(Ссылка) Экспорт
	// Очистим регистр ГрафикРаботыРесурсов
	НаборЗаписейГрафикРаботыРесурсов = РегистрыСведений.ГрафикРаботыРесурсов.СоздатьНаборЗаписей();
	НаборЗаписейГрафикРаботыРесурсов.ДокументОбъект = Ссылка;
	НаборЗаписейГрафикРаботыРесурсов.ОтменаПроведения();
	
	НаборЗаписей = Новый ТаблицаЗначений;
	НаборЗаписей.Колонки.Добавить("Ресурс1", Новый ОписаниеТипов("СправочникСсылка.Автомобили"));
	НаборЗаписей.Колонки.Добавить("Ресурс2", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	НаборЗаписей.Колонки.Добавить("Объект");
	НаборЗаписей.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НаборЗаписей.Колонки.Добавить("НачалоРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("КонецРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("Продолжительность", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("КоличествоНатуральныхЕдиниц", Новый ОписаниеТипов("Число"));
	НаборЗаписей.Колонки.Добавить("НапомнитьЗа", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("ХозОперация", Новый ОписаниеТипов("СправочникСсылка.ХозОперации"));
	НаборЗаписей.Колонки.Добавить("Авторабота",Новый ОписаниеТипов("СправочникСсылка.Автоработы"));
	
	//определим дату начала
	ДатаНачала=Ссылка.ДатаНачала;
	Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
		Попытка
			ДатаНачала=Ссылка.ДатаСоздания;
		Исключение КонецПопытки; 
	ИначеЕсли обЗначениеНеЗаполнено(НачалоДня(ДатаНачала)) Тогда
		Попытка
			ДатаНачала=НачалоДня(Ссылка.ДатаСоздания)+Час(ДатаНачала)*60*60+Минута(ДатаНачала)*60+Секунда(ДатаНачала);
		Исключение КонецПопытки; 
	КонецЕсли; 
	Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
		ДатаНачала=Ссылка.Дата;
	ИначеЕсли обЗначениеНеЗаполнено(НачалоДня(ДатаНачала)) Тогда
		ДатаНачала=НачалоДня(Ссылка.Дата)+Час(ДатаНачала)*60*60+Минута(ДатаНачала)*60+Секунда(ДатаНачала);
	КонецЕсли;
	
	//определим дату окончания
	ДатаОкончания=Ссылка.ДатаОкончания;
	Если обЗначениеНеЗаполнено(ДатаОкончания) ИЛИ обЗначениеНеЗаполнено(НачалоДня(ДатаОкончания)) Тогда
		Сообщить("Не заполнена дата окончания тест-драйва!",СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли; 
	Если ДатаОкончания=ДатаНачала Тогда 
		Сообщить("Дата начала и дата окончания тест-драйва совпадают!",СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли; 
	
	Если НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала) = 0 Тогда
		// Начало окончание в одном дне
		СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
		СтрокаГрафикаРаботыРесурсов.Ресурс1 = Ссылка.Автомобиль;
		СтрокаГрафикаРаботыРесурсов.Ресурс2 = Ссылка.Менеджер;
		СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
		СтрокаГрафикаРаботыРесурсов.Дата = НачалоДня(ДатаНачала);
		СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
		СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101' + (ДатаОкончания - НачалоДня(ДатаОкончания));
		СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени - СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени);
		СтрокаГрафикаРаботыРесурсов.ХозОперация = Ссылка.ХозОперация;
	Иначе
		Разница = (НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала))/60/60/24;
		ТекущийДень = НачалоДня(ДатаНачала);
		Пока Разница >= 0 Цикл
			СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
			СтрокаГрафикаРаботыРесурсов.Ресурс1 = Ссылка.Автомобиль;
			СтрокаГрафикаРаботыРесурсов.Ресурс2 = Ссылка.Менеджер;
			СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
			СтрокаГрафикаРаботыРесурсов.Дата = ТекущийДень;					
			// Установим НачалоРабочегоВремени 
			Если ТекущийДень = НачалоДня(ДатаНачала) Тогда
				СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
			Иначе
				СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101';
			КонецЕсли;
			// Установим КонецРабочегоВремени
			Если ТекущийДень = НачалоДня(ДатаОкончания) Тогда
				СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101' + (ДатаОкончания - НачалоДня(ДатаОкончания));
			Иначе
				СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101235959';
			КонецЕсли;
			// Посчитаем Продолжительность
			СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени - СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени);
			СтрокаГрафикаРаботыРесурсов.ХозОперация = Ссылка.ХозОперация;
			
			ТекущийДень = ТекущийДень + 1*60*60*24;
			Разница = Разница - 1;
		КонецЦикла;
	КонецЕсли;
	
	НаборЗаписейГрафикРаботыРесурсов.НаборЗаписей = НаборЗаписей;
	НаборЗаписейГрафикРаботыРесурсов.Проведение();
	
	Возврат Истина;
КонецФункции // орГрафикРаботыРесурсовПоЗаказНаряду() 

// Возвращает структуру, которая содержит анкеты тест-драйва по данной модели
// Параметры
//   Модель	– Модель автомобиля.
//   Дата	– Дата получения значения
// Возвращаемое значение:
//   Структура  - содержит 2 записи (предварительная и итоговая анкета)
Функция ПолучитьАнкетыТестДрайва(Дата,Модель) Экспорт

	ПредАнкета = Неопределено;
	ИтогАнкета = Неопределено;
	
	//получим анкеты для текущей модели автомобиля
	Если ЗначениеЗаполнено(Модель) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	НастройкиТестДрайваСрезПоследних.Модель КАК Модель,
		               |	НастройкиТестДрайваСрезПоследних.АнкетаПредварительная КАК АнкетаПредварительная,
		               |	НастройкиТестДрайваСрезПоследних.АнкетаИтоговая КАК АнкетаИтоговая
		               |ИЗ
		               |	РегистрСведений.НастройкиТестДрайва.СрезПоследних(&Дата, Модель = &Модель) КАК НастройкиТестДрайваСрезПоследних";
		Запрос.УстановитьПараметр("Дата",Дата);
		Запрос.УстановитьПараметр("Модель",Модель);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество()>0 Тогда
			Выборка.Следующий();
			ПредАнкета = Выборка.АнкетаПредварительная;
			ИтогАнкета = Выборка.АнкетаИтоговая;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ПредАнкета",ПредАнкета);
	СтруктураВозврата.Вставить("ИтогАнкета",ИтогАнкета);
	Возврат СтруктураВозврата;
	
КонецФункции // ПолучитьАнкетыТестДрайва()

// Возвращает структуру, которая содержит ссылки на опросы по данному тест-драйву
// Параметры
//   ДокументСсылка	– Ссылка на документ тест-драйв.
//   ПредАнкета	– Предварительная анкета для данной модели
//   ИтогАнкета	– Итоговая анкета для данной модели
// Возвращаемое значение:
//   Структура  - содержит 2 записи (предварительный и итоговый опрос)
Функция ПолучитьОпросыТестДрайва(ДокументСсылка,ПредАнкета,ИтогАнкета) Экспорт
	
	ПредОпрос = Неопределено;
	ИтогОпрос = Неопределено;
	
	//получим анкетные опросы, введенные на основании текущего документа
	Если ЗначениеЗаполнено(ПредАнкета) ИЛИ ЗначениеЗаполнено(ИтогАнкета) Тогда
		Если ПредАнкета = ИтогАнкета Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	Опрос.Ссылка
			               |ИЗ
			               |	Документ.Опрос КАК Опрос
			               |ГДЕ
			               |	Опрос.ДокументОснование = &ДокументОснование
			               |	И Опрос.ТиповаяАнкета = &Анкета
			               |	И Опрос.ПометкаУдаления = ЛОЖЬ
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Опрос.Дата";
			Запрос.УстановитьПараметр("ДокументОснование",ДокументСсылка);
			Запрос.УстановитьПараметр("Анкета",ПредАнкета);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если ПредОпрос = Неопределено Тогда
					ПредОпрос = Выборка.Ссылка;
				ИначеЕсли ИтогОпрос = Неопределено Тогда
					ИтогОпрос = Выборка.Ссылка;
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			               |	Опрос.Ссылка,
			               |	""Предварительная"" КАК ТипАнкеты
			               |ИЗ
			               |	Документ.Опрос КАК Опрос
			               |ГДЕ
			               |	Опрос.ДокументОснование = &ДокументОснование
			               |	И Опрос.ТиповаяАнкета = &ПредАнкета
			               |	И Опрос.ПометкаУдаления = ЛОЖЬ
			               |
			               |ОБЪЕДИНИТЬ ВСЕ
			               |
			               |ВЫБРАТЬ ПЕРВЫЕ 1
			               |	Опрос.Ссылка,
			               |	""Итоговая""
			               |ИЗ
			               |	Документ.Опрос КАК Опрос
			               |ГДЕ
			               |	Опрос.ДокументОснование = &ДокументОснование
			               |	И Опрос.ТиповаяАнкета = &ИтогАнкета
			               |	И Опрос.ПометкаУдаления = ЛОЖЬ";
			Запрос.УстановитьПараметр("ДокументОснование",ДокументСсылка);
			Запрос.УстановитьПараметр("ПредАнкета",ПредАнкета);
			Запрос.УстановитьПараметр("ИтогАнкета",ИтогАнкета);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.ТипАнкеты = "Предварительная" Тогда
					ПредОпрос = Выборка.Ссылка;
				ИначеЕсли Выборка.ТипАнкеты = "Итоговая" Тогда
					ИтогОпрос = Выборка.Ссылка;	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ПредОпрос",ПредОпрос);
	СтруктураВозврата.Вставить("ИтогОпрос",ИтогОпрос);
	Возврат СтруктураВозврата;
	
КонецФункции // ПолучитьОпросыТестДрайва()

// Возвращает список значений с автомобилей для тест-драйва по текущим остаткам
// Параметры
//
// Возвращаемое значение:
//   СписокЗначений  - содержит 2 записи (предварительный и итоговый опрос)
Функция ПолучитьАвтомобилиДляТестДрайва() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст  = "ВЫБРАТЬ
	                |	АвтомобилиДляТестДрайваОстатки.Автомобиль
	                |ИЗ
	                |	РегистрНакопления.АвтомобилиДляТестДрайва.Остатки КАК АвтомобилиДляТестДрайваОстатки";
					 
	СписокАвто = Новый СписокЗначений;
	СписокАвто.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Автомобиль")); 
	Возврат СписокАвто;
КонецФункции

// Формирует напоминание менеджеру тест-драйва о звонке клиенту
//
Процедура СформироватьНапоминаниеКлиенту(Документ,СрокНапоминанияТестДрайв) Экспорт
	
	Если СрокНапоминанияТестДрайв=0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписейНапоминания = РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
	НаборЗаписейНапоминания.Отбор.Объект.Установить(Документ);
	НаборЗаписейНапоминания.Отбор.Автор.Установить(ПараметрыСеанса.Пользователь);
	НаборЗаписейНапоминания.Отбор.Завершено.Установить(Ложь);
	НаборЗаписейНапоминания.Прочитать();
	
	//Очистим уже созданные напоминания по данному тест-драйву
	Для Каждого ТекСтрока Из НаборЗаписейНапоминания Цикл
		Если ТекСтрока.Тема = "Звонок клиенту - напоминание о тест-драйве" Тогда
			НаборЗаписейНапоминания.Удалить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;

	//Получим пользователей, кому требуется создать напоминание
	//вернет всех пользователей, если для 1 сотрудника заведено несколько пользователей
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Пользователи.Ссылка
	               |ИЗ
	               |	Справочник.Пользователи КАК Пользователи
	               |ГДЕ
	               |	Пользователи.Сотрудник = &Сотрудник";
	Запрос.УстановитьПараметр("Сотрудник", Документ.Менеджер);
	ВыборкаПользователи = Запрос.Выполнить().Выбрать();
	
	ДатаОповещений  = ТекущаяДата();	
	
	Пока ВыборкаПользователи.Следующий() Цикл
		
		Описание = "Телефонный звонок клиенту " + Строка(Документ.Контрагент) + " (" + Строка(Документ.ПредставлениеТелефона)+ ") - напоминание о проведении тест-драйва " + Строка(Формат(Документ.ДатаНачала,"ДЛФ=DDT")) + 
		", модель: " + Строка(Документ.Автомобиль.Модель) + ", маршрут: " + Строка(Документ.Маршрут) + ".";
		Тема  = "Звонок клиенту - напоминание о тест-драйве";
		
		НоваяЗапись 						= НаборЗаписейНапоминания.Добавить();
		НоваяЗапись.Пользователь            = ВыборкаПользователи.Ссылка;
		НоваяЗапись.Автор                   = ПараметрыСеанса.Пользователь;
		НоваяЗапись.Завершено               = Ложь;
		НоваяЗапись.РеальнаяДатаОповещения  = Документ.ДатаНачала-(СрокНапоминанияТестДрайв*60*60);
		НоваяЗапись.ДатаНачала              = Документ.ДатаНачала-(СрокНапоминанияТестДрайв*60*60);
		НоваяЗапись.ДатаОповещения          = Документ.ДатаНачала-(СрокНапоминанияТестДрайв*60*60);
		НоваяЗапись.Редактирование          = Ложь;
		НоваяЗапись.Объект 					= Документ;
		НоваяЗапись.Описание 				= Описание;
		НоваяЗапись.Тема 					= Тема; 
		НоваяЗапись.ТипПериода              = "00"; 
		НоваяЗапись.УдалитьПоИстеченииСрока = Истина;
		
	КонецЦикла;
	
	//записываем сформированные напоминания
	Попытка  
		НаборЗаписейНапоминания.Записать(Истина); 
	Исключение 	
	КонецПопытки;
	
КонецПроцедуры

#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//


// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	СтруктураМакетов.Вставить("Договор"      , "Договор проведения тест-драйва");
	СтруктураМакетов.Вставить("Доверенность" , "Доверенность");
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()


#КонецОбласти
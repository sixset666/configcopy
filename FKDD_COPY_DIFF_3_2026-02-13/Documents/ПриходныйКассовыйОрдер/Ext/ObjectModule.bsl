// Модуль документа ПриходныйКассовыйОрдер

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем СообщениеОбОшибке Экспорт;	// Переменная для накопления сообщений. Если равна "Неопределено", то сообщения выводятся на экран, иначе накапливаются в переменной
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОтключитьПроверкиПередЗаписью Экспорт; // Флаг отключения проверок (используется при программном формировании документа)
Перем ОбработкаРасчетСкидок Экспорт;              // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
Перем ЗначениеСкидкиНаценки Экспорт;
Перем СуммаСкидкиНаценки Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("КассаКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СуммаДокумента",1);
	ОбязательныеРеквизиты.Вставить("СтатьяДДС",1);

	Если ДляПробитияНаФР Тогда
		ОбязательныеРеквизиты.Вставить("ТипСпособаРасчетаККТ",1);
		ОбязательныеРеквизиты.Вставить("ТипРасчета", 1);
	КонецЕсли; 

	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД", 2);
	ОбязательныеТовары.Вставить("ДоговорВзаиморасчетов", 2);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаКомпании", Ложь);
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Если Товары.Количество() > 0 И ДляПробитияНаФР Тогда
		
		Если СуммаДокумента > 0 И Товары.Итог("СуммаОплаты") = 0 Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, "Необходимо произвести пропорциональное распределение суммы вносимой оплаты между предметами платежа");
		ИначеЕсли СуммаДокумента <> Товары.Итог("СуммаОплаты") Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, "Неверно заполнена колонка ""Сумма оплаты"" в табличной части товары, необходимо распределить сумму оплаты");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	Результат = Истина;
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПриходныйКассовыйОрдер","Приходный кассовый ордер",Истина);
	СписокХозОпераций.Добавить("ВозвратДенежныхСредствОтПодотчетника","Возврат денежных средств от подотчетника",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ОБЪЕКТА
	Если Имя="ВалютаДокумента" ИЛИ Имя="КурсДокумента" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОтображениеВалютыДокумента();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Сделка" Тогда
		Если ТипЗнч(Сделка) = Тип("ДокументСсылка.СчетНаОплату") Тогда
			ДоговорВзаиморасчетов = Сделка.ДоговорВзаиморасчетов;
			СуммаДокументаПересчитанная = обПересчет(Сделка.СуммаДокумента,Сделка.ВалютаДокумента,Дата,ВалютаДокумента,КурсДокумента);
			СуммаДокумента = СуммаДокументаПересчитанная;
		КонецЕсли;
		Если ЗначениеЗаполнено(Сделка) Тогда
			Основание = Сделка;
		КонецЕсли;
		дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, Сделка, СуммаДокумента);
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		//Параметры для подстановки или создания договора контрагента
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		Если (ХозОперация=Справочники.ХозОперации.ВозвратДенежныхСредствОтПодотчетника) Тогда
			ВидДоговора	= Перечисления.ВидыДоговоров.Подотчет;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер Тогда
			ВидДоговора	= Перечисления.ВидыДоговоров.Продажа;
		Иначе
			ВидДоговора	= Перечисления.ВидыДоговоров.Прочее;	
		КонецЕсли;	
		ДопПараметры.Вставить("ВидДоговора", ВидДоговора);
		ДопПараметры.Вставить("ВалютаВзаиморасчетов", ВалютаДокумента);
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			ПринятоОТ=СокрЛП(Контрагент.НаименованиеПолное);
			//ПодтверждающийДокумент=спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Паспорт);
			//Если НЕ ПодтверждающийДокумент.Пустая() Тогда
			//	Основание=ПодтверждающийДокумент.Наименование;
			//КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаДокумента" ИЛИ Имя="СтавкаНДС" Тогда
		Если НЕ СтавкаНДС.Пустая() Тогда
			СуммаНДС = Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="КассаККМ" Тогда
		Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
			Возврат Истина;
		КонецЕсли;
		#Если Клиент Тогда
		Рез=Истина;
		Если ЭтаФорма<>Неопределено Тогда
			Если КассаККМ.Организация<>Организация Тогда
				Если Вопрос("Организация кассы отлична от текущей организации документа. Установить организацию документа в соответствие с организацией кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					Организация=КассаККМ.Организация;
					Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.Подразделение<>ПодразделениеКомпании Тогда
				Если Вопрос("Подразделение кассы отлично от текущего подразделения документа. Установить подразделение документа в соответствии с подразделением кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ПодразделениеКомпании=КассаККМ.Подразделение;
					Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента Тогда
				Если Вопрос("Валюта кассы отлична от текущей валюты документа. Установить валюту документа в соответствие с валютой кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
					Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли;
					Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЕсли; 
		Иначе
			Организация=КассаККМ.Организация;
			Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
			ПодразделениеКомпании=КассаККМ.Подразделение;
			Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
			ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
			Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		КонецЕсли; 
		#Иначе
		Организация=КассаККМ.Организация;
		Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
		ПодразделениеКомпании=КассаККМ.Подразделение;
		Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
		ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
		Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ФР" Тогда
		Если НЕ обЗначениеНеЗаполнено(ФР) Тогда
			Если ФР.КлассОборудования<>Перечисления.КлассыОборудования.ФР Тогда
				#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Предупреждение("Выбранное оборудование: " + ФР + " не является фискальным регистратором!
					|Выберите фискальный регистратор",10);
				КонецЕсли; 
				#КонецЕсли
				ФР=Неопределено;
				Возврат ОбработкаРеквизита("ФР",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя = "ДляПробитияНаФР" Тогда
		Рез = Истина;
		
		Если ДляПробитияНаФР И НЕ ЗначениеЗаполнено(ТипРасчета) Тогда
			ТипРасчета = дкТипРасчетаДокумента(ЭтотОбъект);
		КонецЕсли;
		
		Возврат Рез;
		
	//БИЗТЕХ МАМОНОВ 02.10.2025 ++
	ИначеЕсли Имя = "ПодразделениеКомпании" Тогда
		//Подставляем кассу компании по праву
		КассаКомпании = обПолучитьПраваИНастройкиПользователя(ЭтотОбъект.ПодразделениеКомпании,ПланыВидовХарактеристик.ПраваИНастройки.НайтиПоКоду("К0006"),ЭтотОбъект);
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	//БИЗТЕХ МАМОНОВ 02.10.2025 --
	
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПриходныйКассовыйОрдер"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриходныйКассовыйОрдер(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПриходныйКассовыйОрдер");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//Получаем область шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);

	ОбластьМакета.Параметры.СуммаДокумента = Формат(СуммаДокумента,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО = ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.КодПодразделения = ЭтотОбъект.ПодразделениеКомпании.Код;
	Если обЗначениеНеЗаполнено(Основание) Тогда
		ОбластьМакета.Параметры.Основание = ЭтотОбъект.ХозОперация;
	КонецЕсли;
	ОбластьМакета.Параметры.ДебетСубСчет = ?(ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),"50.1","50.2");
	ОбластьМакета.Параметры.КредитСубСчет = ЭтотОбъект.СтатьяДДС.КоррСчет.Код;
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	//БизТех 24.10.2023г. по каждому подразделению свой кассир, поэтому кассир - это автор документа.  <<
	//Кассир = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Кассир);
	//ОбластьМакета.Параметры.Заполнить(Кассир);                                        
	Кассир = ЭтотОбъект.Автор.Наименование;
	ОбластьМакета.Параметры.КассирПредставление = Кассир; 
	//>>

	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(ЭтотОбъект.СуммаДокумента,ЭтотОбъект.ВалютаДокумента);
	ОбластьМакета.Параметры.ВТомЧисле = "НДС (" + дкПолучитьПредставлениеСтавкиНДС(СтавкаНДС) + ") " 
										  + Формат(СуммаНДС, ФорматВыводаСуммы)+" "+ЭтотОбъект.ВалютаДокумента;
										  
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.Подразделение = ПодразделениеКомпании;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПриходныйКассовыйОрдер()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	
	//Bizteh++ 30.09.2022 создаем документ Реализация товаров
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда 
		СуммаДокКСО = Основание.ВариантыСтрахования.Итог("СуммаПремии"); 
		ДокРеализация = СоздатьДокРеализацияУслуг(Основание);
		Если ДокРеализация <> Неопределено Тогда
			Основание = ДокРеализация; 
		Иначе
			Сообщить("Не удалось создать документ Реализация услуг!");
		КонецЕсли;  
	КонецЕсли; 
	//Bizteh--
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,, Ложь) Тогда
		Возврат; 
	КонецЕсли;
	
	//БИЗТЕХ МАМОНОВ 02.10.2025 ++
	//Подставляем кассу компании по праву 
	КассаКомпании = обПолучитьПраваИНастройкиПользователя(ЭтотОбъект.ПодразделениеКомпании,ПланыВидовХарактеристик.ПраваИНастройки.НайтиПоКоду("К0006"),ЭтотОбъект);
	//БИЗТЕХ МАМОНОВ 02.10.2025 --
	
	//Производится заполнение реквизитов документа в зависимости от типа документа основания
	//ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер;
	Если Основание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОснование    = Основание;
	Сделка               = ДокументОснование;
	ЭтотОбъект.Основание = Сделка;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		
		ВремКомментарий = "";
		
		ДокументОбъектСтруктура=Новый Структура();
		ДокументОбъектСтруктура.Вставить("Дата",          ТекущаяДата());
		ДокументОбъектСтруктура.Вставить("МоментВремени", ЭтотОбъект.МоментВремени());
		ДокументОбъектСтруктура.Вставить("Проведен",      ЭтотОбъект.Проведен);
		
		ТаблицаРасчетов = зфЗащищенныеФункцииСервер.ПолучитьТаблицуРасчетовПоЗаказам(ДокументОбъектСтруктура, ДокументОснование);
		
		ДолгПоПредоплате = 0;
		Долг = 0;
		Для Каждого ТекСтрока Из ТаблицаРасчетов Цикл
			ДолгПоПредоплате = ДолгПоПредоплате + ТекСтрока.ДолгПоПредоплате;
			Долг = Долг + ТекСтрока.Долг;
		КонецЦикла;
		
		Если ДолгПоПредоплате>0 Тогда
			СуммаДокумента = ДолгПоПредоплате;
			ВремКомментарий = "Предоплата по документу <"+дкПолучитьПредставление(ДокументОснование)+">";
		Иначе
			СуммаДокумента = Долг;
		КонецЕсли;
		
		Если НЕ ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
			СуммаДокумента = обПересчет(СуммаДокумента, ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		КонецЕсли;
		
		//Статья ДДС - Оплата от покупателя
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;
		Если НЕ ПустаяСтрока(ВремКомментарий) Тогда
			Комментарий=ВремКомментарий;
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетНаОплату") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили") Тогда
		
		//Проверим сумму предоплаты
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаРасход КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрРасход КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		|		Контрагент = &Контрагент И
		|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		|		(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)
		|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка",                ДокументОснование);
		тзОплаты=Запрос.Выполнить().Выгрузить();
		
		ДокументОснованиеСуммаДокумента = ДокументОснование.СуммаДокумента;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			Оплачено = обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		Иначе
			Оплачено = обПересчет(тзОплаты.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсДокумента), Дата, ЭтотОбъект.КурсДокумента), РежимОкругления.Окр15как20);
		КонецЕсли;
		
		СуммаДокумента = ДокументОснованиеСуммаДокумента - Оплачено;
		
		//Статья ДДС - Оплата от покупателя
		СтатьяДДС = Справочники.СтатьиДДС.ОплатаОтПокупателя;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.АвансовыйОтчет") ИЛИ
			ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			ХозОперация = Справочники.ХозОперации.ВозвратДенежныхСредствОтПодотчетника;
		КонецЕсли;
		СтруктураОтбора = Новый Структура();
		СтруктураОтбора.Вставить("Контрагент",ДокументОснование.Контрагент);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") И ЗначениеЗаполнено(ДокументОснование.Сделка) Тогда
			СтруктураОтбора.Вставить("Сделка", ДокументОснование.Сделка);
			Сделка = ДокументОснование.Сделка;
		КонецЕсли;
		
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		Если ВалютаДокумента=ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов Тогда
			Долг=обПересчет(тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.Дата,ВалютаДокумента,ДокументОснование.Дата,РежимОкругления.Окр15как20);
		ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			Долг=тзДолги.Итог("СуммаБаз");
		Иначе
			Долг=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.Дата,ВалютаДокумента,ДокументОснование.Дата,РежимОкругления.Окр15как20);
		КонецЕсли;
		СуммаДокумента=Макс(Долг,0);
		
		//Статья ДДС - Возврат из подотчета
		СтатьяДДС=Справочники.СтатьиДДС.ВозвратИзПодОтчета;
		
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			ТипРасчета = Неопределено;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.Инкассация") Тогда
		
		ИзменилсяКонтрагент = (Контрагент <> Основание.Инкассатор);
		ИзменилсяДоговор    = (ДоговорВзаиморасчетов <> Основание.ДоговорВзаиморасчетовИнкассатор);
		
		Контрагент=Основание.Инкассатор;
		ДоговорВзаиморасчетов=Основание.ДоговорВзаиморасчетовИнкассатор;
		
		Если ИзменилсяКонтрагент Тогда
			ОбработкаРеквизита("Контрагент");
		КонецЕсли;
		
		Если ИзменилсяДоговор Тогда
			ОбработкаРеквизита("ДоговорВзаиморасчетов");
		КонецЕсли;
		
		СуммаДокумента=0;
		Оплаты=Основание.Оплаты.Выгрузить();
		Оплаты.Свернуть("ТипОплаты","Сумма,СуммаВозврат");
		СтрокаОплатНаличными=Оплаты.Найти(Справочники.ТипыОплат.Наличные,"ТипОплаты");
		Если СтрокаОплатНаличными<>Неопределено Тогда
			СуммаДокумента=СтрокаОплатНаличными.Сумма+СтрокаОплатНаличными.СуммаВозврат;
		КонецЕсли; 
		
		КурсВалютыВзаиморасчетов = дкПолучитьКурсВалютыВзаиморасчетов(ЭтотОбъект, ДоговорВзаиморасчетов, Основание);

	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РеализацияАктивов") Тогда
		Контрагент=Основание.Контрагент;
		//вычислим сумму к оплате
		СуммаДок=Основание.Активы.Итог("Сумма");
		СуммаДокумента=обПересчет(СуммаДок,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,Дата);
		СуммаНДС=Основание.Активы.Итог("СуммаНДС");
		СуммаНДС=обПересчет(СуммаНДС,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,Дата);
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;

	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.Инвентаризация") Тогда
		Если ДокументОснование.ХозОперация = Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	СУММА(ИнвентаризацияТовары.Сумма) КАК Сумма
			|ИЗ
			|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
			|ГДЕ
			|	 ИнвентаризацияТовары.Ссылка = &ДокументОснование И ИнвентаризацияТовары.Количество < 0
			|");
			Запрос.УстановитьПараметр("ДокументОснование",ДокументОснование);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() И НЕ обЗначениеНеЗаполнено(Выборка.Сумма) Тогда
				СуммаДокумента = обПересчет(-Выборка.Сумма,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,Дата);
			КонецЕсли;
			СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;
		Иначе
			#Если Клиент Тогда
				Предупреждение("Можно вводить только на основании инвентаризации товаров отданных на комиссию!");
			#КонецЕсли
			
			дкОтменаВводаДокумента(ЭтотОбъект);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПланПоступленияДС") Тогда
		СуммаДокумента = ДокументОснование.СуммаДокумента;
		// В зависимости от хоз операции документа основания будем подставлять Кассу или нет
		Если ДокументОснование.ХозОперация = Справочники.ХозОперации.ПланПоступленияВКассу Тогда
			КассаКомпании = ДокументОснование.СтруктурнаяЕдиница;
		КонецЕсли;
		
		Сделка               = Основание.ДокументОснование;
		ДокументПланирования = Основание;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		//При вводе от заказ-наряда ничего более делать не надо,
		//т.к. все уже сделано в обработке заполнения в отраслевых решениях
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
		//При вводе от заказа на автомобиль ничего более делать не надо,
		//т.к. все уже сделано в обработке заполнения в отраслевых решениях
		
	//misha
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
	
		Контрагент=Основание.Страхователь;
		//вычислим сумму к оплате
		СуммаДок=Основание.ВариантыСтрахования.Итог("СуммаПремии");
		СуммаДокумента=обПересчет(СуммаДок,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,Дата);
		СуммаНДС=0;
		СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;
		ДоговорВзаиморасчетов = Основание.ДоговорКСО;
		КассаКомпании = Справочники.КассыКомпании.НайтиПоКоду("ЦБ000003"); //Касса ИП Ладная-Мороз
	//misha		
		
	Иначе
		//Вычислим сумму
		
		//<<БАА
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.БТ_ПредоплаченноеТО") Тогда
			СуммаДокумента = Основание.СуммаДокумента - Документы.БТ_ПредоплаченноеТО.СуммаОплатПоПредТО(Основание);  
		Иначе
			СтруктураОтбора=Новый Структура(); 
			СтруктураОтбора.Вставить("Контрагент",Контрагент);
			СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
			СтруктураОтбора.Вставить("Сделка",ДокументОснование);
			тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ТекущаяДата(),СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
			Если ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
				ОстатокПоСделке=обПересчет(тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
			ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
			Иначе
				ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			СуммаДокумента=ОстатокПоСделке;

		КонецЕсли;	
		//>>БАА

				//Статья ДДС - Оплата от покупателя
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаОтПокупателя;
		
				
		//Bizteh++ 30.09.2022
		Если СуммаДокумента = 0 и ЗначениеЗаполнено(СуммаДокКСО) Тогда
			 СуммаДокумента = СуммаДокКСО;
		КонецЕсли;
		//Bizteh--
		
	КонецЕсли;
	
	// Для нового документа нельзя заполнять данные ФР
	Если ЭтоНовый() Тогда
		Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	КонецЕсли;
	
	//дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, ЭтотОбъект.ДокументОснование, СуммаДокумента);
	
	//Ульянов 21,03
	Если НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СтраховойПолис") и НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
		дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, ЭтотОбъект.ДокументОснование, СуммаДокумента);
	КонецЕсли;		
	
	КурсВалютыВзаиморасчетов = дкПолучитьКурсВалютыВзаиморасчетов(ЭтотОбъект, ?(обЗначениеНеЗаполнено(ЭтотОбъект.ДоговорВзаиморасчетов), Неопределено, ЭтотОбъект.ДоговорВзаиморасчетов), ?(обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование), Неопределено, ЭтотОбъект.ДокументОснование));
	ОбработкаРеквизита("КассаКомпании",,,Новый Структура("ЗадаватьВопрос",Ложь));
	ОбработкаРеквизита("Контрагент");
	
	Если НЕ ЗначениеЗаполнено(ТипРасчета) Тогда
		ТипРасчета = дкТипРасчетаДокумента(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	
	Товары.Очистить();
	КодыМаркировки.Очистить();
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОтключитьПроверкиПередЗаписью Тогда Возврат; КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	//снимем проверку на запись документа раньше чем документ основание
	МаскаПроверки = "10111";
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения,,,,МаскаПроверки) Тогда Возврат; КонецЕсли;
	
	Если НЕ обПраво("ВводОплатыПоЗаказНаряду",Права,,ЭтотОбъект) Тогда
		ОтказНаВводДокумента=Ложь;
		Если НЕ обЗначениеНеЗаполнено(Сделка) Тогда
			Если ТипЗнч(Сделка)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
				ОтказНаВводДокумента=Истина;
			//Иначе
			//	Попытка
			//		Если ТипЗнч(Сделка.ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			//			ОтказНаВводДокумента=Истина;
			//		КонецЕсли;
			//	Исключение КонецПопытки; 
			КонецЕсли; 
		КонецЕсли;
		Если ОтказНаВводДокумента Тогда
			Сообщить("Формирование приходного кассового ордера на основании заказ-наряда запрещено!",СтатусСообщения.Важное);
			Отказ=Истина;
			дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// приходуем деньги в кассу
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДС.РежимПроведения=Режим;
	НаборЗаписейДС.СтруктурнаяЕдиница=КассаКомпании;
	НаборЗаписейДС.СтатьяДДС=СтатьяДДС;
	НаборЗаписейДС.Валюта=Неопределено;
	НаборЗаписейДС.Сумма=СуммаДокумента;
	Отказ=НЕ НаборЗаписейДС.Приход() ИЛИ Отказ;
	// проводим взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	Если ЗначениеЗаполнено(Сделка)Тогда
		НаборЗаписейВзаиморасчеты.Сделка = Сделка;
		ТипСделка=ТипЗнч(Сделка);
		Если орПолучитьТипыСделок(Ложь).СодержитТип(ТипСделка) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		ИначеЕсли орПолучитьТипыСделок(Истина).СодержитТип(ТипСделка) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		Иначе
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Неопределено;
		КонецЕсли; 
	Иначе
		НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
	КонецЕсли; 
	НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок=АвтоЗакрытиеСделок;
	НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	ПодразделениеКасса = обПолучитьБалансовоеПодразделение(КассаКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКасса);
	Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		
		НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейДиР.Подразделение			= КассаКомпании.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте				= Ложь;
		НаборЗаписейДиР.Доход					= СуммаДокумента;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
		НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейДиР.Подразделение			= ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте				= Ложь;
		НаборЗаписейДиР.Расход					= СуммаДокумента;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;	
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	Если ДляПробитияНаФР=Истина И ЗначениеЗаполнено(КассаККМ) Тогда
		СуммаОплаты=обПересчет(СуммаДокумента,ВалютаДокумента,КурсДокумента,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),Дата);
		НаборЗаписейКассыККМ=Движения.КассыККМ;
		НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
		НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
		НаборЗаписейКассыККМ.КассаККМ 			= КассаККМ;
		НаборЗаписейКассыККМ.СпособОплаты 		= Справочники.ТипыОплат.Наличные;
		НаборЗаписейКассыККМ.Сумма				= СуммаОплаты;	
		Отказ=НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейКассыККМ.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейКассыККМ.РежимПроведения= Режим;
			НаборЗаписейКассыККМ.КассаККМ 		= КассаККМ;
			НаборЗаписейКассыККМ.СпособОплаты 	= Справочники.ТипыОплат.Наличные;
			НаборЗаписейКассыККМ.Сумма			= СуммаОплаты;	
			Отказ=НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ Отказ Тогда
		//проведение в Платежный календарь
		НаборЗаписей = Движения.ПлатежныйКалендарь;
		НаборЗаписей.ДокументОбъект = Ссылка;
		НаборЗаписей.Поступление    = Истина;
		
		Отказ = Не НаборЗаписей.Факт();
	КонецЕсли;
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	Если СообщениеОбОшибке=Неопределено Тогда
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	Иначе
		Если ТипЗнч(РезультатОбработки)=Тип("Строка") Тогда
			СообщениеОбОшибке=РезультатОбработки;
		Иначе
			Для каждого СтрокаОшибки Из РезультатОбработки.Сообщения Цикл
				СообщениеОбОшибке=СообщениеОбОшибке+СтрокаОшибки.Сообщение+"
				|";
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()


Функция СоздатьДокРеализацияУслуг(ДокументОснование)
	//поиск документа
    Запрос = Новый запрос;  
	Запрос.Текст =  "ВЫБРАТЬ
	                |	РеализацияТоваров.Ссылка КАК Ссылка
	                |ИЗ
	                |	Документ.РеализацияТоваров КАК РеализацияТоваров
	                |ГДЕ
	                |	РеализацияТоваров.ДокументОснование = &ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество()>0 Тогда
		ДокРеал = Результат[0].Ссылка; 
		Док = ДокРеал.ПолучитьОбъект();
	Иначе 
		Док = Документы.РеализацияТоваров.СоздатьДокумент(); 
	КонецЕсли;
	//Заполняем документ
	Док.ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг;
	Если НЕ дкОбработкаЗаполнения(Док, ДокументОснование,, Ложь) Тогда
		Возврат Неопределено; 
	КонецЕсли;
	Док.Комитент = ДокументОснование.Комитент;
	Док.ДоговорКомитента = ДокументОснование.ДоговорКомитента;  
	Док.ДокументОснование = ДокументОснование;
	Док.Контрагент = ДокументОснование.Страхователь; 
	//<<ЧалапАЮ БизТех 06.10.2023   
	Если этоНовый() тогда
		Если ЗначениеЗаполнено(Док.Контрагент.БТ_ПризнакЛояльности)  тогда            
			//Сообщить("Контрагент " + Док.Контрагент.Наименование +" имеет признак " + Док.Контрагент.БТ_ПризнакЛояльности +", дальнейшие работы с ним запрещены, необходимо обратиться к руководителю отдела.");	
			//Док.Контрагент = Справочники.Контрагенты.ПустаяСсылка();   
			Сообщить("Контрагент " + Док.Контрагент.Наименование +" имеет признак " + Док.Контрагент.БТ_ПризнакЛояльности); //ЧалапАю БизТех 10.01.2025
		КонецЕсли;
	КонецЕсли;
	//ЧалапАЮ БизТех 06.10.2023>>
	Док.ДоговорВзаиморасчетов = ДокументОснование.ДоговорКСО;
	СтрокаУсл = Док.Товары.Добавить(); 
	Ном = Справочники.Номенклатура.НайтиПоНаименованию("Финансовый продукт страхование");
	СтрокаУсл.Номенклатура = Ном;
	СтрокаУсл.ЕдиницаИзмерения = Ном.ОсновнаяЕдиницаИзмерения;
	СтрокаУсл.Цена = ДокументОснование.ВариантыСтрахования.Итог("СуммаПремии"); 
	СтрокаУсл.Количество = 1;
	СтрокаУсл.Коэффициент = 1;
	СтрокаУсл.Сумма = СтрокаУсл.Цена*СтрокаУсл.Количество;
	СтрокаУсл.СуммаВсего = СтрокаУсл.Сумма;
	СтрокаУсл.СтавкаНДС = Справочники.СтавкиНДС.БезНДС; 
	СтрокаУсл.СуммаНДС = Окр((СтрокаУсл.Сумма * СтрокаУсл.СтавкаНДС.Ставка)/(100 + СтрокаУсл.СтавкаНДС.Ставка),2);
	СтрокаУсл.СтатьяДоходов = Справочники.СтатьиДоходовИРасходов.НайтиПоНаименованию("Финансовй продукт страхования");   
	Док.Комментарий = "Создан автоматически "+ТекущаяДата();
	Док.Записать();  
	Возврат Док.Ссылка;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
#КонецЕсли
ОтключитьПроверкиПередЗаписью = ЛОЖЬ;
СообщениеОбОшибке = Неопределено;
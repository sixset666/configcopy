// Модуль документа ЗаявкаНаХранениеШин

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Заполнение ТЧ шин строками по умолчанию
//
Процедура ЗаполнитьШинамиПоУмолчанию(Шина=Неопределено) Экспорт
	Шины.Очистить();
	Для Инд = 1 По 5 Цикл
		НоваяСтрокаТЧ=Шины.Добавить();
		НоваяСтрокаТЧ.Номенклатура=Шина;
		НоваяСтрокаТЧ.Количество=1;
		ОбработкаРеквизита("Шины.Номенклатура",НоваяСтрокаТЧ);
		НоваяСтрокаТЧ.ИдентификаторКартинки="Строка" + Новый УникальныйИдентификатор();
		Если Инд = 1 Тогда
			НоваяСтрокаТЧ.РасположениеШины=Перечисления.РасположениеШин.ЛевоеПереднее;
		ИначеЕсли Инд = 2 Тогда
			НоваяСтрокаТЧ.РасположениеШины=Перечисления.РасположениеШин.ПравоеПереднее;
		ИначеЕсли Инд = 3 Тогда
			НоваяСтрокаТЧ.РасположениеШины=Перечисления.РасположениеШин.ЛевоеЗаднее;
		ИначеЕсли Инд = 4 Тогда
			НоваяСтрокаТЧ.РасположениеШины=Перечисления.РасположениеШин.ПравоеЗаднее;
		ИначеЕсли Инд = 5 Тогда
			НоваяСтрокаТЧ.РасположениеШины=Перечисления.РасположениеШин.Запасное;
		КонецЕсли;		
	КонецЦикла
КонецПроцедуры // ЗаполнитьШинамиПоУмолчанию() 

// Заполнение ТЧ Услуги строками по умолчанию согласно права
//
Процедура ЗаполнитьУслугамиПоУмолчанию() Экспорт
	УслугаПоУмолчанию = обПраво("УслугаПоСезонномуХранениюШинПоУмолчанию", Права,,ЭтотОбъект);	
	Если обЗначениеНеЗаполнено(УслугаПоУмолчанию) Тогда 
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("Заполнен") И Товары.Количество()=0 Тогда
		ДополнительныеСвойства.Вставить("Заполнен",Истина);
		НайденныеСтроки = Товары.НайтиСтроки(Новый Структура("Номенклатура",УслугаПоУмолчанию));
		Если НайденныеСтроки.Количество()=0 Тогда
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = УслугаПоУмолчанию;
			ДопПараметры = Новый Структура();
			ДопПараметры.Вставить("ВвестиКоличествоНаборов",Ложь);
			ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока,,ДопПараметры);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ЗаполнитьУслугамиПоУмолчанию() 

// Удаляет из регистра картинки и файлы для строк, которые были удалены
// Возвращает Истина, если была удалена хотя бы одна картинка
Функция ОчиститьРегистрКартинкиИФайлы() Экспорт
	
	Результат = Ложь;
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	КартинкиИФайлы.Идентификатор
	             |ИЗ
	             |	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	             |ГДЕ
	             |	КартинкиИФайлы.Объект = &Объект";
	Запрос.УстановитьПараметр("Объект",ЭтотОбъект.Ссылка);
	Выборка=Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Лев(Выборка.Идентификатор,6)="Строка" Тогда
			НайденнаяСтрока = Шины.Найти(Выборка.Идентификатор, "ИдентификаторКартинки");
			Если НайденнаяСтрока = Неопределено Тогда
				МенеджерЗаписи = РегистрыСведений.КартинкиИФайлы.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Объект = ЭтотОбъект.Ссылка;
				МенеджерЗаписи.Идентификатор = Выборка.Идентификатор;
				МенеджерЗаписи.Прочитать();
				МенеджерЗаписи.Удалить();
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ОчиститьРегистрКартинкиИФайлы() 

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры(ИмяТабличнойЧасти="") Экспорт
	Результат=Новый Соответствие();
	Если ИмяТабличнойЧасти = "Шины" Тогда 	
		Для каждого ВидНоменклатуры Из Перечисления.ВидыНоменклатуры Цикл
			Если ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Шины Тогда
				Результат.Вставить(ВидНоменклатуры,0);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для каждого ВидНоменклатуры Из Перечисления.ВидыНоменклатуры Цикл
			Если ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
				Результат.Вставить(ВидНоменклатуры,0);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	ОбязательныеШины=Новый Структура();
	ОбязательныеШины.Вставить("Номенклатура",3);
	ОбязательныеШины.Вставить("Количество",1);
	ОбязательныеШины.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеШины.Вставить("Коэффициент",1);
	ОбязательныеШины.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеШины.Вставить("РасположениеШины",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачала",1);
	ОбязательныеРеквизиты.Вставить("ДатаОкончания",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Шины",ОбязательныеШины);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
		
		Если СкладКомпании.ВидСклада=Перечисления.ВидыСкладов.Обычный Тогда
			дкДобавитьОшибку(Ошибки,"Склад документа имеет вид """+СкладКомпании.ВидСклада+""". Возможно использование только ""Ордерного"" или ""Ячеистого"" склада!");
			Результат=Ложь;
		КонецЕсли; 
	КонецЕсли;
	
	//В ТЧ Шины должны быть шины и диски, в ТЧ Услуги только услуги
	Если Результат Тогда
		Для Каждого ТекСтрока Из Шины Цикл
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
				Если ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Шины Тогда
					дкДобавитьОшибку(Ошибки,"В табличной части ""Шины"" в строке номер " + ТекСтрока.НомерСтроки+  " в поле ""шина"" выбрана номенклатура некорректного типа. Возможно использование только шин или дисков!");
					Результат = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Для Каждого ТекСтрока Из Товары Цикл
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
				Если ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
					дкДобавитьОшибку(Ошибки,"В табличной части ""Услуги"" в строке номер " + ТекСтрока.НомерСтроки+  " выбрана номенклатура некорректного типа. Возможно использование только шин или дисков!");
					Результат = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗаявкаНаХранениеШин", "Заявка на хранение шин",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Возвращает разность между датой окончания и датой начала 
Функция СформироватьРазностьДат(Дата1, Дата2) Экспорт

    Лет        = 0;
    Месяцев    = 0;
    Дней    = 0;

    Если Дата1 > Дата2 Тогда

        ВременнаяДата = Дата1;

        Если День(ВременнаяДата) < День(Дата2) Тогда
            Дней = (ВременнаяДата - ДобавитьМесяц(ВременнаяДата,-1))/86400;
            ВременнаяДата = ДобавитьМесяц(ВременнаяДата,-1);
        КонецЕсли;

        Если Месяц(ВременнаяДата) < Месяц(Дата2) Тогда
            ВременнаяДата = ДобавитьМесяц(ВременнаяДата,-12);
            Месяцев = 12;
        КонецЕсли;

        Лет        = Макс(             Год(ВременнаяДата)        - Год(Дата2),    0);
        Месяцев    = Макс(Месяцев    + Месяц(ВременнаяДата)    - Месяц(Дата2),    0);
        Дней    = Макс(Дней        + День(ВременнаяДата)    - День(Дата2),    0);
		
        // скорректируем отображаемое значение, если "вмешалось" разное количество дней в месяцах
        Если Дата2 <> (ДобавитьМесяц(Дата1,-Лет*12-Месяцев)-Дней*86400) Тогда
            Дней = Дней + (День(КонецМесяца(Дата2)) - День(НачалоМесяца(Дата2))) - (День(КонецМесяца(ДобавитьМесяц(Дата1,-1))) - День(НачалоМесяца(ДобавитьМесяц(Дата1,-1))));
        КонецЕсли;
        
	КонецЕсли;
	
	СтрокаДнейМесяцев = "" + (Месяцев + Лет*12) + " мес. " + Дней + " " + обПолучитьПредставлениеДня(Дней) + " ";
	СтрокаДней = "(всего " + Строка((Дата1 - Дата2)/86400) + " " + обПолучитьПредставлениеДня((Дата1 - Дата2)/86400) + ")";
	
	Возврат СтрокаДнейМесяцев + СтрокаДней;
	
КонецФункции    // СформироватьРазностьДат

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СкладКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,"СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (НЕ Ячейка.Пустая()) И Ячейка.Владелец<>СкладКомпании Тогда
			Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
		КонецЕсли;
		Для каждого СтрокаТЧ Из Шины Цикл
			Если (НЕ СтрокаТЧ.Ячейка.Пустая()) И СтрокаТЧ.Ячейка.Владелец<>СкладКомпании Тогда
				СтрокаТЧ.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла; 
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.УправлениеДиалогом();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ДатаНачала" ИЛИ Имя="ДатаОкончания" Тогда
		Если ДатаНачала>ДатаОкончания Тогда
			ДатаОкончания=ДатаНачала+обПраво("СрокХраненияШинПоУмолчанию", Права)*86400;
		КонецЕсли;
		
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				ЭтаФорма.СрокХранения = "срок хранения составляет " + СформироватьРазностьДат(ДатаОкончания,ДатаНачала);
			КонецЕсли;
		#КонецЕсли
		
		Возврат Истина;
	ИначеЕсли Имя="Автомобиль" Тогда
		
		Рез = Истина;
		
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
			//Получить последнего хозяина
			Хозяин = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин,Дата);
			Если (НЕ обЗначениеНеЗаполнено(Хозяин)) И (Контрагент<>Хозяин) Тогда
				Контрагент=Хозяин;
				Рез=ОбработкаРеквизита("Контрагент",,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли;
		
		#Если Клиент Тогда
			дкСообщитьОЗаметках(ЭтотОбъект,ЭтотОбъект["Автомобиль"],ЭтаФорма);
		#КонецЕсли
		
		Возврат Рез;
		
	ИначеЕсли Имя="Шины.Номенклатура" Тогда
		
		ИмяТабличнойЧасти = "Шины";
		
		Рез = Истина;
		
		// проверим а не набор ли у нас...
		Попытка БлокироватьВидыНоменклатуры = ЭтотОбъект.ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры(ИмяТабличнойЧасти); Исключение БлокироватьВидыНоменклатуры = Неопределено; КонецПопытки;
		Если БлокироватьВидыНоменклатуры <> Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры) = Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество() > 0 Тогда
			Попытка ЕстьОшибка = (БлокироватьВидыНоменклатуры[ТекСтрока.Номенклатура.ВидНоменклатуры] <> Неопределено); Исключение ЕстьОшибка = Ложь; КонецПопытки;
			Если ЕстьОшибка Тогда
				#Если Клиент Тогда
					Предупреждение("В таблицу нельзя вводить номенклатуру вида """ + СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры) + """",5);
				#КонецЕсли
				ТекСтрока.Номенклатура = Неопределено;
			КонецЕсли;
		КонецЕсли;
			
		РезПроверки = дкПроверитьВозможностьОперацийСНоменклатурой(ТекСтрока,ЭтаФорма, ЭтотОбъект);
		
		Если РезПроверки.ЕстьБлок Тогда
			ТекСтрока.Номенклатура = Неопределено;
		КонецЕсли;
		
		Если (РезПроверки.ЕстьПредупреждение ИЛИ РезПроверки.ЕстьБлок) И НЕ ПустаяСтрока(РезПроверки.Сообщение) Тогда
			Сообщить(РезПроверки.Сообщение,СтатусСообщения.Внимание);
		КонецЕсли;
		
		// установим количество
		Попытка
			Если ТекСтрока.Количество = 0 Тогда ТекСтрока.Количество = 1 КонецЕсли;
		Исключение КонецПопытки;
		
		// ХАРАКТЕРИСТИКА: сбросим характеристику, если она не подходит
		Попытка	// В ТЧ может не существовать реквизита Характеристика
			Если НЕ ТекСтрока.ХарактеристикаНоменклатуры.Пустая() Тогда
				Если (ТекСтрока.ХарактеристикаНоменклатуры.Владелец <> ТекСтрока.Номенклатура) И (ТекСтрока.ХарактеристикаНоменклатуры.Владелец <> ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
					ТекСтрока.ХарактеристикаНоменклатуры = Неопределено;
					ЭтотОбъект.ОбработкаРеквизита("Товары.ХарактеристикаНоменклатуры",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			КонецЕсли; 
			ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры; 
		Исключение 
			ХарактеристикаНоменклатуры = Неопределено;	
		КонецПопытки;
		
		// ЕД. ИЗМЕРЕНИЯ: заполним единицу измерения
		НоваяЕдиницаИзмерения = Неопределено;
		Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			ДопПараметры.Свойство("ЕдиницаИзмерения",НоваяЕдиницаИзмерения);
		КонецЕсли; 
		Если НоваяЕдиницаИзмерения = Неопределено Тогда
			Попытка
				ТекЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
				Если (ТекЕдиницаИзмерения.Владелец = ТекСтрока.Номенклатура) ИЛИ (ТекЕдиницаИзмерения.Владелец = ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
					НоваяЕдиницаИзмерения = ТекЕдиницаИзмерения;
				Иначе
					НоваяЕдиницаИзмерения = ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				КонецЕсли;
			Исключение
				Попытка
					НоваяЕдиницаИзмерения = ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				Исключение
					НоваяЕдиницаИзмерения = Неопределено;
				КонецПопытки;
			КонецПопытки;
		КонецЕсли; 
		
		// обработаем изменение единицы измерения
		Попытка // В ТЧ может не существовать реквизита ЕдиницаИзмерения
			ТекСтрока.ЕдиницаИзмерения = НоваяЕдиницаИзмерения;
			ЭтотОбъект.ОбработкаРеквизита("Товары.ЕдиницаИзмерения",ТекСтрока,ЭтаФорма,ДопПараметры);
			ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;	
		Исключение
			ЕдиницаИзмерения = Неопределено;
		КонецПопытки;
		
		//Обработка отображения интерфейса
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				// покажем если надо колонку "характеристика номенклатуры"
				Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
					ТипНоменклатуры = ТекСтрока.Номенклатура.ТипНоменклатуры;
					Если обПраво("АвтоматическиСкрыватьКолонкуХарактеристик",ЭтотОбъект.Права,,ЭтотОбъект) Тогда
						Попытка
							Если НЕ ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
								ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.Видимость = (ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ ТипНоменклатуры.ИспользованиеХарактеристик = 2);
							КонецЕсли; 
						Исключение
						КонецПопытки; 
					КонецЕсли;
					//установим свойства колонки характеристик в зависимости от типа номенклатуры
					РучноеСписаниеХарактеристик = (ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ ТипНоменклатуры.АвтоСписаниеХарактеристик.Пустая());
					Попытка  
						ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.ЭлементУправления.ОтметкаНезаполненного = РучноеСписаниеХарактеристик;
						ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.ПропускатьПриВводе = НЕ РучноеСписаниеХарактеристик;
					Исключение
					КонецПопытки; 
				Иначе
					Попытка
						Если НЕ ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
							ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.Видимость = Ложь;
						КонецЕсли; 
						ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.ЭлементУправления.ОтметкаНезаполненного = Ложь;
						ЭтаФорма.ЭлементыФормы.Шины.Колонки.ХарактеристикаНоменклатуры.ПропускатьПриВводе = Истина;
					Исключение
					КонецПопытки; 
				КонецЕсли; 
			КонецЕсли; 
		#КонецЕсли
		
		Попытка
			Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) ИЛИ обЗначениеНеЗаполнено(ЭтотОбъект.СкладКомпании) Тогда
				ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Иначе
				ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,ЭтотОбъект.СкладКомпании);
				ТекСтрока.Ячейка=ЯчейкаСсылка;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Шины.Диск" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Шины.Количество" Тогда
		Если ТекСтрока.Количество<>1 Тогда
			ТекСтрока.Количество=1;
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,"Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Шины.ЕдиницаИзмерения" Тогда
		Возврат дкОбработкаРеквизита(ЭтотОбъект,"Товары.ЕдиницаИзмерения",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Шины.ХарактеристикаНоменклатуры" Тогда
		Возврат дкОбработкаРеквизита(ЭтотОбъект,"Товары.ХарактеристикаНоменклатуры",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Рез = Истина;
		
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			Если ТекСтрока.Номенклатура.ЭтоГруппа Тогда
				#Если Клиент Тогда
					Предупреждение("В таблицу нельзя вводить группы номенклатуры",5);
				#КонецЕсли
				ТекСтрока.Номенклатура = Неопределено;
			КонецЕсли;
		КонецЕсли;	
			
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
	
		Возврат Рез;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,"Товары.ХарактеристикаНоменклатуры",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//Зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено, ДопПараметры=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ, ДопПараметры);
КонецФункции // Печать()

// Функция печати договора продажи
Функция ПечатьЗаявкаНаХранениеШин(ТабДокумент) Экспорт
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	Макет = ПолучитьМакет("ЗаявкаНаХранениеШин");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата, "ДЛФ=ДД");
	
	ЗакПред =  спПолучитьНаименование(Контрагент) + ", ";
	ЗакПред = ЗакПред + киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый) + ", "; 
	ЗакПред = ЗакПред + " тел.: "+ киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный); 
	ОбластьМакета.Параметры.ЗаказчикПредставление = ЗакПред;
	
	ОбластьМакета.Параметры.Автомобиль = СокрЛП(Строка(Автомобиль));
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШинаСтроки|ШинаСтолбцы");
	ОбластьМакета.Параметры.Расположение = Строка(Перечисления.РасположениеШин.ЛевоеПереднее);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШинаСтроки|ШинаСтолбцы");
	ОбластьМакета.Параметры.Расположение = Строка(Перечисления.РасположениеШин.ПравоеПереднее);
	ТабДокумент.Присоединить(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШинаСтроки|ШинаСтолбцы");
	ОбластьМакета.Параметры.Расположение = Строка(Перечисления.РасположениеШин.ЛевоеЗаднее);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШинаСтроки|ШинаСтолбцы");
	ОбластьМакета.Параметры.Расположение = Строка(Перечисления.РасположениеШин.ПравоеЗаднее);
	ТабДокумент.Присоединить(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШинаСтроки|ШинаСтолбцы");
	ОбластьМакета.Параметры.Расположение = Строка(Перечисления.РасположениеШин.Запасное);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Контрагент);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Функция печати договора продажи
Функция ПечатьАктПриемаПередачи(ТабДокумент) Экспорт
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	Макет = ПолучитьМакет("АктПриемаПередачи");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата, "ДЛФ=ДД");
	
	зфПечатьШК(ЭтотОбъект,ТабДокумент);
	
	ОргПред =  спПолучитьНаименование(Организация) + ", ";
	ОргПред = ОргПред + "ИНН " + Организация.ИНН + ", ";
	ОргПред = ОргПред + "КПП " + Организация.КПП + ", ";
	ОргПред = ОргПред + киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический) + ", "; 
	ОргПред = ОргПред + " тел.: "+ киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий); 
	ОбластьМакета.Параметры.ОрганизацияПредставление = ОргПред;
	
	ЗакПред =  спПолучитьНаименование(Контрагент) + ", ";
	ЗакПред = ЗакПред + киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый) + ", "; 
	ЗакПред = ЗакПред + " тел.: "+ киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный); 
	ОбластьМакета.Параметры.ЗаказчикПредставление = ЗакПред;
	
	ОбластьМакета.Параметры.Автомобиль = СокрЛП(Строка(Автомобиль));
	ОбластьМакета.Параметры.ДатаНачала = Формат(ДатаНачала, "ДЛФ=ДД");
	ОбластьМакета.Параметры.ДатаОкончания = Формат(ДатаОкончания, "ДЛФ=ДД");
	СрокХранения = СформироватьРазностьДат(ДатаОкончания,ДатаНачала);
	ОбластьМакета.Параметры.СрокХранения = СрокХранения;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицыШин");
	ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата, "ДЛФ=ДД");
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтроки = 0;
	Для Каждого ТекСтрока Из Шины Цикл
		НомерСтроки = НомерСтроки + 1;
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицыШин");
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		ПараметрДиск = "";
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Диск) Тогда
			Если Текстрока.Диск = Перечисления.ВидыАвтомобильныхДисков.БезДиска Тогда
				ПараметрДиск = "(" + нрег(Текстрока.Диск) + ")";
			Иначе
				ПараметрДиск = "(" + нрег(Текстрока.Диск) + " диск)";
			КонецЕсли;
		КонецЕсли;
		ОбластьМакета.Параметры.Наименование = спПолучитьНаименование(ТекСтрока.Номенклатура) + ?(НЕ ПустаяСтрока(ПараметрДиск), " " + ПараметрДиск, "");
		ОбластьМакета.Параметры.Размер = обПолучитьЗначениеСвойства(ТекСтрока.Номенклатура,"ШиныРадиус");
		ОбластьМакета.Параметры.Расположение = ТекСтрока.РасположениеШины;
		ОбластьМакета.Параметры.Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		ОбластьМакета.Параметры.Примечание = ТекСтрока.Примечание;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Контрагент);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Функция печати договора продажи
Функция ПечатьДоговорХранения(ТабДокумент) Экспорт
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	//макет
	Макет = ПолучитьМакет("ДоговорХранения");
	//заголовок документа
	ОбластьМакета = Макет.ПолучитьОбласть("ТекстДоговораВерх");
	ДанныеОбъекта = ПолучитьДанныеОбъекта();
	ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,ДанныеОбъекта);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицыТоваров");
	ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,ДанныеОбъекта.ТаблицаТоваров);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Для Каждого ТекСтрока Из ДанныеОбъекта.ТаблицаТоваров.Строки Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицыТоваров");
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,ТекСтрока);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалТаблицыТоваров");
	ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,ДанныеОбъекта.ТаблицаТоваров);
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ТекстДоговораНиз");
	ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,ДанныеОбъекта);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("АдресаРеквизиты");
	ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,ДанныеОбъекта);
	Попытка
		Если НЕ ТабДокумент.ПроверитьВывод(ОбластьМакета) Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьБирок(ТабДокумент) Экспорт
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	ДатаДок = Формат(Дата, "ДЛФ=ДД");
	КонтрагентНаименование = спПолучитьНаименование(Контрагент);	
	
	Макет = ПолучитьМакет("Бирка");
	СтрокНаЛисте 	= 4;
	КолонокНаЛисте 	= 2;
	СтрокаНаЛисте	= 0; 
	КолонкаНаЛисте	= КолонокНаЛисте+1;	
	
	////ячейка и склад - из приходного складского ордера
	//Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//			   |	ПриходныйСкладскойОрдер.СкладКомпании,
	//			   |	ПриходныйСкладскойОрдерТовары.Ячейка,
	//			   |	ПриходныйСкладскойОрдерТовары.ХарактеристикаНоменклатуры,
	//			   |	ПриходныйСкладскойОрдерТовары.Номенклатура
	//			   |ИЗ
	//			   |	Документ.ПриходныйСкладскойОрдер.Товары КАК ПриходныйСкладскойОрдерТовары
	//			   |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйСкладскойОрдер КАК ПриходныйСкладскойОрдер
	//			   |		ПО ПриходныйСкладскойОрдерТовары.Ссылка = ПриходныйСкладскойОрдер.Ссылка
	//			   |ГДЕ
	//			   |	ПриходныйСкладскойОрдер.ДокументОснование = &ДокументОснование";
	//Запрос.УстановитьПараметр("ДокументОснование",Ссылка);
	//ТаблицаХранения = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрока Из Шины Цикл
	
		ОбластьМакета = Макет.ПолучитьОбласть("Строка|Колонка");
		ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаДок = ДатаДок;
		ОбластьМакета.Параметры.Номенклатура = спПолучитьНаименование(ТекСтрока.Номенклатура);
		ОбластьМакета.Параметры.Диск = ТекСтрока.Диск;
		ОбластьМакета.Параметры.Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		ОбластьМакета.Параметры.Расположение = ТекСтрока.РасположениеШины;
		Если обПраво("ХранениеШинКомплектами",Права,,ЭтотОбъект) Тогда
			ОбластьМакета.Параметры.Ячейка = Ячейка;
		Иначе
			ОбластьМакета.Параметры.Ячейка = ТекСтрока.Ячейка;
		КонецЕсли;
		ОбластьМакета.Параметры.Склад = СкладКомпании;
		
		////ищем ячейку и склад
		//СтруктураПоиска = Новый Структура;
		//СтруктураПоиска.Вставить("Номенклатура",ТекСтрока.Номенклатура);
		//СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
		//МассивСтрок = ТаблицаХранения.НайтиСтроки(СтруктураПоиска);
		//Если МассивСтрок.Количество()>0 Тогда
		//	ОбластьМакета.Параметры.Ячейка =  МассивСтрок[0].Ячейка;
		//	ОбластьМакета.Параметры.Склад = МассивСтрок[0].СкладКомпании;
		//КонецЕсли;	
			
		зфПечатьШК(ЭтотОбъект,ТабДокумент);
		
		Если КолонкаНаЛисте>=КолонокНаЛисте Тогда
			//Если в строке выведено этикеток более заданного - начнем новую строку
			СтрокаНаЛисте=СтрокаНаЛисте+1;
			КолонкаНаЛисте=1;
			ТабДокумент.Вывести(ОбластьМакета);
		Иначе
			//Иначе продолжаем вывод в текущую строку
			КолонкаНаЛисте=КолонкаНаЛисте+1;
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		Если СтрокаНаЛисте>=СтрокНаЛисте Тогда
			//Если на листе выведено этикеток более заданного - начнем новый лист
			СтрокаНаЛисте=0;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьЭтикетокИЦенников()

//ШАБЛОНЫ ОФИСНЫХ ДОКУМЕНТОВ

// Формирует структуру с данными объекта, которые необходимы для заполнения шаблона
Функция ПолучитьДанныеОбъекта() Экспорт
		
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ДанныеОбъекта = Новый Структура;
	
	ДанныеОбъекта.Вставить("НомерДокумента", НомерДляПечати);
	ДанныеОбъекта.Вставить("ДатаДокумента", Формат(Дата,"ДЛФ=ДД"));
	ДанныеОбъекта.Вставить("ПредставлениеОрганизации", спПолучитьНаименование(ЭтотОбъект.Организация));
	ДанныеОбъекта.Вставить("ФирмаАдрес", киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический));
	ДанныеОбъекта.Вставить("ФирмаТелефоны", киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий));
	ДанныеОбъекта.Вставить("ЗаказчикПолноеНаименование", спПолучитьНаименование(Контрагент));
	ДанныеОбъекта.Вставить("ЗаказчикПочтовыйАдрес", киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый));
	ДанныеОбъекта.Вставить("ЗаказчикТелефоны", киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "	
		|ВЫБРАТЬ
		|	ПодтверждающиеДокументы.Ссылка КАК Документ,
		|	ПодтверждающиеДокументы.КемВыдан КАК ДокументКемВыдан
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Владелец
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|";
	Запрос.УстановитьПараметр("Владелец", ЭтотОбъект.Контрагент);		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	Если ВыборкаДокументов.Количество()=0 Тогда
		ДанныеОбъекта.Вставить("ЗаказчикДокумент", "__________________________________");
	Иначе 
		ВыборкаДокументов.Следующий();
		ПредставлениеДокумента = Строка(ВыборкаДокументов.Документ);
		Если НЕ ПустаяСтрока(ВыборкаДокументов.ДокументКемВыдан) Тогда
			ПредставлениеДокумента = ПредставлениеДокумента + " " +ВыборкаДокументов.ДокументКемВыдан;
		КонецЕсли;
		ДанныеОбъекта.Вставить("ЗаказчикДокумент", ПредставлениеДокумента);
	КонецЕсли;
	
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ДанныеОбъекта.Вставить("РуководительДолжность", НРЕГ(Руководитель.РуководительДолжность));
	ДанныеОбъекта.Вставить("РуководительПредставление", Руководитель.РуководительПредставление);
	
	ДанныеОбъекта.Вставить("СуммаДокумента", Формат(СуммаДокумента,"ЧДЦ=2; ЧН=0,00"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", ВалютаДокумента);
	ДанныеОбъекта.Вставить("ДатаНачала", Формат(ДатаНачала,"ДФ=dd.MM.yyyy"));
	ДанныеОбъекта.Вставить("ДатаОкончания", Формат(ДатаОкончания,"ДФ=dd.MM.yyyy"));
	
	ДанныеОбъекта.Вставить("ИННКПП", "ИНН " + Организация.ИНН + " / КПП " + Организация.КПП);
	ДанныеОбъекта.Вставить("НомерСчетаПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.НомерСчета);
	ДанныеОбъекта.Вставить("БанкПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк);
	ДанныеОбъекта.Вставить("БИКБанкаПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.Код);
	ДанныеОбъекта.Вставить("СчетБанкаПолучателя", ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.КоррСчет);

	ДанныеОбъекта.Вставить("РуководительДолжность", НРЕГ(Руководитель.РуководительДолжность));
	ДанныеОбъекта.Вставить("РуководительПредставление", Руководитель.РуководительПредставление);
	
	//Таблица товаров (услуг)
	ДанныеОбъекта.Вставить("ТаблицаТоваров", Новый Структура);			
	ДанныеОбъекта.ТаблицаТоваров.Вставить("Строки", Новый Массив);		
	НомерСтрокиПоПорядку = 0;
	Для каждого СтрокаТоваров Из Товары Цикл
		НомерСтрокиПоПорядку = НомерСтрокиПоПорядку + 1;
		СтрокаТаблицы = Новый Структура;
		СтрокаТаблицы.Вставить("НомерСтроки",	НомерСтрокиПоПорядку);
		СтрокаТаблицы.Вставить("Номенклатура",	спПолучитьНаименование(СтрокаТоваров.Номенклатура));
		СтрокаТаблицы.Вставить("СуммаВсего",	Формат(СтрокаТоваров.СуммаВсего,"ЧДЦ=2; ЧН=0,00"));
		СтрокаТаблицы.Вставить("Количество",	СтрокаТоваров.Количество);
		Если СтрокаТоваров.Количество <> 0 Тогда
			СтрокаТаблицы.Вставить("Цена",	Формат((СтрокаТоваров.СуммаВсего/СтрокаТоваров.Количество),"ЧДЦ=2; ЧН=0,00"));
		Иначе
			СтрокаТаблицы.Вставить("Цена",	Формат(0,"ЧДЦ=2; ЧН=0,00"));
		КонецЕсли;
		ДанныеОбъекта.ТаблицаТоваров.Строки.Добавить(СтрокаТаблицы);
	КонецЦикла;
	ДанныеОбъекта.ТаблицаТоваров.Вставить("СуммаИтого", Формат(Товары.Итог("СуммаВсего"),"ЧДЦ=2; ЧН=0,00"));		
	
	Возврат ДанныеОбъекта;
	
КонецФункции

// Формирует структуру с описанием областей шаблона
Функция ПолучитьОписаниеОбластейМакетаОфисногоДокумента() Экспорт
	
	ОписаниеОбластей = Новый Структура;
	
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ВерхнийКолонтитул",	"ВерхнийКолонтитул");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "НижнийКолонтитул",		"НижнийКолонтитул");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ТекстДоговораВерх",	"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ТекстДоговораНиз",		"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "АдресаРеквизиты",		"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаТаблицыТоваров",	"Общая");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицыТоваров",	"СтрокаТаблицы");
	пчДобавитьОписаниеОбласти(ОписаниеОбластей, "ПодвалТаблицыТоваров",	"Общая");
		
	Возврат ОписаниеОбластей;
	
КонецФункции

// Формирует печатную форму "ЗаказНаряд"
// Возвращает сформированный табличный документ:
Функция ПечатьШаблонаДоговорХраненияШин(ТабДокумент,ДвоичныеДанныеМакета,ИмяМакета) Экспорт
	
	Области = ПолучитьОписаниеОбластейМакетаОфисногоДокумента();
	ДанныеОбъекта = ПолучитьДанныеОбъекта();

	Попытка
		
		Если пчПроверитьCOMСоединениеMSWord() Тогда
			ТипМакета = "MS";
		ИначеЕсли пчПроверитьCOMСоединениеOOWriter() Тогда
			ТипМакета = "OO";
		Иначе
			Сообщить("Ошибка при попытке установки соединения. 
			|Для вывода печатных форм требуется, чтобы на компьютере был установлен пакет Microsoft Office или Open Office.");
			Возврат Ложь;	
		КонецЕсли;
		
		Макет		   = пчИнициализироватьМакет(ДвоичныеДанныеМакета, ТипМакета);
	 	ПечатнаяФорма  = пчИнициализироватьПечатнуюФорму(ТипМакета, Макет.НастройкиСтраницыМакета);
		
		//ВесьДокумент = Макет.COMСоединение.ActiveDocument.Content;
		//ВесьДокумент.Select();
		//Поиск = Макет.COMСоединение.Selection.Find;
		
		Если ПечатнаяФорма <> Неопределено И Макет <> Неопределено Тогда
			
			// Вывод колонтитулов документа.
			Область = пчПолучитьОбласть(Макет, Области["ВерхнийКолонтитул"]);
			пчПрисоединитьОбласть(ПечатнаяФорма, Область);
			
			Область = пчПолучитьОбласть(Макет, Области["НижнийКолонтитул"]);
			пчПрисоединитьОбласть(ПечатнаяФорма, Область);
			
			Область = пчПолучитьОбласть(Макет, Области["ТекстДоговораВерх"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта);
			
			Область = пчПолучитьОбласть(Макет, Области["ШапкаТаблицыТоваров"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаТоваров, Ложь);
			
			Область = пчПолучитьОбласть(Макет, Области["СтрокаТаблицыТоваров"]);
			пчПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаТоваров.Строки, Ложь);
			
			Область = пчПолучитьОбласть(Макет, Области["ПодвалТаблицыТоваров"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ТаблицаТоваров, Истина, Истина);
			
			Область = пчПолучитьОбласть(Макет, Области["ТекстДоговораНиз"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Область = пчПолучитьОбласть(Макет, Области["АдресаРеквизиты"]);
			пчПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			пчПоказатьДокумент(ПечатнаяФорма);
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Сообщить(ИнформацияОбОшибке);
		пчОчиститьСсылки(ПечатнаяФорма,Ложь);
		пчОчиститьСсылки(Макет);
		Возврат Ложь;
	КонецПопытки;
	
	пчОчиститьСсылки(ПечатнаяФорма,Ложь);
	пчОчиститьСсылки(Макет);
	
	Возврат ТабДокумент;
	
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	дкОбработкаЗаполнения(ЭтотОбъект,Основание);
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Контрагент=ДокументОснование.Заказчик;
		ДоговорВзаиморасчетов=Неопределено;
		ОбработкаРеквизита("Контрагент");
		Товары.Очистить();
	КонецЕсли;
	
 	ДатаНачала=Дата;
	ДатаОкончания=Неопределено;
	ОбработкаРеквизита("ДатаНачала");
	Если СкладКомпании.ВидСклада=Перечисления.ВидыСкладов.Обычный Тогда
		СкладКомпании = Неопределено;
	КонецЕсли; 
	ЗаполнитьШинамиПоУмолчанию();
	ЗаполнитьУслугамиПоУмолчанию();
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	ДатаНачала=ТекущаяДата();
	ДатаОкончания=Неопределено;
	ОбработкаРеквизита("ДатаНачала");
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ХранениеШинКомплектами=обПраво("ХранениеШинКомплектами",Права,,ЭтотОбъект);
	Если ХранениеШинКомплектами Тогда
		Для каждого СтрокаТЧ Из Шины Цикл
			СтрокаТЧ.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
		КонецЦикла; 
	Иначе
		Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
	КонецЕсли; 
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;

	// Выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли    

// Модуль документа ПеремещениеТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// получает цену на товар из документа поставки
//
// Параметры:
//  ДокПоставки                - Характеристика - ТипыПартий,
//  Номенклатура               - СправочникСсылка - Номенклатура,
//  СтавкаНДС                  - СправочникСсылка - СтавкиНДС,
//  ХарактеристикаНоменклатуры - СправочникСсылка - ХарактеристикиНоменклатуры.
//
// Возвращаемое значение:
//  Цена - число - цена на товар из документа поставки.
//
Функция ПолучитьЦенуИзДокументаПоставки(ДокПоставки,Номенклатура,СтавкаНДС=Неопределено,ХарактеристикаНоменклатуры=Неопределено)
	// проверки
	Если обЗначениеНеЗаполнено(ДокПоставки) Тогда Возврат 0; КонецЕсли;
	Если обЗначениеНеЗаполнено(Номенклатура) Тогда Возврат 0; КонецЕсли;
	ДокПоставкиТовары=ДокПоставки.Метаданные().ТабличныеЧасти.Найти("Товары");
	Если ДокПоставкиТовары=Неопределено Тогда Возврат 0; КонецЕсли; 
	ЕстьЦена=(ДокПоставкиТовары.Реквизиты.Найти("Цена")<>Неопределено);
	ЕстьСтавкаНДС=(ДокПоставкиТовары.Реквизиты.Найти("СтавкаНДС")<>Неопределено);
	ЕстьТипЦен=(ДокПоставки.Метаданные().Реквизиты.Найти("ТипЦен")<>Неопределено);
	
	// получаем цены
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	"+?(ЕстьЦена,"ДокТовары.Цена","0")+" КАК Цена,
	|	"+?(ЕстьСтавкаНДС,"ДокТовары.СтавкаНДС","ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)")+" КАК СтавкаНДС,
	|	"+?(ЕстьТипЦен,"ДокТовары.Ссылка.ТипЦен","ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)")+" КАК ТипЦен
	|ИЗ
	|	Документ."+ДокПоставки.Метаданные().Имя+".Товары КАК ДокТовары
	|ГДЕ
	|	ДокТовары.Ссылка=&Ссылка И ДокТовары.Номенклатура=&Номенклатура
	|"+?(обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры),""," И ДокТовары.ХарактеристикаНоменклатуры=&ХарактеристикаНоменклатуры"));
	Запрос.УстановитьПараметр("Ссылка",ДокПоставки);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
	РезультатЦена=Запрос.Выполнить();
	Цена=0;
	Если НЕ РезультатЦена.Пустой() Тогда
		СтрокаДокумента = РезультатЦена.Выгрузить().Получить(0);
		Цена = дкПересчетЦены(СтрокаДокумента.Цена,СтрокаДокумента.ТипЦен,СтрокаДокумента.СтавкаНДС,ТипЦен,СтавкаНДС);
	КонецЕсли;
	
	Возврат Цена;
КонецФункции // ПолучитьЦенуИзДокументаПоставки()

// Заполняет резервами подразделения
//
// Параметры:
//  ЗаказВнутренний - ДокументСсылка - ДокументОснование.
//
Процедура ЗаполнитьРезервамиПодразделения(ЗаказВнутренний) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст= "ВЫБРАТЬ
				 |		ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
				 |		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				 |		ЗаказыПокупателейОстатки.РезервОстаток КАК Количество
				 |	ИЗ
				 |		РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+",
				 |		Заказ.ПодразделениеКомпании = &ПодразделениеКомпании"+?(ЗаказВнутренний=Неопределено,""," И Заказ = &Заказ")
				 		+ ?(ТипЗнч(СкладКомпании) = Тип("СправочникСсылка.СкладыКомпании")," И СкладКомпании = &СкладКомпании ","") + "
				 |	) КАК ЗаказыПокупателейОстатки ";
	Запрос.УстановитьПараметр("НаМомент",МоментВремени());
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Заказ",ЗаказВнутренний);
	Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если ЗаказВнутренний<>Неопределено Тогда

		Запрос.Текст="ВЫБРАТЬ
		|	ЗаказыПокупателей.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЗаказыПокупателей.ЗаказаноОстаток КАК Заказано,
		|	ЗаказыПокупателей.СуммаУпрОстаток КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+", Заказ = &Заказ) КАК ЗаказыПокупателей";
		ВыборкаЗаказа = Запрос.Выполнить().Выбрать();
		ВалютаУпр     = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Иначе
		ВыборкаЗаказа = Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров);
		Если ВыборкаЗаказа<>Неопределено Тогда
			ВыборкаЗаказа.Сбросить();
			Если ВыборкаЗаказа.НайтиСледующий(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаТоваров.Номенклатура, СтрокаТоваров.ХарактеристикаНоменклатуры)) Тогда
				СтрокаТоваров.СуммаВсего=обПересчет(СтрокаТоваров.Количество*ВыборкаЗаказа.СуммаУпр/ВыборкаЗаказа.Заказано,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
				ОбработкаРеквизита("Товары.СуммаВсего",СтрокаТоваров);
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьРезервамиПодразделения()

// Заполняет резервами под заказ покупателя
//
// Параметры:
//  ЗаказПокупателя - ДокументСсылка - ДокументОснование.
//
Процедура ЗаполнитьРезервамиПодЗаказ(ЗаказПокупателя = Неопределено) Экспорт
	// очистим таблицу товаров
	Товары.Очистить();
	Если НЕ обЗначениеНеЗаполнено(ЗаказПокупателя) Тогда
		ТоварыЗаказа = ЗаказПокупателя.Товары;
	Иначе
		ТоварыЗаказа = Неопределено;
	КонецЕсли;
	
	ЕстьЗаказ = ЗначениеЗаполнено(ЗаказПокупателя);
	Запрос = Новый Запрос;
	Запрос.Текст= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
				|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	ЗаказыПокупателейОстатки.Заказ КАК ЗаказПокупателя,
				|	ЗаказыПокупателейОстатки.РезервОстаток КАК Количество,
				|	ЕСТЬNULL(ЗаказыПокупателей.ЗаказаноОстаток, 0) КАК Заказано,
				|	ЕСТЬNULL(ЗаказыПокупателей.СуммаОстаток,    0) КАК Сумма,
				|	ЕСТЬNULL(ЗаказыПокупателей.СуммаУпрОстаток, 0) КАК СуммаУпр
				|ИЗ
				|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+", " + ?(ЕстьЗаказ, "Заказ = &Заказ И ", "") + "СкладКомпании = &СкладКомпании) КАК ЗаказыПокупателейОстатки
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+", " + ?(ЕстьЗаказ, "Заказ = &Заказ", "") + ") КАК ЗаказыПокупателей
				|ПО
				|	ЗаказыПокупателейОстатки.Заказ                      = ЗаказыПокупателей.Заказ И 
				|	ЗаказыПокупателейОстатки.Номенклатура               = ЗаказыПокупателей.Номенклатура И 
				|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыПокупателей.ХарактеристикаНоменклатуры
				|";
	
	Запрос.УстановитьПараметр("НаМомент",      МоментВремени());
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("Заказ",         ЗаказПокупателя);
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Выборка   = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Коэффициент = Выборка.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
		СтрокаТоваров = Товары.Добавить();
		СтрокаТоваров.Номенклатура               = Выборка.Номенклатура;
		СтрокаТоваров.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		СтрокаТоваров.ЗаказПокупателя            = Выборка.ЗаказПокупателя;
		СтрокаТоваров.Количество                 = Выборка.Количество/?(Коэффициент = 0, 1, Коэффициент);
		ОбработкаРеквизита("Товары.Номенклатура", СтрокаТоваров);
		
		Если Выборка.Заказано>0 Тогда
			СуммаСтроки    = 0;
			Если ТипЗнч(СтрокаТоваров.ЗаказПокупателя)=Тип("ДокументСсылка.ЗаказПокупателя") И СтрокаТоваров.ЗаказПокупателя.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
				СуммаСтроки = Выборка.Сумма;
			Иначе
				СуммаСтроки = обПересчет(Выборка.СуммаУпр, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			Если СуммаСтроки > 0 Тогда
				СтрокаТоваров.СуммаВсего = Выборка.Количество*СуммаСтроки/Выборка.Заказано;
				ОбработкаРеквизита("Товары.СуммаВсего", СтрокаТоваров);
			КонецЕсли;
		КонецЕсли;
		
		//БИЗТЕХ МАМОНОВ 13.08.2025 ++ 
		//Чтобы ячейка тянулась по регистру
		//Если НЕ обЗначениеНеЗаполнено(ТоварыЗаказа) Тогда
		//	МассивЯчеек = ТоварыЗаказа.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",Выборка.Номенклатура,Выборка.ХарактеристикаНоменклатуры));
		//	Если МассивЯчеек.Количество() <> 0 Тогда
		//		СтрокаТоваров.ЯчейкаОтправитель = МассивЯчеек[0].Ячейка;
		//	КонецЕсли;
		//КонецЕсли;
		//БИЗТЕХ МАМОНОВ 13.08.2025 --
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьРезервамиПодЗаказ()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ЗаказПокупателя",2);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("Партия",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	
	//++СнимченкоАА_бизтех++
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	//--СнимченкоАА_бизтех--
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("СкладПолучатель",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары); 
	
	//++СнимченкоАА_бизтех++
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	//--СнимченкоАА_бизтех-- 

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если ТипЗнч(СкладКомпании) = Тип("СправочникСсылка.СкладыКомпании") Тогда
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",   КонтрольПоПодразделению);
			КонецЕсли;
			//Если ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
			//	КонтролируемыеРеквизитыШапки.Вставить("СкладПолучатель", КонтрольПоПодразделению);
			//КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки); 			
			
			Если БалансВедетсяПоОрганизации И обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
				КонтролируемыеРеквизитыТовары = Новый Структура();
				КонтролируемыеРеквизитыТовары.Вставить("Партия", Ложь);
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;		
			   							
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	//++СнимченкоАА_бизтех++
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	//--СнимченкоАА_бизтех--
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПеремещениеТоваров","Перемещение товаров",Истина);
	СписокХозОпераций.Добавить("ПеремещениеТоваровВФилиал","Перемещение товаров в филиал",Ложь);
	СписокХозОпераций.Добавить("ПеремещениеТоваровИзФилиала","Перемещение товаров из филиала",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  ШапкаДокумента - выборка.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента)
    ТекстЗапроса="
    |ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПеремещениеТоваровТовары.Партия КАК Партия,
	|	ПеремещениеТоваровТовары.ГТД КАК ГТД,
	|	СУММА(ПеремещениеТоваровТовары.КоличествоБазовое) КАК Количество,
	|	СРЕДНЕЕ(ПеремещениеТоваровТовары.ЦенаРозничная) КАК ЦенаНовая,
	|	СРЕДНЕЕ(ПеремещениеТоваровТовары.ЦенаРозничная) КАК ЦенаРозничная,
    |	СУММА("+?(ТипЗнч(ШапкаДокумента.СкладПолучатель)=Тип("СправочникСсылка.СкладыКомпании"), ?(ШапкаДокумента.СкладПолучатель.Розничный,"ПеремещениеТоваровТовары.ЦенаРозничная*ПеремещениеТоваровТовары.КоличествоБазовое","0"),"0")+") КАК СуммаРозн
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
    |ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка=&Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеТоваровТовары.Номенклатура,
	|	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры,
	|	ПеремещениеТоваровТовары.Партия,
	|	ПеремещениеТоваровТовары.ГТД
	|";

	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	Запрос.Текст=ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Документ.Ссылка - Ссылка на документ для которого получаем шапку
//
// Возвращаемое значение:
//  Выборка - возвращает выборку по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Если ТипЗнч(ДокументСсылка.СкладКомпании) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		СтрокаПодразделениеОтправителя = "Док.СкладКомпании.Подразделение";
	Иначе
		СтрокаПодразделениеОтправителя = "Док.СкладКомпании";
	КонецЕсли;
	Если ТипЗнч(ДокументСсылка.СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		СтрокаПодразделениеПолучателя = "Док.СкладПолучатель.Подразделение";
	Иначе
		СтрокаПодразделениеПолучателя = "Док.СкладПолучатель";
	КонецЕсли;
	
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании   КАК СкладКомпании,
	|	Док.СкладПолучатель КАК СкладПолучатель,
	|	"+СтрокаПодразделениеОтправителя+" КАК ПодразделениеОтправителя,
	|	"+СтрокаПодразделениеПолучателя+" КАК ПодразделениеПолучателя, 
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// БАЛАНС: Если происходит перемещение товаров между складами подразделений, принадлежащих
	// различным балансовым "веткам", то возможен разрыв баланса.
	ДобавлятьКорректирующиеЗаписи = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	ПодразделениеОтправителя      = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеОтправителя);
	ПодразделениеПолучатель       = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеПолучателя);
    ДобавлятьКорректирующиеЗаписи = ДобавлятьКорректирующиеЗаписи И (ПодразделениеОтправителя<>ПодразделениеПолучатель);
	ДобавлятьКорректирующиеЗаписи = ДобавлятьКорректирующиеЗаписи И (ХозОперация=Справочники.ХозОперации.ПеремещениеТоваров);
	
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейГТД=Движения.ГТДПартийТоваровКомпании;
	// если перемещаем из филиала, то оприходуем товар на склад получателя и все
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		
		СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
		ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
		
		// товары документа
		ЗапросТовары=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокТовары.Номенклатура КАК Номенклатура,
		|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокТовары.СтавкаНДС КАК СтавкаНДС,
		|	"+?(СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя,"&ПартияТоваровОтрицательныхОстатков","ДокТовары.Партия")+" КАК Партия,
		|	ДокТовары.ГТД КАК ГТД,
		|	Сумма(ДокТовары.СуммаНДС) КАК СуммаНДС,
		|	Сумма(ДокТовары.Сумма) КАК Сумма,
		|	Сумма(ДокТовары.СуммаВсего) КАК СуммаВсего,
		|	Сумма(ДокТовары.КоличествоБазовое) КАК Количество
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ДокТовары
		|ГДЕ
		|	  ДокТовары.Ссылка=&Ссылка
		|	И ДокТовары.Номенклатура.ВидНоменклатуры<>&Услуга
		|
		|СГРУППИРОВАТЬ ПО Номенклатура, ХарактеристикаНоменклатуры, СтавкаНДС, Партия, ГТД
		|");
		ЗапросТовары.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		ЗапросТовары.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		ЗапросТовары.УстановитьПараметр("ПартияТоваровОтрицательныхОстатков",ПартияТоваровОтрицательныхОстатков);
		РезультатЗапросаПоТоварам=ЗапросТовары.Выполнить();
		
		// партии документа отгрузки
		ЗапросПартии=Новый Запрос("
		|ВЫБРАТЬ
		|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпании.СтатусПартии КАК СтатусПартии,
		|	"+?(СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя,"&ПартияТоваровОтрицательныхОстатков","ПартииТоваровКомпании.Партия")+" КАК Партия,
		|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
		|	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма,
		|	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаУпр,
		|	СУММА(ПартииТоваровКомпании.СуммаНДС) КАК СуммаНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|ГДЕ
		|	ПартииТоваровКомпании.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровКомпании.Номенклатура,
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпании.СтатусПартии,
		|	ПартииТоваровКомпании.Партия
		|");
		ЗапросПартии.УстановитьПараметр("Регистратор",ШапкаДокумента.ДокументОснование);
		ЗапросПартии.УстановитьПараметр("ПартияТоваровОтрицательныхОстатков",ПартияТоваровОтрицательныхОстатков);
		ТаблицаПартий=ЗапросПартии.Выполнить().Выгрузить();
		
		//ГТД партий товаров компании
		ЗапросГТД=Новый Запрос("
		|ВЫБРАТЬ
		|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	"+?(СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя,"&ПартияТоваровОтрицательныхОстатков","ГТДПартийТоваровКомпании.Партия")+" КАК Партия,
		|	ГТДПартийТоваровКомпании.ГТД КАК ГТД,
		|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК Количество
		|ИЗ
		|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
		|ГДЕ
		|	ГТДПартийТоваровКомпании.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ГТДПартийТоваровКомпании.Номенклатура,
		|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры,
		|	ГТДПартийТоваровКомпании.Партия,
		|	ГТДПартийТоваровКомпании.ГТД
		|");
		ЗапросГТД.УстановитьПараметр("Регистратор",ШапкаДокумента.ДокументОснование);
		ЗапросГТД.УстановитьПараметр("ПартияТоваровОтрицательныхОстатков",ПартияТоваровОтрицательныхОстатков);
		ТаблицаГТД=ЗапросГТД.Выполнить().Выгрузить();
		
		// приходуем
		ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
		КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
			КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр = ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;	
		ВыборкаТовары=РезультатЗапросаПоТоварам.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			НадоОприходовать=ВыборкаТовары.Количество;
			НадоОприходоватьГТД=ВыборкаТовары.Количество;
			СтруктураОтбора=Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ВыборкаТовары.Номенклатура,ВыборкаТовары.ХарактеристикаНоменклатуры);
			Если НЕ обЗначениеНеЗаполнено(ВыборкаТовары.ХарактеристикаНоменклатуры) Тогда
				СтруктураОтбора.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
			Иначе
				СтруктураОтбора.ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ВыборкаТовары.Партия) Тогда
				СтруктураОтбора.Вставить("Партия",ВыборкаТовары.Партия);
			КонецЕсли; 
			МассивНайденныхПартий=ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
			Для Сч=0 По МассивНайденныхПартий.ВГраница() Цикл
				ТекСтрока=МассивНайденныхПартий[Сч];
				КоличествоПартииВсего=ТекСтрока.Количество;
				НоваяЗапись=НаборЗаписейПартии.Добавить();
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период=ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗапись.Партия = ТекСтрока.Партия;
				НоваяЗапись.СтатусПартии=ТекСтрока.СтатусПартии;
				НоваяЗапись.СкладКомпании=ШапкаДокумента.СкладПолучатель;
				НоваяЗапись.Номенклатура=ВыборкаТовары.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗапись.СтавкаНДС = ВыборкаТовары.СтавкаНДС;
				НоваяЗапись.Количество=Мин(ТекСтрока.Количество,НадоОприходовать);
				НоваяЗапись.Сумма=Окр(ТекСтрока.Сумма*НоваяЗапись.Количество/КоличествоПартииВсего,2);
				НоваяЗапись.СуммаУпр=Окр(ТекСтрока.СуммаУпр*НоваяЗапись.Количество/КоличествоПартииВсего,2);
				НоваяЗапись.СуммаНДС=Окр(ТекСтрока.СуммаНДС*НоваяЗапись.Количество/КоличествоПартииВсего,2);
				//Уменьшаем приходуемое количество
				НадоОприходовать=НадоОприходовать-НоваяЗапись.Количество;
				НадоОприходоватьПоПартии=НоваяЗапись.Количество;
				//Оприходуем ГТД партий
				СтруктураОтбора.Вставить("Партия",ТекСтрока.Партия);
				Если НЕ обЗначениеНеЗаполнено(ВыборкаТовары.ГТД) Тогда
					СтруктураОтбора.Вставить("ГТД",ВыборкаТовары.ГТД);
				КонецЕсли; 
				МассивНайденныхГТД=ТаблицаГТД.НайтиСтроки(СтруктураОтбора);
				Для СчГТД=0 По МассивНайденныхГТД.ВГраница() Цикл
					Если НадоОприходоватьПоПартии=0 Тогда
						Прервать;
					КонецЕсли; 
					ТекСтрокаГТД=МассивНайденныхГТД[СчГТД];
					НоваяЗаписьГТД=НаборЗаписейГТД.Добавить();
					НоваяЗаписьГТД.ВидДвижения=ВидДвиженияНакопления.Приход;
					НоваяЗаписьГТД.Период=ШапкаДокумента.Дата;
					НоваяЗаписьГТД.Регистратор=ШапкаДокумента.Ссылка;
					НоваяЗаписьГТД.Партия=ТекСтрокаГТД.Партия;
					НоваяЗаписьГТД.ГТД=ТекСтрокаГТД.ГТД;
					НоваяЗаписьГТД.СкладКомпании=ШапкаДокумента.СкладПолучатель;
					НоваяЗаписьГТД.Номенклатура=ТекСтрокаГТД.Номенклатура;
					НоваяЗаписьГТД.ХарактеристикаНоменклатуры=ТекСтрокаГТД.ХарактеристикаНоменклатуры;
					НоваяЗаписьГТД.ХозОперация=ШапкаДокумента.ХозОперация;
					НоваяЗаписьГТД.Количество=Мин(ТекСтрокаГТД.Количество,НадоОприходоватьПоПартии);
					НадоОприходоватьПоПартии=НадоОприходоватьПоПартии-НоваяЗаписьГТД.Количество;
					НадоОприходоватьГТД=НадоОприходоватьГТД-НоваяЗаписьГТД.Количество;
					ТекСтрокаГТД.Количество=ТекСтрокаГТД.Количество-НоваяЗаписьГТД.Количество;
					Если ТекСтрокаГТД.Количество=0 Тогда
						// удалим за ненадобностью
						ТаблицаГТД.Удалить(ТекСтрокаГТД);
					КонецЕсли; 
				КонецЦикла;
				//Если указана ГТД, то оприходуем и ее
				Если (НЕ обЗначениеНеЗаполнено(ВыборкаТовары.ГТД)) И НадоОприходоватьПоПартии>0 Тогда
					НоваяЗаписьГТД=НаборЗаписейГТД.Добавить();
					НоваяЗаписьГТД.ВидДвижения=ВидДвиженияНакопления.Приход;
					НоваяЗаписьГТД.Период=ШапкаДокумента.Дата;
					НоваяЗаписьГТД.Регистратор=ШапкаДокумента.Ссылка;
					НоваяЗаписьГТД.Партия=ТекСтрока.Партия;
					НоваяЗаписьГТД.ГТД=ВыборкаТовары.ГТД;
					НоваяЗаписьГТД.СкладКомпании=ШапкаДокумента.СкладПолучатель;
					НоваяЗаписьГТД.Номенклатура=ВыборкаТовары.Номенклатура;
					НоваяЗаписьГТД.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
					НоваяЗаписьГТД.ХозОперация=ШапкаДокумента.ХозОперация;
					НоваяЗаписьГТД.Количество=НадоОприходоватьПоПартии;
					НадоОприходоватьГТД=НадоОприходоватьГТД-НоваяЗаписьГТД.Количество;
				КонецЕсли; 
				ТекСтрока.Количество=ТекСтрока.Количество-НоваяЗапись.Количество;
				ТекСтрока.Сумма=ТекСтрока.Сумма - НоваяЗапись.Сумма;
				ТекСтрока.СуммаУпр=ТекСтрока.СуммаУпр - НоваяЗапись.СуммаУпр;
				ТекСтрока.СуммаНДС=ТекСтрока.СуммаНДС - НоваяЗапись.СуммаНДС;
				Если ТекСтрока.Количество=0 Тогда
					// удалим за ненадобностью
					ТаблицаПартий.Удалить(ТекСтрока);
				КонецЕсли; 
				// надо ли дальше приходовать
				Если НадоОприходовать<=0 Тогда Прервать; КонецЕсли;
			КонецЦикла;
			// Если остался не оприходованный товар, то оприходуем на себя или на указанную партию
			Если НадоОприходовать>0 Тогда
				НоваяЗапись=НаборЗаписейПартии.Добавить();
				НоваяЗапись.ВидДвижения   = ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период        = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор   = ШапкаДокумента.Ссылка;
				НоваяЗапись.Партия        = ?(обЗначениеНеЗаполнено(ВыборкаТовары.Партия),ШапкаДокумента.Ссылка,ВыборкаТовары.Партия);
				Если обЗначениеНеЗаполнено(ВыборкаТовары.Партия) Тогда
					НоваяЗапись.СтатусПартии  = Перечисления.СтатусыПартий.ТоварКупленный;
				Иначе
					СтруктураОтбора=Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Партия");
					СтруктураОтбора.Номенклатура=ВыборкаТовары.Номенклатура; 
					СтруктураОтбора.Партия=ВыборкаТовары.Партия;
					Если НЕ обЗначениеНеЗаполнено(ВыборкаТовары.ХарактеристикаНоменклатуры) Тогда
						СтруктураОтбора.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
					Иначе
						СтруктураОтбора.ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
					КонецЕсли;
					МассивНайденныхПартий=ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
					Если МассивНайденныхПартий.Количество() > 0 Тогда
						НоваяЗапись.СтатусПартии = МассивНайденныхПартий[0].СтатусПартии;
					Иначе
						НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
					КонецЕсли;
				КонецЕсли;
				НоваяЗапись.СкладКомпании = ШапкаДокумента.СкладПолучатель;
				НоваяЗапись.Номенклатура  = ВыборкаТовары.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаТовары.ХарактеристикаНоменклатуры;
				НоваяЗапись.ХозОперация = ШапкаДокумента.ХозОперация;
				НоваяЗапись.Количество  = НадоОприходовать;
				НоваяЗапись.СтавкаНДС   = ВыборкаТовары.СтавкаНДС;
				НоваяЗапись.Сумма       = Окр(обПересчет(ВыборкаТовары.СуммаВсего*НоваяЗапись.Количество/ВыборкаТовары.Количество,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл),2);
				НоваяЗапись.СуммаУпр    = Окр(обПересчет(ВыборкаТовары.СуммаВсего*НоваяЗапись.Количество/ВыборкаТовары.Количество,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр),2);
				НоваяЗапись.СуммаНДС    = Окр(обПересчет(ВыборкаТовары.СуммаНДС*НоваяЗапись.Количество/ВыборкаТовары.Количество,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл),2);
			КонецЕсли;
			//Если указана ГТД, то оприходуем и ее
			Если (НЕ обЗначениеНеЗаполнено(ВыборкаТовары.ГТД)) И НадоОприходоватьГТД>0 Тогда
				НоваяЗаписьГТД=НаборЗаписейГТД.Добавить();
				НоваяЗаписьГТД.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗаписьГТД.Период=ШапкаДокумента.Дата;
				НоваяЗаписьГТД.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗаписьГТД.Партия=ВыборкаТовары.Партия;
				НоваяЗаписьГТД.ГТД=ВыборкаТовары.ГТД;
				НоваяЗаписьГТД.СкладКомпании=ШапкаДокумента.СкладПолучатель;
				НоваяЗаписьГТД.Номенклатура=ВыборкаТовары.Номенклатура;
				НоваяЗаписьГТД.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
				НоваяЗаписьГТД.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗаписьГТД.Количество=НадоОприходоватьГТД;
				//НадоОприходоватьГТД=НадоОприходоватьГТД-НоваяЗаписьГТД.Количество;
			КонецЕсли; 
		КонецЦикла;
		
		// доходы и расходы
		НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыРасходы.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДоходыРасходы.Подразделение  = ШапкаДокумента.ПодразделениеПолучателя;
		НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
		НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
		НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыРасходы.Доход			 = 0;
		Для Каждого ЗаписьПартий Из НаборЗаписейПартии Цикл
			Если ЗаписьПартий.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный Тогда
				НаборЗаписейДоходыРасходы.Доход=НаборЗаписейДоходыРасходы.Доход+ЗаписьПартий.СуммаУпр;
			КонецЕсли;
		КонецЦикла;
		Отказ=НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
	Иначе
		// проверяем, присутствуют ли партии в табличной части
		ЕстьПартии = Ложь;
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
		РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента);
		//НаборЗаписейПартии        = Движения.ПартииТоваровКомпании;
		НаборЗаписейПартии.ДокументОбъект       = ЭтотОбъект;
		НаборЗаписейПартии.СкладКомпании        = ШапкаДокумента.СкладКомпании;
		НаборЗаписейПартии.ИмяРеквизитаДокумент = ?(ЕстьПартии,"Партия","");
		//НаборЗаписейПартии.ИмяРеквизитаДокумент = ?(обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект),"Партия","");
		НаборЗаписейПартии.СкладКомпанииКуда    = ШапкаДокумента.СкладПолучатель;
		НаборЗаписейПартии.ШапкаДокумента       = ШапкаДокумента;
		
		// если перемещение в филиал, то не будем приходовать
		Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
			НаборЗаписейПартии.СкладКомпанииКуда=Неопределено;
		КонецЕсли;
		НаборЗаписейПартии.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
		НаборЗаписейПартии.ПоБазовомуКоличеству=Истина;
		Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
		НаборЗаписейПартии.ГраницаРасчетаОстатков=Неопределено; // сбросим границу
		
		// Доходы и расходы
		Если ДобавлятьКорректирующиеЗаписи ИЛИ (ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровВФилиал) Тогда
			// У подразделения склада-отправителя возникает расход.
			// У подразделения склада-получателя - доход.
			// Если это перемещение в филиал, то движений "приход" не будет.
			ТаблицаПартий = НаборЗаписейПартии.Выгрузить();
			ТаблицаПартий.Свернуть("СтатусПартии, ВидДвижения","СуммаУпр");
			
			Для Каждого ТекСтрокаДвижения Из ТаблицаПартий Цикл  	
				Если ТекСтрокаДвижения.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаДиР = ТекСтрокаДвижения.СуммаУпр;
				Если СуммаДиР<>0 Тогда
					Если ТекСтрокаДвижения.ВидДвижения=ВидДвиженияНакопления.Расход Тогда
						НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
						НаборЗаписейДоходыРасходы.ДокументОбъект = ЭтотОбъект;
						// Если способ ведения баланса не по подразделению то подразделение должно быть как у документа
						Если Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению Тогда
							НаборЗаписейДоходыРасходы.Подразделение = ШапкаДокумента.ПодразделениеОтправителя;
						Иначе
							НаборЗаписейДоходыРасходы.Подразделение = Неопределено;
						КонецЕсли;
						НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
						НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
						НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
						НаборЗаписейДоходыРасходы.Расход         = СуммаДиР;
						Отказ = НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
					Иначе
						НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
						НаборЗаписейДоходыРасходы.ДокументОбъект = ЭтотОбъект;
						НаборЗаписейДоходыРасходы.Подразделение  = ШапкаДокумента.ПодразделениеПолучателя; 
						НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
						НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
						НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
						НаборЗаписейДоходыРасходы.Доход          = СуммаДиР;
						Отказ = НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
					КонецЕсли;
				КонецЕсли;			
			КонецЦикла;			
		КонецЕсли; 	
	КонецЕсли;
	
	Если НЕ Отказ Тогда НаборЗаписейПартии.Записать(); КонецЕсли; 	
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаСкладов = Движения.ПартииТоваровКомпании.Выгрузить();
			ТаблицаСкладов.Свернуть("СкладКомпании");

			НаборЗаписейГраницыПартий.СкладКомпании		= ТаблицаСкладов.ВыгрузитьКолонку("СкладКомпании");;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
//
// Возвращаемое значение:
//  СуммаВсего - число.
//
Функция РассчитатьСуммуВсего()Экспорт
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		СуммаВсего = Товары.Итог("СуммаВсего");
	Иначе
		СуммаВсего = дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", тКэшСуммСписания);
	КонецЕсли;
	
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Перем флПолучатель; // флаг изменения склада
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СкладКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			ДопПараметры.Вставить("Получатель",Ложь);
		Иначе
			ДопПараметры = Новый Структура("Получатель",Ложь);
		КонецЕсли;
		ОбработкаРеквизита("ОчиститьЯчейки",,,ДопПараметры);
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="СкладПолучатель" Тогда
		Если ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
			Рез=ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Если НЕ обЗначениеНеЗаполнено(СкладПолучатель) Тогда
				Если СкладПолучатель.Розничный Тогда
					Для Каждого СтрокаТЧ Из Товары Цикл
						СтрокаТЧ.ЦенаРозничная=обПолучитьЦену(СкладПолучатель.ТипЦенРозничнойТорговли,СтрокаТЧ.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, СтрокаТЧ.ХарактеристикаНоменклатуры, СтрокаТЧ.ЕдиницаИзмерения, СкладПолучатель.Подразделение);
						Рез=ОбработкаРеквизита("Товары.ЦенаРозничная",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Рез = Истина;
		КонецЕсли;
		Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			ДопПараметры.Вставить("Получатель",Истина);
		Иначе
			ДопПараметры = Новый Структура("Получатель",Истина);
		КонецЕсли;
		ОбработкаРеквизита("ОчиститьЯчейки",,,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании" И "СкладПолучатель"
		Если НЕ ЗначениеЗаполнено(СкладКомпании) И НЕ ЗначениеЗаполнено(СкладПолучатель) Тогда
	    КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ОчиститьЯчейки" Тогда
		// очистим ячейки
		Если Товары.Количество() <> 0 Тогда
			Если НЕ ДопПараметры.Свойство("Получатель",флПолучатель) Тогда
				флПолучатель = Ложь;
			КонецЕсли;
			Если флПолучатель Тогда
				ВремСклад  = СкладПолучатель;
				ИмяКолонки = "ЯчейкаПолучатель";
			Иначе
				ВремСклад  = СкладКомпании;
				ИмяКолонки = "ЯчейкаОтправитель";
			КонецЕсли;
			
			Для Каждого стрТовары Из Товары Цикл
				Если НЕ обЗначениеНеЗаполнено(стрТовары.Номенклатура) Тогда
					стрТовары[ИмяКолонки] = Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.Номенклатура,ВремСклад);
				Иначе
					стрТовары[ИмяКолонки] = Справочники.ЯчейкиХранения.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ЗначениеЗаполнено(ПодразделениеКомпании) Тогда
			Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
			Иначе
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
			КонецЕсли;
			
			СпособКонтроляКорректностиДоговораИСклада = обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании, "КонтрольКорректностиДоговораИСкладаДокумента", ЭтотОбъект);
			ЗакрытиеЗаказовПоОрганизации = (НЕ СпособКонтроляКорректностиДоговораИСклада = Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		КонецЕсли;
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		// глобально
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если (НЕ обЗначениеНеЗаполнено(СкладПолучатель) И ?(ТипЗнч(СкладПолучатель)=Тип("СправочникСсылка.СкладыКомпании"),СкладПолучатель.Розничный,Ложь)) И НЕ ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладПолучатель.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладПолучатель.Подразделение);
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровВФилиал И НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
		КонецЕсли;
		Рез=ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма) И Рез;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			// изменим сумму документа
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли;
		ОбработкаРеквизита("Товары.Ячейки",ТекСтрока,ЭтаФорма);
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.КоличествоБазовое" Тогда
		// глобально
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма) И Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
		КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл)); 
		ТекСтрока.ЦенаРозничная=ТекЦенаЗакупки+(ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
		Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
			ТекСтрока.ЦенаРозничная=ТекСтрока.ЦенаРозничная*(1+(ТекСтрока.СтавкаНДС.Ставка/100));
		КонецЕсли; 
		//ОбработкаРеквизита("УстановитьПроцентНаценки", ТекСтрока, ЭтаФорма, ДопПараметры);		
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=?(ТекСтрока.КоличествоБазовое=0,0,ТекСтрока.СуммаРозничная/ТекСтрока.КоличествоБазовое);
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Партия" Тогда
		Если ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
			// попытаемся найти цену на этот товар в документе партии
			Цена=ПолучитьЦенуИзДокументаПоставки(ТекСтрока.Партия,ТекСтрока.Номенклатура,,ТекСтрока.ХарактеристикаНоменклатуры);
			Если Цена>0 Тогда
				ТекСтрока.Цена=Цена;
				ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.Ячейки" Тогда
		// если это набор установим в попытку
		Попытка
			Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
				ТекСтрока.ЯчейкаОтправитель=Справочники.ЯчейкиХранения.ПустаяСсылка();
				ТекСтрока.ЯчейкаПолучатель=Справочники.ЯчейкиХранения.ПустаяСсылка();
				Возврат Истина;
			КонецЕсли;
			ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладКомпании);
			ТекСтрока.ЯчейкаОтправитель=ЯчейкаСсылка;
			ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладПолучатель);
			ТекСтрока.ЯчейкаПолучатель=ЯчейкаСсылка;
			Возврат Истина;
		Исключение
		КонецПопытки;
		
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена=0 Тогда
			ТекСтрока.ПроцентНаценки=0;
		Иначе
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл)); 
			ЦенаРозничная  = ТекСтрока.ЦенаРозничная;
			
			Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
				ЦенаРозничная = ЦенаРозничная * 100 / (100 + ТекСтрока.СтавкаНДС.Ставка);
			КонецЕсли;
			
			ТекСтрока.ПроцентНаценки=((ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И ТипЗнч(СкладКомпании)=Тип("СправочникСсылка.СкладыКомпании") И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
				Если обЗначениеНеЗаполнено(ТекСтрока.ЦенаРозничная) И НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура.ПроцентНаценки) Тогда
					ТекСтрока.ЦенаРозничная = ТекСтрока.Цена + ТекСтрока.Цена*ТекСтрока.Номенклатура.ПроцентНаценки/100;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную цену (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.КоличествоБазовое;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	СтруктураПараметров=Неопределено;
	Если НЕ обЗначениеНеЗаполнено(СкладПолучатель) И ?(ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании"),СкладПолучатель.Розничный,Ложь) Тогда
		СтруктураПараметров=Новый Структура("ИмяРеквизитаЦена,ТипЦен","ЦенаРозничная",СкладПолучатель.ТипЦенРозничнойТорговли);
	КонецЕсли;
	дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПеремещениеТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПеремещениеТоваров");
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	
	//настройка макета отчета
	
	//Удаляем колонку "ЯчейкаХранения", если ни в одной из номенклатур этот реквизит не заполнен
	//Увеличиваем за её счёт колонку "Товар"
	ЕстьЯчейкиХранения=Ложь;
	Для Каждого СтрокаТЧ Из ВыборкаТабличнойЧасти Цикл
		
		Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ЯчейкаОтправитель) ИЛИ НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ЯчейкаПолучатель) Тогда
			ЕстьЯчейкиХранения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьЯчейкиХранения Тогда
		ОбластьТовар = Макет.Область("Товар");
		ОбластьЯчейкаХранения = Макет.Область("ЯчейкаХранения");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЯчейкаХранения.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейкаХранения,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	
	//Удаляем колонку с розничной ценой и увеличиваем ширину колонки "Товар" за счёт ширины удалённой колонки
	ПечататьРозничныеЦены = ?(ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании"), СкладПолучатель.Розничный,Ложь) И (ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровВФилиал);
	
	Если НЕ ПечататьРозничныеЦены Тогда
		ОбластьТовар = Макет.Область("Товар");
		ОбластьЦенаРозничная = Макет.Область("ЦенаРозничная");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЦенаРозничная.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЦенаРозничная,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.ПредставлениеСкладаПолучателя = спПолучитьНаименование(ЭтотОбъект.СкладПолучатель);
	
	// Розничные цены всегда идут в регламентированной валюте
	Если ПечататьРозничныеЦены Тогда
		ОбластьМакета.Параметры.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура();
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки 		= дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		//В возвращаемой структуре нет ключа "Ячейка", создадим его
		СтруктураСтроки.Вставить("Ячейка");
		СтруктураСтроки.Вставить("ЯчейкаПолуч");
		СтруктураСтроки.Цена 	= Формат(СтрокаТабличнойЧасти.ЦенаРозничная,ФорматВыводаСуммы);
		//Если ЯчейкаХранения не определена для данной номенклатуры, то печатаем пробел
		ЯчейкаДляПечати = СокрЛП(СтрокаТабличнойЧасти.ЯчейкаОтправитель.Код);
		СтруктураСтроки.Ячейка 	= ?(обЗначениеНеЗаполнено(ЯчейкаДляПечати)," ",ЯчейкаДляПечати);
		ЯчейкаДляПечати = СокрЛП(СтрокаТабличнойЧасти.ЯчейкаПолучатель.Код);
		СтруктураСтроки.ЯчейкаПолуч = ?(обЗначениеНеЗаполнено(ЯчейкаДляПечати)," ",ЯчейкаДляПечати);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура();
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество();
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПеремещениеТоваров()

// Формирует печатную форму "ТОРГ13"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ13(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ13");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ОтправительПодразделение	= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.ПолучательПодразделение		= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладПолучатель);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Розничный		= ?(ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании"),ЭтотОбъект.СкладПолучатель.Розничный,Ложь) И (ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровВФилиал);
	ВалютаРегл		= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса	= обКурсДляВалюты(ВалютаРегл, ЭтотОбъект.Дата);
	КурсРегл		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр		= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаОтчета	= ВалютаРегл;
	КурсОтчета		= КурсРегл;
	
	Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ЭтотОбъект.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ЭтотОбъект.КурсВалютыУпр; 
	КонецЕсли; 
	
	СтрокНаСтранице = 25;
	СтрокШапки      = 12;
	СтрокПодвала    = 5;
	НомерСтраницы   = 1;

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ВалютаОтчета;
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();
    Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку	= 0;
	Иначе
		ЦелыхСтраницСПодвалом		= Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала		= Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку	= ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// Определим, что показывать в колонке кода - код или артикул
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	// инициализация итогов по странице
	ИтогКоличествоМестПоСтранице = 0;
	ИтогМассаБруттоПоСтранице    = 0;
	ИтогМассыНеттоПоСтранице     = 0;
	ИтогСуммыПоСтранице          = 0;

	// инициализация итогов по документу
	ИтогоКоличество  = 0;
	ИтогоМассаБрутто = 0;
	ИтогоМассаНетто  = 0;
	ИтогоСумма       = 0;
    Ном = 0;

	// Выводим многострочную часть документа
	ОбластьИтоговПоСтранице	= Макет.ПолучитьОбласть("ИтогиПоСтранице");
	ОбластьМакета			= Макет.ПолучитьОбласть("Строка");
	
	СтрокаКолонокСвертки = "Количество, КоличествоБазовое,СуммаНДС";
	Если Розничный Тогда
		ЕстьПартии = Ложь;
		ИмяРегистра = "ОстаткиТоваровКомпании";
		ИспользуемыеРесурсы = Новый Структура("СуммаРозн");
		СтрокаКолонокСвертки = СтрокаКолонокСвертки+",СуммаРозн";
	Иначе
		ЕстьПартии = Истина;
		ИмяРегистра = "ПартииТоваровКомпании";
		ИспользуемыеРесурсы = Новый Структура(",Сумма"+?(ВалютаОтчета=ВалютаУпр,"Упр","")+",СуммаНДС");
		СтрокаКолонокСвертки = СтрокаКолонокСвертки+",Сумма"+?(ВалютаОтчета=ВалютаУпр,"Упр","")+",СуммаНДС";
	КонецЕсли;
	
	ВидДвиженияПриход = ?(ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал, ВидДвиженияНакопления.Расход, ВидДвиженияНакопления.Приход);
	// передаем свой текст запроса, т.к. нужна колонка СуммаНДС
	ТекстЗапроса = "
	 |ВЫБРАТЬ
	 |	ДокТовары.Номенклатура КАК Номенклатура,
	 |	ДокТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	 |	ДокТовары.ЗаказПокупателя КАК ЗаказПокупателя,
	 |	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	 |	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Сумма"+?(Розничный,"Розн",?(ВалютаОтчета=ВалютаУпр,"Упр",""))+",
	 |	СУММА(ДокТовары.Количество) КАК Количество,
	 |	СУММА(ДокТовары.КоличествоБазовое) КАК КоличествоБазовое,
	 |	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	 |	ДокТовары.СтавкаНДС КАК СтавкаНДС,
	 |	ДокТовары.Партия
	 |ИЗ
	 |	Документ.ПеремещениеТоваров.Товары КАК ДокТовары
	 |ГДЕ
	 |	ДокТовары.Ссылка = &Ссылка
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ДокТовары.Номенклатура,
	 |	ДокТовары.ЕдиницаИзмерения,
	 |	ДокТовары.ЗаказПокупателя,
	 |	ДокТовары.ХарактеристикаНоменклатуры,
	 |	ДокТовары.СтавкаНДС,
	 |	ДокТовары.Партия
	 |";
	
	ТаблицаСуммСписания = дкПолучитьТаблицуСуммСписания(Ссылка, ЕстьПартии, ИмяРегистра, ИспользуемыеРесурсы, ВидДвиженияПриход,ТекстЗапроса);
	
	ТаблицаСуммСписания.Свернуть("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры,СтавкаНДС", СтрокаКолонокСвертки);
	
	Для Каждого ТекСтрока Из ТаблицаСуммСписания Цикл
		Ном = Ном + 1;
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = Формат(ИтогКоличествоМестПоСтранице,ФорматВыводаКоличества);
			ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = Формат(ИтогСуммыПоСтранице,ФорматВыводаСуммы);
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			// инициализация итогов по странице
			ИтогКоличествоМестПоСтранице = 0;
			ИтогМассаБруттоПоСтранице    = 0;
			ИтогМассаНеттоПоСтранице     = 0;
			ИтогСуммыПоСтранице          = 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;
		
		Если Розничный Тогда
			Сумма = ТекСтрока.СуммаРозн;
		Иначе
			Если ВалютаОтчета=ВалютаУпр Тогда
				Сумма = ТекСтрока.СуммаУпр-?(ТекСтрока.СуммаНДС<>0, обПересчет(ТекСтрока.СуммаНДС, ВалютаРегл, КурсРегл, ВалютаОтчета, КурсОтчета), 0);
			ИначеЕсли ВалютаОтчета=ВалютаРегл Тогда
				Сумма = ТекСтрока.Сумма-ТекСтрока.СуммаНДС;
			Иначе
				Сумма = обПересчет(ТекСтрока.Сумма, ВалютаРегл, КурсРегл, ВалютаОтчета, КурсОтчета)-?(ТекСтрока.СуммаНДС<>0, обПересчет(ТекСтрока.СуммаНДС, ВалютаРегл, КурсРегл, ВалютаОтчета, КурсОтчета),0);
			КонецЕсли; 
		КонецЕсли;

		ОбластьМакета.Параметры.Заполнить(ТекСтрока);
		Характеристика										= Строка(ТекСтрока.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(ТекСтрока.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					= дкПолучитьЗначениеКолонкиКода(ТекСтрока.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		ОбластьМакета.Параметры.ЕдиницаИзмерения			= ТекСтрока.ЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ	= ТекСтрока.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		
		ОбластьМакета.Параметры.КоличествоВОдномМесте		= Формат(ТекСтрока.ЕдиницаИзмерения.Коэффициент,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.КоличествоМест				= Формат(ТекСтрока.Количество,ФорматВыводаКоличества);
		МассаБрутто 										= ТекСтрока.Количество * Справочники.Номенклатура.ПолучитьВесНоменклатуры(ТекСтрока.Номенклатура, ТекСтрока.ЕдиницаИзмерения);
		ОбластьМакета.Параметры.МассаБрутто					= МассаБрутто;
		ОбластьМакета.Параметры.Цена						= Формат(Сумма/ТекСтрока.Количество,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Сумма						= Формат(Сумма,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Обновим итоги по странице
		ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + ТекСтрока.Количество;
		ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице    + МассаБрутто;
		ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице     + 0;
		ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице          + Сумма;
		
		// Обновим итоги по документу
		ИтогоКоличество  = ИтогоКоличество  + ТекСтрока.Количество;
		ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБрутто;
		ИтогоМассаНетто  = ИтогоМассаНетто  + 0;
		ИтогоСумма       = ИтогоСумма       + Сумма;
	КонецЦикла;

	ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = Формат(ИтогКоличествоМестПоСтранице,ФорматВыводаКоличества);
	ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = Формат(ИтогСуммыПоСтранице,ФорматВыводаСуммы);
    ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим итоги по документу в целом
	ОбластьМакета = Макет.ПолучитьОбласть("Всего");
	ОбластьМакета.Параметры.ИтогоКоличествоМест = Формат(ИтогоКоличество,ФорматВыводаКоличества);
	ОбластьМакета.Параметры.ИтогоМассаБрутто    = ИтогоМассаБрутто;
	ОбластьМакета.Параметры.ИтогоМассаНетто     = ИтогоМассаНетто;
	ОбластьМакета.Параметры.ИтогоСумма          = Формат(ИтогоСумма,ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	// текст без копеек (не Сто 00   , а   просто Сто)
	// текст без копеек (не Сто 00   , а   просто Сто)
	ПараметрыПрописиНаРусскомОбщ = ВалютаОтчета.ПараметрыПрописиНаРусском;
	ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусскомОбщ, "1", "0");
	ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "2", "0");
	ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "3", "0");
	ОбластьМакета.Параметры.ИтогоСуммаПрописью = ЧислоПрописью(Цел(ИтогоСумма), "L=ru_RU; НП=Ложь; НД=Ложь", ПараметрыПрописиНаРусском);
	//ОбластьМакета.Параметры.ИтогоСуммаКоп      = Формат(Цел((ИтогоСумма-Цел(ИтогоСумма))*100), "ЧЦ=2; ЧН=00");
    ОбластьМакета.Параметры.Валюта = ВалютаОтчета;
	ОбщаяСумма = ЧислоПрописью(ИтогоСумма, "L=ru_RU; НП=Ложь; НД=Истина", ПараметрыПрописиНаРусскомОбщ);
	ОбластьМакета.Параметры.ИтогоСуммаКоп      = СокрЛП(СтрЗаменить(ОбщаяСумма,ОбластьМакета.Параметры.ИтогоСуммаПрописью,""));
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
	
КонецФункции // ПечатьТОРГ13()

// Формирует печатную форму Т-1 "Товарно-транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТ1(ТабДокумент) Экспорт
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТТН");
	
	//дальше заполнить Шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	//ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления2=Новый Структура(
	"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с ");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.НомерДокумента                = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДокумента                 = Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.Грузоотправитель			  = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.Грузополучатель				  = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладПолучатель);
	
	Если ТипЗнч(ОбластьМакета.Параметры.Грузоотправитель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		ОбластьМакета.Параметры.ГрузоотправительПредставление = спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,СтруктураПредставления2);
		ОбластьМакета.Параметры.ГрузоотправительПоОКПО		  = ОбластьМакета.Параметры.Грузоотправитель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузоотправительПредставление = ОбластьМакета.Параметры.Грузоотправитель.Наименование;
	КонецЕсли;
	
	Если ТипЗнч(ОбластьМакета.Параметры.Грузополучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		ОбластьМакета.Параметры.ГрузополучательПредставление  = спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
		ОбластьМакета.Параметры.ГрузополучательПоОКПО		  = ОбластьМакета.Параметры.Грузополучатель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузополучательПредставление  = ОбластьМакета.Параметры.Грузополучатель.Наименование;
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим заголовок таблицы
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	ОбластьСтрока	= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги	= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего	= Макет.ПолучитьОбласть("Всего");
	ОбластьПодвал	= Макет.ПолучитьОбласть("Подвал");
	
	// инициализация итогов по странице
	СтруктураИтоговПоСтранице = Новый Структура("Количество, СуммаВсего",0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы  = "Страница: " + НомерСтраницы;
	ОбластьЗаголовокТаблицы.Параметры.ТекстЗаголовка = "Товарно-транспортная накладная товарный раздел";
	
	Розничный = ?(ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании"),ЭтотОбъект.СкладПолучатель.Розничный,Ложь) И (ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровВФилиал);
	
	// инициализация итогов по документу
	Ном             = 0;
	ИтогоМест       = 0;
	ИтогоКоличество = 0;
	ИтогоВес        = 0;
	ИтогоСумма      = 0;
	
	СтрокаКолонокСвертки = "Количество, КоличествоБазовое";
	Если Розничный Тогда
		ЕстьПартии = Ложь;
		ИмяРегистра = "ОстаткиТоваровКомпании";
		ИспользуемыеРесурсы = Новый Структура("СуммаРозн");
		СтрокаКолонокСвертки = СтрокаКолонокСвертки+",СуммаРозн";
	Иначе
		ЕстьПартии = Истина;
		ИмяРегистра = "ПартииТоваровКомпании";
		ИспользуемыеРесурсы = Новый Структура("Сумма");
		СтрокаКолонокСвертки = СтрокаКолонокСвертки+",Сумма";
	КонецЕсли;
	
	ВидДвиженияПриход = ?(ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал, ВидДвиженияНакопления.Расход,ВидДвиженияНакопления.Приход);
	
	ТаблицаСуммСписания = дкПолучитьТаблицуСуммСписания(Ссылка, ЕстьПартии, ИмяРегистра, ИспользуемыеРесурсы, ВидДвиженияПриход);
	ТаблицаСуммСписания.Свернуть("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры", СтрокаКолонокСвертки);
	КоличествоСтрок = ТаблицаСуммСписания.Количество();
	
	//доп. области
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьВсего);
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	Для Каждого ТекСтрока Из ТаблицаСуммСписания Цикл
		//заполняем данные строки
		Ном = Ном + 1;
		ТоварНаименование = спПолучитьНаименование(ТекСтрока.Номенклатура);
		Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		//характеристику выводим в строке с наименованием
		ТоварНаименование = ТоварНаименование + ?(НЕ обЗначениеНеЗаполнено(Характеристика),", " + спПолучитьНаименование(Характеристика),"");
		
		Сумма = ?(Розничный, ТекСтрока.СуммаРозн, ТекСтрока.Сумма);
		Цена=Сумма/ТекСтрока.КоличествоБазовое;
		МассаБрутто = ТекСтрока.Количество * Справочники.Номенклатура.ПолучитьВесНоменклатуры(ТекСтрока.Номенклатура, ТекСтрока.ЕдиницаИзмерения);
		
		ОбластьСтрока.Параметры.КодПродукции				= ТекСтрока.Номенклатура.Код;
		//ОбластьСтрока.Параметры.НомерПрейскуранта
		ОбластьСтрока.Параметры.Артикул 					= ТекСтрока.Номенклатура.Артикул;
		ОбластьСтрока.Параметры.Количество					= Формат(ТекСтрока.КоличествоБазовое, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена						= Формат(Цена, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.ТоварНаименование			= ТоварНаименование;
		ОбластьСтрока.Параметры.Номенклатура				= ТекСтрока.Номенклатура;
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= спПолучитьНаименование(ТекСтрока.ЕдиницаИзмерения);
		ОбластьСтрока.Параметры.ВидУпаковки					= ТекСтрока.ЕдиницаИзмерения.ЕдиницаПоКлассификатору;
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(ТекСтрока.Количество, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Масса						= Формат(МассаБрутто, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.СуммаВсего					= Формат(Сумма, ФорматВыводаСуммы);
		//ОбластьСтрока.Параметры.НомерЗаписиВКартотеке		= 
		
		ИтогоМест		= ИтогоМест + ТекСтрока.Количество;
		ИтогоВес		= ИтогоВес + МассаБрутто;
		ИтогоКоличество	= ИтогоКоличество + ТекСтрока.КоличествоБазовое;
		ИтогоСумма		= ИтогоСумма + Сумма;
		
		//выводим строку, делая проверку попадания на лист		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, ?(Ном=КоличествоСтрок,мсвДопОбластиПодвала,Неопределено));
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("Количество, СуммаВсего",0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//обновим итоги по странице
		СтруктураИтоговПоСтранице.Количество = СтруктураИтоговПоСтранице.Количество + ТекСтрока.КоличествоБазовое;
		СтруктураИтоговПоСтранице.СуммаВсего = СтруктураИтоговПоСтранице.СуммаВсего + Сумма;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.Количество = Формат(ИтогоКоличество, ФорматВыводаКоличества);
	ОбластьВсего.Параметры.СуммаВсего = Формат(ИтогоСумма, ФорматВыводаСуммы);
	
	ТабДокумент.Вывести(ОбластьВсего);
	
	// Выводим подвал документа
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	ОбластьПодвал.Параметры.КоличествоСтрок         = КоличествоСтрок;
	ОбластьПодвал.Параметры.ОтпущеноНаСуммуПрописью = обЧислоПрописью(ИтогоСумма,ВалютаРегл);
	ОбластьПодвал.Параметры.ВсегоКОплате			 = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.МассаГрузаБрутто		= ЧислоПрописью(ИтогоВес,,",,,,,,,,0");
	ОбластьПодвал.Параметры.ВсегоМестПрописью       = ЧислоПрописью(ИтогоМест,,",,,,,,,,0");
	ОбластьПодвал.Параметры.ВсегоНаименований       = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	
	ТабДокумент.Вывести(ОбластьПодвал);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета   = Макет.ПолучитьОбласть("ТранспортныйРаздел");
	
	ЛицензионнаяКарточка 		= обПолучитьЗначениеСвойства(ЭтотОбъект, "ЛицензионнаяКарточка");
	СрокДоставки 				= обПолучитьЗначениеСвойства(ЭтотОбъект, "СрокДоставки");
	Перевозчик 					= обПолучитьЗначениеСвойства(ЭтотОбъект, "ОрганизацияПеревозчик");
	МаркаАвтомобиля     	    = обПолучитьЗначениеСвойства(ЭтотОбъект, "МаркаАвтомобиля");
	ГосНомерАвтомобиля	        = обПолучитьЗначениеСвойства(ЭтотОбъект, "ГосНомерАвтомобиля");
	Заказчик	 				= обПолучитьЗначениеСвойства(ЭтотОбъект, "ОрганизацияЗаказчик");
	Водитель					= обПолучитьЗначениеСвойства(ЭтотОбъект, "ФИОВодителя");
	ВодительскоеУдостоверение	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ВодительскоеУдостоверение");
	ВидПеревозки	            = обПолучитьЗначениеСвойства(ЭтотОбъект, "ВидПеревозки");
	ПунктПогрузки             	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПунктПогрузки");
	ПунктРазгрузки            	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПунктРазгрузки");
	МаркаПрицепа 			  	= обПолучитьЗначениеСвойства(ЭтотОбъект, "Прицеп");
	ГосНомерПрицепа	           	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ГосНомерПрицепа");
	
	Если ТипЗнч(ЛицензионнаяКарточка) = Тип("Булево") Тогда
		ШрифтСтандарт   = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , , НЕ ЛицензионнаяКарточка);
		ШрифтОграничено = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , , ЛицензионнаяКарточка);
		ОбластьМакета.Области.Стандарт.Шрифт   = ШрифтСтандарт;
		ОбластьМакета.Области.Ограничено.Шрифт = ШрифтОграничено;
	КонецЕсли;
	
	ОбластьМакета.Параметры.СрокДоставки              = СрокДоставки;
	ОбластьМакета.Параметры.Номер                     = НомерДляПечати;
	ОбластьМакета.Параметры.ОрганизацияПеревозчик     = Перевозчик;
	ОбластьМакета.Параметры.МаркаАвтомобиля           = МаркаАвтомобиля;
	ОбластьМакета.Параметры.ГосНомерАвтомобиля        = ГосНомерАвтомобиля;
	ОбластьМакета.Параметры.ОрганизацияЗаказчик       = Заказчик;
	ОбластьМакета.Параметры.ФИОВодителя               = Водитель;
	ОбластьМакета.Параметры.ВодительскоеУдостоверение = ВодительскоеУдостоверение;
	ОбластьМакета.Параметры.ВидПеревозки              = ВидПеревозки;
	ОбластьМакета.Параметры.ПунктПогрузки             = ПунктПогрузки;
	ОбластьМакета.Параметры.ПунктРазгрузки            = ПунктРазгрузки;
	ОбластьМакета.Параметры.Прицеп                    = МаркаПрицепа;
	ОбластьМакета.Параметры.ГосНомерПрицепа           = ГосНомерПрицепа;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СведенияОГрузе");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалСведенийОГрузе");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПогрузочныеОперации");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПрочиеСведения");
	ОбластьМакета.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьТ1()

// Формирует печатную форму Приложение №4 "Транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриложение4(ТабДокумент) Экспорт
	ТабДокумент = дкПечатьНовойТТН(ЭтотОбъект,ТабДокумент);
	Возврат ТабДокумент;
КонецФункции // ПечатьПриложение4()

// Формирует печатную форму "М-11"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ11(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("М11");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
	"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.Заголовок = "ТРЕБОВАНИЕ-НАКЛАДНАЯ № "+дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.КодОКПО		= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ДатаСоставления			= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.СкладОтправитель	= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.СкладПолучатель			= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладПолучатель);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//переменные управляющие постраничным выводом
	СтрокНаСтранице = 25;
	СтрокШапки      = 10;
	СтрокПодвала    = 6;
	НомерСтраницы   = 1;		
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
	
	// выводим заголовок таблицы
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	ВыборкаСтрок = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// Определим имя показываемой колонки кода
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	Ном = 0;
	
	// проверим, входит ли НДС в стоимость	
	ЦенаВключаетНДС = ТипЦен.ЦенаВключаетНДС;
	
	// Выводим многострочную часть документа
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаСтрок Цикл
		Ном = Ном + 1;
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		Характеристика											= Строка(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
		ОбластьСтрока.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьСтрока.Параметры.НоменклатурныйНомер				= дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод				= СтрокаТабличнойЧасти.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование	= СтрокаТабличнойЧасти.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.Количество						= Формат(СтрокаТабличнойЧасти.Количество,ФорматВыводаКоличества);
		Если ЦенаВключаетНДС Тогда
			ОбластьСтрока.Параметры.Цена		= Формат(СтрокаТабличнойЧасти.Цена,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаБезНДС	= Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
		Иначе
			ОбластьСтрока.Параметры.Цена 		= Формат(СтрокаТабличнойЧасти.Цена,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаБезНДС	= Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
		КонецЕсли;		
		ТабДокумент.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	// выводим подвал
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб=Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьМ11()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

//Формирует отчет "Распределение по заказам покупателей
Процедура СформироватьОтчетПоРаспределению() Экспорт
	ДокСсылка = ЭтотОбъект.Ссылка;
	Если обЗначениеНеЗаполнено(ДокСсылка) Тогда 
		Сообщить("Для формирования отчета документ должен быть записан!", СтатусСообщения.Информация);
		Возврат;
	КонецЕсли;
	
	Отчет = Отчеты.РаспределениеПоЗаказамПокупателей.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	//ВариантОтчета     = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьВариантОтчета(МетаданныеОтчета.ПолноеИмя(), "Основной");
	//НастройкиВарианта = ВариантОтчета.Настройки.Получить();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.Основной;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",    НачалоДня(ДокСсылка.Дата));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ДокСсылка.Дата));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("Документ",      ДокСсылка);
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
КонецПроцедуры

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Перем ВремХран;
	ОбработкаЗаполненияОтказ = НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание);
	
	Если ЗначениеЗаполнено(ПодразделениеКомпании) Тогда
		// контроль заказов по подразделению
		Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
			ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
		Иначе
			ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
		КонецЕсли; 
		// контроль заказов по организации
		СпособКонтроляКорректностиДоговораИСклада = обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании, "КонтрольКорректностиДоговораИСкладаДокумента", ЭтотОбъект);
		ЗакрытиеЗаказовПоОрганизации = (НЕ СпособКонтроляКорректностиДоговораИСклада = Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
	КонецЕсли;
	
	Если ОбработкаЗаполненияОтказ Тогда Возврат; КонецЕсли;
	
	Если НЕ Основание = Неопределено Тогда
		Если (Основание.ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдер)
		 ИЛИ (Основание.ХозОперация = Справочники.ХозОперации.РасходныйСкладскойОрдер) Тогда
			СкладКомпании = Неопределено;
			СкладПолучатель = Неопределено;
			Если ТипЗнч(Основание.СкладКомпании)=Тип("СправочникСсылка.СкладыКомпании") И Основание.СкладКомпании.Розничный Тогда
				Если Основание.ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдер Тогда
					СкладПолучатель = Основание.СкладКомпании;
				Иначе
					СкладКомпании = Основание.СкладКомпании;
				КонецЕсли;
			Иначе
				ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров;
				Если Основание.ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдер Тогда
					СкладПолучатель = Основание.СкладКомпании;
				Иначе
					СкладКомпании = Основание.СкладКомпании;
				КонецЕсли;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(Дата) Тогда
				Дата = ТекущаяДата();
			КонецЕсли;
			Для Каждого СтрТовар Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура", СтрТовар);
			КонецЦикла;
			
			// заполняем ячейки
			Попытка
				Для Каждого стрТовар Из Товары Цикл
					Если стрТовар.Номенклатура = Основание.Товары[стрТовар.НомерСтроки-1].Номенклатура Тогда
						Если Основание.ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдер Тогда
							стрТовар.ЯчейкаПолучатель  = Основание.Товары[стрТовар.НомерСтроки-1].Ячейка;
						Иначе
							стрТовар.ЯчейкаОтправитель = Основание.Товары[стрТовар.НомерСтроки-1].Ячейка;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
			ТипЗнч(Основание)=Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			
			// Пересчитаем мест в ТЧ Товары
			Если обПраво("ОтображатьБазовоеКоличество",Права,,ЭтотОбъект) Тогда
				Для Каждого ТекущаяСтрока Из Товары Цикл
					ТекущаяСтрока.Количество = Окр(ТекущаяСтрока.КоличествоБазовое / ?(ТекущаяСтрока.Коэффициент = 0, 1, ТекущаяСтрока.Коэффициент), 3);
				КонецЦикла;
			КонецЕсли;
			
			Если (НЕ обЗначениеНеЗаполнено(СкладКомпании))И(СкладКомпании.Розничный)И(ТипЦен <> СкладКомпании.ТипЦенРозничнойТорговли) Тогда
				ТипЦен = СкладКомпании.ТипЦенРозничнойТорговли;
				ВалютаДокумента = обВалютаТипаЦены(Неопределено,ТипЦен);
				СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
				КурсДокумента  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
				Для Каждого СтрТовар Из Товары Цикл
					СтрТовар.Цена = обПолучитьЦену(ТипЦен,СтрТовар.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, СтрТовар.ХарактеристикаНоменклатуры, СтрТовар.ЕдиницаИзмерения, ПодразделениеКомпании);
					ОбработкаРеквизита("Товары.Цена",СтрТовар);
				КонецЦикла;
			КонецЕсли;
			
			// Заполняем партии
			Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
				Для Каждого СтрТовар Из Товары Цикл
					СтрТовар.Партия = Основание;
				КонецЦикла;
			КонецЕсли;
		
			// заполняем ячейки
			Попытка
				Для Каждого стрТовар Из Товары Цикл
					Если стрТовар.Номенклатура = Основание.Товары[стрТовар.НомерСтроки-1].Номенклатура Тогда
						стрТовар.ЯчейкаОтправитель = Основание.Товары[стрТовар.НомерСтроки-1].Ячейка;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			ЗаполнитьРезервамиПодразделения(Основание);
			
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Если обЗначениеНеЗаполнено(СкладКомпании) Тогда
				СкладКомпании=обПраво("ОсновнойСкладКомпании",Права,,ЭтотОбъект);
			КонецЕсли;
			
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
			Если О2_API_ОбщийМодуль.ЯвляетсяЛиЗаказО2(Основание) тогда 
				ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал;
				СписокСкладов = О2_API_ОбщийМодуль.ПолучитьСкладыЗаказа(Основание);	
				Если СписокСкладов.Количество() = 1 Тогда
					СкладКомпании = СписокСкладов[0].Значение;
				ИначеЕсли СписокСкладов.Количество() > 1 Тогда
					ВыбранныйЭлемент = СписокСкладов.ВыбратьЭлемент("Выберите склад по которому нужно выполнить перемещение товаров");
					Если ВыбранныйЭлемент = Неопределено тогда
						Возврат;
					КонецЕсли;
					СкладКомпании = ВыбранныйЭлемент.Значение;
				КонецЕсли;
				СкладПолучатель = Основание.СкладКомпании;
			КонецЕсли;			
			//
			
			ЗаполнитьРезервамиПодЗаказ(Основание);
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
			// заполняем ячейки
			Попытка
				Для Каждого стрТовар Из Товары Цикл
					Если стрТовар.Номенклатура = Основание.Товары[стрТовар.НомерСтроки-1].Номенклатура Тогда
						стрТовар.ЯчейкаОтправитель = Основание.Товары[стрТовар.НомерСтроки-1].Ячейка;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
		ИначеЕсли обЭтотТип(Основание, "ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
			// проверим хоз. операцию
			Если Основание.ХозОперация <> Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт Тогда
				Сообщить(НСтр("ru = 'Ввод возможен только на основании документа с хоз. операцией Таможенная декларация(импорт)'"));
				дкОтменаВводаДокумента(ЭтотОбъект);
				Возврат;
			КонецЕсли;
			Товары.Очистить();
			
			// заполняем табличную часть автомобилей
			Для Каждого Товар Из Основание.Товары Цикл
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Товар);
				НоваяСтрока.Количество = 1;
			КонецЦикла;
		КонецЕсли;
		
		// пересчитаем строки
		Если НЕ (ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
			ТипЗнч(Основание)=Тип("ДокументСсылка.АвансовыйОтчет")) Тогда
			Для Каждого СтрТовар Из Товары Цикл
				Если СтрТовар.Количество=0 Тогда СтрТовар.Количество=1; КонецЕсли;
				ОбработкаРеквизита("Товары.Количество", СтрТовар);
			КонецЦикла;
		КонецЕсли;
		
		// если ввели на основании перемещения, то поменяем склады местами
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПеремещениеТоваров") 
			// БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
			И Не Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
			// БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
			
			СкладКомпании=Основание.СкладПолучатель;
			СкладПолучатель=Основание.СкладКомпании;
			
			Для Каждого стрТовары Из Товары Цикл
				ВремХран = стрТовары.ЯчейкаОтправитель;
				стрТовары.ЯчейкаОтправитель = стрТовары.ЯчейкаПолучатель;
				стрТовары.ЯчейкаПолучатель  = ВремХран;
			КонецЦикла;
		// БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПеремещениеТоваров") И Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
			ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала;
		// БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
		КонецЕсли;
		
		// если вводим на основании пересортицы, то ...
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПересортицаТоваров") Тогда
			Товары.Очистить();
			Для Каждого СтрокаТЧ Из Основание.Товары Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрокаТЧ.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТЧ.ХарактеристикаНоменклатуры;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				НоваяСтрока.КоличествоБазовое = СтрокаТЧ.Количество;
				ОбработкаРеквизита("Товары.КоличествоБазовое",НоваяСтрока);
				НоваяСтрока.ЦенаРозничная = СтрокаТЧ.ЦенаРозничная;
				ОбработкаРеквизита("Товары.ЦенаРозничная",НоваяСтрока);
			КонецЦикла;
			Товары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,ЦенаРозничная,ХарактеристикаНоменклатуры,Цена,СтавкаНДС","Количество,КоличествоБазовое,СуммаРозничная,Сумма,СуммаВсего,СуммаНДС");
		КонецЕсли;
	КонецЕсли;
	
	// Есть ли возможность укзания партии и ГТД
	ВыборочноеСписаниеПартий=обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект);
	СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	Если НЕ ВыборочноеСписаниеПартий Тогда
		Товары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,ХарактеристикаНоменклатуры,СтавкаНДС,ЗаказПокупателя,ЯчейкаОтправитель,ЯчейкаПолучатель,СпособРаспределения","Количество,КоличествоБазовое,СуммаРозничная,Сумма,СуммаВсего,СуммаНДС");
		Для Каждого СтрокаТовар Из Товары Цикл
			СуммаНДС=СтрокаТовар.СуммаНДС;
			СуммаРозничная=СтрокаТовар.СуммаРозничная;
			ОбработкаРеквизита("Товары.СуммаВсего",СтрокаТовар);
			СтрокаТовар.СуммаРозничная=СуммаРозничная;
			ОбработкаРеквизита("Товары.СуммаРозничная",СтрокаТовар);
			СтрокаТовар.СуммаНДС=СуммаНДС;
		КонецЦикла;
	КонецЕсли;
	
	// БИЗТЕХ КОВАЛЬ 24.10.2025 ++ Фикс идентификатора товара
	Для Каждого Товар Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(Товар.ИдентификаторТовара) Тогда
			Товар.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;  
	
	//++СнимченкоАА_бизтех++
	КодыМаркировки.Очистить();
	//--СнимченкоАА_бизтех--

КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Почистим распределения товаров по заказам
	СтрокиУдаления = Новый Массив;
	СтруктураОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	Для Каждого СтрокаРаспределения Из РаспределениеТовара Цикл
		СтруктураОтбора.Номенклатура               = СтрокаРаспределения.Номенклатура;
		СтруктураОтбора.ХарактеристикаНоменклатуры = СтрокаРаспределения.ХарактеристикаНоменклатуры;
		НайденныеСтроки = Товары.НайтиСтроки(СтруктураОтбора);
		УдалитьРаспределение = Истина;
		Для Каждого ТекСтрока Из НайденныеСтроки Цикл
			Если ТекСтрока.СпособРаспределения = 2 И (НЕ ЗначениеЗаполнено(СтрокаРаспределения.ЗаказПокупателя)) Тогда
				Продолжить;
			ИначеЕсли ТекСтрока.СпособРаспределения>0 Тогда
				УдалитьРаспределение = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если УдалитьРаспределение Тогда 
			СтрокиУдаления.Добавить(СтрокаРаспределения);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из СтрокиУдаления Цикл
		РаспределениеТовара.Удалить(ТекСтрока);
	КонецЦикла;
	
	// Установим для новых документов признак, для корректного проведения перемещения по заказу
	Если ЭтоНовый() Тогда
		РежимПереносаЗаказов = Истина;
	КонецЕсли;
	
	Результат = Истина;
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ ФлагПерерасчет Тогда
		// Обнулим не используемые колонки.   
		Если ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		    СкладПолучательРозничный = СкладПолучатель.Розничный;
		Иначе	
		    СкладПолучательРозничный = Ложь;
		КонецЕсли; 
		НеобходимоОбнулить = (обЗначениеНеЗаполнено(СкладПолучатель) ИЛИ НЕ СкладПолучательРозничный) И (ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровВФилиал);
		Если НеобходимоОбнулить Тогда
			Для Каждого ТекСтрокаТЧ Из Товары Цикл  	
				ТекСтрокаТЧ.ЦенаРозничная  = 0;
				ТекСтрокаТЧ.СуммаРозничная = 0;
				ТекСтрокаТЧ.ПроцентНаценки = 0;
			КонецЦикла;	
		КонецЕсли;
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли; 
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Выборка = Запрос.Выполнить();
		Если НЕ Выборка.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Выборка.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПоставщика
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Выборка = Запрос.Выполнить();
		Если НЕ Выборка.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Выборка.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли; 
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --
	
	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--	
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	// проверим, а не равны ли склады
	Если СкладПолучатель = СкладКомпании Тогда 
		Сообщить("Склад-отправитель равен складу-получателю ! Перемещение невозможно !"); Отказ=Истина;
		Возврат;
	КонецЕсли;
	
	ТаблицаСписанияСоСкладаОтправителя=Неопределено;
	ЭтоПодразделениеПолучатель = (ТипЗнч(СкладПолучатель)<>Тип("СправочникСсылка.СкладыКомпании"));
	
	// если перемещение из филиала, то ничего с филиала не снимаем
	Если ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		//Закрытие внутреннего заказа
		РезультатЗапросаПоТоварам = Неопределено;
		//Закрываем внутренние заказы по FIFO
		ТекстЗапроса = "ВЫБРАТЬ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	МАКСИМУМ(ДокументТовары.ЦенаРозничная) КАК ЦенаРозничная,
		|	СУММА(ДокументТовары.КоличествоБазовое) КАК Количество,
		|	СУММА(0) КАК Резерв,
		|	СУММА(ДокументТовары.СуммаРозничная) КАК Сумма
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|	И ДокументТовары.ЗаказПокупателя В (ЗНАЧЕНИЕ(Документ.ЗаказВнутренний.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры";
		Запрос=Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		//Закрываем заказы
		НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
		НаборЗаписейЗаказыПокупателей.Контрагент = ?(ЭтоПодразделениеПолучатель,СкладПолучатель,СкладПолучатель.Подразделение);
		НаборЗаписейЗаказыПокупателей.Заказ = Неопределено;
		НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
		НаборЗаписейЗаказыПокупателей.Заказывать = Истина;
		НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
		НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству=Истина;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.ЗакрытиеЗаказовПокупателя() ИЛИ Отказ;
		
		// Если раскомментировать,то не закроется распределение внутренних заказов, т.к.
		// при записи пропадет резерв.
		//Если НЕ Отказ Тогда
		//	НаборЗаписейЗаказыПокупателей.Записать();
		//КонецЕсли;
		
		//Если было списание резервов то таблица товаров содержит списанные резервы
		РезультатЗапросаПоТоварам=НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам;
		
		//Снимаем распределение заказа покупателя
		НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
		НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
		НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = Неопределено;
		НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
		НаборЗаписейРаспределениеЗаказов.ПоБазовомуКоличеству=Истина;
		НаборЗаписейРаспределениеЗаказов.Контрагент = ?(ЭтоПодразделениеПолучатель,СкладПолучатель,СкладПолучатель.Подразделение);

		Отказ=НЕ НаборЗаписейРаспределениеЗаказов.ЗакрытиеЗаказовПокупателя() ИЛИ Отказ;
		
		// 1. Спишем товар со склада отправителя
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
		НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
		НаборЗаписейОстатки.Резервировать=РезультатЗапросаПоТоварам<>Неопределено;
		Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
		Если Отказ Тогда
			// выведем сообщения об ошибках и предупреждениях
			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
			Возврат; // дальше смысла не имеет
		КонецЕсли;
		ТаблицаСписанияСоСкладаОтправителя = НаборЗаписейОстатки.Выгрузить();
		
		//ТаблицаСписанияСоСкладаОтправителя.Сортировать("Номенклатура, ХарактеристикаНоменклатуры УБЫВ");
		ТаблицаСписанияСоСкладаОтправителя.ЗаполнитьЗначения(0, "Резерв");
		ТаблицаСписанияСоСкладаОтправителя.Колонки.Добавить("ЦенаРозничная", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		ТаблицаСписанияСоСкладаОтправителя.Колонки.Добавить("СпособРаспределения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0)));
		ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		СтруктураОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
		Для Каждого СтрокаСписания Из ТаблицаСписанияСоСкладаОтправителя Цикл
			СтруктураОтбора.Номенклатура               = СтрокаСписания.Номенклатура;
			СтруктураОтбора.ХарактеристикаНоменклатуры = СтрокаСписания.ХарактеристикаНоменклатуры;
			СтрокиТаблицыТовары = Товары.НайтиСтроки(СтруктураОтбора);
			Если СтрокиТаблицыТовары.Количество() = 0 И (НЕ СтрокаСписания.ХарактеристикаНоменклатуры = ПустаяХарактеристика) Тогда
				СтруктураОтбора.ХарактеристикаНоменклатуры = ПустаяХарактеристика;
				СтрокиТаблицыТовары = Товары.НайтиСтроки(СтруктураОтбора);
				СпособРаспределения = 0;
				Для Каждого СтрокаТовары Из СтрокиТаблицыТовары Цикл
					СпособРаспределения = Макс(СпособРаспределения, СтрокаТовары.СпособРаспределения);
				КонецЦикла;
				СтрокаСписания.ЦенаРозничная       = СтрокиТаблицыТовары[0].ЦенаРозничная;
				СтрокаСписания.СпособРаспределения = СпособРаспределения;
			Иначе
				СпособРаспределения = 0;
				Для Каждого СтрокаТовары Из СтрокиТаблицыТовары Цикл
					СпособРаспределения = Макс(СпособРаспределения, СтрокаТовары.СпособРаспределения);
				КонецЦикла;
				СтрокаСписания.ЦенаРозничная       = СтрокиТаблицыТовары[0].ЦенаРозничная;
				СтрокаСписания.СпособРаспределения = СпособРаспределения;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// если перемещение в филиал, то ничего в этот филиал не приходуем
	Если ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
		// Закрытие распределений внутренних заказов
		Если ТаблицаСписанияСоСкладаОтправителя = Неопределено Тогда
			ТекстЗапроса = "ВЫБРАТЬ
			|	ДокументТовары.Номенклатура КАК Номенклатура,
			|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	МАКСИМУМ(ДокументТовары.СпособРаспределения) КАК СпособРаспределения,
			|	МАКСИМУМ(ДокументТовары.ЦенаРозничная) КАК ЦенаРозничная,
			|	СУММА(ДокументТовары.КоличествоБазовое) КАК Количество,
			|	СУММА(0) КАК Резерв,
			|	СУММА(ДокументТовары.СуммаРозничная) КАК Сумма
			|ИЗ
			|	Документ.ПеремещениеТоваров.Товары КАК ДокументТовары
			|ГДЕ
			|	ДокументТовары.Ссылка = &Ссылка
			|	И ДокументТовары.ЗаказПокупателя В (ЗНАЧЕНИЕ(Документ.ЗаказВнутренний.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.ХарактеристикаНоменклатуры";
			Запрос=Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			ТаблицаСписанияСоСкладаОтправителя = Запрос.Выполнить();
		КонецЕсли;
		
		НаборЗаписейРаспределения=Движения.ЗаказыРаспределение;
		НаборЗаписейРаспределения.РежимПроведения=Режим;
		НаборЗаписейРаспределения.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРаспределения.РезультатЗапросаПоТоварам=ТаблицаСписанияСоСкладаОтправителя;
		НаборЗаписейРаспределения.ЗаказПокупателя=Неопределено;
		НаборЗаписейРаспределения.ЗаказПоставщика=Неопределено;
		Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда
			НаборЗаписейРаспределения.СкладКомпании = СкладКомпании;
		КонецЕсли;
		НаборЗаписейРаспределения.ПоБазовомуКоличеству=Истина;
		НаборЗаписейРаспределения.ДвиженияПоРознице=Истина;
		НаборЗаписейРаспределения.Контрагент=?(ЭтоПодразделениеПолучатель,СкладПолучатель,СкладПолучатель.Подразделение);
		Отказ=НЕ НаборЗаписейРаспределения.ЗакрытиеРаспределенийЗаказовПоставщику() ИЛИ Отказ;
		
		// 2. оприходуем на склад получатель
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=НаборЗаписейРаспределения.РезультатЗапросаПоТоварам;
		НаборЗаписейОстатки.СкладКомпании=СкладПолучатель;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.Резервировать=Истина;
		НаборЗаписейОстатки.ДвиженияПоРознице=?(ЭтоПодразделениеПолучатель,Ложь,СкладПолучатель.Розничный);
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
		Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
		
		// Резервирование товаров по заказам покупателей
		НаборЗаписейЗаказыПокупателей=Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=НаборЗаписейРаспределения.ТаблицаЗакрытияРаспределений;
		НаборЗаписейЗаказыПокупателей.Контрагент=Неопределено;
		НаборЗаписейЗаказыПокупателей.Заказ=Неопределено;
		НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладПолучатель;
		НаборЗаписейЗаказыПокупателей.Заказывать=Ложь;
		НаборЗаписейЗаказыПокупателей.Резервировать=Истина;
		НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству=Истина;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
		
		Если НЕ Отказ Тогда
			НаборЗаписейЗаказыПокупателей.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
	// Блок перемещения по заказам, которые указаны в ТЧ
	
	// если перемещение из филиала, то ничего с филиала не снимаем
	ТаблицаСписанияСоСкладаОтправителя=Неопределено;
	ДвиженияЗаказыПокупателейССуммами=Неопределено;
	
	// Установим признак учета заказов покупателей
	ПередаватьЗаказыСУчетомЗаказанного =
		(ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров И РежимПереносаЗаказов)//;
		//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API++
		ИЛИ (
		(ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал И О2_API_ОбщийМодуль.ЯвляетсяЛиЗаказО2(ДокументОснование))
		ИЛИ
		(ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала И О2_API_ОбщийМодуль.ЯвляетсяЛиЗаказО2(ДокументОснование.ДокументОснование))
		);
		// --
	
	Если ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		РезультатЗапросаПоТоварам=Неопределено;
		//Перемещение заказов, которые указаны в товарных строках
		ТекстЗапроса="ВЫБРАТЬ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя КАК Заказ,
		|	ВЫБОР
		|		КОГДА ДокументТовары.ЗаказПокупателя ССЫЛКА Документ.ЗаказВнутренний
		|			ТОГДА ДокументТовары.ЗаказПокупателя.ПодразделениеКомпании
		|		ИНАЧЕ ДокументТовары.ЗаказПокупателя.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	МАКСИМУМ(ДокументТовары.ЦенаРозничная) КАК ЦенаРозничная,
		|	СУММА(ДокументТовары.КоличествоБазовое) КАК Количество,
		|	0 КАК Резерв,
		|	СУММА(ДокументТовары.СуммаРозничная) КАК Сумма
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|	И (НЕ ДокументТовары.ЗаказПокупателя В (ЗНАЧЕНИЕ(Документ.ЗаказВнутренний.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка), НЕОПРЕДЕЛЕНО))
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя,
		|	ВЫБОР
		|		КОГДА ДокументТовары.ЗаказПокупателя ССЫЛКА Документ.ЗаказВнутренний
		|			ТОГДА ДокументТовары.ЗаказПокупателя.ПодразделениеКомпании
		|		ИНАЧЕ ДокументТовары.ЗаказПокупателя.Контрагент
		|	КОНЕЦ";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		//Снимаем Резерв с заказа
		НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
		НаборЗаписейЗаказыПокупателей.Контрагент = Неопределено;
		НаборЗаписейЗаказыПокупателей.Заказ = Неопределено;
		НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
		НаборЗаписейЗаказыПокупателей.Заказывать = Ложь;
		НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
		НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству=Истина;
		НаборЗаписейЗаказыПокупателей.ПеремещениеТоваров = ПередаватьЗаказыСУчетомЗаказанного;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.Расход() ИЛИ Отказ;
		//Если было списание резервов то таблица товаров содержит списанные резервы
		РезультатЗапросаПоТоварам = НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам;
		ДвиженияЗаказыПокупателейССуммами = НаборЗаписейЗаказыПокупателей.Выгрузить();
		
		// 1. Спишем товар со склада отправителя
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
		НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
		НаборЗаписейОстатки.Резервировать=РезультатЗапросаПоТоварам<>Неопределено;
		Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
		
		Если Отказ Тогда
			// выведем сообщения об ошибках и предупреждениях
			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
			Возврат; // дальше смысла не имеет
		КонецЕсли;
		ТаблицаСписанияСоСкладаОтправителя=НаборЗаписейОстатки.Выгрузить();
		ТаблицаСписанияСоСкладаОтправителя.Колонки.Добавить("ЦенаРозничная",Новый ОписаниеТипов("Число"));
		Для Каждого СтрокаСписания Из ТаблицаСписанияСоСкладаОтправителя Цикл
			СтрокаТовары = Товары.Найти(СтрокаСписания.Номенклатура,"Номенклатура");
			Если СтрокаТовары=Неопределено Тогда Продолжить; КонецЕсли;
			СтрокаСписания.ЦенаРозничная=СтрокаТовары.ЦенаРозничная;
		КонецЦикла;
	КонецЕсли;
	
	// если перемещение в филиал, то ничего в этот филиал не приходуем
	Если ХозОперация<>Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
		ТекстЗапроса="ВЫБРАТЬ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя КАК Заказ,
		|	ВЫБОР
		|		КОГДА ДокументТовары.ЗаказПокупателя ССЫЛКА Документ.ЗаказВнутренний
		|			ТОГДА ДокументТовары.ЗаказПокупателя.ПодразделениеКомпании
		|		ИНАЧЕ ДокументТовары.ЗаказПокупателя.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	МАКСИМУМ(ДокументТовары.ЦенаРозничная) КАК ЦенаРозничная,
		|	СУММА(ДокументТовары.КоличествоБазовое) КАК Количество,
		|	СУММА(ДокументТовары.КоличествоБазовое) КАК Резерв,
		|	СУММА(-1) КАК КоличествоРасход,
		|	СУММА(ДокументТовары.СуммаРозничная) КАК Сумма
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|	И (НЕ ДокументТовары.ЗаказПокупателя В (ЗНАЧЕНИЕ(Документ.ЗаказВнутренний.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка), НЕОПРЕДЕЛЕНО))
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя,
		|	ВЫБОР
		|		КОГДА ДокументТовары.ЗаказПокупателя ССЫЛКА Документ.ЗаказВнутренний
		|			ТОГДА ДокументТовары.ЗаказПокупателя.ПодразделениеКомпании
		|		ИНАЧЕ ДокументТовары.ЗаказПокупателя.Контрагент
		|	КОНЕЦ";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		ТаблицаСписанияСоСкладаОтправителя=Запрос.Выполнить();
		
		Если ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваров") Тогда
			ТаблицаСписанияСоСкладаОтправителя = ТаблицаСписанияСоСкладаОтправителя.Выгрузить();
			ДополнитьТаблицуСписанияСуммами(ТаблицаСписанияСоСкладаОтправителя, ДвиженияЗаказыПокупателейССуммами);
		КонецЕсли;
		// Резервирование товаров по заказам покупателей
		НаборЗаписейЗаказыПокупателей=Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=ТаблицаСписанияСоСкладаОтправителя;
		НаборЗаписейЗаказыПокупателей.Контрагент=Неопределено;
		НаборЗаписейЗаказыПокупателей.Заказ=Неопределено;
		НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладПолучатель;
		НаборЗаписейЗаказыПокупателей.Заказывать    = Ложь;
		НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
		НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству = Истина;
		Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда
			НаборЗаписейЗаказыПокупателей.ВидОперации = Перечисления.ВидыОперацийЗаказов.ПеремещениеРезерва;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
		//Отказ=НЕ НаборЗаписейЗаказыПокупателей.КорректировкаЗаказаПокупателя() ИЛИ Отказ;
		
		// Оприходуем на склад получатель
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=ТаблицаСписанияСоСкладаОтправителя;
		НаборЗаписейОстатки.СкладКомпании=СкладПолучатель;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.Резервировать=Истина;
		НаборЗаписейОстатки.ДвиженияПоРознице=?(ЭтоПодразделениеПолучатель,Ложь,СкладПолучатель.Розничный);
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
		Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// если перемещение товаров в розницу, то установим розничные цены
	Если НЕ Отказ И ?(ЭтоПодразделениеПолучатель,Ложь,СкладПолучатель.Розничный) Тогда
		Если ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладПолучатель.ТипЦенРозничнойТорговли.Рассчитывается Тогда
			НаборЗаписейЦены = Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейЦены.Контрагент     = Неопределено;  			
			НаборЗаписейЦены.ТипЦен         = СкладПолучатель.ТипЦенРозничнойТорговли;
			НаборЗаписейЦены.ПодразделениеКомпании     = СкладПолучатель.Подразделение;			
			НаборЗаписейЦены.РезультатЗапросаПоТоварам = Неопределено;        			
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
		
	Если НЕ Отказ Тогда
		// если перемещаем товары из филиала, то установим цены на все товары
		Если ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И ХозОперация=Справочники.ХозОперации.ПеремещениеТоваровИзФилиала И НЕ ТипЦен.Рассчитывается Тогда
			НаборЗаписейЦены = Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейЦены.Контрагент     = Неопределено;
			НаборЗаписейЦены.ТипЦен         = ТипЦен;
			НаборЗаписейЦены.ПодразделениеКомпании     = ПодразделениеКомпании;
			НаборЗаписейЦены.РезультатЗапросаПоТоварам = Неопределено;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыПокупателей.Количество()>0 ИЛИ Движения.ЗаказыРаспределение.Количество()>0));
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для Каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				//Расход    
				Если ХозОперация <> Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
					
					Движения.КодыМаркировки.Записывать = Истина;
					Движение = Движения.КодыМаркировки.Добавить(); 
					Движение.Период = Дата;
					Движение.Организация = Организация;  
					Движение.Склад = СкладКомпании;
					Движение.Номенклатура = тс.Номенклатура; 
					Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
					
					СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
					
					Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
					Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
					
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Количество = тс.Количество;  
					
					Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
					
				КонецЕсли;
				
				//Приход    
				Если ХозОперация <> Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
					
					Движения.КодыМаркировки.Записывать = Истина;
					Движение = Движения.КодыМаркировки.Добавить(); 
					Движение.Период = Дата;
					Движение.Организация = Организация;  
					Движение.Склад = СкладПолучатель;
					Движение.Номенклатура = тс.Номенклатура; 
					Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
					
					СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
					
					Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
					Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
					
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.Количество = тс.Количество; 
					
				КонецЕсли;  
			
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ФлагПерерасчет = Истина;
	КонецЕсли;
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
	Если Не Отказ тогда
		Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал И
			ЗначениеЗаполнено(ДокументОснование) И
			ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") И
			О2_API_ОбщийМодуль.ЯвляетсяЛиЗаказО2(ДокументОснование)
			Тогда
			
			//О2_API_ОбщийМодуль.УстановитьГалочкуОтправленНаТоваре(ДокументОснование,Товары.Выгрузить());
			//О2_API_ОбщийМодуль.ОбновитьСтатусЗаказа(ДокументОснование);
			
		ИначеЕсли ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала И
			ЗначениеЗаполнено(ДокументОснование.ДокументОснование) И
			ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") И
			О2_API_ОбщийМодуль.ЯвляетсяЛиЗаказО2(ДокументОснование.ДокументОснование)
			Тогда
						
			//О2_API_ОбщийМодуль.ОбновитьСтатусЗаказа(ДокументОснование.ДокументОснование);
			
		КонецЕсли;
	КонецЕсли;
    //БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

  //++СнимченкоАА_бизтех++
Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
  //--СнимченкоАА_бизтех--

Процедура ДополнитьТаблицуСписанияСуммами(Приемник, Источник)
	
	Если Источник = Неопределено Тогда
		Возврат ;
	КонецЕсли;
	
	Приемник.Колонки.Добавить("СуммаЗаказа", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Приемник.Колонки.Добавить("СуммаУпр", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Для Каждого Строка Из Приемник Цикл
		
		Отбор = Новый Структура("Контрагент,Заказ,Номенклатура,ХарактеристикаНоменклатуры");
		ЗаполнитьЗначенияСвойств(Отбор, Строка);
		Строки = Источник.НайтиСтроки(Отбор);
		
		Если Строки.Количество() > 0 Тогда
			// Определим способ заполнения заказаного по реквизиту документа.
			Если РежимПереносаЗаказов Тогда
				Строка.КоличествоРасход = Строки[0].Заказано;
			КонецЕсли;
			Строка.СуммаЗаказа = Строки[0].Сумма;
			Строка.СуммаУпр    = Строки[0].СуммаУпр;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Если ТипЗнч(СкладКомпании) = Тип("СправочникСсылка.СкладыКомпании") Тогда
	ЭтоПодразделениеОтправитель = Ложь;
Иначе
	ЭтоПодразделениеОтправитель = Истина;
КонецЕсли;


// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
ФлагПерерасчет = Ложь;
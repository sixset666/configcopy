// Модуль документа

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ДвиженияЗаказа Экспорт; 		// Результат запроса по движениям заказов
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
									 
										  
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Получить состояние заказа клиента
// 
// Возвращаемое значение:
//   ТаблицаЗначений – таблица значений с результатами движений по заказу
//
Функция ПолучитьДвиженияЗаказа() Экспорт
	//Сначала получим список номенклатуры по документу
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ
	|	ТаблицаЗаказа
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка = &Заказ
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) КАК Заказано,
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) КАК Зарезервировано,
	|	ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)КАК Распределено,
	|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК СкладОстаток,
	|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0) КАК СкладЗарезервировано
	|ИЗ
	|	ТаблицаЗаказа КАК ДокументТовары
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(, Заказ=&Заказ) КАК ЗаказыПокупателейОстатки
	|ПО
	|	ДокументТовары.Номенклатура               = ЗаказыПокупателейОстатки.Номенклатура И 
	|	ДокументТовары.ХарактеристикаНоменклатуры = ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыРаспределение.Остатки(,ЗаказПокупателя=&Заказ) КАК ЗаказыРаспределениеОстатки
	|ПО
	|	ДокументТовары.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
	|	ДокументТовары.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,
	|				(Номенклатура, ХарактеристикаНоменклатуры) В 
	|					(ВЫБРАТЬ
	|						ТаблицаЗаказа.Номенклатура КАК Номенклатура,
	|						ТаблицаЗаказа.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|					ИЗ
	|						ТаблицаЗаказа КАК ТаблицаЗаказа)) КАК ОстаткиТоваровКомпанииОстатки
	|ПО
	|	ДокументТовары.Номенклатура               = ОстаткиТоваровКомпанииОстатки.Номенклатура И 
	|	ДокументТовары.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|");
	Запрос.УстановитьПараметр("Заказ",Ссылка);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	//Выгрузка.Свернуть("Номенклатура,ХарактеристикаНоменклатуры","Заказано,Зарезервировано,Распределено,СкладОстаток,СкладЗарезервировано");
	
	Возврат Выгрузка;
	
КонецФункции // ПолучитьДвиженияЗаказа()

// Получение остатков распределения заказа по заказам поставщикам
//
// Возвращаемое значение:
//  Возвращает выборку запроса.
//
Функция ПолучитьРаспределенияЗаказа()
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ЗаказыРаспределение.Номенклатура КАК Номенклатура,
	             |	ЗаказыРаспределение.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	СУММА(ЗаказыРаспределение.Количество) КАК Количество
	             |ИЗ
	             |	РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
	             |ГДЕ
	             |	ЗаказыРаспределение.ВидДвижения = &ВидДвиженияПриход
	             |	И ЗаказыРаспределение.ЗаказПокупателя = &ЗаказПокупателя
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ЗаказыРаспределение.Номенклатура,
	             |	ЗаказыРаспределение.ХарактеристикаНоменклатуры";
	Запрос.УстановитьПараметр("ВидДвиженияПриход",ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ЗаказПокупателя",Ссылка);
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции // ПолучитьРаспределенияЗаказа()

#Область КОВАЛЬ_Запрет_редактирования_при_зависимостях_к_другим_документам
//06.11.2024
Функция ПроверитьЗависимостиЗаказа(Заказ) Экспорт
	
	ЗаказыПоставщику = ПолучитьЗаказыПоставщикуРаспределенные(Заказ);
	
	ДокументыИспользующиеЗаказ = ПолучитьДокументыИспользующиеЗаказ(Заказ);
	
	Если ЗаказыПоставщику.Количество() ИЛИ ДокументыИспользующиеЗаказ.Количество() Тогда
		
		Сообщить(Строка(Заказ) + " распределен по заказу поставщика или зависим от других документов. Редактирование запрещено!");
		Сообщить("Список зависимых документов: ");
		Для Каждого ДокСсылка из ЗаказыПоставщику Цикл
			Сообщить(ДокСсылка.Ссылка);	
		КонецЦикла;
		Для Каждого ДокСсылка из ДокументыИспользующиеЗаказ Цикл
			Сообщить(ДокСсылка.Ссылка);	
		КонецЦикла;
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьЗаказыПоставщикуРаспределенные(Заказ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыРаспределение.ЗаказПоставщика КАК Ссылка
		|ИЗ
		|	РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|ГДЕ
		|	ЗаказыРаспределение.ЗаказПокупателя = &Заказ
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыРаспределение.ЗаказПоставщика";
	
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьДокументыИспользующиеЗаказ(Заказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыПокупателей.Регистратор КАК Ссылка
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|ГДЕ
		|	ЗаказыПокупателей.Заказ = &Заказ
		|	И ЗаказыПокупателей.Регистратор <> &Заказ
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПокупателей.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказыПокупателей.Регистратор УБЫВ";
	
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СрокПоставки",1);
	ОбязательныеРеквизиты.Вставить("ВидОплаты",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки); 			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗаказПокупателя","Заказ покупателя",Ложь);
	СписокХозОпераций.Добавить("ЗаказРезервированиеПокупателя","Заказ и резервирование покупателя",Истина);
	СписокХозОпераций.Добавить("РезервированиеПокупателя","Резервирование покупателя",Ложь);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ХозОперация" Тогда
		Если ХозОперация=Справочники.ХозОперации.ЗаказПокупателя Тогда
			Для каждого СтрокаТоваров Из Товары Цикл
				Если СтрокаТоваров.Резерв<>0 Тогда
					СтрокаТоваров.Резерв = 0;
				КонецЕсли; 
			КонецЦикла; 
		ИначеЕсли ХозОперация=Справочники.ХозОперации.РезервированиеПокупателя Тогда
			Для каждого СтрокаТоваров Из Товары Цикл
				Если СтрокаТоваров.Количество<>СтрокаТоваров.Резерв Тогда
					СтрокаТоваров.Количество = СтрокаТоваров.Резерв;
					ОбработкаРеквизита("Товары.Количество", СтрокаТоваров, ЭтаФорма, ДопПараметры);
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ОтображениеВалютыПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Контрагент" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			//Чтение значения для срока поставки
			СтруктураОтбора=Новый Структура("Организация,Объект",Контрагент,Перечисления.ВидыОбъектовСведений.СрокПоставки);
			СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(?(обЗначениеНеЗаполнено(Дата),ТекущаяДата(),Дата),СтруктураОтбора);
			СрокПоставкиДней=?(обЗначениеНеЗаполнено(СтруктураСведений.Значение),обПраво("СрокПоставкиПокупателюПоУмолчанию",Права,,ЭтотОбъект),СтруктураСведений.Значение);
			СрокПоставки=НачалоДня(Дата)+СрокПоставкиДней*60*60*24;
			ОбработкаРеквизита("ОтображениеСрокаПоставки",ТекСтрока,ЭтаФорма,ДопПараметры);
			ОбработкаРеквизита("ОтображениеСрокаСнятияРезерва",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// нельзя выбирать розничный склад
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
			Предупреждение("На розничном складе товар не резервируется !",10); 
			#КонецЕсли
			СкладКомпании=Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ДоговорВзаиморасчетов<>Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка() Тогда
			ПроцентПредоплатыНовый=?(ДоговорВзаиморасчетов.ПроцентПредоплаты=0,обПраво("МинимальныйПроцентПредоплаты",Права,,ЭтотОбъект),ДоговорВзаиморасчетов.ПроцентПредоплаты);
			Если ПроцентПредоплаты<>ПроцентПредоплатыНовый Тогда
				ПроцентПредоплаты=ПроцентПредоплатыНовый;
				Рез=ОбработкаРеквизита("ПроцентПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобиль" Тогда
		Рез=Истина;
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
			//Получить последнего хозяина
			НовыйКонтрагент = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин, Дата);
			Если (НЕ обЗначениеНеЗаполнено(НовыйКонтрагент)) И (НовыйКонтрагент<>Контрагент) Тогда
				Контрагент=НовыйКонтрагент;
				Рез=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="ПроцентПредоплаты" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаСоСкидкой	= Товары.Итог("СуммаВсего");
		СуммаПредоплаты	= (СуммаСоСкидкой*ПроцентПредоплаты)/100;
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаПредоплаты" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаСоСкидкой=ЭтотОбъект.Товары.Итог("СуммаВсего");
		Если СуммаСоСкидкой=0 Тогда ПроцентПредоплаты=0;
		Иначе
			ВремПроцентПредоплаты = (СуммаПредоплаты*100)/СуммаСоСкидкой;
			ВремПроцентПредоплаты = Цел(ВремПроцентПредоплаты*100)/100;
			ПроцентПредоплаты = ВремПроцентПредоплаты;
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СрокПоставки" Тогда
		Возврат ОбработкаРеквизита("ОтображениеСрокаПоставки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СрокСнятияРезерва" Тогда
		Возврат ОбработкаРеквизита("ОтображениеСрокаСнятияРезерва",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ОтображениеВалютыПредоплаты" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.тВалютаПредоплаты.Заголовок=СокрЛП(ВалютаДокумента.Наименование)+":";
		КонецЕсли; 
		#КонецЕсли
		Возврат Истина;
		
	ИначеЕсли Имя="ОтображениеСрокаПоставки" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(СрокПоставки) Тогда
				ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="срок не определен";
				ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(0,0,128);
			Иначе
				
				Если Ссылка.Пустая() ИЛИ НЕ Ссылка.Проведен Тогда
					ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
				Иначе
					Запрос = Новый Запрос("ВЫБРАТЬ
					|	МАКСИМУМ(ЗаказыПокупателей.Период) КАК Период,
					|	СУММА(ВЫБОР
					|		КОГДА ЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			ЗаказыПокупателей.Заказано
					|		ИНАЧЕ
					|			-ЗаказыПокупателей.Заказано
					|	КОНЕЦ) КАК Заказано
					|ИЗ
					|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
					|ГДЕ
					|	ЗаказыПокупателей.Заказ = &Заказ");
					Запрос.УстановитьПараметр("Заказ", Ссылка);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Если Выборка.Период = NULL Тогда
							ПросроченоДней = 0;
						ИначеЕсли Выборка.Заказано > 0 Тогда
							ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
						Иначе
							ПросроченоДней = 0;
						КонецЕсли;
					Иначе
						ПросроченоДней = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ПросроченоДней>0 Тогда
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="срок поставки истек " + СокрЛП(ПросроченоДней)+ " " + обПолучитьПредставлениеДня(ПросроченоДней) + " назад";
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(128,0,0);
				Иначе
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="";
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(0,128,0);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Возврат Истина;
		
	ИначеЕсли Имя="ОтображениеСрокаСнятияРезерва" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(СрокСнятияРезерва) Тогда
				ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.Заголовок="срок не определен";
				ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.ЦветТекста=Новый Цвет(0,0,128);
			Иначе
				
				Если Ссылка.Пустая() ИЛИ НЕ Ссылка.Проведен Тогда
					ПросроченоДней = Цел((ТекущаяДата()-СрокСнятияРезерва)/86400);
				Иначе
					Запрос = Новый Запрос("ВЫБРАТЬ
					|	МАКСИМУМ(ЗаказыПокупателей.Период) КАК Период,
					|	СУММА(ВЫБОР
					|		КОГДА ЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			ЗаказыПокупателей.Заказано
					|		ИНАЧЕ
					|			-ЗаказыПокупателей.Заказано
					|	КОНЕЦ) КАК Заказано
					|ИЗ
					|	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
					|ГДЕ
					|	ЗаказыПокупателей.Заказ = &Заказ");
					Запрос.УстановитьПараметр("Заказ", Ссылка);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Если Выборка.Период = NULL Тогда
							ПросроченоДней = 0;
						ИначеЕсли Выборка.Заказано > 0 Тогда
							ПросроченоДней = Цел((ТекущаяДата()-СрокСнятияРезерва)/86400);
						Иначе
							ПросроченоДней = 0;
						КонецЕсли;
					Иначе
						ПросроченоДней = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ПросроченоДней>0 Тогда
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.Заголовок="срок снятия резерва истек " + СокрЛП(ПросроченоДней)+ " " + обПолучитьПредставлениеДня(ПросроченоДней) + " назад";
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.ЦветТекста=Новый Цвет(128,0,0);
				Иначе
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.Заголовок="";
					ЭтаФорма.ЭлементыФормы.тЗадержкаСнятияРезерва.ЦветТекста=Новый Цвет(0,128,0);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Возврат Истина;		
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		ТекСтрока.Производитель=ТекСтрока.Номенклатура.Производитель;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОбновитьПодборЗамен();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ХозОперация=Справочники.ХозОперации.РезервированиеПокупателя Тогда
			ТекСтрока.Количество=ТекСтрока.Резерв;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ЗаказПокупателя Тогда
			ТекСтрока.Резерв=0;
		КонецЕсли; 
		НовоеКоличество=ТекСтрока.Количество;
		Если НовоеКоличество<ТекСтрока.Резерв Тогда
			НовоеКоличество=ТекСтрока.Резерв;
		КонецЕсли; 
		ТекСтрока.Количество=НовоеКоличество;
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПроцентПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Резерв" Тогда
		Рез = Истина;
		Если ХозОперация = Справочники.ХозОперации.ЗаказПокупателя Тогда
			ТекСтрока.Резерв = 0;
		ИначеЕсли ТекСтрока.Резерв>ТекСтрока.Количество ИЛИ ХозОперация = Справочники.ХозОперации.РезервированиеПокупателя Тогда
			ТекСтрока.Количество = ТекСтрока.Резерв;
			Рез = ОбработкаРеквизита("Товары.Количество", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли (Имя="Товары.Цена") ИЛИ (Имя="Товары.Сумма") ИЛИ (Имя="Товары.СуммаВсего") ИЛИ (Имя="Товары.СтавкаНДС") ИЛИ (Имя="Товары.СуммаНДС") ИЛИ (Имя="Товары.СуммаСкидки") Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПроцентПредоплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// заполнение резерва остатками со склада
Процедура ЗаполнитьРезервОстатками() Экспорт
	Если ХозОперация<>Справочники.ХозОперации.ЗаказРезервированиеПокупателя Тогда Возврат; КонецЕсли;
	Если обЗначениеНеЗаполнено(СкладКомпании) Тогда Возврат; КонецЕсли;
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	             |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	             |	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0)) - СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) КАК Остаток
	             |ИЗ
	             |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	             |		&НаМомент,
	             |		Номенклатура В (&Номенклатура)
	             |		    И СкладКомпании = &СкладКомпании
	             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	             |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры";
	Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
	Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	ТаблицаОстатков=Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТоваров Из Товары Цикл
		КоличествоЗаказа=СтрокаТоваров.Количество;
		СтрокаТоваров.Резерв=0;
		МассивОстатков=ТаблицаОстатков.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрокаТоваров.Номенклатура,СтрокаТоваров.ХарактеристикаНоменклатуры));
		Для каждого СтрокаОстатков Из МассивОстатков Цикл
			РезервЗаказа=СтрокаТоваров.Резерв;
			Остаток=СтрокаОстатков.Остаток/СтрокаТоваров.Коэффициент;
			Резерв=Мин(КоличествоЗаказа-РезервЗаказа,Остаток);
			СтрокаТоваров.Резерв=СтрокаТоваров.Резерв+Резерв;
			СтрокаОстатков.Остаток=СтрокаОстатков.Остаток-(Резерв*СтрокаТоваров.Коэффициент);
			Если СтрокаТоваров.Количество=СтрокаТоваров.Резерв Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЦикла; 
КонецПроцедуры 

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Заказ"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗаказПокупателя(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ЗаказПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод образца заполнения платёжного поручения
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета"); 
	ОбластьМакета.Параметры.ПредставлениеПоставщика			= спПолучитьНаименование(ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.СчетПоставщикаПредставление		= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.НомерСчета;
	ОбластьМакета.Параметры.БИКБанкаПоставщика				= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.Код;
	ОбластьМакета.Параметры.БанкПоставщикаПредставление		= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк;
	ОбластьМакета.Параметры.СчетБанкаПоставщикаПредставление= ЭтотОбъект.Организация.ОсновнойБанковскийСчет.Банк.КоррСчет;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Автомобиль");
		ОбластьМакета.Параметры.ПредставлениеАвтомобиля = СокрЛП(Автомобиль);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") > 0 ИЛИ ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки") > 0 Тогда
		СуммаСкидки = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
		СуммаСкидкиСтроки = ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки");
			
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СуммаСкидки + СуммаСкидкиСтроки,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительОрганизация");
	Исполнитель.ИсполнительОрганизацияПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительОрганизация),"","/ "+ Исполнитель.ИсполнительОрганизацияПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик    = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикКонтрагент");
	Заказчик.ЗаказчикКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикКонтрагент),"","/ "+ Заказчик.ЗаказчикКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЗаказПокупателя()

// Формирует печатную форму "УсловияПоставки"
// Возвращает сформированный табличный документ:
Функция ПечатьУсловияПоставки(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("УсловияПоставки");
	
	Макет.Параметры.СрокВыполненияЗаказа = (НачалоДня(СрокПоставки) - НачалоДня(Дата)) / 86400;
	
	ТабДокумент.Вывести(Макет);
	
	Возврат ТабДокумент;
КонецФункции

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Перем ВремЯчейка;
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если Основание = Неопределено ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ТипЦенНовый=обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);		
		Если ТипЦен<>ТипЦенНовый Тогда
			ТипЦен=ТипЦенНовый;
			Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
				ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Контрагент = Неопределено;
		ДоговорВзаиморасчетов = Неопределено;
		Для каждого СтрокаТоваров Из Товары Цикл
			ВремЯчейка = СтрокаТоваров.Ячейка;
			ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров);
			СтрокаТоваров.Ячейка = ВремЯчейка;
		КонецЦикла; 
		//СуммаДокумента=Товары.Итог("СуммаВсего");
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		//Чтение значения для срока поставки
		СтруктураОтбора=Новый Структура("Организация,Объект",Контрагент,Перечисления.ВидыОбъектовСведений.СрокПоставки);
		СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(?(обЗначениеНеЗаполнено(Дата),ТекущаяДата(),Дата),СтруктураОтбора);
		СрокПоставкиДней=?(обЗначениеНеЗаполнено(СтруктураСведений.Значение),обПраво("СрокПоставкиПокупателюПоУмолчанию",Права,,ЭтотОбъект),СтруктураСведений.Значение);
		СрокПоставки=НачалоДня(?(обЗначениеНеЗаполнено(Дата),ТекущаяДата(),Дата))+СрокПоставкиДней*60*60*24;
	КонецЕсли; 
	
	//Заполним ПроцентПредоплаты для нового документа
	Если ЗначениеЗаполнено(Основание) И ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		//Для заказа покупателя по заказ-наряду все потом оплатим
		ПроцентПредоплаты=0;
	ИначеЕсли обЗначениеНеЗаполнено(ПроцентПредоплаты) Тогда
		Если ДоговорВзаиморасчетов.ПроцентПредоплаты = 0 Тогда
			ПроцентПредоплаты = обПраво("МинимальныйПроцентПредоплаты", Права,,ЭтотОбъект);
		Иначе
			ПроцентПредоплаты = ДоговорВзаиморасчетов.ПроцентПредоплаты;
		КонецЕсли;
	КонецЕсли;
	
	СуммаСоСкидкой=Товары.Итог("СуммаВсего");
	СуммаПредоплаты=СуммаСоСкидкой*ПроцентПредоплаты/100;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Для Каждого ТекСтрока Из Товары Цикл
		Если обЗначениеНеЗаполнено(ТекСтрока.ИдентификаторСтроки) Тогда
			ТекСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ ЭтотОбъект.ОбменДанными.Загрузка Тогда
		ОбработкаРеквизита("ХозОперация");
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ИзменениеПроцентаПредоплаты") И 
		ДополнительныеСвойства.ИзменениеПроцентаПредоплаты = Истина Тогда
		ОбменДанными.Загрузка = Ложь;
		
		дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
		
		Если (НЕ ЗначениеЗаполнено(ДокументОснование)) ИЛИ (НЕ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")) Тогда
			МинимальныйПроцентПредоплаты = обПраво("МинимальныйПроцентПредоплаты", Права,, ЭтотОбъект);
			Если МинимальныйПроцентПредоплаты > ПроцентПредоплаты Тогда
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки:",СтатусСообщения.Внимание);
				Сообщить("Запрещено изменять минимальный процент предоплаты менее, чем " + Формат(МинимальныйПроцентПредоплаты, "ЧЦ=5; ЧДЦ=2; ЧН=0")+"%", СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОбменДанными.Загрузка = Истина;
		Возврат;
	Иначе
		Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам",Права,,ЭтотОбъект) Тогда
		//Проверим наличие распределения данного заказа
		Если НЕ обПраво("РедактированиеЗаказовПриНаличииРаспределения",Права,,ЭтотОбъект) Тогда
			ВыборкаРаспределения=ПолучитьРаспределенияЗаказа();
			Если РежимЗаписи=РежимЗаписиДокумента.Запись Тогда
				Если ВыборкаРаспределения.Количество()>0 Тогда
					Сообщить("Товары по заказу покупателя распределены по заказам поставщикам. Запись без проведения запрещена.",СтатусСообщения.Важное);
					Отказ=Истина;
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат;
				КонецЕсли; 
			ИначеЕсли РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения Тогда
				Если ВыборкаРаспределения.Количество()>0 Тогда
					Сообщить("Товары по заказу покупателя распределены по заказам поставщикам. Отмена проведения запрещена.",СтатусСообщения.Важное);
					Отказ=Истина;
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат;
				КонецЕсли; 
			ИначеЕсли РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
				//Свернем товары с учетом единиц измерения
				ТоварыКопия=Товары.Выгрузить();
				Для каждого СтрокаТоваров Из ТоварыКопия Цикл
					СтрокаТоваров.Количество=СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
				КонецЦикла; 
				ТоварыКопия.Свернуть("Номенклатура,ХарактеристикаНоменклатуры","Количество");
				ВыборкаРаспределения.Сбросить();
				Пока ВыборкаРаспределения.Следующий() Цикл
					СтрокиТоваров=ТоварыКопия.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ВыборкаРаспределения.Номенклатура,ВыборкаРаспределения.ХарактеристикаНоменклатуры));
					ТоварКоличество=0;
					Если СтрокиТоваров.Количество()>0 Тогда
						ТоварКоличество=СтрокиТоваров[0].Количество;
					КонецЕсли;
					Если ТоварКоличество<ВыборкаРаспределения.Количество Тогда
						Если обЗначениеНеЗаполнено(ВыборкаРаспределения.ХарактеристикаНоменклатуры) Тогда
							Сообщить("["+СокрЛП(ВыборкаРаспределения.Номенклатура.Код)+"] Товар """+СокрЛП(ВыборкаРаспределения.Номенклатура)+""" распределен по заказам поставщикам в количестве "+Формат(ВыборкаРаспределения.Количество,"ЧЦ=15; ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(ВыборкаРаспределения.Номенклатура.БазоваяЕдиницаИзмерения)+" !",СтатусСообщения.Внимание);
						Иначе
							Сообщить("["+СокрЛП(ВыборкаРаспределения.Номенклатура.Код)+"] Товар """+СокрЛП(ВыборкаРаспределения.Номенклатура)+""" с характеристикой """+СокрЛП(ВыборкаРаспределения.ХарактеристикаНоменклатуры)+""" распределен по заказам поставщикам в количестве "+Формат(ВыборкаРаспределения.Количество,"ЧЦ=15; ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(ВыборкаРаспределения.Номенклатура.БазоваяЕдиницаИзмерения)+" !",СтатусСообщения.Внимание);
						КонецЕсли; 
						Отказ=Истина;
					КонецЕсли;
				КонецЦикла; 
				Если Отказ Тогда 
					Сообщить("Проведение заказа покупателя <"+СокрЛП(Ссылка)+"> отменено !",СтатусСообщения.Важное);
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли; 
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд") И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Если ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
			ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			Если НЕ ЭтотОбъект.Проведен Тогда
				Сообщить("Заказ-наряд выполнен или закрыт. Вводить заказы покупателей запрещено.");
				Отказ = Истина;
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект, Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ЗаказПокупателя Тогда
		Для Каждого СтрокаТабличнойЧасти Из Товары Цикл
			СтрокаТабличнойЧасти.Резерв = 0;
		КонецЦикла;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка";

		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	//БИЗТЕХ КОВАЛЬ 06.11.2024 ++
	//Блокировка распроведения и т.д. если по заказу были движения
	Если НЕ обПраво("РедактированиеЗаказовПокупателейПриДвижениях",Права,,ЭтотОбъект) Тогда
		Если РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения
			ИЛИ (РежимЗаписи=РежимЗаписиДокумента.Проведение И РежимПроведения=РежимПроведенияДокумента.Оперативный)
			И ПроверитьЗависимостиЗаказа(Ссылка) Тогда
				Отказ=Истина;
		КонецЕсли;
	КонецЕсли;
	//-
	
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	//Проверим сумму предоплаты
	Если СуммаДокумента<СуммаПредоплаты Тогда
		Сообщить("Сумма предоплаты не может быть больше суммы документа");
		Отказ=Истина; Возврат;
	КонецЕсли; 
	//Проверим количество резервируемого товара
	Если ХозОперация=Справочники.ХозОперации.ЗаказРезервированиеПокупателя Тогда
		Для каждого СтрокаТабличнойЧасти Из Товары Цикл
			Если СтрокаТабличнойЧасти.Количество<СтрокаТабличнойЧасти.Резерв Тогда
				Сообщить("Количество номенклатуры не может быть менее количества резервирования.");
				Отказ=Истина;
				Возврат;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	Если обПраво("ПроверкаМаксимальногоПревышенияКредитаКонтрагента",Права,,ЭтотОбъект) И НЕ ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита Тогда
		// получим долг
		СтруктураОтбора=Новый Структура();
		СтруктураОтбора.Вставить("Контрагент",Контрагент);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		//получаем текущие остатки, т.к. оплата может быть позже заказа
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма,СуммаУпр");
		Долг=тзДолги.Итог("Сумма");
		СуммаДокументаВВалютеДоговора=обПересчет(СуммаДокумента,ВалютаДокумента,КурсДокумента,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,Дата);
		Если (Долг+СуммаДокументаВВалютеДоговора)>ДоговорВзаиморасчетов.МаксимальныйКредит Тогда
			ВалютаВзаиморасчетов=СокрЛП(ДоговорВзаиморасчетов.ВалютаВзаиморасчетов.Наименование);
			дкСообщитьРезультат("   Сумма долга по договору составляет "+Формат(Долг,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
								|   Сумма заказа составляет "+Формат(СуммаДокументаВВалютеДоговора,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
								|   Итого "+Формат(Долг+СуммаДокументаВВалютеДоговора,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".
								|   А максимальный кредит по договору "+Формат(ДоговорВзаиморасчетов.МаксимальныйКредит,"ЧДЦ=2; ЧН=0.00")+" "+ВалютаВзаиморасчетов+".",,ЭтотОбъект);
			//Отказ=Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	Заказывать=Истина;
	Резервировать=((ХозОперация=Справочники.ХозОперации.ЗаказРезервированиеПокупателя) ИЛИ (ХозОперация=Справочники.ХозОперации.РезервированиеПокупателя));
	
	// проводим заказ покупателя
	НаборЗаписейЗаказыПокупателей=Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейЗаказыПокупателей.Контрагент=Контрагент;
	НаборЗаписейЗаказыПокупателей.Заказ=Ссылка;
	НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладКомпании;
	НаборЗаписейЗаказыПокупателей.Заказывать=Заказывать;
	НаборЗаписейЗаказыПокупателей.Резервировать=Резервировать;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
	
	Если Резервировать Тогда
		// резервирование заказа покупателя
		НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения           = Режим;
		НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам = Неопределено;
		НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
		НаборЗаписейОстатки.ДвиженияПоРознице         = Ложь;
		Отказ = НЕ НаборЗаписейОстатки.Зарезервировать() ИЛИ Отказ;
	КонецЕсли; 
	
	Если НЕ Отказ Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ЗаказПокупателяТовары.ИдентификаторСтроки,
		|	ЗаказПокупателяТовары.Номенклатура,
		|	ЗаказПокупателяТовары.ХарактеристикаНоменклатуры,
		|	ЗаказПокупателяТовары.Количество*ЗаказПокупателяТовары.Коэффициент КАК КоличествоЗаказа,
		|	ГрафикПоставок.Количество,
		|	ГрафикПоставок.ДатаПоставки,
		|	ГрафикПоставок.Контрагент,
		|	ГрафикПоставок.ДатаПоступления,
		|	ГрафикПоставок.Информация
		|ИЗ
		|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ГрафикПоставок КАК ГрафикПоставок
		|ПО
		|	ЗаказПокупателяТовары.Ссылка = &Ссылка И 
		|	ЗаказПокупателяТовары.Ссылка = ГрафикПоставок.ЗаказПокупателя И 
		|	ЗаказПокупателяТовары.ИдентификаторСтроки = ГрафикПоставок.ИдентификаторСтроки
		|ГДЕ
		|	ЗаказПокупателяТовары.Ссылка = &Ссылка
		|ИТОГИ
		|	МАКСИМУМ(КоличествоЗаказа)
		|ПО
		|	ЗаказПокупателяТовары.ИдентификаторСтроки
		|";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ВыборкаСтроки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		НаборЗаписейГрафикПоставок = РегистрыСведений.ГрафикПоставок.СоздатьНаборЗаписей();
		НаборЗаписейГрафикПоставок.Отбор.ЗаказПокупателя.Установить(Ссылка);
		
		Пока ВыборкаСтроки.Следующий() Цикл
			КоличествоЗаказа    = ВыборкаСтроки.КоличествоЗаказа;
			Выборка = ВыборкаСтроки.Выбрать(ОбходРезультатаЗапроса.Прямой);
			Пока Выборка.Следующий() Цикл
				Если КоличествоЗаказа <= 0 Тогда
					Прервать;
				КонецЕсли;
				Если Выборка.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				Количество = Мин(Выборка.Количество, КоличествоЗаказа);
				Запись = НаборЗаписейГрафикПоставок.Добавить();
				Запись.ЗаказПокупателя            = Ссылка;
				Запись.ИдентификаторСтроки        = Выборка.ИдентификаторСтроки;
				Запись.Номенклатура               = Выборка.Номенклатура;
				Запись.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				Запись.Количество                 = Количество;
				Запись.ДатаПоставки               = Выборка.ДатаПоставки;
				Запись.Контрагент                 = Выборка.Контрагент;
				Запись.ДатаПоступления            = Выборка.ДатаПоступления;
				Запись.Информация                 = Выборка.Информация;
				КоличествоЗаказа = КоличествоЗаказа - Количество;
			КонецЦикла;
		КонецЦикла;
		
		НаборЗаписейГрафикПоставок.Записать();
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыЗаказы.Заказ = Ссылка;
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли



// Модуль документа ПоступлениеАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка наличия других заказов на автомобиль с данным VINом
//
// Параметры
//  VINАвтомобиля  – <Строка> – VIN автомобиля, заказы на который проверяются
//  ОбъектПроверки  – <ДокументОбъект> – Документ, который проверяет наличие заказов
//
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль()
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.ПоступлениеАвтомобилейПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.ПоступлениеАвтомобилейПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Функция производит поиск заказа по заданному автомобилю
Функция НайтиЗаказАвтомобиля(Автомобиль) Экспорт
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("ЭтоНовый",ЭтоНовый());
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	Возврат зфЗащищенныеФункцииСервер.ПоступлениеАвтомобилейНайтиЗаказАвтомобиля(ДокументОбъектСтруктура,Автомобиль);
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("VIN",        3);
	ОбязательныеАвтомобили.Вставить("Автомобиль", 3);
	ОбязательныеАвтомобили.Вставить("ИдентификаторАвтомобиля",1);
	ОбязательныеАвтомобили.Вставить("Количество", 1);
	ОбязательныеОпции=Новый Структура();
	ОбязательныеОпции.Вставить("ИдентификаторАвтомобиля",3);
	ОбязательныеОпции.Вставить("Опция",3);
	ОбязательныеОпции.Вставить("Количество",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	ОбязательныеРеквизиты.Вставить("Опции",ОбязательныеОпции);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И Результат;
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект, "Автомобили", "Автомобиль");
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПоступлениеАвтомобилей","Поступление автомобилей",Истина);
	СписокХозОпераций.Добавить("ПоступлениеАвтомобилейКомиссия","Поступление автомобилей (комиссия)");
	СписокХозОпераций.Добавить("ПоступлениеАвтомобилейОтветственноеХранение","Поступление автомобилей (ответственное хранение)");
	СписокХозОпераций.Добавить("ПереходВСобственностьАвтомобилей","Переход автомобилей в собственность (с ответственного хранения)");

	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	|
	|ИЗ
	|	Документ.ПоступлениеАвтомобилей КАК Док
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// функция возвращает список опций для текущего набора модели или варианта комплектации
Функция ПолучитьСписокДоступныхОпций(Модель, ВариантКомплектации = Неопределено) Экспорт
	СписокОборудования=Новый СписокЗначений;
	Запрос = Новый Запрос;
	Если обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
		Запрос.Текст = "ВЫБРАТЬ
		|	МоделиОпции.Опция КАК Опция
		|ИЗ
		|	Справочник.Модели.Опции КАК МоделиОпции
		|ГДЕ
		|	МоделиОпции.Ссылка = &Модель";
		Запрос.УстановитьПараметр("Модель", Модель);
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		|	ОпцииВариантовКомплектации.Опция КАК Опция
		|ИЗ
		|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
		|ГДЕ
		|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
		|	И ОпцииВариантовКомплектации.ВидВключения = 1
		|";
		Запрос.УстановитьПараметр("ВариантКомплектации",ВариантКомплектации);
	КонецЕсли;
	СписокОборудования.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Опция"));
	Возврат СписокОборудования
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Автомобили.Итог("СуммаВсего")+Опции.Итог("СуммаВсего");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
			Для каждого СтрокаТЧ Из Опции Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Опции.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
	ИначеЕсли Имя = "ТипЦен" Тогда
		
		ТребуетсяПересчет = Истина;
		Если ДопПараметры <> Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет",ТребуетсяПересчет) Тогда
				ТребуетсяПересчет = Истина;
			КонецЕсли; 
		КонецЕсли; 
		
		// пересчитываем
		Если ТребуетсяПересчет Тогда
			Для Каждого СтрокаАвтомобиля Из Автомобили Цикл
				
				ЗаказНаАвтомобиль   = СтрокаАвтомобиля.ЗаказНаАвтомобиль;
				//Если ЗначениеЗаполнено(ЗаказНаАвтомобиль) И ЗаказНаАвтомобиль.ХозОперация = Справочники.ХозОперации.ЗаказАвтомобиляНаСклад Тогда
				СтрокаАвтомобиля.Цена = ЗаказНаАвтомобиль.ЦенаАвтомобиля;
				//Иначе
				//	СтрокаАвтомобиля.Цена = 0;
				//КонецЕсли;
				
				Если СтрокаАвтомобиля.Цена=0 Тогда
					СтрокаАвтомобиля.Цена = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль,ВариантКомплектации", СтрокаАвтомобиля.Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()), ?(Ссылка.Пустая(), Дата, МоментВремени()), , ТекущаяВалютаДокумента, ТекущийКурсДокумента);
				КонецЕсли;
				ОбработкаРеквизита("Автомобили.Цена", СтрокаАвтомобиля, ЭтаФорма, ДопПараметры);
				
				НайденныеОпции      = Опции.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля", СтрокаАвтомобиля.ИдентификаторАвтомобиля));
				Модель              = СтрокаАвтомобиля.Автомобиль.Модель;
				ВариантКомплектации = СтрокаАвтомобиля.Автомобиль.ВариантКомплектации;
				
				//ПоискОпцийВЗаказе = (ЗначениеЗаполнено(ЗаказНаАвтомобиль) И ЗаказНаАвтомобиль.ХозОперация = Справочники.ХозОперации.ЗаказАвтомобиляНаСклад);
				Для Каждого СтрокаОпции Из НайденныеОпции Цикл
					
					ОпцияИзЗаказа=Неопределено;
					//Если ПоискОпцийВЗаказе Тогда
						ОпцияИзЗаказа = ЗаказНаАвтомобиль.Опции.Найти(СтрокаОпции.Опция, "Опция");
						Если ОпцияИзЗаказа<>Неопределено Тогда
							СтрокаОпции.Цена=ОпцияИзЗаказа.Цена;
						Иначе
							СтрокаОпции.Цена = 0;
						КонецЕсли;
					//Иначе
					//	СтрокаОпции.Цена = 0;
					//КонецЕсли;
					Если СтрокаОпции.Цена=0 Тогда
						СтрокаОпции.Цена = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации, Опция", Модель, ВариантКомплектации, СтрокаОпции.Опция),?(Ссылка.Пустая(), Дата, МоментВремени()),, ТекущаяВалютаДокумента, ТекущийКурсДокумента);
					КонецЕсли;
					ОбработкаРеквизита("Опции.Цена", СтрокаОпции, ЭтаФорма, ДопПараметры);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		Иначе
			НеверныйДоговор=Ложь;
			Если ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда
				Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомитентом И
					 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
					 НеверныйДоговор=Истина;
				КонецЕсли; 
			Иначе
				Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Покупка И
					 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
					 НеверныйДоговор=Истина;
				КонецЕсли; 
			КонецЕсли;
			Если НеверныйДоговор Тогда
				#Если Клиент Тогда
				Предупреждение("Неправильный вид договора !");
				#КонецЕсли
				ДоговорВзаиморасчетов=Неопределено;
				ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли; 
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании = Неопределено;
			КонецЕсли; 
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		//Проставим количество и цену
		Если ТекСтрока.Количество=0 Тогда
			ТекСтрока.Количество=1
		КонецЕсли;
		// проверим заметик
		#Если Клиент Тогда
			дкСообщитьОЗаметках(ЭтотОбъект,ТекСтрока.Автомобиль,ЭтаФорма);
		#КонецЕсли
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			Если НЕ обЗначениеНеЗаполнено(Контрагент) И Контрагент.ОсвобожденОтНДС Тогда
				Запрос=Новый Запрос("
				|ВЫБРАТЬ ПЕРВЫЕ 1 СпрСтавкиНДС.Ссылка КАК СтавкаБезНДС
				|ИЗ Справочник.СтавкиНДС КАК СпрСтавкиНДС
				|ГДЕ СпрСтавкиНДС.Ставка=0");
				РезультатСтавкаБезНДС=Запрос.Выполнить();
				Если РезультатСтавкаБезНДС.Пустой() Тогда
					ТекСтрока.СтавкаНДС=Неопределено;
				Иначе
					ТекСтрока.СтавкаНДС=РезультатСтавкаБезНДС.Выгрузить().Получить(0).СтавкаБезНДС;
				КонецЕсли;
			Иначе
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
		КонецЕсли;
		Если НЕ ПустаяСтрока(ТекСтрока.Автомобиль.VIN) Тогда
			ТекСтрока.VIN = ТекСтрока.Автомобиль.VIN;
		КонецЕсли;
		ТекСтрока.Цена = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации",ТекСтрока.Автомобиль,Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
		ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		//Попробуем подставить ГТД
		Если обЗначениеНеЗаполнено(ТекСтрока.ГТД) Тогда
			ИндексПредСтроки=Автомобили.Индекс(ТекСтрока)-1;
			Если ИндексПредСтроки>=0 Тогда
				ПредСтрока=Автомобили.Получить(ИндексПредСтроки);
				Если НЕ обЗначениеНеЗаполнено(ПредСтрока.ГТД) Тогда
					ТекСтрока.ГТД=ПредСтрока.ГТД;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		Если ЗначениеЗаполнено(ТекСтрока.Автомобиль) Тогда
			Заказ=НайтиЗаказАвтомобиля(ТекСтрока.Автомобиль);
			ТекСтрока.ЗаказНаАвтомобиль=Заказ;
			Рез = ОбработкаРеквизита("Автомобили.ЗаказНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
			Если НЕ Заказ = Неопределено Тогда
				Возврат Рез;
			КонецЕсли;
			ТекСтрока.АвтомобильБУ = (Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель);
			
			// запишем опции уже имеющиеся на регистре комплектации
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КомплектацияАвтомобилейОстатки.Номенклатура КАК Опция,
			|	КомплектацияАвтомобилейОстатки.КоличествоОстаток КАК Количество
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей.Остатки КАК КомплектацияАвтомобилейОстатки
			|ГДЕ
			|	КомплектацияАвтомобилейОстатки.Номенклатура ССЫЛКА Справочник.Опции
			|	И КомплектацияАвтомобилейОстатки.Автомобиль = &Автомобиль";
			Запрос.УстановитьПараметр("Автомобиль", ТекСтрока.Автомобиль);
			ОпцииЗаказа = Запрос.Выполнить().Выгрузить();
			
			Если ОпцииЗаказа.Количество() > 0 Тогда
				// очистим опции текущего автомобиля
				ОпцииТекущегоАвтомобиля = Опции.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля", ТекСтрока.ИдентификаторАвтомобиля));
				Для Каждого СтрокаОпций Из ОпцииТекущегоАвтомобиля Цикл
					Опции.Удалить(СтрокаОпций);
				КонецЦикла;
			КонецЕсли;
			
			Для Каждого ОпцияЗаказа Из ОпцииЗаказа Цикл
				ОпцииТекущегоАвтомобиля=Опции.Добавить();
				ОпцииТекущегоАвтомобиля.ИдентификаторАвтомобиля=ТекСтрока.ИдентификаторАвтомобиля;
				ОпцииТекущегоАвтомобиля.Опция=ОпцияЗаказа.Опция;
				ОпцииТекущегоАвтомобиля.Количество=ОпцияЗаказа.Количество;
				ОбработкаРеквизита("Опции.Опция",ОпцииТекущегоАвтомобиля,ЭтаФорма);
			КонецЦикла;
		КонецЕсли;
		
		//получим список доступных опций
		Если обЗначениеНеЗаполнено(ТекСтрока.Автомобиль) Тогда
			Модель = Справочники.Модели.ПустаяСсылка();
			Вариант = Неопределено;
		Иначе
			Модель = ТекСтрока.Автомобиль.Модель;
			Вариант = ТекСтрока.Автомобиль.ВариантКомплектации;
		КонецЕсли;
		
		СписокДостОпций = ПолучитьСписокДоступныхОпций(Модель,Вариант);
		//получим опции автомобиля
		МассивОпций = Опции.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",ТекСтрока.ИдентификаторАвтомобиля));
		
		//найдем опции для удаления
		МассивДляУдаления = Новый Массив;
		Для Каждого стрОпций Из МассивОпций Цикл
			Если СписокДостОпций.НайтиПоЗначению(стрОпций.Опция) = Неопределено Тогда
				МассивДляУдаления.Добавить(стрОпций);
			КонецЕсли;
		КонецЦикла;
		
		//удаление опций из списка
		Для Каждого стрУдалить Из МассивДляУдаления Цикл
			Опции.Удалить(стрУдалить);
		КонецЦикла;
		Возврат Истина;
	
	ИначеЕсли Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		ЦенаВключаетНДС=?(обЗначениеНеЗаполнено(ТипЦен),Истина,ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен - Получим сумму НДС
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		ТекСтрока.Цена=ТекСтрока.Сумма/?(ТекСтрока.Количество=0,1,ТекСтрока.Количество);
		ЦенаВключаетНДС=?(обЗначениеНеЗаполнено(ТипЦен),Истина,ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен - Получим сумму НДС
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		//Получим новую сумму НДС как процент от суммы всего
		ЦенаВключаетНДС=?(обЗначениеНеЗаполнено(ТипЦен),Истина,ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			ТекСтрока.Сумма=ТекСтрока.СуммаВсего;
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен
			ТекСтрока.Сумма=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-ТекСтрока.Сумма;
		КонецЕсли; 
		ТекСтрока.Цена=ТекСтрока.Сумма/?(ТекСтрока.Количество=0,1,ТекСтрока.Количество);
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаНДС" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.АвтомобильБезЗаказа" Тогда
		Рез=Истина;
		Если ТекСтрока.АвтомобильБезЗаказа Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ЗаказНаАвтомобиль) Тогда
				ТекСтрока.ЗаказНаАвтомобиль=Неопределено;
				Рез=ОбработкаРеквизита("Автомобили.ЗаказНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобили.ЗаказНаАвтомобиль" Тогда
		ЗаказНаАвтомобиль = ТекСтрока.ЗаказНаАвтомобиль;
		Если ЗначениеЗаполнено(ЗаказНаАвтомобиль) Тогда
			// очистим опции текущего автомобиля
			ОпцииТекущегоАвтомобиля = Опции.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля", ТекСтрока.ИдентификаторАвтомобиля));
			Для Каждого СтрокаОпций Из ОпцииТекущегоАвтомобиля Цикл
				Опции.Удалить(СтрокаОпций);
			КонецЦикла;
			//Если ЗаказНаАвтомобиль.ХозОперация = Справочники.ХозОперации.ЗаказАвтомобиляНаСклад Тогда
			ЦенаАвтомобиля = ЗаказНаАвтомобиль.ЦенаАвтомобиля;
			//Иначе
			//	ЦенаАвтомобиля = 0;
			//КонецЕсли;
			Если ЦенаАвтомобиля<>0 Тогда
				ТекСтрока.Цена      = ЦенаАвтомобиля;
				ТекСтрока.СтавкаНДС = ЗаказНаАвтомобиль.СтавкаНДСНаАвтомобиль;
				ОбработкаРеквизита("Автомобили.Цена", ТекСтрока, ЭтаФорма,ДопПараметры);
			КонецЕсли;
			ОпцииЗаказа = ЗаказНаАвтомобиль.Опции;
			Для Каждого ОпцияЗаказа Из ОпцииЗаказа Цикл
				ОпцииТекущегоАвтомобиля=Опции.Добавить();
				ОпцииТекущегоАвтомобиля.ИдентификаторАвтомобиля=ТекСтрока.ИдентификаторАвтомобиля;
				ОпцииТекущегоАвтомобиля.Опция=ОпцияЗаказа.Опция;
				ОпцииТекущегоАвтомобиля.Количество=ОпцияЗаказа.Количество;
				ОбработкаРеквизита("Опции.Опция",ОпцииТекущегоАвтомобиля,ЭтаФорма);
			КонецЦикла; 
			ТекСтрока.АвтомобильБезЗаказа = Ложь;
		Иначе
		//Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ЗаказНаАвтомобиль) Тогда
			ТекСтрока.АвтомобильБезЗаказа = Истина;
		КонецЕсли;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ДОПОЛНИТЕЛЬНЫЕ ОПЦИИ"
	ИначеЕсли Имя="Опции.Опция" Тогда
		СтрокаАвтомобиля=Автомобили.Найти(ТекСтрока.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаАвтомобиля=Неопределено Тогда
			//Автомобиль не найден, значит и опций нет
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда
				ТекСтрока.Опция=Неопределено;
				Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли;
		Автомобиль=СтрокаАвтомобиля.Автомобиль;
		ЗаказНаАвтомобиль = СтрокаАвтомобиля.ЗаказНаАвтомобиль;
		Если обЗначениеНеЗаполнено(Автомобиль) И обЗначениеНеЗаполнено(ЗаказНаАвтомобиль) Тогда
			//Автомобиль и заказ не прописан, значит и опций нет
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда
				ТекСтрока.Опция=Неопределено;
				Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли;
		НаборОпций=Ложь;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("НаборОпций",НаборОпций) Тогда
				НаборОпций=Ложь;
			КонецЕсли; 
		КонецЕсли; 
		Если (НЕ НаборОпций) И (НЕ обЗначениеНеЗаполнено(ТекСтрока.Опция)) Тогда
			Если ЗначениеЗаполнено(Автомобиль) Тогда
				ВариантКомплектации=Автомобиль.ВариантКомплектации;
				Модель=Автомобиль.Модель;
			ИначеЕсли ЗначениеЗаполнено(ЗаказНаАвтомобиль) Тогда
				ВариантКомплектации=ЗаказНаАвтомобиль.ВариантКомплектации;
				Модель=ЗаказНаАвтомобиль.Модель;
			Иначе
				ВариантКомплектации=Неопределено;
				Модель=Справочники.Модели.ПустаяСсылка();
			КонецЕсли; 
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Опция",ТекСтрока.Опция);
			Если обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
				Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
					|	МоделиОпции.Опция КАК Опция
					|ИЗ
					|	Справочник.Модели.Опции КАК МоделиОпции
					|ГДЕ
					|	МоделиОпции.Ссылка = &Модель
					|	И МоделиОпции.Опция = &Опция";
				Запрос.УстановитьПараметр("Модель", Модель);
			Иначе
				Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				    |	ОпцииВариантовКомплектации.Опция КАК Опция
				    |ИЗ
				    |	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
				    |ГДЕ
				    |	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
				    |	И ОпцииВариантовКомплектации.Опция = &Опция
				    |	И ОпцииВариантовКомплектации.ВидВключения = 1";
				Запрос.УстановитьПараметр("ВариантКомплектации",ВариантКомплектации);
			КонецЕсли; 
			Если Запрос.Выполнить().Пустой() Тогда
				//Такой опции автомобиля нет
				Если ЗначениеЗаполнено(ТекСтрока.Опция) Тогда
					ТекСтрока.Опция=Неопределено;
					Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		Рез=Истина;
		Если ТекСтрока.Опция.ПризнакНабора Тогда
			// имеем дело с набором, разложим его...
			Набор=ТекСтрока.Опция; КоличествоНабора=ТекСтрока.Количество;
			Если КоличествоНабора=0 Тогда КоличествоНабора=1; КонецЕсли; 
			// удалим строку из табличной части
			Опции.Удалить(ТекСтрока);
			// теперь будем рекурсивно добавлять состав набора
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры=Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("НаборОпций",Истина);
			Для Каждого ИзНабора Из Набор.СоставНабора Цикл
				Если ИзНабора.Опция.ПризнакНабора Тогда
					//Вот это да, набор в наборе ... игнорируем
					#Если Клиент Тогда
					Сообщить("В наборе """+СокрЛП(Набор)+""" присутствует набор """+СокрЛП(ИзНабора.Опция)+""". Добавление набора """+СокрЛП(ИзНабора.Опция)+""" отменено.",СтатусСообщения.Внимание);
					#КонецЕсли
				Иначе
					// поищем оборудование из набора в таблице
					МассивСтрок=Опции.НайтиСтроки(Новый Структура("Опция",ИзНабора.Опция));
					Если МассивСтрок.Количество()>0 Тогда
						//Если добавляемое оборудование уже есть в табличной части
						//просто увеличим количество, если его запрашивать не надо
						СтрокаТЧ=МассивСтрок[0];
						СтрокаТЧ.Количество=СтрокаТЧ.Количество+(ИзНабора.Количество*КоличествоНабора);
						Рез=ОбработкаРеквизита("Опции.Количество",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
					Иначе
						СтрокаТЧ=Опции.Добавить();
						СтрокаТЧ.ИдентификаторАвтомобиля=СтрокаАвтомобиля.ИдентификаторАвтомобиля;
						СтрокаТЧ.Опция=ИзНабора.Опция;
						СтрокаТЧ.Количество=ИзНабора.Количество*КоличествоНабора;
						Рез=ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла;
			ДопПараметры.Удалить("НаборОпций");
		Иначе
			СтрокаАвтомобиля=Автомобили.Найти(ТекСтрока.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
			Если СтрокаАвтомобиля=Неопределено Тогда
				Модель=Справочники.Модели.ПустаяСсылка();
				ВариантКомплектации=Справочники.ВариантыКомплектации.ПустаяСсылка();
				ЗаказНаАвтомобиль = Документы.ЗаказПоставщикуНаАвтомобиль.ПустаяСсылка();
			Иначе
				Модель=СтрокаАвтомобиля.Автомобиль.Модель;
				ВариантКомплектации=СтрокаАвтомобиля.Автомобиль.ВариантКомплектации;
				ЗаказНаАвтомобиль = СтрокаАвтомобиля.ЗаказНаАвтомобиль;
			КонецЕсли;
			ОпцияИзЗаказа=Неопределено;
			Если ЗначениеЗаполнено(ЗаказНаАвтомобиль) Тогда
				ОпцияИзЗаказа = ЗаказНаАвтомобиль.Опции.Найти(ТекСтрока.Опция, "Опция");
				Если ОпцияИзЗаказа<>Неопределено Тогда
					ТекСтрока.Количество=ОпцияИзЗаказа.Количество;
					//Если ЗаказНаАвтомобиль.ХозОперация = Справочники.ХозОперации.ЗаказАвтомобиляНаСклад Тогда
						ТекСтрока.СтавкаНДС=ОпцияИзЗаказа.СтавкаНДС;
						ТекСтрока.Цена=ОпцияИзЗаказа.Цена;
					//КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			//Проставим количество
			Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
			//Установим ставку НДС
			Если ТекСтрока.Цена=0 Тогда
				Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
					ОсвобожденОтНДС	= ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
						ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;	
				Иначе
					ОсвобожденОтНДС	= ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;	
				КонецЕсли;
				Если ОсвобожденОтНДС Тогда
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				ИначеЕсли ТипЗнч(ТекСтрока.Опция)=Тип("СправочникСсылка.Опции") Тогда
					ТекСтрока.СтавкаНДС = ТекСтрока.Опция.СтавкаНДС; 
				Иначе
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС; 
				КонецЕсли;
				ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации,Опция",Модель,ВариантКомплектации,ТекСтрока.Опция),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			Рез=ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Опции.Количество" Тогда
		Возврат ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		ЦенаВключаетНДС=?(обЗначениеНеЗаполнено(ТипЦен),Истина,ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен - Получим сумму НДС
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.Сумма" Тогда
		ТекСтрока.Цена=ТекСтрока.Сумма/?(ТекСтрока.Количество=0,1,ТекСтрока.Количество);
		ЦенаВключаетНДС=?(обЗначениеНеЗаполнено(ТипЦен),Истина,ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен - Получим сумму НДС
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.СуммаВсего" Тогда
		ЦенаВключаетНДС=?(обЗначениеНеЗаполнено(ТипЦен),Истина,ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			ТекСтрока.Сумма=ТекСтрока.СуммаВсего;
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен
			ТекСтрока.Сумма=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-ТекСтрока.Сумма;
		КонецЕсли; 
		ТекСтрока.Цена=ТекСтрока.Сумма/?(ТекСтрока.Количество=0,1,ТекСтрока.Количество);
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.СуммаНДС" Тогда
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Процедура проверяет заказы на авто и если найдены несоответствия то подставляет подходящий
// или устанавливает без заказа
Процедура ПерезаполнитьЗаказы()
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.Выгрузить());
	зфЗащищенныеФункцииСервер.ПоступлениеАвтомобилейПерезаполнитьЗаказы(ДокументОбъектСтруктура);
	Автомобили.Очистить();
	Автомобили.Загрузить(ДокументОбъектСтруктура.Автомобили);
КонецПроцедуры

// Нарашивание строки ошибок
Функция ДобавитьОшибку(Ошибки="",ЕщеСтрока="") 
	Ошибки=?(Ошибки="",ЕщеСтрока,Ошибки+Символы.ПС+ЕщеСтрока);
	Возврат(Ошибки);
КонецФункции // ДобавитьОшибку()

Процедура ЗаписатьСтатусы(Отказ) Экспорт
	// Зарезервируем автомобили под заказы клиентов
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Автомобиль,
	|	ЗаказыАвтомобилейОстатки.Заказ,
	|	ЗаказыАвтомобилейОстатки.Заказ.СтатусАвтомобиля КАК СтатусАвтомобиля,
	|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0) КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(
	|			&НаГраницу,
	|			Автомобиль В (&Автомобили)) КАК ЗаказыАвтомобилейОстатки
	|ГДЕ
	|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0)<>0
	|";
	Запрос.УстановитьПараметр("НаГраницу",  Новый Граница(МоментВремени(),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Автомобили", Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.СтатусАвтомобиля<>СтатусАвтомобиля Тогда
			ЗаказОбъект = Выборка.Заказ.ПолучитьОбъект();
			ЗаказОбъект.ОбменДанными.Загрузка = Истина;
			ЗаказФорма = ЗаказОбъект.ПолучитьФорму();
			ЗаказФорма.СтатусАвтомобиля=СтатусАвтомобиля;
			Попытка
				ЗаказФорма.Записать();
				ЗаказФорма.Обновить();
			Исключение
				дкСообщитьРезультат(ОписаниеОшибки(), СтатусСообщения.Внимание, ЭтотОбъект);
				Отказ = Истина;
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ПоступлениеАвтомобили - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Для контрагентов из ЕАЭС другая форма отчетности
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(Контрагент.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ОсвобождентОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Автомобили.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ПоступлениеАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
	|	СУММА(ПоступлениеАвтомобилейАвтомобили.Количество) КАК КоличествоПрослеживаемости,
	|	ПоступлениеАвтомобилейАвтомобили.ГТД КАК РНПТ,
	|	ПоступлениеАвтомобилейАвтомобили.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ПоступлениеАвтомобилейАвтомобили.СуммаВсего - ПоступлениеАвтомобилейАвтомобили.СуммаНДС) КАК СуммаБезНДС
	|ИЗ
	|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
	|ГДЕ
	|	ПоступлениеАвтомобилейАвтомобили.Ссылка = &Ссылка
	|	И ПоступлениеАвтомобилейАвтомобили.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеАвтомобилейАвтомобили.Автомобиль,
	|	ПоступлениеАвтомобилейАвтомобили.ГТД,
	|	ПоступлениеАвтомобилейАвтомобили.СтавкаНДС";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим, что в документе есть РНПТ
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = Запрос.Выполнить().Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	КодОперации = Справочники.КодыОперацийПрослеживаемости.ПоступлениеТоваров;
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	// Сформируем таблицу
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.КодОперации = КодОперации;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = ВхДокНомер;
		НоваяСтрока.ДатаДокумента = ВхДокДата;
		НоваяСтрока.Контрагент = Контрагент;
		
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(обПересчет(НоваяСтрока.СуммаБезНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,Дата), 2);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПоступлениеАвтомобилей"
// Возвращает сформированный табличный документ:
Функция ПечатьПоступлениеАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПоступлениеАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = "Приходная накладная (автомобили) № " + дкПолучитьНомерДляПечати(ЭтотОбъект)
		+ " от " + Формат(Дата, "ДФ = dd.MM.yyyy");
	
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Контрагент);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	//подготовим дополнительную таблицу
	ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
	ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.Найти(СтрокаТабличнойЧасти.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиОпции <> Неопределено Тогда
			СтрокаТабличнойЧасти.Сумма		= СтрокаТабличнойЧасти.Сумма		+ СтрокаТабличнойЧастиОпции.Сумма;
			СтрокаТабличнойЧасти.СуммаНДС	= СтрокаТабличнойЧасти.СуммаНДС		+ СтрокаТабличнойЧастиОпции.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаВсего	= СтрокаТабличнойЧасти.СуммаВсего	+ СтрокаТабличнойЧастиОпции.СуммаВсего;
		КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "Акт приема"
// Возвращает сформированный табличный документ:
Функция ПечатьАктПриема(ТабДокумент) Экспорт
	
	ДокументОбъект = ЭтотОбъект;
	
	Макет = ПолучитьМакет("АктПриема");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ДокументОбъект.ЭтотОбъект,Истина);
	
	Для Каждого Строка Из ДокументОбъект.Автомобили Цикл
		
		ОбластьМакета.Параметры.ЗаголовокПФ = "приема автомобиля на комиссию";
		ОбластьМакета.Параметры.Номер = НомерДляПечати;
		ОбластьМакета.Параметры.Дата = Формат(ДокументОбъект.Дата,"ДЛФ=DD");
		Если ЗначениеЗаполнено(Строка.Автомобиль) Тогда
			ОбластьМакета.Параметры.МодельНаименование = спПолучитьНаименование(Строка.Автомобиль.Модель);
			ОбластьМакета.Параметры.Модель = Строка.Автомобиль.Модель;
			ОбластьМакета.Параметры.VIN = Строка.Автомобиль.VIN;
			ОбластьМакета.Параметры.ГодВыпуска = Формат(Строка.Автомобиль.ГодВыпуска,"ДФ=гггг");
			ОбластьМакета.Параметры.Цвет = Строка.Автомобиль.Цвет;
			ОбластьМакета.Параметры.Пробег = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Строка.Автомобиль,Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег, ДокументОбъект.Дата));
			ОбластьМакета.Параметры.Комплектация = Строка.Автомобиль.ВариантКомплектации;
			ОбластьМакета.Параметры.ТипСалона = Строка.Автомобиль.ТипСалона;
			
			Если Строка.Автомобиль.ТипДвигателя.Пустая() 
				И НЕ Строка.Автомобиль.ВариантКомплектации.Пустая() 
				И НЕ Строка.Автомобиль.ВариантКомплектации.ТипДвигателя.Пустая() Тогда
					ОбластьМакета.Параметры.ОбъемДвигателя = Строка.Автомобиль.ВариантКомплектации.ТипДвигателя.ОбъемДвигателя;
			Иначе	
				ОбластьМакета.Параметры.ОбъемДвигателя = Строка.Автомобиль.ТипДвигателя.ОбъемДвигателя;
			КонецЕсли;
			
			Если Строка.Автомобиль.ТипКПП.Пустая() 
				И НЕ Строка.Автомобиль.ВариантКомплектации.Пустая() Тогда 
				ОбластьМакета.Параметры.ТипКПП = Строка.Автомобиль.ВариантКомплектации.ТипКПП;
			Иначе	
				ОбластьМакета.Параметры.ТипКПП = Строка.Автомобиль.ТипКПП;
			КонецЕсли;
			
		КонецЕсли;
			
		ОбластьМакета.Параметры.ОписаниеКомитента = Контрагент.НаименованиеПолное;
		ОбластьМакета.Параметры.ОписаниеКомиссионера = Организация.НаименованиеПолное;
		
		ТабДокумент.Вывести(ОбластьМакета);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

// Формирует печатную форму "Договор комиссии"
// Возвращает сформированный табличный документ:
Функция ПечатьДоговорКомиссии(ТабДокумент) Экспорт
	
	ДокументОбъект =  ЭтотОбъект;
	
	Макет = ПолучитьМакет("ДоговорКомиссии");
	
	НомерДокумента = дкПолучитьНомерДляПечати(ДокументОбъект,Истина);
	
	Для Каждого Строка Из ДокументОбъект.Автомобили Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		//заполнение параметров шапки печатной формы
		
		ОбластьМакета.Параметры.Номер = НомерДокумента;
		ОбластьМакета.Параметры.Город = киПолучитьГород(ДокументОбъект.ПодразделениеКомпании,Справочники.ВидыКонтактнойИнформации.АдресФактический);
		ОбластьМакета.Параметры.Дата = Формат(ДокументОбъект.Дата,"ДЛФ=DD");
		
		//заполнение параметров комитента
		
		ОбластьМакета.Параметры.Контрагент = ДокументОбъект.Контрагент.НаименованиеПолное;
		Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(ДокументОбъект.Контрагент,Перечисления.ВидыДокументов.Паспорт);
		Если ЗначениеЗаполнено(Паспорт) Тогда
			ПаспортныеДанные = " паспорт серии " + Паспорт.Серия + " № "+ Паспорт.Номер + " выдан " + Паспорт.КемВыдан + " " + Формат(Паспорт.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г.";
			ОбластьМакета.Параметры.ПаспортныеДанные = " (" + ПаспортныеДанные + ")";
		КонецЕсли;
		Доверенность = спПолучитьПодтверждающийДокументОбъектаПоВиду(ДокументОбъект.Контрагент,Перечисления.ВидыДокументов.Доверенность);
		Если ЗначениеЗаполнено(Доверенность) Тогда
			ОбластьМакета.Параметры.Доверенность = ", действующего  на основании " + Доверенность;
		КонецЕсли;
		
		//заполнение параметров комиссионера
	
		ОбластьМакета.Параметры.Организация = ДокументОбъект.Организация.НаименованиеПолное;
		Руководитель = дкОтветственноеЛицо(ДокументОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
		Если ЗначениеЗаполнено(Руководитель) И ТипЗнч(Руководитель)=Тип("Структура") Тогда
			Должность = ", в лице";
			Если ЗначениеЗаполнено(Руководитель.РуководительДолжность) Тогда
				Должность = Должность + " " + СокрЛП(НРег(Руководитель.РуководительДолжность));
			КонецЕсли;
			ОбластьМакета.Параметры.РуководительДолжность = Должность;
			РуководительПредставление = Руководитель.РуководительПредставление;
			ДоверенностьРуководителя = спПолучитьПодтверждающийДокументОбъектаПоВиду(Руководитель.Руководитель,Перечисления.ВидыДокументов.Доверенность);
			Если ЗначениеЗаполнено(ДоверенностьРуководителя) Тогда
				РуководительПредставление = РуководительПредставление + ", действующего  на основании Доверенности №" + ДоверенностьРуководителя.Номер + " от "
											+ Формат(ДоверенностьРуководителя.ДатаВыдачи,"ДЛФ=D"); ;
			Иначе
				РуководительПредставление = ", действующего  на основании Устава"; 		
			КонецЕсли;
			ОбластьМакета.Параметры.РуководительПредставление = РуководительПредставление;
		КонецЕсли;
		
		//заполнение параметров автомобиля
		
		Автомобиль = Строка.Автомобиль;
		ГосНомер = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер, ДокументОбъект.Дата));
		Пробег = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег, ДокументОбъект.Дата));
		ПТС = спПолучитьПодтверждающийДокументОбъектаПоВиду(Автомобиль,Перечисления.ВидыДокументов.ПТС);
		ПредставлениеПТС = "ПТС " + ПТС.Серия + " № "+ ПТС.Номер + " выдан " + ПТС.КемВыдан + " " + Формат(ПТС.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г. ";
		
		СтрАвто = "";
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Автомобиль.Модель),"модели" + " " + спПолучитьНаименование(Автомобиль.Модель) + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Автомобиль.Цвет),"цвет: " + Автомобиль.Цвет + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(ГосНомер),"государственный номер: " + ГосНомер + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Автомобиль.VIN),"VIN: " + Автомобиль.VIN + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Пробег),"пробег: "+ Пробег + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Автомобиль.ГодВыпуска),"год выпуска: " + Формат(Автомобиль.ГодВыпуска,"ДФ=гггг") + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Автомобиль.НомерДвигателя),"номер двигателя: " + Автомобиль.НомерДвигателя + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(Автомобиль.НомерКузова),"номер кузова: " + Автомобиль.НомерКузова + ", ","");
		СтрАвто = СтрАвто + ?(ЗначениеЗаполнено(ПТС),ПредставлениеПТС + ", ","");
		СтрАвто = ?(Прав(СтрАвто,2) = ", ",Лев(СтрАвто,СтрДлина(СтрАвто)-2),СтрАвто); //удаление лишней запятой справа
		ОбластьМакета.Параметры.Автомобиль = СтрАвто;
		
		//заполнение параметров договора - различные сроки, суммы, проценты
		
		ЦенаАвтомобиля = Строка.СуммаВсего;
		ОбластьМакета.Параметры.ЦенаАвтомобиля = Формат(Окр(ЦенаАвтомобиля),"ЧДЦ=2; ЧН=0") + " " + ДокументОбъект.ВалютаДокумента + " ("  + обЧислоПрописью(ЦенаАвтомобиля,ДокументОбъект.ВалютаДокумента) + ")";
		
		Если ЗначениеЗаполнено(ДокументОбъект.ДоговорВзаиморасчетов) Тогда
			ПроцентВознаграждения = ДокументОбъект.ДоговорВзаиморасчетов.ПроцентКомиссионногоВознаграждения;
			СуммаВознаграждения = ДокументОбъект.ДоговорВзаиморасчетов.СуммаКомиссионногоВознаграждения; 
			Если ЗначениеЗаполнено(ПроцентВознаграждения) Тогда
				СуммаВознаграждения = ЦенаАвтомобиля*ПроцентВознаграждения/100;
			ИначеЕсли ЗначениеЗаполнено(СуммаВознаграждения) И ЗначениеЗаполнено(ЦенаАвтомобиля) Тогда
				ПроцентВознаграждения = СуммаВознаграждения/ЦенаАвтомобиля;
			КонецЕсли;
			ОбластьМакета.Параметры.РазмерВознаграждения = Формат(Окр(СуммаВознаграждения),"ЧДЦ=2; ЧН=0") + " " + ДокументОбъект.ВалютаДокумента + " ("  + обЧислоПрописью(СуммаВознаграждения,ДокументОбъект.ВалютаДокумента) + ")";
			ОбластьМакета.Параметры.ПроцентВознаграждения = Строка(Формат(ПроцентВознаграждения,"ЧЦ=8; ЧДЦ=4; ЧН=0")) + "% ";
			ОбластьМакета.Параметры.ПроцентОтПолученнойВыгоды = Строка(Формат(ПроцентВознаграждения,"ЧЦ=8; ЧДЦ=4; ЧН=0")) + "% ";
		КонецЕсли;
		
		СрокПриобретения = обПолучитьЗначениеСвойства(ДокументОбъект.Ссылка,ПланыВидовХарактеристик.СвойстваОбъектов.СрокПриобретенияАвтомобиляКомиссионером,0);
		ОбластьМакета.Параметры.СрокПриобретенияЗначение = Строка(СрокПриобретения) + " " +  "(" + СокрЛП(ЧислоПрописью(СрокПриобретения,"Л=ru_RU", " , , , , , , , , 0")) + ")"+ " " + обПолучитьПредставлениеДня(СрокПриобретения);
		
		СрокПередачи = обПолучитьЗначениеСвойства(ДокументОбъект.Ссылка,ПланыВидовХарактеристик.СвойстваОбъектов.СрокПередачиАвтомобиляКомитенту,0);
		ОбластьМакета.Параметры.СрокПередачиЗначение = Строка(СрокПередачи) + " " +  "(" + СокрЛП(ЧислоПрописью(СрокПередачи,"Л=ru_RU", " , , , , , , , , 0")) + ")" + " " + обПолучитьПредставлениеДня(СрокПередачи);
		
		СрокПеречисления = обПолучитьЗначениеСвойства(ДокументОбъект.Ссылка,ПланыВидовХарактеристик.СвойстваОбъектов.СрокПеречисленияВознагражденияКомиссионеру,0);
		ОбластьМакета.Параметры.СрокПеречисленияЗначение = Строка(СрокПеречисления) + " " +  "(" + СокрЛП(ЧислоПрописью(СрокПеречисления,"Л=ru_RU", " , , , , , , , , 0")) + ")" + " " + обПолучитьПредставлениеДня(СрокПеречисления);
		
		СуммаНеустойки = обПолучитьЗначениеСвойства(ДокументОбъект.Ссылка,ПланыВидовХарактеристик.СвойстваОбъектов.СуммаНеустойкиЗаДеньПросрочкиКомиссионером,0);
		ОбластьМакета.Параметры.СуммаНеустойкиЗаДеньПросрочкиКомиссионером = Строка(Формат(СуммаНеустойки,"ЧН=0")) + " руб. (" + обЧислоПрописью(СуммаНеустойки,Справочники.Валюты.НайтиПоКоду("643")) + ")";
		
		ПроцентНеустойки = обПолучитьЗначениеСвойства(ДокументОбъект.Ссылка,ПланыВидовХарактеристик.СвойстваОбъектов.ПроцентНеустойкиЗаПросрочкуКомитентом,0);
		ОбластьМакета.Параметры.ПроцентНеустойкиЗаПросрочкуКомитентом = Строка(ПроцентНеустойки) + "% " +  "(" + СокрЛП(ЧислоПрописью(ПроцентНеустойки,"Л=ru_RU", "процент,процента,процентов,м,,,,,0")) + ")";
		
		//заполнение параметров подвала договора
		
		СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		
		СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.ДляПечати = Истина;
		ОбластьМакета.Параметры.ОписаниеКомиссионера = спПолучитьПредставление(ДокументОбъект.Организация,
			СтруктураПредставления1, ДополнительныеПараметры);
		ОбластьМакета.Параметры.ОписаниеКомитента 	= спПолучитьПредставление(ДокументОбъект.Контрагент,
			СтруктураПредставления2,ДополнительныеПараметры);
		
		ТабДокумент.Вывести(ОбластьМакета);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	 
	 Возврат ТабДокумент;   	
 КонецФункции

// Формирует печатную форму "ТОРГ-1"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ1(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ1");
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий",
		"","ИНН ","КПП ","","тел.: "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий",
		"","ИНН ","КПП ","","тел.: "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления1);
	ОбластьМакета.Параметры.КодПоОКПО					= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	
	ОбластьМакета.Параметры.Номер			= НомерДляПечати;
	ОбластьМакета.Параметры.Дата			= Формат(ЭтотОбъект.Дата, "ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.МестоПриемки	= ЭтотОбъект.СкладКомпании;
	
	// Заполним информацию о руководителе организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Введение");
	
	ОбластьМакета.Параметры.Грузоотправитель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузоотправительПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Поставщик				= ЭтотОбъект.Контрагент;
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления1);
	
	Производитель = дкОтветственноеЛицо(ЭтотОбъект,"Производитель");
	ОбластьМакета.Параметры.Заполнить(Производитель);
	
	СтраховаяКомпания = дкОтветственноеЛицо(ЭтотОбъект,"СтраховаяКомпания");
	ОбластьМакета.Параметры.Заполнить(СтраховаяКомпания);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СопроводительныеДокументы"	, обПолучитьЗначениеСвойства(ЭтотОбъект,"СопроводительныеДокументы"));
	Параметры.Вставить("СпособДоставки"				, обПолучитьЗначениеСвойства(ЭтотОбъект,"СпособДоставки"));
	Параметры.Вставить("НомерТранспортногоСредства"	, обПолучитьЗначениеСвойства(ЭтотОбъект,"НомерТранспортногоСредства"));
	Параметры.Вставить("ДатаОтправления"			, обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтправления"));
	ОбластьМакета.Параметры.Заполнить(Параметры);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	//переменные управляющие постраничным выводом
	СтрокНаСтранице = 41;
	СтрокШапки      = 5;
	СтрокПодвала    = 3;
	НомерСтраницы   = 2;	
	
 	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	ОбластьСтрокаСертификата= Макет.ПолучитьОбласть("СтрокаСертификата");
	
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);

	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаРегл);

	КоличествоСтрок = ВыборкаТабличнойЧасти.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// инициализация итогов по странице
	ИтогоКоличествоНаСтранице	= 0;
	ИтогоСуммаНаСтранице		= 0;
	ИтогоНДСНаСтранице			= 0;
	ИтогоСНДСНаСтранице			= 0;
	ИтогоМассаБруттоНаСтранице  = 0;
	
	// инициализация итогов по документу
	ИтогоКоличество  = 0;
	ИтогоСумма       = 0;
	ИтогоНДС         = 0;
	ИтогоСНДС        = 0;
	Ном              = 0;
	ИтогоМассаБрутто = 0;

	//подготовим дополнительную таблицу
	ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
	ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	зфПерерасчетТаблицыТоваров(ТаблицаОпцийПоАвтомобилям,ЭтотОбъект,ВалютаРегл);
	
    // Выводим многострочную часть документа
	Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.Найти(СтрокаТоваров.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиОпции <> Неопределено Тогда
			СтрокаТоваров.Сумма		= СтрокаТоваров.Сумма	+ СтрокаТабличнойЧастиОпции.Сумма;
			СтрокаТоваров.СуммаНДС	= СтрокаТоваров.СуммаНДС		+ СтрокаТабличнойЧастиОпции.СуммаНДС;
			СтрокаТоваров.СуммаВсего	= СтрокаТоваров.СуммаВсего	+ СтрокаТабличнойЧастиОпции.СуммаВсего;
		КонецЕсли;
		
		Ном = Ном + 1;
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоги.Параметры.ПоставкаКоличествоПоСтранице  = ИтогоКоличествоНаСтранице;
			ОбластьИтоги.Параметры.ПоставкаМассаБруттоПоСтранице = ИтогоМассаБруттоНаСтранице;
			ОбластьИтоги.Параметры.ПоставкаСтоимостьПоСтранице   = ИтогоСуммаНаСтранице;
			ТабДокумент.Вывести(ОбластьИтоги);
			
			// очистим итоги по странице
			ИтогоКоличествоНаСтранице	= 0;
			ИтогоМассаБруттоНаСтранице	= 0;
			ИтогоСуммаНаСтранице		= 0;
			ИтогоНДСНаСтранице			= 0;
			ИтогоСуммаСНДСНаСтранице	= 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		КонецЕсли;

		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.Номенклатура				  = СтрокаТоваров.Автомобиль;
		ОбластьСтрока.Параметры.ТоварНаименование             = спПолучитьНаименование(СтрокаТоваров.Автомобиль);
		ОбластьСтрока.Параметры.ТоварКод                      = СтрокаТоваров.Автомобиль.VIN;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование  = "шт";
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКодПоОКЕИ     = 796; 
		
		ОбластьСтрока.Параметры.ПоставкаКоличествоВОдномМесте	= 1;
		ОбластьСтрока.Параметры.ПоставкаКоличествоМест			= 1;
		МассаБруттоСтр											= 0;
		ОбластьСтрока.Параметры.ПоставкаМассаБрутто				= МассаБруттоСтр;
		ОбластьСтрока.Параметры.ПоставкаСтоимость				= СтрокаТоваров.Сумма;
		ТабДокумент.Вывести(ОбластьСтрока);
		
		// увеличим итоги по странице
		ИтогоКоличествоНаСтранице  = ИтогоКоличествоНаСтранице  + 1;
		ИтогоМассаБруттоНаСтранице = ИтогоМассаБруттоНаСтранице + МассаБруттоСтр;
		ИтогоСуммаНаСтранице       = ИтогоСуммаНаСтранице       + СтрокаТоваров.Сумма;
		ИтогоНДСНаСтранице         = ИтогоНДСНаСтранице         + СтрокаТоваров.СуммаНДС;
		ИтогоСНДСНаСтранице        = ИтогоСНДСНаСтранице        + СтрокаТоваров.СуммаВсего;
		
		// увеличим итоги по странице
		ИтогоКоличество  = ИтогоКоличество  + 1;
		ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБруттоСтр;
		ИтогоСумма       = ИтогоСумма       + СтрокаТоваров.Сумма;
		ИтогоНДС         = ИтогоНДС         + СтрокаТоваров.СуммаНДС;
		ИтогоСНДС        = ИтогоСНДС        + СтрокаТоваров.СуммаВсего;
	КонецЦикла;
		
	// Выводим итоги по последней странице
	ОбластьИтоги.Параметры.ПоставкаКоличествоПоСтранице  = ИтогоКоличествоНаСтранице;
	ОбластьИтоги.Параметры.ПоставкаМассаБруттоПоСтранице = ИтогоМассаБруттоНаСтранице;
	ОбластьИтоги.Параметры.ПоставкаСтоимостьПоСтранице   = ИтогоСуммаНаСтранице;
	ТабДокумент.Вывести(ОбластьИтоги);
	
	// Выводим итоги по всему документу
	ОбластьВсего.Параметры.ПоставкаКоличество  = ИтогоКоличество;
	ОбластьВсего.Параметры.ПоставкаМассаБрутто = ИтогоМассаБрутто;
	ОбластьВсего.Параметры.ПоставкаСтоимость   = ИтогоСумма;
	ТабДокумент.Вывести(ОбластьВсего);
	
	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("ОсмотрТовара");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	НомерСтраницы = НомерСтраницы + 1;
	ОбластьШапкаТаблицы2 = Макет.ПолучитьОбласть("ЗаголовокТаблицы2");
	ОбластьШапкаТаблицы2.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	ОбластьШапкаТаблицы2.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьШапкаТаблицы2);
	
	ИтогоКоличествоНаСтранице	= 0;
	ИтогоСуммаНаСтранице		= 0;
	ИтогоНДСНаСтранице			= 0;
	ИтогоСНДСНаСтранице			= 0;
	ИтогоМассаБруттоНаСтранице 	= 0;
	
	ИтогоНДС  = 0;
	ИтогоСНДС = 0;
	
	ОбластьСтрока2 = Макет.ПолучитьОбласть("СтрокаТаблицы2");
	
	// Выводим многострочную часть документа
	Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
		
		Если НЕ (ТабДокумент.ПроверитьВывод(ОбластьСтрока)) Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("ИтогоПоСтранице2");
			ОбластьМакета.Параметры.СуммаНДСПоСтранице  = ИтогоНДСНаСтранице;
			ОбластьМакета.Параметры.СуммаСНДСПоСтранице = ИтогоСНДСНаСтранице;
			ТабДокумент.Вывести(ОбластьМакета);
			
			// Очистим итоги по странице
			ИтогоКоличествоНаСтранице	= 0;
			ИтогоМассаБруттоНаСтранице	= 0;
			ИтогоСуммаНаСтранице		= 0;
			ИтогоНДСНаСтранице			= 0;
			ИтогоСуммаСНДСНаСтранице	= 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьШапкаТаблицы2.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьШапкаТаблицы2);
		КонецЕсли;
		
		ОбластьСтрока2.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока2.Параметры.СуммаСНДС = СтрокаТоваров.СуммаВсего;
		ОбластьСтрока2.Параметры.СуммаНДС  = СтрокаТоваров.СуммаНДС;
		ТабДокумент.Вывести(ОбластьСтрока2);
		
		// Увеличим итоги по странице
		ИтогоНДСНаСтранице  = ИтогоНДСНаСтранице   + СтрокаТоваров.СуммаНДС;
		ИтогоСНДСНаСтранице = ИтогоСНДСНаСтранице  + СтрокаТоваров.СуммаВсего;
		
		// Увеличим итоги по странице
		ИтогоНДС = ИтогоНДС + СтрокаТоваров.СуммаНДС;
		ИтогоСНДС = ИтогоСНДС + СтрокаТоваров.СуммаВсего;
		
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоПоСтранице2");
	ОбластьМакета.Параметры.СуммаНДСПоСтранице  = ИтогоНДСНаСтранице;
	ОбластьМакета.Параметры.СуммаСНДСПоСтранице = ИтогоСНДСНаСтранице;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Всего2");
	ОбластьМакета.Параметры.СуммаНДС  = ИтогоНДС;
	ОбластьМакета.Параметры.СуммаСНДС = ИтогоСНДС;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	// Комиссия
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	
	ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	
	ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	
	// Бухгалтер
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	// Кладовщик
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Возврат ТабДокумент;
КонецФункции

 //Формирует печатную форму "ТОРГ-2"
 //    
 //    Параметры:
 //     ТабДокумент - табличный документ.
 //   
 //    Возвращаемое значение:
 //     ТабДокумент - возвращает сформированный табличный документ.
	
Функция ПечатьТОРГ2(ТабДокумент) Экспорт
		Перем тзВрем; // Временная таблица значений
		Перем ФорматВыводаСуммы; // Строка формата вывода суммы
		Перем ФорматВыводаКоличества; // Строка формата вывода количества
		
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		
		ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы",     Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
		
		// Получим табличную часть за исключением услуг
		тзВрем = Автомобили.Выгрузить();
		зфПерерасчетТаблицыТоваров(тзВрем, ЭтотОбъект, ВалютаРегл);
			мсвУдалить = Новый Массив;
				
		Макет = ПолучитьОбщийМакет("ТОРГ2");
		
		ОбластьСтраница1 = Макет.ПолучитьОбласть("ШапкаСтраница1");
		ОбластьСтраница1.Параметры.Заполнить(ЭтотОбъект);
		
		Параметры = Новый Структура;
		
		СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий",
		"","ИНН ","КПП ","","тел.: "
		);
		СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий",
		"","ИНН ","КПП ","","тел.: "
		);
		
		// Правим, что автоматом не заполнилось, или заполнилось неправильно
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		Параметры.Вставить("ОрганизацияПредставление",
			спПолучитьПредставление(Организация, СтруктураПредставления1, ДополнительныеПараметры));
		Параметры.Вставить("КодПоОКПО",				 Организация.КодПоОКПО);
		Параметры.Вставить("ВидДеятельностиПоОКДП",	 Организация.КодПоОКДП);
		
		Параметры.Вставить("ПредставлениеДокумента", дкПолучитьПредставление(Ссылка));
		Параметры.Вставить("Номер", дкПолучитьНомерДляПечати(ЭтотОбъект));
		Параметры.Вставить("Дата",	 Дата);
		
		Параметры.Вставить("МестоПриемки",	                 СкладКомпании);
		Параметры.Вставить("ГрузоотправительПредставление",	 спПолучитьПредставление(обПолучитьЗначениеСвойства(,"Грузоотправитель",Контрагент),СтруктураПредставления2));
		Параметры.Вставить("ПоставщикПредставление",	     спПолучитьПредставление(Контрагент,СтруктураПредставления1));
		Параметры.Вставить("ПроизводительПредставление",	 дкОтветственноеЛицо(ЭтотОбъект, "Производитель").ПроизводительПредставление);
		Параметры.Вставить("СтраховаяКомпанияПредставление", дкОтветственноеЛицо(ЭтотОбъект, "СтраховаяКомпания").СтраховаяКомпанияПредставление);
		Параметры.Вставить("СпособДоставкиПредставление",    обПолучитьЗначениеСвойства(ЭтотОбъект, ПланыВидовХарактеристик.СвойстваОбъектов.СпособДоставки, ""));
		
		ДатаОтправления = обПолучитьЗначениеСвойства(ЭтотОбъект, ПланыВидовХарактеристик.СвойстваОбъектов.ДатаОтправления, "");
		Если ДатаОтправления<>"" Тогда
			ДатаОтправления = "Дата отправления товара: " + Формат(ДатаОтправления, "ДЛФ=DD");
		Иначе
			ДатаОтправления = "Дата отправления товара " + " ""_______""  _____________________ _______ г.";
		КонецЕсли;
		
		Параметры.Вставить("ДатаОтправления", ДатаОтправления);
		
		Параметры.Вставить("НомерДоговора",	СокрЛП(ДоговорВзаиморасчетов.НомерДоговора));
		Параметры.Вставить("ДатаДоговора",  Формат(ДоговорВзаиморасчетов.ДатаНачала, "ДЛФ=DD"));
		
		// Счет-фактура, если есть.
		МассивВидов=Новый Массив(1); МассивВидов[0]="СчетФактураПолученный"; 
		МассивПодчиненных = дкПолучитьМассивПодчиненных(Ссылка, МассивВидов);
		Если МассивПодчиненных.Количество()>0 Тогда
			// нашли
			СФ=МассивПодчиненных[0].Ссылка;
			Попытка
				НомерСФ = дкПолучитьНомерДляПечати(СФ);
				ДатаСФ  = Формат(СФ.Дата, "ДЛФ=DD");
			Исключение
				НомерСФ = "";
				ДатаСФ  = """" + "_______"  + """" + "  __________________________  " + "__________ г.";
			КонецПопытки; 
		Иначе
			НомерСФ = "";
			ДатаСФ  = """" + "_______"  + """" + "  __________________________  " + "__________ г.";
		КонецЕсли; 
		
		Параметры.Вставить("НомерСФ", НомерСФ);
		Параметры.Вставить("ДатаСФ",  ДатаСФ);
		
		ОбластьСтраница1.Параметры.Заполнить(Параметры);
		
		ТабДокумент.Вывести(ОбластьСтраница1);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// ВТОРАЯ СТРАНИЦА
		НомерСтраницы     = 3;
		НомерСтраницыПред = НомерСтраницы;
		
		ОбластьНомерСтроки = Макет.ПолучитьОбласть("НомерСтраницы");
		ОбластьНомерСтроки.Параметры.НомерСтраницы = "Страница: " + 2; 
		ТабДокумент.Вывести(ОбластьНомерСтроки);
		
		ОбластьСтраница2   = Макет.ПолучитьОбласть("ШапкаСтраница2"); 
		ОбластьСтраница2.Параметры.КоличествоМестПоДокументу = Формат(тзВрем.Итог("Количество"), ФорматВыводаКоличества);
    
		ТабДокумент.Вывести(ОбластьСтраница2);
		
		// ВЫВОДИМ ТАБЛИЧНУЮ ЧАСТЬ
		ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТабличнойЧасти"); 
		ОбластьШапкаТЧ.Параметры.Валюта = ВалютаРегл;
		ТабДокумент.Вывести(ОбластьШапкаТЧ);
		// Для последующего вывода
		ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		
		// получаем макет строки
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка1");
		
		//перебор строк
		ВыборкаТабличнойЧасти = АВтомобили;
		Для каждого СтрокаТабличнойЧасти Из тзВрем Цикл
			
			ОбластьСтрока.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
			ОбластьСтрока.Параметры.ТоварНаименование = спПолучитьНаименование(СтрокаТабличнойЧасти.Автомобиль);
			ОбластьСтрока.Параметры.Артикул          = "" + СтрокаТабличнойЧасти.VIN;
			ОбластьСтрока.Параметры.ЕдиницаИзмерения = "шт";
			ОбластьСтрока.Параметры.КодЕдиницыПоОКЕИ = "796";

			// Рассчитаем цену.
			
			ОбластьСтрока.Параметры.Цена = Формат(СтрокаТабличнойЧасти.СуммаВсего / СтрокаТабличнойЧасти.Количество, ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.Количество = Формат (СтрокаТабличнойЧасти.Количество, ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.СуммаВсего = Формат(СтрокаТабличнойЧасти.СуммаВсего, ФорматВыводаСуммы);
					
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТЧ, , НомерСтраницы, ,ЭтотОбъект);  		
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли; 		
		КонецЦикла;
		
		
		// ВЫВОДИМ 3-Ю СТРАНИЦУ
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();	
		ОбластьНомерСтроки = Макет.ПолучитьОбласть("НомерСтраницы");
		ОбластьНомерСтроки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ТабДокумент.Вывести(ОбластьНомерСтроки);
		
		ОбластьСтраница3   = Макет.ПолучитьОбласть("ШапкаСтраница3"); 	
		ТабДокумент.Вывести(ОбластьСтраница3);
		
		НомерСтраницы = НомерСтраницы + 1;
		
		// ВЫВОДИМ ПРОДОЛЖЕНИЕ ТАБЛИЧНОЙ ЧАСТИ
		ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТабличнойЧасти2"); 
		ОбластьШапкаТЧ.Параметры.Валюта = ВалютаРегл;
		ТабДокумент.Вывести(ОбластьШапкаТЧ);
		// Для последующего вывода
		ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		
		// получаем макет строки
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка2");
		
		//перебор строк
		Для каждого СтрокаТабличнойЧасти Из тзВрем Цикл
			
		    ОбластьСтрока.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
			ОбластьСтрока.Параметры.Артикул = "" + СтрокаТабличнойЧасти.VIN;
			
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТЧ, , НомерСтраницы, ,ЭтотОбъект);  		
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли; 		
		КонецЦикла;
		
		
		// ВЫВОДИМ 4-Ю СТРАНИЦУ
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ОбластьНомерСтроки = Макет.ПолучитьОбласть("НомерСтраницы");
		ОбластьНомерСтроки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ТабДокумент.Вывести(ОбластьНомерСтроки);
		
		ОбластьСтраница4 = Макет.ПолучитьОбласть("ШапкаСтраница4");
		
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,"ГлавныйБухгалтер");
		ОбластьСтраница4.Параметры.Заполнить(ГлавныйБухгалтер);
		
		// Комиссия
		ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
		ОбластьСтраница4.Параметры.Заполнить(ПредседательКомиссии);
		
		ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
		ОбластьСтраница4.Параметры.Заполнить(ЧленКомиссии1);
		
		ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
		ОбластьСтраница4.Параметры.Заполнить(ЧленКомиссии2);
		
		ТабДокумент.Вывести(ОбластьСтраница4); 
		
		
		// Зададим параметры макета
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 15;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		Возврат ТабДокумент;	
	КонецФункции // ПечатьТОРГ2()
//	
// Формирует печатную форму "ТОРГ-4"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ4(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ4");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	ОбластьМакета.Параметры.ОрганизацияПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления1);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО 				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ПодразделениеКомпании			= ЭтотОбъект.ПодразделениеКомпании;
	ОбластьМакета.Параметры.ПоставщикПредставление			= спПолучитьПредставление(ЭтотОбъект.Контрагент,СтруктураПредставления1);
	ОбластьМакета.Параметры.Поставщик						= ЭтотОбъект.Контрагент;
	
	ОбластьМакета.Параметры.Грузоотправитель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузоотправительПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Номер			= НомерДляПечати;
	ОбластьМакета.Параметры.Дата			= Формат(ЭтотОбъект.Дата, "ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.МестоПриемки	= ЭтотОбъект.СкладКомпании;
	
	// Заполним информацию о руководителе организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	//переменные управляющие постраничным выводом
	СтрокНаСтранице = 23;
	СтрокШапки      = 20;
	СтрокПодвала    = 7;
	НомерСтраницы   = 1;
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги            = Макет.ПолучитьОбласть("ИтогиПоСтранице");
	ОбластьВсего            = Макет.ПолучитьОбласть("Всего");
	
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);

	ВыборкаСтрок = ЭтотОбъект.Автомобили.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаСтрок, ЭтотОбъект, ВалютаРегл);
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// инициализация итогов по странице
	ИтогоМассаБруттоПоСтранице = 0;
	ИтогоКоличествоПоСтранице  = 0;
	ИтогоСуммаПоСтранице       = 0;
	
	// инициализация итогов по документу
	ИтогоМассаБрутто = 0;
	ИтогоКоличество  = 0;
	ИтогоСумма       = 0;
	Ном              = 0;
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	ФорматВыводаСуммы = ОбПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);

	//подготовим дополнительную таблицу
	ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
	ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	зфПерерасчетТаблицыТоваров(ТаблицаОпцийПоАвтомобилям, ЭтотОбъект, ВалютаРегл);
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.Найти(СтрокаТоваров.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиОпции <> Неопределено Тогда
			СтрокаТоваров.Сумма		= СтрокаТоваров.Сумма	+ СтрокаТабличнойЧастиОпции.Сумма;
			СтрокаТоваров.СуммаНДС	= СтрокаТоваров.СуммаНДС		+ СтрокаТабличнойЧастиОпции.СуммаНДС;
			СтрокаТоваров.СуммаВсего	= СтрокаТоваров.СуммаВсего	+ СтрокаТабличнойЧастиОпции.СуммаВсего;
		КонецЕсли;
		
		Ном = Ном + 1;
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоги.Параметры.ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице;
			ОбластьИтоги.Параметры.ИтогоШтукПоСтранице        = ИтогоКоличествоПоСтранице;
			ОбластьИтоги.Параметры.ИтогоСтоимостьПоСтранице   = ИтогоСуммаПоСтранице;
			ТабДокумент.Вывести(ОбластьИтоги);
			
			// очистим итоги по странице
			ИтогоБруттоПоСтранице     = 0;
			ИтогоКоличествоПоСтранице = 0;
			ИтогоСуммаПоСтранице      = 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.Номенклатура 					= СтрокаТоваров.Автомобиль;
		ОбластьСтрока.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТоваров.Автомобиль);
		ОбластьСтрока.Параметры.ТоварКод						= СтрокаТоваров.Автомобиль.VIN;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование	= "шт";
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКодПоОКЕИ		= 796;
		
		МассаБруттоСтр											= 0;
		ОбластьСтрока.Параметры.МассаБрутто						= МассаБруттоСтр;
		ОбластьСтрока.Параметры.КоличествоМест					= 1;
		ОбластьСтрока.Параметры.КоличествоВОдномМесте			= 1;
		ОбластьСтрока.Параметры.КоличествоШтук					= 1;
		ОбластьСтрока.Параметры.Цена							= Формат(СтрокаТоваров.Сумма,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.Стоимость						= ОбластьСтрока.Параметры.Цена;
		ТабДокумент.Вывести(ОбластьСтрока);
		
		// увеличим итоги по странице
		ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице + МассаБруттоСтр;
		ИтогоКоличествоПоСтранице  = ИтогоКоличествоПоСтранице  + 1;
		ИтогоСуммаПоСтранице       = ИтогоСуммаПоСтранице       + СтрокаТоваров.Сумма;
		
		// увеличим итоги по документу
		ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБруттоСтр;
		ИтогоКоличество  = ИтогоКоличество  + 1;
		ИтогоСумма       = ИтогоСумма       + СтрокаТоваров.Сумма;
	КонецЦикла;
	
	// Выводим итоги по последней странице
	ОбластьИтоги.Параметры.ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице;
	ОбластьИтоги.Параметры.ИтогоШтукПоСтранице        = ИтогоКоличествоПоСтранице;
	ОбластьИтоги.Параметры.ИтогоСтоимостьПоСтранице   = ИтогоСуммаПоСтранице;
	ТабДокумент.Вывести(ОбластьИтоги);
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогоМассаБрутто = ИтогоМассаБрутто;
	ОбластьВсего.Параметры.ИтогоШтук        = ИтогоКоличество;
	ОбластьВсего.Параметры.ИтогоСтоимость   = ИтогоСумма;
	ТабДокумент.Вывести(ОбластьВсего);

	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	// Комиссия
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	
	ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	
	ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	
	// Кладовщик
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ-12"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);	
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ЭтотОбъект.Контрагент,СтруктураПредставления1);
	ОбластьМакета.Параметры.ПодразделениеКомпании	= Неопределено;
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикКонтрагент",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.ПодразделениеКомпании);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ГрузополучательПредставление = спПолучитьПредставление(
		ОбластьМакета.Параметры.Грузополучатель, СтруктураПредставления2, ДополнительныеПараметры);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления2);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Контрагент.КодПоОКПО;
	Если ТипЗнч(ОбластьМакета.Параметры.Грузополучатель)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	КонецЕсли;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	Если ТипЗнч(ОбластьМакета.Параметры.Плательщик)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = "";
	ОбластьМакета.Параметры.Номер = ?(обЗначениеНеЗаполнено(ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект), ВхДокНомер);
	ОбластьМакета.Параметры.Дата  = ?(обЗначениеНеЗаполнено(ВхДокНомер),Дата, ВхДокДата);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	// в новой ПФ не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	
	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
	
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ВыборкаСтрок = ЭтотОбъект.Автомобили.Выгрузить();
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	
	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	ФорматВыводаСуммы = ОбПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	//подготовим дополнительную таблицу
	ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
	ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля,СтавкаНДС","Сумма,СуммаНДС,СуммаВсего");
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		СтруктураПоиска = Новый Структура("ИдентификаторАвтомобиля",СтрокаТоваров.ИдентификаторАвтомобиля);
		
		СуммаНДС=СтрокаТоваров.СуммаНДС;
		
		//предварительная обработка строки ТЧ
		МассивТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.НайтиСтроки(СтруктураПоиска);
		Для Каждого СтрокаТабличнойЧастиОпции Из МассивТабличнойЧастиОпции Цикл
			СтрокаТоваров.Сумма		= СтрокаТоваров.Сумма		+ СтрокаТабличнойЧастиОпции.Сумма;
			СуммаНДС				= СуммаНДС					+ СтрокаТабличнойЧастиОпции.СуммаНДС;
			СтрокаТоваров.СуммаВсего= СтрокаТоваров.СуммаВсего	+ СтрокаТабличнойЧастиОпции.СуммаВсего;
			// итоги по ставкам НДС
			Если НЕ обЗначениеНеЗаполнено(СтрокаТабличнойЧастиОпции.СтавкаНДС) Тогда
				Если СоответствиеИтоговПоставкамНДС[СтрокаТабличнойЧастиОпции.СтавкаНДС]=Неопределено Тогда
					СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТабличнойЧастиОпции.СтавкаНДС,0);
				КонецЕсли;
				СоответствиеИтоговПоставкамНДС[СтрокаТабличнойЧастиОпции.СтавкаНДС]=СоответствиеИтоговПоставкамНДС[СтрокаТабличнойЧастиОпции.СтавкаНДС]+СтрокаТабличнойЧастиОпции.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код = СтрокаТоваров.Автомобиль.VIN;
		ОбластьСтрока.Параметры.ТоварНаименование 			= спПолучитьНаименование(СтрокаТоваров.Автомобиль);
		ОбластьСтрока.Параметры.Номенклатура 				= СтрокаТоваров.Автомобиль;
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= "шт";
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= 796;
		ОбластьСтрока.Параметры.Количество					= 1;
		ОбластьСтрока.Параметры.КоличествоМест				= 1;
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= 1;
		
		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = СуммаВсего;
		ОбластьСтрока.Параметры.СуммаНДС    = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", СуммаНДС);
		ОбластьСтрока.Параметры.СуммаБезНДС = СуммаБезНДС;
		ОбластьСтрока.Параметры.ЦенаБезНДС = СуммаБезНДС;
		
		//выводим строку, делая проверку попадания на лист		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице	= 1;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		// итоги по документу
		ИтогоКоличество = ИтогоКоличество+ 1;
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = ИтогоКоличество;
	ОбластьВсего.Параметры.ИтогСуммы      = ИтогоСумма;
	ОбластьВсего.Параметры.ИтогНДС        = ИтогоНДС;
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = ИтогоСуммаСНДС;
	ТабДокумент.Вывести(ОбластьВсего);
	
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "+дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьВсегоСтавкаНДС, , , НомерСтраницы,,ЭтотОбъект);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьМакета.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьМакета.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьМакета.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьМакета.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьМакета.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьМакета.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);

	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ13"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ13(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ13");
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ОтправительПодразделение	= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПолучательПодразделение		= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.Номер						= дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.Дата						= Дата;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	СтрокНаСтранице = 25;
	СтрокШапки      = 12;
	СтрокПодвала    = 5;
	НомерСтраницы   = 1;

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Автомобили.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаСтрокТовары, ЭтотОбъект, ВалютаРегл);
	
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();
    Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку	= 0;
	Иначе
		ЦелыхСтраницСПодвалом		= Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала		= Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку	= ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// инициализация итогов по странице
	ИтогКоличествоМестПоСтранице = 0;
	ИтогМассаБруттоПоСтранице    = 0;
	ИтогМассыНеттоПоСтранице     = 0;
	ИтогСуммыПоСтранице          = 0;

	// инициализация итогов по документу
	ИтогоКоличество  = 0;
	ИтогоМассаБрутто = 0;
	ИтогоМассаНетто  = 0;
	ИтогоСумма       = 0;
    Ном = 0;

	ФорматВыводаСуммы = ОбПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект); 
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	// Выводим многострочную часть документа
	ОбластьИтоговПоСтранице	= Макет.ПолучитьОбласть("ИтогиПоСтранице");
	ОбластьМакета			= Макет.ПолучитьОбласть("Строка");
	
	//подготовим дополнительную таблицу
	ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
	ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	зфПерерасчетТаблицыТоваров(ТаблицаОпцийПоАвтомобилям, ЭтотОбъект, ВалютаРегл);
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.Найти(СтрокаТовар.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиОпции <> Неопределено Тогда
			СтрокаТовар.Сумма		= СтрокаТовар.Сумма	+ СтрокаТабличнойЧастиОпции.Сумма;
			СтрокаТовар.СуммаНДС	= СтрокаТовар.СуммаНДС		+ СтрокаТабличнойЧастиОпции.СуммаНДС;
			СтрокаТовар.СуммаВсего	= СтрокаТовар.СуммаВсего	+ СтрокаТабличнойЧастиОпции.СуммаВсего;
		КонецЕсли;
		
		Ном = Ном + 1;
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			// инициализация итогов по странице
			ИтогКоличествоМестПоСтранице = 0;
			ИтогМассаБруттоПоСтранице    = 0;
			ИтогМассаНеттоПоСтранице     = 0;
			ИтогСуммыПоСтранице          = 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
        ОбластьМакета.Параметры.Номенклатура				= СтрокаТовар.Автомобиль;
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(СтрокаТовар.Автомобиль);
		ОбластьМакета.Параметры.ТоварКод					= СтрокаТовар.Автомобиль.VIN;
		ОбластьМакета.Параметры.ЕдиницаИзмерения			= "шт";
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ	= 796;
		
		ОбластьМакета.Параметры.КоличествоВОдномМесте		= 1;
		ОбластьМакета.Параметры.КоличествоМест				= 1;
		МассаБруттоСтр										= 0;
		ОбластьМакета.Параметры.МассаБрутто					= МассаБруттоСтр;
		Сумма												= СтрокаТовар.СуммаВсего - СтрокаТовар.СуммаНДС;
		ОбластьМакета.Параметры.Цена						= Формат(Сумма,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Сумма						= ОбластьМакета.Параметры.Цена;
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Обновим итоги по странице
		ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + 1;
		ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице    + МассаБруттоСтр;
		ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице     + 0;
		ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице          + Сумма;
		
		// Обновим итоги по документу
		ИтогоКоличество  = ИтогоКоличество  + 1;
		ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБруттоСтр;
		ИтогоМассаНетто  = ИтогоМассаНетто  + 0;
		ИтогоСумма       = ИтогоСумма       + Сумма;
  	КонецЦикла;

	ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
    ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим итоги по документу в целом
	ОбластьМакета = Макет.ПолучитьОбласть("Всего");
	ОбластьМакета.Параметры.ИтогоКоличествоМест = ИтогоКоличество;
	ОбластьМакета.Параметры.ИтогоМассаБрутто    = ИтогоМассаБрутто;
	ОбластьМакета.Параметры.ИтогоМассаНетто     = ИтогоМассаНетто;
	ОбластьМакета.Параметры.ИтогоСумма          = ИтогоСумма;
    ТабДокумент.Вывести(ОбластьМакета);

	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ИтогоСуммаПрописью = обЧислоПрописью(ИтогоСумма,ВалютаРегл);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "М-4"
// Возвращает сформированный табличный документ
Функция ПечатьМ4(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("М4");
	
	// выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО		= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.Номер					= дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	// выводим заголовок документа
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ДатаСоставления			= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.МестоПриемки			= СкладКомпании;
	ОбластьМакета.Параметры.ПоставщикНаименование	= спПолучитьНаименование(Контрагент);
	ОбластьМакета.Параметры.Поставщик				= Контрагент;
	ОбластьМакета.Параметры.ПоставщикКод			= Контрагент.Код;
	СтраховаяКомпания = дкОтветственноеЛицо(ЭтотОбъект,"СтраховаяКомпания");
	ОбластьМакета.Параметры.Заполнить(СтраховаяКомпания);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//переменные управляющие постраничным выводом
	СтрокНаСтранице = 24;
	СтрокШапки      = 10;
	СтрокПодвала    = 6;
	НомерСтраницы   = 1;		
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
	ОбластьИтогиПоСтранице  = Макет.ПолучитьОбласть("Итого");
	
	// выводим заголовок таблицы
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	ВыборкаСтрок = ЭтотОбъект.Автомобили.Выгрузить();
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	                                                             
	// инициализация итогов по странице
	ИтогоКоличествоПринятоПоСтранице = 0;
	ИтогоСуммаБезНДСПоСтранице       = 0;
	ИтогоСуммаНДСПоСтранице 		 = 0;
	ИтогоВсегоСНДСПоСтранице		 = 0;
	Ном 							 = 0;
	
	// проверим, входит ли НДС в стоимость	
	ЦенаВключаетНДС = ТипЦен.ЦенаВключаетНДС;
	
	//подготовим дополнительную таблицу
	ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
	ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТабличнойЧасти Из ВыборкаСтрок Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.Найти(СтрокаТабличнойЧасти.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиОпции <> Неопределено Тогда
			СтрокаТабличнойЧасти.Сумма		= СтрокаТабличнойЧасти.Сумма	+ СтрокаТабличнойЧастиОпции.Сумма;
			СтрокаТабличнойЧасти.СуммаНДС	= СтрокаТабличнойЧасти.СуммаНДС		+ СтрокаТабличнойЧастиОпции.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаВсего	= СтрокаТабличнойЧасти.СуммаВсего	+ СтрокаТабличнойЧастиОпции.СуммаВсего;
		КонецЕсли;
		
		Ном = Ном + 1;
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтогиПоСтранице.Параметры.ИтогоКоличествоПринято = ИтогоКоличествоПринятоПоСтранице;
			ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаБезНДС = ИтогоСуммаБезНДСПоСтранице;
			ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаНДС = ИтогоСуммаНДСПоСтранице;
			ОбластьИтогиПоСтранице.Параметры.ИтогоВсегоСНДС = ИтогоВсегоСНДСПоСтранице;
			ТабДокумент.Вывести(ОбластьИтогиПоСтранице);

			// очистим итоги по странице
			ИтогоКоличествоПринятоПоСтранице = 0;
			ИтогоСуммаБезНДСПоСтранице       = 0;
			ИтогоСуммаНДСПоСтранице 		 = 0;
			ИтогоВсегоСНДСПоСтранице		 = 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		КонецЕсли;

		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьСтрока.Параметры.Номенклатура					= СтрокаТабличнойЧасти.Автомобиль; 
		ОбластьСтрока.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТабличнойЧасти.Автомобиль);
		ОбластьСтрока.Параметры.ТоварКод						= СтрокаТабличнойЧасти.Автомобиль.VIN;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод				= 796;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование	= "шт";
		ОбластьСтрока.Параметры.КоличествоПринято				= 1;
		Если ЦенаВключаетНДС = 1 Тогда
			ОбластьСтрока.Параметры.Цена		= СтрокаТабличнойЧасти.Сумма - ОбластьСтрока.Параметры.СуммаНДС;
			ОбластьСтрока.Параметры.СуммаБезНДС	= СтрокаТабличнойЧасти.Сумма - ОбластьСтрока.Параметры.СуммаНДС;
			ИтогоСуммаБезНДСПоСтранице			= ИтогоСуммаБезНДСПоСтранице + СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаНДС;
		Иначе
			ОбластьСтрока.Параметры.Цена		= СтрокаТабличнойЧасти.Сумма;
			ОбластьСтрока.Параметры.СуммаБезНДС	= СтрокаТабличнойЧасти.Сумма;
			ИтогоСуммаБезНДСПоСтранице			= ИтогоСуммаБезНДСПоСтранице + СтрокаТабличнойЧасти.Сумма;
		КонецЕсли;		
		ОбластьСтрока.Параметры.ВсегоСНДС		= СтрокаТабличнойЧасти.СуммаВсего;
		ТабДокумент.Вывести(ОбластьСтрока);
		
		// увеличим итоги по странице
		ИтогоКоличествоПринятоПоСтранице = ИтогоКоличествоПринятоПоСтранице + 1;
		ИтогоСуммаНДСПоСтранице			 = ИтогоСуммаНДСПоСтранице + СтрокаТабличнойЧасти.СуммаНДС;
		ИтогоВсегоСНДСПоСтранице		 = ИтогоВсегоСНДСПоСтранице + СтрокаТабличнойЧасти.СуммаВсего;
	КонецЦикла;
		
	// выводим итоги по последней странице
	ОбластьИтогиПоСтранице.Параметры.ИтогоКоличествоПринято = ИтогоКоличествоПринятоПоСтранице;
	ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаБезНДС = ИтогоСуммаБезНДСПоСтранице;
	ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаНДС = ИтогоСуммаНДСПоСтранице;
	ОбластьИтогиПоСтранице.Параметры.ИтогоВсегоСНДС = ИтогоВсегоСНДСПоСтранице;
	ТабДокумент.Вывести(ОбластьИтогиПоСтранице);
	
	// выводим подвал
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции

//Функция печати Акт о преме-передачи ТМЦ
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьМХ1(ТабДокумент) Экспорт
	Если ХозОперация = Справочники.ХозОперации.ПоступлениеАвтомобилейОтветственноеХранение Тогда
		Макет = ПолучитьОбщийМакет("МХ1");
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.НомерДокумента                   = Номер;
		ОбластьЗаголовок.Параметры.ДатаДокумента                    = Дата;
		ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО                = Организация.КодПоОКПО;
		ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП = Организация.КодПоОКДП;
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			ОбластьЗаголовок.Параметры.ПредставлениеВладельца = спПолучитьПредставление(Контрагент);
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = Контрагент.КодПоОКПО;
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьЗаголовок);
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ТабДокумент.Вывести(ОбластьШапка);
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ВыборкаСтрок = Автомобили.Выгрузить();
		НомерСтроки = 1;
		
		//подготовим дополнительную таблицу
		ТаблицаОпцийПоАвтомобилям = ЭтотОбъект.Опции.Выгрузить();
		ТаблицаОпцийПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
		Для Каждого СтрокаТоваров Из ВыборкаСтрок Цикл
			ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
			ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Автомобиль);
			СтрокаТабличнойЧастиОпции = ТаблицаОпцийПоАвтомобилям.Найти(СтрокаТоваров.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
			Если СтрокаТабличнойЧастиОпции <> Неопределено Тогда
				СтрокаТоваров.Сумма = СтрокаТоваров.Сумма + СтрокаТабличнойЧастиОпции.Сумма;
				СтрокаТоваров.СуммаВсего = СтрокаТоваров.СуммаВсего + СтрокаТабличнойЧастиОпции.СуммаВсего;
			КонецЕсли;
			ВалютаРегУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			Если Не ВалютаДокумента = ВалютаРегУчета Тогда
				СтрокаТоваров.Сумма = обПересчет(СтрокаТоваров.Сумма,ВалютаДокумента,Дата,ВалютаРегУчета,Дата);
				СтрокаТоваров.СуммаВсего = обПересчет(СтрокаТоваров.СуммаВсего,ВалютаДокумента,Дата,ВалютаРегУчета,Дата);
			КонецЕсли;
			ОбластьСтрока.Параметры.Цена = СтрокаТоваров.Сумма;
			ОбластьСтрока.Параметры.Стоимость = СтрокаТоваров.Сумма;
			ОбластьСтрока.Параметры.Количество =  Формат(1,ФорматВыводаКоличества);
			ТабДокумент.Вывести(ОбластьСтрока);
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		ОбластьИтого = Макет.ПолучитьОбласть("Итого");
		ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"),ФорматВыводаКоличества);
		ОбластьИтого.Параметры.ИтогЦена = ВыборкаСтрок.Итог("Сумма");
		ОбластьИтого.Параметры.ИтогСтоимость = ВыборкаСтрок.Итог("Сумма");
		ТабДокумент.Вывести(ОбластьИтого);
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ТабДокумент.Вывести(ОбластьПодвал);
		Возврат ТабДокумент;
	Иначе
		Сообщить("Печать акта о приеме-передаче товарно-материальных ценностей на хранение возможна только для документов с хозяйственной операцией ""Поступление автомобилей на ответственное хранение""");
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ПечатьМХ1(ТабДокумент)

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПоступлениеАвтомобилей") И
		ДокументОснование.ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейОтветственноеХранение Тогда
		
		ХозОперация=Справочники.ХозОперации.ПереходВСобственностьАвтомобилей;
		
		//найдем документ корректировку поступления автомобилей
		ДокументКорректировки = Документы.КорректировкаПоступленияАвтомобилей.НайтиПоРеквизиту("ДокументОснование", Основание);
		Если ДокументКорректировки <> Неопределено Тогда
			
			Для Каждого ТекСтрока Из ДокументКорректировки.Автомобили Цикл
				СтрокаАвтомобиля = Автомобили.Найти(ТекСтрока.Автомобиль, "Автомобиль");
				Если СтрокаАвтомобиля <> Неопределено Тогда
					СтрокаАвтомобиля.Цена = ТекСтрока.Цена;
					СтрокаАвтомобиля.Сумма = ТекСтрока.Сумма;
					СтрокаАвтомобиля.СуммаВсего = ТекСтрока.СуммаВсего;
					СтрокаАвтомобиля.СтавкаНДС = ТекСтрока.СтавкаНДС;
					СтрокаАвтомобиля.СуммаНДС = ТекСтрока.СуммаНДС;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		// проверим всели заказы досихпор используются
		ПерезаполнитьЗаказы();
	КонецЕсли; 
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;   
	
	//<<чАЛАПаю БизТех 18.09.2024
	Если ЭтоНовый() тогда 
		БТ_НомерСЧФ = "";
		БТ_ДатаСЧФ = Дата(1,1,1); 
		БТ_СтатусСЧФ = "";
		БТ_СуммаНДС = 0;  
	КонецЕсли;
	//чАЛАПаю БизТех 18.09.2024>>

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования)
		
		Тогда Возврат;
	КонецЕсли;
	
	ВхДокНомер = "";
	#Если Клиент Тогда
	ВхДокДата = РабочаяДата;
	#Иначе
	ВхДокДата = ТекущаяДата();
#КонецЕсли       

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("АвтомобильЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 	
	
	//Проверим расстановку флажков, для поступления автомобилей без заказа
	Для Каждого СтрокаАвтомобиля Из Автомобили Цикл
		Если обЗначениеНеЗаполнено(СтрокаАвтомобиля.ЗаказНаАвтомобиль) И СтрокаАвтомобиля.АвтомобильБезЗаказа=Ложь Тогда
			Сообщить("Автомобиль <"+СокрЛП(СтрокаАвтомобиля.Автомобиль)+"> поступает без заказа. Установите признак поступления автомобиля без заказа.");
			Отказ=Истина;
		КонецЕсли; 
	КонецЦикла; 
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Автомобиль КАК Автомобиль
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Автомобиль КАК Автомобиль
		|	ИЗ
		|		РегистрНакопления.ЗаказыАвтомобилей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыПоставщикам.Автомобиль
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикамНаАвтомобили КАК ЗаказыПоставщикам
		|	ГДЕ
		|		ЗаказыПоставщикам.Регистратор = &Ссылка
		|	) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	//<<чАЛАПаю БизТех 18.09.2024
	Если ЭтоНовый()и не отказ тогда 
		Если Автомобили.найти(Справочники.СтавкиНДС.ОсновнаяСтавкаНДС,"СтавкаНДС")= Неопределено тогда    //без ндс
			БТ_СтатусСЧФ = Строка("Не требуется"); 
			БТ_НомерСЧФ = "";     
			БТ_ДатаСЧФ = Дата(1,1,1);
		иначе
			БТ_СтатусСЧФ= Строка("Нет счет-фактуры"); 
			БТ_НомерСЧФ = "";     
			БТ_ДатаСЧФ = Дата(1,1,1);
		КонецЕсли;
		БТ_СуммаНДС = Строка(Автомобили.Итог("СуммаНДС"));  
	КонецЕсли;
	//чАЛАПаю БизТех 18.09.2024>>

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если (Не Отказ) И ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		//Проверим VIN автомобилей
		Для Каждого СтрокаАвтомобиля Из Автомобили Цикл
			ТекVIN = СтрокаАвтомобиля.Автомобиль.VIN;
			
			Если СтрокаАвтомобиля.VIN = ТекVIN Тогда
				Если ПустаяСтрока(СтрокаАвтомобиля.VIN) Тогда
					Сообщить("У автомобиля <"+СокрЛП(СтрокаАвтомобиля.Автомобиль)+"> не задан VIN. Заполните VIN автомобиля.");
					Отказ = Истина;
				Иначе
					//ОбъектАвтомобиль = СтрокаАвтомобиля.Автомобиль.ПолучитьОбъект();
					Если СтрокаАвтомобиля.АвтомобильБУ Тогда
						Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(СтрокаАвтомобиля.Автомобиль, Перечисления.ВидАвтомобиля.АвтомобильСПробегом, Перечисления.ДополнительнаяИнформацияАвтомобилей.ВидАвтомобиля, Дата);
					Иначе
						Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(СтрокаАвтомобиля.Автомобиль, Перечисления.ВидАвтомобиля.Новый, Перечисления.ДополнительнаяИнформацияАвтомобилей.ВидАвтомобиля, Дата);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли НЕ ПустаяСтрока(СтрокаАвтомобиля.VIN) Тогда
				Если ПустаяСтрока(ТекVIN) Тогда
					ОбъектАвтомобиль = СтрокаАвтомобиля.Автомобиль.ПолучитьОбъект();
					ОбъектАвтомобиль.VIN = СтрокаАвтомобиля.VIN;
					//Получить последний ГосНомер
					ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ОбъектАвтомобиль.Ссылка, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
					ОбъектАвтомобиль.ФормированиеНаименованияАвтомобиля(ГосНомер);
					Попытка
						ТекстОшибки = "";
						ОбъектАвтомобиль.ПроверитьКорректность(ТекстОшибки);
						Если Не ПустаяСтрока(ТекстОшибки) Тогда
							Сообщить("Ошибка ввода VIN. В автомобиле <"+СокрЛП(СтрокаАвтомобиля.Автомобиль)+"> " + ТекстОшибки);
							Отказ = Истина;
							Продолжить;
						КонецЕсли;
						
						ОбъектАвтомобиль.Записать();
						Если СтрокаАвтомобиля.АвтомобильБУ Тогда
							Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(ОбъектАвтомобиль.Ссылка, Перечисления.ВидАвтомобиля.АвтомобильСПробегом, Перечисления.ДополнительнаяИнформацияАвтомобилей.ВидАвтомобиля, Дата);
						Иначе
							Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(ОбъектАвтомобиль.Ссылка, Перечисления.ВидАвтомобиля.Новый, Перечисления.ДополнительнаяИнформацияАвтомобилей.ВидАвтомобиля, Дата);
						КонецЕсли;
					Исключение
						Отказ = Истина;
						Возврат;
					КонецПопытки;
				Иначе
					Сообщить("Ошибка ввода VIN. В автомобиле <"+СокрЛП(СтрокаАвтомобиля.Автомобиль)+"> задан другой VIN.");
					//Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);

	//<<БАА
	Если Проведен И Не Отказ И црмРаботаСсоединением.ПодразделениеЦРМ(ПодразделениеКомпании) Тогда
		
		Для Каждого СтрокаАвтомобиля Из Автомобили Цикл
			
			Обр = Обработки.БТ_црмОтправитьАвтомобиль.Создать();
			Обр.Автомобиль = СтрокаАвтомобиля.Автомобиль;
			Обр.Статус = "DEALER";
			Обр.ДатаСтатуса = Дата;
			
			Обр.ОтправитьАвто();
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Отказ = Отказ ИЛИ дкЕстьВведенныеДокументыПрослеживаемости(ЭтотОбъект);
	
КонецПроцедуры

// Функция списания автомобиля с ответственного хранения при переходе в собственность
//
// Параметры
//  Отказ  – Булево – Результат проведения
//  Режим  – РежимПроведения – Режим проведения
//
// Возвращаемое значение:
//   Булево   – Результат проведения
//
Функция СписаниеСОтветственногоХранения(Отказ, Режим)
	
	Результат = Истина;
	
	Если ХозОперация=Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда
		
		//Для перехода автомобиля в собственность сначала спишем его со склада
		НаборЗаписейОстаткиАвтомобилей = Движения.ОстаткиАвтомобилей;
		НаборЗаписейОстаткиАвтомобилей.РежимПроведения = Режим;
		НаборЗаписейОстаткиАвтомобилей.ДокументОбъект  = ЭтотОбъект;
		
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ 
				|	ДокументАвтомобили.Автомобиль КАК Автомобиль,
				|	СУММА(ДокументАвтомобили.Количество) КАК Количество
				|ИЗ
				|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ДокументАвтомобили
				|ГДЕ
				|	ДокументАвтомобили.Ссылка = &Ссылка
				|СГРУППИРОВАТЬ ПО
				|	ДокументАвтомобили.Автомобиль";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям = Запрос.Выполнить().Выгрузить();
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии  = Перечисления.СтатусыПартий.ТоварОтветственноеХранение;
		НаборЗаписейОстаткиАвтомобилей.СкладКомпании = СкладКомпании;
		НаборЗаписейОстаткиАвтомобилей.СторноПриход  = Истина;
		Отказ = НЕ НаборЗаписейОстаткиАвтомобилей.Расход() ИЛИ Отказ;
		НаборЗаписейОстаткиАвтомобилей.ГраницаРасчетаОстатков = Неопределено;
		Если НЕ Отказ Тогда
			НаборЗаписейОстаткиАвтомобилей.Записать();
		КонецЕсли;
		
		//Спишем оборудование автомобиля
		ШапкаДокумента=ПолучитьШапкуДокумента(Ссылка);
		НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			////Спишем оборудование по документу
			//Спишем опции, установленные производителем
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения           = Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам = Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль                = СтрокаТЧ.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании             = СкладКомпании;
			//НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования           = "Опции";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента            = ШапкаДокумента;
			НаборЗаписейКомплектацияАвтомобилей.СторноПриход              = Истина;
			Отказ = НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
		КонецЦикла;
	КонецЕсли;
	
	Результат = Результат И (НЕ Отказ);
	
	Возврат Результат;
	
КонецФункции // СписаниеСОтветственногоХранения() 

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//проверим, что автомобиль отсутствует на остатках по различным регистрам
	Если ХозОперация <> Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда
		Если НЕ РегистрыНакопления.ОстаткиАвтомобилей.ПроверитьОстаткиАвтомобилей(ЭтотОбъект,Отказ) Тогда 
			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);		
			Возврат; 
		КонецЕсли;
	КонецЕсли;
	
   //Bizteh++ 04.07.2022г.
	//если была корректировка поступления авто на первое поступление на ОХ, то изменилась себестоимость авто, необходимо поменять цифры в ТЧ
	Если ХозОперация = Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда 
		Для каждого Стр из Автомобили Цикл
			ОстатокОХ = ПроверитьСтоимостьАвтоОХиИзменитьТЧ(Стр.Автомобиль); 
			Если ОстатокОХ <>0 и ОстатокОХ<>Стр.Цена Тогда
				Стр.Цена = ОстатокОХ; 
				ОбработкаРеквизита("Автомобили.Цена",Стр);
				ЭтотОбъект.Записать();  
				Сообщить("Изменена цена авто "+ Стр.Автомобиль+ " в ТЧ документа "+ЭтотОбъект.Номер+" в соответствии с ценой остатка на ОХ "); 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
    //Bizteh--
	
	ФлагУпрВалюты = (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	
	СписаниеСОтветственногоХранения(Отказ, Режим);
	// проведем взаиморасчеты если поступление не комиссионное и не на ответхранение
	// проведем взаиморасчеты если поступление покупки
	Если ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилей ИЛИ
		 ХозОперация=Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
		Иначе
			НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
		КонецЕсли; 
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	Отказ = ПроверитьЗаказыНаАвтомобиль() ИЛИ Отказ;
	
	ТаблицаАвтомобилей = Автомобили.Выгрузить(,"Автомобиль");
	Запрос=Новый Запрос;
	Запрос.Текст="
			|ВЫБРАТЬ
			|	ПоступлениеАвтомобилейАвтомобили.Автомобиль КАК Автомобиль
			|ПОМЕСТИТЬ
			|	ТаблицаАвтомобилей
			|ИЗ
			|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
			|ГДЕ
			|	ПоступлениеАвтомобилейАвтомобили.Ссылка = &Ссылка И 
			|	ПоступлениеАвтомобилейАвтомобили.АвтомобильБезЗаказа = ЛОЖЬ
			|ИНДЕКСИРОВАТЬ ПО
			|	Автомобиль
			|;
			|
			|ВЫБРАТЬ
			|	ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
			|	ЗаказыАвтомобилейОстатки.ЗаказПоставщику КАК Заказ,
			|	ЗаказыАвтомобилейОстатки.ЗаказПоставщику.Контрагент КАК Контрагент,
			|	ЗаказыАвтомобилейОстатки.ЗаказПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
			|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0) КАК Количество,
			|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.СуммаОстаток, 0) КАК Сумма,
			|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр
			|ИЗ
			|	РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки(
			|			&НаГраницу,
			|			Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ ТаблицаАвтомобилей)) КАК ЗаказыАвтомобилейОстатки
			|ГДЕ
			|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0)<>0
			|";
	Запрос.УстановитьПараметр("НаГраницу",  Новый Граница(МоментВремени(),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Ссылка",  Ссылка);
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПоставщикамНаАвтомобили");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата));
	
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаАвтомобилей);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Автомобиль", "Автомобиль");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	ТаблицаЗаказовНаАвтомобили = Запрос.Выполнить().Выгрузить();
	ТаблицаЗаказовНаАвтомобили.Индексы.Добавить("Автомобиль");
	//Закроем заказы поставщикам
	НаборЗаписейЗаказыПоставщикам = Движения.ЗаказыПоставщикамНаАвтомобили;
	Для Каждого ТекСтрока Из Автомобили Цикл
		
		Если ТекСтрока.АвтомобильБезЗаказа Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаЗаказа = ТаблицаЗаказовНаАвтомобили.Найти(ТекСтрока.Автомобиль, "Автомобиль");
		
		Если СтрокаЗаказа = Неопределено Тогда
			дкСообщитьРезультат("Автомобиль """+СокрЛП(ТекСтрока.Автомобиль)+""" не заказывался. Закрытие заказа невозможно.","Важное&Заказы поставщикам на автомобили:",ЭтотОбъект, ТекСтрока.Автомобиль);
			Отказ = Истина;
			Продолжить;
		ИначеЕсли СтрокаЗаказа.Заказ <> ТекСтрока.ЗаказНаАвтомобиль Тогда
			дкСообщитьРезультат("Автомобиль """+СокрЛП(СтрокаЗаказа.Автомобиль)+""" заказан документом """+СокрЛП(СтрокаЗаказа.Заказ)+""". Закрытие чужого заказа невозможно.","Важное&Заказы поставщикам на автомобили:",ЭтотОбъект,СтрокаЗаказа.Автомобиль);
			Отказ=Истина;
			Продолжить;
		ИначеЕсли СтрокаЗаказа.Контрагент <> Контрагент Тогда
			дкСообщитьРезультат("Автомобиль """+СокрЛП(СтрокаЗаказа.Автомобиль)+""" заказан контрагентом """+СокрЛП(СтрокаЗаказа.Контрагент)+""". Закрытие чужого заказа невозможно.","Важное&Заказы поставщикам на автомобили:",ЭтотОбъект,СтрокаЗаказа.Автомобиль);
			Отказ=Истина;
			Продолжить;
		ИначеЕсли СтрокаЗаказа.ДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
			дкСообщитьРезультат("Автомобиль """+СокрЛП(СтрокаЗаказа.Автомобиль)+""" заказан контрагентом согласно договора """+СокрЛП(СтрокаЗаказа.ДоговорВзаиморасчетов)+""". Закрытие заказа по другому договору невозможно.","Важное&Заказы поставщикам на автомобили:",ЭтотОбъект,СтрокаЗаказа.Автомобиль);
			Отказ=Истина;
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписейЗаказыПоставщикам.Добавить();
		НоваяЗапись.ВидДвижения     = ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период          = Дата;
		НоваяЗапись.Регистратор     = Ссылка;
		НоваяЗапись.ЗаказПоставщику = ТекСтрока.ЗаказНаАвтомобиль;
		НоваяЗапись.Автомобиль      = ТекСтрока.Автомобиль;
		НоваяЗапись.Количество      = СтрокаЗаказа.Количество;
		НоваяЗапись.Сумма           = СтрокаЗаказа.Сумма;
		НоваяЗапись.СуммаУпр        = СтрокаЗаказа.СуммаУпр;
		НоваяЗапись.ХозОперация     = ХозОперация;
		
	КонецЦикла;
	
	Если Отказ Тогда
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	Иначе
		НаборЗаписейЗаказыПоставщикам.Записать();
	КонецЕсли;
	
	// Зарезервируем автомобили под заказы клиентов
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаказыАвтомобилейОстатки.Автомобиль,
	|	ЗаказыАвтомобилейОстатки.Заказ,
	|	ЗаказыАвтомобилейОстатки.Заказ.СтатусАвтомобиля КАК СтатусАвтомобиля,
	|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.РезервОстаток, 0) КАК Резерв,
	|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0) КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗаказыАвтомобилей.Остатки(
	|			&НаГраницу,
	|			Автомобиль В (&Автомобили)) КАК ЗаказыАвтомобилейОстатки
	|ГДЕ
	|	ЕСТЬNULL(ЗаказыАвтомобилейОстатки.КоличествоОстаток, 0)<>0
	|";
	Запрос.УстановитьПараметр("НаГраницу",  Новый Граница(МоментВремени(),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Автомобили", Автомобили.ВыгрузитьКолонку("Автомобиль"));
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыАвтомобилей");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата));
	
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаЗаказовНаАвтомобили);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Автомобиль", "Автомобиль");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	НаборЗаписейЗаказыАвтомобилей = Движения.ЗаказыАвтомобилей;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Резерв > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Решили, что при поступлении всегда резервировать. Если есть долг по предоплате, то блокировать только заказ поставщику.
		//ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, Выборка.Заказ);
		//Если ТаблицаДолгов.Количество()>0 Тогда
		//	СтрокаДолга = ТаблицаДолгов[0];
		//	Сообщить("Автомобиль """+СокрЛП(Выборка.Автомобиль)+""" нельзя зарезервировать по заказу """+СокрЛП(Выборка.Заказ)+""". Долг по предоплате составляет: "+Формат(СтрокаДолга.Долг, "ЧЦ=15; ЧДЦ=2")+" "+СокрЛП(СтрокаДолга.Валюта)+".", СтатусСообщения.Внимание);
		//	Продолжить;
		//КонецЕсли;
		
		НоваяЗапись = НаборЗаписейЗаказыАвтомобилей.Добавить();
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период      = Дата;
		НоваяЗапись.Регистратор = Ссылка;
		НоваяЗапись.Автомобиль  = Выборка.Автомобиль;
		НоваяЗапись.Заказ       = Выборка.Заказ;
		НоваяЗапись.Резерв      = 1;
		НоваяЗапись.ХозОперация = ХозОперация;
		
	КонецЦикла;
	
	НаборЗаписейЗаказыАвтомобилей.Записать();
	
	НаборЗаписейОстатки = Движения.ОстаткиАвтомобилей;
	НаборЗаписейОстатки.РежимПроведения                         = Режим;
	НаборЗаписейОстатки.ДокументОбъект                          = ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоАвтомобилям           = Автомобили.Выгрузить();
	НаборЗаписейОстатки.СкладКомпании                           = СкладКомпании;
	Если ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда
		НаборЗаписейОстатки.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейОтветственноеХранение Тогда
		НаборЗаписейОстатки.СтатусПартии = Перечисления.СтатусыПартий.ТоварОтветственноеХранение;
	Иначе
		НаборЗаписейОстатки.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
	КонецЕсли; 
	Если ХозОперация = Справочники.ХозОперации.ПереходВСобственностьАвтомобилей Тогда
		НаборЗаписейОстатки.ГраницаРасчетаОстатков = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	КонецЕсли; 
	НаборЗаписейОстатки.Партия        = Ссылка;
	
	Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	НаборЗаписейОстатки.ГраницаРасчетаОстатков = Неопределено;
	
	Если НЕ Отказ Тогда
		ШапкаДокумента=ПолучитьШапкуДокумента(Ссылка);
		//Запишем комплектацию автомобилей
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
				|	ПоступлениеАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
				|	ПоступлениеАвтомобилейОпции.Опция КАК Номенклатура,
				|	NULL КАК ХарактеристикаНоменклатуры,
				|	&СтатусПартии КАК СтатусПартии,
				|	ПоступлениеАвтомобилейОпции.Ссылка КАК Партия,
				|	ПоступлениеАвтомобилейАвтомобили.ГТД КАК ГТД,
				|	ПоступлениеАвтомобилейОпции.СтавкаНДС КАК СтавкаНДС,
				|	ПоступлениеАвтомобилейОпции.Количество КАК Количество,
				|	ПоступлениеАвтомобилейОпции.СуммаВсего КАК Сумма,
				|	ПоступлениеАвтомобилейОпции.СуммаВсего КАК СуммаУпр,
				|	ПоступлениеАвтомобилейОпции.СуммаНДС КАК СуммаНДС
				|ИЗ
				|	Документ.ПоступлениеАвтомобилей.Опции КАК ПоступлениеАвтомобилейОпции
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
				|ПО
				|	ПоступлениеАвтомобилейОпции.Ссылка = ПоступлениеАвтомобилейАвтомобили.Ссылка И 
				|	ПоступлениеАвтомобилейОпции.ИдентификаторАвтомобиля = ПоступлениеАвтомобилейАвтомобили.ИдентификаторАвтомобиля
				|ГДЕ
				|	ПоступлениеАвтомобилейОпции.Ссылка = &Ссылка";
		Если ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда
			Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейОтветственноеХранение Тогда
			Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварОтветственноеХранение);
		Иначе
			Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварКупленный);
		КонецЕсли;
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		ТаблицаОпций=Запрос.Выполнить().Выгрузить();
		
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Если ВалютаДокумента<>ВалютаРегл Тогда
			Для каждого СтрокаОпций Из ТаблицаОпций Цикл
				СтрокаОпций.Сумма=обПересчет(СтрокаОпций.Сумма,ВалютаДокумента,Дата,ВалютаРегл,Дата);
				СтрокаОпций.СуммаНДС=обПересчет(СтрокаОпций.СуммаНДС,ВалютаДокумента,Дата,ВалютаРегл,Дата);
			КонецЦикла; 
		КонецЕсли; 
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,МоментВремени());
			КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр        = КурсВалютыУпр;
		КонецЕсли;
		Если ВалютаДокумента<>ВалютаУпр Тогда
			Для каждого СтрокаОпций Из ТаблицаОпций Цикл
				СтрокаОпций.СуммаУпр=обПересчет(СтрокаОпций.СуммаУпр,ВалютаДокумента,Дата,ВалютаУпр,КурсУпр);
			КонецЦикла; 
		КонецЕсли; 
		НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
		НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=ТаблицаОпций;
		НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=СкладКомпании;
		НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Приход() ИЛИ Отказ;
	КонецЕсли; 
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	Если ВедетсяБалансПоПодразделению И ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилей Тогда
		ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
		ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
		// способ ведения не по подразделениям или балансовые подразделения равны то ничего не будем корректировать
		Если БалансовыеПодразделенияНеРавны Тогда
			СуммаАвтомобилей = Движения.ОстаткиАвтомобилей.Итог("СуммаУпр") + Движения.КомплектацияАвтомобилей.Итог("СуммаУпр");
			Если СуммаАвтомобилей<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение = СкладКомпании.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				НаборЗаписейДиР.Доход = СуммаАвтомобилей;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
			КонецЕсли;			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Ложь;
			НаборЗаписейДиР.Расход = СуммаДокумента;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
		КонецЕсли;
	КонецЕсли; 
	
	// если необходимо, то установим цены на автомобили и опции
	Если НЕ Отказ И НЕ ТипЦен.Рассчитывается И ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилей И ПодразделениеКомпании.ФормироватьЗакупочнуюЦену Тогда
		НаборЗаписейЦены=Движения.ЦеныАвтомобилей;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.ТипЦен=ТипЦен;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокументАвтомобили.Автомобиль.Модель КАК Автомобиль,
		|	ДокументАвтомобили.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
		|	МАКСИМУМ(ДокументАвтомобили.Цена) КАК НоваяЦена
		|ПОМЕСТИТЬ
		|	ТаблицаАвтомобилей
		|ИЗ
		|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ДокументАвтомобили
		|ГДЕ
		|	ДокументАвтомобили.Ссылка = &Ссылка И 
		|	ДокументАвтомобили.Цена > 0
		|СГРУППИРОВАТЬ ПО
		|	ДокументАвтомобили.Автомобиль.Модель,
		|	ДокументАвтомобили.Автомобиль.ВариантКомплектации
		|ИНДЕКСИРОВАТЬ ПО
		|	Автомобиль,
		|	ВариантКомплектации
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
		|	ТаблицаАвтомобилей.ВариантКомплектации КАК ВариантКомплектации,
		|	ТаблицаАвтомобилей.НоваяЦена КАК НоваяЦена,
		|	ЕСТЬNULL(ЦеныВБазе.Цена, 0) КАК ЦенаВБазе
		|ИЗ
		|	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныАвтомобилей.СрезПоследних(&Момент, ТипЦен = &ТипЦен И Автомобиль Ссылка Справочник.Модели И
		|		ВариантКомплектации В (ВЫБРАТЬ ВариантКомплектации ИЗ ТаблицаАвтомобилей)) КАК ЦеныВБазе
		|ПО
		|	ТаблицаАвтомобилей.Автомобиль = ЦеныВБазе.Автомобиль И 
		|	ТаблицаАвтомобилей.ВариантКомплектации = ЦеныВБазе.ВариантКомплектации");
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
		Запрос.УстановитьПараметр("Момент", МоментВремени());
		НаборЗаписейЦены.РезультатЗапросаПоАвтомобилям = Запрос.Выполнить();
		НаборЗаписейЦены.ПроверятьОдинаковыеЦены = Ложь;
		Отказ=НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		
		Если НЕ Отказ Тогда
			// устанавливаем цены
			НаборЗаписейЦены = Движения.ЦеныОпций;
			НаборЗаписейЦены.ДокументОбъект	= ЭтотОбъект;
			НаборЗаписейЦены.ТипЦен			= ТипЦен;
			
			Запрос=Новый Запрос("
			|ВЫБРАТЬ
			|	ПоступлениеАвтомобилейАвтомобили.Автомобиль.Модель КАК Модель,
			|	ПоступлениеАвтомобилейАвтомобили.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
			|	ДокументОпции.Опция КАК Опция,
			|	МАКСИМУМ(ДокументОпции.Цена) КАК НоваяЦена
			|ПОМЕСТИТЬ
			|	ТаблицаОпций
			|ИЗ
			|	Документ.ПоступлениеАвтомобилей.Опции КАК ДокументОпции
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
			|ПО 
			|	ДокументОпции.ИдентификаторАвтомобиля = ПоступлениеАвтомобилейАвтомобили.ИдентификаторАвтомобиля И 
			|	ПоступлениеАвтомобилейАвтомобили.Ссылка = &Ссылка
			|ГДЕ
			|	ДокументОпции.Ссылка = &Ссылка И 
			|	ДокументОпции.Цена > 0
			|СГРУППИРОВАТЬ ПО 
			|	ПоступлениеАвтомобилейАвтомобили.Автомобиль.Модель,
			|	ПоступлениеАвтомобилейАвтомобили.Автомобиль.ВариантКомплектации,
			|	ДокументОпции.Опция
			|ИНДЕКСИРОВАТЬ ПО
			|	Модель,
			|	ВариантКомплектации
			|;
			|
			|ВЫБРАТЬ
			|	ТаблицаОпций.Модель,
			|	ТаблицаОпций.ВариантКомплектации,
			|	ТаблицаОпций.Опция,
			|	ТаблицаОпций.НоваяЦена,
			|	ЕСТЬNULL(ЦеныВБазе.Цена,0) КАК ЦенаВБазе
			|ИЗ
			|	ТаблицаОпций КАК ТаблицаОпций
			|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныОпций.СрезПоследних(&Момент, ТипЦен = &ТипЦен И 
			|		(Модель, ВариантКомплектации, Опция) В 
			|		(ВЫБРАТЬ
			|			Модель,
			|			ВариантКомплектации,
			|			Опция
			|		ИЗ
			|			ТаблицаОпций)) КАК ЦеныВБазе
			|ПО
			|	ТаблицаОпций.Модель              = ЦеныВБазе.Модель И 
			|	ТаблицаОпций.ВариантКомплектации = ЦеныВБазе.ВариантКомплектации И 
			|	ТаблицаОпций.Опция               = ЦеныВБазе.Опция");
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
			Запрос.УстановитьПараметр("Момент", МоментВремени());
			НаборЗаписейЦены.РезультатЗапросаПоОпциям = Запрос.Выполнить();
			НаборЗаписейЦены.ПроверятьОдинаковыеЦены=Ложь;
			Отказ=НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// Проверим налиие прослеживаемых товаров, которые были возвращены из розницы
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыАвтомобилей.Количество()>0 ИЛИ Движения.ЗаказыПоставщикамНаАвтомобили.Количество()>0));
	// двигаем границу последовательности заказы на автомобиль
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказыНаАвтомобили.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаАвтомобилей = Движения.ЗаказыАвтомобилей.Выгрузить();
			Для Каждого СтрокаАвтомобиль Из Движения.ЗаказыПоставщикамНаАвтомобили Цикл
				НоваяСтрока = ТаблицаАвтомобилей.Добавить();
				НоваяСтрока.Автомобиль = СтрокаАвтомобиль.Автомобиль;
			КонецЦикла;
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыЗаказы.Автомобиль       = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыЗаказы.МоментВремени    = Дата;
			НаборЗаписейГраницыЗаказы.АвтомобильБыл    = ДополнительныеСвойства.АвтомобильЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Автомобиль       = ДополнительныеСвойства.АвтомобильЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени    = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.АвтомобильБыл    = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.КомплектацияАвтомобилей.Количество()>0);
	// двигаем границу последовательности комплектаций автомобилей
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  	 = Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	//bizteh++
	Если СкладКомпании.тт_КатегорияСклада.Код = "000000009"  и Дата > Дата ('20220101') Тогда //Трейд-ин
		Для каждого текАвтомобиль Из Автомобили Цикл
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	тт_РабочийЛистТрейдИн.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.тт_РабочийЛистТрейдИн КАК тт_РабочийЛистТрейдИн
			|ГДЕ
			|	тт_РабочийЛистТрейдИн.Автомобиль = &Автомобиль
			|	И НЕ тт_РабочийЛистТрейдИн.ПометкаУдаления";
			Запрос.УстановитьПараметр("Автомобиль", текАвтомобиль.Автомобиль);
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				
				док_тт_РабочийЛистТрейдИн = Документы.тт_РабочийЛистТрейдИн.СоздатьДокумент(); 
				док_тт_РабочийЛистТрейдИн.Дата = ТекущаяДата();
				док_тт_РабочийЛистТрейдИн.Автомобиль = текАвтомобиль.Автомобиль;  
				док_тт_РабочийЛистТрейдИн.Модель = текАвтомобиль.Автомобиль.Модель;
				док_тт_РабочийЛистТрейдИн.Марка = текАвтомобиль.Автомобиль.БТ_МаркаАвто;
				док_тт_РабочийЛистТрейдИн.ХозОперация = Справочники.ХозОперации.НайтиПоНаименованию("РабочийЛист",Истина);
				док_тт_РабочийЛистТрейдИн.Состояние = Перечисления.БТ_ТрейдИнСостояние.Ремонт;   
				док_тт_РабочийЛистТрейдИн.Статус = Перечисления.БТ_ТрейдИнСтатус.TradeIn;
				док_тт_РабочийЛистТрейдИн.ПодразделениеНахождения = Справочники.ПодразделенияКомпании.НайтиПоКоду("ЦБ000129"); 
				док_тт_РабочийЛистТрейдИн.Автор = ПараметрыСеанса.Пользователь;
				док_тт_РабочийЛистТрейдИн.Организация = ПараметрыСеанса.Организация;
				док_тт_РабочийЛистТрейдИн.ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
				док_тт_РабочийЛистТрейдИн.Комментарий = "Создан автоматически по поступлению автомобилей № " + Номер + " от " + формат(Дата,"ДФ=dd.MM.yyyy");	 
				
				док_тт_РабочийЛистТрейдИн.Записать(РежимЗаписиДокумента.Запись); 
				
				Стр = Документы.тт_РабочийЛистТрейдИн.НайтиДанныеРеализации(текАвтомобиль.Автомобиль,док_тт_РабочийЛистТрейдИн.Ссылка);
				док_тт_РабочийЛистТрейдИн.ЦенаПродажи = Стр.ЦенаПродажи;  
				док_тт_РабочийЛистТрейдИн.ДокументПродажи = Стр.ДокументПродажи;   
				
				Стр = Документы.тт_РабочийЛистТрейдИн.НайтиДанныеПоступления(текАвтомобиль.Автомобиль,док_тт_РабочийЛистТрейдИн.Ссылка);
				
				док_тт_РабочийЛистТрейдИн.ЦенаПокупки = Стр.ЦенаПокупки;  
				док_тт_РабочийЛистТрейдИн.ДокументПоступленияНаСклад = Стр.ДокументПоступленияНаСклад;
				док_тт_РабочийЛистТрейдИн.Записать(РежимЗаписиДокумента.Проведение); 
				
				Сообщить("Создан рабочий лист трейд-ин "+док_тт_РабочийЛистТрейдИн.Номер+" от "+Формат(док_тт_РабочийЛистТрейдИн.Дата,"ДФ=dd.MM.yyyy"));
				
				//1
				ОшибкаЗаполнения = Ложь;
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.МодельНовогоАвто) и док_тт_РабочийЛистТрейдИн.Статус = Перечисления.БТ_ТрейдИнСтатус.TradeIn  Тогда
					Сообщить("Укажите Модель нового авто.");
					ОшибкаЗаполнения = Истина;
				КонецЕсли;	
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ГарМаржа) Тогда
					Сообщить("Укажите Гарантированную маржу.");
					ОшибкаЗаполнения = Истина;
				КонецЕсли;	
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ПодразделениеПриема) Тогда
					Сообщить("Укажите ДЦ приема.");		
					ОшибкаЗаполнения = Истина;
				КонецЕсли;
				
				//НаличиеКолес++
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.НаличиеКолес) Тогда
					Сообщить("Укажите Наличие колес.");
					ОшибкаЗаполнения = Истина;
				КонецЕсли;	
				Если док_тт_РабочийЛистТрейдИн.НаличиеКолес = Перечисления.БТ_НаличиеКолесТрейдИн.Да Тогда
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.КоличествоКомплектовКолес) Тогда
						Сообщить("Укажите Количество комплектов колес.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					//Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.НаименованиеКолес) Тогда
					//	Сообщить("Укажите Наименование колес.");
					//	ОшибкаЗаполнения = Истина;
					//КонецЕсли;	
					//Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.МРЦКолес) Тогда
					//	Сообщить("Укажите МРЦ на комплект колес.");
					//	ОшибкаЗаполнения = Истина;
					//КонецЕсли;	
				КонецЕсли;
				//НаличиеКолес--  
				
				Если ОшибкаЗаполнения Тогда
					ДокФорма = док_тт_РабочийЛистТрейдИн.ПолучитьФорму("ФормаДокумента");	
					ДокФорма.ОткрытьМодально();	 
				КонецЕсли;     
				
				//2
				ОшибкаЗаполнения = Ложь;
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.МодельНовогоАвто) и док_тт_РабочийЛистТрейдИн.Статус = Перечисления.БТ_ТрейдИнСтатус.TradeIn Тогда
					Сообщить("Укажите Модель нового авто.");
					ОшибкаЗаполнения = Истина;
				КонецЕсли;	
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ГарМаржа) Тогда
					Сообщить("Укажите Гарантированную маржу.");
					ОшибкаЗаполнения = Истина;
				КонецЕсли;	
				Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ПодразделениеПриема) Тогда
					Сообщить("Укажите ДЦ приема.");
					ОшибкаЗаполнения = Истина;
				КонецЕсли;						
				
				Отказ = (Отказ или ОшибкаЗаполнения);
				
			Иначе
				
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					док_тт_РабочийЛистТрейдИн = ВыборкаДетальныеЗаписи.Ссылка; 
					
					//1
					ОшибкаЗаполнения = Ложь; 
					Если Не РольДоступна("ВосстПослед") Тогда
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.МодельНовогоАвто) и док_тт_РабочийЛистТрейдИн.Статус = Перечисления.БТ_ТрейдИнСтатус.TradeIn Тогда
						Сообщить("Укажите Модель нового авто.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ГарМаржа) Тогда
						Сообщить("Укажите Гарантированную маржу.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ПодразделениеПриема) Тогда
						Сообщить("Укажите ДЦ приема.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					
					//НаличиеКолес++
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.НаличиеКолес) Тогда
						Сообщить("Укажите Наличие колес.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					Если док_тт_РабочийЛистТрейдИн.НаличиеКолес = Перечисления.БТ_НаличиеКолесТрейдИн.Да Тогда
						Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.КоличествоКомплектовКолес) Тогда
							Сообщить("Укажите Количество комплектов колес.");
							ОшибкаЗаполнения = Истина;
						КонецЕсли;	
						//Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.НаименованиеКолес) Тогда
						//	Сообщить("Укажите Наименование колес.");
						//	ОшибкаЗаполнения = Истина;
						//КонецЕсли;	
						//Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.МРЦКолес) Тогда
						//	Сообщить("Укажите МРЦ на комплект колес.");
						//	ОшибкаЗаполнения = Истина;
						//КонецЕсли;	
					КонецЕсли;
					//НаличиеКолес--
					
					Если ОшибкаЗаполнения Тогда
						ДокФорма = док_тт_РабочийЛистТрейдИн.ПолучитьФорму("ФормаДокумента");	
						ДокФорма.ОткрытьМодально();	 
					КонецЕсли;  
					
					//2
					ОшибкаЗаполнения = Ложь;
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.МодельНовогоАвто) и док_тт_РабочийЛистТрейдИн.Статус = Перечисления.БТ_ТрейдИнСтатус.TradeIn Тогда
						Сообщить("Укажите Модель нового авто.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ГарМаржа) Тогда
						Сообщить("Укажите Гарантированную маржу.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;	
					Если НЕ ЗначениеЗаполнено(док_тт_РабочийЛистТрейдИн.ПодразделениеПриема) Тогда
						Сообщить("Укажите ДЦ приема.");
						ОшибкаЗаполнения = Истина;
					КонецЕсли;						
					
					Отказ = (Отказ или ОшибкаЗаполнения);
					КонецЕсли;	
				КонецЦикла;

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//bizteh-  
	
	//БИЗТЕХ КОВАЛЬ 05.06.2025 ++
	//Выставление статуса "На складе" в CME
	//Если Проведен И Не Отказ И ПодразделениеКомпании = Справочники.ПодразделенияКомпании.НайтиПоКоду("ЦБ000373")
	//	И Формат(Дата,"ДФ=dd.MM.yyyy") = Формат(ТекущаяДатаСеанса(),"ДФ=dd.MM.yyyy") // Для проведенных в тот же день
	//	И Автомобили.Количество() < 5 Тогда // Для защиты от поступления автомобилей на 400 авто
	//	ОбрCME = Обработки.БТ_ОбменCMExpert.Создать();
	//	Для Каждого СтрокаАвтомобиля Из Автомобили Цикл
	//		ОбрCME.НайтиАвтомобильНаОценкеИВыполнитьЕгоПоступление(СтрокаАвтомобиля.Автомобиль,Ссылка);
	//	КонецЦикла;
	//КонецЕсли;
	//БИЗТЕХ КОВАЛЬ 05.06.2025 --

	
	
	//<<БАА 02.12.2023
	Если црмОбщиеФункции.ТребуетсяВыгрузкаПоступления(Ссылка) И Не Отказ Тогда
		
		Для Каждого СтрокаАвто Из Автомобили Цикл
			Если црмОбщиеФункции.МаркаДляВыгрузкиАвто(строкаавто.Автомобиль.БТ_МаркаАвто) Тогда
				црмОбщиеФункции.ДобавитьАвтоВВыгрузку(СтрокаАвто.Автомобиль); 
			КонецЕсли;	
		КонецЦикла;	
		
	КонецЕсли;	
	//>>БАА 02.12.2023

	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

//Bizteh++ Функция возвращает стоимость остатка авто на ОХ
Функция ПроверитьСтоимостьАвтоОХиИзменитьТЧ(Авто) 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиАвтомобилейОстатки.СуммаОстаток КАК СуммаОстаток
	               |ИЗ
	               |	РегистрНакопления.ОстаткиАвтомобилей.Остатки(
	               |			&ДатаОстатка,
	               |			Автомобиль = &Авто
	               |				И СтатусПартии = &ОХ) КАК ОстаткиАвтомобилейОстатки";
	Запрос.УстановитьПараметр("ДатаОстатка", ЭтотОбъект.Дата);
	Запрос.УстановитьПараметр("Авто", Авто);
	Запрос.УстановитьПараметр("ОХ", Перечисления.СтатусыПартий.ТоварОтветственноеХранение);
	Результат = Запрос.Выполнить().Выгрузить(); 
	Если Результат.Количество()<>0 Тогда
		Возврат Результат[0].СуммаОстаток;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

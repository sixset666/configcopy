// Модуль документа Переоценка

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// пересчитывает количество, старую цену и старую сумму.
//
// Параметры:
//  ТекСтрока - строка табличной части, если не определена, то пересчитывается вся табличная часть
//
Процедура ПересчитатьСтроку(ТекСтрока=Неопределено) Экспорт
	// посмотрим пересчитываем строку или всю табл. часть
	ВсюТабличнуюЧасть=(ТекСтрока=Неопределено);
	
	ВалютаИз=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	// формируем условия расчета
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию Тогда
		ИмяРегистра="ПартииТоваровОтданные"; ИмяРесурса="СуммаОстаток";
		УсловияОтбора="Контрагент=&Контрагент И Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) И ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов";
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию Тогда
		ИмяРегистра="ПартииТоваровКомпании"; ИмяРесурса="СуммаОстаток";
		УсловияОтбора="СкладКомпании=&СкладКомпании И Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) И СтатусПартии=&СтатусПартии И Партия.ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов И Партия.Контрагент=&Контрагент";
	Иначе	
		ИмяРегистра="ОстаткиТоваровКомпании"; ИмяРесурса="СуммаРознОстаток";
		УсловияОтбора="Номенклатура В (&Номенклатура) И СкладКомпании=&СкладКомпании И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)";
	КонецЕсли;
	
	МоментОстатков=?(обЗначениеНеЗаполнено(ДокументОснование),Новый МоментВремени(КонецДня(Дата)),Новый МоментВремени(КонецДня(ТекущаяДата())));
	МоментВремени=?(обЗначениеНеЗаполнено(Ссылка),МоментОстатков,МоментВремени());
	ВалютаИз=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КурсИз = 1;
	
	Запрос=Новый Запрос;
	Запрос.Текст = " ВЫБРАТЬ
	|	Рег.Номенклатура,
	|	Рег.ХарактеристикаНоменклатуры,
	|	"+?(ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию,"Рег.ДокументПередачи КАК ДокументПередачи,","")+"
	|	"+?(ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию,"Рег.Партия КАК ДокументПередачи,","")+"
	|	Рег.КоличествоОстаток КАК Количество,
	|	ВЫБОР КОГДА Рег.КоличествоОстаток=0 ИЛИ Рег.КоличествоОстаток ЕСТЬ NULL ТОГДА 0 ИНАЧЕ (Рег."+ИмяРесурса+" / Рег.КоличествоОстаток)КОНЕЦ КАК ЦенаСтарая
	|ИЗ
	|	РегистрНакопления."+ИмяРегистра+".Остатки(&МоментВремени, "+УсловияОтбора+") КАК Рег";
	
	Запрос.УстановитьПараметр("МоментВремени",МоментВремени);
	Запрос.УстановитьПараметр("Номенклатура",?(ВсюТабличнуюЧасть,Товары.ВыгрузитьКолонку("Номенклатура"),ТекСтрока.Номенклатура));
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("СтатусПартии", Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",?(ВсюТабличнуюЧасть,Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"),ТекСтрока.ХарактеристикаНоменклатуры));
	
	Результат=Запрос.Выполнить();
		
	// Выгружаем результат запроса в таблицу значений
	ТЗ=Результат.Выгрузить();
	//Если это вся табличная часть - то перебор всей табличной части
	Если ТекСтрока=Неопределено Тогда
		СтруктураОтбора=Новый Структура();
		Для каждого ТекСтрокаТЧ Из Товары Цикл
			СтруктураОтбора.Вставить("Номенклатура", ТекСтрокаТЧ.Номенклатура);
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", ТекСтрокаТЧ.ХарактеристикаНоменклатуры);
			Если НЕ ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
				СтруктураОтбора.Вставить("ДокументПередачи", ТекСтрокаТЧ.ДокументПередачи);
			КонецЕсли;
			МассивНайденныхСтрок=ТЗ.НайтиСтроки(СтруктураОтбора);
			ТекСтрокаТЧ.Количество=?(МассивНайденныхСтрок.Количество()=0,0,МассивНайденныхСтрок[0].Количество);
			ТекСтрокаТЧ.ЦенаСтарая=?(МассивНайденныхСтрок.Количество()=0,0,МассивНайденныхСтрок[0].ЦенаСтарая);
			ТекСтрокаТЧ.ЦенаСтарая=обПересчет(ТекСтрокаТЧ.ЦенаСтарая,ВалютаИз,КурсИз,ВалютаДокумента,КурсДокумента);
			Если ТекСтрокаТЧ.Цена=0 Тогда ТекСтрокаТЧ.Цена=ТекСтрокаТЧ.ЦенаСтарая; КонецЕсли;
			
			ТекСтрокаТЧ.Сумма=(ТекСтрокаТЧ.Цена-ТекСтрокаТЧ.ЦенаСтарая)*ТекСтрокаТЧ.Количество*ТекСтрокаТЧ.Коэффициент;
			Попытка
				ТекСтрокаТЧ.ДокументПередачи=?(МассивНайденныхСтрок.Количество()=0,Неопределено,МассивНайденныхСтрок[0].ДокументПередачи);
			Исключение
			КонецПопытки
		КонецЦикла; 
	Иначе //установка по текущей строке
		СтруктураОтбора = Новый Структура();
		СтруктураОтбора.Вставить("Номенклатура", ТекСтрока.Номенклатура);
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", ТекСтрока.ХарактеристикаНоменклатуры);
		Если НЕ ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
			СтруктураОтбора.Вставить("ДокументПередачи", ТекСтрока.ДокументПередачи);
		КонецЕсли;
		МассивНайденныхСтрок=ТЗ.НайтиСтроки(СтруктураОтбора);
		ТекСтрока.Количество=?(МассивНайденныхСтрок.Количество()=0,0,МассивНайденныхСтрок[0].Количество);
		ТекСтрока.ЦенаСтарая=?(МассивНайденныхСтрок.Количество()=0,0,МассивНайденныхСтрок[0].ЦенаСтарая);
		ТекСтрока.ЦенаСтарая=обПересчет(ТекСтрока.ЦенаСтарая,ВалютаИз,КурсИз,ВалютаДокумента,КурсДокумента);
		Если ТекСтрока.Цена=0 Тогда ТекСтрока.Цена=ТекСтрока.ЦенаСтарая; КонецЕсли;
		
		ТекСтрока.Сумма=(ТекСтрока.Цена-ТекСтрока.ЦенаСтарая)*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Попытка
			ТекСтрока.ДокументПередачи=?(МассивНайденныхСтрок.Количество()=0,Неопределено,МассивНайденныхСтрок[0].ДокументПередачи);
		Исключение
		КонецПопытки
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьСтроку()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если ХозОперация<>Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
		ОбязательныеТовары.Вставить("ДокументПередачи",3);
	КонецЕсли;

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	Если ХозОперация<>Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	Иначе
        ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если ХозОперация<>Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Иначе	
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;	
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации И (ХозОперация<>Справочники.ХозОперации.ПереоценкаТоваровВРознице) Тогда
				КонтролируемыеРеквизитыТовары = Новый Структура();
				КонтролируемыеРеквизитыТовары.Вставить("ДокументПередачи");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПереоценкаТоваровВРознице","Переоценка товаров в рознице",Истина);
	СписокХозОпераций.Добавить("ПереоценкаТоваровОтданныхНаКомиссию","Переоценка товаров отданных на комиссию");
	СписокХозОпераций.Добавить("ПереоценкаТоваровПринятыхНаКомиссию","Переоценка товаров принятых на комиссию");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// переоцениваем регистр "ПартииТоваровОтданные"
	ИмяРегистраПартии = ?(ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию, "ПартииТоваровОтданные", "ПартииТоваровКомпании");
	НаборЗаписейПартииТоваров=Движения[ИмяРегистраПартии];
	НаборЗаписейПартииТоваров.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартииТоваров.ШапкаДокумента=ШапкаДокумента;
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию Тогда
		НаборЗаписейПартииТоваров.СкладКомпании=ШапкаДокумента.СкладКомпании;
	КонецЕсли;
	НаборЗаписейПартииТоваров.Сторно=Ложь;
	НаборЗаписейПартииТоваров.РезультатЗапросаПоТоварам=Неопределено;
	Отказ = НЕ НаборЗаписейПартииТоваров.Переоценка() ИЛИ Отказ;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	
	// двигаем границу последовательности партий
	ДвигаемГраницу = ХозОперация<>Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию И Режим<>РежимПроведенияДокумента.Оперативный;
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Товары.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		ИначеЕсли ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез = ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Если СкладКомпании.Розничный И ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
			ВалютаДокумента = обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
			СтруктураКурса  = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		КонецЕсли;
		Если (Товары.Количество()>0) И НЕ СкладКомпании.Пустая() Тогда
			#Если Клиент Тогда
			Если Вопрос("Произвести пересчет показателей табличной части?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				ЭтотОбъект.ПересчитатьСтроку(Неопределено);
			КонецЕсли; 
			#Иначе
			ЭтотОбъект.ПересчитатьСтроку(Неопределено);
			#КонецЕсли
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании"
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли (ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровВРознице) И (Не СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранный склад: <" + СкладКомпании + "> не является розничным!
			|Переоценка товаров компании возможна только на разничном складе.",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда Возврат Ложь; КонецЕсли;
		Если ЗначениеЗаполнено(Контрагент) И ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли;
			ДопПараметры.Вставить("Контрагент",Контрагент);
        КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		// заполним единицу измерения
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			ТекСтрока.ЕдиницаИзмерения=ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
			ТекСтрока.Коэффициент=ТекСтрока.ЕдиницаИзмерения.Коэффициент;
		КонецЕсли;	
	//	ПересчитатьСтроку(ТекСтрока);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			// изменим сумму документа
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ДокументПередачи" Тогда
		ПересчитатьСтроку(ТекСтрока);
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		ПересчитатьСтроку(ТекСтрока);
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		ПересчитатьСтроку(ТекСтрока);
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда	
		ПересчитатьСтроку(ТекСтрока); 
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПереоценка(ТабДокумент) Экспорт
	РазрешитьПечать = ОбработкаРеквизита("ПроверкаСкладаКомпании");
	
	Если РазрешитьПечать Тогда
		
		Макет = ПолучитьМакет("Переоценка");
		
		//для начала настроим макет
		ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
		
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		
		//вывод заголовка документа
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
		
		ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
		
		//вывод свойств
		СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
		ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
		ТабДокумент.Вывести(ОбластьМакета);
		
		НомерСтраницы = 2;
		НомерСтраницыПред = НомерСтраницы;
		
		//теперь выводим шапку
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		//готовим области строки
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			
		//перебор строк
		ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
		Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			СтруктураСтроки.Вставить("СуммаВсего",Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("ЦенаСтарая",Формат(СтрокаТабличнойЧасти.ЦенаСтарая,ФорматВыводаСуммы));
			ПроцентНаценки = ?(СтрокаТабличнойЧасти.ЦенаСтарая=0,0,100*(СтрокаТабличнойЧасти.Цена-СтрокаТабличнойЧасти.ЦенаСтарая)/СтрокаТабличнойЧасти.ЦенаСтарая);
			СтруктураСтроки.Вставить("ПроцентНаценки",Формат(ПроцентНаценки,"ЧЦ=6; ЧДЦ=2"));
			
			ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
			
		КонецЦикла;
		
		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;	
		
		//итоги
		ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
		ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований " + ВыборкаТабличнойЧасти.Количество()
			+ " на сумму " + обЧислоПрописью(СуммаВсего, ВалютаДокумента);
		
		// Выводим представления и расшифровки подписей
		МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
		МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
		ОбластьПодвал.Параметры.Заполнить(МОЛ);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
		
		Возврат ТабДокумент;
		
	КонецЕсли;
КонецФункции // ПечатьПереоценка()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	//заполним стандартно, без заполнения ТЧ
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание, Ложь, Ложь) Тогда Возврат; КонецЕсли;
			
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		Если (Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров)
			ИЛИ (Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала) Тогда 
			//для перемещения товаров сделаем переоценку на складе - получателе товара
			//исключение - перемещение товаров в филиал, там переоценку сделаем на складе - отправителе
			СкладКомпании = Основание.СкладПолучатель;
		ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
			//установим соответствующие хоз. операции для случая комиссии
			ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию;
		ИначеЕсли (Основание.ХозОперация = Справочники.ХозОперации.РеализацияТоваровКомиссия)
			ИЛИ (Основание.ХозОперация = Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию) Тогда
			ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию;
		КонецЕсли;
	КонецЕсли; 
	
	ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	Если ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
		Если (СкладКомпании<>Неопределено) И (СкладКомпании.Розничный) Тогда
			
			ТипЦен = СкладКомпании.ТипЦенРозничнойТорговли;
			ВалютаДокумента = обВалютаТипаЦены(Неопределено, ТипЦен, Ложь);
			СтруктураКурса  = обКурсДляВалюты(ВалютаДокумента, Дата);
			КурсДокумента   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
		Иначе
			СкладКомпании = Справочники.СкладыКомпании.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	УсловияОтбора = "";
	//заполним ТЧ
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию Тогда
		ИмяРегистра="ПартииТоваровОтданные"; ИмяРесурса="СуммаОстаток";
		УсловияОтбора="Контрагент=&Контрагент И Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) И ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов";
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию Тогда
		ИмяРегистра="ПартииТоваровКомпании"; ИмяРесурса="СуммаОстаток";
		УсловияОтбора="СкладКомпании=&СкладКомпании И Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) И СтатусПартии=&СтатусПартии И Партия.ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов И Партия.Контрагент=&Контрагент";
	Иначе
		ИмяРегистра="ОстаткиТоваровКомпании"; ИмяРесурса="СуммаРознОстаток";
		
		Если ЗначениеЗаполнено(Основание) Тогда
			
			УсловияОтбора="Номенклатура В (&Номенклатура) И СкладКомпании=&СкладКомпании И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)";
			
			МетаданныеОснования = Основание.Метаданные();
			МетаданныеТаблицы = МетаданныеОснования.ТабличныеЧасти.Найти("Товары");
			Если НЕ МетаданныеТаблицы = Неопределено Тогда
				
				ИмяРеквизитаЦены = Неопределено;
				Если НЕ МетаданныеТаблицы.Реквизиты.Найти("ЦенаРозничная") = Неопределено Тогда
					ИмяРеквизитаЦены = "ЦенаРозничная";
				ИначеЕсли НЕ МетаданныеТаблицы.Реквизиты.Найти("Цена") = Неопределено Тогда
					ИмяРеквизитаЦены = "Цена";
				КонецЕсли;
				
				Если НЕ ИмяРеквизитаЦены = Неопределено Тогда
					
					Запрос = Новый Запрос("ВЫБРАТЬ
					                      |	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
					                      |	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					                      |	ТаблицаТоваров."+ИмяРеквизитаЦены+" КАК Цена,
					                      |	ТаблицаТоваров.НомерСтроки
					                      |ПОМЕСТИТЬ ТаблицаТоваров
					                      |ИЗ
					                      |	&ТаблицаТоваров КАК ТаблицаТоваров
					                      |;
					                      |
					                      |////////////////////////////////////////////////////////////////////////////////
					                      |ВЫБРАТЬ
					                      |	ТаблицаТоваров.Номенклатура КАК Номенклатура,
					                      |	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					                      |	МАКСИМУМ(ТаблицаТоваров.Цена) КАК Цена,
					                      |	ТаблицаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
					                      |	ТаблицаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
					                      |	ТаблицаТоваров.НомерСтроки КАК НомерСтроки
					                      |ИЗ
					                      |	ТаблицаТоваров КАК ТаблицаТоваров
					                      |
					                      |СГРУППИРОВАТЬ ПО
					                      |	ТаблицаТоваров.Номенклатура,
					                      |	ТаблицаТоваров.ХарактеристикаНоменклатуры,
					                      |	ТаблицаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения,
					                      |	ТаблицаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент,
					                      |	ТаблицаТоваров.НомерСтроки
					                      |
					                      |УПОРЯДОЧИТЬ ПО
					                      |	НомерСтроки");
					
					Запрос.УстановитьПараметр("ТаблицаТоваров", Основание.Товары);
					Товары.Загрузить(Запрос.Выполнить().Выгрузить());
					
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			
			УсловияОтбора = "СкладКомпании=&СкладКомпании";
			
		КонецЕсли;
		
	КонецЕсли;
	
	//для заполнения на основании инвентаризации нужны только те строки, в которых количество изменилось
	//поэтому сделаем соединение с ТЧ основания
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Инвентаризация") Тогда
		СтрокаВложЗапроса1 = " (ВЫБРАТЬ
		|	Осн.Номенклатура КАК Номенклатура,
		|	Осн.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ИЗ
		|	Документ.Инвентаризация.Товары КАК Осн
		|ГДЕ
		|	Осн.Ссылка = &ДокОснование
		|	И Осн.Количество <> 0) КАК ВложенныйЗапрос
		|ЛЕВОЕ СОЕДИНЕНИЕ ";
		СтрокаВложЗапроса2 = " ПО ВложенныйЗапрос.Номенклатура = Рег.Номенклатура
		|	И ВложенныйЗапрос.ХарактеристикаНоменклатуры = Рег.ХарактеристикаНоменклатуры "
	Иначе 
		СтрокаВложЗапроса1 = "";
		СтрокаВложЗапроса2 = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		//Без основания заполнять ТЧ товаров не будем. Для этого есть пункт заполнение.
		МоментОстатков=?(обЗначениеНеЗаполнено(Основание),Новый МоментВремени(КонецДня(Дата)),Новый МоментВремени(КонецДня(ТекущаяДата())));
		МоментВремени=?(обЗначениеНеЗаполнено(Ссылка),МоментОстатков,МоментВремени());
		ВалютаИз = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		КурсИз   = 1;
		
		Запрос=Новый Запрос;
		Запрос.Текст = " ВЫБРАТЬ
		|	Рег.Номенклатура,
		|	Рег.ХарактеристикаНоменклатуры,
		|	"+?(ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию,"Рег.ДокументПередачи КАК ДокументПередачи,","")+"
		|	"+?(ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию,"Рег.Партия КАК ДокументПередачи,","")+"
		|	Рег.КоличествоОстаток КАК Количество,
		|	ВЫБОР КОГДА Рег.КоличествоОстаток=0 ИЛИ Рег.КоличествоОстаток ЕСТЬ NULL ТОГДА 0 ИНАЧЕ (Рег."+ИмяРесурса+" / Рег.КоличествоОстаток)КОНЕЦ КАК ЦенаСтарая
		|ИЗ " + СтрокаВложЗапроса1 + "
		|	РегистрНакопления."+ИмяРегистра+".Остатки(&МоментВремени, "+УсловияОтбора+") КАК Рег " +СтрокаВложЗапроса2;
		
		Запрос.УстановитьПараметр("МоментВремени",МоментВремени);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("СтатусПартии", Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		Запрос.УстановитьПараметр("ДокОснование", Основание);
		
		//у пересортицы по две колонки номенклатуры и характеристики номенклатуры - приход и расход
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
			//выгрузим колонки номенклатуры прихода и расхода в один массив
			МассивНоменклатуры = Основание.Товары.ВыгрузитьКолонку("Номенклатура");
			Буфер1 = Основание.Товары.ВыгрузитьКолонку("НоменклатураРасход");
			//выгрузим колонки характеристики прихода и расхода в один массив
			МассивХарактеристик = Основание.Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры");
			Буфер2 = Основание.Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатурыРасход");
			Для Сч = 0 По Буфер1.ВГраница() Цикл
				МассивНоменклатуры.Добавить(Буфер1[Сч]);
				МассивХарактеристик.Добавить(Буфер2[Сч]);
			КонецЦикла;
			Запрос.УстановитьПараметр("Номенклатура",МассивНоменклатуры);
			Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",МассивХарактеристик);			
		ИначеЕсли НЕ обЗначениеНеЗаполнено(Основание) Тогда
			Запрос.УстановитьПараметр("Номенклатура",Основание.Товары.ВыгрузитьКолонку("Номенклатура"));	
			Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Основание.Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
		Иначе
			ПустойМассив = Новый Массив;
			Запрос.УстановитьПараметр("Номенклатура",ПустойМассив);	
			Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ПустойМассив);
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураОтбора.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
			Если НЕ ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
				СтруктураОтбора.Вставить("ДокументПередачи", Выборка.ДокументПередачи);
			КонецЕсли;
			НайденныеСтроки = Товары.НайтиСтроки(СтруктураОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура               = Выборка.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				Если ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
					НоваяСтрока.ЕдиницаИзмерения       = НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				КонецЕсли;
				НоваяСтрока.Коэффициент = 1;
			Иначе
				НоваяСтрока = НайденныеСтроки[0];
			КонецЕсли;
			
			НоваяСтрока.Количество = Выборка.Количество;
			
			Если НоваяСтрока.Цена = 0 И ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
				НоваяСтрока.Цена = обПолучитьЦену(ТипЦен, НоваяСтрока.Номенклатура, МоментВремени(),, ВалютаДокумента, КурсДокумента, НоваяСтрока.ХарактеристикаНоменклатуры, НоваяСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
			КонецЕсли;
			
			Если НЕ ХозОперация = Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
				НоваяСтрока.ДокументПередачи = Выборка.ДокументПередачи;
			КонецЕсли;
			
			НоваяСтрока.ЦенаСтарая = обПересчет(Выборка.ЦенаСтарая, ВалютаИз, КурсИз, ВалютаДокумента, КурсДокумента);
			Если НоваяСтрока.Цена = 0 Тогда
				НоваяСтрока.Цена = НоваяСтрока.ЦенаСтарая;
			КонецЕсли;
			НоваяСтрока.Сумма = (НоваяСтрока.Цена - НоваяСтрока.ЦенаСтарая) * НоваяСтрока.Количество;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		ПересчитатьСтроку();
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// переоценим остатки
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровВРознице Тогда
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокументТовары.Количество*ДокументТовары.Коэффициент КАК Количество,
		|	ДокументТовары.Цена КАК ЦенаРозничная,
		|	0 КАК Резерв
		|ИЗ
		|	Документ.Переоценка.Товары КАК ДокументТовары
		|ГДЕ
		|	  ДокументТовары.Ссылка=&Ссылка
		|");
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		РезультатЗапросаПоТоварам=Запрос.Выполнить();
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		Отказ=НЕ НаборЗаписейОстатки.Переоценка(ХозОперация,Истина,Ложь) ИЛИ Отказ;
	КонецЕсли;
	
	// партии
	Если ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию ИЛИ ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию Тогда
		Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	КонецЕсли;
	
	// установим изменившиеся цены
	Если НЕ Отказ И ХозОперация=Справочники.ХозОперации.ПереоценкаТоваровВРознице И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент            = Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаЦена      = "Цена";
		НаборЗаписейЦены.ТипЦен                = СкладКомпании.ТипЦенРозничнойТорговли;
		НаборЗаписейЦены.ПодразделениеКомпании = СкладКомпании.Подразделение;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль документа "Таможенная декларация импорт"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// заполнение документа по поступлению товаров
//
Процедура ЗаполнитьПоПоступлениюТоваров(Основание)
	
	Если НЕ обЭтотТип(Основание, "ДокументСсылка.ПоступлениеТоваров") Тогда
		Возврат;
	КонецЕсли;
	
	Товары.Очистить();
	
	ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт;
	
	// получим данные документа основания
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТоваровКомпании.СкладКомпании КАК Склад,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
	|	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма,
	|	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаУпр,
	|	ПартииТоваровКомпании.Номенклатура.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Партия = &Партия
	|	И (ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
	|	ИЛИ ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	|	ИЛИ ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ПоступлениеДопРасходов)
	|	И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.СкладКомпании,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.Номенклатура.СтавкаНДС
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаУпр)
	|ПО
	|	СтавкаНДС,
	|	СкладКомпании";	
	
	Запрос.УстановитьПараметр("Партия", Основание);
	
	ВыборкаНДС = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// валюты и коэфициенты
	ВалютаРегл     = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаУпр      = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаУпр, Дата);
	КурсУпр        =  СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Пока ВыборкаНДС.Следующий() Цикл
		ВыборкаСклад = ВЫборкаНДС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Сч = 0;
		Пока ВыборкаСклад.Следующий() Цикл
			// добавляем разделы
			Сч = Сч + 1;
			НовыйРаздел = Разделы.Добавить();
			НовыйРаздел.НомерРаздела = Сч;
			НовыйРаздел.Склад = ВыборкаСклад.Склад;
			НовыйРаздел.СтавкаНДС = ВыборкаСклад.СтавкаНДС;
			
			НовыйРаздел.ТаможеннаяСтоимость =
				?(ВалютаДокумента = ВалютаРегл, ВыборкаСклад.Сумма, обПересчет(ВыборкаСклад.СуммаУпр, ВалютаУпр, КурсУпр, ВалютаДокумента, КурсДокумента));
				
			ОбработкаРеквизита("Разделы.ТаможеннаяСтоимость", НовыйРаздел);
			
			ВыборкаНоменклатура = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				Если ВыборкаНоменклатура.СуммаУпр = 0 И ВыборкаНоменклатура.Сумма = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НовыйТовар = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйТовар, ВыборкаНоменклатура);
				ОбработкаРеквизита("Товары.Номенклатура", НовыйТовар);
				
				НовыйТовар.НомерРаздела = Сч;
				НовыйТовар.Партия = Основание;
				НовыйТовар.ТаможеннаяСтоимость = 
					?(ВалютаДокумента = ВалютаРегл, ВыборкаНоменклатура.Сумма, обПересчет(ВыборкаНоменклатура.СуммаУпр, ВалютаУпр, КурсУпр, ВалютаДокумента, КурсДокумента));
					
				ОбработкаРеквизита("Товары.ТаможеннаяСтоимость", НовыйТовар,, Новый Структура("ДанныеРаздела", НовыйРаздел));
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Заполнение документа по поступлению автомобилей
//
Процедура ЗаполнитьПоПоступлениюАвтомобилей(Основание)
	
	// проверим соответствие типа
	Если НЕ обЭтотТип(Основание, "ДокументСсылка.ПоступлениеАвтомобилей") Тогда
		Возврат;
	КонецЕсли;
	
	Автомобили.Очистить();
	
	ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпортАвтомобилей;
	
	// получим данные заполнения
	Запрос = Новый Запрос;
	запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Автомобиль,
	|	ВложенныйЗапрос.ГТД,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
	|		ОстаткиАвтомобилей.СкладКомпании КАК Склад,
	|		ОстаткиАвтомобилей.ГТД КАК ГТД,
	|		ОстаткиАвтомобилей.Сумма КАК Сумма,
	|		ОстаткиАвтомобилей.СуммаУпр КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|	ГДЕ
	|		(ОстаткиАвтомобилей.Регистратор ССЫЛКА Документ.ПоступлениеАвтомобилей
	|				ИЛИ ОстаткиАвтомобилей.Регистратор ССЫЛКА Документ.КорректировкаПоступленияАвтомобилей
	|				ИЛИ ОстаткиАвтомобилей.Регистратор ССЫЛКА Документ.ПоступлениеДопРасходов
	|				ИЛИ ОстаткиАвтомобилей.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт)
	|		И ОстаткиАвтомобилей.Партия = &Партия
	|		И ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КомплектацияАвтомобилей.Автомобиль,
	|		КомплектацияАвтомобилей.СкладКомпании,
	|		КомплектацияАвтомобилей.ГТД,
	|		КомплектацияАвтомобилей.Сумма,
	|		КомплектацияАвтомобилей.СуммаУпр
	|	ИЗ
	|		РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	|	ГДЕ
	|		КомплектацияАвтомобилей.Регистратор ССЫЛКА Документ.ПоступлениеАвтомобилей
	|		И КомплектацияАвтомобилей.Партия = &Партия
	|		И КомплектацияАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.Сумма <> 0
	|	И ВложенныйЗапрос.СуммаУпр <> 0
	|	И ВложенныйЗапрос.ГТД = &пустоеГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.Автомобиль,
	|	ВложенныйЗапрос.ГТД
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаУпр)
	|ПО
	|	Склад";
	Запрос.УстановитьПараметр("Партия"    , Основание);
	Запрос.УстановитьПараметр("пустоеГТД" , Справочники.ГТД.ПустаяСсылка());
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		// валюты и коэфициенты
		ВалютаРегл     = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		ВалютаУпр      = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр, Дата);
		КурсУпр        = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		ОсновнаяСтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		
		ВыборкаСклады = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); Сч = 0;
		Пока ВыборкаСклады.Следующий() Цикл
			Сч = Сч + 1;
			
			НовыйРаздел = Разделы.Добавить();
			НовыйРаздел.НомерРаздела = Сч;
			НовыйРаздел.Склад = ВыборкаСклады.Склад;
			НовыйРаздел.СтавкаНДС = ОсновнаяСтавкаНДС;
			
			НовыйРаздел.ТаможеннаяСтоимость =
				?(ВалютаДокумента = ВалютаРегл, ВыборкаСклады.Сумма, обПересчет(ВыборкаСклады.СуммаУпр, ВалютаУпр, КурсУпр, ВалютаДокумента, КурсДокумента));
				
			ОбработкаРеквизита("Разделы.ТаможеннаяСтоимость", НовыйРаздел);
			
			ВыборкаАвтомобили = ВыборкаСклады.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаАвтомобили.Следующий() Цикл
				Если (выборкаАвтомобили.СуммаУпр = 0 И выборкаАвтомобили.Сумма = 0) Тогда
					Продолжить;
				КонецЕсли;
				
				НовыйАвтомобиль = Автомобили.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйАвтомобиль, ВыборкаАвтомобили,, "ГТД");
				НовыйАвтомобиль.НомерРаздела = Сч;
				НовыйАвтомобиль.ТаможеннаяСтоимость =
					?(ВалютаДокумента = ВалютаРегл, ВыборкаАвтомобили.Сумма, обПересчет(ВыборкаАвтомобили.СуммаУпр, ВалютаУпр, КурсУпр, ВалютаДокумента, КурсДокумента));
					
				ОбработкаРеквизита("Автомобили.ТаможеннаяСтоимость", НовыйАвтомобиль,, Новый Структура("ДанныеРаздела", НовыйРаздел));
			КонецЦикла;
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

Функция НайтиГТДПоПараметрам(Номер, Страна, ЭтоРНПТ = Ложь, СоздаватьЕслиНеНашли = Ложь)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГТД.Ссылка КАК ГТД
	|ИЗ
	|	Справочник.ГТД КАК ГТД
	|ГДЕ
	|	ГТД.Наименование = &Наименование
	|	И ГТД.Страна = &Страна
	|	И ГТД.ПометкаУдаления = ЛОЖЬ
	|	И ГТД.РНПТ = &РНПТ");
	
	Запрос.УстановитьПараметр("Наименование", Номер);
	Запрос.УстановитьПараметр("Страна", Страна);
	Запрос.УстановитьПараметр("РНПТ", ЭтоРНПТ);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат ?(НЕ СоздаватьЕслиНеНашли, Неопределено, СоздатьГТДПоПараметрам(Номер, Страна, ЭтоРНПТ));
	КонецЕсли;
	
	Возврат Результат.Выгрузить()[0].ГТД;
	
КонецФункции

Функция СоздатьГТДПоПараметрам(Номер, Страна, ЭтоРНПТ)
	
	НовыйГТД = Справочники.ГТД.СоздатьЭлемент();
	НовыйГТД.УстановитьНовыйКод();
	НовыйГТД.Наименование = Номер;
	НовыйГТД.Страна = Страна;
	НовыйГТД.РНПТ = ЭтоРНПТ;
	
	Попытка
		НовыйГТД.ОбменДанными.Загрузка = Истина;
		НовыйГТД.Записать();
		Возврат НовыйГТД.Ссылка;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРОВЕДЕНИЯ ОБЪЕКТА

Функция ПровестиПоПартиям(РежимПроведения, ДокументСсылка) Экспорт
	
	Отказ = Ложь;
	
	ШапкаДокумента = ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ = НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;	
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда Возврат НЕ Отказ; КонецЕсли;

	
	Если ДокументСсылка.ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт Тогда
		Отказ = НЕ ПровестиПоТоварам(ШапкаДокумента, РежимПроведения) ИЛИ Отказ;
	Иначе
		Отказ = НЕ ПровестиПоАвтомобилям(ШапкаДокумента, РежимПроведения) ИЛИ Отказ;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

Функция ПровестиПоТоварам(ШапкаДокумента, РежимПроведения)
	
	Отказ = Ложь;
	
	// получим данные по товарам и ГТД
	РезультатЗапросаПоТоварам = ПолучитьТаблицуТоваров(ШапкаДокумента);
	
	Если РезультатЗапросаПоТоварам.Пустой() Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	НаборПартии = Движения.ПартииТоваровКомпании;
	НаборГТД = Движения.ГТДПартийТоваровКомпании;
	НаборГТД.Загрузить(ПолучитьТаблицуСписываемыхГТДТоваров(ШапкаДокумента));
	
	// подготовим валюты и курсы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл, ШапкаДокумента.Дата);
	КурсРегл = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаУпр, ШапкаДокумента.Дата);
	КурсУпр = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	ВыборкаСуммы = РезультатЗапросаПоТоварам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаСуммы.Следующий(); ОбщаяТаможеннаяСтоимость = ВыборкаСуммы.ТаможеннаяСтоимость;
	СуммаЗаЕд = ?(ОбщаяТаможеннаяСтоимость = 0, 0, СуммаТаможенногоСбора/ОбщаяТаможеннаяСтоимость);
	
	ВыборкаТоваров = ВыборкаСуммы.Выбрать();
	
	СписанныеПартии = ""; ОшибочныеПартии = "";
	
	РаспределяемаяСумма = СуммаТаможенногоСбора;
	
	Пока ВыборкаТоваров.Следующий() Цикл
		Если ВыборкаТоваров.ПартияСписана Тогда
			дкДобавитьОшибку(СписанныеПартии,Строка(ВыборкаТоваров.Партия));
			Продолжить;
		ИначеЕсли ВыборкаТоваров.ПоПартииБылиДвижения Тогда
			дкДобавитьОшибку(ОшибочныеПартии, Строка(ВыборкаТоваров.Партия));
			Продолжить;
		КонецЕсли;
		
		НоваяЗаписьПартии = НаборПартии.Добавить();
		НоваяЗаписьПартии.Регистратор                = ШапкаДокумента.Ссылка;
		НоваяЗаписьПартии.Период                     = ШапкаДокумента.Дата;
		НоваяЗаписьПартии.ВидДвижения                = ВидДвиженияНакопления.Приход;
		НоваяЗаписьПартии.Номенклатура               = ВыборкаТоваров.Номенклатура;
		НоваяЗаписьПартии.ХарактеристикаНоменклатуры = ВыборкаТоваров.ХарактеристикаНоменклатуры;
		НоваяЗаписьПартии.СкладКомпании              = ВыборкаТоваров.Склад;
		НоваяЗаписьПартии.Партия                     = ВыборкаТоваров.Партия;
		НоваяЗаписьПартии.СтатусПартии               = ВыборкаТоваров.СтатусПартии;
		НоваяЗаписьПартии.ХозОперация                = ШапкаДокумента.ХозОперация;
		
		// посчитаем суммы
		РасчетнаяСумма      = Окр(ВыборкаТоваров.ТаможеннаяСтоимость*СуммаЗаЕд, 2);
		РаспределяемаяСумма = РаспределяемаяСумма - РасчетнаяСумма;		
		
		НоваяЗаписьПартии.Сумма    = обПересчет(ВыборкаТоваров.СуммаВсего+РасчетнаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл);
		НоваяЗаписьПартии.СуммаУпр = обПересчет(ВыборкаТоваров.СуммаВсего+РасчетнаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр);
		НоваяЗаписьПартии.СуммаНДС = обПересчет(ВыборкаТоваров.СуммаНДС, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл);
		
		// установим ГТД по товару
		НоваяЗаписьГТД = НаборГТД.Добавить();
		НоваяЗаписьГТД.Регистратор                = ШапкаДокумента.Ссылка;
		НоваяЗаписьГТД.Период                     = ШапкаДокумента.Дата;
		НоваяЗаписьГТД.ВидДвижения                = ВидДвиженияНакопления.Приход;
		НоваяЗаписьГТД.ХозОперация                = ШапкаДокумента.ХозОперация;
		НоваяЗаписьГТД.Номенклатура               = ВыборкаТоваров.Номенклатура;
		НоваяЗаписьГТД.ХарактеристикаНоменклатуры = ВыборкаТоваров.ХарактеристикаНоменклатуры;
		НоваяЗаписьГТД.СкладКомпании              = ВыборкаТоваров.Склад;
		НоваяЗаписьГТД.Партия                     = ВыборкаТоваров.Партия;
		НоваяЗаписьГТД.ГТД                        = ВыборкаТоваров.ГТД;
		НоваяЗаписьГТД.Количество                 = ВыборкаТоваров.Количество;		
	КонецЦикла;
	
	// если не распределилась сумма в 0 тогда, закинем корректировку на последнюю строку
	Если РаспределяемаяСумма <> 0 И ПустаяСтрока(СписанныеПартии) И ПустаяСтрока(ОшибочныеПартии) Тогда
		НоваяЗаписьПартии.Сумма    = НоваяЗаписьПартии.Сумма + обПересчет(РаспределяемаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл);
		НоваяЗаписьПартии.СуммаУпр = НоваяЗаписьПартии.СуммаУпр + обПересчет(РаспределяемаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр);
	КонецЕсли;
	
	НаборЗаписейДиР = Движения.ДоходыИРасходы;

	БалансовоеПодразделениеДоговора = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение);
	БалансовоеПодразделениеШапки    = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеКомпании);
	
	// БАЛАНС: определим необходимость формирования корректирующих проводок по партиям.
	Если ВедетсяБалансПоПодразделению И Движения.ПартииТоваровКомпании.Количество()> 0 Тогда
		
		ДвиженияПартииТоваровКомпании = Движения.ПартииТоваровКомпании.Выгрузить(,"СкладКомпании, СуммаУпр");
		ДвиженияПартииТоваровКомпании.Свернуть("СкладКомпании", "СуммаУпр");
		ДвиженияПартииТоваровКомпании.Колонки.Добавить("Подразделение");
		
		Для Каждого ТекущаяСтрока Из ДвиженияПартииТоваровКомпании Цикл
			ТекущаяСтрока.Подразделение = ТекущаяСтрока.СкладКомпании.Подразделение;
		КонецЦикла;
		
		ДвиженияПартииТоваровКомпании.Свернуть("Подразделение", "СуммаУпр");
		
		СуммаКорректировки = 0;
		Для Каждого ТекущаяСтрока Из ДвиженияПартииТоваровКомпании Цикл
			Если ТекущаяСтрока.СуммаУпр = 0 Тогда Продолжить; КонецЕсли;
			
			ПодразделениеСклада = ТекущаяСтрока.Подразделение;
			БалансовоеПодразделениеСклада = обПолучитьБалансовоеПодразделение(ПодразделениеСклада);
			
			Если БалансовоеПодразделениеДоговора = БалансовоеПодразделениеСклада Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ПодразделениеСклада;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Доход                  = ТекущаяСтрока.СуммаУпр;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			СуммаКорректировки = СуммаКорректировки + ТекущаяСтрока.СуммаУпр;
		КонецЦикла;
		
		// БАЛАНС: определим необходимость формирования корректирующих проводок по взаиморасчетам.
		Если (НЕ СуммаКорректировки = 0) Тогда
			
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Расход                 = СуммаКорректировки;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
		КонецЕсли;
	КонецЕсли;	

	//
	Если НЕ ПустаяСтрока(СписанныеПартии) Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Партия(-и) была(-и) расформирована(-ы). Проведение невозможно:'")+Символы.ПС+СписанныеПартии, СтатусСообщения.ОченьВажное);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ОшибочныеПартии) Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'По партии(-ям) были движения проведение невозможно:'")+Символы.ПС+ОшибочныеПартии, СтатусСообщения.ОченьВажное);
	КонецЕсли;
	
	// двигаем границу последовательности партий
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если РежимПроведения<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ШапкаДокумента.Ссылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ШапкаДокумента.МоментВремени;
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если РежимПроведения<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;

	
	Возврат НЕ Отказ;
	
КонецФункции

Функция ПровестиПоАвтомобилям(ШапкаДокумента, РежимПроведения)
	
	Отказ = Ложь;
	
	// получим данные по автомобилю
	РезультатЗапросаПоАвтомобилям = ПолучитьТаблицуАвтомобилей(ШапкаДокумента);
	
	Если РезультатЗапросаПоАвтомобилям.Пустой() Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	НаборАвтомобилей = Движения.ОстаткиАвтомобилей;
	
	// подготовим валюты и курсы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл, ШапкаДокумента.Дата);
	КурсРегл = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаУпр, ШапкаДокумента.Дата);
	КурсУпр = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	ОшибочныеАвтомобили = "";
	
	Если ВедетсяБалансПоПодразделению Тогда
		ТаблицаБаланса = Новый ТаблицаЗначений;
		ТаблицаБаланса.Колонки.Добавить("Подразделение");
		ТаблицаБаланса.Колонки.Добавить("СуммаУпр");
	КонецЕсли;
	
	ВыборкаСуммы = РезультатЗапросаПоАвтомобилям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаСуммы.Следующий(); ОбщаяТаможеннаяСтоимость = ВыборкаСуммы.ТаможеннаяСтоимость;
	СуммаЗаЕд = ?(ОбщаяТаможеннаяСтоимость = 0, 0, СуммаТаможенногоСбора/ОбщаяТаможеннаяСтоимость);
	
	ВыборкаАвтомобили = ВыборкаСуммы.Выбрать();
	
	РаспределяемаяСумма = СуммаТаможенногоСбора;
	
	Пока ВыборкаАвтомобили.Следующий() Цикл
		Если НЕ ВыборкаАвтомобили.ЕстьВНаличии Тогда
			дкДобавитьОшибку(ОшибочныеАвтомобили, "Автомобиль " + ВыборкаАвтомобили.Автомобиль + " склад " + ВыборкаАвтомобили.СкладКомпании);
			Отказ = Истина;
			Продолжить;
		КонецЕсли;
		
		// если разные ГТД то сделаем корректирующие движение
		Если НЕ ВыборкаАвтомобили.ГТДСовпадают Тогда
			КорректирующаяЗапись = НаборАвтомобилей.Добавить();
			ЗаполнитьЗначенияСвойств(КорректирующаяЗапись, ВыборкаАвтомобили);
			КорректирующаяЗапись.Регистратор = ШапкаДокумента.Ссылка;
			КорректирующаяЗапись.Период      = ШапкаДокумента.Дата;
			КорректирующаяЗапись.ХозОперация = ШапкаДокумента.ХозОперация;
			КорректирующаяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		КонецЕсли;
		
		НоваяЗапись = НаборАвтомобилей.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаАвтомобили, "Автомобиль,СкладКомпании,СтатусПартии,Партия");
		
		НоваяЗапись.Регистратор = ШапкаДокумента.Ссылка;
		НоваяЗапись.Период      = ШапкаДокумента.Дата;
		НоваяЗапись.ХозОперация = ШапкаДокумента.ХозОперация;
		НоваяЗапись.ГТД         = ВыборкаАвтомобили.ГТДНовое;
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		
		НоваяЗапись.Количество = ?(НЕ ВыборкаАвтомобили.ГТДСовпадают, 1, 0);
		
		// посчитаем суммы
		РасчетнаяСумма      = Окр(ВыборкаАвтомобили.ТаможеннаяСтоимость*СуммаЗаЕд, 2);
		РаспределяемаяСумма = РаспределяемаяСумма - РасчетнаяСумма;		
				
		НоваяЗапись.Сумма    = обПересчет(ВыборкаАвтомобили.СуммаВсегоДок+РасчетнаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл)
					     +?(НЕ ВыборкаАвтомобили.ГТДСовпадают, -КорректирующаяЗапись.Сумма, 0);
		НоваяЗапись.СуммаУпр = обПересчет(ВыборкаАвтомобили.СуммаВсегоДок+РасчетнаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр)
					     +?(НЕ ВыборкаАвтомобили.ГТДСовпадают, -КорректирующаяЗапись.СуммаУпр, 0);
		НоваяЗапись.СуммаНДС = обПересчет(ВыборкаАвтомобили.СуммаНДСДок, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл)
					     +?(НЕ ВыборкаАвтомобили.ГТДСовпадают, -КорректирующаяЗапись.СуммаНДС, 0);
		
		Если ВедетсяБалансПоПодразделению Тогда
			СтрокаБаланса = ТаблицаБаланса.Добавить();
			СтрокаБаланса.Подразделение = ВыборкаАвтомобили.СкладКомпании.Подразделение;
			СтрокаБаланса.СуммаУпр      = НоваяЗапись.СуммаУпр;
		КонецЕсли;		
	КонецЦикла;
	
	// если не распределилась сумма в 0 тогда, закинем корректировку на последнюю строку
	Если РаспределяемаяСумма <> 0 Тогда
		НоваяЗапись.Сумма    = НоваяЗапись.Сумма + обПересчет(РаспределяемаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаРегл, КурсРегл);
		НоваяЗапись.СуммаУпр = НоваяЗапись.СуммаУпр + обПересчет(РаспределяемаяСумма, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр);
	КонецЕсли;
	
	Если Отказ Тогда
		Сообщить(НСтр("ru = 'Автомобиля(-ей) нет в наличии на указанном складе:'")+Символы.ПС+ОшибочныеАвтомобили, СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
	
	НаборЗаписейДиР = Движения.ДоходыИРасходы;

	БалансовоеПодразделениеДоговора = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение);
	БалансовоеПодразделениеШапки    = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеКомпании);
	
	// БАЛАНС: определим необходимость формирования корректирующих проводок по партиям.
	Если ВедетсяБалансПоПодразделению И Движения.ПартииТоваровКомпании.Количество()> 0 Тогда
			
		ТаблицаБаланса.Свернуть("Подразделение", "СуммаУпр");
		
		СуммаКорректировки = 0;
		Для Каждого ТекущаяСтрока Из ТаблицаБаланса Цикл
			Если ТекущаяСтрока.СуммаУпр = 0 Тогда Продолжить; КонецЕсли;
			
			ПодразделениеСклада = ТекущаяСтрока.Подразделение;
			БалансовоеПодразделениеСклада = обПолучитьБалансовоеПодразделение(ПодразделениеСклада);
			
			Если БалансовоеПодразделениеДоговора = БалансовоеПодразделениеСклада Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ПодразделениеСклада;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Доход                  = ТекущаяСтрока.СуммаУпр;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			СуммаКорректировки = СуммаКорректировки + ТекущаяСтрока.СуммаУпр;
		КонецЦикла;
		
		// БАЛАНС: определим необходимость формирования корректирующих проводок по взаиморасчетам.
		Если (НЕ СуммаКорректировки = 0) Тогда
			
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Расход                 = СуммаКорректировки;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
		КонецЕсли;
	КонецЕсли;	
	
	Возврат НЕ Отказ;
	
КонецФункции

Функция ПолучитьТаблицуТоваров(ШапкаДокумента)
	
	Менеджер = Новый МенеджерВременныхТаблиц;
	
	// корректирование себестоимости товара на остатках
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаможеннаяДекларацияИмпортТовары.Номенклатура,
	|	ТаможеннаяДекларацияИмпортТовары.ХарактеристикаНоменклатуры,
	|	СУММА(ТаможеннаяДекларацияИмпортТовары.КоличествоБазовое) КАК Количество,
	|	СУММА(ТаможеннаяДекларацияИмпортТовары.СуммаПошлины) КАК СуммаПошлины,
	|	СУММА(ТаможеннаяДекларацияИмпортТовары.СуммаНДС) КАК СуммаНДС,
	|	ТаможеннаяДекларацияИмпортТовары.Партия,
	|	ТаможеннаяДекларацияИмпортТовары.Склад,
	|	ТаможеннаяДекларацияИмпортТовары.ГТД,
	|	ТаможеннаяДекларацияИмпортТовары.СуммаПошлины + ТаможеннаяДекларацияИмпортТовары.СуммаНДС КАК СуммаВсего,
	|	ТаможеннаяДекларацияИмпортТовары.ТаможеннаяСтоимость
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
	|ГДЕ
	|	ТаможеннаяДекларацияИмпортТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаможеннаяДекларацияИмпортТовары.Номенклатура,
	|	ТаможеннаяДекларацияИмпортТовары.Склад,
	|	ТаможеннаяДекларацияИмпортТовары.ХарактеристикаНоменклатуры,
	|	ТаможеннаяДекларацияИмпортТовары.Партия,
	|	ТаможеннаяДекларацияИмпортТовары.ГТД,
	|	ТаможеннаяДекларацияИмпортТовары.СуммаПошлины + ТаможеннаяДекларацияИмпортТовары.СуммаНДС,
	|	ТаможеннаяДекларацияИмпортТовары.ТаможеннаяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпании.СкладКомпании,
	|	ПартииТоваровКомпании.Партия,
	|	ПартииТоваровКомпании.Количество КАК КоличествоНачОстаток
	|ПОМЕСТИТЬ НачальныеОстатки
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	(ПартииТоваровКомпании.Номенклатура, ПартииТоваровКомпании.ХарактеристикаНоменклатуры, ПартииТоваровКомпании.СкладКомпании, ПартииТоваровКомпании.Партия) В
	|			(ВЫБРАТЬ
	|				ТаблицаДокумента.Номенклатура,
	|				ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|				ТаблицаДокумента.Склад,
	|				ТаблицаДокумента.Партия
	|			ИЗ
	|				ТаблицаДокумента КАК ТаблицаДокумента)
	|	И (ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.КорректировкаПоступления
	|			ИЛИ ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ПоступлениеТоваров)
	|	И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|	ТаблицаДокумента.Склад,
	|	ТаблицаДокумента.Партия,
	|	ТаблицаДокумента.ГТД,
	|	ТаблицаДокумента.Количество,
	|	ТаблицаДокумента.СуммаПошлины,
	|	ТаблицаДокумента.СуммаНДС,
	|	ТаблицаДокумента.СуммаВсего,
	|	ЕСТЬNULL(НачальныеОстатки.КоличествоНачОстаток, 0) КАК КоличествоНачОстаток,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ПартииТоваровКомпанииОстатки.СтатусПартии,
	|	ТаблицаДокумента.ТаможеннаяСтоимость
	|ПОМЕСТИТЬ ИтоговаяТаблица
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачальныеОстатки КАК НачальныеОстатки
	|		ПО ТаблицаДокумента.Номенклатура = НачальныеОстатки.Номенклатура
	|			И ТаблицаДокумента.ХарактеристикаНоменклатуры = НачальныеОстатки.ХарактеристикаНоменклатуры
	|			И ТаблицаДокумента.Склад = НачальныеОстатки.СкладКомпании
	|			И ТаблицаДокумента.Партия = НачальныеОстатки.Партия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(
	|				&Момент,
	|				(Номенклатура, ХарактеристикаНоменклатуры, СкладКомпании, Партия) В
	|					(ВЫБРАТЬ
	|						ТаблицаДокумента.Номенклатура,
	|						ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|						ТаблицаДокумента.Склад,
	|						ТаблицаДокумента.Партия
	|					ИЗ
	|						ТаблицаДокумента КАК ТаблицаДокумента)) КАК ПартииТоваровКомпанииОстатки
	|		ПО ТаблицаДокумента.Номенклатура = ПартииТоваровКомпанииОстатки.Номенклатура
	|			И ТаблицаДокумента.ХарактеристикаНоменклатуры = ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|			И ТаблицаДокумента.Склад = ПартииТоваровКомпанииОстатки.СкладКомпании
	|			И ТаблицаДокумента.Партия = ПартииТоваровКомпанииОстатки.Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НачальныеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтоговаяТаблица.Номенклатура,
	|	ИтоговаяТаблица.ХарактеристикаНоменклатуры,
	|	ИтоговаяТаблица.Склад,
	|	ИтоговаяТаблица.Партия,
	|	ИтоговаяТаблица.ГТД,
	|	ИтоговаяТаблица.Количество,
	|	ИтоговаяТаблица.СуммаПошлины,
	|	ИтоговаяТаблица.СуммаНДС,
	|	ИтоговаяТаблица.СуммаВсего,
	|	ВЫБОР
	|		КОГДА ИтоговаяТаблица.КоличествоНачОстаток = ИтоговаяТаблица.КоличествоОстаток
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПоПартииБылиДвижения,
	|	ВЫБОР
	|		КОГДА ИтоговаяТаблица.КоличествоОстаток = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПартияСписана,
	|	ИтоговаяТаблица.СтатусПартии,
	|	ИтоговаяТаблица.ТаможеннаяСтоимость КАК ТаможеннаяСтоимость
	|ИЗ
	|	ИтоговаяТаблица КАК ИтоговаяТаблица
	|ИТОГИ
	|	СУММА(ТаможеннаяСтоимость)
	|ПО
	|	ОБЩИЕ";
	Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("Момент", ШапкаДокумента.МоментВремени);
		
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПолучитьТаблицуСписываемыхГТДТоваров(ШапкаДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГТДПартийТоваровКомпанииОстатки.СкладКомпании,
	|	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
	|	ГТДПартийТоваровКомпанииОстатки.Партия,
	|	ГТДПартийТоваровКомпанииОстатки.ГТД,
	|	-ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
	|	&ХозОперация,
	|	&Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ИСТИНА КАК Активность,
	|	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	|			&Момент,
	|			(Номенклатура, ХарактеристикаНоменклатуры, Партия) В
	|				(ВЫБРАТЬ
	|					ТаможеннаяДекларацияИмпортТовары.Номенклатура,
	|					ТаможеннаяДекларацияИмпортТовары.ХарактеристикаНоменклатуры,
	|					ТаможеннаяДекларацияИмпортТовары.Партия
	|				ИЗ
	|					Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
	|				ГДЕ
	|					ТаможеннаяДекларацияИмпортТовары.Ссылка = &Ссылка)) КАК ГТДПартийТоваровКомпанииОстатки";
	Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("Момент", ШапкаДокумента.МоментВремени);
	Запрос.УстановитьПараметр("ХозОперация", ШапкаДокумента.ХозОперация);
	Запрос.УстановитьПараметр("Период", ШапкаДокумента.Дата);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьТаблицуАвтомобилей(ШапкаДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаможеннаяДекларацияИмпортАвтомобили.Автомобиль,
	|	ТаможеннаяДекларацияИмпортАвтомобили.ГТД,
	|	ТаможеннаяДекларацияИмпортАвтомобили.ТаможеннаяСтоимость,
	|	ТаможеннаяДекларацияИмпортАвтомобили.СуммаПошлины,
	|	ТаможеннаяДекларацияИмпортАвтомобили.СуммаНДС,
	|	ТаможеннаяДекларацияИмпортАвтомобили.СуммаНДС + ТаможеннаяДекларацияИмпортАвтомобили.СуммаПошлины КАК СуммаВсего,
	|	ТаможеннаяДекларацияИмпортАвтомобили.Склад
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Автомобили КАК ТаможеннаяДекларацияИмпортАвтомобили
	|ГДЕ
	|	ТаможеннаяДекларацияИмпортАвтомобили.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Автомобиль,
	|	ТаблицаДокумента.ГТД КАК ГТДНовое,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДСДок,
	|	ТаблицаДокумента.СуммаВсего КАК СуммаВсегоДок,
	|	ОстаткиАвтомобилейОстатки.СтатусПартии,
	|	ОстаткиАвтомобилейОстатки.Партия,
	|	ОстаткиАвтомобилейОстатки.ГТД,
	|	-ЕСТЬNULL(ОстаткиАвтомобилейОстатки.КоличествоОстаток, 0) КАК Количество,
	|	-ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаОстаток, 0) КАК Сумма,
	|	-ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
	|	-ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаНДСОстаток, 0) КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиАвтомобилейОстатки.КоличествоОстаток, 0) = 0
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьВНаличии,
	|	ВЫБОР
	|		КОГДА ОстаткиАвтомобилейОстатки.ГТД ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОстаткиАвтомобилейОстатки.ГТД = ТаблицаДокумента.ГТД
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ГТДСовпадают,
	|	ТаблицаДокумента.Склад КАК СкладКомпании,
	|	ТаблицаДокумента.ТаможеннаяСтоимость КАК ТаможеннаяСтоимость
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Остатки(
	|				&Момент,
	|				(Автомобиль, СкладКомпании) В
	|					(ВЫБРАТЬ
	|						ТаблицаДокумента.Автомобиль,
	|						ТаблицаДокумента.Склад
	|					ИЗ
	|						ТаблицаДокумента КАК ТаблицаДокумента)) КАК ОстаткиАвтомобилейОстатки
	|		ПО ТаблицаДокумента.Автомобиль = ОстаткиАвтомобилейОстатки.Автомобиль
	|			И ТаблицаДокумента.Склад = ОстаткиАвтомобилейОстатки.СкладКомпании
	|ИТОГИ
	|	СУММА(ТаможеннаяСтоимость)
	|ПО
	|	ОБЩИЕ";
	Запрос.УстановитьПараметр("Момент", ШапкаДокумента.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
	
	Возврат Запрос.Выполнить();
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка = Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	
	Возврат Новый Структура(Перечисления.ВидыНоменклатуры.Услуга, 0);
	
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары = Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ТаможеннаяСтоимость",1);
	ОбязательныеТовары.Вставить("Партия",3);
	ОбязательныеТовары.Вставить("Склад",1);
	ОбязательныеТовары.Вставить("СтранаПроисхождения",3);
	ОбязательныеТовары.Вставить("НомерДляСФ",3);
	ОбязательныеТовары.Вставить("НомерРаздела",1);
	
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили = Новый Структура();
	ОбязательныеАвтомобили.Вставить("НомерРаздела", 1);
	ОбязательныеАвтомобили.Вставить("Автомобиль", 3);
	ОбязательныеАвтомобили.Вставить("ТаможеннаяСтоимость", 1);
	ОбязательныеАвтомобили.Вставить("Склад", 1);
	ОбязательныеАвтомобили.Вставить("СтранаПроисхождения", 1);
	ОбязательныеАвтомобили.Вставить("НомерДляСФ", 3);	
	
	ОбязательныеРазделы = Новый Структура();
	ОбязательныеРазделы.Вставить("НомерРаздела", 3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("НомерДекларации",1);
	Если (СуммаТаможенногоСбора + СуммаТаможенногоШтрафа) > 0. Тогда
		ОбязательныеРеквизиты.Вставить("СтатьяДоходовИРасходов",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Автомобили", ОбязательныеАвтомобили);
	ОбязательныеРеквизиты.Вставить("Разделы", ОбязательныеРазделы);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект, КонтролируемыеРеквизиты, Ошибки) и Результат;
		КонецЕсли;
	КонецЕсли;
	
	
	// проверка правильности ввода сумм
	Если Результат Тогда
		Таблица = ?(ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт, Товары, Автомобили);
		
		СписокРазделов = "";
		Для Каждого СтрокаРаздела Из Разделы Цикл
			НайденныеСтроки = Таблица.НайтиСтроки(Новый Структура("НомерРаздела", СтрокаРаздела.НомерРаздела));
			СтруктураСумм = Новый Структура("СуммаПошлины,СуммаНДС", СтрокаРаздела.СуммаПошлины, СтрокаРаздела.СуммаНДС);
			Для Каждого Строка Из НайденныеСтроки Цикл
				СтруктураСумм.СуммаПошлины = СтруктураСумм.СуммаПошлины - Строка.СуммаПошлины; 
				СтруктураСумм.СуммаНДС     = СтруктураСумм.СуммаНДС - Строка.СуммаНДС;
			КонецЦикла;
			
			Если СтруктураСумм.СуммаПошлины <> 0 ИЛИ СтруктураСумм.СуммаНДС <> 0 Тогда
				СписокРазделов = СписокРазделов +?(ПустаяСтрока(СписокРазделов), "",", ") + СтрокаРаздела.НомерРаздела;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ПустаяСтрока(СписокРазделов) Тогда
			дкДобавитьОшибку(Ошибки, СтрЗаменить(НСтр("ru = 'Для раздела(-ов) -%1- суммы не совподают с итоговой суммой состава.'"), "%1", СписокРазделов));
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпортАвтомобилей Тогда
		дкПроверитьКорректностьРНПТ(ЭтотОбъект, "Автомобили", "Автомобиль");
	Иначе
		дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ТаможеннаяДекларацияИмпорт",			"Таможенная декларация (импорт)",Истина);
	СписокХозОпераций.Добавить("ТаможеннаяДекларацияИмпортАвтомобилей",	"Таможенная декларация (импорт автомобилей)");
		
	Возврат СписокХозОпераций;
	
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя, ТекСтрока = Неопределено, ЭтаФорма = Неопределено, ДопПараметры = Неопределено) Экспорт
	
	Если Имя = "НомерДекларации" Тогда
		Для Каждого Строка Из Товары Цикл
			Строка.НомерДляСФ = Документы.ТаможеннаяДекларацияИмпорт.НомерДляСФ(ЭтотОбъект.НомерДекларации, Строка.НомерСтроки);
		КонецЦикла;
		
		Для Каждого Строка Из Автомобили Цикл
			Строка.НомерДляСФ = Документы.ТаможеннаяДекларацияИмпорт.НомерДляСФ(ЭтотОбъект.НомерДекларации, Строка.НомерСтроки);
		КонецЦикла;
	ИначеЕсли Имя = "КурсДокумента" Тогда
		ТребуетсяПересчет = Ложь;
		Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("ТребуетсяПересчет", ТребуетсяПересчет) И ТребуетсяПересчет Тогда
			ТаблицаИмя = ?(ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт, "Товары", "Автомобили");
			Таблица = ЭтотОбъект[ТаблицаИмя];
			Для Каждого СтрокаРаздела Из Разделы Цикл
				СтрокаРаздела.ТаможеннаяСтоимость = обПересчет(СтрокаРаздела.ТаможеннаяСтоимость, ДопПараметры.СтараяВалюта, ДопПараметры.СтарыйКурс, ВалютаДокумента, КурсДокумента);
				ОбработкаРеквизита("Разделы.ТаможеннаяСтоимость", СтрокаРаздела);
				
				НайденныеСтроки = Таблица.НайтиСтроки(Новый Структура("НомерРаздела", СтрокаРаздела.НомерРаздела));
				ДанныеРаздела = Новый Структура("ДанныеРаздела", СтрокаРаздела);
				Для Каждого Строка Из НайденныеСтроки Цикл
					Строка.ТаможеннаяСтоимость = обПересчет(Строка.ТаможеннаяСтоимость, ДопПараметры.СтараяВалюта, ДопПараметры.СтарыйКурс, ВалютаДокумента, КурсДокумента);
					ОбработкаРеквизита(ТаблицаИмя+".ТаможеннаяСтоимость", Строка,, ДанныеРаздела);
				КонецЦикла;				
			КонецЦикла;
			
			СуммаТаможенногоСбора  = обПересчет(СуммаТаможенногоСбора, ДопПараметры.СтараяВалюта, ДопПараметры.СтарыйКурс, ВалютаДокумента, КурсДокумента);
			СуммаТаможенногоШтрафа = обПересчет(СуммаТаможенногоШтрафа, ДопПараметры.СтараяВалюта, ДопПараметры.СтарыйКурс, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
	ИначеЕсли Имя = "Контрагент" Тогда
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
		КодТаможни = обПолучитьЗначениеСвойства(Контрагент, ПланыВидовХарактеристик.СвойстваОбъектов.КодТаможни, "");
		Возврат Ложь;
	ИначеЕсли Имя = "Разделы.ТаможеннаяСтоимость"  Тогда
		ТекСтрока.СуммаПошлины = ТекСтрока.ТаможеннаяСтоимость * ТекСтрока.СтавкаПошлины / 100.;
		ТекСтрока.СуммаНДС     = (ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ТекСтрока.СтавкаНДС.Ставка/100.);
		
		Возврат Ложь;
	ИначеЕсли Имя = "Разделы.СтавкаПошлины" Тогда
		ТекСтрока.СуммаПошлины = ТекСтрока.ТаможеннаяСтоимость * ТекСтрока.СтавкаПошлины / 100.;
		ТекСтрока.СуммаНДС     = (ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ТекСтрока.СтавкаНДС.Ставка/100.);
		
		ЭтоИмпортТоваров = (ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт);
		
		ДанныеРаздела = Новый Структура("ДанныеРаздела", ТекСтрока);
		
		// получим подчиненные разделу строки
		НайденныеСтроки = ?(ЭтоИмпортТоваров, Товары, Автомобили).НайтиСтроки(Новый Структура("НомерРаздела", ТекСтрока.НомерРаздела));
		Для Каждого Строка Из НайденныеСтроки Цикл
			ОбработкаРеквизита(?(ЭтоИмпортТоваров, "Товары", "Автомобили")+".ТаможеннаяСтоимость", Строка, ЭтаФорма, ДанныеРаздела);
		КонецЦикла;
		
		Возврат Ложь;
	ИначеЕсли Имя = "Разделы.СуммаПошлины" Тогда
		ТекСтрока.СтавкаПошлины = 100. * ?(ТекСтрока.ТаможеннаяСтоимость=0., 0., ТекСтрока.СуммаПошлины / ТекСтрока.ТаможеннаяСтоимость);
		ТекСтрока.СуммаНДС      = (ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ТекСтрока.СтавкаНДС.Ставка/100.);
		
		Возврат Ложь;
	ИначеЕсли Имя = "Разделы.СтавкаНДС" Тогда
		ТекСтрока.СуммаНДС = (ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ТекСтрока.СтавкаНДС.Ставка/100.);
		
		Возврат Ложь;
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		
		Рез = Истина;
		// Если добавлен набор - разложим его
		Если ТекСтрока.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор Тогда
			Набор = ТекСтрока.Номенклатура;
			КоличествоНаборов = 1;
			НомерРаздела = ТекСтрока.НомерРаздела;
			Склад = ТекСтрока.Склад;
			СтранаПроисхождения = ТекСтрока.СтранаПроисхождения;
			ВвестиКоличествоНаборов = Истина;
			Попытка
				Если НЕ ДопПараметры.Свойство("ВвестиКоличествоНаборов",ВвестиКоличествоНаборов) Тогда
					ВвестиКоличествоНаборов = Истина;
				КонецЕсли;
			Исключение
			КонецПопытки;
			ВвестиКоличествоНабора = ?(ВвестиКоличествоНаборов = Неопределено, Истина, ВвестиКоличествоНаборов);
			Попытка
				КоличествоНаборов = ТекСтрока.Количество;
			Исключение
				ВвестиКоличествоНаборов = Ложь;
			КонецПопытки;
			Если ВвестиКоличествоНаборов Тогда
				#Если Клиент Тогда
					Если НЕ ВвестиЧисло(КоличествоНаборов, "Введите количество наборов", 2) Тогда
						КоличествоНаборов = 0;
					КонецЕсли;
				#КонецЕсли
			КонецЕсли;
			Товары.Удалить(ТекСтрока);
			Если КоличествоНаборов = 0 Тогда Возврат Ложь; КонецЕсли;
			Для Каждого ТоварИзНабора Из Набор.СоставНабора Цикл
				Если ТоварИзНабора.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор Тогда
					//Набор в наборе ... игнорируем
					#Если Клиент Тогда
						Сообщить("В наборе """ + СокрЛП(Набор) + """ присутствует набор """ + СокрЛП(ТоварИзНабора.Номенклатура) + """. Добавление набора """ + СокрЛП(ТоварИзНабора.Номенклатура) + """ отменено.",СтатусСообщения.Внимание);
					#КонецЕсли
				Иначе
					//Поищем номенклатуру из набора в таблице товаров
					МассивСтрок = Товары.НайтиСтроки(Новый Структура("Номенклатура,НомерРаздела",ТоварИзНабора.Номенклатура,НомерРаздела));
					Если МассивСтрок.Количество() > 0 Тогда
						СтрокаТЧ = МассивСтрок[0];
						СтрокаТЧ.Количество = СтрокаТЧ.Количество + (ТоварИзНабора.Количество*КоличествоНаборов/?(СтрокаТЧ.Коэффициент = 0,1,СтрокаТЧ.Коэффициент));
					Иначе
						СтрокаТЧ = Товары.Добавить();
						СтрокаТЧ.Номенклатура = ТоварИзНабора.Номенклатура;
						СтрокаТЧ.НомерРаздела = НомерРаздела;
						СтрокаТЧ.Склад        = Склад;
						СтрокаТЧ.СтранаПроисхождения = СтранаПроисхождения;
						ТекРез = ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаТЧ,ЭтаФорма,ДопПараметры);
						Если НЕ ТипЗнч(ТекРез) = Тип("Булево") Тогда
							ТекРез = Истина;
						КонецЕсли;
						Рез = ТекРез И Рез;
						Попытка
							Коэффициент = ?(СтрокаТЧ.Коэффициент = 0,1,СтрокаТЧ.Коэффициент);
						Исключение
							Коэффициент = 1;
						КонецПопытки; 
							СтрокаТЧ.Количество = ТоварИзНабора.Количество*КоличествоНаборов/Коэффициент;
						КонецЕсли;
						Рез = ЭтотОбъект.ОбработкаРеквизита("Товары.Количество",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.ТаможеннаяСтоимость" Тогда
		ТекСтрока.СуммаПошлины = ТекСтрока.ТаможеннаяСтоимость * ДопПараметры.ДанныеРаздела.СтавкаПошлины / 100.;
		ТекСтрока.СуммаНДС     = ?(ДопПараметры.ДанныеРаздела.СтавкаНДС = Неопределено, 0, (ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ДопПараметры.ДанныеРаздела.СтавкаНДС.Ставка/100.));
		
		ОбработкаРеквизита("ЗаполнитьИтогиПоРазделу", ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Возврат Ложь;
	ИначеЕсли Имя = "Товары.СуммаПошлины" Тогда
		ТекСтрока.СуммаНДС = ?(ДопПараметры.ДанныеРаздела.СтавкаНДС = Неопределено, 0,(ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ДопПараметры.ДанныеРаздела.СтавкаНДС.Ставка/100.));
		
		ОбработкаРеквизита("ЗаполнитьИтогиПоРазделу", ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Возврат Ложь;
	ИначеЕсли Имя = "Автомобили.ТаможеннаяСтоимость" Тогда
		ТекСтрока.СуммаПошлины = ТекСтрока.ТаможеннаяСтоимость * ДопПараметры.ДанныеРаздела.СтавкаПошлины / 100.;
		ТекСтрока.СуммаНДС     = ?(ДопПараметры.ДанныеРаздела.СтавкаНДС = Неопределено, 0, (ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ДопПараметры.ДанныеРаздела.СтавкаНДС.Ставка/100.));
		
		ОбработкаРеквизита("ЗаполнитьИтогиПоРазделу", ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Возврат Ложь;
	ИначеЕсли Имя = "Автомобили.СуммаПошлины" Тогда
		ТекСтрока.СуммаНДС = ?(ДопПараметры.ДанныеРаздела.СтавкаНДС = Неопределено, 0,(ТекСтрока.СуммаПошлины + ТекСтрока.ТаможеннаяСтоимость)*(ДопПараметры.ДанныеРаздела.СтавкаНДС.Ставка/100.));
		
		ОбработкаРеквизита("ЗаполнитьИтогиПоРазделу", ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Возврат Ложь;
	ИначеЕсли Имя = "ЗаполнитьИтогиПоРазделу" Тогда
		ДанныеРаздела = Неопределено; общТаможеннаяСтоимость = 0; общСуммаПошлины = 0; общСуммаНДС = 0;
		
		Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("ДанныеРаздела", ДанныеРаздела) И ДанныеРаздела <> Неопределено Тогда
			Отбор = Новый Структура("НомерРаздела", ТекСтрока.НомерРаздела);
			
			НайденныеТовары = Товары.НайтиСтроки(Отбор);
			Для Каждого Товар Из НайденныеТовары Цикл
				общТаможеннаяСтоимость = общТаможеннаяСтоимость + Товар.ТаможеннаяСтоимость;
				общСуммаПошлины        = общСуммаПошлины + Товар.СуммаПошлины;
				общСуммаНДС            = общСуммаНДС + Товар.СуммаНДС;
			КонецЦикла;
			
			НайденныеАвтомобили = Автомобили.НайтиСтроки(Отбор);
			Для Каждого Автомобиль Из НайденныеАвтомобили Цикл
				общТаможеннаяСтоимость = общТаможеннаяСтоимость + Автомобиль.ТаможеннаяСтоимость;
				общСуммаПошлины        = общСуммаПошлины + Автомобиль.СуммаПошлины;
				общСуммаНДС            = общСуммаНДС + Автомобиль.СуммаНДС;
			КонецЦикла;
			
			ДанныеРаздела.ТаможеннаяСтоимость = общТаможеннаяСтоимость;
			ДанныеРаздела.СуммаПошлины        = общСуммаПошлины;
			ДанныеРаздела.СуммаНДС            = общСуммаНДС;
		КонецЕсли;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

Процедура ОбработкаЗаполнения(Основание, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,, Ложь) Тогда Возврат; КонецЕсли;
	
	ЗаполнитьПоПоступлениюТоваров(Основание);
	ЗаполнитьПоПоступлениюАвтомобилей(Основание);
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// почистим ненужную ТЧ
	Таблица = ?(ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт, Автомобили, Товары);
	Таблица.Очистить();
	
	// посчитаем сумму документа
	СуммаДокумента = СуммаТаможенногоСбора + СуммаТаможенногоШтрафа + Разделы.Итог("СуммаПошлины") + Разделы.Итог("СуммаНДС");
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	// заполним справочники ГТД
	Если НЕ Отказ Тогда
	
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
		ОшибочныеНомераСФ = "";
		
		Таблица = ?(ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт, Товары, Автомобили);
		ИмяНоменклатуры = ?(ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт, "Номенклатура", "Автомобиль");
		Для Каждого СтрТаблицы Из Таблица Цикл
			Если ЗначениеЗаполнено(СтрТаблицы.ГТД) Тогда
				Продолжить;
			КонецЕсли;
			
			ПрослеживаемыйТовар = СтрТаблицы[ИмяНоменклатуры].Прослеживаемый;
			
			// поищем ГТД
			НайденныйГТД = НайтиГТДПоПараметрам(СтрТаблицы.НомерДляСФ, СтрТаблицы.СтранаПроисхождения, ПрослеживаемыйТовар, Истина);
			Если НайденныйГТД = Неопределено Тогда
				дкДобавитьОшибку(ОшибочныеНомераСФ,СтрТаблицы.НомерДляСФ);
			Иначе
				СтрТаблицы.ГТД = НайденныйГТД;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ПустаяСтрока(ОшибочныеНомераСФ) Тогда
			Отказ = Истина;
			Сообщить(НСтр("ru = 'Не удалось записать ГТД с номерами:'") + Символы.ПС + ОшибочныеНомераСФ);
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	ПодразделениеСклад                 = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
	
	// выполним движения по взаиморачетам с контрагентом
	НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
	
	НаборЗаписейВзаиморасчеты.ДокументОбъект                   = ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения                  = Режим;
	НаборЗаписейВзаиморасчеты.Контрагент                       = Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов            = ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем        = Ложь;
	НаборЗаписейВзаиморасчеты.Сумма                            = СуммаДокумента;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
	
	Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// проведем партии товаров и ГТД
	Отказ = НЕ ПровестиПоПартиям(Режим, Ссылка) ИЛИ Отказ;
	
	// доходы и расходы по таможне
	НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
	
	НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте             = (ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = СтатьяДоходовИРасходов;
	НаборЗаписейДоходыИРасходы.Расход                 = СуммаТаможенногоШтрафа;
	
	СуммаУслугТаможни = СуммаТаможенногоШтрафа; 
	Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	
	//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
	Если ВедетсяБалансПоПодразделению И (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКомпании) Тогда
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДоходыИРасходы.Расход                 = СуммаУслугТаможни;
		
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = ПодразделениеКомпании;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             =(ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДоходыИРасходы.Доход                  = СуммаУслугТаможни;
		
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;

#Если Клиент Тогда
Права = глПрава;
#КонецЕсли    

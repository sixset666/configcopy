// Модуль документа Комплектация

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ДобавлятьСтрокуВЗН Экспорт;	// Флаг необходимости добавления строки комплектации в заказ-наряд

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	//++СнимченкоАА_бизтех++
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	//--СнимченкоАА_бизтех--
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Комплект",1);
	Если ХозОперация=Справочники.ХозОперации.КомплектацияВПроизводство Тогда
		ОбязательныеРеквизиты.Вставить("Цех",1);
		ОбязательныеРеквизиты.Вставить("КомплектЕдиницаИзмерения",1);
		ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	КонецЕсли;
	
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	УчетВедется = Комплект.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ Комплект.ТипНоменклатуры.ИспользованиеХарактеристик = 2;
	Если УчетВедется Тогда
		ОбязательныеРеквизиты.Вставить("ХарактеристикаКомплекта",1);
	КонецЕсли;
	
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	//++СнимченкоАА_бизтех++
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	//--СнимченкоАА_бизтех-- 
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
		
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	//++СнимченкоАА_бизтех++
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	//--СнимченкоАА_бизтех--
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Комплектация","Комплектация",Истина);
	СписокХозОпераций.Добавить("КомплектацияВПроизводство","Комплектация в производство",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  Режим          - число, 0-состав комплекта, 1-сам комплект,
//  ШапкаДокумента - ДокументСсылка - текущий документ, либо выборка.
//
// Возвращаемое значение:
//  Возвращает результат выборки.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента,Режим=0)
	Запрос=Новый Запрос();
	Если Режим=1 Тогда
		// КОМПЛЕКТ
	    ТекстЗапроса="
		|ВЫБРАТЬ
		|	Комплектация.Комплект КАК Номенклатура,
		|	Комплектация.ХарактеристикаКомплекта КАК ХарактеристикаНоменклатуры,
		|	Комплектация.ЦенаКомплектаРозничная КАК ЦенаРозничная,
		|	Комплектация.КоличествоКомплектов * Комплектация.КомплектЕдиницаИзмерения.Коэффициент КАК Количество,
		|	Комплектация.КоличествоКомплектов*Комплектация.ЦенаКомплекта КАК СуммаВсего,";
		Если ШапкаДокумента.СкладКомпании.Розничный Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	Комплектация.КоличествоКомплектов*Комплектация.ЦенаКомплектаРозничная КАК СуммаРозн,";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|	0 КАК СуммаРозн,";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|	0 КАК СуммаНДС
		|ИЗ
		|	Документ.Комплектация КАК Комплектация
	    |ГДЕ
		|	Комплектация.Ссылка=&Ссылка
		|";
	Иначе
		// СОСТАВ КОМПЛЕКТА
	    ТекстЗапроса="
		|ВЫБРАТЬ
		|	КомплектацияТовары.Номенклатура КАК Номенклатура,
		|	КомплектацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	КомплектацияТовары.ЦенаРозничная КАК ЦенаРозничная,
		|	КомплектацияТовары.Количество*КомплектацияТовары.Коэффициент"+?(НЕ СписаниеПоТЧ,("*&КоличествоКомплектов"),"")+" КАК Количество,";
		Если ШапкаДокумента.СкладКомпании.Розничный Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	КомплектацияТовары.СуммаРозничная КАК СуммаРозн,";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|	0 КАК СуммаРозн,";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|	КомплектацияТовары.Сумма КАК СуммаВсего,
		|	0 КАК СуммаНДС
		|
		|ИЗ
		|	Документ.Комплектация.Товары КАК КомплектацияТовары
	    |ГДЕ
		|	КомплектацияТовары.Ссылка=&Ссылка
		|";
		Запрос.УстановитьПараметр("КоличествоКомплектов",ШапкаДокумента.КоличествоКомплектов);
	КонецЕсли;

	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.КоличествоКомплектов КАК КоличествоКомплектов,
	|	Док.СкладКомпании КАК СкладКомпании,
	|   Док.СкладКомпании.Подразделение КАК ПодразделениеСклада,
	|	Док.Цех КАК Цех
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// получим результаты запроса по составу комплекта
	РезультатЗапросаПоТоварамСостав=ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента);
	// у нас нет доп. расходов
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;	
	
	// 1. Списываем состав
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварамСостав;
	НаборЗаписейПартии.СтатусПартии=Неопределено;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;	
	Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	ТаблицаСписания=НаборЗаписейПартии.Выгрузить();
	СуммаНДСКомплектующих=ТаблицаСписания.Итог("СуммаНДС");
	
	// 2. спишем израсходованные комиссионные товары
	ТаблицаСписанияПринятые=ТаблицаСписания.Скопировать();
	СтрокаСписанияКупленный=ТаблицаСписанияПринятые.Найти(Перечисления.СтатусыПартий.ТоварКупленный,"СтатусПартии");
	Пока СтрокаСписанияКупленный<>Неопределено Цикл
		ТаблицаСписанияПринятые.Удалить(СтрокаСписанияКупленный);
		СтрокаСписанияКупленный=ТаблицаСписанияПринятые.Найти(Перечисления.СтатусыПартий.ТоварКупленный,"СтатусПартии");
	КонецЦикла;
	Если ТаблицаСписанияПринятые.Количество()>0 Тогда
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Списание=Истина;
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоТоварам=ТаблицаСписанияПринятые;
		НаборЗаписейРеализованныеТовары.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
	КонецЕсли; 
	
	// Подготовим таблицу движений в разрезе подразделений комиссионных товаров
	ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
	ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");
	
	ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		Если БалансВедетсяПоПодразделениям Тогда
			СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
		Иначе
			СтрокаСписания.Подразделение = ПодразделениеКомпании;
		КонецЕсли;
	КонецЦикла;	
	ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
	
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;	
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЦикла; 
	
	
	// получим результаты запроса по комплекту
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Комплектация.Комплект КАК Номенклатура,
	|	Комплектация.ХарактеристикаКомплекта КАК ХарактеристикаНоменклатуры,
	|	Комплектация.КоличествоКомплектов * Комплектация.КомплектЕдиницаИзмерения.Коэффициент КАК Количество,
	|	Комплектация.КоличествоКомплектов * Комплектация.ЦенаКомплекта КАК СуммаВсего,
	|	"+?(ШапкаДокумента.СкладКомпании.Розничный,"Комплектация.КоличествоКомплектов*Комплектация.ЦенаКомплекта","0")+" КАК СуммаРозн,
	|	Комплектация.Комплект.СтавкаНДС КАК СтавкаНДС,
	|	&СуммаНДСКомплектующих КАК СуммаНДС
	|ИЗ
	|	Документ.Комплектация КАК Комплектация
	|ГДЕ
	|	Комплектация.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("СуммаНДСКомплектующих",СуммаНДСКомплектующих);
	ТаблицаТоваров=Запрос.Выполнить().Выгрузить();
	Если ТаблицаТоваров.Количество()>0 Тогда		
		Если КомплектацияПоСебестоимости ИЛИ (ТаблицаТоваров[0].СуммаВсего=0 И ТипЗнч(ТаблицаСписания)=Тип("ТаблицаЗначений")) Тогда
			Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				ТаблицаТоваров[0].СуммаВсего = ТаблицаСписания.Итог("Сумма");
				ТаблицаТоваров[0].СуммаВсего = обПересчет(ТаблицаТоваров[0].СуммаВсего,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),Дата,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента);
			Иначе
				ТаблицаТоваров[0].СуммаВсего = ТаблицаСписания.Итог("СуммаУпр");
				ТаблицаТоваров[0].СуммаВсего = обПересчет(ТаблицаТоваров[0].СуммаВсего,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),Дата,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	СуммаДокумента=ТаблицаТоваров[0].СуммаВсего;
	ЦенаКомплекта=Окр(СуммаДокумента/КоличествоКомплектов,2);
	
	// 3. Приходуем комплект
	Если ХозОперация = Справочники.ХозОперации.Комплектация Тогда
		НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
		НаборЗаписейПартии.РезультатЗапросаПоТоварам=ТаблицаТоваров;
		НаборЗаписейПартии.ЕстьСтавкаНДС=Истина;
		Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
	Иначе
		// добавим комплект в производство
		НаборЗаписейПроизводство=Движения.ТоварыВПроизводстве;
		НаборЗаписейПроизводство.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПроизводство.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейПроизводство.РезультатЗапросаПоТоварам=ТаблицаТоваров;
		НаборЗаписейПроизводство.ЕстьСтавкаНДС=Истина;
		Отказ=НЕ НаборЗаписейПроизводство.ПриходКомплекта() ИЛИ Отказ;
		
		Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказНарядБыл) Тогда
			// если идет допроведение, то надо получить те значения которые были раньше
			Если Ссылка<>ДокументСсылка Тогда
				Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаряд ИЗ РегистрНакопления.ТоварыВПроизводстве ГДЕ Регистратор=&Ссылка");
				Запрос.УстановитьПараметр("Ссылка",Ссылка);
				Результат = Запрос.Выполнить(); ЗаказНарядБыл=Неопределено;
				Если НЕ Результат.Пустой() Тогда
					ЗаказНарядБыл = Результат.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд");
				КонецЕсли;
				ДополнительныеСвойства.Вставить("ЗаказНарядБыл",ЗаказНарядБыл);
				ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
			КонецЕсли;
			
			НаборЗаписейГраницыПроизводство = РегистрыСведений.ГраницыПроизводство.СоздатьНаборЗаписей();
			Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
				НаборЗаписейГраницыПроизводство.ЗаказНаряд     		= ШапкаДокумента.ДокументОснование;
				НаборЗаписейГраницыПроизводство.МоментВремени  		= ШапкаДокумента.Дата;
				НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= ДополнительныеСвойства.ЗаказНарядБыл;
				НаборЗаписейГраницыПроизводство.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
			Иначе
				НаборЗаписейГраницыПроизводство.ЗаказНаряд			= ДополнительныеСвойства.ЗаказНарядБыл;
				НаборЗаписейГраницыПроизводство.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
				НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= Неопределено;
				НаборЗаписейГраницыПроизводство.МоментВремениБыл	= Неопределено;
			КонецЕсли;
			НаборЗаписейГраницыПроизводство.ДокументСсылка	= ШапкаДокумента.Ссылка;
			НаборЗаписейГраницыПроизводство.ИзменитьГраницу();
		КонецЕсли;
	КонецЕсли;
	
	// Доходы и расходы.
	// Найдем разницу между Приходом и Расходам (только для товаров собственных)
	ТабДиРПроизводство = Движения.ТоварыВПроизводстве.Выгрузить();
	ТаблицаДиР=НаборЗаписейПартии.Выгрузить();
	ТабДиРПроизводство.Свернуть("СтатусПартии, ВидДвижения","СуммаУпр");
	ТаблицаДиР.Свернуть("СтатусПартии, ВидДвижения","СуммаУпр");
	
	ПриходСумма = 0;
	РасходСумма = 0;

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СтатусПартии", Перечисления.СтатусыПартий.ТоварКупленный);
	СтруктураПоиска.Вставить("ВидДвижения",  ВидДвиженияНакопления.Приход);
	// Приход
	МассивСтрок = ?(ХозОперация=Справочники.ХозОперации.Комплектация,ТаблицаДиР.НайтиСтроки(СтруктураПоиска),ТабДиРПроизводство.НайтиСтроки(СтруктураПоиска));
	Если МассивСтрок.Количество()<>0 Тогда // Строка будет одна.
		ПриходСумма = МассивСтрок[0].СуммаУпр;
	КонецЕсли;
	
	СтруктураПоиска.Вставить("ВидДвижения",  ВидДвиженияНакопления.Расход);
	// Расход
	МассивСтрок = ТаблицаДиР.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрок.Количество()<>0 Тогда // Строка будет одна.
		РасходСумма = МассивСтрок[0].СуммаУпр;
	КонецЕсли;
    		
	Если ПриходСумма<>РасходСумма Тогда
		НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.ПодразделениеСклада;
		КонецЕсли;
		НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.ОтклонениеСебестоимостиПриКомплектации;
		НаборЗаписейДиР.ВУпрВалюте				= Истина; 
		
		Разница = ПриходСумма-РасходСумма;
		Если Разница > 0 Тогда
			НаборЗаписейДиР.Доход  = Разница;
		Иначе
			НаборЗаписейДиР.Расход = -Разница;
		КонецЕсли; 

		Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли; 
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего() Экспорт
	Если Проведен И КомплектацияПоСебестоимости Тогда
		Запрос=Новый Запрос("ВЫБРАТЬ
		                    |	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма,
		                    |	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаУпр
		                    |ИЗ
		                    |	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		                    |ГДЕ
		                    |	ПартииТоваровКомпании.Регистратор = &Регистратор
		                    |	И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)");
		Запрос.УстановитьПараметр("Регистратор",Ссылка);
		РезультатЗапроса=Запрос.Выполнить().Выгрузить();
		Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			СуммаСписания = РезультатЗапроса.Итог("Сумма");
			Возврат обПересчет(СуммаСписания,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),Дата,ВалютаДокумента,КурсДокумента);
		Иначе
			СуммаСписания = РезультатЗапроса.Итог("СуммаУпр");
			Возврат обПересчет(СуммаСписания,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),Дата,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
	КонецЕсли;
	Возврат ЦенаКомплекта*КоличествоКомплектов;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="СкладКомпании" Тогда
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			// заполняем ячейки
			// комплекта
			Если обЗначениеНеЗаполнено(Комплект) Тогда
				Ячейка = Справочники.ЯчейкиХранения.ПустаяСсылка();
			Иначе
				Ячейка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(Комплект,СкладКомпании);
			КонецЕсли;
			
			// Товаров
			Для Каждого стрТовары Из Товары Цикл
				Если НЕ обЗначениеНеЗаполнено(стрТовары.Номенклатура) Тогда
					стрТовары.Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.Номенклатура,СкладКомпании);
				Иначе
					стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
			
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Рез=ОбработкаРеквизита("УстановитьРозничнуюЦенуКомплекта",,ЭтаФорма,ДопПараметры);
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма,ДопПараметры) И Рез;
				КонецЦикла;
				Возврат Рез;
			КонецЕсли;
		Иначе
			Ячейка = Справочники.ЯчейкиХранения.ПустаяСсылка();
			Для Каждого стрТовары Из Товары Цикл
				стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЦикла;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Комплект" Тогда
		Если НЕ обЗначениеНеЗаполнено(Комплект) Тогда
			Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			ВПроизводство=(ХозОперация=Справочники.ХозОперации.КомплектацияВПроизводство);
			Если НЕ ВПроизводство Тогда
				Если Комплект.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Комплект Тогда
					#Если Клиент Тогда
					Предупреждение("Необходимо выбирать только комплекты !",10);
					#КонецЕсли
					Комплект=Неопределено;
					Возврат Ложь;
				КонецЕсли;
			Иначе
				Если Комплект.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Комплект И Комплект.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.ЛКМ Тогда
					#Если Клиент Тогда
					Предупреждение("Необходимо выбирать только краски !",10);
					#КонецЕсли
					Комплект=Неопределено;
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			
			// заполням ячейку
			Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(Комплект,СкладКомпании);
			
			Если (НЕ обЗначениеНеЗаполнено(ХарактеристикаКомплекта)) И (ХарактеристикаКомплекта.Владелец<>Комплект) И (ХарактеристикаКомплекта.Владелец<>Комплект.ТипНоменклатуры) Тогда
				ХарактеристикаКомплекта=Неопределено;
			КонецЕсли;
			
			Если КомплектацияПоСебестоимости Тогда
				ЦенаКомплекта=0;
			Иначе
				ЦенаКомплекта=обПолучитьЦену(ТипЦен,Комплект,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ХарактеристикаКомплекта,, ПодразделениеКомпании);
			КонецЕсли;
			ЦенаКомплектаРозничная=0;
			Если НЕ ВПроизводство Тогда
				ОбработкаРеквизита("УстановитьРозничнуюЦенуКомплекта",,ЭтаФорма,ДопПараметры);
			КонецЕсли;
			КоличествоКомплектов=1;			
		Иначе
			ЦенаКомплекта=0;
			ЦенаКомплектаРозничная=0;
			КоличествоКомплектов=0;
			ХарактеристикаКомплекта=Неопределено;
			Товары.Очистить();
			Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="КомплектацияПоСебестоимости" Тогда
		Если КомплектацияПоСебестоимости Тогда
			ЦенаКомплекта=0;
		Иначе
			ЦенаКомплекта=обПолучитьЦену(ТипЦен,Комплект,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ХарактеристикаКомплекта,, ПодразделениеКомпании);
		КонецЕсли;
		Возврат ОбработкаРеквизита("ЦенаКомплекта",СтрокаТоваров,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="КоличествоКомплектов" Тогда
		Рез = Истина;
		Для Каждого СтрокаТоваров Из Товары Цикл
			Рез = ОбработкаРеквизита("Товары.Количество",СтрокаТоваров,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ЦенаКомплекта" Тогда
		Рез = Истина;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ХарактеристикаКомплекта" Тогда
		ИзменятьКоличество = (НЕ обЗначениеНеЗаполнено(ХарактеристикаКомплекта)) И КоличествоКомплектов <> 1
							И Комплект.ТипНоменклатуры.УникальностьСерийногоНомера;
		Если ИзменятьКоличество Тогда
			КоличествоКомплектов = 1;
			#Если Клиент Тогда
			Сообщить("Для характеристики комплекта """+СокрЛП(Комплект)+""" установлена уникальность серийного номера. Количество не может быть не равным 1.");
			#КонецЕсли
			Возврат ОбработкаРеквизита("КоличествоКомплектов",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Истина;
		
	// ТОВАРЫ
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл ,КурсРегл, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
			Рез = ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат Рез;
	
	ИначеЕсли Имя="Товары.Количество" Тогда
		
		//Если ХозОперация = Справочники.ХозОперации.КомплектацияВПроизводство Тогда
		//	// подгодовим количество
		//	КоличествоВсего = 0;
		//	Для Каждого стрТов Из Товары Цикл
		//		КоличествоВсего = КоличествоВсего + (стрТов.Количество*стрТов.Коэффициент);
		//	КонецЦикла;
		//	КоличествоКомплектов = КоличествоВсего/?(обЗначениеНеЗаполнено(КомплектЕдиницаИзмерения.Коэффициент),1,КомплектЕдиницаИзмерения.Коэффициент);
		//КонецЕсли;
		
		Рез = ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		РезРозн = ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Цена" Тогда	
		//ТекСтрока.Сумма = ТекСтрока.Количество*ТекСтрока.Коэффициент*ТекСтрока.Цена*КоличествоКомплектов;
		ТекСтрока.Сумма = ТекСтрока.Количество*ТекСтрока.Коэффициент*ТекСтрока.Цена;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		//ТекКоличество = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов;
		ТекКоличество = ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Если ТекКоличество <> 0 Тогда
			ТекСтрока.Цена = ТекСтрока.Сумма/ТекКоличество;	
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда		
		//ТекСтрока.СуммаРозничная = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов*ТекСтрока.ЦенаРозничная;
		ТекСтрока.СуммаРозничная = ТекСтрока.Количество*ТекСтрока.Коэффициент*ТекСтрока.ЦенаРозничная;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		//ТекКоличество = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов;
		ТекКоличество = ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Если ТекКоличество <> 0 Тогда
			ТекСтрока.ЦенаРозничная = ТекСтрока.СуммаРозничная/ТекКоличество;	
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) Тогда
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл ,КурсРегл, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюЦенуКомплекта" Тогда
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(Комплект)) Тогда
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ЦенаКомплектаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,Комплект,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл,КурсРегл, ХарактеристикаКомплекта,, СкладКомпании.Подразделение);
		КонецЕсли;
		Возврат Истина;
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

Процедура ПровестиИзмененияПослеЗаписи(Режим)
	#Если Клиент Тогда
	// уберем файлик заполнения
	Если Режим = РежимЗаписиДокумента.Проведение И ХозОперация=Справочники.ХозОперации.КомплектацияВПроизводство И НЕ ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Если НЕ обЗначениеНеЗаполнено(ПутьКФайлуОбмена) Тогда
			Попытка
				Файл = Новый Файл(ПутьКФайлуОбмена);
				GUID = Новый УникальныйИдентификатор;
				НовоеИмя = Файл.Путь + Строка(ДокументОснование.Номер) + GUID;
				ПереместитьФайл(ПутьКФайлуОбмена, НовоеИмя);
				СделатьЗапись = Истина;
			Исключение
				Сообщить("Не удалось переименовать файл "+ПутьКФайлуОбмена,СтатусСообщения.Внимание);
				СделатьЗапись = Ложь;
			КонецПопытки;
			
			// сделаем запись в журнал регистрации
			Если СделатьЗапись Тогда
				стрСобытие = "Переименование файла.";
				стррКомментарий = "Переименование файла " + Файл.Имя + " в " + Строка(ДокументОснование.Номер) + GUID;
				ЗаписьЖурналаРегистрации(стрСобытие,УровеньЖурналаРегистрации.Информация,,ДокументОснование);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			Если ДобавлятьСтрокуВЗН Тогда
				ФормаАРМ = Неопределено; ФормаДок = Неопределено;
				ДокЗНОбъект = ДокументОснование.ПолучитьОбъект();
				// поиск формы арм
				Если глОткрытаяФормаФункциональнойПанели <> Неопределено Тогда
					СтруктураПоиск = Новый Структура("Имя","АРММашинозаезд");
					МассивСтрок = глОткрытаяФормаФункциональнойПанели.ТаблицаОткрытыхАРМ.НайтиСтроки(СтруктураПоиск);
					Если МассивСтрок.Количество() > 0 Тогда
						ФормаАРМ = МассивСтрок[0].Форма;
						Если НЕ ФормаАРМ.Открыта() Тогда
							ФормаАРМ = Неопределено;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				// поиск формы документа
				Если ФормаАРМ = Неопределено Тогда
					ФормаДок = ДокументОснование.ПолучитьФорму();
					Если ФормаДок.Открыта() Тогда
						ОбъектЗаполнения = ФормаДок;
					Иначе
						ОбъектЗаполнения = ДокЗНОбъект;
						ФормаДок = Неопределено;
					КонецЕсли;
				Иначе
					ОбъектЗаполнения = ФормаАРМ;
				КонецЕсли;
				СтрокиДеталей=ОбъектЗаполнения.Товары.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",Комплект,ХарактеристикаКомплекта));
				СтрокаДетали=Неопределено;
				Для Каждого СтрокаДеталиТоваров Из СтрокиДеталей Цикл
					Если СтрокаДетали=Неопределено Тогда СтрокаДетали=СтрокаДеталиТоваров; КонецЕсли; 
				КонецЦикла;
				Если СтрокаДетали=Неопределено Тогда
					НоваяСтрокаЛКМ = ОбъектЗаполнения.Товары.Добавить();
					НоваяСтрокаЛКМ.Номенклатура = Комплект;
					НоваяСтрокаЛКМ.ХарактеристикаНоменклатуры = ХарактеристикаКомплекта;
					НоваяСтрокаЛКМ.СкладКомпании = СкладКомпании;
					ДопПараметры=Новый Структура("ЗапрашиватьИсточникТовара",Ложь);
					ОбъектЗаполнения.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрокаЛКМ,ФормаДок,ДопПараметры);
					НоваяСтрокаЛКМ.ЕдиницаИзмерения = КомплектЕдиницаИзмерения;
					ОбъектЗаполнения.ОбработкаРеквизита("Товары.ЕдиницаИзмерения",НоваяСтрокаЛКМ,ФормаДок);
					НоваяСтрокаЛКМ.Количество = КоличествоКомплектов;
					ОбъектЗаполнения.ОбработкаРеквизита("Товары.Количество",НоваяСтрокаЛКМ,ФормаДок);
					Если ВалютаДокумента = ОбъектЗаполнения.ВалютаДокумента Тогда
						НоваяСтрокаЛКМ.СуммаВсего = СуммаДокумента;
						ОбъектЗаполнения.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаЛКМ,ФормаДок);
					Иначе
						НоваяСтрокаЛКМ.СуммаВсего = обПересчет(СуммаДокумента,ВалютаДокумента,КурсДокумента,ОбъектЗаполнения.ВалютаДокумента,ОбъектЗаполнения.КурсДокумента);
						ОбъектЗаполнения.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаЛКМ,ФормаДок);
					КонецЕсли;
				Иначе
					СтрокаДетали.Количество = СтрокаДетали.Количество+КоличествоКомплектов;
					ОбъектЗаполнения.ОбработкаРеквизита("Товары.Количество",СтрокаДетали,ФормаДок);
				КонецЕсли;
				ДобавлятьСтрокуВЗН = Ложь;
				Если ФормаДок = Неопределено И ФормаАРМ = Неопределено Тогда
					Попытка
						//ОбъектЗаполнения.ОбменДанными.Загрузка=Истина;
						ОбъектЗаполнения.Записать();
					Исключение
						#Если Клиент Тогда
							Сообщить("Не удалось добавить строку в заказ наряд!",СтатусСообщения.Важное);
						#КонецЕсли
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Комплектация"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКомплектация(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Комплектация");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);

	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеКомплекта	= спПолучитьНаименование(Комплект)+ ", " + Строка(ЭтотОбъект.КоличествоКомплектов) + " " + ЭтотОбъект.Комплект.БазоваяЕдиницаИзмерения;
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма, СуммаВсего",ВалютаДокумента,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		Если СписаниеПоТЧ Тогда
			СтруктураСтроки.Вставить("КоличествоВсего", СтруктураСтроки.Количество);
		Иначе
			СтруктураСтроки.Вставить("КоличествоВсего", Формат(СтруктураСтроки.Количество*КоличествоКомплектов, ФорматВыводаКоличества));
		КонецЕсли; 
		СтруктураСтроки.Вставить("Сумма", Формат(СтрокаТабличнойЧасти.Сумма, ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("СуммаВсего", Формат(СтрокаТабличнойЧасти.Сумма*КоличествоКомплектов, ФорматВыводаСуммы));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма, СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		СтруктураИтоговПоСтранице.Сумма = СтруктураИтоговПоСтранице.Сумма + СтрокаТабличнойЧасти.Сумма;
		СтруктураИтоговПоСтранице.СуммаВсего = СтруктураИтоговПоСтранице.СуммаВсего + СтрокаТабличнойЧасти.Сумма*КоличествоКомплектов;		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	Сумма = ВыборкаТабличнойЧасти.Итог("Сумма");
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма")*КоличествоКомплектов;
	ОбластьПодвал.Параметры.Сумма 		= Формат(Сумма,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаВсего 	= Формат(СуммаВсего,ФорматВыводаСуммы);
	
	ОбластьПодвал.Параметры.ЧислоПозиций = "Всего наименований: "+ВыборкаТабличнойЧасти.Количество();
	ОбластьПодвал.Параметры.СуммаПрописью = "Себестоимость комплекта: " + обЧислоПрописью(Сумма,ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.СуммаПрописьюВсего = "Себестоимость всех комплектующих: " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьКомплектация()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	ВыбСтрока = Неопределено;
	КомплектацияПоСебестоимости=Истина;
		
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Если Основание.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт ИЛИ Основание.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен Тогда
			дкОтменаВводаДокумента(ЭтотОбъект);
			#Если Клиент Тогда
				Предупреждение(НСтр("ru = 'Комплектация деталей в производство на основании выполненных и закрытых заказ-нарядов запрещена!'"));
			#КонецЕсли
			Возврат;
		КонецЕсли;
		Товары.Очистить();
		ХозОперация = Справочники.ХозОперации.КомплектацияВПроизводство;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНарядТовары.Номенклатура                КАК Комплект,
		|	ЗаказНарядТовары.Количество                  КАК КоличествоКомплектов,
		|	ЗаказНарядТовары.ЕдиницаИзмерения            КАК КомплектЕдиницаИзмерения,
		|	ЗаказНарядТовары.Коэффициент,
		|	ЗаказНарядТовары.Сумма                       КАК СуммаРозн,
		|	ЗаказНарядТовары.ХарактеристикаНоменклатуры  КАК ХарактеристикаКомплекта
		|ИЗ
		|	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
		|ГДЕ
		|	ЗаказНарядТовары.Ссылка = &Ссылка
		|	И (ЗаказНарядТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Комплект)
		|			ИЛИ ЗаказНарядТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.ЛКМ))";
		Запрос.УстановитьПараметр("Ссылка", Основание);
		
		ТабТоваров = Запрос.Выполнить().Выгрузить();
		
		Если ТабТоваров.Количество() > 1 Тогда
			#Если Клиент Тогда
				ВыбСтрока = ТабТоваров.ВыбратьСтроку("Выбор комплекта");
			#Иначе
				ВыбСтрока = ТабТоваров[0];
			#КонецЕсли
		ИначеЕсли ТабТоваров.Количество() = 1 Тогда 
			ВыбСтрока = ТабТоваров[0];
		КонецЕсли;
		
		Если НЕ ВыбСтрока = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект,ВыбСтрока);
			ЦенаКомплектаРозничная = ?(ВыбСтрока.КоличествоКомплектов = 0, 0, ВыбСтрока.СуммаРозн/ВыбСтрока.КоличествоКомплектов);
			
			Если НЕ обЗначениеНеЗаполнено(Комплект) Тогда
				Товары.Очистить();
				Для Каждого СтрокаКомплекта Из Комплект.СоставНабора Цикл
					НоваяСтрока=Товары.Добавить();
					НоваяСтрока.Номенклатура=СтрокаКомплекта.Номенклатура;
					ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
					Если НЕ обДопустимаяСтрока(НоваяСтрока) Тогда
						Продолжить;
					КонецЕсли;
					НоваяСтрока.ХарактеристикаНоменклатуры=СтрокаКомплекта.ХарактеристикаНоменклатуры;
					НоваяСтрока.Количество=СтрокаКомплекта.Количество;
					ОбработкаРеквизита("УстановитьРозничнуюЦену",НоваяСтрока);
					ОбработкаРеквизита("Товары.Количество",НоваяСтрока);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли; 
	
	//++СнимченкоАА_бизтех++
	КодыМаркировки.Очистить();
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	ДополнительныеСвойства.Вставить("ЗаказНарядБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Если ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			Если НЕ ЭтотОбъект.Проведен Тогда
				Сообщить("Заказ-наряд закрыт. Комплектация в производство запрещена.");
				Отказ=Истина;
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
				Возврат;
			КонецЕсли;
		ИначеЕсли ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен Тогда
			Если НЕ ЭтотОбъект.Проведен Тогда
				Сообщить("Заказ-наряд выполнен. Комплектация в производство запрещена.");
				Отказ = Истина;
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
	// отработаем право "Запись раньше документа-основания"
	Если (Дата < ДокументОснование.ДатаСоздания) Тогда
		ЗН="Заказ-наряд № "+СокрЛП(ДокументОснование.Номер)+" от "+Формат(ДокументОснование.ДатаСоздания,"ДФ='dd.MM.yyyy ЧЧ:мм:сс'");
		Если обПраво("ЗаписьРаньшеДокументаОснования",Права,,ЭтотОбъект) Тогда
			Если обПраво("ВыводитьСообщенияПроверкиПравДоступа",Права,,ЭтотОбъект) Тогда
				Сообщить("Внимание! Документ <"+Строка(ЭтотОбъект)+"> записывается раньше создания своего документа-основания: <"+ЗН+">",СтатусСообщения.Внимание);
			КонецЕсли;
		Иначе
			Сообщить("Нет прав на запись раньше создания чем документ-основание: <"+ЗН+">");
			Отказ = Истина;
			дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			Возврат;
		КонецЕсли;
	ИначеЕсли (ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт) И (Дата > ДокументОснование.ДатаОкончания) Тогда
		ЗН="Заказ-наряд № "+СокрЛП(ДокументОснование.Номер)+" от "+Формат(ДокументОснование.ДатаОкончания,"ДФ='dd.MM.yyyy ЧЧ:мм:сс'");
		Если обПраво("ЗаписьРаньшеДокументаОснования",Права,,ЭтотОбъект) Тогда
			Если обПраво("ВыводитьСообщенияПроверкиПравДоступа",Права,,ЭтотОбъект) Тогда
				Сообщить("Внимание! Документ <"+Строка(ЭтотОбъект)+"> записывается позже окончания своего документа-основания: <"+ЗН+">",СтатусСообщения.Внимание);
			КонецЕсли;
		Иначе
			Сообщить("Нет прав на запись позже окончания чем документ-основание: <"+ЗН+">");
			Отказ = Истина;
			дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.КомплектацияВПроизводство Тогда
		Если НЕ обПраво("ПеремещениеДеталейВПроизводство",Права,,ЭтотОбъект) Тогда
			Если РежимЗаписи=РежимЗаписиДокумента.Проведение ИЛИ РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения Тогда
				Сообщить("Нет прав на комплектацию в производство в производство!
				|Обратитесь к администратор системы.",СтатусСообщения.Важное);
				Отказ=Истина;
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаряд ИЗ РегистрНакопления.ТоварыВПроизводстве ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказНарядБыл", Результат.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли; 
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --

	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
		 ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
		Сообщить("Заказ-наряд закрыт. Отмена комплектации в производство запрещена.");
		Отказ=Истина; 
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	Если КоличествоКомплектов=0 Тогда
		Отказ = Истина;
		РезультатОбработки = "Не задано количество комплектов";
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	КонецЕсли;
	// 1. Списываем состав
	РезультатЗапросаПоТоварамСостав=ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект);
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварамСостав;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
	Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // дальше смысла не имеет
	КонецЕсли;
	
	// 2. Приходуем комплект
	РезультатЗапросаПоТоварамКомплект=ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект,1);
	Если ХозОперация=Справочники.ХозОперации.Комплектация Тогда
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварамКомплект;
		НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
		Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	//Если комплект приходуется на розничный склад, то нужно поменять цену комплекта
	Если НЕ Отказ И СкладКомпании.Розничный И ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
		
		Запрос = Новый Запрос;			
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Комплектация.Комплект.Код КАК Код,
		|	Комплектация.Комплект КАК Номенклатура,
		|	Комплектация.Комплект.Наименование КАК НоменклатураНаименование,
		|	Комплектация.ЦенаКомплектаРозничная КАК НоваяЦена,";
		Если СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
			Запрос.Текст = Запрос.Текст + "
			|	Комплектация.ХарактеристикаКомплекта КАК ХарактеристикаНоменклатуры,
			|	Комплектация.Комплект.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров КАК УчетЦенТолькоВРазрезеДопПараметров,";
		ИначеЕсли СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
			Запрос.Текст = Запрос.Текст + "
			|	ЗНАЧЕНИЕ(Справочник.ЕдиницаИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
			|	Комплектация.Комплект.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров КАК УчетЦенТолькоВРазрезеДопПараметров,";
		КонецЕсли;

		Запрос.Текст = Запрос.Текст + "
		|	ЕСТЬNULL(ЦеныВБазе.Цена,0) КАК ЦенаВБазе
		|ИЗ
		|	Документ.Комплектация КАК Комплектация
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
		|		&Момент,
		|		ТипЦен=&ТипЦен 
		|			И Контрагент=&Контрагент
		|			И ПодразделениеКомпании=&ПодразделениеКомпании) КАК ЦеныВБазе
		|	ПО Комплектация.Комплект = ЦеныВБазе.Номенклатура И
		|	ЦеныВБазе.Цена > 0 ";
	
		Если СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И Комплектация.ХарактеристикаКомплекта = ЦеныВБазе.ХарактеристикаНоменклатуры";
		ИначеЕсли СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И ЗНАЧЕНИЕ(Справочник.ЕдиницаИзмерения.ПустаяСсылка) = ЦеныВБазе.ЕдиницаИзмерения";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ
		| Комплектация.Ссылка = &Ссылка";

		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Момент", МоментВремени());
		Запрос.УстановитьПараметр("ТипЦен", СкладКомпании.ТипЦенРозничнойТорговли);
		Запрос.УстановитьПараметр("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
		Запрос.УстановитьПараметр("ПодразделениеКомпании", СкладКомпании.Подразделение);
		РезультатЗапросаПоТоварам = Запрос.Выполнить();
		
		// устанавливаем цену комплекта          
		НаборЗаписейЦены = Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент     = Справочники.Контрагенты.ПустаяСсылка();
		НаборЗаписейЦены.ТипЦен         = СкладКомпании.ТипЦенРозничнойТорговли;
		НаборЗаписейЦены.ПодразделениеКомпании     = СкладКомпании.Подразделение;
		НаборЗаписейЦены.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		НаборЗаписейЦены.ПроверятьОдинаковыеЦены   = Ложь;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	//установим нормативные цены компании
	Если НЕ Отказ И ПодразделениеКомпании.ФормироватьНормативнуюЦену И НЕ Справочники.ТипыЦен.НормативнаяЦена.Рассчитывается Тогда
		
		Запрос = Новый Запрос;			
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Комплектация.Комплект.Код КАК Код,
		|	Комплектация.Комплект КАК Номенклатура,
		|	Комплектация.Комплект.Наименование КАК НоменклатураНаименование,
		|	Комплектация.ЦенаКомплекта КАК НоваяЦена,";
		Если СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
			Запрос.Текст = Запрос.Текст + "
			|	Комплектация.ХарактеристикаКомплекта КАК ХарактеристикаНоменклатуры,
			|	Комплектация.Комплект.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров КАК УчетЦенТолькоВРазрезеДопПараметров,";
		ИначеЕсли СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
			Запрос.Текст = Запрос.Текст + "
			|	ЗНАЧЕНИЕ(Справочник.ЕдиницаИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
			|	Комплектация.Комплект.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров КАК УчетЦенТолькоВРазрезеДопПараметров,";
		КонецЕсли;

		Запрос.Текст = Запрос.Текст + "
		|	ЕСТЬNULL(ЦеныВБазе.Цена,0) КАК ЦенаВБазе
		|ИЗ
		|	Документ.Комплектация КАК Комплектация
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
		|		&Момент,
		|		ТипЦен=&ТипЦен 
		|			И Контрагент=&Контрагент
		|			И ПодразделениеКомпании=&ПодразделениеКомпании) КАК ЦеныВБазе
		|	ПО Комплектация.Комплект = ЦеныВБазе.Номенклатура И
		|	ЦеныВБазе.Цена > 0 ";
	
		Если СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И Комплектация.ХарактеристикаКомплекта = ЦеныВБазе.ХарактеристикаНоменклатуры";
		ИначеЕсли СкладКомпании.ТипЦенРозничнойТорговли.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И ЗНАЧЕНИЕ(Справочник.ЕдиницаИзмерения.ПустаяСсылка) = ЦеныВБазе.ЕдиницаИзмерения";
		КонецЕсли;

		Запрос.Текст = Запрос.Текст + "
		|
		|ГДЕ 
		|	Комплектация.Ссылка = &Ссылка
		|	И Комплектация.ЦенаКомплекта > 0";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Момент", МоментВремени());
		Запрос.УстановитьПараметр("ТипЦен", Справочники.ТипыЦен.НормативнаяЦена);
		Запрос.УстановитьПараметр("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
		Запрос.УстановитьПараметр("ПодразделениеКомпании", СкладКомпании.Подразделение);
		РезультатЗапросаПоТоварам = Запрос.Выполнить();
		
		НаборЗаписейЦены = Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент     = Справочники.Контрагенты.ПустаяСсылка();
		НаборЗаписейЦены.ТипЦен         = Справочники.ТипыЦен.НормативнаяЦена;
		НаборЗаписейЦены.ПодразделениеКомпании     = СкладКомпании.Подразделение;
		НаборЗаписейЦены.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		НаборЗаписейЦены.ПроверятьОдинаковыеЦены   = Ложь;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				//Расход
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Количество = тс.Количество;  
				
				Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	Если НЕ Отказ Тогда
		ПровестиИзмененияПослеЗаписи(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
	Если НЕ Отказ И Модифицированность() Тогда
		ЭтотОбъект.ОбменДанными.Загрузка = Истина;
		Записать(РежимЗаписиДокумента.Запись);
		ЭтотОбъект.ОбменДанными.Загрузка = Ложь;
	КонецЕсли;
КонецПроцедуры // ОбработкаПроведения()

  //++СнимченкоАА_бизтех++
Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
  //--СнимченкоАА_бизтех--

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
ДобавлятьСтрокуВЗН = Ложь;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
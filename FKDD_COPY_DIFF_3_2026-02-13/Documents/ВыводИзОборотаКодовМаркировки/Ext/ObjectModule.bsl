// Модуль документа Вывод из оборота кодов маркировки


////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция СостояниеКодаМаркировки()
	
	Результат = Неопределено;
	
	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Возврат Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаВозвращенФизическомуЛицу;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ДляСобственныхНужд Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаИспользованДляСобственныхНуждПредприятия;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Конфискация Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаКонфискация;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Ликвидация Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаЛиквидацияПредприятия;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.РозничнаяПродажа Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Уничтожение Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаПриУничтожении;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.УтратаПовреждение Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаПриУтратеИлиПовреждении;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Экспорт Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаЭкспортированЗаПределыСтранЕАЭС;
	ИначеЕсли ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ЭкспортЕАЭС Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаЭкспортированВСтраныЕАЭС;
	Иначе
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборота;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ДистанционнаяПродажа
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоОбразцам
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.РозничнаяПродажа Тогда
		ОбязательныеТовары.Вставить("Цена",1);
	КонецЕсли;
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ДатаВыводаИзОборота", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("Статус",1);
	ОбязательныеРеквизиты.Вставить("ПричинаВывода",1);
	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ЭкспортЕАЭС
		 И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ПродажаПоГосКонтракту
		 И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ПродажаПоСделкеСГосТайной Тогда
		 ОбязательныеРеквизиты.Вставить("ВидПервичногоДокумента",1);
		 ОбязательныеРеквизиты.Вставить("ДатаПервичногоДокумента",1);
		 ОбязательныеРеквизиты.Вставить("НомерПервичногоДокумента",1);
	КонецЕсли;
	
	Если ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументов.Прочее Тогда
		ОбязательныеРеквизиты.Вставить("НаименованиеПервичногоДокумента", 1);
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.БезвозмезднаяПередача Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент", 1);
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ЭкспортЕАЭС Тогда
		ОбязательныеРеквизиты.Вставить("Импортер", 1);
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Другое Тогда
		ОбязательныеРеквизиты.Вставить("ОписаниеПричины", 1);
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоГосКонтракту
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоСделкеСГосТайной Тогда
		ОбязательныеРеквизиты.Вставить("ИдентификаторГосударственногоКонтракта", 1);
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Возврат
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ДистанционнаяПродажа
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Другое
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Конфискация
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоОбразцам
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.РозничнаяПродажа
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Уничтожение
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.Утилизация
		ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.УтратаПовреждение Тогда
		ОбязательныеРеквизиты.Вставить("АдресМестаВыбытия", 1);
	КонецЕсли;
	
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	ОбязательныеРеквизиты.Вставить("ТоварнаяГруппа",1);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	Результат = Результат И дкПроверитьСоответствиеНоменклатурыТоварнойГруппе(ЭтотОбъект,,, Ошибки);

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.БезвозмезднаяПередача И НЕ Контрагент.Пустая() Тогда
		Если НЕ ЗначениеЗаполнено(Контрагент.ИНН) Тогда
			дкДобавитьОшибку(Ошибки, "У контрагента не указан ИНН.");
        	Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВыводИзОборотаКодовМаркировки", "Вывод из оборота", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	Если Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	КонецЕсли;
	
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

Функция ПолучитьНовуюСтруктуруАдреса() Экспорт
	НоваяСтруктураАдреса = Новый Структура;

	НоваяСтруктураАдреса.Вставить("Страна", "");	
	НоваяСтруктураАдреса.Вставить("Индекс", "");	
	НоваяСтруктураАдреса.Вставить("Регион", "");	
	НоваяСтруктураАдреса.Вставить("Район", "");	
	НоваяСтруктураАдреса.Вставить("Город", "");	
	НоваяСтруктураАдреса.Вставить("НаселенныйПункт", "");	
	НоваяСтруктураАдреса.Вставить("Улица", "");	
	НоваяСтруктураАдреса.Вставить("ТипДома", Перечисления.ТипыДомов.Дом);	
	НоваяСтруктураАдреса.Вставить("Дом", "");	
	НоваяСтруктураАдреса.Вставить("ТипКорпуса", Перечисления.ТипыКорпусов.Корпус);	
	НоваяСтруктураАдреса.Вставить("Корпус", "");	
	НоваяСтруктураАдреса.Вставить("ТипКвартиры", Перечисления.ТипыКвартир.Квартира);	
	НоваяСтруктураАдреса.Вставить("Квартира", "");	
	НоваяСтруктураАдреса.Вставить("Представление", "");	
	НоваяСтруктураАдреса.Вставить("УИНУлицы", "");	
	НоваяСтруктураАдреса.Вставить("УИНДома", "");	
	
	Возврат НоваяСтруктураАдреса;
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события установки нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Статус) Тогда
		Статус = Перечисления.СтатусыВыводаИзОборота.Новый;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаВыводаИзОборота) Тогда
		ДатаВыводаИзОборота = Дата;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		
		КодыМаркировки.Очистить();
		Товары.Очистить();
		
		ДатаВыводаИзОборота = Основание.Дата;
		НаименованиеПервичногоДокумента = Строка(Основание.ХозОперация);
		ДатаПервичногоДокумента = Основание.Дата;
		НомерПервичногоДокумента = Основание.Номер;
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд")
			ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			
			ТипДокументаОснования = ?(ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд"), "ЗаказНаряд", "КорректировкаРеализации");
			
			// Запросом получим коды маркировок
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СостоянияКодовМаркировки.Номенклатура КАК Номенклатура,
			|	СостоянияКодовМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	СостоянияКодовМаркировки.КодМаркировки КАК КодМаркировки,
			|	1 КАК Количество,
			|	ЕСТЬNULL(ДокументОснованиеТовары.СуммаВсего, 0) КАК СуммаВсего
			|ИЗ
			|	РегистрСведений.СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ." + ТипДокументаОснования + ".Товары КАК ДокументОснованиеТовары
			|		ПО СостоянияКодовМаркировки.Номенклатура = ДокументОснованиеТовары.Номенклатура
			|			И СостоянияКодовМаркировки.ХарактеристикаНоменклатуры = ДокументОснованиеТовары.ХарактеристикаНоменклатуры
			|ГДЕ
			|	СостоянияКодовМаркировки.ДокументОснование = &Основание
			|	И СостоянияКодовМаркировки.Состояние В(&Состояние)
			|	И ДокументОснованиеТовары.Ссылка = &Основание";
			Запрос.УстановитьПараметр("Основание", Основание);
			
			СписокСостояний = Новый Массив;
			СписокСостояний.Добавить(Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа);
			СписокСостояний.Добавить(Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаИспользованДляСобственныхНуждПредприятия);
			СписокСостояний.Добавить(Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику);
			
			Запрос.УстановитьПараметр("Состояние", СписокСостояний);
			
			ТаблицаМаркировки = Запрос.Выполнить().Выгрузить();
			
			ТаблицаНоменклатуры = ТаблицаМаркировки.Скопировать();
			ТаблицаНоменклатуры.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,СуммаВсего", "Количество");
			
			// Заполним ТЧ Товары
			Для Каждого ТекущаяСтрока Из ТаблицаНоменклатуры Цикл
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
				ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
				//НоваяСтрока.Цена = ТекущаяСтрока.Цена;
				НоваяСтрока.Цена = Окр(ТекущаяСтрока.СуммаВсего / ТекущаяСтрока.Количество, 2);
				НоваяСтрока = Новый УникальныйИдентификатор;
			КонецЦикла;
			
			// Заполним ТЧ Коды Маркировки
			СтруктураПоискаСтроки = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
			Для Каждого ТекущаяСтрока Из ТаблицаМаркировки Цикл
				НоваяСтрока = КодыМаркировки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
				ЗаполнитьЗначенияСвойств(СтруктураПоискаСтроки, ТекущаяСтрока);
				НоваяСтрока.ИдентификаторТовара = Товары.НайтиСтроки(СтруктураПоискаСтроки)[0].ИдентификаторТовара;
			КонецЦикла;
			
			Если ТипДокументаОснования = "ЗаказНаряд" Тогда
				Если Основание.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
					ДатаВыводаИзОборота = Основание.ДатаЗакрытия;
					ДатаПервичногоДокумента = Основание.ДатаЗакрытия;
				ИначеЕсли ЗначениеЗаполнено(Основание.ДатаОкончания) Тогда
					ДатаВыводаИзОборота = Основание.ДатаОкончания;
					ДатаПервичногоДокумента = Основание.ДатаОкончания;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			Если ТипЗнч(Основание) = Тип("ДокументСсылка.Чек") Тогда
				Если Основание.Хозоперация <> Справочники.ХозОперации.Чек Тогда
					#Если Клиент Тогда
						Сообщить("Вывод из оборота доступно только при вводе на основании чека продажи.", СтатусСообщения.Внимание);
					#КонецЕсли
					Основание=Неопределено;
					ДокументОснование=Неопределено;
					дкОтменаВводаДокумента(ЭтотОбъект);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			// Загрузим все коды маркировоки
			КодыМаркировки.Загрузить(Основание.КодыМаркировки.Выгрузить());
			
			// Пройдемся по ТЧ
			СтруктураПоиска = Новый Структура("ИдентификаторТовара");
			
			Для Каждого ТекущаяСтрока Из Основание.Товары Цикл
				
				СтруктураПоиска.ИдентификаторТовара = ТекущаяСтрока.ИдентификаторТовара;
				НайденныеСтроки = КодыМаркировки.НайтиСтроки(СтруктураПоиска);
				
				КоличествоКодов = НайденныеСтроки.Количество();
				
				// У данной номенклатуры нет кодов маркировки
				// не будем ее выгружать
				Если КоличествоКодов = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
				
				Коэффициент = ?(НоваяСтрока.Коэффициент = 0, 1, НоваяСтрока.Коэффициент);
				Если КоличествоКодов <> НоваяСтрока.Количество * Коэффициент Тогда
					НоваяСтрока.Количество = Окр(КоличествоКодов / Коэффициент, 3);
				КонецЕсли;
				
				ТекКоличество = ТекущаяСтрока.Количество * ТекущаяСтрока.Коэффициент;
				Если ТекКоличество = 0 Тогда
					ТекКоличество = 1;
				КонецЕсли;
				НоваяСтрока.Цена = Окр(ТекущаяСтрока.СуммаВсего / ТекКоличество, 2);

			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого СтрокаТовар Из Товары Цикл
		
		// Заполним доп. поля для товарной строки
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
	КонецЦикла;
	
	Если обЗначениеНеЗаполнено(ТоварнаяГруппа) Тогда
		ТоварнаяГруппа = дкТоварнаяГруппа(Товары.ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;

	СтруктураАдреса = АдресСтруктура.Получить();
	Если ТипЗнч(СтруктураАдреса) <> Тип("Структура") Тогда
		АдресСтруктура = ПолучитьНовуюСтруктуруАдреса();
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Статус = Перечисления.СтатусыВыводаИзОборота.Новый;
	
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ЭкспортЕАЭС Тогда
		ВидПервичногоДокумента = Неопределено;
		НомерПервичногоДокумента = Неопределено;
		ДатаПервичногоДокумента = Неопределено;
	КонецЕсли;

	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.БезвозмезднаяПередача
		 И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ДляСобственныхНужд Тогда
		Контрагент = Неопределено;
	КонецЕсли;

	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ЭкспортЕАЭС Тогда
		Импортер = Неопределено;
	КонецЕсли;
	
	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.Другое Тогда
		ОписаниеПричины = Неопределено;
	КонецЕсли;

	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ПродажаПоГосКонтракту
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ПродажаПоСделкеСГосТайной Тогда
		ИдентификаторГосударственногоКонтракта = Неопределено;
	КонецЕсли;

	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.Возврат
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ДистанционнаяПродажа
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.Другое
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.Конфискация
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ПродажаПоОбразцам
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.РозничнаяПродажа
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.Уничтожение
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.Утилизация
		И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.УтратаПовреждение Тогда
		АдресМестаВыбытия = Неопределено;
	КонецЕсли;

	Если ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ДистанционнаяПродажа
		 И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.ПродажаПоОбразцам
		 И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.РозничнаяПродажа
		 И ПричинаВывода <> Перечисления.ПричиныВыводаИзОборота.БезвозмезднаяПередача Тогда 
		 Для Каждого СтрокаТЧ Из Товары Цикл
			 СтрокаТЧ.Цена = 0;
		 КонецЦикла;
	КонецЕсли;

	Если ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоГосКонтракту
		 ИЛИ ПричинаВывода = Перечисления.ПричиныВыводаИзОборота.ПродажаПоСделкеСГосТайной Тогда
		 ВидПервичногоДокумента = Неопределено;
		 НаименованиеПервичногоДокумента = Неопределено;
		 НомерПервичногоДокумента = Неопределено;
		 ДатаПервичногоДокумента = Неопределено;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		орЖурналСостояний(Ссылка, Статус);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Обработчик события удаления проведения
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события проведения
//
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И Статус = Перечисления.СтатусыВыводаИзОборота.Выполнен Тогда
		
		// Отменим записи состояний документа
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
		
		ТаблицаМаркировки = НаборЗаписейСостоянияКодовМаркировки.ТаблицаКодовМаркировки();
		
		ТаблицаМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
		ТаблицаМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
		
		ОбъектШК = Обработки.ШтрихКоды.Создать();
		
		// Разберем маркировку на состовляющие для поиска
		Для Каждого ТекущийКодМаркировки Из ТаблицаМаркировки Цикл
			
			Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировки) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировки);
			
			Если НЕ СтруктураМаркировки.Разобран Тогда
				Продолжить;
			КонецЕсли;
			
			ТекущийКодМаркировки.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
			ТекущийКодМаркировки.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
			
		КонецЦикла;
		
		ТекущиеСтатусыМаркировки = РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(ТаблицаМаркировки, МоментВремени());
		
		ВыведенныеТовары = Новый Массив;
		СостоянияВОбороте = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
		
		Для Каждого ТекущаяСтрока Из ТекущиеСтатусыМаркировки Цикл
			Если СостоянияВОбороте.Найти(ТекущаяСтрока.Состояние) = Неопределено Тогда
				ВыведенныеТовары.Добавить(ТекущаяСтрока);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекущаяСтрока Из ВыведенныеТовары Цикл
			ТекущиеСтатусыМаркировки.Удалить(ТекущаяСтрока);
		КонецЦикла;
		
		// Определим состояния вывода по причине
		СостояниеВывода = СостояниеКодаМаркировки();
		
		// Изменим состояние маркировки, которые ранее не были выведены
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТекущиеСтатусыМаркировки;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = СостояниеВывода;
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

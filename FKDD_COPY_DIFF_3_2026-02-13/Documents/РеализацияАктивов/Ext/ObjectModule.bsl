// Модуль документа РеализацияАктивов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ВалютаКурсДок Экспорт;        // Кэш валюты и курса документа
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем РезультатЗапросаПоТоварам;
Перем РезультатЗапросаПоАктивам;
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Возвращает результат запроса по товарам, соответствующим активам табличной части
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоТоварам(СписокАвтомобилей)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияАктивовАктивы.ПрочийАктив.Номенклатура КАК Номенклатура,
	               |	РеализацияАктивовАктивы.ПрочийАктив.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	               |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
				   |	ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладКомпании,
				   |	ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка) КАК Партия,
	               |	0 КАК ВидДвижения,
	               |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
	               |	РеализацияАктивовАктивы.СтавкаНДС КАК СтавкаНДС,
	               |	РеализацияАктивовАктивы.Сумма КАК Сумма,
	               |	0 КАК СуммаУпр,
	               |	РеализацияАктивовАктивы.СуммаНДС КАК СуммаНДС,
	               |	0 КАК СуммаСкидки,
	               |	РеализацияАктивовАктивы.Количество
	               |ИЗ
	               |	Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
	               |ГДЕ
	               |	РеализацияАктивовАктивы.Ссылка = &Ссылка
				   |	"+?(СписокАвтомобилей=Неопределено,"","И НЕ РеализацияАктивовАктивы.ПрочийАктив В (&СписокАвтомобилей)")+"
				   |";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СписокАвтомобилей", СписокАвтомобилей);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// сформирует свой результат запроса по активам
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоАктивам()
	ТекстЗапроса = "ВЫБРАТЬ
	               |	РеализацияАктивовАктивы.ПрочийАктив КАК Актив,
	               |	РеализацияАктивовАктивы.Сумма КАК Сумма,
	               |	РеализацияАктивовАктивы.Количество
	               |ИЗ
	               |	Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
	               |ГДЕ
	               |	РеализацияАктивовАктивы.Ссылка = &Ссылка";

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоАктивам()

// Возвращает результат запроса по партиям,соответствующим активам табличной части
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоПартиям(СписокАвтомобилей)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияАктивовАктивы.ПрочийАктив КАК ПрочийАктив,
	               |	РеализацияАктивовАктивы.Количество
	               |ПОМЕСТИТЬ РеализацияАктивовАктивы
	               |ИЗ
	               |	Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
	               |ГДЕ
	               |	РеализацияАктивовАктивы.Ссылка = &Ссылка
				   |	"+?(СписокАвтомобилей=Неопределено,"","И НЕ РеализацияАктивовАктивы.ПрочийАктив В (&СписокАвтомобилей)")+"
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ПрочийАктив
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПрочиеАктивыВЭксплуатации.ПрочийАктив.Номенклатура КАК Номенклатура,
				   |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
				   |	ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладКомпании,
				   |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
				   |	ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка) КАК Партия,
	               |	ВЫБОР
	               |		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток = РеализацияАктивовАктивы.Количество
	               |			ТОГДА ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьОстаток - ПрочиеАктивыВЭксплуатации.СуммаАмортизацииОстаток
	               |		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток<>0 
				   |			ТОГДА ((ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьОстаток - ПрочиеАктивыВЭксплуатации.СуммаАмортизацииОстаток) / ПрочиеАктивыВЭксплуатации.КоличествоОстаток) * РеализацияАктивовАктивы.Количество
				   |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток = РеализацияАктивовАктивы.Количество
	               |			ТОГДА ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпрОстаток - ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпрОстаток
	               |		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток<>0
				   |			ТОГДА ((ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпрОстаток - ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпрОстаток) / ПрочиеАктивыВЭксплуатации.КоличествоОстаток) * РеализацияАктивовАктивы.Количество
				   |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаУпр,
	               |	0 КАК ВидДвижения,
	               |	0 КАК СуммаНДС,
	               |	РеализацияАктивовАктивы.Количество
	               |ИЗ
	               |	РеализацияАктивовАктивы КАК РеализацияАктивовАктивы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(
	               |				&Момент,
	               |				ПрочийАктив В
	               |					(ВЫБРАТЬ
	               |						РеализацияАктивовАктивы.ПрочийАктив
	               |					ИЗ
	               |						РеализацияАктивовАктивы)) КАК ПрочиеАктивыВЭксплуатации
	               |		ПО РеализацияАктивовАктивы.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
    Запрос.УстановитьПараметр("Момент", МоментВремени());
	Запрос.УстановитьПараметр("СписокАвтомобилей", СписокАвтомобилей);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции // ПолучитьРезультатЗапросаПоПартиям()

// Возвращает результат запроса по автомобилям, соответствующим активам табличной части
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоАвтомобилям(Запрос)
	
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияАктивовАктивы.ПрочийАктив КАК ПрочийАктив,
	|	РеализацияАктивовАктивы.Количество КАК Количество,
	|	РеализацияАктивовАктивы.Сумма КАК Сумма,
	|	РеализацияАктивовАктивы.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияАктивовАктивы.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ РеализацияАктивовАктивы
	|ИЗ
	|	Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
	|ГДЕ
	|	РеализацияАктивовАктивы.Ссылка = &Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	ПрочийАктив
	|;
	|ВЫБРАТЬ
	|	ВводВЭксплуатациюАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	|	РеализацияАктивовАктивы.ПрочийАктив КАК ПрочийАктив,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладКомпании,
	|	ЗНАЧЕНИЕ(Документ.ПоступлениеАвтомобилей.ПустаяСсылка) КАК ДокументПоставки,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Поставщик,
	|	0 КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
	|	РеализацияАктивовАктивы.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияАктивовАктивы.Сумма КАК Сумма,
	|	0 КАК СуммаУпр,
	|	РеализацияАктивовАктивы.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаСкидки,
	|	РеализацияАктивовАктивы.Количество КАК Количество,
	|	ЕСТЬNULL(ДвиженияВЭксплуатации.СуммаУпр, 0) КАК Себестоимость,
    |	0 КАК СебестоимостьКомплектация,
    |	ЕСТЬNULL(ДвиженияВЭксплуатации.Сумма, 0) КАК СебестоимостьРегл,
    |	0 КАК СебестоимостьКомплектацияРегл,
	|	0 КАК СуммаНДСВходящий,
	|	0 КАК СуммаНДСВходящийКомплектация
	|
	|ИЗ
	|	РеализацияАктивовАктивы КАК РеализацияАктивовАктивы
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводВЭксплуатациюАвтомобилей.Автомобили КАК ВводВЭксплуатациюАвтомобилейАвтомобили
	|	ПО РеализацияАктивовАктивы.ПрочийАктив = ВводВЭксплуатациюАвтомобилейАвтомобили.Актив
	|ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияПрочиеАктивыВЭксплуатации КАК ДвиженияВЭксплуатации
	|	ПО РеализацияАктивовАктивы.ПрочийАктив = ДвиженияВЭксплуатации.ПрочийАктив";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьРезультатЗапросаПоАвтомобилям()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////	
	|	Док.Ссылка КАК ДокументПродажи,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы активов
	ОбязательныеАктивы = Новый Структура();
	ОбязательныеАктивы.Вставить("ПрочийАктив", 3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента", 1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента", 1);
	ОбязательныеРеквизиты.Вставить("Контрагент", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов", 1);
	ОбязательныеРеквизиты.Вставить("Активы", ОбязательныеАктивы);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);		
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Активы Цикл
		Если НЕ (ТекСтрока.ПрочийАктив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
			     ТекСтрока.ПрочийАктив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
				 ТекСтрока.ПрочийАктив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты) Тогда
			Если НЕ (ТекСтрока.Количество = 1) Тогда
				ТекСтрока.Количество = 1;
				#Если Клиент Тогда
				Сообщить("Табличная часть ""Активы"" строка " + ТекСтрока.НомерСтроки + ": количество актива """ + СокрЛП(ТекСтрока.ПрочийАктив) + """ заменено на 1,000.", СтатусСообщения.Важное);
				#КонецЕсли
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РеализацияАктивов", "Реализация активов", Истина);
		
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка=Неопределено) Экспорт
	Отказ=Ложь;
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет общей суммы реализации
//
// Возвращаемое значение:
//  СуммаВсего - число.
//
Функция РассчитатьСуммуВсего() Экспорт
	СуммаВсего = дкПолучитьСуммуСписания(ЭтотОбъект, "ПрочиеАктивыВЭксплуатации", тКэшСуммСписания);
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "ДоговорВзаиморасчетов" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли ДоговорВзаиморасчетов.ВидДоговора <> Перечисления.ВидыДоговоров.Продажа И
			 ДоговорВзаиморасчетов.ВидДоговора <> Перечисления.ВидыДоговоров.Прочее Тогда
			 #Если Клиент Тогда
			 Предупреждение("Неправильный вид договора !");
			 #КонецЕсли
			 ДоговорВзаиморасчетов = Неопределено;
			 ОбработкаРеквизита("ДоговорВзаиморасчетов", , ЭтаФорма);
			 Возврат Ложь;
		КонецЕсли;
		Возврат Рез;
		
	//валюта и курс	
	ИначеЕсли Имя = "ВалютаДокумента" И (ВалютаКурсДок.Валюта <> ВалютаДокумента ИЛИ ВалютаКурсДок.Курс <> КурсДокумента) Тогда
		ВалютаКурсДок.Валюта = ВалютаДокумента;
		ВалютаКурсДок.Курс = КурсДокумента;
		Возврат Истина;
		
	ИначеЕсли Имя = "Организация" Тогда
		ОсвобожденОтНДС = Ложь;
		Если ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС Тогда
			ОсвобожденОтНДС = Истина;
		КонецЕсли; 
		Для Каждого Строка Из Активы Цикл
			Если ОсвобожденОтНДС Тогда
				Строка.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				Строка.СтавкаНДС = Строка.ПрочийАктив.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			ОбработкаРеквизита("Активы.СтавкаНДС", Строка, ЭтаФорма);
		КонецЦикла;
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "Активы.Актив" Тогда
		//получим остаточную стоимость из регистра		
		Если ЭтоНовый() Тогда
			МоментВремени = ТекущаяДата();
		Иначе
			МоментВремени = ?(Проведен, Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая), КонецМесяца(Дата));
		КонецЕсли;
		ТаблАктив = дкПолучитьОстаткиАктивов(ПодразделениеКомпании, , ТекСтрока.ПрочийАктив, МоментВремени);
		Если ТаблАктив.Количество() = 0 Тогда
			#Если Клиент Тогда
			Сообщить("Прочий актив """ + СокрЛП(ТекСтрока.ПрочийАктив) + """. Подразделение """ + СокрЛП(ПодразделениеКомпании) + """. Не обнаружен", СтатусСообщения.Внимание);
			#КонецЕсли
		Иначе
			Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				БалансоваяСтоимость_ = ТаблАктив[0].БалансоваяСтоимостьРегл;
				СуммаАмортизации_   = ТаблАктив[0].АмортизацияРегл;
			Иначе
				БалансоваяСтоимость_ = обПересчет(ТаблАктив[0].БалансоваяСтоимость, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
				СуммаАмортизации_   = обПересчет(ТаблАктив[0].Амортизация, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
			КонецЕсли; 
			ТекСтрока.Сумма = БалансоваяСтоимость_ - СуммаАмортизации_;
			
			Если ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе	
				ТекСтрока.СтавкаНДС = ТекСтрока.ПрочийАктив.Номенклатура.СтавкаНДС;
			КонецЕсли;	
			ОбработкаРеквизита("Активы.СтавкаНДС", ТекСтрока, ЭтаФорма);

		КонецЕсли;
			
		Возврат Истина;
		
	ИначеЕсли Имя = "Активы.СтавкаНДС" Тогда
		ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма* ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		Возврат Рез;
		
	ИначеЕсли Имя = "Активы.Сумма" Тогда
		ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма* ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция ПечатьРеализацияАктивов(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("РеализацияАктивов");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	//bz++
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
	    ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация);
	КонецЕсли;	   
	
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	//bz--
	
	ОбластьМакета.Параметры.ПредставлениеПодразделения	= спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеКонтрагента	= спПолучитьПредставление(Контрагент);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СтоимостьРеализации",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	Если Проведен Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	РеализацияАктивовАктивы.НомерСтроки,
		|	ПрочиеАктивыВЭксплуатации.ПрочийАктив КАК ПрочийАктив,
		|	РеализацияАктивовАктивы.Сумма КАК СтоимостьРеализации,
		|	ВЫБОР
		|		КОГДА ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр ЕСТЬ NULL 
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр * &МножительКурса
		|	КОНЕЦ - ВЫБОР
		|		КОГДА ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпр ЕСТЬ NULL 
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпр * &МножительКурса
		|	КОНЕЦ КАК ОстаточнаяСтоимость
		|ИЗ
		|	Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
		|		ПО РеализацияАктивовАктивы.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив
		|ГДЕ
		|	ПрочиеАктивыВЭксплуатации.ВидДвижения = &ВидДвиженияНакопленияРасход
		|	И ПрочиеАктивыВЭксплуатации.Регистратор = &Ссылка
		|	И РеализацияАктивовАктивы.Ссылка = &Ссылка";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход",ВидДвиженияНакопления.Расход);
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
			МножительКурса = обПересчет(1, ВалютаУправленческогоУчета, Дата , ВалютаДокумента, КурсДокумента);
		Иначе
			МножительКурса = обПересчет(1, ВалютаУправленческогоУчета, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		Запрос.УстановитьПараметр("МножительКурса",МножительКурса);
		ВыборкаТабличнойЧасти = Запрос.Выполнить().Выгрузить();
	Иначе
		Сообщить("Документ не проведен данные о остаточной стоимости не доступны!", СтатусСообщения.Важное);
		ВыборкаТабличнойЧасти = Активы.Выгрузить();
		ВыборкаТабличнойЧасти.Колонки.Добавить("ОстаточнаяСтоимость", Новый ОписаниеТипов("Число",,Новый КвалификаторыЧисла(15,2)));
		ВыборкаТабличнойЧасти.ЗаполнитьЗначения(0, "ОстаточнаяСтоимость");
		ВыборкаТабличнойЧасти.Колонки.Сумма.Имя = "СтоимостьРеализации";
	КонецЕсли;
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.ПрочийАктив			= СтрокаТабличнойЧасти.ПрочийАктив;
		ОбластьМакета.Параметры.АктивНаименование	= спПолучитьНаименование(СтрокаТабличнойЧасти.ПрочийАктив);
		ОбластьМакета.Параметры.Код					= СтрокаТабличнойЧасти.ПрочийАктив.ИнвентарныйНомер;
		ОбластьМакета.Параметры.ОстаточнаяСтоимость	= Формат(СтрокаТабличнойЧасти.ОстаточнаяСтоимость,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СтоимостьРеализации	= Формат(СтрокаТабличнойЧасти.СтоимостьРеализации,ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СтоимостьРеализации",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СтоимостьРеализации");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
	Получил.ПолучилКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Получил.Получилконтрагент),"","/ "+ Получил.ПолучилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРеализацияАктивов()

// Формирует печатную форму "М-15"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ15(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("М15");
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	// выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.НомерДокумента = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	//bz++
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	//bz--
	
	ОбластьМакета.Параметры.ОрганизацияПоОКПО = Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ДатаСоставления = Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.ПодразделениеНаименование = спПолучитьНаименование(ПодразделениеКомпании);
	Если Не обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда 
		ОбластьМакета.Параметры.Основание = спПолучитьНаименование(ДоговорВзаиморасчетов);
	КонецЕсли;
	ОбластьМакета.Параметры.КонтрагентНаименование = спПолучитьНаименование(Контрагент);
	ОбластьМакета.Параметры.Получатель = обПолучитьЗначениеСвойства(ЭтотОбъект,"Получатель");
	ТабДокумент.Вывести(ОбластьМакета); // конец шапки
	
	ВыборкаСтрок = ЭтотОбъект.Активы;
	
	//подвал выводим во временный
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(ВыборкаСтрок.Количество(), ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ВыборкаСтрок.Итог("Сумма"), ВалютаДокумента);
	ОбластьПодвал.Параметры.ИтогНДС = обЧислоПрописью(ВыборкаСтрок.Итог("СуммаНДС"), ВалютаДокумента);
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект, "Отпустил");
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
	Получил.Вставить("ПолучилПредставление", Получил.ПолучилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	ТабДокументПодвал = Новый ТабличныйДокумент;
	ТабДокументПодвал.Вывести(ОбластьПодвал);
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьРазделителя = Макет.ПолучитьОбласть("Разделитель");
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	// выводим заголовок таблицы
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
	// Выводим многострочную часть документа
	Для каждого СтрокаТабличнойЧасти Из ВыборкаСтрок Цикл
		СтавкаНДС = СтрокаТабличнойЧасти.ПрочийАктив.Номенклатура.СтавкаНДС.Ставка;
		
		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьСтрока.Параметры.ТоварНаименование            = спПолучитьНаименование(СтрокаТабличнойЧасти.ПрочийАктив);
		ОбластьСтрока.Параметры.ТоварКод					 = СтрокаТабличнойЧасти.ПрочийАктив.Код;
		ОбластьСтрока.Параметры.ИнвентарныйНомер			 = СтрокаТабличнойЧасти.ПрочийАктив.ИнвентарныйНомер;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод 		 = СтрокаТабличнойЧасти.ПрочийАктив.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТабличнойЧасти.ПрочийАктив.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьСтрока.Параметры.Количество 					 = Формат(1,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена 						 = Формат(СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаНДС,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаБезНДС 				 = Формат(СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаНДС,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаСНДС 					 = Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС					 = Формат(СтрокаТабличнойЧасти.СуммаНДС,ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТабличнойЧасти) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ТабДокументПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьРазделителя,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
		
	// выводим подвал
	ТабДокумент.Вывести(ТабДокументПодвал); // конец подвала
	
	Возврат ТабДокумент;
КонецФункции // ПечатьМ15()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Формирует печатную форму "УПД"
// Возвращает сформированный табличный документ:
Функция ПечатьУПД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
	
	Возврат ТабДокумент;
	
КонецФункции //ПечатьУПД()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура -  обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Для каждого ТекСтрока Из Активы Цикл
		ОбработкаРеквизита("Активы.Актив", ТекСтрока);
	КонецЦикла;
	ИдентификаторГосударственногоКонтракта = ?(ЗначениеЗаполнено(ИдентификаторГосударственногоКонтракта),
		ИдентификаторГосударственногоКонтракта,
		ДоговорВзаиморасчетов.ИдентификаторГосударственногоКонтракта);
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Результат = Истина;
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если Не ФлагПерерасчет Тогда
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда Возврат; КонецЕсли; 
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	РезультатЗапросаПоАктивам = ПолучитьРезультатЗапросаПоАктивам().Выгрузить().Скопировать();

	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(Ссылка);
	
	// 1) спишем из "Активов в эксплуатации"
	НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
	НаборЗаписейЭксплуатация.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейЭксплуатация.ПодразделениеКомпании     = ПодразделениеКомпании;
	НаборЗаписейЭксплуатация.ЭтоВыбытие                = Истина;
	НаборЗаписейЭксплуатация.РезультатЗапросаПоАктивам = РезультатЗапросаПоАктивам;
	Отказ = НЕ НаборЗаписейЭксплуатация.Расход() ИЛИ Отказ;
	
	// 2) Проведем по ТестДрайву
	НаборЗаписейТестДрайв = Движения.АвтомобилиДляТестДрайва;
	НаборЗаписейТестДрайв.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейТестДрайв.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписейТестДрайв.РезультатЗапросаПоАвтомобилям = НаборЗаписейЭксплуатация.Выгрузить();
	НаборЗаписейТестДрайв.Расход();
	
	// 3) проведем взаиморасчеты
	НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании; 
	НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.ШапкаДокумента 		= ШапкаДокумента;	
	НаборЗаписейВзаиморасчеты.РежимПроведения       = Режим;
	НаборЗаписейВзаиморасчеты.Контрагент            = Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.Сделка				= Неопределено;
	НаборЗаписейВзаиморасчеты.Сумма                 = РезультатЗапросаПоАктивам.Итог("Сумма");
	Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
	
	// 4) запишем ДиР
	ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	ИтогРасходПоРеализованнымАктивам = НаборЗаписейЭксплуатация.Итог("БалансоваяСтоимостьУпр") - НаборЗаписейЭксплуатация.Итог("СуммаАмортизацииУпр");	
	Если ВУпрВалюте Тогда
		ИтогДоходПоРеализованнымАктивам = РезультатЗапросаПоАктивам.Итог("Сумма");						  
	Иначе							  
		ИтогДоходПоРеализованнымАктивам = обПересчет(РезультатЗапросаПоАктивам.Итог("Сумма"), ВалютаДокумента, КурсДокумента, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр));
	КонецЕсли; 
	
	Если Окр(обМод(ИтогРасходПоРеализованнымАктивам), 2, РежимОкругления.Окр15как20) >= 0.01 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;		
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.СписаниеАктивов;
		НаборЗаписейДиР.ВУпрВалюте				= Истина;
		НаборЗаписейДиР.Расход = ИтогРасходПоРеализованнымАктивам;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;	
	
	Если Окр(обМод(ИтогДоходПоРеализованнымАктивам), 2, РежимОкругления.Окр15как20) >= 0.01 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;		
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.СписаниеАктивов;
		НаборЗаписейДиР.ВУпрВалюте				= Истина;
		НаборЗаписейДиР.Доход  = ИтогДоходПоРеализованнымАктивам;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// 5) запишем в продажи
	Если НЕ Отказ Тогда
		ЗапросПоСебестоимости = Новый Запрос("ВЫБРАТЬ
		|	ДвиженияПрочиеАктивыВЭксплуатации.ПрочийАктив,
		|	ДвиженияПрочиеАктивыВЭксплуатации.БалансоваяСтоимость-ДвиженияПрочиеАктивыВЭксплуатации.СуммаАмортизации КАК Сумма,
		|	ДвиженияПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр-ДвиженияПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпр КАК СуммаУпр,
		|	ДвиженияПрочиеАктивыВЭксплуатации.Количество
		|ПОМЕСТИТЬ ДвиженияПрочиеАктивыВЭксплуатации
		|ИЗ
		|	&ДвиженияПрочиеАктивыВЭксплуатации КАК ДвиженияПрочиеАктивыВЭксплуатации
		|ИНДЕКСИРОВАТЬ ПО
		|	ПрочийАктив");
		ЗапросПоСебестоимости.МенеджерВременныхТаблиц=Новый МенеджерВременныхТаблиц;
		ЗапросПоСебестоимости.УстановитьПараметр("ДвиженияПрочиеАктивыВЭксплуатации", Движения.ПрочиеАктивыВЭксплуатации.Выгрузить());
		ЗапросПоСебестоимости.Выполнить();
		//получаем таблицу активов-автомобилей из списания партий по документу "Ввод в эксплуатацию" (нужно только для продаж)
		РезультатЗапросаПоАвтомобилям = ПолучитьРезультатЗапросаПоАвтомобилям(ЗапросПоСебестоимости);
		СписокАвтомобилей = РезультатЗапросаПоАвтомобилям.ВыгрузитьКолонку("ПрочийАктив");
		//получаем таблицу товаров из списания партий по документу "Ввод в эксплуатацию" (нужно только для продаж)
		РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоТоварам(СписокАвтомобилей);
		Если РезультатЗапросаПоТоварам.Количество()>0 Тогда
			//получаем таблицу активов с расчитаной себестоимостью для приходования по регистру "Продажи"
			РезультатЗапросаПоПартиям = ПолучитьРезультатЗапросаПоПартиям(СписокАвтомобилей);	
			
			НаборЗаписейПродажи = Движения.Продажи;
			НаборЗаписейПродажи.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейПродажи.ШапкаДокумента = ШапкаДокумента;
			НаборЗаписейПродажи.ДокументПродажи = ШапкаДокумента.ДокументПродажи;
			НаборЗаписейПродажи.Сторно = Ложь;
			НаборЗаписейПродажи.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам.Скопировать();
			НаборЗаписейПродажи.РезультатЗапросаПоПартиям = РезультатЗапросаПоПартиям.Скопировать();
			НаборЗаписейПродажи.Покупатель            = Контрагент;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании = ПодразделениеКомпании;
			НаборЗаписейПродажи.Комиссия = Ложь;
			НаборЗаписейПродажи.ПоБазовомуКоличеству = Истина;
			Отказ = НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли; 
	КонецЕсли;	
	
	//по автомобилям
	Если НЕ Отказ И РезультатЗапросаПоАвтомобилям.Количество()>0 Тогда
		НаборЗаписейПродажиАвтомобилей = Движения.ПродажиАвтомобилей;
		НаборЗаписейПродажиАвтомобилей.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейПродажиАвтомобилей.ДокументПродажи = Ссылка;
		НаборЗаписейПродажиАвтомобилей.Сторно = Ложь;
		НаборЗаписейПродажиАвтомобилей.РезультатЗапросаПоАвтомобилям = РезультатЗапросаПоАвтомобилям.Скопировать();
		НаборЗаписейПродажиАвтомобилей.Покупатель            = Контрагент;
		НаборЗаписейПродажиАвтомобилей.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
		НаборЗаписейПродажиАвтомобилей.ПодразделениеКомпании = ПодразделениеКомпании;
		НаборЗаписейПродажиАвтомобилей.Комиссия = Ложь;
		Отказ = НЕ НаборЗаписейПродажиАвтомобилей.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ФлагПерерасчет = Истина;
		Записать();
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	Если НЕ Отказ Тогда
		//Запишем историю владельцев автомобилей
		Для Каждого ТекСтрока Из РезультатЗапросаПоАвтомобилям Цикл
			Справочники.Автомобили.ЗаписьЗначенияРегистраСведения(ТекСтрока.Автомобиль, Контрагент,Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин);
		КонецЦикла;
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// инициализация кэша
ВалютаКурсДок = Новый Структура("Валюта, Курс", ВалютаДокумента, КурсДокумента);
ФлагПерерасчет = Ложь;
// Модуль документа АвансовыйОтчет

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ВедетсяБалансПоПодразделению; // Переменная для определения ведется ли баланс на уровне подразделения
Перем БалансовыеПодразделенияНеРавны; // Переменная для определения неравенства балансовых подразделений документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт

	// Обязательные поля таблицы выданных авансов
	ОбязательныеАвансы=Новый Структура();
	ОбязательныеАвансы.Вставить("ДокументОснование",1);
	ОбязательныеАвансы.Вставить("Сумма",1);
	// Обязательные поля расходов
	ОбязательныеРасходы=Новый Структура();
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	
	//ОбязательныеТовары.Вставить("Номенклатура",3);
	//ОбязательныеТовары.Вставить("Количество",1);
	//ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	//ОбязательныеТовары.Вставить("Коэффициент",1);
	//ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	//ОбязательныеТовары.Вставить("ГТД",2);  
	
	ОбязательныеТовары.Вставить("Номенклатура",1);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",1);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	
	//KamikadZe end changes
	
	//KamikadZe begin comments 10.12.2015 18:22:22
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	Если Товары.Количество()>0 Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("ВыданныеАвансы",ОбязательныеАвансы);
	ОбязательныеРеквизиты.Вставить("Расходы",ОбязательныеРасходы);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
			
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", 
	// то дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			КонтролируемыеРеквизитыРасход = Новый Структура();
			КонтролируемыеРеквизитыРасход.Вставить("Договор", КонтрольПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Расходы",КонтролируемыеРеквизитыРасход);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);		
			   							
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;		
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("АвансовыйОтчет","Авансовый отчет",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Проверяет правильность заполнения строк табличной части "Расходы".
//
// Параметры:
//  Ошибки - строка, передает описание ошибки.
//
// Возвращаемое значение:
//  Истина - если нет ошибок заполнения.
//  Ложь   - если найдены ошибки. 
//
Функция ПроверитьЗаполнениеРасходов(Ошибки) Экспорт
	Ошибки = "";
	Результат=Истина;
	Для Каждого Строка Из ЭтотОбъект.Расходы Цикл
		Если Строка.НаРасчеты Тогда
			Если обЗначениеНеЗаполнено(Строка.Контрагент) Тогда
				Результат=Ложь;
				Ошибки=Ошибки+"Таблица Расходы значение колонки Контрагент не заполнено ! Строка номер "+СокрЛП(Строка.НомерСтроки)+Символы.ПС;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(Строка.Договор) Тогда
				Результат=Ложь;
				Ошибки=Ошибки+"Таблица Расходы значение колонки Договор не заполнено ! Строка номер "+СокрЛП(Строка.НомерСтроки)+Символы.ПС;
			КонецЕсли;	
		Иначе
			Если обЗначениеНеЗаполнено(Строка.СтатьяРасхода) Тогда
				Результат=Ложь;
				Ошибки=Ошибки+"Таблица Расходы значение колонки СтатьяРасхода не заполнено ! Строка номер "+СокрЛП(Строка.НомерСтроки)+Символы.ПС;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции // ПроверитьЗаполнениеРасходов()

// Заполняет табличную часть "ВыданныеАвансы" по неоплаченным сделкам
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Процедура ЗаполнитьВыданныеАвансы() Экспорт
	ЭтотОбъект.ВыданныеАвансы.Очистить();
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|ВзаиморасчетыКомпанииОстаткиИОбороты.Сделка КАК Сделка,
	|ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаОстаток,
	|ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаПриход КАК СуммаПриход
	|
	|ИЗ
	|РегистрНакопления.ВзаиморасчетыКомпании.ОстаткиИОбороты(&ПериодС,&ПериодПо) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
	|
	|ГДЕ
	|	(ВзаиморасчетыКомпанииОстаткиИОбороты.Контрагент=&Контрагент) 
	|	И (ВзаиморасчетыКомпанииОстаткиИОбороты.ДоговорВзаиморасчетов=&Договор)
	|	И (ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаПриход>0)
	|");
	Запрос.УстановитьПараметр("Контрагент",ЭтотОбъект.Контрагент);
	Запрос.УстановитьПараметр("Договор",ЭтотОбъект.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ПериодС",ЭтотОбъект.ПериодС);
	Запрос.УстановитьПараметр("ПериодПо",ЭтотОбъект.ПериодПо);
	РезультатЗапросаПоСделкам = Запрос.Выполнить();
	Если НЕ РезультатЗапросаПоСделкам.Пустой() Тогда
		Выборка = РезультатЗапросаПоСделкам.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаАвансы = ЭтотОбъект.ВыданныеАвансы.Добавить();
			СтрокаАвансы.ДокументОснование = Выборка.Сделка;
			СтрокаАвансы.Сумма = Выборка.СуммаОстаток;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ЗаполнитьВыданныеАвансы()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Число - Рассчитанный итог по колонке "Сумма".
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Расходы.Итог("Сумма")+Товары.Итог("СуммаВсего");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка			- Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма				- Ссылка на форму документа. Если значение неопределено, 
//									  производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Контрагент" Тогда
		//Параметры для подстановки или создания договора контрагента
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		ВидДоговора	= Перечисления.ВидыДоговоров.Подотчет;
		ДопПараметры.Вставить("ВидДоговора", ВидДоговора);
		ДопПараметры.Вставить("ВалютаВзаиморасчетов", ВалютаДокумента);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);		
		Возврат Рез;		

	ИначеЕсли Имя="СкладКомпании" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Для Каждого СтрокаТоваров Из Товары Цикл
					Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма) И Рез;
					Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",СтрокаТоваров,ЭтаФорма) И Рез;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;		
		
	ИначеЕсли Имя="Расходы.Сделка" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Сделка) Тогда
			Возврат ЛОЖЬ;
		КонецЕсли;
		Если ТекСтрока.Сделка = ЭтотОбъект.Ссылка И ТипЗнч(ТекСтрока.Сделка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			ТекСтрока.Сделка = Неопределено;
		КонецЕсли;
		Рез=Истина;
		Попытка
			ТекСтрока.Сумма = обПересчет(ТекСтрока.Сделка.СуммаДокумента, ТекСтрока.Сделка.ВалютаДокумента, Дата, ВалютаДокумента, КурсДокумента);
			Рез=ОбработкаРеквизита("Расходы.Сумма", ТекСтрока, ЭтаФорма) И Рез;
		Исключение
		КонецПопытки;
		Попытка
			ТекСтрока.Договор = ТекСтрока.Сделка.ДоговорВзаиморасчетов;
			Рез=ОбработкаРеквизита("Расходы.ДоговорВзаиморасчетов", ТекСтрока, ЭтаФорма) И Рез;
		Исключение
		КонецПопытки;
		Попытка
			ТекСтрока.Контрагент = ТекСтрока.Сделка.Контрагент;
			Рез=ОбработкаРеквизита("Расходы.Контрагент", ТекСтрока, ЭтаФорма) И Рез;
		Исключение
		КонецПопытки;
		Возврат Рез;
		
	ИначеЕсли Имя="Расходы.Контрагент" Тогда
		
		Если ТекСтрока.Контрагент.Пустая() Тогда
			Возврат ЛОЖЬ;
		КонецЕсли;
		
		Если ТекСтрока.Договор.Пустая() ИЛИ(ТекСтрока.Договор.Владелец <> ТекСтрока.Контрагент)Тогда
			//++бизтех++
			
			//ТекСтрока.Договор = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ЭтотОбъект.Контрагент, Перечисления.ВидыДоговоров.Подотчет, ЭтотОбъект,, Истина, Истина);
			ТекСтрока.Договор = ТекСтрока.Контрагент.ОсновнойДоговорВзаиморасчетов;
			
			//--бизтех--
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="ВыданныеАвансы.ДокументОснование" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.ДокументОснование) Тогда
			ТекСтрока.Сумма=0;
		Иначе
			Если Тип("ДокументСсылка.РасходныйКассовыйОрдер") = ТипЗнч(ТекСтрока.ДокументОснование) Тогда
				СтруктураОтбора=Новый Структура();
				СтруктураОтбора.Вставить("Сделка",ТекСтрока.ДокументОснование);
				тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(Дата,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
				Если ТекСтрока.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
					Если ДокументОснование = Неопределено Тогда 
						ОстатокПоСделке = обПересчет(тзДолги.Итог("Сумма"), ТекСтрока.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ТекСтрока.ДокументОснование.Дата, ВалютаДокумента, ТекСтрока.ДокументОснование.Дата, РежимОкругления.Окр15как20); 		       
					Иначе 						
						ОстатокПоСделке = обПересчет(тзДолги.Итог("Сумма"), ТекСтрока.ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ДокументОснование.Дата, ВалютаДокумента, ДокументОснование.Дата, РежимОкругления.Окр15как20); 		       
					КонецЕсли; 
				ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
					ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
				Иначе
					ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ТекСтрока.ДокументОснование.Дата,ВалютаДокумента,ТекСтрока.ДокументОснование.Дата,РежимОкругления.Окр15как20);
				КонецЕсли;
				
				Если ОстатокПоСделке<0 Тогда
					ТекСтрока.Сумма=0;
				Иначе
					ТекСтрока.Сумма=ОстатокПоСделке;
				КонецЕсли; 
			ИначеЕсли Тип("ДокументСсылка.Выписка") = ТипЗнч(ТекСтрока.ДокументОснование) Тогда
				Если ТекСтрока.ДокументОснование.ХозОперация = Справочники.ХозОперации.БанковскаяВыписка Тогда
					ТекСтрока.ДокументОснование = Документы.Выписка.ПустаяСсылка();
				Иначе
					Если ТекСтрока.ДокументОснование.ВалютаДокумента = ВалютаДокумента Тогда
						ТекСтрока.Сумма = ТекСтрока.ДокументОснование.СуммаДокументаРасход;
					Иначе
						ТекСтрока.Сумма = обПересчет(ТекСтрока.ДокументОснование.СуммаДокументаРасход, ТекСтрока.ДокументОснование.ВалютаДокумента, ТекСтрока.ДокументОснование.Дата, ВалютаДокумента, ТекСтрока.ДокументОснование.Дата, РежимОкругления.Окр15как20);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("Контрагент",Контрагент);
		КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			Рез = ОбработкаРеквизита("УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма,ДопПараметры);
			Рез = ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				// изменим сумму документа
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
		КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл)); 
		ТекСтрока.ЦенаРозничная=ТекЦенаЗакупки+(ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
		Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
			ТекСтрока.ЦенаРозничная=ТекСтрока.ЦенаРозничная*(1+(ТекСтрока.СтавкаНДС.Ставка/100));
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=?(ТекСтрока.КоличествоБазовое=0,0,ТекСтрока.СуммаРозничная/ТекСтрока.КоличествоБазовое);
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена=0 Тогда
			ТекСтрока.ПроцентНаценки=0;
		Иначе
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл)); 
			ТекСтрока.ПроцентНаценки=((ТекСтрока.ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная = обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную цену (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.КоличествоБазовое;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	СтруктураПараметров=Неопределено;
	Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
		СтруктураПараметров=Новый Структура("ИмяРеквизитаЦена,ТипЦен","ЦенаРозничная",СкладКомпании.ТипЦенРозничнойТорговли);
	КонецЕсли;
	дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "АвансовыйОтчет"
//
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращает сформированный табличный документ:
Функция ПечатьАО1(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("АО1");

	//валюты
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	//получим данные о ранее выданных этому лицу авансах
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Момент",Новый Граница(МоментВремени(),ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		
	Запрос.Текст = "ВЫБРАТЬ
					|	ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток КАК Сумма
					|ИЗ
					|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
					|			&Момент,
					|			Контрагент = &Контрагент
					|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК ВзаиморасчетыКомпанииОстатки";
	ВыборкаПредАвансы = Запрос.Выполнить().Выбрать();
	ОстатокНаНачало = 0;
	Если ВыборкаПредАвансы.Следующий() И (НЕ ВыборкаПредАвансы.Сумма = null) Тогда
		ОстатокНаНачало = ВыборкаПредАвансы.Сумма;
	КонецЕсли;	
	//ОстатокНаНачало > 0 --> контрагент должен организации  --> остаток
	//ОстатокНаНачало < 0 --> организация должна контрагенту --> перерасход
			  			   
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	НомерСтраницы = 2;
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//Получаем область титула документа
	ОбластьМакета = Макет.ПолучитьОбласть("Титул");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	СуммаАвансыОбщ 	= обПересчет(ВыданныеАвансы.Итог("Сумма"), ВалютаДокумента, Дата, ВалютаРеглУчета, Дата);
	ОстатокНаНачало = ОстатокНаНачало; //вычтем из всех выданных ранее авансов те, за которые контрагент отчитывается данным документом
	
	//++бизтех++
	//СуммаРасходыОбщ = обПересчет(Расходы.Итог("Сумма")+Товары.Итог("СуммаВсего"), ВалютаДокумента, Дата, ВалютаРеглУчета, Дата); 
	СуммаРасходыОбщ = обПересчет(Расходы.Итог("Сумма")+Товары.Итог("Сумма"), ВалютаДокумента, Дата, ВалютаРеглУчета, Дата);
	//--бизтех--
	
	СуммаРасходыРуб = Цел(СуммаРасходыОбщ);
	СуммаРасходыКоп = 100 * (Окр(СуммаРасходыОбщ - СуммаРасходыРуб, 2, 1));
	ОбластьМакета.Параметры.ИзрасходованоРуб = СуммаРасходыРуб;
	ОбластьМакета.Параметры.ИзрасходованоКоп = Формат(СуммаРасходыКоп, "ЧЦ=2; ЧН=00; ЧВН=");
	
	ОбластьМакета.Параметры.Подразделение = ПодразделениеКомпании;
	ОбластьМакета.Параметры.ПредставлениеПодразделения = спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.КодПодразделения = ПодразделениеКомпании.Код;
	ОбластьМакета.Параметры.ПодотчетноеЛицо = Контрагент;
	ОбластьМакета.Параметры.ПредставлениеПодотчетногоЛица = спПолучитьНаименование(Контрагент);
	ОбластьМакета.Параметры.ТабельныйНомер = Контрагент.Сотрудник.ТабельныйНомер;
	ОбластьМакета.Параметры.ДолжностьПодотчетногоЛицаПредставление = ?(Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ПодотчетноеЛицо, Контрагент.Сотрудник.Должность.ПолноеНаименование(), "");
	ОбластьМакета.Параметры.ПредставлениеВалютыДокумента = ВалютаДокумента;
	ОбластьМакета.Параметры.НачальныйОстаток    = ?((ОстатокНаНачало - СуммаАвансыОбщ) > 0, Формат(ОстатокНаНачало - СуммаАвансыОбщ, ФорматВыводаСуммы), "");
	ОбластьМакета.Параметры.НачальныйПерерасход = ?(ОстатокНаНачало < 0, Формат(-ОстатокНаНачало, ФорматВыводаСуммы), "");
	ОбластьМакета.Параметры.ПолученоИзКассы = Формат(СуммаАвансыОбщ, ФорматВыводаСуммы);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО = Организация.КодПоОКПО;	
	Если НЕ (ВалютаДокумента = ВалютаРеглУчета) Тогда
		ОбластьМакета.Параметры.ПолученоИзКассыВВалюте = Формат(ВыданныеАвансы.Итог("Сумма"), ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьМакета.Параметры.ИтогоПолучено = Формат(СуммаАвансыОбщ, ФорматВыводаСуммы);	
	ОбластьМакета.Параметры.Израсходовано = Формат(СуммаРасходыОбщ, ФорматВыводаСуммы);
	ОстатокНаКонец = Макс(ОстатокНаНачало, СуммаАвансыОбщ) - СуммаРасходыОбщ;
	ОбластьМакета.Параметры.КонечныйОстаток    = ?(ОстатокНаКонец > 0, Формат(ОстатокНаКонец, ФорматВыводаСуммы), "");
	ОбластьМакета.Параметры.КонечныйПерерасход = ?(ОстатокНаКонец < 0, Формат(-ОстатокНаКонец, ФорматВыводаСуммы), "");
	
	ОбластьМакета.Параметры.СуммаОтчетаПрописью = обЧислоПрописью(СуммаДокумента, ВалютаДокумента);
	
	//заполним ответственных
	Руководитель		= дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ГлавныйБухгалтер	= дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	Кассир				= дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Кассир);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(Кассир);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	тблРасходы = Расходы.Выгрузить().Скопировать();
	//обработка таблицы
	тблРасходы.Колонки.Сумма.Имя = "ПоОтчету";
	тблРасходы.Колонки.Добавить("ПоУчету", тблРасходы.Колонки.ПоОтчету.ТипЗначения);
	тблРасходы.Колонки.Добавить("ПоОтчетуВВалюте", тблРасходы.Колонки.ПоОтчету.ТипЗначения);
	тблРасходы.Колонки.Добавить("ПоУчетуВВалюте", тблРасходы.Колонки.ПоОтчету.ТипЗначения);
	Для Каждого ТекСтрока Из тблРасходы Цикл
		Если ВалютаДокумента <> ВалютаРеглУчета Тогда
			ТекСтрока.ПоОтчетуВВалюте = ТекСтрока.ПоОтчету;
		КонецЕсли;
		ТекСтрока.ПоОтчету = обПересчет(ТекСтрока.ПоОтчету, ВалютаДокумента, Дата, ВалютаРеглУчета, Дата);
		ТекСтрока.ПоУчету = ТекСтрока.ПоОтчету;
		ТекСтрока.ПоУчетуВВалюте = ТекСтрока.ПоОтчетуВВалюте;
	КонецЦикла;
	
	//Если Товары.Количество() <> 0 Тогда
	//	ОбластьМакета.Параметры["СубСчетДебет1"]  = "41";
	//	ОбластьМакета.Параметры["СуммаДебет1"]    = Формат(Товары.Итог("Сумма"), ФорматВыводаСуммы);
	//	ОбластьМакета.Параметры["СубСчетКредит1"] = ?(ВалютаДокумента = ВалютаРеглУчета, "71.1", "71.2");
	//	ОбластьМакета.Параметры["СуммаКредит1"]   = Формат(Товары.Итог("Сумма"), ФорматВыводаСуммы);
	//	Сч = 2; //счетчик строк таблицы бух. запись
	//Иначе
	//	Сч = 1; //счетчик строк таблицы бух. запись
	//КонецЕсли;
	//	
	//Взаиморасчеты = Расходы.Выгрузить();
	//Взаиморасчеты.Свернуть("НаРасчеты","Сумма");
	//
	//Для Каждого СтрокаВзаиморасчетов Из Взаиморасчеты Цикл
	//	Если СтрокаВзаиморасчетов.НаРасчеты = Истина Тогда
	//		ОбластьМакета.Параметры["СубСчетДебет" + Строка(Сч)] = "60.01";
	//		ОбластьМакета.Параметры["СуммаДебет" + Строка(Сч)] = Формат(СтрокаВзаиморасчетов.Сумма, ФорматВыводаСуммы);        	
	//		ОбластьМакета.Параметры["СубСчетКредит" + Строка(Сч)] = "71.01";
	//		ОбластьМакета.Параметры["СуммаКредит" + Строка(Сч)] = Формат(СтрокаВзаиморасчетов.Сумма, ФорматВыводаСуммы); 
	//   		Сч = Сч + 1;
	//	КонецЕсли;	
	//КонецЦикла; 
	//
	//Для Каждого СтрокаТабличнойЧасти Из тблРасходы Цикл
	//	Если НЕ (СтрокаТабличнойЧасти.НаРасчеты ИЛИ (Сч > 8)) Тогда
	//		//заполним бух. запись суммами по статьям расходов
	//		ОбластьМакета.Параметры["СубСчетДебет" + Строка(Сч)] = СтрокаТабличнойЧасти.СтатьяРасхода.КоррСчет;
	//		ОбластьМакета.Параметры["СуммаДебет" + Строка(Сч)] = Формат(СтрокаТабличнойЧасти.ПоОтчету, ФорматВыводаСуммы);        	
	//		ОбластьМакета.Параметры["СубСчетКредит" + Строка(Сч)] = ?(ВалютаДокумента = ВалютаРеглУчета, "71.1", "71.2");
	//		ОбластьМакета.Параметры["СуммаКредит" + Строка(Сч)] = Формат(СтрокаТабличнойЧасти.ПоОтчету, ФорматВыводаСуммы); 
	//   		Сч = Сч + 1;
	//	КонецЕсли;	
	//КонецЦикла;  
	
//<<Павличенко 06.12.17
	Сч = 1;
	ТаблицаВАО1 = Товары.Выгрузить();
	ТаблицаВАО1.Свернуть("КоррСчет", "Сумма");
	
	Для Каждого СтрокаТабличнойЧасти Из ТаблицаВАО1 Цикл
		ОбластьМакета.Параметры["СубСчетДебет1"]  = СтрокаТабличнойЧасти.КоррСчет;
		ОбластьМакета.Параметры["СуммаДебет1"]    = Формат(СтрокаТабличнойЧасти.Сумма, ФорматВыводаСуммы);
		ОбластьМакета.Параметры["СубСчетКредит1"] = ?(ВалютаДокумента = ВалютаРеглУчета, "71.1", "71.2");
		ОбластьМакета.Параметры["СуммаКредит1"]   = Формат(СтрокаТабличнойЧасти.Сумма, ФорматВыводаСуммы);
		Сч = Сч + 1; //счетчик строк таблицы бух. запись
	КонецЦикла;
	ТаблицаВАО1 = Расходы.Выгрузить();
	ТаблицаВАО1.Свернуть("КоррСчет, НаРасчеты", "Сумма");
	Для Каждого СтрокаТабличнойЧасти Из ТаблицаВАО1 Цикл
		Если НЕ (СтрокаТабличнойЧасти.НаРасчеты ИЛИ (Сч > 8)) Тогда
			//заполним бух. запись суммами по статьям расходов
			ОбластьМакета.Параметры["СубСчетДебет" + Строка(Сч)] = СтрокаТабличнойЧасти.КоррСчет;
			ОбластьМакета.Параметры["СуммаДебет" + Строка(Сч)] = Формат(СтрокаТабличнойЧасти.Сумма, ФорматВыводаСуммы);        	
			ОбластьМакета.Параметры["СубСчетКредит" + Строка(Сч)] = ?(ВалютаДокумента = ВалютаРеглУчета, "71.1", "71.2");
			ОбластьМакета.Параметры["СуммаКредит" + Строка(Сч)] = Формат(СтрокаТабличнойЧасти.Сумма, ФорматВыводаСуммы); 
	   		Сч = Сч + 1;
		КонецЕсли;	
	КонецЦикла;     
	//>>Павличенко 06.12.17
	
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ПоОтчету, ПоОтчетуВВалюте, ПоУчету, ПоУчетуВВалюте", 0, 0, 0, 0);
	
	НомерСтраницыПред = НомерСтраницы;
	
	//Получаем область шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	//доп. области
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);

	//Получаем область строки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	НоваяСтрока 		    = тблРасходы.Добавить();
	НоваяСтрока.ДатаРасхода = Дата;         
	
	//++бизтех++
	//НоваяСтрока.ПоОтчету    = Товары.Итог("СуммаВсего");
	НоваяСтрока.ПоОтчету    = Товары.Итог("Сумма");
	//--бизтех--
	
	НоваяСтрока.НаРасчеты   = Ложь;
	Если ВалютаДокумента <> ВалютаРеглУчета Тогда
		НоваяСтрока.ПоОтчетуВВалюте = НоваяСтрока.ПоОтчету;
	КонецЕсли;
	НоваяСтрока.ПоОтчету       = обПересчет(НоваяСтрока.ПоОтчету, ВалютаДокумента, Дата, ВалютаРеглУчета, Дата);
	НоваяСтрока.ПоУчету        = НоваяСтрока.ПоОтчету;
	НоваяСтрока.ПоУчетуВВалюте = НоваяСтрока.ПоОтчетуВВалюте;
	НоваяСтрока.НомерСтроки    = тблРасходы.Количество();
	Для Каждого СтрокаТабличнойЧасти Из тблРасходы Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.ПоОтчету = Формат(СтрокаТабличнойЧасти.ПоОтчету,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ПоОтчетуВВалюте = Формат(СтрокаТабличнойЧасти.ПоОтчетуВВалюте,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ПоУчету = Формат(СтрокаТабличнойЧасти.ПоУчету,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ПоУчетуВВалюте = Формат(СтрокаТабличнойЧасти.ПоУчетуВВалюте,ФорматВыводаСуммы);
		Если обЗначениеНеЗаполнено(СтрокаТабличнойЧасти.Сделка) Тогда
			ОбластьМакета.Параметры.ДокументДата = Формат(СтрокаТабличнойЧасти.ДатаРасхода,"ДФ=dd.MM.yy");
			ОбластьМакета.Параметры.ДокументНомер = "";
			ОбластьМакета.Параметры.ДокументРасходаПредставление = Неопределено;
		Иначе
			ОбластьМакета.Параметры.ДокументДата = Формат(СтрокаТабличнойЧасти.Сделка.Дата,"ДФ=dd.MM.yy");
			ОбластьМакета.Параметры.ДокументНомер = дкПолучитьНомерДляПечати(СтрокаТабличнойЧасти.Сделка);
			ОбластьМакета.Параметры.ДокументРасхода = СтрокаТабличнойЧасти.Сделка.ХозОперация;
			ОбластьМакета.Параметры.ДокументРасходаПредставление = СтрокаТабличнойЧасти.Сделка;
		КонецЕсли;
		
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если Расходы.Индекс(СтрокаТабличнойЧасти) = Расходы.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ПоОтчету, ПоОтчетуВВалюте, ПоУчету, ПоУчетуВВалюте", 0, 0, 0, 0);
			НомерСтраницыПред = НомерСтраницы;
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти, СтруктураИтоговПоСтранице);
	КонецЦикла;

	//Получаем область подвала документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.Заполнить(Кассир);
	ОбластьПодвал.Параметры.ПредставлениеПодотчетногоЛица = спПолучитьНаименование(Контрагент);
	
	//bz
	//ИтогоРегл = обПересчет(Расходы.Итог("Сумма")+Товары.Итог("СуммаВсего"), ВалютаДокумента, Дата, ВалютаРеглУчета, Дата);
	ИтогоРегл = обПересчет(Расходы.Итог("Сумма")+Товары.Итог("Сумма"), ВалютаДокумента, Дата, ВалютаРеглУчета, Дата);
	
	ОбластьПодвал.Параметры.ИтогоПоОтчету = Формат(ИтогоРегл, ФорматВыводаСуммы);
	Если ВалютаДокумента <> ВалютаРеглУчета Тогда
		//bz
		//ОбластьПодвал.Параметры.ИтогоПоОтчетуВВалюте = Формат(Расходы.Итог("Сумма")+Товары.Итог("СуммаВсего"), ФорматВыводаСуммы);
		//ОбластьПодвал.Параметры.ИтогоПоУчетуВВалюте = Формат(Расходы.Итог("Сумма")+Товары.Итог("СуммаВсего"), ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.ИтогоПоОтчетуВВалюте = Формат(Расходы.Итог("Сумма")+Товары.Итог("Сумма"), ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.ИтогоПоУчетуВВалюте = Формат(Расходы.Итог("Сумма")+Товары.Итог("Сумма"), ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.ИтогоПоУчету = Формат(ИтогоРегл, ФорматВыводаСуммы);
	
	ТабДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАО1()

// Формирует печатную форму "АвансовыйОтчет".
//
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращает сформированный табличный документ:
Функция ПечатьАвансовыйОтчет(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("АвансовыйОтчет");
	
	//для начала настроим макет
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	НомерСтраницы = 2;
	
	//Получаем область шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.Номер 	 = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.Дата	 = Формат(ЭтотОбъект.Дата,"ДФ=dd.MM.yyyy"); 
	ОбластьМакета.Параметры.ПериодС	 = Формат(ЭтотОбъект.ПериодС,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ПериодПо = Формат(ЭтотОбъект.ПериодПо,"ДФ=dd.MM.yyyy");
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.Подразделение = ПодразделениеКомпании;
	ОбластьМакета.Параметры.ПодотчетноеЛицо = Контрагент;
	
	ОбластьМакета.Параметры.ПредставлениеПодразделения		= ЭтотОбъект.ПодразделениеКомпании;
	ОбластьМакета.Параметры.ПредставлениеПодотчетногоЛица	= спПолучитьПредставление(ЭтотОбъект.Контрагент);
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма", ВалютаДокумента, 0);
	
	НомерСтраницыПред = НомерСтраницы;
	
	//Получаем область строки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ВыборкаТабличнойЧасти = ЭтотОбъект.Расходы;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.Сумма = Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма", ВалютаДокумента, 0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти, СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);

	//Получаем область подвала документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.Израсходовано = Формат(Расходы.Итог("Сумма"), ФорматВыводаСуммы);
    ОбластьПодвал.Параметры.РуководительПредставление		= ?(обЗначениеНеЗаполнено(Руководитель.Руководитель),"","/ "+ Руководитель.РуководительПредставление);
	ОбластьПодвал.Параметры.ПодотчетноеЛицоПредставление	= "/ " + спПолучитьНаименование(Контрагент);
	ТабДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАвансовыйОтчет()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик заполнения на основании
Процедура ОбработкаЗаполнения(Основание) Экспорт
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если Не обЗначениеНеЗаполнено(ДокументОснование) И ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		СтрокаАвансов=ВыданныеАвансы.Добавить();
		СтрокаАвансов.ДокументОснование=ДокументОснование;
		ОбработкаРеквизита("ВыданныеАвансы.ДокументОснование",СтрокаАвансов);
		Если СтрокаАвансов.Сумма>0 Тогда
			СтрокаРасходов=Расходы.Добавить();
			СтрокаРасходов.ДатаРасхода=ДокументОснование.Дата;
			СтрокаРасходов.Сумма=СтрокаАвансов.Сумма;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.Выписка") Тогда
		Если Основание.ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
			СтрокаАвансов=ВыданныеАвансы.Добавить();
			СтрокаАвансов.ДокументОснование=Основание;
			ОбработкаРеквизита("ВыданныеАвансы.ДокументОснование",СтрокаАвансов);
			Если СтрокаАвансов.Сумма > 0 Тогда
				СтрокаРасходов = Расходы.Добавить();
				СтрокаРасходов.ДатаРасхода = Основание.Дата;
				СтрокаРасходов.Сумма = СтрокаАвансов.Сумма;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаявкаНаРасходДС") Тогда
		Для Каждого ТекСтрока Из ДокументОснование.Платежи Цикл
			СтрокаРасходов = Расходы.Добавить();
			СтрокаРасходов.ДатаРасхода   = ТекСтрока.ДатаПлатежа;
			СтрокаРасходов.СтатьяРасхода = ТекСтрока.СтатьяРасходов;
			СтрокаРасходов.Сумма 		 = ТекСтрока.Сумма;
			СтрокаРасходов.Содержание 	 = ТекСтрока.Описание;
		КонецЦикла;		
	КонецЕсли; 
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	// установим итоговую сумму для журнала
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если обЗначениеНеЗаполнено(СкладКомпании) Или НЕ СкладКомпании.Розничный Тогда
		Для каждого ТекСтрокаТЧ Из Товары Цикл  	
			ТекСтрокаТЧ.ПроцентНаценки = 0;
			ТекСтрокаТЧ.ЦенаРозничная  = 0;
			ТекСтрокаТЧ.СуммаРозничная = 0;
		КонецЦикла;	
	КонецЕсли;
	
	ТекстОшибки = "";
	Если Не ПроверитьЗаполнениеРасходов(ТекстОшибки) Тогда
		Сообщить(ТекстОшибки);
		Отказ = Истина;
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
	КонецЕсли;
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		БылиДвижения = Ложь;
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ = Ложь;
	
	// получим шапку документа
	ШапкаДокумента	= ПолучитьШапкуДокумента(ДокументСсылка);
	
	// посчитаем суммы товаров и услуг
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.НаДоходыИРасходы) ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар) КОНЕЦ КАК ВидНоменклатуры,
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) ТОГДА ДокументТовары.Номенклатура.СтатьяДопРасходов ИНАЧЕ NULL КОНЕЦ КАК СтатьяДопРасходов,
	|	СУММА(ДокументТовары.СуммаВсего) КАК СуммаВсего
	|ИЗ
	|	Документ.АвансовыйОтчет.Товары 	КАК ДокументТовары
	|
	|ГДЕ  
	|	ДокументТовары.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО 
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.НаДоходыИРасходы) ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар) КОНЕЦ,
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) ТОГДА ДокументТовары.Номенклатура.СтатьяДопРасходов ИНАЧЕ NULL КОНЕЦ
	|
	|ИТОГИ ПО ВидНоменклатуры
	|");
	Запрос.УстановитьПараметр("ДокументСсылка",ШапкаДокумента.Ссылка);
	ДеревоДоходовИРасходов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям					= Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект	= ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента	= ШапкаДокумента;
	Отказ	= Не НаборЗаписейДопроведениеПоПартиям.Зафиксировать() Или Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НайденнаяСтрокаТоваров	= ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Товар,"ВидНоменклатуры");
		НайденнаяСтрокаУслуг	= ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Услуга,"ВидНоменклатуры");
		Если (НайденнаяСтрокаТоваров<>Неопределено ИЛИ НайденнаяСтрокаУслуг<>Неопределено) Тогда
			// Доходы и расходы на себестоимость не оприходованных партий
			СуммаВсего=?(НайденнаяСтрокаТоваров<>Неопределено,НайденнаяСтрокаТоваров.СуммаВсего,0)+?(НайденнаяСтрокаУслуг<>Неопределено,НайденнаяСтрокаУслуг.СуммаВсего,0);
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				//bz
				//НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
				НаборЗаписейДоходыИРасходы.ПодразделениеКомпании=ШапкаДокумента.СкладКомпании.ПодразделениеКомпании;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
			НаборЗаписейДоходыИРасходы.Расход = СуммаВсего; 
			
			//KamikadZe begin add 08.02.2018 18:39:38 ДиРБезНДС
			НаборЗаписейДоходыИРасходы.РасходБезНДС = СуммаВсего;
			//KamikadZe end add
			
			НаборЗаписейДоходыИРасходы.Приход();
		КонецЕсли;
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.ЕстьСтавкаНДС=Истина;
	НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	НаборЗаписейПартии.ПоБазовомуКоличеству=Истина;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	НаборЗаписейПартии.РежимДопРасходы=1;
	Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
	
	// запишем в ДоходыИРасходы услуги стоимость которых не распределяется на себестоимость поступления
	НайденнаяСтрокаУслуг=ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Услуга,"ВидНоменклатуры");
	Если НайденнаяСтрокаУслуг<>Неопределено Тогда
		СуммаУслуг = 0;
		Для Каждого СтрокаУслуги Из НайденнаяСтрокаУслуг.Строки Цикл
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ШапкаДокумента = ШапкаДокумента;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = СтрокаУслуги.СтатьяДопРасходов;
			НаборЗаписейДиР.ВУпрВалюте = (ШапкаДокумента.ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДиР.Расход = СтрокаУслуги.СуммаВсего;
			
			//KamikadZe begin add 08.02.2018 18:40:43 ДиРБезНДС
			НаборЗаписейДиР.РасходБезНДС = СтрокаУслуги.СуммаВсего;
			//KamikadZe end add
			
			СуммаУслуг = СуммаУслуг+СтрокаУслуги.СуммаВсего;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЦикла;
		// способ ведения не по подразделениям то ничего не будем корректировать
		Если ВедетсяБалансПоПодразделению и БалансовыеПодразделенияНеРавны  и СуммаУслуг<>0 Тогда	
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Ложь;
			НаборЗаписейДиР.Доход = СуммаУслуг; 
			
			//KamikadZe begin add 08.02.2018 18:41:01 ДиРБезНДС
			НаборЗаписейДиР.ДоходБезНДС = СуммаУслуг;
			//KamikadZe end add
						
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
		КонецЕсли;	
	КонецЕсли;

	// двигаем границу последовательности партий
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	ФлагУпрВалюты = (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	
	ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	
	// подготовим таблицу для накопления сумм суммовых разниц по подразделениям и сумм по взаиморасчетам
	ТаблицаСуммовыхРазниц = Новый ТаблицаЗначений();
	ОписаниеТиповПодразделение=Новый ОписаниеТипов;
	ОписаниеТиповПодразделение.Типы().Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Подразделение",ОписаниеТиповПодразделение);
	ОписаниеТиповЧисло=Новый ОписаниеТипов;
	ОписаниеТиповЧисло.Типы().Добавить(Тип("Число"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Сумма",ОписаниеТиповЧисло);
	
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений();
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение",ОписаниеТиповПодразделение);
	ТаблицаВзаиморасчетов.Колонки.Добавить("Сумма",ОписаниеТиповЧисло);
	
	// проводим взаиморасчеты с подотчетником (погашение задолженности подотчетника за полученные деньги)
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.Сделка=?(обЗначениеНеЗаполнено(ДокументОснование),Неопределено,ДокументОснование);
	НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
	НаборЗаписейВзаиморасчеты.Сумма=Расходы.Итог("Сумма")+Товары.Итог("СуммаВсего");
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	СтрокаСуммовыхРазниц = ТаблицаСуммовыхРазниц.Добавить();
	СтрокаСуммовыхРазниц.Подразделение = ДоговорВзаиморасчетов.Подразделение;
	СтрокаСуммовыхРазниц.Сумма = СуммаДоходаРасходаСуммовыхРазниц;
	
	// проводим взаиморасчеты по строкам где на расчеты
	Если Не Отказ Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДокументРасходы.Контрагент КАК Контрагент,
		|	ДокументРасходы.Договор КАК Договор,
		|	ДокументРасходы.Сделка КАК Сделка,
		|	Сумма(ДокументРасходы.Сумма) КАК СуммаВзаиморасчетов
		|ИЗ
		|	Документ.АвансовыйОтчет.Расходы КАК ДокументРасходы
		|
		|ГДЕ  (ДокументРасходы.Ссылка = &ДокументСсылка)
		| 	 И(ДокументРасходы.НаРасчеты = Истина)
		|
		|СГРУППИРОВАТЬ ПО 
		|	ДокументРасходы.Контрагент,
		|	ДокументРасходы.Договор,
		|	ДокументРасходы.Сделка
		|");
		Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
		РезультатЗапросаПоРасходам=Запрос.Выполнить();
		Если НЕ РезультатЗапросаПоРасходам.Пустой() Тогда
			НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
			НаборЗаписейВзаиморасчеты.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.РежимПроведения = Режим;
			Выборка=РезультатЗапросаПоРасходам.Выбрать();
			Пока Выборка.Следующий() И НЕ Отказ Цикл
				НаборЗаписейВзаиморасчеты.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.Контрагент = Выборка.Контрагент;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = Выборка.Договор;
				НаборЗаписейВзаиморасчеты.КурсВзаиморасчетов = МоментВремени();
				Если Не ОбЗначениеНеЗаполнено(Выборка.Сделка) Тогда
					НаборЗаписейВзаиморасчеты.Сделка = Выборка.Сделка;
				Иначе
					НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
				КонецЕсли;
				НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
				НаборЗаписейВзаиморасчеты.Сумма = Выборка.СуммаВзаиморасчетов;
				НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
				
				// доходы и расходы по суммовым разницам
				СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
				СтрокаСуммовыхРазниц = ТаблицаСуммовыхРазниц.Добавить();
				СтрокаСуммовыхРазниц.Подразделение = Выборка.Договор.Подразделение;
				СтрокаСуммовыхРазниц.Сумма = СуммаДоходаРасходаСуммовыхРазниц;
				
				СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
				СтрокаВзаиморасчетов.Подразделение = Выборка.Договор.Подразделение;
				СтрокаВзаиморасчетов.Сумма = Выборка.СуммаВзаиморасчетов;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// доходы и расходы по суммовым разницам
	ТаблицаСуммовыхРазниц.Свернуть("Подразделение","Сумма");
	Если ВедетсяБалансПоПодразделению Тогда
		Для Каждого СтрокаСуммовыхРазниц Из ТаблицаСуммовыхРазниц Цикл
			Если СтрокаСуммовыхРазниц.Сумма<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.Подразделение = СтрокаСуммовыхРазниц.Подразделение;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				Если СтрокаСуммовыхРазниц.Сумма<0 Тогда
					НаборЗаписейДиР.Расход = -СтрокаСуммовыхРазниц.Сумма; 
					
					//KamikadZe begin add 08.02.2018 18:44:08 ДиРБезНДС
					НаборЗаписейДиР.РасходБезНДС = СтрокаСуммовыхРазниц.Сумма;
					//KamikadZe end add					
					
				Иначе
					НаборЗаписейДиР.Доход = СтрокаСуммовыхРазниц.Сумма; 
					
					//KamikadZe begin add 08.02.2018 18:44:08 ДиРБезНДС
					НаборЗаписейДиР.ДоходБезНДС = СтрокаСуммовыхРазниц.Сумма;
					//KamikadZe end add
					
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЦикла;
	Иначе
		СуммаДоходаРасходаСуммовыхРазниц = ТаблицаСуммовыхРазниц.Итог("Сумма");
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц; 
				
				//KamikadZe begin add 08.02.2018 18:45:24 ДиРБезНДС
				НаборЗаписейДиР.РасходБезНДС = -СуммаДоходаРасходаСуммовыхРазниц;
				//KamikadZe end add	
				
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				
				//KamikadZe begin add 08.02.2018 18:45:29 ДиРБезНДС
				НаборЗаписейДиР.ДоходБезНДС = СуммаДоходаРасходаСуммовыхРазниц;
				//KamikadZe end add
				
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// доходы и расходы по взаиморасчетам
	ТаблицаВзаиморасчетов.Свернуть("Подразделение","Сумма");
	Для Каждого СтрокаВзаиморасчетов Из ТаблицаВзаиморасчетов Цикл
		ПодразделениеВзаиморасчета = обПолучитьБалансовоеПодразделение(СтрокаВзаиморасчетов.Подразделение);
		ПодразделениеДоговор = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = ПодразделениеДоговор<>ПодразделениеВзаиморасчета;
		Если ВедетсяБалансПоПодразделению и БалансовыеПодразделенияНеРавны и СтрокаВзаиморасчетов.Сумма<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = СтрокаВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = ФлагУпрВалюты;
			НаборЗаписейДиР.Доход = СтрокаВзаиморасчетов.Сумма; 
			
			//KamikadZe begin add 08.02.2018 18:44:08 ДиРБезНДС
			НаборЗаписейДиР.ДоходБезНДС = СтрокаВзаиморасчетов.Сумма;
			//KamikadZe end add
			
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = ФлагУпрВалюты;
			НаборЗаписейДиР.Расход = СтрокаВзаиморасчетов.Сумма;  
			
			//KamikadZe begin add 08.02.2018 18:44:08 ДиРБезНДС
			НаборЗаписейДиР.РасходБезНДС = СтрокаВзаиморасчетов.Сумма;
			//KamikadZe end add
			
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Отказ Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ДокументРасходы.СтатьяРасхода КАК СтатьяРасхода,
		|	СУММА(ДокументРасходы.Сумма) КАК СуммаРасхода
		|ИЗ
		|	Документ.АвансовыйОтчет.Расходы КАК ДокументРасходы
		|ГДЕ
		|	ДокументРасходы.Ссылка = &ДокументСсылка
		|	И ДокументРасходы.НаРасчеты = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументРасходы.СтатьяРасхода");
		Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
		РезультатЗапросаПоРасходам = Запрос.Выполнить();
		
		Если НЕ РезультатЗапросаПоРасходам.Пустой() Тогда
			Выборка = РезультатЗапросаПоРасходам.Выбрать();
			Пока Выборка.Следующий() И НЕ Отказ Цикл
				НаборЗаписейДиР							= Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
				// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
				Если ВедетсяБалансПоПодразделению Тогда
					НаборЗаписейДиР.Подразделение 			= ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов	= Выборка.СтатьяРасхода;
				НаборЗаписейДиР.ВУпрВалюте				= ФлагУпрВалюты;
				НаборЗаписейДиР.Расход					= Выборка.СуммаРасхода; 
				
				//KamikadZe begin add 08.02.2018 18:46:58 ДиРБезНДС
				НаборЗаписейДиР.РасходБезНДС    		= Выборка.СуммаРасхода;
				//KamikadZe end add
				
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// Произведем движения на основе ТЧ товары
	Если Товары.Количество()>0 Тогда
		// проведем остатки товаров
		НаборЗаписейОстатки								= Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения				= Режим;
		НаборЗаписейОстатки.ДокументОбъект				= ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам	= Неопределено;
		НаборЗаписейОстатки.СкладКомпании				= СкладКомпании;
		НаборЗаписейОстатки.Приходовать					= Истина;
		НаборЗаписейОстатки.Резервировать				= Ложь;
		НаборЗаписейОстатки.Контрагент					= Контрагент;
		НаборЗаписейОстатки.ПоБазовомуКоличеству		= Истина;
		Если СкладКомпании.Розничный Тогда
			НаборЗаписейОстатки.ДвиженияПоРознице			= Истина;
			НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная	= "ЦенаРозничная";
			НаборЗаписейОстатки.ИмяРеквизитаСуммаРозничная  = "СуммаРозничная";
			НаборЗаписейОстатки.РазрешитьПереоценку			= Истина;
		Иначе
			НаборЗаписейОстатки.ДвиженияПоРознице			= Ложь;
		КонецЕсли;
		Отказ = Не НаборЗаписейОстатки.Приход() ИЛИ Отказ;
		
		// проведем партии товаров
		Отказ = Не ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;		
		
		// если приходуем товар на розничный склад, то установим розничные цены на этот товар
		Если НЕ Отказ И СкладКомпании.Розничный И ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
			НаборЗаписейЦены						= Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейЦены.Контрагент				= Неопределено;
			НаборЗаписейЦены.ТипЦен					= СкладКомпании.ТипЦенРозничнойТорговли;
			НаборЗаписейЦены.ПодразделениеКомпании	= СкладКомпании.Подразделение;
			НаборЗаписейЦены.УстанавливатьЦеныУслуг	= Ложь;
			НаборЗаписейЦены.ИмяРеквизитаЦена		= "ЦенаРозничная";
			Отказ = Не НаборЗаписейЦены.УстановитьЦены() Или Отказ;
		КонецЕсли;

		// установим цены контрагентов
		Если НЕ Отказ И ТипЦен.РегистрироватьЦеныПоПриходу И (НЕ обЗначениеНеЗаполнено(Контрагент)) И НЕ ТипЦен.Рассчитывается Тогда
			НаборЗаписейЦены						= Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейЦены.Контрагент				= Контрагент;
			НаборЗаписейЦены.ТипЦен					= ТипЦен;
			НаборЗаписейЦены.УстанавливатьЦеныУслуг	= Истина;
			НаборЗаписейЦены.ИмяРеквизитаЦена		= "Цена";
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
		// установим закупочные цены контрагентов, если, конечно контрагент имеется
		Если НЕ Отказ И ПодразделениеКомпании.ФормироватьЗакупочнуюЦену И НЕ Справочники.ТипыЦен.ОсновнойТипЦенЗакупки.Рассчитывается Тогда
			НаборЗаписейЦены						= Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейЦены.Контрагент				= Неопределено;
			НаборЗаписейЦены.ИмяРеквизитаЦена		= "Цена";
			НаборЗаписейЦены.ТипЦен					= Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
		//установим нормативные цены компании
		Если НЕ Отказ И ПодразделениеКомпании.ФормироватьНормативнуюЦену И НЕ Справочники.ТипыЦен.НормативнаяЦена.Рассчитывается Тогда
			НаборЗаписейЦены						= Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейЦены.Контрагент				= Неопределено;
			НаборЗаписейЦены.ИмяРеквизитаЦена		= "Цена";
			НаборЗаписейЦены.ТипЦен					= Справочники.ТипыЦен.НормативнаяЦена;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
		// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
		// необходимо списать на доходы и расходы.
		Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;		

	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
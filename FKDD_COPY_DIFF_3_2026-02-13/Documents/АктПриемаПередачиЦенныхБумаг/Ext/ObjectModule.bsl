// Модуль документа АктПриемаПередачиЦенныхБумаг

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("ЦеннаяБумага",3);
	ОбязательныеТовары.Вставить("Сумма",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СтатьяДДС",1);
	ОбязательныеРеквизиты.Вставить("ЦенныеБумаги",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  "Ошибки"      - строка, в случае некорректного заполнения содержит описание ошибок.
//  ДопРеквизиты  - структура, на вход может быть передана структура с дополнительными реквизитами для проверки
//  Заполнение    - булево, флаг проверки,
//  Уникальность  - булево, флаг проверки.
//
// Возвращаемое значение:
//  Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
			
	СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
	СпособВеденияБаланса       = Константы.СпособВеденияБаланса.Получить();
	
	// Проведем проверку соответсвия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению",
	// то дополнительно осуществляется проверка соответсвия подразделений.
	Если Результат Тогда
		БалансВедетсяПоОрганизации = (СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ (СпособКонтроляКорректности<>Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует) Тогда
			
			Если СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению Тогда 
				ПроверятьПоПодразделению = Истина;
			Иначе
				ПроверятьПоПодразделению = Ложь;
			КонецЕсли;
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", ПроверятьПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			// Если баланс ведется по организации, то необходимо проверить также реквизит табличной части "Ценная бумага".
			// Для права же "КонтрольКорректностиДоговораИСкладаДокумента" этот реквизит не интересен.
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыЦенныеБумаги = Новый Структура();
				КонтролируемыеРеквизитыЦенныеБумаги.Вставить("ЦеннаяБумага", Ложь);
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("ЦенныеБумаги",КонтролируемыеРеквизитыЦенныеБумаги);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);		
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	// У ценных бумаг, обязательно должно быть выбрано подразделение.
	Для Каждого ТекСтрока Из ЦенныеБумаги Цикл  	
		Если ТекСтрока.ЦеннаяБумага.Подразделение.Пустая() Тогда
			Ошибки = ?(ПустаяСтрока(Ошибки), "", Символы.ПС) + "Ошибка! У ценной бумаги <" + ТекСтрока.ЦеннаяБумага + "> не выбрано подразделение.";		
			Результат = Ложь;	
		КонецЕсли;
	КонецЦикла;
	
	// Проверим, а не указал ли пользователь отрицательные суммы. Ценная бумага не может быть на отрицательную сумму.
	Для Каждого ТекСтрокаТЧ Из ЦенныеБумаги Цикл  	
		Если ТекСтрокаТЧ.Сумма<0 Тогда
			Результат = Ложь;
			дкДобавитьОшибку(Ошибки, "Ошибка! Табличная часть <Ценные бумаги>, отрицательное значение суммы. Строка: " + ТекСтрокаТЧ.НомерСтроки);	
		КонецЕсли;
	КонецЦикла; 	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура.
//
// Возвращаемое значение:
//  Возвращает Истина, если изменения возможны, ложь иначе
//  Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПриемЦенныхБумаг","Акт приема ценных бумаг",Истина);
	СписокХозОпераций.Добавить("ПередачаЦенныхБумаг","Акт передачи ценных бумаг");

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат ЦенныеБумаги.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка			- Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="ЦенныеБумаги.ЦеннаяБумага" Тогда
		
		ТекСтрока.Номинал = обПересчет(ТекСтрока.ЦеннаяБумага.Номинал, ТекСтрока.ЦеннаяБумага.ВалютаДенежныхСредств, Дата, ВалютаДокумента, КурсДокумента);
        ОбработкаРеквизита("ЦенныеБумаги.Номинал",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ЦенныеБумаги.Номинал" Тогда	
		
		Если обЗначениеНеЗаполнено(ТекСтрока.Количество) Тогда
			ТекСтрока.Количество = 1;
		КонецЕсли;
		ОбработкаРеквизита("ЦенныеБумаги.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ЦенныеБумаги.Количество" Тогда
		
		ТекСтрока.Сумма = ТекСтрока.Номинал * ТекСтрока.Количество;
 
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "АктПриемаПередачиЦенныхБумаг"
//
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  Возвращает сформированный табличный документ.
//
Функция ПечатьАктПриемаПередачиЦенныхБумаг(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("АктПриемаПередачиЦенныхБумаг");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	_ОтФирмы		= "_________________________";
	_ДоверенностьФ	= "_________________________";
	_ОтКлиента		= "_________________________";
	_ДоверенностьК	= "_________________________";
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	СтрокаПредставления = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрокаПредставления;
	
	ОбластьМакета.Параметры.Организация = Организация;
	
	Если ХозОперация = Справочники.ХозОперации.ПередачаЦенныхБумаг Тогда
		Текст1 = "
			|" + спПолучитьНаименование(Организация) + " в лице Исполнительного директора "+_ОтФирмы+", "+
			"действующего на основании доверенности № "+_ДоверенностьФ+", с одной стороны и "+
			спПолучитьНаименование(Контрагент)+" в лице "+_ОтКлиента+", действующего на основании "+ 
			"доверенности № "+_ДоверенностьК+", с другой стороны, составили настоящий акт о том, что "+
			спПолучитьНаименование(Организация)+" передал, а "+спПолучитьНаименование(Контрагент)+" приняла ценные бумаги "+
			"со следующими реквизитами:";
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ПриемЦенныхБумаг Тогда
		Текст1 = "
			|" + спПолучитьНаименование(Организация) + "в лице Исполнительного директора "+_ОтФирмы+", "+
			"действующего на основании доверенности № "+_ДоверенностьФ+", с одной стороны и "+
			спПолучитьНаименование(Контрагент)+" в лице "+_ОтКлиента+", действующего на основании "+ 
			"доверенности № "+_ДоверенностьК+", с другой стороны, составили настоящий акт о том, что "+
			спПолучитьНаименование(Контрагент)+" передала, а "+спПолучитьНаименование(Организация)+" принял ценные бумаги "+
			"со следующими реквизитами:";
	КонецЕсли;
		
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;	
	ОбластьМакета.Параметры.Текст1 = Текст1;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапкаТаблицы.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	//корректировка таблицы
	ВыборкаТабличнойЧасти = ЦенныеБумаги.Выгрузить().Скопировать();
	ВыборкаТабличнойЧасти.Колонки.Добавить("СуммаНом", Новый ОписаниеТипов("Число"));
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		СтрокаТабличнойЧасти.СуммаНом = СтрокаТабличнойЧасти.ЦеннаяБумага.Номинал;
	КонецЦикла;

	//заполняем подвал, чтобы получить его высоту
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ТабДокументПодвал = Новый ТабличныйДокумент;
	Если ХозОперация = Справочники.ХозОперации.ПередачаЦенныхБумаг Тогда
		Передал = СтрокаПредставления;
		Принял  = спПолучитьПредставление(Контрагент);
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ПриемЦенныхБумаг Тогда
		Принял  = СтрокаПредставления;
		Передал = спПолучитьПредставление(Контрагент);
	КонецЕсли;
	ИтогоНом = ВыборкаТабличнойЧасти.Итог("СуммаНом");
	Итого = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьМакета.Параметры.Текст2 = "1. Итого по номиналу: " + Формат(ИтогоНом, ФорматВыводаСуммы) + " " + ВалютаДокумента +  " ("+обЧислоПрописью(ИтогоНом,ВалютаДокумента) + ")";
	ОбластьМакета.Параметры.Текст3 = "2. Сумма сделки: " + Формат(Итого, ФорматВыводаСуммы) + " " + ВалютаДокумента + " ("+обЧислоПрописью(Итого,ВалютаДокумента) + ")";
	ОбластьМакета.Параметры.Текст4 = "Приложение: " + Комментарий;
	ОбластьМакета.Параметры.Передал = Передал;
	ОбластьМакета.Параметры.Принял = Принял;
	ТабДокументПодвал.Вывести(ОбластьМакета);
	
	//Получаем область строки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ИтогНом=0;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		текЦеннаяБумага = СтрокаТабличнойЧасти.ЦеннаяБумага;
		ОбластьМакета.Параметры.ЦеннаяБумага = спПолучитьНаименование(текЦеннаяБумага);
        ОбластьМакета.Параметры.ЦеннаяБумагаКод = текЦеннаяБумага.Код;
		ОбластьМакета.Параметры.ЦеннаяБумагаНоминал = Формат(текЦеннаяБумага.Номинал,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ЦеннаяБумагаДатаПринятияКУчету = Формат(текЦеннаяБумага.ДатаПринятияКУчету, "ДЛФ=Д");
		ОбластьМакета.Параметры.ЦеннаяБумагаПредполагаемаяДатаПродажи = Формат(текЦеннаяБумага.ПредполагаемаяДатаПродажи, "ДЛФ=Д");
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ТабДокументПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
			
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;

	ТабДокумент.Вывести(ТабДокументПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАктПриемаПередачиЦенныхБумаг()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		БылиДвижения = Ложь;
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// деньги
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.СтатьяДДС=СтатьяДДС;
	
	// взаиморасчеты
	СуммаДоходаРасходаСуммовыхРазниц=0;
	
	// БАЛАНС: определяем, нужно ли будет вводить корректирующие движения
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);	
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений;
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпр",      Новый ОписаниеТипов("Число"));
	
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
	НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Неопределено;
	НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок=АвтоЗакрытиеСделок;
	Для Каждого СтрокаТЧ Из ЦенныеБумаги Цикл
		НаборЗаписейДС.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.Валюта = Неопределено;
		НаборЗаписейВзаиморасчеты.Сумма=СтрокаТЧ.Сумма;
		НаборЗаписейДС.СтруктурнаяЕдиница=СтрокаТЧ.ЦеннаяБумага;
		НаборЗаписейДС.Сумма=СтрокаТЧ.Сумма;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		
		ЭтоПриемЦенныхБумаг = (ХозОперация=Справочники.ХозОперации.ПриемЦенныхБумаг);
		Если Не ЭтоПриемЦенныхБумаг Тогда
			Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			Отказ=НЕ НаборЗаписейДС.Расход() ИЛИ Отказ;
			СуммаДоходаРасходаСуммовыхРазниц=СуммаДоходаРасходаСуммовыхРазниц+НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;									
		Иначе // Прием ценных бумаг
			Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
			Отказ=НЕ НаборЗаписейДС.Приход() ИЛИ Отказ;
			СуммаДоходаРасходаСуммовыхРазниц=СуммаДоходаРасходаСуммовыхРазниц+НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		
		// БАЛАНС: Если ведется баланс по подразделению и соответствующие балансовые подразделения 
		// подразделения ценной бумаги и подразделения договора не совпадают, то будет разрыв баланса.
		// Необходимо будет добавить корректирующие движения.
		Если ВедетсяБалансПоПодразделению Тогда
			ПодразделениеЦеннойБумаги = обПолучитьБалансовоеПодразделение(СтрокаТЧ.ЦеннаяБумага.Подразделение);		
			Если ПодразделениеЦеннойБумаги<>ПодразделениеДоговорВзаиморасчетов Тогда
				// Необходимо получить сумму в управленческой валюте. 
				// Она автоматом рассчитывается в модуле набора записей.
				СуммаЗаписиУпр = НаборЗаписейДС.ДополнительныеСвойства.СуммаЗаписиУпр;				
				
				// Добавляем строку в таблицу.
				НоваяСтрока = ТаблицаВзаиморасчетов.Добавить();
				НоваяСтрока.Подразделение = СтрокаТЧ.ЦеннаяБумага.Подразделение;
				НоваяСтрока.СуммаУпр      = СуммаЗаписиУпр; 															
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
	// БАЛАНС: Если таблица значений "ТаблицаВзаиморасчетов" не пустая, значит необходимо создать корректирующие записи
	// Предварительно свернем таблицу по подразделению, чтобы уменьшить количество создаваемых записей.
	Если ТаблицаВзаиморасчетов.Количество()<>0 Тогда
		ТаблицаВзаиморасчетов.Свернуть("Подразделение", "СуммаУпр");
		Для Каждого ТекСтрока Из ТаблицаВзаиморасчетов  Цикл  	
			Если Не ЭтоПриемЦенныхБумаг Тогда // Передача ценных бумаг				
				Если ТекСтрока.СуммаУпр<>0 Тогда
					// Корректируем увеличение денежных средств.  					
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение          = ТекСтрока.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте             = Истина;
					НаборЗаписейДиР.Доход                  = ТекСтрока.СуммаУпр;
					Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
					
					// Корректируем рост дебиторская задолженность. 
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетов.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					НаборЗаписейДиР.Доход      = ТекСтрока.СуммаУпр;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли; 				
			Иначе // Прием ценных бумаг. 				
				Если ТекСтрока.СуммаУпр<>0 Тогда
					// Корректируем уменьшение денежных средств. 
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ТекСтрока.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					НаборЗаписейДиР.Доход = ТекСтрока.СуммаУпр;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
					
					// Корректируем рост кредиторская задолженность.
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетов.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					НаборЗаписейДиР.Расход = ТекСтрока.СуммаУпр;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;  	
	
	// доходы и расходы по суммовым разницам
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		
		// Если баланс ведется по подразделениям, то необходимо принудительно установить подразделение договора.
		// В противном случае, подразделение передавать не нужно, т.к. будет автоматически задействовано подразделение
		// шапки документа.
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
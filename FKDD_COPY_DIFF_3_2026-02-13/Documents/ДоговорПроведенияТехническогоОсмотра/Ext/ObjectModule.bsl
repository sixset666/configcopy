
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ОбязательныеРеквизиты.Вставить("ПредставительКонтрагента",1);
	КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Автомобиль",1);
	ОбязательныеРеквизиты.Вставить("ТипПриводаТормознойСистемы",1);
	ОбязательныеРеквизиты.Вставить("МаркаШин",1);
	ОбязательныеРеквизиты.Вставить("МассаБезНагрузки",1);
	ОбязательныеРеквизиты.Вставить("РазрешеннаяМаксимальнаяМасса",1);
	ОбязательныеРеквизиты.Вставить("Номенклатура",1);
	ОбязательныеРеквизиты.Вставить("ТипТоплива",1);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ДоговорПроведенияТехническогоОсмотра","Договор проведения технического осмотра",Истина);
	СписокХозОпераций.Добавить("ПовторныйДоговорПроведенияТехническогоОсмотра","Повторный договор проведения технического осмотра",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	
	Результат = Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Комплект,         0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Набор,            0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Тара,             0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Товар,            0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,     0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Диски,            0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.НомерныеАгрегаты, 0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Шины,             0);
	
	Возврат Результат;
	
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Расчет суммы документа
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Рез=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Дата" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
			Пробег=Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль,Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,Дата);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя = "ТипЦен" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТребуетсяПересчет = Истина;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет",ТребуетсяПересчет) Тогда
				ТребуетсяПересчет = Истина;
			КонецЕсли;
		КонецЕсли;
		Если ТребуетсяПересчет Тогда
			Рез = ОбработкаРеквизита("Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя = "Контрагент" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
			Если НЕ РаботаСКонтактнымиЛицами.ЭтоВедущий(ПредставительКонтрагента, Контрагент) Тогда
				ПредставительКонтрагента = Справочники.Контрагенты.ПустаяСсылка();
				Рез = ОбработкаРеквизита("ПредставительКонтрагента",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя = "ПредставительКонтрагента" Тогда
		
		Если ЗначениеЗаполнено(ПредставительКонтрагента) И НЕ РаботаСКонтактнымиЛицами.ЭтоВедущий(ПредставительКонтрагента, Контрагент) Тогда
			Контрагент = РаботаСКонтактнымиЛицами.ПолучитьВедущегоКонтрагента(ПредставительКонтрагента);
			Рез = ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобиль" Тогда
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
			НовыйКонтрагент = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин,Дата);
			Если НовыйКонтрагент<>Контрагент Тогда
				Контрагент=НовыйКонтрагент;
				Рез=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
			Пробег=Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль,Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,Дата);
			ТипПриводаТормознойСистемы=обПолучитьЗначениеСвойства(Автомобиль.Модель,ПланыВидовХарактеристик.СвойстваОбъектов.ТипПриводаТормознойСистемы);
			МассаБезНагрузки=обПолучитьЗначениеСвойства(Автомобиль.Модель,ПланыВидовХарактеристик.СвойстваОбъектов.МассаБезНагрузки);
			РазрешеннаяМаксимальнаяМасса=обПолучитьЗначениеСвойства(Автомобиль.Модель,ПланыВидовХарактеристик.СвойстваОбъектов.РазрешеннаяМаксимальнаяМасса);
			
			ТипТоплива = обПолучитьЗначениеСвойства(Автомобиль.ВариантКомплектации, ПланыВидовХарактеристик.СвойстваОбъектов.ТипТоплива);
			Если обЗначениеНеЗаполнено(ТипТоплива) Тогда
				ТипТоплива = обПолучитьЗначениеСвойства(Автомобиль.Модель,ПланыВидовХарактеристик.СвойстваОбъектов.ТипТоплива);
			КонецЕсли;
			
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Номенклатура" Тогда
		НоваяЦена=обПолучитьЦену(ТипЦен,Номенклатура,?(Ссылка.Пустая(), Дата, МоментВремени()),,ВалютаДокумента,КурсДокумента,,,ПодразделениеКомпании,);
		НоваяСтавкаНДС=Номенклатура.СтавкаНДС;
		Если НоваяЦена<>Цена ИЛИ НоваяСтавкаНДС<>СтавкаНДС Тогда
			Цена=НоваяЦена;
			СтавкаНДС=НоваяСтавкаНДС;
			Рез=ОбработкаРеквизита("СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Цена" Тогда
		Рез=ОбработкаРеквизита("СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="СтавкаНДС" Тогда
		Если ТипЦен.ЦенаВключаетНДС Тогда
			СуммаДокумента = Цена;
			СуммаНДС = Цена * СтавкаНДС.Ставка / (100+СтавкаНДС.Ставка);
		Иначе
			СуммаНДС = Цена * СтавкаНДС.Ставка / 100;
			СуммаДокумента = Цена + СуммаНДС;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаНДС" Тогда
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаДокумента" Тогда
		Если ТипЦен.ЦенаВключаетНДС Тогда
			Цена = СуммаДокумента;
			СуммаНДС = Цена * СтавкаНДС.Ставка / (100+СтавкаНДС.Ставка);
		Иначе
			СуммаНДС = СуммаДокумента * СтавкаНДС.Ставка / (100+СтавкаНДС.Ставка);
			Цена = СуммаДокумента - СуммаНДС;
		КонецЕсли;
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
                                      
Функция ПечатьДоговор(ТабДокумент) Экспорт 
	
	//ПТС = спПолучитьПодтверждающийДокументОбъектаПоВиду(Автомобиль,Перечисления.ВидыДокументов.ПТС);
	ПТС = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ТехПаспорт);
	Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
		Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(ПредставительКонтрагента,Перечисления.ВидыДокументов.Паспорт);
		Доверенность = спПолучитьПодтверждающийДокументОбъектаПоВиду(ПредставительКонтрагента,Перечисления.ВидыДокументов.Доверенность);
	Иначе
		Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Паспорт);
	КонецЕслИ;
	
	АттестатТО = спПолучитьПодтверждающийДокументОбъектаПоВиду(ПодразделениеКомпании,Перечисления.ВидыДокументов.АттестатТО);
	
	Макет = ПолучитьМакет("Договор");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект,Истина);
	ОбластьМакета.Параметры.Город = 	киПолучитьГород(ЭтотОбъект.ПодразделениеКомпании,Справочники.ВидыКонтактнойИнформации.АдресФактический);
	ОбластьМакета.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	ОбластьМакета.Параметры.Организация = Организация.НаименованиеПолное;
	
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	Если ЗначениеЗаполнено(Руководитель) И ТипЗнч(Руководитель)=Тип("Структура") Тогда
		Должность = ", в лице";
		Если ЗначениеЗаполнено(Руководитель.РуководительДолжность) Тогда
			Должность = Должность + " " + СокрЛП(НРег(Руководитель.РуководительДолжность));
		КонецЕсли;
		ОбластьМакета.Параметры.ОрганизацияДолжность = Должность;
		
		ОрганизацияЛицоРП = Руководитель.РуководительПредставление;
		ДоверенностьРуководителя = спПолучитьПодтверждающийДокументОбъектаПоВиду(Руководитель.Руководитель,Перечисления.ВидыДокументов.Доверенность);
		Если ЗначениеЗаполнено(ДоверенностьРуководителя) Тогда
			ОрганизацияЛицоРП = ОрганизацияЛицоРП + ", действующего  на основании " + ДоверенностьРуководителя;
		КонецЕсли;
		ОбластьМакета.Параметры.ОрганизацияЛицоРП = ОрганизацияЛицоРП;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АттестатТО) Тогда
		Текст =  ", на основании аттестата аккредитации оператора технического осмотра № " + АттестатТО.Номер;
		Если ЗначениеЗаполнено(АттестатТО.ДатаВыдачи) Тогда
			Текст = Текст + ", выдан " + Формат(АттестатТО.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г."; 	
		КонецЕсли;
		Если ЗначениеЗаполнено(АттестатТО.НомерВРеестреОператоровТО) Тогда
			Текст = Текст + ", номер в реестре операторов технического осмотра: " + СокрЛП(АттестатТО.НомерВРеестреОператоровТО); 	
		КонецЕсли;
		ОбластьМакета.Параметры.АттестатТО = Текст;
	КонецЕсли;
	
	ОбластьМакета.Параметры.Покупатель = Контрагент.НаименованиеПолное;
	Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
		ОбластьМакета.Параметры.Представитель = ", в лице " + ПредставительКонтрагента;
	КонецЕсли;
	Если ЗначениеЗаполнено(Паспорт) Тогда
		ПаспортныеДанные = "паспорт серии " + Паспорт.Серия + " № "+ Паспорт.Номер + " выдан " + Паспорт.КемВыдан + " " + Формат(Паспорт.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г.";
		ОбластьМакета.Параметры.ПаспортныеДанные = " (" + ПаспортныеДанные + ")";
	КонецЕсли;
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ОбластьМакета.Параметры.Доверенность = ", действующего  на основании " + Доверенность;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Автомобиль.Модель) Тогда
		Если ЗначениеЗаполнено(Автомобиль.Модель.КатегорияТС) Тогда
			ОбластьМакета.Параметры.КатегорияТС = Автомобиль.Модель.КатегорияТС.КодПоКлассификатору;
		КонецЕсли;
		ОбластьМакета.Параметры.МодельТС = Автомобиль.Модель;  
	КонецЕсли;
	ОбластьМакета.Параметры.МодификацияТС = Автомобиль.ВариантКомплектации;
	ОбластьМакета.Параметры.VIN = Автомобиль.VIN;
	Если ЗначениеЗаполнено(ПТС) Тогда
		ОбластьМакета.Параметры.ПТС ="";
		ОбластьМакета.Параметры.ПТС = Строка(ПТС.ВидПодтверждающегоДокумента) + " " + ПТС.Серия + " № "+ ПТС.Номер + " выдан " + ПТС.КемВыдан + " " + Формат(ПТС.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г. ";
	КонецЕсли;
	ОбластьМакета.Параметры.АдресТехосмотра = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.АдресФактический);
	Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.АдресТехосмотра)) Тогда
		ОбластьМакета.Параметры.АдресТехосмотра = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресФактический);	
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДатаПроведения = Формат(ДатаПроведения,"ДФ='dd MMMM yyyy '")+ "г. ";
	ОбластьМакета.Параметры.СрокПредупреждения = "7 дней";
	ОбластьМакета.Параметры.Сумма = обЧислоПрописью(СуммаДокумента,ЭтотОбъект.ВалютаДокумента);
	ОбластьМакета.Параметры.ПроцентНеустойкиЗаказчик = "__";
	ОбластьМакета.Параметры.ПроцентНеустойкиИсполнитель = "__";
	
	СтруктураПредставления=Новый Структура(
	"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ОбластьМакета.Параметры.ОписаниеИсполнителя = спПолучитьПредставление(ПодразделениеКомпании,
		СтруктураПредставления, ДополнительныеПараметры);
	
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.ДляПечати = Истина;
		ДополнительныеПараметры.РасчетныйСчетДокумента = Организация.ОсновнойБанковскийСчет;
		ОписаниеЗаказчика = спПолучитьПредставление(Контрагент, СтруктураПредставления, ДополнительныеПараметры);
	Иначе
		ОписаниеЗаказчика = спПолучитьНаименование(Контрагент);
		ЗаказчикПочтовыйАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		Если ЗначениеЗаполнено(ПаспортныеДанные) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + "," + Символы.ПС + ПаспортныеДанные;
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗаказчикПочтовыйАдрес) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Адрес: " + ЗаказчикПочтовыйАдрес;
		КонецЕсли;
		ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		Если ЗначениеЗаполнено(ЗаказчикТелефоны) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Телефон: " +  ЗаказчикТелефоны;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
		ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Контактное лицо: " +  спПолучитьНаименование(ПредставительКонтрагента);
		Если ЗначениеЗаполнено(Доверенность) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + Доверенность;
		КонецЕсли;
	КонецЕсли;
	ОбластьМакета.Параметры.ОписаниеЗаказчика = ОписаниеЗаказчика;
	
	ТабДокумент.Вывести(ОбластьМакета);
	Возврат ТабДокумент;	
КонецФункции

Функция ПечатьПовторныйДоговор(ТабДокумент) Экспорт
	
	//ПТС = спПолучитьПодтверждающийДокументОбъектаПоВиду(ДокументОбъект.Автомобиль,Перечисления.ВидыДокументов.ПТС);
	Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
		Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(ПредставительКонтрагента,Перечисления.ВидыДокументов.Паспорт);
		Доверенность = спПолучитьПодтверждающийДокументОбъектаПоВиду(ПредставительКонтрагента,Перечисления.ВидыДокументов.Доверенность);
	Иначе
		Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Паспорт);
	КонецЕслИ;
	
	Макет = ПолучитьМакет("ПовторныйДоговор");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект,Истина);
	
	ЗапросАдреса = Новый Запрос();
	ЗапросАдреса.Текст = "ВЫБРАТЬ
	                     |	КонтактнаяИнформация.Поле4
	                     |ИЗ
	                     |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                     |ГДЕ
	                     |	КонтактнаяИнформация.Объект = &Объект
	                     |	И КонтактнаяИнформация.Тип = &Тип
	                     |	И КонтактнаяИнформация.Вид = &Вид";
						 
	ЗапросАдреса.УстановитьПараметр("Объект", ПодразделениеКомпании);
	ЗапросАдреса.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	ЗапросАдреса.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресФактический);
	ВыборкаАдреса = ЗАпросАдреса.Выполнить().Выбрать();
	Если ВыборкаАдреса.Следующий() Тогда
		Адрес = СокрЛП(ВыборкаАдреса.Поле4);
	Иначе
		Адрес = "";
	КонецЕсли;
	
	ОбластьМакета.Параметры.Город = Адрес;
	
	ОбластьМакета.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	
	ОбластьМакета.Параметры.Покупатель = Контрагент.НаименованиеПолное;
	Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
		ОбластьМакета.Параметры.Представитель = ", в лице " + ПредставительКонтрагента;
	КонецЕсли;
	Если ЗначениеЗаполнено(Паспорт) Тогда
		ПаспортныеДанные = "паспорт серии " + Паспорт.Серия + " № "+ Паспорт.Номер + " выдан " + Паспорт.КемВыдан + " " + Формат(Паспорт.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г.";
		ОбластьМакета.Параметры.ПаспортныеДанные = " (" + ПаспортныеДанные + ")";
	КонецЕсли;
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ОбластьМакета.Параметры.Доверенность = ", действующего  на основании " + Доверенность;
	КонецЕсли;

	
	ОбластьМакета.Параметры.Организация = Организация.НаименованиеПолное;
	Если ЗначениеЗаполнено(Автор) И ЗначениеЗаполнено(Автор.Сотрудник) Тогда
		ОбластьМакета.Параметры.ОрганизацияДолжность = НРег(Автор.Сотрудник.Должность);
		ОбластьМакета.Параметры.ОрганизацияЛицоРП = Автор.Сотрудник;
	КонецЕсли;
	
		
	//Если ЗначениеЗаполнено(ПТС) Тогда
	//	ОбластьМакета.Параметры.ПТС = "ПТС " + ПТС.Серия + " № "+ ПТС.Номер + " выдан " + ПТС.КемВыдан + " " + Формат(ПТС.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г. ";
	//КонецЕсли;
	ОбластьМакета.Параметры.АдресТехосмотра = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.АдресФактический);
	Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.АдресТехосмотра)) Тогда
		ОбластьМакета.Параметры.АдресТехосмотра = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресФактический);	
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДатаПроведения = Формат(ДатаПроведения, "ДЛФ=DD");
	//ОбластьМакета.Параметры.СрокПредупреждения = "7 дней";
	ОбластьМакета.Параметры.Сумма = обЧислоПрописью(СуммаДокумента,ВалютаДокумента);
	//ОбластьМакета.Параметры.ПроцентНеустойкиЗаказчик = "__";
	//ОбластьМакета.Параметры.ПроцентНеустойкиИсполнитель = "__";
	
	СтруктураПредставления=Новый Структура(
	"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = Организация.ОсновнойБанковскийСчет;
	ОбластьМакета.Параметры.ОписаниеИсполнителя = спПолучитьПредставление(
		ПодразделениеКомпании, СтруктураПредставления, ДополнительныеПараметры);

	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.ДляПечати = Истина;
		ДополнительныеПараметры.РасчетныйСчетДокумента = Организация.ОсновнойБанковскийСчет;
		ОписаниеЗаказчика = спПолучитьПредставление(Контрагент, СтруктураПредставления, ДополнительныеПараметры);
	Иначе
		ОписаниеЗаказчика = спПолучитьНаименование(Контрагент);
		ЗаказчикПочтовыйАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		Если ЗначениеЗаполнено(ПаспортныеДанные) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + "," + Символы.ПС + ПаспортныеДанные;
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗаказчикПочтовыйАдрес) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Адрес: " + ЗаказчикПочтовыйАдрес;
		КонецЕсли;
		ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		Если ЗначениеЗаполнено(ЗаказчикТелефоны) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Телефон: " +  ЗаказчикТелефоны;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставительКонтрагента) Тогда
		ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Контактное лицо: " +  спПолучитьНаименование(ПредставительКонтрагента);
		Если ЗначениеЗаполнено(Доверенность) Тогда
			ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + Доверенность;
		КонецЕсли;
	КонецЕсли;
	ОбластьМакета.Параметры.ОписаниеЗаказчика = ОписаниеЗаказчика;
	
	ТабДокумент.Вывести(ОбластьМакета);
	Возврат ТабДокумент;	
	
КонецФункции

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		Пробег=Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль,Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,Дата);
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
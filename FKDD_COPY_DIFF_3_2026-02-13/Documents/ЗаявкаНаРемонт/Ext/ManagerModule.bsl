
// Контроллирует корректность заполнения ТЧ Планирование в зависимости от значения прав
// "КонтрольПересеченияВремениРабот", "КонтрольПланированияПрошедшимВременем"
//
Функция КонтрольЗаполненияРабот(ЭтотОбъект, Отказ, РежимЗаписи, ДатаНачалаВремя=Неопределено, ДатаОкончанияВремя=Неопределено) Экспорт
	Результат = Истина;
	
	// Контроль наложения работ в планировании
	ВариантКонтроляПересечения = обПраво("КонтрольПересеченияВремениРабот");
	Если НЕ (ВариантКонтроляПересечения = Перечисления.ВариантыОтветов.Нет) И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ГрафикРаботыРесурсов.Ресурс1 КАК Цех,
		               |	ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.НачалоРабочегоВремени)) КАК Начало,
		               |	ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.КонецРабочегоВремени)) КАК Окончание,
		               |	ГрафикРаботыРесурсов.Объект
		               |ПОМЕСТИТЬ ГрафикиРаботЦехов
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		               |ГДЕ
		               |	ГрафикРаботыРесурсов.Ресурс1 В(&ТаблицаЦехов)
		               |	И ГрафикРаботыРесурсов.Ресурс1 <> ЗНАЧЕНИЕ(Справочник.Цеха.пустаяссылка)
		               |	И ГрафикРаботыРесурсов.Дата >= &ДатаНачала
		               |	И ГрафикРаботыРесурсов.Дата <= &ДатаОкончания
		               |	И ГрафикРаботыРесурсов.Объект <> &Ссылка
		               |	И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.планРемонта)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ГрафикРаботыРесурсов.Ресурс2 КАК Исполнитель,
		               |	ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.НачалоРабочегоВремени)) КАК Начало,
		               |	ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.КонецРабочегоВремени)) КАК Окончание,
		               |	ГрафикРаботыРесурсов.Объект
		               |ПОМЕСТИТЬ ГрафикиРаботИсполнителей
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		               |ГДЕ
		               |	ГрафикРаботыРесурсов.Ресурс2 В(&ТаблицаИсполнителей)
		               |	И ГрафикРаботыРесурсов.Ресурс2 <> ЗНАЧЕНИЕ(Справочник.Сотрудники.пустаяссылка)
		               |	И ГрафикРаботыРесурсов.Дата >= &ДатаНачала
		               |	И ГрафикРаботыРесурсов.Дата <= &ДатаОкончания
		               |	И ГрафикРаботыРесурсов.Объект <> &Ссылка
		               |	И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.планРемонта)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЗаявкаНаРемонтПланирование.РабочееМесто КАК Цех,
		               |	ЗаявкаНаРемонтПланирование.Исполнитель,
		               |	ЗаявкаНаРемонтПланирование.НачалоВыполнения,
		               |	ЗаявкаНаРемонтПланирование.ОкончаниеВыполнения
		               |ПОМЕСТИТЬ ЗаявкаНаРемонт
		               |ИЗ
		               |	&ЗаявкаПланирование КАК ЗаявкаНаРемонтПланирование
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ГрафикиРаботЦехов.Объект КАК Объект,
		               |	ГрафикиРаботЦехов.Цех КАК Ресурс,
		               |	ГрафикиРаботЦехов.Начало КАК Начало,
		               |	ГрафикиРаботЦехов.Окончание КАК Окончание
		               |ИЗ
		               |	ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрафикиРаботЦехов КАК ГрафикиРаботЦехов
		               |		ПО ЗаявкаНаРемонт.НачалоВыполнения < ГрафикиРаботЦехов.Окончание
		               |			И ЗаявкаНаРемонт.ОкончаниеВыполнения > ГрафикиРаботЦехов.Начало
		               |			И ЗаявкаНаРемонт.Цех = ГрафикиРаботЦехов.Цех
		               |
		               |ОБЪЕДИНИТЬ
		               |
		               |ВЫБРАТЬ
		               |	ГрафикиРаботИсполнителей.Объект,
		               |	ГрафикиРаботИсполнителей.Исполнитель,
		               |	ГрафикиРаботИсполнителей.Начало,
		               |	ГрафикиРаботИсполнителей.Окончание
		               |ИЗ
		               |	ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрафикиРаботИсполнителей КАК ГрафикиРаботИсполнителей
		               |		ПО ЗаявкаНаРемонт.Исполнитель = ГрафикиРаботИсполнителей.Исполнитель
		               |			И ЗаявкаНаРемонт.НачалоВыполнения < ГрафикиРаботИсполнителей.Окончание
		               |			И ЗаявкаНаРемонт.ОкончаниеВыполнения > ГрафикиРаботИсполнителей.Начало
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Ресурс,
		               |	Начало,
		               |	Объект";
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
		ЗаявкаПланирование = ЭтотОбъект.Планирование.Выгрузить();
		Запрос.УстановитьПараметр("ЗаявкаПланирование", ЗаявкаПланирование);
		Запрос.УстановитьПараметр("ТаблицаЦехов",ЭтотОбъект.Планирование.ВыгрузитьКолонку("РабочееМесто"));
		Запрос.УстановитьПараметр("ТаблицаИсполнителей",ЭтотОбъект.Планирование.ВыгрузитьКолонку("Исполнитель"));
		Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(ЭтотОбъект.ДатаНачала));
		Запрос.УстановитьПараметр("ДатаОкончания",НачалоДня(КонецДня(ЭтотОбъект.ДатаОкончания)+1));
		ТаблицаПересечений = Запрос.Выполнить().Выгрузить();
	
		// присвоим отказ или продолжим
		Если ТаблицаПересечений.Количество() > 0 Тогда
			
			// построим текст сообщения
			ТекстОшибки = "";
			дкДобавитьОшибку(ТекстОшибки, "В планировании имеются пересечения работ с другими Заявкам на ремонт:");
			Для Каждого ТекСтрока Из ТаблицаПересечений Цикл
				дкДобавитьОшибку(ТекстОшибки, "для ресурса " + Строка(ТекСтрока.Ресурс)+ " с " + Строка(ТекСтрока.Начало) + " по " + Строка(ТекСтрока.Окончание) + " документом <" + Строка(ТекСтрока.Объект) + ">");
			КонецЦикла;
			
			// если право стоит в режиме уходить в отказ или спрашивать
			Если ВариантКонтроляПересечения = Перечисления.ВариантыОтветов.Да Тогда
				Отказ = Истина;
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
				Сообщить(ТекстОшибки, СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				Возврат Результат;
			ИначеЕсли ВариантКонтроляПересечения = Перечисления.ВариантыОтветов.Спрашивать Тогда
				#Если Клиент Тогда
				дкДобавитьОшибку(ТекстОшибки, "Выполнять проведение?");
				Ответ = Вопрос(ТекстОшибки, РежимДиалогаВопрос.ДаНет, обПраво("ТаймаутДиалогов",ЭтотОбъект.Права));
				Если НЕ (Ответ = КодВозвратаДиалога.Да) Тогда
					Отказ = Истина;
					Сообщить("Операция записи была отменена пользователем.",СтатусСообщения.Важное);
					Возврат Результат;
				КонецЕсли;
				#КонецЕсли				
			КонецЕсли;

		КонецЕсли;
	КонецЕсли;
	
	// Контроль планирования в прошлое
	ВариантКонтроляПрошлое = обПраво("КонтрольПланированияПрошедшимВременем");
	Если НЕ (ВариантКонтроляПрошлое = Перечисления.ВариантыОтветов.Нет) И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		ПланированиеПрошлое = Ложь;
		ТекущийДень = НачалоДня(ТекущаяДата());
		
		СписокСтрок = Новый СписокЗначений;
		
		Для Каждого СтрокаПланирования Из ЭтотОбъект.Планирование Цикл
			Если ЭтотОбъект.Ссылка.Планирование.НайтиСтроки(Новый Структура("РабочееМесто,
				|Исполнитель,
				|НачалоВыполнения,
				|ОкончаниеВыполнения",
				СтрокаПланирования.РабочееМесто,
				СтрокаПланирования.Исполнитель,
				СтрокаПланирования.НачалоВыполнения,
 				СтрокаПланирования.ОкончаниеВыполнения)).Количество() = 0  Тогда
				Если (СтрокаПланирования.НачалоВыполнения <> Дата("00010101") И СтрокаПланирования.НачалоВыполнения< ТекущийДень)
					ИЛИ (СтрокаПланирования.ОкончаниеВыполнения <> Дата("00010101") И СтрокаПланирования.ОкончаниеВыполнения < ТекущийДень) Тогда
					СписокСтрок.Добавить(СтрокаПланирования.НомерСтроки);
				КонецЕсли;            
			КонецЕсли;	
		КонецЦикла;
		
		// присвоим отказ или продолжим
		Если СписокСтрок.Количество()>0 Тогда
			// построим текст сообщения
			ТекстОшибки = "";
			дкДобавитьОшибку(ТекстОшибки, "В табличную часть ""Планирование"" внесены изменения в строки с записями за прошлый период!");
			
			// если право стоит в режиме уходить в отказ или спрашивать
			Если ВариантКонтроляПрошлое = Перечисления.ВариантыОтветов.Да Тогда
				Отказ = Истина;
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
				Сообщить(ТекстОшибки, СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				Возврат Результат;
			ИначеЕсли ВариантКонтроляПрошлое = Перечисления.ВариантыОтветов.Спрашивать Тогда
				#Если Клиент Тогда
				дкДобавитьОшибку(ТекстОшибки, "Продолжить?");
				Ответ = Вопрос(ТекстОшибки, РежимДиалогаВопрос.ДаНет, обПраво("ТаймаутДиалогов",ЭтотОбъект.Права));
				Если НЕ (Ответ = КодВозвратаДиалога.Да) Тогда
					Отказ = Истина;
					Сообщить("Операция записи была отменена пользователем.",СтатусСообщения.Важное);
					Возврат Результат;
				КонецЕсли;
				#КонецЕсли				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Ошибки="";
	Для Каждого СтрокаРабот Из ЭтотОбъект.Работы Цикл
		НомерИсполнителя=0; //ПроцентУчастия=0;
		СтрокиИсполнителей=ЭтотОбъект.Исполнители.НайтиСтроки(Новый Структура("ИдентификаторРаботы",СтрокаРабот.ИдентификаторРаботы));
		Для Каждого СтрокаИсполнителей Из СтрокиИсполнителей Цикл
			НомерИсполнителя=НомерИсполнителя+1;
			//ПроцентУчастия=ПроцентУчастия+СтрокаИсполнителей.Процент;
			Если обЗначениеНеЗаполнено(СтрокаИсполнителей.Исполнитель) Тогда
				дкДобавитьОшибку(Ошибки,"Для работы <"+СокрЛП(СтрокаРабот.Работа)+"> в строке "+Формат(НомерИсполнителя,"ЧДЦ=0")+" не указан исполнитель.");
				Отказ=Истина;
				ТекущийИсполнитель="Не задан";
			Иначе
				ТекущийИсполнитель=СокрЛП(СтрокаИсполнителей.Исполнитель);
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла; 
	
	//проверим, все ли реквизиты могут участвовать в планировании
	Для Каждого СтрокаРабот Из ЭтотОбъект.Планирование Цикл
		Если НЕ обЗначениеНеЗаполнено(СтрокаРабот.Авторабота) Тогда 
			ВидИспользованияРаботы = обПолучитьЗначениеСвойства(СтрокаРабот.Авторабота,ПланыВидовХарактеристик.СвойстваОбъектов.ВидИспользованияАвтоработы,Перечисления.ВидыИспользованияАвторабот.Производство);
			Если ВидИспользованияРаботы = Перечисления.ВидыИспользованияАвторабот.Производство Тогда
				Сообщить("Внимание! Работа """ + Строка(СтрокаРабот.Авторабота) +  """ не может использоваться при планировании!");	
			КонецЕсли;
		КонецЕсли;

		Если НЕ обЗначениеНеЗаполнено(СтрокаРабот.РабочееМесто) Тогда
			СвойствоВидИспользования = ПланыВидовХарактеристик.СвойстваОбъектов.ВидИспользованияРабочегоМеста;
			ВидИспользованияЗначениеСвойства = обПолучитьЗначениеСвойства(СтрокаРабот.РабочееМесто,СвойствоВидИспользования,Перечисления.ВидыИспользованияРабочихМест.НеУчаствуетВПланировании);
			Если ВидИспользованияЗначениеСвойства = Перечисления.ВидыИспользованияРабочихМест.НеУчаствуетВПланировании Тогда
				Сообщить("Внимание! Цех """ + Строка(СтрокаРабот.РабочееМесто) +  """ не может использоваться при планировании!");	
			КонецЕсли;			
		КонецЕсли;

		Если НЕ обЗначениеНеЗаполнено(СтрокаРабот.Исполнитель) Тогда
			Если НЕ СтрокаРабот.Исполнитель.Исполнитель  Тогда
				Сообщить("Внимание! Сотрудник """ + Строка(СтрокаРабот.Исполнитель) +  """ не может быть исполнителем работ!");	
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла; 
	
	Если Отказ Тогда 
		Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
		Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
		Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
		Возврат Результат; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//


// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	СтруктураМакетов.Вставить("БланкЗаявки"            , "Бланк заявки");
	СтруктураМакетов.Вставить("РабочаяЗаявка"          , "Рабочая заявка");
	СтруктураМакетов.Вставить("АктОсмотра"             , "Акт осмотра");
	СтруктураМакетов.Вставить("АктОсмотраПровреждения" , "Акт осмотра(повреждения)");
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()


#КонецОбласти
// Модуль документа РазукомплектацияАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	// Обязательные поля таблицы опций
	ОбязательныеОпции=Новый Структура();
	ОбязательныеОпции.Вставить("Опция",2);
	//ОбязательныеОпции.Вставить("КоличествоОпций",1);
	ОбязательныеОпции.Вставить("Номенклатура",3);
	ОбязательныеОпции.Вставить("Количество",1);
	ОбязательныеОпции.Вставить("ЕдиницаИзмерения",1);
	ОбязательныеОпции.Вставить("Коэффициент",1);
	ОбязательныеОпции.Вставить("ХарактеристикаНоменклатуры",2);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Опции",ОбязательныеОпции);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	ВремСуммаНоменклатурыДокумента=Товары.Итог("СуммаВсего");
	ВремСуммаОпцийДокумента=Опции.Итог("СуммаВсего");
	ВремСуммаДокумента=ВремСуммаОпцийДокумента+ВремСуммаНоменклатурыДокумента;
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	Если Результат И обПраво("ПроверкаЗаполненияСправочниковИДокументов",Права,,ЭтотОбъект) Тогда
		//Проверим что документ основания есть заказ-наряд с видом ремонта комплектация
		Если 
			//(НЕ зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон")) ИЛИ
			 (ДокументОснование.ВидРемонта<>Справочники.ВидыРемонта.КомплектацияАвтомобиля) Тогда
			дкДобавитьОшибку(Ошибки,"Вид ремонта заказ-наряда, на основании которого введена разукомплектация, должен быть <Комплектация автомобиля>.");
			Результат=Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	Результат = Истина;
	Если Товары.Количество()=0 И Опции.Количество()=0 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(Ошибки,"Документ заполнен некорректно! Табличные части документа не содержат данных!");
	КонецЕсли; 
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РазукомплектацияАвтомобилей","Разукомплектация автомобилей",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// определим способ ведения баланса
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	//Спишем оборудование по документу
	ЕстьАвтомобиль=Ложь;
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
	             |	ОстаткиАвтомобилейОстатки.СкладКомпании КАК СкладКомпании,
	             |	ОстаткиАвтомобилейОстатки.СтатусПартии КАК СтатусПартии,
	             |	ОстаткиАвтомобилейОстатки.Партия КАК Партия,
	             |	ОстаткиАвтомобилейОстатки.ГТД КАК ГТД,
	             |	ОстаткиАвтомобилейОстатки.КоличествоОстаток КАК Количество,
	             |	ОстаткиАвтомобилейОстатки.СуммаОстаток КАК Сумма,
	             |	ОстаткиАвтомобилейОстатки.СуммаУпрОстаток КАК СуммаУпр,
	             |	ОстаткиАвтомобилейОстатки.СуммаНДСОстаток КАК СуммаНДС
	             |ИЗ
	             |	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&НаМомент, Автомобиль = &Автомобиль) КАК ОстаткиАвтомобилейОстатки";
	Запрос.УстановитьПараметр("НаМомент",Новый Граница(МоментВремени(),ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Автомобиль",ШапкаДокумента.Автомобиль);
	ВыборкаАвтомобиль=Запрос.Выполнить().Выбрать();
	Если ВыборкаАвтомобиль.Следующий() Тогда
		Если ВыборкаАвтомобиль.Количество>0 Тогда
			НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			             |	РазукомплектацияАвтомобилейТовары.Номенклатура КАК Номенклатура,
			             |	РазукомплектацияАвтомобилейТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	РазукомплектацияАвтомобилейТовары.Количество*РазукомплектацияАвтомобилейТовары.Коэффициент КАК Количество
			             |ИЗ
			             |	Документ.РазукомплектацияАвтомобилей.Товары КАК РазукомплектацияАвтомобилейТовары
			             |ГДЕ
			             |	РазукомплектацияАвтомобилейТовары.Ссылка = &Ссылка";
			СкладАвтомобиля=ВыборкаАвтомобиль.СкладКомпании;
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Запрос.Выполнить();
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ШапкаДокумента.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=СкладАвтомобиля;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
			
			НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			             |	РазукомплектацияАвтомобилейОпции.Опция КАК Номенклатура,
			             |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
			             |	РазукомплектацияАвтомобилейОпции.КоличествоОпций КАК Количество
			             |ИЗ
			             |	Документ.РазукомплектацияАвтомобилей.Опции КАК РазукомплектацияАвтомобилейОпции
			             |ГДЕ
			             |	РазукомплектацияАвтомобилейОпции.Ссылка = &Ссылка
						 |	И РазукомплектацияАвтомобилейОпции.Опция<>ЗНАЧЕНИЕ(Справочник.Опции.ПустаяСсылка)";
			СкладАвтомобиля=ВыборкаАвтомобиль.СкладКомпании;
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Запрос.Выполнить();
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ШапкаДокумента.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=СкладАвтомобиля;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
			ЕстьАвтомобиль=Истина;
		КонецЕсли; 
	КонецЕсли;
	Если ЕстьАвтомобиль=Ложь Тогда
		дкСообщитьРезультат("Автомобиль """+СокрЛП(ШапкаДокумента.Автомобиль)+""" отсутствует на складах.","Важное&Разукомплектация автомобилей:",ЭтотОбъект);
		Отказ=Истина;
	КонецЕсли;
	
	//Оприходуем партии товаров и ГТД
	Если НЕ Отказ Тогда
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		НаборЗаписейГТД=Движения.ГТДПартийТоваровКомпании;
		Для каждого СтрокаКомплектации Из НаборЗаписейКомплектацияАвтомобилей Цикл
			//Партии товаров компании
			НоваяЗапись = НаборЗаписейПартии.Добавить();
			НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период						= ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
			НоваяЗапись.СкладКомпании				= ШапкаДокумента.СкладКомпании;
			Если ТипЗнч(СтрокаКомплектации.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
				НоменклатураПартии						= СтрокаКомплектации.Номенклатура;
				ХарактеристикаПартии					= СтрокаКомплектации.ХарактеристикаНоменклатуры; 
			Иначе
				СтрокаОпций=Опции.Найти(СтрокаКомплектации.Номенклатура,"Опция");
				НоменклатураПартии						= СтрокаОпций.Номенклатура;
				ХарактеристикаПартии					= СтрокаОпций.ХарактеристикаНоменклатуры;
			КонецЕсли; 
			НоваяЗапись.Номенклатура				= НоменклатураПартии;
			НоваяЗапись.ХарактеристикаНоменклатуры	= ХарактеристикаПартии;
			НоваяЗапись.СтатусПартии				= СтрокаКомплектации.СтатусПартии;
			НоваяЗапись.Партия						= СтрокаКомплектации.Партия;
			НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
			НоваяЗапись.СтавкаНДС					= СтрокаКомплектации.СтавкаНДС;
			НоваяЗапись.Проект						= ШапкаДокумента.Проект;
			НоваяЗапись.Количество					= СтрокаКомплектации.Количество;
			НоваяЗапись.Сумма						= СтрокаКомплектации.Сумма;
			НоваяЗапись.СуммаУпр					= СтрокаКомплектации.СуммаУпр;
			НоваяЗапись.СуммаНДС					= СтрокаКомплектации.СуммаНДС;
			Если ЗначениеЗаполнено(СтрокаКомплектации.ГТД) Тогда
				//ГТД партии товаров компании
				НоваяЗапись = НаборЗаписейГТД.Добавить();
				НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период						= ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
				НоваяЗапись.СкладКомпании				= ШапкаДокумента.СкладКомпании;
				НоваяЗапись.Номенклатура				= НоменклатураПартии;
				НоваяЗапись.ХарактеристикаНоменклатуры	= ХарактеристикаПартии;
				НоваяЗапись.Партия						= СтрокаКомплектации.Партия;
				НоваяЗапись.ГТД							= СтрокаКомплектации.ГТД;
				НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
				НоваяЗапись.Количество					= СтрокаКомплектации.Количество;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	//Оприходуем допоборудование
	Если НЕ Отказ Тогда
		СкладАвтомобиля=ВыборкаАвтомобиль.СкладКомпании;
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		НаборЗаписейГТД=Движения.ГТДПартийТоваровКомпании;
		НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
		
		ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.Дата);
		КурсРегл	   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
		КурсУпр=ШапкаДокумента.КурсВалютыУпр;
		
		СуммаВсего=0;
		СуммаУпрВсего=0;
		СуммаНДСВсего=0;
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	РазукомплектацияАвтомобилейОпции.Номенклатура КАК Номенклатура,
		             |	РазукомплектацияАвтомобилейОпции.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	РазукомплектацияАвтомобилейОпции.Количество * РазукомплектацияАвтомобилейОпции.Коэффициент КАК Количество,
		             |	РазукомплектацияАвтомобилейОпции.СуммаВсего КАК СуммаВсего,
		             |	РазукомплектацияАвтомобилейОпции.СтавкаНДС КАК СтавкаНДС,
		             |	РазукомплектацияАвтомобилейОпции.СуммаНДС КАК СуммаНДС
		             |ИЗ
		             |	Документ.РазукомплектацияАвтомобилей.Опции КАК РазукомплектацияАвтомобилейОпции
		             |ГДЕ
		             |	РазукомплектацияАвтомобилейОпции.Ссылка = &Ссылка
		             |	И РазукомплектацияАвтомобилейОпции.Опция = ЗНАЧЕНИЕ(Справочник.Опции.ПустаяСсылка)";
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		ТаблицаДопоборудования=Запрос.Выполнить().Выгрузить();
		
		Для каждого СтрокаДопоборудования Из ТаблицаДопоборудования Цикл
			//Партии товаров компании
			НоваяЗапись = НаборЗаписейПартии.Добавить();
			НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период						= ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
			НоваяЗапись.СкладКомпании				= ШапкаДокумента.СкладКомпании;
			НоваяЗапись.Номенклатура				= СтрокаДопоборудования.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры	= СтрокаДопоборудования.ХарактеристикаНоменклатуры;
			НоваяЗапись.СтатусПартии				= ВыборкаАвтомобиль.СтатусПартии;
			НоваяЗапись.Партия						= ВыборкаАвтомобиль.Партия;
			НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
			НоваяЗапись.СтавкаНДС					= СтрокаДопоборудования.СтавкаНДС;
			НоваяЗапись.Проект						= ШапкаДокумента.Проект;
			НоваяЗапись.Количество					= СтрокаДопоборудования.Количество;
			НоваяЗапись.Сумма=Окр(обПересчет(СтрокаДопоборудования.СуммаВсего,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл),2);
			НоваяЗапись.СуммаУпр=Окр(обПересчет(СтрокаДопоборудования.СуммаВсего,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр),2);
			НоваяЗапись.СуммаНДС=Окр(обПересчет(СтрокаДопоборудования.СуммаНДС,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаРегл,КурсРегл),2);
			СуммаВсего=СуммаВсего+НоваяЗапись.Сумма;
			СуммаУпрВсего=СуммаУпрВсего+НоваяЗапись.СуммаУпр;
			СуммаНДСВсего=СуммаНДСВсего+НоваяЗапись.СуммаНДС;
			Если ЗначениеЗаполнено(ВыборкаАвтомобиль.ГТД) Тогда
				//ГТД партии товаров компании
				НоваяЗапись = НаборЗаписейГТД.Добавить();
				НоваяЗапись.ВидДвижения					= ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период						= ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор					= ШапкаДокумента.Ссылка;
				НоваяЗапись.СкладКомпании				= ШапкаДокумента.СкладКомпании;
				НоваяЗапись.Номенклатура				= СтрокаДопоборудования.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры	= СтрокаДопоборудования.ХарактеристикаНоменклатуры;
				НоваяЗапись.Партия						= ВыборкаАвтомобиль.Партия;
				НоваяЗапись.ГТД							= ВыборкаАвтомобиль.ГТД;
				НоваяЗапись.ХозОперация					= ШапкаДокумента.ХозОперация;
				НоваяЗапись.Количество					= СтрокаДопоборудования.Количество;
			КонецЕсли; 
		КонецЦикла; 
		
		Если СуммаВсего>ВыборкаАвтомобиль.Сумма Тогда
			дкСообщитьРезультат("Себестоимость автомобиля """+СокрЛП(ШапкаДокумента.Автомобиль)+""" составляет "+Формат(ВыборкаАвтомобиль.Сумма,"ЧДЦ=2; ЧН=0,00")+" "+ВалютаРегл+"
			|Сумма приходуемых комплектующих составляет "+Формат(СуммаВсего,"ЧДЦ=2; ЧН=0,00")+" "+ВалютаРегл,"Важное&Разукомплектация автомобилей:",ЭтотОбъект);
			Отказ=Истина;
		КонецЕсли; 
		Если СуммаНДСВсего>ВыборкаАвтомобиль.СуммаНДС Тогда
			дкСообщитьРезультат("Сумма НДС автомобиля """+СокрЛП(ШапкаДокумента.Автомобиль)+""" составляет "+Формат(ВыборкаАвтомобиль.СуммаНДС,"ЧДЦ=2; ЧН=0,00")+" "+ВалютаРегл+"
			|Сумма НДС приходуемых комплектующих составляет "+Формат(СуммаНДСВсего,"ЧДЦ=2; ЧН=0,00")+" "+ВалютаРегл,"Важное&Разукомплектация автомобилей:",ЭтотОбъект);
			Отказ=Истина;
		КонецЕсли; 
		
		Если НЕ Отказ Тогда
			Если СуммаВсего<>0 ИЛИ СуммаУпрВсего<>0 ИЛИ СуммаНДСВсего<>0 Тогда
				НоваяЗаписьОстаткиАвтомобилей=НаборЗаписейОстаткиАвтомобилей.Добавить();
				НоваяЗаписьОстаткиАвтомобилей.ВидДвижения=ВидДвиженияНакопления.Расход;
				НоваяЗаписьОстаткиАвтомобилей.Период=ШапкаДокумента.Дата;
				НоваяЗаписьОстаткиАвтомобилей.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗаписьОстаткиАвтомобилей.Автомобиль=ВыборкаАвтомобиль.Автомобиль;
				НоваяЗаписьОстаткиАвтомобилей.СкладКомпании=СкладАвтомобиля;
				НоваяЗаписьОстаткиАвтомобилей.СтатусПартии=ВыборкаАвтомобиль.СтатусПартии;
				НоваяЗаписьОстаткиАвтомобилей.Партия=ВыборкаАвтомобиль.Партия;
				НоваяЗаписьОстаткиАвтомобилей.ГТД=ВыборкаАвтомобиль.ГТД;
				НоваяЗаписьОстаткиАвтомобилей.Количество=0;
				НоваяЗаписьОстаткиАвтомобилей.Сумма=СуммаВсего;
				НоваяЗаписьОстаткиАвтомобилей.СуммаУпр=СуммаУпрВсего;
				НоваяЗаписьОстаткиАвтомобилей.СуммаНДС=СуммаНДСВсего;
				НоваяЗаписьОстаткиАвтомобилей.ХозОперация=ШапкаДокумента.ХозОперация;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		// БАЛАНС: Если происходит перемещение товаров между складами подразделений, принадлежащих
		// различным балансовым "веткам", то возможен разрыв баланса.
		ДобавлятьКорректирующиеЗаписи = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
		ПодразделениеОтправитель      = обПолучитьБалансовоеПодразделение(СкладАвтомобиля.Подразделение);
		ПодразделениеПолучатель       = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
	    ДобавлятьКорректирующиеЗаписи = ДобавлятьКорректирующиеЗаписи И (ПодразделениеОтправитель<>ПодразделениеПолучатель);
		// Доходы и расходы
		Если ДобавлятьКорректирующиеЗаписи Тогда
			// У подразделения склада-отправителя возникает расход.
			// У подразделения склада-получателя - доход.
			// Если это перемещение в филиал, то движений "приход" не будет.
			ТаблицаПартий=НаборЗаписейКомплектацияАвтомобилей.Выгрузить();
			ТаблицаПартий.Свернуть("СтатусПартии, ВидДвижения","СуммаУпр");
			Для каждого ТекСтрокаДвижения Из ТаблицаПартий Цикл  	
				Если ТекСтрокаДвижения.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия Тогда
					Продолжить;
				КонецЕсли;
				СуммаДиР=ТекСтрокаДвижения.СуммаУпр;
				Если СуммаДиР<>0 Тогда
					НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
					НаборЗаписейДоходыРасходы.ДокументОбъект=ЭтотОбъект;
					НаборЗаписейДоходыРасходы.Подразделение=СкладАвтомобиля.Подразделение;
					НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
					НаборЗаписейДоходыРасходы.Расход         = СуммаДиР;
					Отказ = НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
					НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
					НаборЗаписейДоходыРасходы.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДоходыРасходы.Подразделение  = ШапкаДокумента.СкладКомпании.Подразделение; 
					НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
					НаборЗаписейДоходыРасходы.Доход          = СуммаДиР;
					Отказ = НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
				КонецЕсли;			
			КонецЦикла;			
		КонецЕсли; 	
	КонецЕсли; 
	
	// двигаем границу последовательности партий
	СвойствоАвтомобильБыл = неопределено;
	ДополнительныеСвойства.Свойство("АвтомобильБыл", СвойствоАвтомобильБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоАвтомобильБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Результат = Запрос.Выполнить(); АвтомобильБыл = Неопределено;
			Если НЕ Результат.Пустой() Тогда
				АвтомобильБыл = Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", АвтомобильБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыКомплектация.Автомобиль       = ШапкаДокумента.Автомобиль;
			НаборЗаписейГраницыКомплектация.МоментВремени    = ШапкаДокумента.Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл    = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль       = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени    = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл    = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности партий
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СкладКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,"СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма) И Рез;
					Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",СтрокаТоваров,ЭтаФорма) И Рез;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		
		// Подставляем ставку НДС. Учитываем соответствующий флаг в контрагенте и организации.
		ОсвобожденОтНДС = Ложь;
		Если Не ЭтотОбъект.Организация.Пустая() Тогда
			ОсвобожденОтНДС = ОсвобожденОтНДС Или Организация.ОсвобожденОтНДС;
		КонецЕсли;
		Если Не ЭтотОбъект.ПодразделениеКомпании.Пустая() Тогда
			ОсвобожденОтНДС = ОсвобожденОтНДС;// Или ПодразделениеКомпании.ОсвобожденОтНДС;
		КонецЕсли;
		Если ОсвобожденОтНДС Тогда
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			ОбработкаРеквизита("Товары.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			Рез = ОбработкаРеквизита("УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма,ДопПараметры);
			Рез = ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				// изменим сумму документа
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			КонецЕсли; 
		#КонецЕсли
		
		Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Номенклатура.Артикул) Тогда
				Попытка
					Если НЕ ЗначениеЗаполнено(ТекСтрока.Опция) Тогда
						НоменклатураОпции=Справочники.Опции.НайтиПоРеквизиту("Артикул",ТекСтрока.Номенклатура.Артикул);
						Если ЗначениеЗаполнено(НоменклатураОпции) Тогда
							ТекСтрока.Опция=НоменклатураОпции;
							Рез=ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
						КонецЕсли; 
					КонецЕсли; 
				Исключение
				КонецПопытки; 
			КонецЕсли; 
		КонецЕсли; 
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		Рез = Истина;
		Если ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ВалютаРозницы  = обВалютаТипаЦены(ТекСтрока.Номенклатура, СкладКомпании.ТипЦенРозничнойТорговли, Истина);
			СтруктураКурса = обКурсДляВалюты(ВалютаРозницы, Дата);
			КурсРозницы    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(КурсРозницы = КурсДокумента, ТекСтрока.Цена, обПересчет(ТекСтрока.Цена, ВалютаДокумента, КурсДокумента, ВалютаРозницы, КурсРозницы)); 
			ТекСтрока.ЦенаРозничная = ТекЦенаЗакупки + (ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
			Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
				ТекСтрока.ЦенаРозничная = ТекСтрока.ЦенаРозничная*(1 + (ТекСтрока.СтавкаНДС.Ставка/100));
			КонецЕсли;
			Рез = ОбработкаРеквизита("УстановитьРозничнуюСумму", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=ТекСтрока.СуммаРозничная/ТекСтрока.Количество;
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена = 0 Тогда
			ТекСтрока.ПроцентНаценки = 0;
		ИначеЕсли ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ВалютаРозницы  = обВалютаТипаЦены(ТекСтрока.Номенклатура, СкладКомпании.ТипЦенРозничнойТорговли, Истина);
			СтруктураКурса = обКурсДляВалюты(ВалютаРозницы, Дата);
			КурсРозницы    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(КурсРозницы = КурсДокумента, ТекСтрока.Цена, обПересчет(ТекСтрока.Цена, ВалютаДокумента, КурсДокумента, ВалютаРозницы, КурсРозницы)); 
			ЦенаРозничная  = ТекСтрока.ЦенаРозничная;
			Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
				ЦенаРозничная = ЦенаРозничная * 100 / (100 + ТекСтрока.СтавкаНДС.Ставка);
			КонецЕсли;
			ТекСтрока.ПроцентНаценки=((ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.Номенклатура, ТекСтрока.Номенклатура, СкладКомпании.Подразделение);
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную цену (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.Количество;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ДОПОЛНИТЕЛЬНЫЕ ОПЦИИ"
	ИначеЕсли Имя="Опции.Опция" Тогда
		Рез=Истина;
		Если ТекСтрока.Опция.ПризнакНабора Тогда
			// Имеем дело с набором, разложим его...
			Набор=ТекСтрока.Опция;
			КоличествоНабора=ТекСтрока.КоличествоОпций;
			Если КоличествоНабора=0 Тогда
				Если ЗначениеЗаполнено(ТекСтрока.Опция) Тогда
					КоличествоНабора=1;
				КонецЕсли; 
			КонецЕсли; 
			// Удалим строку из табличной части
			Опции.Удалить(ТекСтрока);
			// Теперь будем рекурсивно добавлять состав набора
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры=Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("НаборОпций",Истина);
			Для Каждого ИзНабора Из Набор.СоставНабора Цикл
				Если ИзНабора.Опция.ПризнакНабора Тогда
					//Вот это да, набор в наборе ... игнорируем
					#Если Клиент Тогда
					Сообщить("В наборе """+СокрЛП(Набор)+""" присутствует набор """+СокрЛП(ИзНабора.Опция)+""". Добавление набора """+СокрЛП(ИзНабора.Опция)+""" отменено.",СтатусСообщения.Внимание);
					#КонецЕсли
				Иначе
					// Поищем оборудование из набора в таблице
					МассивСтрок=Опции.НайтиСтроки(Новый Структура("Опция",ИзНабора.Опция));
					Если МассивСтрок.Количество()>0 Тогда
						//Если добавляемое оборудование уже есть в табличной части
						//просто увеличим количество, если его запрашивать не надо
						СтрокаТЧ=МассивСтрок[0];
						СтрокаТЧ.КоличествоОпций=СтрокаТЧ.КоличествоОпций+(ИзНабора.Количество*КоличествоНабора);
						Рез=ОбработкаРеквизита("Опции.КоличествоОпций",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
					Иначе
						СтрокаТЧ=Опции.Добавить();
						СтрокаТЧ.Опция=ИзНабора.Опция;
						СтрокаТЧ.КоличествоОпций=ИзНабора.Количество*КоличествоНабора;
						Рез=ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла;
			ДопПараметры.Удалить("НаборОпций");
		Иначе
			//Проставим количество
			Если ЗначениеЗаполнено(ТекСтрока.Опция) Тогда
				Если ТекСтрока.КоличествоОпций=0 Тогда
					ТекСтрока.КоличествоОпций=1;
				КонецЕсли; 
			Иначе
				Если ТекСтрока.КоличествоОпций<>0 Тогда
					ТекСтрока.КоличествоОпций=0;
				КонецЕсли; 
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекСтрока.Опция) Тогда
				Если ЗначениеЗаполнено(ТекСтрока.Опция.Артикул) Тогда
					Если НЕ ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
						//НоменклатураОпции=Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",ТекСтрока.Опция.Артикул);
						Запрос = Новый Запрос;
						Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
						               |	МоделиОпции.Ссылка.Производитель
						               |ИЗ
						               |	Справочник.Модели.Опции КАК МоделиОпции
						               |ГДЕ
						               |	НЕ МоделиОпции.Ссылка.ПометкаУдаления
						               |	И МоделиОпции.Опция = &Опция";
						
						Запрос.УстановитьПараметр("Опция",ТекСтрока.Опция);
						
						Результат = Запрос.Выполнить();
						Выборка = Результат.Выбрать();
						НоменклатураОпции = Справочники.Номенклатура.ПустаяСсылка();
						Если Выборка.Следующий() Тогда
							АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(ТекСтрока.Опция.Артикул,Выборка.Производитель);
							НоменклатураОпции = Справочники.Номенклатура.НайтиНоменклатуру(АртикулДляПоиска,Выборка.Производитель);					
						КонецЕсли;
						Если ЗначениеЗаполнено(НоменклатураОпции) Тогда
							ТекСтрока.Номенклатура=НоменклатураОпции;
							Рез=ОбработкаРеквизита("Товары.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
						КонецЕсли; 
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Опции.КоличествоОпций" Тогда
		Если ТекСтрока.Коэффициент=0 Тогда
			ТекСтрока.Количество=ТекСтрока.КоличествоОпций;
		Иначе
			ТекСтрока.Количество=ТекСтрока.КоличествоОпций/ТекСтрока.Коэффициент;
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.Номенклатура" Тогда
		Рез=ОбработкаРеквизита("Товары.Номенклатура",ТекСтрока,ЭтаФорма);
		Возврат Рез;
		
	ИначеЕсли Имя="Опции.Количество" Тогда
		Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма);
		Если ЗначениеЗаполнено(ТекСтрока.Опция) Тогда
			ТекСтрока.КоличествоОпций=ТекСтрока.Количество*ТекСтрока.Коэффициент;
		КонецЕсли;
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.ДокументОснование.Автомобиль КАК Автомобиль,
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// функция возвращает список опций для текущего набора модели или варианта комплектации
Функция ПолучитьСписокДоступныхОпций(Модель, ВариантКомплектации = Неопределено) Экспорт
	СписокОборудования=Новый СписокЗначений;
	Запрос = Новый Запрос;
	Если обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
		Запрос.Текст = "ВЫБРАТЬ
		|	МоделиОпции.Опция КАК Опция
		|ИЗ
		|	Справочник.Модели.Опции КАК МоделиОпции
		|ГДЕ
		|	МоделиОпции.Ссылка = &Модель";
		Запрос.УстановитьПараметр("Модель", Модель);
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		|	ОпцииВариантовКомплектации.Опция КАК Опция
		|ИЗ
		|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
		|ГДЕ
		|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
		|	И ОпцииВариантовКомплектации.ВидВключения = 1
		|";
		Запрос.УстановитьПараметр("ВариантКомплектации",ВариантКомплектации);
	КонецЕсли;
	СписокОборудования.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Опция"));
	Возврат СписокОборудования
КонецФункции

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

//Функция печати этикеток и ценников
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
		СтруктураПараметров=Неопределено;
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			СтруктураПараметров=Новый Структура("ИмяРеквизитаЦена,ТипЦен","ЦенаРозничная",СкладКомпании.ТипЦенРозничнойТорговли);
		КонецЕсли;
		дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
	КонецФункции // ПечатьЭтикетокИЦенников()
	
// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРазукомплектацияАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РазукомплектацияАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеЗаказНаряд = СокрЛП(ДокументОснование);
	ОбластьМакета.Параметры.ЗаказНаряд = ДокументОснование;
	ОбластьМакета.Параметры.Автомобиль = ДокументОснование.Автомобиль;
	Автомобиль = ДокументОснование.Автомобиль;
	ОбластьМакета.Параметры.АвтомобильМодель   = Автомобиль.Модель;
	ОбластьМакета.Параметры.АвтомобильГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,Дата);
	ОбластьМакета.Параметры.АвтомобильКод      = Автомобиль.VIN;
	ОбластьМакета.Параметры.ПредставлениеСкладКомпании = спПолучитьНаименование(СкладКомпании);
	ОбластьМакета.Параметры.СкладКомпании      = СкладКомпании;

	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары.Выгрузить();
	Для каждого СтрокаОпций Из Опции Цикл
		СтрокаДляПечати=ВыборкаТабличнойЧасти.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДляПечати,СтрокаОпций);
	КонецЦикла;
	НомерСтроки=1;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		ОбластьМакета.Параметры.НомерСтроки=НомерСтроки;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
		НомерСтроки=НомерСтроки+1;
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРазукомплектацияАвтомобилей()

// Функция осуществляет печать произвольного документа.
	// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
	//
	// Параметры:
	//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
	//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
	//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
	//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
	//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
	//
	// Возвращаемое значение:
	//   ТабличныйДокумент	- Сформированная печатная форма
	//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
		Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Рез=дкОбработкаЗаполнения(ЭтотОбъект, Основание,,Истина);
	
	Если НЕ Рез Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		
		Товары.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	КомплектацияАвтомобилейОстатки.Номенклатура КАК Номенклатура,
		             |	КомплектацияАвтомобилейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	КомплектацияАвтомобилейОстатки.КоличествоОстаток КАК Количество,
		             |	КомплектацияАвтомобилейОстатки.СуммаОстаток КАК Сумма,
		             |	КомплектацияАвтомобилейОстатки.СуммаУпрОстаток КАК СуммаУпр,
		             |	КомплектацияАвтомобилейОстатки.СуммаНДСОстаток КАК СуммаНДС
		             |ИЗ
		             |	РегистрНакопления.КомплектацияАвтомобилей.Остатки("+?(ЭтоНовый(),"","&НаМомент")+", Автомобиль = &Автомобиль И Номенклатура ССЫЛКА Справочник.Номенклатура) КАК КомплектацияАвтомобилейОстатки
		             |";
		Запрос.УстановитьПараметр("НаМомент",Новый Граница(МоментВремени(),ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("Автомобиль",ДокументОснование.Автомобиль);
		Выборка = Запрос.Выполнить().Выбрать();
		ВалютаРегламентированногоУчета=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		ВалютаУправленческогоУчета=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока=Товары.Добавить();
			НоваяСтрока.Номенклатура=Выборка.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры=Выборка.ХарактеристикаНоменклатуры;
			НоваяСтрока.Количество=Выборка.Количество;
			ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
			Если ВалютаДокумента=ВалютаРегламентированногоУчета Тогда
				НоваяСтрока.СуммаВсего=Выборка.Сумма;
			Иначе
				НоваяСтрока.СуммаВсего=обПересчет(Выборка.СуммаУпр,ВалютаУправленческогоУчета,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
			НоваяСтрока.СуммаНДС=Выборка.СуммаНДС;
		КонецЦикла;
	КонецЕсли; 
	
	Если ЭтоНовый() Тогда
		ТипЦенНовый=обПраво("ОсновнойТипЦенЗакупки",Права,,ЭтотОбъект);
		Если ТипЦен<>ТипЦенНовый Тогда
			ТипЦен=ТипЦенНовый;
			Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
				ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(СкладКомпании) ИЛИ НЕ СкладКомпании.Розничный Тогда
		Для каждого ТекСтрокаТЧ Из Товары Цикл  	
			ТекСтрокаТЧ.ПроцентНаценки = 0;
			ТекСтрокаТЧ.ЦенаРозничная  = 0;
			ТекСтрокаТЧ.СуммаРозничная = 0;
		КонецЦикла;	
		Для каждого ТекСтрокаТЧ Из Опции Цикл  	
			ТекСтрокаТЧ.ПроцентНаценки = 0;
			ТекСтрокаТЧ.ЦенаРозничная  = 0;
			ТекСтрокаТЧ.СуммаРозничная = 0;
		КонецЦикла;	
	КонецЕсли;
	Для каждого ТекСтрокаТЧ Из Опции Цикл
		Если НЕ ЗначениеЗаполнено(ТекСтрокаТЧ.Опция) Тогда
			ТекСтрокаТЧ.КоличествоОпций=0;
		КонецЕсли;
	КонецЦикла; 
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проведем остатки товаров
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	РазукомплектацияАвтомобилейТЧ.Номенклатура КАК Номенклатура,
	             |	РазукомплектацияАвтомобилейТЧ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	РазукомплектацияАвтомобилейТЧ.СкладКомпании КАК СкладКомпании,
	             |	МАКСИМУМ(РазукомплектацияАвтомобилейТЧ.ЦенаРозничная) КАК ЦенаРозничная,
	             |	МАКСИМУМ(РазукомплектацияАвтомобилейТЧ.СуммаРозничная) КАК СуммаРозничная,
	             |	МАКСИМУМ(РазукомплектацияАвтомобилейТЧ.СуммаСкидки) КАК СуммаСкидки,
	             |	СУММА(РазукомплектацияАвтомобилейТЧ.Количество) КАК Количество,
	             |	СУММА(РазукомплектацияАвтомобилейТЧ.Резерв) КАК Резерв
	             |ИЗ
	             |	(ВЫБРАТЬ
	             |		РазукомплектацияАвтомобилейТовары.Номенклатура КАК Номенклатура,
	             |		РазукомплектацияАвтомобилейТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |		РазукомплектацияАвтомобилейТовары.Ссылка.СкладКомпании КАК СкладКомпании,
				 |		"+?(СкладКомпании.Розничный,"РазукомплектацияАвтомобилейТовары.ЦенаРозничная","0")+" КАК ЦенаРозничная,
				 |		"+?(СкладКомпании.Розничный,"РазукомплектацияАвтомобилейТовары.СуммаРозничная","0")+" КАК СуммаРозничная,
	             |		0 КАК СуммаСкидки,
	             |		РазукомплектацияАвтомобилейТовары.Количество * РазукомплектацияАвтомобилейТовары.Коэффициент КАК Количество,
	             |		0 КАК Резерв
	             |	ИЗ
	             |		Документ.РазукомплектацияАвтомобилей.Товары КАК РазукомплектацияАвтомобилейТовары
	             |	ГДЕ
	             |		РазукомплектацияАвтомобилейТовары.Ссылка = &Ссылка
	             |	
	             |	ОБЪЕДИНИТЬ ВСЕ
	             |	
	             |	ВЫБРАТЬ
	             |		РазукомплектацияАвтомобилейОпции.Номенклатура КАК Номенклатура,
	             |		РазукомплектацияАвтомобилейОпции.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |		РазукомплектацияАвтомобилейОпции.Ссылка.СкладКомпании КАК СкладКомпании,
				 |		"+?(СкладКомпании.Розничный,"РазукомплектацияАвтомобилейОпции.ЦенаРозничная","0")+" КАК ЦенаРозничная,
				 |		"+?(СкладКомпании.Розничный,"РазукомплектацияАвтомобилейОпции.СуммаРозничная","0")+" КАК СуммаРозничная,
	             |		0 КАК СуммаСкидки,
	             |		РазукомплектацияАвтомобилейОпции.Количество * РазукомплектацияАвтомобилейОпции.Коэффициент КАК Количество,
	             |		0 КАК Резерв
	             |	ИЗ
	             |		Документ.РазукомплектацияАвтомобилей.Опции КАК РазукомплектацияАвтомобилейОпции
	             |	ГДЕ
	             |		РазукомплектацияАвтомобилейОпции.Ссылка = &Ссылка) КАК РазукомплектацияАвтомобилейТЧ
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	РазукомплектацияАвтомобилейТЧ.Номенклатура,
	             |	РазукомплектацияАвтомобилейТЧ.ХарактеристикаНоменклатуры,
	             |	РазукомплектацияАвтомобилейТЧ.СкладКомпании";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=Запрос.Выполнить().Выгрузить();
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.Приходовать=Истина;
	НаборЗаписейОстатки.Резервировать=Ложь;
	//НаборЗаписейОстатки.Контрагент=Контрагент;
	НаборЗаписейОстатки.ПоБазовомуКоличеству=Ложь;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
	Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
		
	// если приходуем товар на розничный склад, то установим розничные цены на этот товар
	Если НЕ Отказ И СкладКомпании.Розничный И ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ТипЦен=СкладКомпании.ТипЦенРозничнойТорговли;
		НаборЗаписейЦены.ПодразделениеКомпании = СкладКомпании.Подразделение;
		НаборЗаписейЦены.УстанавливатьЦеныУслуг=Ложь;
		НаборЗаписейЦены.ИмяРеквизитаЦена="ЦенаРозничная";
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

// Модуль документа НазначениеСкидокСтроки

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Набор,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы скидок
	ОбязательныеСкидки = Новый Структура();
	ОбязательныеСкидки.Вставить("Скидка",3);
	ОбязательныеСкидки.Вставить("СкидкаНаТовары",2);
	ОбязательныеСкидки.Вставить("СкидкаНаРаботы",2);
	ОбязательныеСкидки.Вставить("ЗначениеСкидки",2);
	ОбязательныеСкидки.Вставить("Номенклатура",2);
	ОбязательныеСкидки.Вставить("ТипНоменклатуры",2);
	ОбязательныеСкидки.Вставить("Свойство",2);
	ОбязательныеСкидки.Вставить("Количество",2); 
	ОбязательныеСкидки.Вставить("СуммаСтроки",2); 
	ОбязательныеСкидки.Вставить("ДисконтнаяКарта",2);
	ОбязательныеСкидки.Вставить("НачВремя",2);
	ОбязательныеСкидки.Вставить("КонВремя",2);
	ОбязательныеСкидки.Вставить("ДниНедели",2); 
	ОбязательныеСкидки.Вставить("СкладКомпании",2);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачалаДействия",1);
	ОбязательныеРеквизиты.Вставить("Скидки",ОбязательныеСкидки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	НетОшибок = ИСТИНА;
	
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Для Каждого ТекСтрока Из Скидки Цикл
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) И ТекСтрока.ТипНоменклатуры.Пустая() Тогда
			Ошибки = "Таблица <Скидки> значение одной из двух колонок <Номенклатура> или <Тип номенклатуры> не заполнено ! Строка номер "+ТекСтрока.НомерСтроки;
			НетОшибок = ЛОЖЬ;
		ИначеЕсли (НЕ ТекСтрока.Скидка.ЭтоПодарок) И ТекСтрока.ЗначениеСкидки = 0 Тогда
			Ошибки = "Таблица <Скидки> значение колонки <Значение скидки> не заполнено ! Строка номер "+ТекСтрока.НомерСтроки;
			НетОшибок = ЛОЖЬ;
		ИначеЕсли ТекСтрока.Скидка.ЭтоПодарок И ТекСтрока.ЗначениеСкидки <> 0 Тогда
			Ошибки = "Таблица <Скидки> значение колонки <Значение скидки> для подарка должно быть не заполнено ! Строка номер "+ТекСтрока.НомерСтроки;
			НетОшибок = ЛОЖЬ;
		КонецЕсли;
	КонецЦикла;
	
	// отработаем стандартно
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И НетОшибок;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УстановкаСкидкиНаСтрокуДокумента","Установка скидки",Истина);
	СписокХозОпераций.Добавить("ОтменаСкидкиНаСтрокуДокумента","Отмена скидки");
	
	Возврат СписокХозОпераций;
КонецФункции

// Функция получает таблицу скидок и их параметров действия
Функция ПолучитьТаблицуСкидок() Экспорт
	ТекстЗапроса = "ВЫБРАТЬ
	|	НазначениеСкидокСтрокиСкидки.Скидка.РучнаяСкидка КАК РучнаяСкидка,
	|	ВЫБОР
	|		КОГДА НазначениеСкидокСтрокиСкидки.Номенклатура <> &НоменклатураПустая
	|				И НазначениеСкидокСтрокиСкидки.Номенклатура <> &РаботаПустая
	|				И НазначениеСкидокСтрокиСкидки.Номенклатура <> Неопределено 
	|			ТОГДА НазначениеСкидокСтрокиСкидки.Номенклатура
	|		ИНАЧЕ НазначениеСкидокСтрокиСкидки.ТипНоменклатуры
	|	КОНЕЦ КАК Объект,
	|	НазначениеСкидокСтрокиСкидки.НачВремя,
	|	НазначениеСкидокСтрокиСкидки.КонВремя,
	|	НазначениеСкидокСтрокиСкидки.ДниНедели,
	|	НазначениеСкидокСтрокиСкидки.Скидка.ЭтоПодарок КАК ЭтоПодарок,
	|	НазначениеСкидокСтрокиСкидки.ДисконтнаяКарта,
	|	НазначениеСкидокСтрокиСкидки.СуммаНакопления КАК ОтСуммыНакопленияНаКарте,
	|	НазначениеСкидокСтрокиСкидки.СкладКомпании КАК ЗалОбслуживания,
	|	НазначениеСкидокСтрокиСкидки.Свойство,
	|	НазначениеСкидокСтрокиСкидки.СуммаСтроки КАК ОтСуммыСтроки,
	|	НазначениеСкидокСтрокиСкидки.Количество КАК ОтКоличества,
	|	НазначениеСкидокСтрокиСкидки.Правило,
	|	НазначениеСкидокСтрокиСкидки.Скидка,
	|	НазначениеСкидокСтрокиСкидки.Ссылка КАК РегистраторСкидки,
	|	НазначениеСкидокСтрокиСкидки.ЗначениеСкидки,
	|	НазначениеСкидокСтрокиСкидки.Скидка.СпособВычисления КАК СпособВычисления,
	|	НазначениеСкидокСтрокиСкидки.ФлагВытеснения,
	|	НазначениеСкидокСтрокиСкидки.ВидПрайсЛистаПодарка,
	|	НазначениеСкидокСтрокиСкидки.МаксимальнаяСуммаПодарка,
	|	НазначениеСкидокСтрокиСкидки.ИдентификаторСкидки,
	|	НазначениеСкидокСтрокиСкидки.СкидкаНаТовары,
	|	НазначениеСкидокСтрокиСкидки.СкидкаНаРаботы,
	|	ЕСТЬNULL(СкидкиСтрокиСрезПоследних.Действует, ЛОЖЬ) КАК СкидкаУстановлена
	|ИЗ
	|	Документ.НазначениеСкидокСтроки.Скидки КАК НазначениеСкидокСтрокиСкидки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СкидкиСтроки.СрезПоследних(
	|		&Дата,
	|		Подразделение = &ПодразделениеКомпании
	|		    И Скидка В
	|		        (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		            НазначениеСкидокСтрокиСкидки.Скидка
	|		        ИЗ
	|		            Документ.НазначениеСкидокСтроки.Скидки КАК НазначениеСкидокСтрокиСкидки
	|		        ГДЕ
	|		            НазначениеСкидокСтрокиСкидки.Ссылка = &Ссылка)) КАК СкидкиСтрокиСрезПоследних
	|		ПО НазначениеСкидокСтрокиСкидки.НачВремя = СкидкиСтрокиСрезПоследних.НачВремя
	|			И НазначениеСкидокСтрокиСкидки.КонВремя = СкидкиСтрокиСрезПоследних.КонВремя
	|			И НазначениеСкидокСтрокиСкидки.ДниНедели = СкидкиСтрокиСрезПоследних.ДниНедели
	|			И НазначениеСкидокСтрокиСкидки.ДисконтнаяКарта = СкидкиСтрокиСрезПоследних.ДисконтнаяКарта
	|			И НазначениеСкидокСтрокиСкидки.СуммаНакопления = СкидкиСтрокиСрезПоследних.ОтСуммыНакопленияНаКарте
	|			И НазначениеСкидокСтрокиСкидки.СкладКомпании = СкидкиСтрокиСрезПоследних.ЗалОбслуживания
	|			И НазначениеСкидокСтрокиСкидки.Свойство = СкидкиСтрокиСрезПоследних.Свойство
	|			И НазначениеСкидокСтрокиСкидки.Скидка = СкидкиСтрокиСрезПоследних.Скидка
	|			И (НазначениеСкидокСтрокиСкидки.Номенклатура = СкидкиСтрокиСрезПоследних.Объект
	|				ИЛИ НазначениеСкидокСтрокиСкидки.ТипНоменклатуры = СкидкиСтрокиСрезПоследних.Объект)
	|			И НазначениеСкидокСтрокиСкидки.Количество = СкидкиСтрокиСрезПоследних.ОтКоличества
	|			И НазначениеСкидокСтрокиСкидки.Правило = СкидкиСтрокиСрезПоследних.Правило
	|			И НазначениеСкидокСтрокиСкидки.ЗначениеСкидки = СкидкиСтрокиСрезПоследних.ЗначениеСкидки
	|			И НазначениеСкидокСтрокиСкидки.СкидкаНаТовары = СкидкиСтрокиСрезПоследних.СкидкаНаТовары
	|			И НазначениеСкидокСтрокиСкидки.СкидкаНаРаботы = СкидкиСтрокиСрезПоследних.СкидкаНаРаботы
	|			И (СкидкиСтрокиСрезПоследних.Действует)
	|ГДЕ
	|	НазначениеСкидокСтрокиСкидки.Ссылка = &Ссылка";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",	Ссылка);
	Запрос.УстановитьПараметр("Дата",	Новый Граница(ДатаНачалаДействия, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("НоменклатураПустая",    Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("РаботаПустая",          Справочники.Автоработы.ПустаяСсылка());
	
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьТаблицуТоваров()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя, ТекСтрока = Неопределено, ЭтаФорма = Неопределено, ДопПараметры = Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ИмяРеквизита" Тогда
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "СКИДКИ"
	ИначеЕсли Имя="Скидки.Скидка" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Скидка) Тогда
			Возврат ЛОЖЬ;
		КонецЕсли;
		
		// Проверим вид скидки
		Если ТекСтрока.Скидка.ВидСкидки <> Перечисления.ВидыСкидок.НаСтрокуДокумента Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранная скидка используется для документов в целом и не может быть назначена на товарные строки.");
			#КонецЕсли
			Возврат ЛОЖЬ;
		КонецЕсли;
		
		Если ХозОперация = Справочники.ХозОперации.УстановкаСкидкиНаСтрокуДокумента Тогда
			// Произведем первоначальное заполнение некоторых полей текущей строки
			ТекСтрока.ФлагВытеснения= ТекСтрока.Скидка.ФлагВытеснения;
			Если ТекСтрока.Скидка.ЭтоПодарок Тогда
				ТекСтрока.ЗначениеСкидки			= 0;
			Иначе
				ТекСтрока.ВидПрайсЛистаПодарка		= 0;
				ТекСтрока.МаксимальнаяСуммаПодарка	= 0;
			КонецЕсли;
			ТекСтрока.Правило		= ?(обЗначениеНеЗаполнено(ТекСтрока.Правило), Перечисления.ПравилаРасчетаСкидкиПоКоличеству.НеИспользуется, ТекСтрока.Правило);
			ТекСтрока.КонВремя		= ?(обЗначениеНеЗаполнено(ТекСтрока.КонВремя), '00010101235959', ТекСтрока.КонВремя);
			ТекСтрока.ДниНедели		= ?(ПустаяСтрока(ТекСтрока.ДниНедели), "1111111", ТекСтрока.ДниНедели);
			
			Если обЗначениеНеЗаполнено(ТекСтрока.Свойство) Тогда
				ТипЗначения = ТекСтрока.Скидка.ВидСвойства.ТипЗначения;
				Если НЕ обЗначениеНеЗаполнено(ТипЗначения) Тогда
					ТекСтрока.Свойство = обПустоеЗначениеТипа(ТипЗначения.Типы()[0]);
					#Если Клиент Тогда
					Попытка
						ЭтаФорма.ЭлементыФормы.Скидки.Колонки.Свойство.ЭлементУправления.ОграничениеТипа = ТипЗначения;
					Исключение КонецПопытки;
					Попытка
						ЭтаФорма.ЭлементыФормы.РегистрСведенийСписок.Колонки.Свойство.ЭлементУправления.ОграничениеТипа = ТипЗначения;
					Исключение КонецПопытки;
					#КонецЕсли
				КонецЕсли;
			КонецЕсли;
			
			// Попробуем заполнить дополнительные поля (для регистра)
			Попытка
				ТекСтрока.РучнаяСкидка = ТекСтрока.Скидка.РучнаяСкидка;
			Исключение КонецПопытки;
			Попытка
				ТекСтрока.ЭтоПодарок = ТекСтрока.Скидка.ЭтоПодарок;
			Исключение КонецПопытки;
			Попытка
				ТекСтрока.СпособВычисления = ТекСтрока.Скидка.СпособВычисления;
			Исключение КонецПопытки;
			
			// Проверим корректность заполнения реквизита "ЗначениеСкидки"
			ОбработкаРеквизита("Скидки.ЗначениеСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);
		Иначе
			// Отмена скидок. Скидка выбрана, найдем ее среди установленных
			ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	СкидкиСтрокиСрезПоследних.ФлагВытеснения,
			|	СкидкиСтрокиСрезПоследних.ЗначениеСкидки,
			|	СкидкиСтрокиСрезПоследних.Объект КАК Номенклатура,
			|	СкидкиСтрокиСрезПоследних.Объект КАК ТипНоменклатуры,
			|	СкидкиСтрокиСрезПоследних.Свойство,
			|	СкидкиСтрокиСрезПоследних.ОтКоличества КАК Количество,
			|	СкидкиСтрокиСрезПоследних.Правило,
			|	СкидкиСтрокиСрезПоследних.ОтСуммыСтроки КАК СуммаСтроки,
			|	СкидкиСтрокиСрезПоследних.ДисконтнаяКарта,
			|	СкидкиСтрокиСрезПоследних.ОтСуммыНакопленияНаКарте КАК СуммаНакопления,
			|	СкидкиСтрокиСрезПоследних.НачВремя,
			|	СкидкиСтрокиСрезПоследних.КонВремя,
			|	СкидкиСтрокиСрезПоследних.ДниНедели,
			|	СкидкиСтрокиСрезПоследних.ВидПрайсЛистаПодарка,
			|	СкидкиСтрокиСрезПоследних.МаксимальнаяСуммаПодарка,
			|	СкидкиСтрокиСрезПоследних.ЗалОбслуживания КАК СкладКомпании,
			|	СкидкиСтрокиСрезПоследних.СкидкаНаТовары,
			|	СкидкиСтрокиСрезПоследних.СкидкаНаРаботы
			|ИЗ
			|	РегистрСведений.СкидкиСтроки.СрезПоследних(
			|		&Дата,
			|		Подразделение = &ПодразделениеКомпании
			|//ФИЛЬТРЫ
			|			И Скидка = &Скидка) КАК СкидкиСтрокиСрезПоследних
			|ГДЕ
			|	СкидкиСтрокиСрезПоследних.Действует
			|
			|УПОРЯДОЧИТЬ ПО
			|	СкидкиСтрокиСрезПоследних.Период
			|АВТОУПОРЯДОЧИВАНИЕ";
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Дата", Новый Граница(ДатаНачалаДействия, ВидГраницы.Исключая));
			Запрос.УстановитьПараметр("ПодразделениеКомпании",ПодразделениеКомпании);
			Запрос.УстановитьПараметр("Скидка",ТекСтрока.Скидка);
			
			// Построим дополнительный фильтр по всем заполненным полям текущей строки
			СписокПолей = Новый Структура("Объект,ОтКоличества,Правило,НачВремя,КонВремя,ДниНедели,ДисконтнаяКарта,ОтСуммыНакопленияНаКарте,ЗалОбслуживания,Свойство,ОтСуммыСтроки", ?(обЗначениеНеЗаполнено(ТекСтрока.Номенклатура), ТекСтрока.ТипНоменклатуры, ТекСтрока.Номенклатура), ТекСтрока.Количество, ТекСтрока.Правило, ТекСтрока.НачВремя, ТекСтрока.КонВремя, ?(ТекСтрока.ДниНедели = "0000000", "", ТекСтрока.ДниНедели), ТекСтрока.ДисконтнаяКарта, ТекСтрока.СуммаНакопления,ТекСтрока.СкладКомпании,ТекСтрока.Свойство,ТекСтрока.СуммаСтроки); ДопПоляОтбора = "";
			Для Каждого ТекПоле Из СписокПолей Цикл
				Если обЗначениеНеЗаполнено(ТекПоле.Значение) Тогда
					Продолжить;
				КонецЕсли;
				
				ДопПоляОтбора = ДопПоляОтбора + "			И "+ТекПоле.Ключ+" = &"+ТекПоле.Ключ + Символы.ПС;
				Запрос.УстановитьПараметр(ТекПоле.Ключ,ТекПоле.Значение);
			КонецЦикла;
			Если НЕ ПустаяСтрока(ДопПоляОтбора) Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ФИЛЬТРЫ", ДопПоляОтбора);
			КонецЕсли;
			
			// Установим текст запроса и произведем заполнение строки документа найденными данными
			Запрос.Текст = ТекстЗапроса;
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ТекСтрока, Выборка);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя="Скидки.ЗначениеСкидки" Тогда
		Если (ТекСтрока.ЗначениеСкидки > 100) И (ТекСтрока.Скидка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная) И (НЕ обЗначениеНеЗаполнено(ТекСтрока.Скидка)) Тогда
			ТекСтрока.ЗначениеСкидки = 100;
			#Если Клиент Тогда
				Сигнал();
			#КонецЕсли
		КонецЕсли;
		
	ИначеЕсли Имя="Скидки.ДниНедели" Тогда
		ТекСтрока.ДниНедели = ?(ПустаяСтрока(ТекСтрока.ДниНедели),"0000000",ТекСтрока.ДниНедели);
		
		НомераДней = Новый Структура("Пн,Вт,Ср,Чт,пт,Сб,Вс",1,2,3,4,5,6,7);
		Попытка
			НомерДня = НомераДней[ДопПараметры];
			Значение = ?(Сред(ТекСтрока.ДниНедели,НомерДня,1) = "1", "0", "1");
			ТекСтрока.ДниНедели = Лев(ТекСтрока.ДниНедели, НомерДня - 1) + Значение + Прав(ТекСтрока.ДниНедели, СтрДлина(ТекСтрока.ДниНедели) - НомерДня);
		Исключение КонецПопытки;
		
	ИначеЕсли Имя="Скидки.ДисконтнаяКарта" Тогда
		Попытка
			ТекСтрока.СуммаНакопления = ?(обЗначениеНеЗаполнено(ТекСтрока.ДисконтнаяКарта), 0, ТекСтрока.СуммаНакопления);
		Исключение КонецПопытки;
		Попытка
			ТекСтрока.ОтСуммыНакопленияНаКарте = ?(обЗначениеНеЗаполнено(ТекСтрока.ДисконтнаяКарта), 0, ТекСтрока.ОтСуммыНакопленияНаКарте);
		Исключение КонецПопытки;
		
	ИначеЕсли Имя="Скидки.Номенклатура" Тогда
		
		Если ТипЗнч(ТекСтрока.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			// получим виды номенклатуры которые нельзя выбирать
			БлокироватьВидыНоменклатуры = ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
			Если БлокироватьВидыНоменклатуры <> Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры) = Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество() > 0 Тогда
				Попытка ЕстьОшибка = (БлокироватьВидыНоменклатуры[ТекСтрока.Номенклатура.ВидНоменклатуры] <> Неопределено); Исключение ЕстьОшибка = Ложь; КонецПопытки;
				Если ЕстьОшибка Тогда
					#Если Клиент Тогда
						Предупреждение("В таблицу нельзя вводить номенклатуру вида """ + СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры) + """",5);
					#КонецЕсли
					ТекСтрока.Номенклатура = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ТекСтрока.ТипНоменклатуры = ?(обЗначениеНеЗаполнено(ТекСтрока.Номенклатура), ТекСтрока.ТипНоменклатуры, Справочники.ТипыНоменклатуры.ПустаяСсылка());
		
	ИначеЕсли Имя="Скидки.ТипНоменклатуры" Тогда
		
		Если ЗначениеЗаполнено(ТекСтрока.ТипНоменклатуры) Тогда
			Если ТекСтрока.СкидкаНаТовары И ТекСтрока.СкидкаНаРаботы Тогда
				ТекСтрока.Номенклатура = Неопределено;
			ИначеЕсли ТекСтрока.СкидкаНаТовары Тогда
				ТекСтрока.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
			ИначеЕсли ТекСтрока.СкидкаНаРаботы Тогда
				ТекСтрока.Номенклатура = Справочники.Автоработы.ПустаяСсылка();
			Иначе
				ТекСтрока.Номенклатура = Неопределено;
			КонецЕсли;
		Иначе
			ТекСтрока.Номенклатура = ТекСтрока.Номенклатура;
		КонецЕсли;
		
	ИначеЕсли Имя="Скидки.СкидкаНаТовары" Тогда
		
	ИначеЕсли Имя="Скидки.СкидкаНаРаботы" Тогда
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
#Если Клиент Тогда

// Печать назначение скидок строки
// Параметры:
//	ТабДокумент - табличный документ
//
Функция ПечатьНазначениеСкидокСтроки(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("НазначениеСкидокСтроки");
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ФорматВыводаСуммы		= обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества	= обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(ЭтотОбъект.Дата,"ДФ=dd.MM.yyyy");//дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПодразделениеКомпанииПредставление	= спПолучитьНаименование(ПодразделениеКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	// Получим данные для печати
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НазначениеСкидокСтрокиСкидки.НомерСтроки,
	|	НазначениеСкидокСтрокиСкидки.Скидка,
	|	НазначениеСкидокСтрокиСкидки.Скидка.РучнаяСкидка КАК РучнаяСкидка,
	|	НазначениеСкидокСтрокиСкидки.Скидка.ЭтоПодарок КАК ЭтоПодарок,
	|	НазначениеСкидокСтрокиСкидки.ФлагВытеснения,
	|	НазначениеСкидокСтрокиСкидки.ЗначениеСкидки,
	|	НазначениеСкидокСтрокиСкидки.Номенклатура,
	|	НазначениеСкидокСтрокиСкидки.ТипНоменклатуры,
	|	НазначениеСкидокСтрокиСкидки.Свойство,
	|	НазначениеСкидокСтрокиСкидки.Количество,
	|	НазначениеСкидокСтрокиСкидки.Правило,
	|	НазначениеСкидокСтрокиСкидки.СуммаСтроки,
	|	НазначениеСкидокСтрокиСкидки.ДисконтнаяКарта,
	|	НазначениеСкидокСтрокиСкидки.СуммаНакопления,
	|	НазначениеСкидокСтрокиСкидки.НачВремя,
	|	НазначениеСкидокСтрокиСкидки.КонВремя,
	|	НазначениеСкидокСтрокиСкидки.ДниНедели,
	|	ИСТИНА КАК Пн,
	|	ИСТИНА КАК Вт,
	|	ИСТИНА КАК Ср,
	|	ИСТИНА КАК Чт,
	|	ИСТИНА КАК Пт,
	|	ИСТИНА КАК Сб,
	|	ИСТИНА КАК Вс,
	|	НазначениеСкидокСтрокиСкидки.ВидПрайсЛистаПодарка,
	|	НазначениеСкидокСтрокиСкидки.МаксимальнаяСуммаПодарка,
	|	НазначениеСкидокСтрокиСкидки.СкладКомпании
	|ИЗ
	|	Документ.НазначениеСкидокСтроки.Скидки КАК НазначениеСкидокСтрокиСкидки
	|ГДЕ
	|	НазначениеСкидокСтрокиСкидки.Ссылка = &Ссылка";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	//перебор строк
	ВыборкаТабличнойЧасти = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ДниНедели = СтрокаТабличнойЧасти.ДниНедели;
		СтрокаТабличнойЧасти.Пн = (Сред(ДниНедели,1,1)="1");
		СтрокаТабличнойЧасти.Вт = (Сред(ДниНедели,2,1)="1");
		СтрокаТабличнойЧасти.Ср = (Сред(ДниНедели,3,1)="1");
		СтрокаТабличнойЧасти.Чт = (Сред(ДниНедели,4,1)="1");
		СтрокаТабличнойЧасти.Пт = (Сред(ДниНедели,5,1)="1");
		СтрокаТабличнойЧасти.Сб = (Сред(ДниНедели,6,1)="1");
		СтрокаТабличнойЧасти.Вс = (Сред(ДниНедели,7,1)="1");
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		//применим форматирование к полям строки
		ОбластьМакета.Параметры.ЗначениеСкидки				= Формат(СтрокаТабличнойЧасти.ЗначениеСкидки, ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаСтроки					= Формат(СтрокаТабличнойЧасти.СуммаСтроки, ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Количество					= Формат(СтрокаТабличнойЧасти.Количество, ФорматВыводаКоличества);
		ОбластьМакета.Параметры.СуммаНакопления				= Формат(СтрокаТабличнойЧасти.СуммаНакопления, ФорматВыводаСуммы);
		ОбластьМакета.Параметры.МаксимальнаяСуммаПодарка	= Формат(СтрокаТабличнойЧасти.МаксимальнаяСуммаПодарка, ФорматВыводаСуммы);
			
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, Неопределено, НомерСтраницы, Неопределено, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество();
	
	// Выводим представления и расшифровки подписей
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ПредседательКомиссии.ПредседательКомиссииПредставление = ?(обЗначениеНеЗаполнено(ПредседательКомиссии.ПредседательКомиссии),"","/ "+ ПредседательКомиссии.ПредседательКомиссииПредставление);
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ГлавныйБухгалтер  = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	//ГлавныйБухгалтер  = дкОтветственноеЛицо(ЭтотОбъект,"ГлавныйБухгалтер");
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/ "+ ГлавныйБухгалтер.ГлавныйБухгалтерПредставление);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

// Формирует отчет текущие скидки
//
Процедура ОтчетТекущиеСкидки() Экспорт
	
	// заполним параметры отчета
	Отчет = Отчеты.ТекущиеСкидки.Создать();
	Отчет.ВидОтчета = Перечисления.ВидыОтчетов.ПрочиеОстатки;
	Отчет.РежимВыводаОтчета=Перечисления.РежимыВыводаОтчета.ТабличныйДокумент;
	Отчет.РежимНастройки=Перечисления.РежимыНастройкиОтчетов.Эксперт;
	Отчет.ИмяФормыНастроек="НастройкиОстатки";
	Отчет.СпособФормирования = 2;
	Отчет.ЗаполнитьНачальныеНастройки("ТекстЗапросаТолькоТоварные");
		
	// показатели все
	Для Каждого СтрокаПоказателей Из Отчет.Показатели Цикл
		СтрокаПоказателей.Использование=Истина;
	КонецЦикла;
	// фильтры
	Отбор = Отчет.ПостроительОтчета.Отбор;
	ТекОтбор = Отбор.Найти("РегистраторСкидки");
	Если ТекОтбор=Неопределено Тогда
		ТекОтбор = Отбор.Добавить("РегистраторСкидки","РегистраторСкидки",Отчет.ДеревоДоступныхПолей.Строки.Найти("РегистраторСкидки","ПутьКДанным").Представление);
	КонецЕсли;
	ТекОтбор.ВидСравнения = ВидСравнения.Равно;
	ТекОтбор.Значение=Ссылка;
	ТекОтбор.Использование=Истина;
		
	Отчет.ДатаКонца    = КонецДня(?(ЗначениеЗаполнено(ДатаНачалаДействия), ДатаНачалаДействия, ТекущаяДата()));
	
	ФормаОтчета=ПолучитьОбщуюФорму("Отчет");
	ФормаОтчета.ОтчетОбъект=Отчет;
	ФормаОтчета.Заголовок="Текущие скидки";
	ФормаОтчета.Открыть();
	
КонецПроцедуры

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Обработка заполнения
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	// Произведем заполнение реквизитов документа
	ДатаНачалаДействия = ЭтотОбъект.Дата;
	
	// Заполним на основании назначения скидок строки
	Если Основание <> Неопределено И обЭтотТип(Основание, "ДокументСсылка.НазначениеСкидокСтроки") Тогда
		
		ХозОперация = Справочники.ХозОперации.ОтменаСкидкиНаСтрокуДокумента;
		
		// скидки доступные для закрытия
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СкидкиСтрокиСрезПоследних.Скидка,
		|	СкидкиСтрокиСрезПоследних.ФлагВытеснения,
		|	СкидкиСтрокиСрезПоследних.СкидкаНаТовары,
		|	СкидкиСтрокиСрезПоследних.СкидкаНаРаботы,
		|	СкидкиСтрокиСрезПоследних.ЗначениеСкидки,
		|	СкидкиСтрокиСрезПоследних.Объект КАК Номенклатура,
		|	СкидкиСтрокиСрезПоследних.Объект КАК ТипНоменклатуры,
		|	СкидкиСтрокиСрезПоследних.Свойство,
		|	СкидкиСтрокиСрезПоследних.ОтКоличества КАК Количество,
		|	СкидкиСтрокиСрезПоследних.Правило,
		|	СкидкиСтрокиСрезПоследних.ОтСуммыСтроки КАК СуммаСтроки,
		|	СкидкиСтрокиСрезПоследних.ДисконтнаяКарта,
		|	СкидкиСтрокиСрезПоследних.ОтСуммыНакопленияНаКарте КАК СуммаНакопления,
		|	СкидкиСтрокиСрезПоследних.НачВремя,
		|	СкидкиСтрокиСрезПоследних.КонВремя,
		|	СкидкиСтрокиСрезПоследних.ДниНедели,
		|	СкидкиСтрокиСрезПоследних.ВидПрайсЛистаПодарка,
		|	СкидкиСтрокиСрезПоследних.МаксимальнаяСуммаПодарка,
		|	СкидкиСтрокиСрезПоследних.ЗалОбслуживания КАК СкладКомпании,
		|	СкидкиСтрокиСрезПоследних.ИдентификаторСкидки КАК ИдентификаторСкидки
		|ИЗ
		|	РегистрСведений.СкидкиСтроки.СрезПоследних(
		|			&Дата,
		|			ИдентификаторСкидки В
		|					(ВЫБРАТЬ
		|						НазначениеСкидокСтрокиСкидки.ИдентификаторСкидки
		|					ИЗ
		|						Документ.НазначениеСкидокСтроки.Скидки КАК НазначениеСкидокСтрокиСкидки
		|					ГДЕ
		|						НазначениеСкидокСтрокиСкидки.Ссылка = &Основание)
		|				И Подразделение = &ПодразделениеКомпании
		|				И Скидка.ВидСкидки = &ВидСкидкиНаСтроку) КАК СкидкиСтрокиСрезПоследних
		|ГДЕ
		|	СкидкиСтрокиСрезПоследних.Действует
		|
		|УПОРЯДОЧИТЬ ПО
		|	СкидкиСтрокиСрезПоследних.Период
		|АВТОУПОРЯДОЧИВАНИЕ";
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("Основание", Основание);
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
		Запрос.УстановитьПараметр("ВидСкидкиНаСтроку", Перечисления.ВидыСкидок.НаСтрокуДокумента);
		
		Скидки.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	// Произведем заполнение реквизитов документа
	ДатаНачалаДействия = ТекущаяДата();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если Отказ Тогда
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли;
	
	// установим идентификатор скидки для каждой строки
	Для Каждого Строка Из Скидки Цикл
		Параметры=Новый Структура();
		Параметры.Вставить("Подразделение",		ПодразделениеКомпании);
		Параметры.Вставить("РучнаяСкидка",		Строка.Скидка.РучнаяСкидка);
		Параметры.Вставить("Объект",			?(ЗначениеЗаполнено(Строка.Номенклатура), Строка.Номенклатура, Строка.ТипНоменклатуры));
		Параметры.Вставить("НачВремя",			Строка.НачВремя);
		Параметры.Вставить("КонВремя",			Строка.КонВремя);
		Параметры.Вставить("ДниНедели",			Строка.ДниНедели);
		Параметры.Вставить("ЭтоПодарок",		Строка.Скидка.ЭтоПодарок);
		Параметры.Вставить("ДисконтнаяКарта",	Строка.ДисконтнаяКарта);
		Параметры.Вставить("ОтСуммыНакопленияНаКарте",Строка.СуммаНакопления);
		Параметры.Вставить("ЗалОбслуживания",	Строка.СкладКомпании);
		Параметры.Вставить("Свойство",			Строка.Свойство);
		Параметры.Вставить("ОтСуммыСтроки",		Строка.СуммаСтроки);
		Параметры.Вставить("ОтКоличества",		Строка.Количество);
		Параметры.Вставить("Правило",			Строка.Правило);
		Параметры.Вставить("Скидка",			Строка.Скидка);
		Строка.ИдентификаторСкидки=рсПолучитьИдентификаторСкидкиСтроки(Параметры,Строка.ИдентификаторСкидки)+?(обЗначениеНеЗаполнено(Строка.ИдентификаторСкидки),Строка.НомерСтроки,0);
		Если (НЕ Строка.СкидкаНаТовары) И (НЕ Строка.СкидкаНаРаботы) Тогда
			Строка.СкидкаНаТовары = Истина;
		КонецЕсли; 
	КонецЦикла;	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Вызываем вручную обработчик удаления проведений
	Если (НЕ Отказ) И ЭтотОбъект.Проведен Тогда
		Попытка
			ОбработкаУдаленияПроведения(Отказ);
		Исключение
			Отказ = ИСТИНА;
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ,Проведение=Ложь)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	//если уже отказ
	Если Отказ Тогда Возврат; КонецЕсли;
	
	// при загрузке не проверяем запрет
	Загрузка = одОбменДанными.ПолучитьЗагрузку(ЭтотОбъект);
	// проверим права на отмену проведения
	Если Проведение=Неопределено Тогда
		Проведение=Ложь;
	КонецЕсли; 
	Если НЕ Проведение Тогда
		Отказ = НЕ обПраво("РазрешитьОтменуПроведенияДокументовСкидок", Права,,ЭтотОбъект);
	КонецЕсли; 
	Отказ = (НЕ Загрузка И Отказ);
	Если Отказ Тогда 
		ДополнительныеСвойства.Вставить("ОтменаПроведенияЗапрещена", Истина);
		Сообщить("Нет прав для отмены проведения документов скидок!", СтатусСообщения.Важное); 
		Возврат; 
	КонецЕсли;
	
	// Очистим движения документа 
	НаборЗаписейСкидки = РегистрыСведений.СкидкиСтроки.СоздатьНаборЗаписей();
	НаборЗаписейСкидки.ДокументОбъект = ЭтотОбъект;
	Отказ = НаборЗаписейСкидки.ОтменаПроведения();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Т.к. регистр и документ "не связаны", то вручную отменим предыдущие движения документа
	Если (НЕ Отказ) И Проведен Тогда
		Попытка
			ОбработкаУдаленияПроведения(Отказ,Истина);
		Исключение
			Отказ = ИСТИНА;
		КонецПопытки;
	КонецЕсли;
	
	// Делаем движения по регистру "СкидкиСтроки"
	НаборЗаписейСкидки = РегистрыСведений.СкидкиСтроки.СоздатьНаборЗаписей();
	НаборЗаписейСкидки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСкидки.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписейСкидки.РезультатЗапросаПоСкидкам = ПолучитьТаблицуСкидок();
	
	Если ХозОперация = Справочники.ХозОперации.УстановкаСкидкиНаСтрокуДокумента Тогда
		// Установка скидки на строку документа
		Отказ = Отказ ИЛИ НаборЗаписейСкидки.УстановитьСкидки();
	Иначе
		// Отмена скидки на строку документа
		Отказ = Отказ ИЛИ НаборЗаписейСкидки.ОтменитьСкидки();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
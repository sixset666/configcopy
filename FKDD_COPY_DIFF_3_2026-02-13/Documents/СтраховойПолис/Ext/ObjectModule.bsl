// Модуль документа Страховой полис

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем ПТС Экспорт;		// ТехПаспорт
Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/атрикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
									 
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция проихводит посимвольный разбор даты на составные части и
// возвращает результат в виде структуры
Функция РазобратьДатуВСтруктуру(ТекДата, КлючевоеИмя)
	Результат	= Новый Структура;
	
	Если обЗначениеНеЗаполнено(ТекДата) Тогда Возврат Результат; КонецЕсли;
	
	ТекЧас		= Час (ТекДата);
	ТекМинута	= Минута(ТекДата);
	ТекДень		= День(ТекДата);
	ТекМесяц	= Месяц(ТекДата);
	ТекГод		= Год(ТекДата);	
	
	Если ТекЧас>9 Тогда
		Результат.Вставить(КлючевоеИмя+"Ч1", Сред(ТекЧас,1,1));
		Результат.Вставить(КлючевоеИмя+"Ч2", Сред(ТекЧас,2,1));
	Иначе
		Результат.Вставить(КлючевоеИмя+"Ч1", "0");
		Результат.Вставить(КлючевоеИмя+"Ч2", Строка(ТекЧас));
	КонецЕсли;
	
	Если ТекМинута>9 Тогда
		Результат.Вставить(КлючевоеИмя+"Мин1", Сред(ТекМинута,1,1));
		Результат.Вставить(КлючевоеИмя+"Мин2", Сред(ТекМинута,2,1));
	Иначе
		Результат.Вставить(КлючевоеИмя+"Мин1", "0");
		Результат.Вставить(КлючевоеИмя+"Мин2", Строка(ТекМинута));
	КонецЕсли;
	
	Если ТекДень>9 Тогда
		Результат.Вставить(КлючевоеИмя+"Д1", Сред(ТекДень,1,1));
		Результат.Вставить(КлючевоеИмя+"Д2", Сред(ТекДень,2,1));
	Иначе
		Результат.Вставить(КлючевоеИмя+"Д1", "0");
		Результат.Вставить(КлючевоеИмя+"Д2", Строка(ТекДень));
	КонецЕсли;
	
	Если ТекМесяц>9 Тогда
		Результат.Вставить(КлючевоеИмя+"М1", Сред(ТекМесяц,1,1));
		Результат.Вставить(КлючевоеИмя+"М2", Сред(ТекМесяц,2,1));
	Иначе
		Результат.Вставить(КлючевоеИмя+"М1", "0");
		Результат.Вставить(КлючевоеИмя+"М2", Строка(ТекМесяц));
	КонецЕсли;
	
	Результат.Вставить(КлючевоеИмя+"Г1", Сред(ТекГод,4,1));
	Результат.Вставить(КлючевоеИмя+"Г2", Сред(ТекГод,5,1));	
	
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("Страхователь",1);
	ОбязательныеРеквизиты.Вставить("Страховщик",1);
	ОбязательныеРеквизиты.Вставить("ПрограммаСтрахования",1);
	ОбязательныеРеквизиты.Вставить("ДатаВыдачи",1);
	ОбязательныеРеквизиты.Вставить("НомерПолиса",1);
	ОбязательныеРеквизиты.Вставить("НомерКвитанции",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Возврат Результат;	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СтраховойПолис","Страховой полис",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="ДокументОснование" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			ВалютаДокумента			= ДокументОснование.ВалютаДокумента;
			Автор					= ДокументОснование.Автор;
			КурсДокумента			= ДокументОснование.КурсДокумента;
			КурсВалютыУпр			= ДокументОснование.КурсВалютыУпр;
			ПодразделениеКомпании	= ДокументОснование.ПодразделениеКомпании;
			Организация				= ДокументОснование.Организация;
			Проект					= ДокументОснование.Проект;
			Страхователь			= ДокументОснование.Страхователь;
			ОбработкаРеквизита("Страхователь",,ЭтаФорма);
			
			// Воспользуемся первой строкой вариантов страхования из основания
			Для Каждого ТекСтрока Из ДокументОснование.ВариантыСтрахования Цикл
				Если ТипЗнч(ТекСтрока.ОбъектСтрахования)=Тип("СправочникСсылка.Модели") Тогда
					ТранспортноеСредство	= ТекСтрока.ОбъектСтрахования;
					ОбработкаРеквизита("ТранспортноеСредство");
				КонецЕсли;
				ВидСтрахования			= ТекСтрока.ВидСтрахования;
				ПрограммаСтрахования	= ТекСтрока.ПрограммаСтрахования;
				Страховщик				= ТекСтрока.Страховщик;
				СуммаПремии				= ТекСтрока.СуммаПремии;
				Прервать;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя="Страхователь" Тогда
		Если НЕ обЗначениеНеЗаполнено(Страхователь) Тогда
			Если обЗначениеНеЗаполнено(СобственникТС) Тогда
				СобственникТС	= Страхователь;
			КонецЕсли;
			
			Если ЛицаДопущенныеКУправлениюТС.Количество()=0 Тогда
				НоваяСтрока	= ЛицаДопущенныеКУправлениюТС.Добавить();
				НоваяСтрока.ЛицоДопущенное	= Страхователь;
				
				Если НЕ обЗначениеНеЗаполнено(СобственникТС) И СобственникТС<>Страхователь Тогда
					НоваяСтрока	= ЛицаДопущенныеКУправлениюТС.Добавить();
					НоваяСтрока.ЛицоДопущенное	= СобственникТС;					
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;		
		
	ИначеЕсли  Имя="ПрограммаСтрахования" Тогда
		Если НЕ обЗначениеНеЗаполнено(ПрограммаСтрахования) Тогда
			ВидСтрахования	= ПрограммаСтрахования.ВидСтрахования;
		КонецЕсли;
		
	ИначеЕсли Имя="ВидСтрахования" Тогда
		Если НЕ обЗначениеНеЗаполнено(ВидСтрахования) И
			НЕ обЗначениеНеЗаполнено(ПрограммаСтрахования) И
			ПрограммаСтрахования.ВидСтрахования<>ВидСтрахования Тогда
			ПрограммаСтрахования	= Справочники.ПрограммыСтрахования.ПустаяСсылка();
		КонецЕсли;
		
	ИначеЕсли Имя="Автомобиль" Тогда
		Результат = Истина;
		
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			Если ТипЗнч(ЭтаФорма)=Тип("Форма") Тогда
				//Получить последний ГосНомер
				ЭтаФорма.ГосНомер   = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
				//Получить последний техпаспорт
				ЭтаФорма.ТехПаспорт = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ТехПаспорт);
			КонецЕсли;
						
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("НеИзменятьЦенуАвтомобиля",Истина);
			ТранспортноеСредство	= Автомобиль.Модель;
			Результат				= ОбработкаРеквизита("Модель", ТекСтрока, ЭтаФорма, ДопПараметры);
			ДопПараметры.Удалить("НеИзменятьЦенуАвтомобиля");
		КонецЕсли;
		
		Возврат Результат;
		
	ИначеЕсли Имя="ТранспортноеСредство" Тогда
		
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) И Автомобиль.Модель<>ТранспортноеСредство Тогда
			Автомобиль	= Справочники.Автомобили.ПустаяСсылка();
			Результат	= ОбработкаРеквизита("Автомобиль", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;		
		
		Возврат Результат;
		
	Иначе	
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

//Получает месяц словами, а не цифрой {
Функция ПолучитьМесяцДляВывода(ДатаВывода)
	ДатаСтрока = Формат(ДатаВывода,"ДЛФ=DD");
	БлижайшийПробел = Найти(ДатаСтрока," ");
	ДатаСтрока = Прав(ДатаСтрока,СтрДлина(ДатаСтрока) - БлижайшийПробел);
	БлижайшийПробел = Найти(ДатаСтрока," ");
	ДатаСтрока = Лев(ДатаСтрока,БлижайшийПробел);
	Возврат ДатаСтрока;
КонецФункции

// Формирует печатную форму "Квитанция на получение страховой премии"
// Возвращает сформированный табличный документ:
Функция ПечатьСтраховойПолис(ТабДокумент) Экспорт
	//валюта печати	
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	КоэффициентПересчета= обПересчет(1,ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента, ВалютаПечатногоДокумента, ЭтотОбъект.Дата);
	ФорматВыводаСуммы	= обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	// приступим к печати
	Макет = ПолучитьМакет("СтраховойПолис");	
	
	ОбластьШапка			= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");
	
	СтруктураШапки	= Новый Структура();
	СтруктураШапки.Вставить("НаименованиеПоставщика",		спПолучитьПредставление(ЭтотОбъект.Организация));
	//СтруктураШапки.Вставить("СерияПолиса",				ЭтотОбъект.СерияПолиса);
	//СтруктураШапки.Вставить("НомерПолиса",				ЭтотОбъект.НомерПолиса);
	
	// Посимвольный вывод даты начала и окончания страхования
	Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаНачалаСтрахования) И
		НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаОкончанияСтрахования)Тогда		
		// Дата начала страхования
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаНачалаСтрахования, "ДатаНачалаСтрахования");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;
		// Дата окончания страхования
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаОкончанияСтрахования, "ДатаОкончанияСтрахования");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;			
	КонецЕсли;
	
	// Посимвольный вывод дат использования ТС
	Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаНачалаИспользованияТС1) И
		НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаОкончанияИспользованияТС1) Тогда
		// Дата начала использования ТС
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаНачалаИспользованияТС1, "ДатаНачалаИспользованияТС1");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;
		// Дата окончания использования ТС
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаОкончанияИспользованияТС1, "ДатаОкончанияИспользованияТС1");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;		
	КонецЕсли;
	
	// Посимвольный вывод дат использования ТС
	Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаНачалаИспользованияТС2) И
		НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаОкончанияИспользованияТС2) Тогда
		// Дата начала использования ТС
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаНачалаИспользованияТС2, "ДатаНачалаИспользованияТС2");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;
		// Дата окончания использования ТС
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаОкончанияИспользованияТС2, "ДатаОкончанияИспользованияТС2");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;		
	КонецЕсли;
	   
	// Посимвольный вывод дат использования ТС
	Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаНачалаИспользованияТС3) И
		НЕ обЗначениеНеЗаполнено(ЭтотОбъект.ДатаОкончанияИспользованияТС3) Тогда
		// Дата начала использования ТС
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаНачалаИспользованияТС3, "ДатаНачалаИспользованияТС3");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;
		// Дата окончания использования ТС
		СтруктураДаты	= РазобратьДатуВСтруктуру(ЭтотОбъект.ДатаОкончанияИспользованияТС3, "ДатаОкончанияИспользованияТС3");
		Для Каждого ТекСтрока Из СтруктураДаты Цикл
			СтруктураШапки.Вставить(ТекСтрока.Ключ, ТекСтрока.Значение);
		КонецЦикла;		
	КонецЕсли;	
	
	СтруктураШапки.Вставить("Страхователь",			ЭтотОбъект.Страхователь);
	СтруктураШапки.Вставить("СобственникТС",		ЭтотОбъект.СобственникТС);
	СтруктураШапки.Вставить("ТранспортноеСредство",	ЭтотОбъект.ТранспортноеСредство);
	Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.Автомобиль) Тогда
		ГосНомер   = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ЭтотОбъект.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
		СтруктураШапки.Вставить("ГосНомер",			ГосНомер);
		Если НЕ ПустаяСтрока(ЭтотОбъект.Автомобиль.VIN) Тогда
			СтрокаДляРазбора	= СокрЛП(ЭтотОбъект.Автомобиль.VIN);
			Счетчик	= 1;
			ДлинаСтрокиРазбора	= СтрДлина(СтрокаДляРазбора);
			Для Индекс=1 По ДлинаСтрокиРазбора Цикл
				// Не может быть больше 17 символов
				Если Счетчик=18 Тогда Прервать; КонецЕсли;
				
				ТекСимвол	= Сред(СтрокаДляРазбора, Индекс, 1);
				СтруктураШапки.Вставить("VIN"+Индекс,	ТекСимвол);
				Счетчик	= Счетчик+1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(ПТС) Тогда
		СтруктураШапки.Вставить("ВидПТС",	ПТС.ВидПодтверждающегоДокумента);
		СтруктураШапки.Вставить("СерияПТС",	ПТС.Серия);
		СтруктураШапки.Вставить("НомерПТС",	ПТС.Номер);
	КонецЕсли;
	
	Если ЭтотОбъект.ВидДействияДоговора=1 Тогда
		СтруктураШапки.Вставить("ВидДействияДоговора1",	"Х");
		СтруктураШапки.Вставить("ВидДействияДоговора2",	"");
	Иначе
		СтруктураШапки.Вставить("ВидДействияДоговора1",	"");
		СтруктураШапки.Вставить("ВидДействияДоговора2",	"Х");		
	КонецЕсли;
			
	ОбластьШапка.Параметры.Заполнить(СтруктураШапки);
	ТабДокумент.Вывести(ОбластьШапка);
	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы		= 2;
	НомерСтраницыПред	= НомерСтраницы;
	Счетчик				= 0;
	СтруктураИтоговПоСтранице	= Новый Структура();
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.ЛицаДопущенныеКУправлениюТС;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		Счетчик	= Счетчик + 1;
		
		//заполняем данные строки
		СтруктураСтроки = Новый Структура();
		СтруктураСтроки.Вставить("Номер",	Счетчик);
		СтруктураСтроки.Вставить("ЛицоДопущенное", СтрокаТабличнойЧасти.ЛицоДопущенное);
		Если НЕ обЗначениеНеЗаполнено(СтрокаТабличнойЧасти.ВодительскоеУдостоверение) Тогда
			Удостоверение	= СтрокаТабличнойЧасти.ВодительскоеУдостоверение;
			СерияНомер		= Удостоверение.Серия+" "+Удостоверение.Номер;
			СтруктураСтроки.Вставить("ВодительскоеУдостоверение",	СерияНомер);
		КонецЕсли;
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		//Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
		//	мсвДопОбластиПодвала = Новый Массив;
		//	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		//КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТаблицы, ,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	// На странице должно быть как минимум 5 строк
	Если Счетчик<5 Тогда
		ЧислоЦиклов	= 5-Счетчик;
		Для Индекс=1 По ЧислоЦиклов Цикл
			ОбластьСтрока.Параметры.Номер		= "";
			ОбластьСтрока.Параметры.ЛицоДопущенное	= "";
			ОбластьСтрока.Параметры.ВодительскоеУдостоверение	= "";
			ТабДокумент.Вывести(ОбластьСтрока);			
		КонецЦикла;
	КонецЕсли;
	
	СтруктураПодвала	= Новый Структура();
	СтруктураПодвала.Вставить("СуммаПремии",			ЭтотОбъект.СуммаПремии);
	СтруктураПодвала.Вставить("СуммаПремииПрописью",	обЧислоПрописью(ЭтотОбъект.СуммаПремии, ВалютаПечатногоДокумента));
	
	ДатаДляВывода	= ЭтотОбъект.Дата;
	Если НЕ обЗначениеНеЗаполнено(ДатаДляВывода) Тогда
		ТекДень		= День(ДатаДляВывода);
		//ТекМесяц	= Месяц(ДатаДляВывода);
		ТекМесяц	= ПолучитьМесяцДляВывода(ДатаДляВывода);
		ТекГод		= Год(ДатаДляВывода);	
		
		СтруктураПодвала.Вставить("ДеньЗаключения", Строка(ТекДень));
		СтруктураПодвала.Вставить("МесяцЗаключения", Строка(ТекМесяц));
		СтруктураПодвала.Вставить("ГодЗаключения", Сред(ТекГод,4));
	КонецЕсли;
	
	ДатаДляВывода	= ЭтотОбъект.ДатаВыдачи;
	Если НЕ обЗначениеНеЗаполнено(ДатаДляВывода) Тогда
		ТекДень		= День(ДатаДляВывода); 
		//ТекМесяц	= Месяц(ДатаДляВывода);
        ТекМесяц	= ПолучитьМесяцДляВывода(ДатаДляВывода);
 		ТекГод		= Год(ДатаДляВывода);	
		
		СтруктураПодвала.Вставить("ДеньПолиса", Строка(ТекДень));
		СтруктураПодвала.Вставить("МесяцПолиса", Строка(ТекМесяц));
		СтруктураПодвала.Вставить("ГодПолиса", Сред(ТекГод,4));
	КонецЕсли;		
	
	СтруктураПодвала.Вставить("ФИОСтраховщика",	"                                           ");
	
	ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
	ТабДокумент.Вывести(ОбластьПодвал);	
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху			= 10;
	ТабДокумент.ПолеСлева			= 10;
	ТабДокумент.ПолеСнизу			= 10;
	ТабДокумент.ПолеСправа			= 10;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДокумент.Автомасштаб			= Истина;
	
	Возврат ТабДокумент;

КонецФункции

// Формирует печатную форму "Страховой полис"
// Возвращает сформированный табличный документ:
Функция ПечатьКвитанцияНаПолучениеСтраховойПремии(ТабДокумент) Экспорт
	// Предварительные обработки данных	
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	// КоэффициентПересчета= обПересчет(1, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента, ВалютаПечатногоДокумента, ЭтотОбъект.Дата);	
	ФорматВыводаСуммы	= обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	// Необходимо отдельно выводить целую часть суммы прописью без валюты и отдельно дробную
	ДробнаяЧасть		= СуммаПремии-Цел(СуммаПремии);	
	Если ДробнаяЧасть=0 Тогда
		ДробнаяЧасть	= "00";
	Иначе
		ДробнаяЧасть	= Сред(Формат(ДробнаяЧасть,"ЧДЦ=2"), 3);	
	КонецЕсли;	
	СуммаПремииПрописью	= обЧислоПрописью(СуммаПремии, ВалютаПечатногоДокумента, "Л=ru_RU;НП=Ложь;НД=Ложь;ДП=Ложь");
    Позиция	= найти(СуммаПремииПрописью, ДробнаяЧасть);
	Если Позиция>0 Тогда
		СуммаПремииПрописью	= Лев(СуммаПремииПрописью, Позиция-2);
	КонецЕсли;
	Позиция	= 0;
	Для Индекс=1 По СтрДлина(СуммаПремииПрописью)Цикл
		Если Сред(СуммаПремииПрописью, Индекс,1)=" " Тогда
			Позиция	= Индекс;
		КонецЕсли;
	КонецЦикла;
	Если Позиция>0 Тогда
		СуммаПремииПрописью	= Лев(СуммаПремииПрописью, Позиция-1);
	КонецЕсли;
	
	ДатаОплаты	= Формат(Дата, "ДЛФ=DD");
	Позиция	= Найти(ДатаОплаты, " ");
	Если Позиция>0 Тогда
		ДатаОплаты	= Сред(ДатаОплаты, Позиция+1);
	КонецЕсли;	
	
	// приступим к печати
	Макет = ПолучитьМакет("КвитанцияНаПолучениеСтраховойПремии");	
	
	ОбластьШапка			= Макет.ПолучитьОбласть("Шапка");
	
	СтруктураШапки	= Новый Структура();
	СтруктураШапки.Вставить("НаименованиеПоставщика",		спПолучитьПредставление(Организация));
	СтруктураШапки.Вставить("СерияКвитанции",				СерияКвитанции);
	СтруктураШапки.Вставить("НомерКвитанции",				НомерКвитанции);
	СтруктураШапки.Вставить("ПодразделениеКомпании",		ПодразделениеКомпании);
	СтруктураШапки.Вставить("СерияНомерСтраховогоПолиса",	""+СерияПолиса+"  "+НомерПолиса);
	СтруктураШапки.Вставить("Страхователь",					Страхователь);
	Если НЕ обЗначениеНеЗаполнено(ПрограммаСтрахования) Тогда
		СтруктураШапки.Вставить("ВидСтрахования",			ПрограммаСтрахования.ВидСтрахования);
	КонецЕсли;
	СтруктураШапки.Вставить("ВалютаПечатногоДокумента",		?(Найти(НРег(ВалютаПечатногоДокумента),"руб")>0, "руб.", ВалютаПечатногоДокумента));
	
	СтруктураШапки.Вставить("СуммаПремииЦелая",				СуммаПремииПрописью);
	СтруктураШапки.Вставить("СуммаПремииДробная",			ДробнаяЧасть);
	Если НЕ ОплатаКвитанцииКартой Тогда
		СтруктураШапки.Вставить("ОплатаНаличными",				"Х");
		СтруктураШапки.Вставить("ОплатаКарточкой",				"");
		СтруктураШапки.Вставить("СуммаПремииНаличнымиЦелая",	СуммаПремииПрописью);
		СтруктураШапки.Вставить("СуммаПремииНаличнымиДробная",	ДробнаяЧасть);
		СтруктураШапки.Вставить("СуммаПремииКартойЦелая",		"");
		СтруктураШапки.Вставить("СуммаПремииКартойДробная",		"");
	Иначе
		СтруктураШапки.Вставить("ОплатаНаличными",				"");
		СтруктураШапки.Вставить("ОплатаКарточкой",				"Х");
		СтруктураШапки.Вставить("СуммаПремииНаличнымиЦелая",	"");
		СтруктураШапки.Вставить("СуммаПремииНаличнымиДробная",	"");
		СтруктураШапки.Вставить("СуммаПремииКартойЦелая",		СуммаПремииПрописью);
		СтруктураШапки.Вставить("СуммаПремииКартойДробная",		ДробнаяЧасть);
	КонецЕсли;
	
	СтруктураШапки.Вставить("ДолжностьСтраховщика",	ДолжностьСтраховщика);
	
	СтруктураШапки.Вставить("Страховщик",					Страховщик);
	СтруктураШапки.Вставить("ДеньОплаты",					""""+День(Дата)+"""");
	СтруктураШапки.Вставить("ДатаОплаты",					ДатаОплаты);	
			
	ОбластьШапка.Параметры.Заполнить(СтруктураШапки);
	ТабДокумент.Вывести(ОбластьШапка);
		
	// Зададим параметры макета
	ТабДокумент.ПолеСверху			= 5;
	ТабДокумент.ПолеСлева			= 5;
	ТабДокумент.ПолеСнизу			= 5;
	ТабДокумент.ПолеСправа			= 5;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДокумент.Автомасштаб			= Ложь;
	
	Возврат ТабДокумент;
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события установки нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	#Если Клиент Тогда
	зфСтраховойПолисОбработкаЗаполнения(ЭтотОбъект,Основание);
	#Иначе
	зфЗащищенныеФункцииСервер.СтраховойПолисОбработкаЗаполнения(ЭтотОбъект,Основание);
	#КонецЕсли

	Если НЕ обЗначениеНеЗаполнено(Страхователь) Тогда
		Если обЗначениеНеЗаполнено(СобственникТС) Тогда
			СобственникТС	= Страхователь;
		КонецЕсли;
		
		Если ЛицаДопущенныеКУправлениюТС.Количество()=0 Тогда
			НоваяСтрока	= ЛицаДопущенныеКУправлениюТС.Добавить();
			НоваяСтрока.ЛицоДопущенное	= Страхователь;
			
			Если НЕ обЗначениеНеЗаполнено(СобственникТС) И СобственникТС<>Страхователь Тогда
				НоваяСтрока	= ЛицаДопущенныеКУправлениюТС.Добавить();
				НоваяСтрока.ЛицоДопущенное	= СобственникТС;					
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;		
	
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		//Получить последний ГосНомер
		ГосНомер	= Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
		//Получить последний техпаспорт
		ТехПаспорт	= Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ТехПаспорт);
		ПТС			= ТехПаспорт;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если НЕ Кредит Тогда

		Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	
	// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
		// Взаиморасчеты
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения       = РежимПроведения;
		НаборЗаписейВзаиморасчеты.Контрагент            = Страхователь;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
		НаборЗаписейВзаиморасчеты.Сделка                = ЭтотОбъект.Ссылка;
		НаборЗаписейВзаиморасчеты.Сумма                 = СуммаПремии;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
		
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
		
		
		// Взаиморасчеты
		
		//misha удалить
		
		//НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		//НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
		//НаборЗаписейВзаиморасчеты.РежимПроведения       = РежимПроведения;
		//НаборЗаписейВзаиморасчеты.Контрагент            = Страховщик;
		//НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ПрограммаСтрахования.Договор;
		//НаборЗаписейВзаиморасчеты.Организация = ПрограммаСтрахования.Договор;
		//НаборЗаписейВзаиморасчеты.Сделка                = ЭтотОбъект.Ссылка;
		//НаборЗаписейВзаиморасчеты.Сумма                 = СуммаПремии;
		//НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
		//
		//Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	КонецЕсли;	
	

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
		Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ПТС	= Справочники.ПодтверждающиеДокументы.ПустаяСсылка();

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

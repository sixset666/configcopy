// Модуль документа ОтчетКомиссионераЗаАвтомобили

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("ИдентификаторАвтомобиля",1);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("ИдентификаторАвтомобиля",3);
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыАвтомобили = Новый Структура();
				КонтролируемыеРеквизитыАвтомобили.Вставить("ДокументПередачи");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Автомобили",КонтролируемыеРеквизитыАвтомобили);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);  			
			КонецЕсли;
		
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ОтчетКомиссионераЗаАвтомобили","Отчет комиссионера за автомобили",Истина);
	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(СуммыТаблиц.СуммаАвтомобилей) КАК СуммаАвтомобилей,
	|	СУММА(СуммыТаблиц.СуммаДопОборудования) КАК СуммаДопОборудования
	|ПОМЕСТИТЬ
	|	СуммыТаблиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаАвтомобили.СуммаВсего КАК СуммаАвтомобилей,
	|		0 КАК СуммаДопОборудования
	|	ИЗ
	|		Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ТаблицаАвтомобили
	|	ГДЕ
	|		ТаблицаАвтомобили.Ссылка=&Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		0,
	|		ТаблицаТовары.СуммаВсего
	|	ИЗ
	|		Документ.ОтчетКомиссионераЗаАвтомобили.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка=&Ссылка) КАК СуммыТаблиц
	|;
	|
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	/////////// ПРИВАТ ////////////
	|	Док.Ссылка КАК ДокументПродажи,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СуммаВознаграждения КАК СуммаВознаграждения,
	|	ЕСТЬNULL(СуммыТаблиц.СуммаАвтомобилей, 0) КАК СуммаАвтомобилей,
	|	ЕСТЬNULL(СуммыТаблиц.СуммаДопОборудования, 0) КАК СуммаДопОборудования
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	СуммыТаблиц КАК СуммыТаблиц
	|ПО
	|	ИСТИНА
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		//Закроем взаиморасчеты с контрагентом
		Если (ШапкаДокумента.СуммаДокумента-ШапкаДокумента.СуммаВознаграждения)<>0 Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
			НаборЗаписейДоходыИРасходы.Доход = ШапкаДокумента.СуммаДокумента-ШапкаДокумента.СуммаВознаграждения;
			НаборЗаписейДоходыИРасходы.Приход();
		КонецЕсли; 
		//Закроем переданные на комиссию автомобили
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(АвтомобилиОтданные.СуммаСебестоимостиУпр),0) КАК СуммаСебестоимостиУпр
		|ИЗ
		|	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
		|ГДЕ
		|	АвтомобилиОтданные.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаСебестоимостиУпр<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте = Истина;
				НаборЗаписейДоходыИРасходы.Расход = Выборка.СуммаСебестоимостиУпр;
				НаборЗаписейДоходыИРасходы.Приход();
			КонецЕсли; 
		КонецЕсли; 		 
		Возврат НЕ Отказ;
	КонецЕсли;
	
	Отказ=Ложь;
	НаборЗаписейПартииОтданные = Движения.ПартииТоваровОтданные;
	НаборЗаписейПартииОтданные.ДокументОбъект        = ЭтотОбъект;
	НаборЗаписейПартииОтданные.Контрагент            = ШапкаДокумента.Контрагент;
	НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов = ШапкаДокумента.ДоговорВзаиморасчетов;
	НаборЗаписейПартииОтданные.ОтчетКомиссионера     = Истина;
	НаборЗаписейПартииОтданные.ШапкаДокумента        = ШапкаДокумента;
	НаборЗаписейПартииОтданные.ЕстьАвтомобиль        = Истина;
	Запрос = Новый Запрос;
	Запрос.Текст = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи КАК ДокументПередачи,
				|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Автомобиль КАК Автомобиль
				//|	ОтчетКомиссионераЗаАвтомобилиТовары.Номенклатура КАК Номенклатура
				|ПОМЕСТИТЬ
				|	ТаблицаАвтомобилей
				|ИЗ
				|	Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ОтчетКомиссионераЗаАвтомобилиАвтомобили
				//|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
				//|	Документ.ОтчетКомиссионераЗаАвтомобили.Товары КАК ОтчетКомиссионераЗаАвтомобилиТовары
				//|ПО
				//|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = &Ссылка И 
				//|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = ОтчетКомиссионераЗаАвтомобилиТовары.Ссылка И 
				//|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ИдентификаторАвтомобиля = ОтчетКомиссионераЗаАвтомобилиТовары.ИдентификаторАвтомобиля
				|ГДЕ
				|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = &Ссылка
				|;
				|
				|ВЫБРАТЬ
				|	ПартииТоваровОтданные.Автомобиль КАК Автомобиль,
				|	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
				|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	ПартииТоваровОтданные.ДокументПередачи КАК ДокументПередачи,
				|	ПартииТоваровОтданные.Партия КАК Партия,
				|	ПартииТоваровОтданные.ГТД КАК ГТД,
				|	СУММА(ВЫБОР
				|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
				|			ПартииТоваровОтданные.Количество
				|		ИНАЧЕ
				|			-ПартииТоваровОтданные.Количество
				|	КОНЕЦ) КАК Количество,
				|	СУММА(ВЫБОР
				|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
				|			ПартииТоваровОтданные.Сумма
				|		ИНАЧЕ
				|			-ПартииТоваровОтданные.Сумма
				|	КОНЕЦ) КАК Сумма,
				|	СУММА(ВЫБОР
				|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
				|			ПартииТоваровОтданные.СуммаУпр
				|		ИНАЧЕ
				|			-ПартииТоваровОтданные.СуммаУпр
				|	КОНЕЦ) КАК СуммаУпр,
				|	СУММА(ВЫБОР
				|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
				|			ПартииТоваровОтданные.СуммаНДС
				|		ИНАЧЕ
				|			-ПартииТоваровОтданные.СуммаНДС
				|	КОНЕЦ) КАК СуммаНДС,
				|	СУММА(ВЫБОР
				|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
				|			ПартииТоваровОтданные.СуммаСебестоимостиУпр
				|		ИНАЧЕ
				|			-ПартииТоваровОтданные.СуммаСебестоимостиУпр
				|	КОНЕЦ) КАК СуммаСебестоимостиУпр
				|ИЗ
				|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
				|ГДЕ
				|	ПартииТоваровОтданные.Период <=&ДатаКон И 
				|	ПартииТоваровОтданные.Контрагент = &Контрагент И 
				|	ПартииТоваровОтданные.ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И 
				|	(ПартииТоваровОтданные.Автомобиль, ПартииТоваровОтданные.ДокументПередачи) В 
				|	(ВЫБРАТЬ
				|		ТаблицаАвтомобилей.Автомобиль,
				|		ТаблицаАвтомобилей.ДокументПередачи
				|	ИЗ
				|		ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
				|СГРУППИРОВАТЬ ПО
				|	ПартииТоваровОтданные.Автомобиль,
				|	ПартииТоваровОтданные.Номенклатура,
				|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
				|	ПартииТоваровОтданные.ДокументПередачи,
				|	ПартииТоваровОтданные.Партия,
				|	ПартииТоваровОтданные.ГТД
				|";
	Запрос.УстановитьПараметр("ДатаКон",               ШапкаДокумента.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Ссылка",                ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("Контрагент",            ШапкаДокумента.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ШапкаДокумента.ДоговорВзаиморасчетов);
	НаборЗаписейПартииОтданные.РезультатЗапросаПоПартиям = Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	             |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Автомобиль КАК Автомобиль,
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.Номенклатура КАК Номенклатура,
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи КАК ДокументПередачи,
	             |	СУММА(ОтчетКомиссионераЗаАвтомобилиТовары.СуммаНДС) КАК СуммаНДС,
	             |	СУММА(ОтчетКомиссионераЗаАвтомобилиТовары.СуммаВсего) КАК СуммаВсего,
	             |	СУММА(ОтчетКомиссионераЗаАвтомобилиТовары.Количество * ОтчетКомиссионераЗаАвтомобилиТовары.Коэффициент) КАК Количество
	             |ИЗ
	             |	Документ.ОтчетКомиссионераЗаАвтомобили.Товары КАК ОтчетКомиссионераЗаАвтомобилиТовары
	             |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	             |	Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ОтчетКомиссионераЗаАвтомобилиАвтомобили
	             |ПО
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.Ссылка = &Ссылка И 
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.Ссылка = ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка И 
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.ИдентификаторАвтомобиля = ОтчетКомиссионераЗаАвтомобилиАвтомобили.ИдентификаторАвтомобиля
	             |ГДЕ
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.Ссылка = &Ссылка
	             |СГРУППИРОВАТЬ ПО
	             |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Автомобиль,
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.Номенклатура,
	             |	ОтчетКомиссионераЗаАвтомобилиТовары.ХарактеристикаНоменклатуры,
	             |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи";
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам=Запрос.Выполнить();
	Отказ = НЕ НаборЗаписейПартииОтданные.Расход() ИЛИ Отказ;
	//Спишем переданные опции, установленные производителем
	Запрос = Новый Запрос;
	Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи,
			|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Автомобиль
			|ПОМЕСТИТЬ
			|	ТаблицаАвтомобилей
			|ИЗ
			|	Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ОтчетКомиссионераЗаАвтомобилиАвтомобили
			|ГДЕ
			|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = &Ссылка
			|;
			|
			|ВЫБРАТЬ
			|	ПартииТоваровОтданные.Автомобиль КАК Автомобиль,
			|	ПартииТоваровОтданные.Контрагент КАК Контрагент,
			|	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
			|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ПартииТоваровОтданные.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
			|	ПартииТоваровОтданные.ДокументПередачи КАК ДокументПередачи,
			|	ПартииТоваровОтданные.Партия КАК Партия,
			|	ПартииТоваровОтданные.ГТД КАК ГТД,
			|	СУММА(ВЫБОР
			|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ПартииТоваровОтданные.Количество
			|		ИНАЧЕ
			|			-ПартииТоваровОтданные.Количество
			|	КОНЕЦ) КАК Количество,
			|	СУММА(ВЫБОР
			|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ПартииТоваровОтданные.Сумма
			|		ИНАЧЕ
			|			-ПартииТоваровОтданные.Сумма
			|	КОНЕЦ) КАК Сумма,
			|	СУММА(ВЫБОР
			|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ПартииТоваровОтданные.СуммаУпр
			|		ИНАЧЕ
			|			-ПартииТоваровОтданные.СуммаУпр
			|	КОНЕЦ) КАК СуммаУпр,
			|	СУММА(ВЫБОР
			|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ПартииТоваровОтданные.СуммаНДС
			|		ИНАЧЕ
			|			-ПартииТоваровОтданные.СуммаНДС
			|	КОНЕЦ) КАК СуммаНДС,
			|	СУММА(ВЫБОР
			|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ПартииТоваровОтданные.СуммаСебестоимостиУпр
			|		ИНАЧЕ
			|			-ПартииТоваровОтданные.СуммаСебестоимостиУпр
			|	КОНЕЦ) КАК СуммаСебестоимостиУпр,
			|	СУММА(ВЫБОР
			|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ПартииТоваровОтданные.СуммаСебестоимостиРегл
			|		ИНАЧЕ
			|			-ПартииТоваровОтданные.СуммаСебестоимостиРегл
			|	КОНЕЦ) КАК СуммаСебестоимостиРегл
			|ИЗ
			|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
			|ГДЕ
			|	ПартииТоваровОтданные.Период <= &НаДату И 
			|	ПартииТоваровОтданные.Номенклатура = &ОпцияПроизводителя И 
			|	(ПартииТоваровОтданные.Автомобиль, ПартииТоваровОтданные.Регистратор) В 
			|	(ВЫБРАТЬ
			|		ТаблицаАвтомобилей.Автомобиль,
			|		ТаблицаАвтомобилей.ДокументПередачи
			|	ИЗ
			|		ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
			|СГРУППИРОВАТЬ ПО
			|	ПартииТоваровОтданные.Автомобиль,
			|	ПартииТоваровОтданные.Контрагент,
			|	ПартииТоваровОтданные.Номенклатура,
			|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
			|	ПартииТоваровОтданные.ДоговорВзаиморасчетов,
			|	ПартииТоваровОтданные.ДокументПередачи,
			|	ПартииТоваровОтданные.Партия,
			|	ПартииТоваровОтданные.ГТД
			|";
	Запрос.УстановитьПараметр("НаДату",             ШапкаДокумента.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Ссылка",             ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("ОпцияПроизводителя", Справочники.Номенклатура.ОпцияПроизводителя);
	Выборка=Запрос.Выполнить().Выбрать();
	НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = НаборЗаписейПартииОтданные.Добавить();
		НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период                     = ШапкаДокумента.Дата;
		НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
		НоваяЗапись.Контрагент                 = Выборка.Контрагент;
		НоваяЗапись.Автомобиль                 = Выборка.Автомобиль;
		НоваяЗапись.Номенклатура               = Выборка.Номенклатура;
		НоваяЗапись.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		НоваяЗапись.ДоговорВзаиморасчетов      = Выборка.ДоговорВзаиморасчетов;
		НоваяЗапись.ДокументПередачи           = Выборка.ДокументПередачи;
		НоваяЗапись.Партия                     = Выборка.Партия;
		НоваяЗапись.ГТД                        = Выборка.ГТД;
		НоваяЗапись.Количество                 = Выборка.Количество;
		НоваяЗапись.Сумма                      = Выборка.Сумма;
		НоваяЗапись.СуммаУпр                   = Выборка.СуммаУпр;
		НоваяЗапись.СуммаНДС                   = Выборка.СуммаНДС;
		НоваяЗапись.СуммаСебестоимостиУпр      = Выборка.СуммаСебестоимостиУпр;
		НоваяЗапись.СуммаСебестоимостиРегл     = Выборка.СуммаСебестоимостиРегл;
		НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		НаборЗаписейПартииОтданные.Записать();
	КонецЕсли; 
	
	// продажи
	Если НЕ Отказ Тогда
		НаборЗаписейПродажи = Движения.Продажи;
		НаборЗаписейПродажи.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейПродажи.СкладКомпании         = Справочники.СкладыКомпании.ПустаяСсылка();
		НаборЗаписейПродажи.ДокументПродажи		  = ШапкаДокумента.ДокументПродажи;
		НаборЗаписейПродажи.Сторно                = Ложь;
		НаборЗаписейПродажи.Покупатель            = ШапкаДокумента.Контрагент;
		НаборЗаписейПродажи.ДоговорВзаиморасчетов = ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПродажи.ПодразделениеКомпании = ШапкаДокумента.ПодразделениеКомпании;
		НаборЗаписейПродажи.Комиссия              = Истина;
		НаборЗаписейПродажи.ЕстьАвтомобиль        = Истина;
		НаборЗаписейПродажи.ШапкаДокумента        = ШапкаДокумента;
		Запрос=Новый Запрос("
							|ВЫБРАТЬ РАЗЛИЧНЫЕ
							|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи КАК ДокументПередачи,
							|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Автомобиль КАК Автомобиль
							//|	ОтчетКомиссионераЗаАвтомобилиТовары.Номенклатура КАК Номенклатура,
							//|	ОтчетКомиссионераЗаАвтомобилиТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
							|ПОМЕСТИТЬ
							|	ТаблицаАвтомобилей
							|ИЗ
							|	Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ОтчетКомиссионераЗаАвтомобилиАвтомобили
							//|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
							//|	Документ.ОтчетКомиссионераЗаАвтомобили.Товары КАК ОтчетКомиссионераЗаАвтомобилиТовары
							//|ПО
							//|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = &Ссылка И 
							//|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = ОтчетКомиссионераЗаАвтомобилиТовары.Ссылка И 
							//|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ИдентификаторАвтомобиля = ОтчетКомиссионераЗаАвтомобилиТовары.ИдентификаторАвтомобиля
							|ГДЕ
							|	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = &Ссылка
							|;
							|
							|ВЫБРАТЬ
							|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
							|	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
							|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
							|	КомплектацияАвтомобилей.Партия КАК Партия,
							|	КомплектацияАвтомобилей.ГТД КАК ГТД,
							|	КомплектацияАвтомобилей.СтавкаНДС,
							|	СУММА(КомплектацияАвтомобилей.Количество) КАК Количество,
							|	СУММА(КомплектацияАвтомобилей.Сумма) КАК Сумма,
							|	СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр,
							|	СУММА(КомплектацияАвтомобилей.СуммаНДС) КАК СуммаНДС
							|ИЗ
							|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
							|ГДЕ
							|	КомплектацияАвтомобилей.ВидДвижения = &ВидДвижения И 
							|	КомплектацияАвтомобилей.Партия В (&Партии) И 
							|	КомплектацияАвтомобилей.СтатусПартии = &СтатусПартии И 
							|	(КомплектацияАвтомобилей.Автомобиль, КомплектацияАвтомобилей.Регистратор) В 
							|	(ВЫБРАТЬ
							|		ТаблицаАвтомобилей.Автомобиль,
							|		ТаблицаАвтомобилей.ДокументПередачи
							|	ИЗ
							|		ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
							|
							|СГРУППИРОВАТЬ ПО
							|	КомплектацияАвтомобилей.Автомобиль,
							|	КомплектацияАвтомобилей.Номенклатура,
							|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
							|	КомплектацияАвтомобилей.Партия,
							|	КомплектацияАвтомобилей.ГТД,
							|	КомплектацияАвтомобилей.Регистратор,
							|	КомплектацияАвтомобилей.СтавкаНДС");
		Запрос.УстановитьПараметр("ВидДвижения",  ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("СтатусПартии", Перечисления.СтатусыПартий.ТоварКупленный);
		Запрос.УстановитьПараметр("Ссылка",       ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("Партии",       НаборЗаписейПартииОтданные.ВыгрузитьКолонку("Партия"));
		НаборЗаписейПродажи.КэшСебестоимостиПриКомиссии = Запрос.Выполнить().Выгрузить();
		Отказ = НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	НаборЗаписейПартииТоваровОтданные=Движения.ПартииТоваровОтданные;
	СебестоимостьАвтомобилейУпр  = 0;
	СебестоимостьОборудованияУпр = НаборЗаписейПартииТоваровОтданные.Итог("СуммаСебестоимостиУпр");
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(АвтомобилиОтданные.СуммаСебестоимостиУпр),0) КАК СуммаСебестоимостиУпр
	|ИЗ
	|	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
	|ГДЕ
	|	АвтомобилиОтданные.Регистратор = &Регистратор";
	Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СебестоимостьАвтомобилейУпр = Выборка.СуммаСебестоимостиУпр;
	КонецЕсли; 
	//доходы и расходы
	//сумма себестоимости списанных автомобилей
	Если СебестоимостьАвтомобилейУпр <> 0 Тогда
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли; 
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьАвтомобилейУпр;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Если СебестоимостьОборудованияУпр <> 0 Тогда
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли; 
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьОборудованияУпр;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// определим суммы по подразделениям
	// подготовим таблицу для накопления сумм по подразделениям
	ТаблицаСуммПоПодразделениям = Новый ТаблицаЗначений();
	ТаблицаСуммПоПодразделениям.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммПоПодразделениям.Колонки.Добавить("СуммаВсего",     Новый ОписаниеТипов("Число"));
	ТаблицаСуммПоПодразделениям.Колонки.Добавить("Вознаграждение", Новый ОписаниеТипов("Число"));
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
				 |	ОбъединенныйЗапрос.Подразделение КАК Подразделение,
				 |	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаВсего),0) КАК СуммаВсего,
				 |	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.Вознаграждение),0) КАК Вознаграждение
				 |ИЗ(
				 |ВЫБРАТЬ
				 |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
				 |	ЕСТЬNULL(СУММА(ОтчетКомиссионераЗаАвтомобилиАвтомобили.СуммаВсего),0) КАК СуммаВсего,
				 |	ЕСТЬNULL(СУММА(ОтчетКомиссионераЗаАвтомобилиАвтомобили.Вознаграждение),0) КАК Вознаграждение
				 |ИЗ
				 |	Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ОтчетКомиссионераЗаАвтомобилиАвтомобили
				 |ГДЕ
				 |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.Ссылка = &Ссылка
				 |СГРУППИРОВАТЬ ПО
				 |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи.ДоговорВзаиморасчетов.Подразделение
				 |
				 |ОБЪЕДИНИТЬ ВСЕ
				 |
				 |ВЫБРАТЬ
				 |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи.ДоговорВзаиморасчетов.Подразделение,
				 |	ЕСТЬNULL(СУММА(ОтчетКомиссионераЗаАвтомобилиТовары.СуммаВсего),0),
				 |	ЕСТЬNULL(СУММА(ОтчетКомиссионераЗаАвтомобилиТовары.Вознаграждение),0)
				 |ИЗ
				 |	Документ.ОтчетКомиссионераЗаАвтомобили.Товары КАК ОтчетКомиссионераЗаАвтомобилиТовары
				 |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераЗаАвтомобили.Автомобили КАК ОтчетКомиссионераЗаАвтомобилиАвтомобили
				 |		ПО ОтчетКомиссионераЗаАвтомобилиТовары.ИдентификаторАвтомобиля = ОтчетКомиссионераЗаАвтомобилиАвтомобили.ИдентификаторАвтомобиля
				 |ГДЕ
				 |	ОтчетКомиссионераЗаАвтомобилиТовары.Ссылка = &Ссылка
				 |СГРУППИРОВАТЬ ПО
				 |	ОтчетКомиссионераЗаАвтомобилиАвтомобили.ДокументПередачи.ДоговорВзаиморасчетов.Подразделение
				 |)КАК ОбъединенныйЗапрос
				 |
				 |СГРУППИРОВАТЬ ПО
				 |	ОбъединенныйЗапрос.Подразделение";
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаСумм = ТаблицаСуммПоПодразделениям.Добавить();
		Если БалансВедетсяПоПодразделениям Тогда
			СтрокаСумм.Подразделение  = Выборка.Подразделение;
		Иначе
			СтрокаСумм.Подразделение  = ШапкаДокумента.ПодразделениеКомпании;
		КонецЕсли;
		СтрокаСумм.СуммаВсего     = Выборка.СуммаВсего;
		СтрокаСумм.Вознаграждение = Выборка.Вознаграждение;
	КонецЦикла; 
	ТаблицаСуммПоПодразделениям.Свернуть("Подразделение","СуммаВсего,Вознаграждение");
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КурсУпр = ?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр);
	Для Каждого СтрокаСумм Из ТаблицаСуммПоПодразделениям Цикл
		//Сумма комиссионного вознаграждения
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение=СтрокаСумм.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Вознаграждение;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
		НаборЗаписейДоходыИРасходы.Расход=обПересчет(СтрокаСумм.Вознаграждение,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр);
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		
		Если ШапкаДокумента.СуммаАвтомобилей <> 0 Тогда
			//СуммаВсего - списанная сумма товарной задолженности
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСумм.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоАвтомобилям;
			НаборЗаписейДоходыИРасходы.Доход                  = обПересчет(ШапкаДокумента.СуммаАвтомобилей , ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр,КурсУпр);
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		Если ШапкаДокумента.СуммаДопОборудования <> 0 Тогда
			//СуммаВсего - списанная сумма товарной задолженности
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСумм.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
			НаборЗаписейДоходыИРасходы.Доход                  = обПересчет(ШапкаДокумента.СуммаДопОборудования, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр);
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НЕ Отказ;
КонецФункции

//Добавим оборудование автомобиля
Процедура ДобавитьОборудованиеАвтомобиля(Знач Автомобиль,ДокументПередачи,ИзменятьЦену)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.Выгрузить());
	ДокументОбъектСтруктура.Вставить("Товары",Товары.Выгрузить());
	зфЗащищенныеФункцииСервер.ОтчетКомиссионераЗаАвтомобилиДобавитьОборудованиеАвтомобиля(ДокументОбъектСтруктура,Автомобиль,ДокументПередачи);
	
	Если НЕ ДокументОбъектСтруктура.Свойство("ТаблицаТоваров") Тогда
		Возврат;
	КонецЕсли; 
	
	Товары.Очистить();
	Товары.Загрузить(ДокументОбъектСтруктура.Товары);
	
	Если ИзменятьЦену Тогда
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
		Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
			КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр        = КурсВалютыУпр;
		КонецЕсли;
	КонецЕсли;
	Для каждого СтрокаТоваров Из ДокументОбъектСтруктура.ТаблицаТоваров Цикл
		СтрокиОборудования=Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля,Номенклатура,ХарактеристикаНоменклатуры",ДокументОбъектСтруктура.ИдентификаторАвтомобиля,СтрокаТоваров.Номенклатура,СтрокаТоваров.ХарактеристикаНоменклатуры));
		Если СтрокиОборудования.Количество()=0 Тогда 
			НоваяСтрокаТоваров=Товары.Добавить();
			НоваяСтрокаТоваров.ИдентификаторАвтомобиля=ДокументОбъектСтруктура.ИдентификаторАвтомобиля;
			НоваяСтрокаТоваров.Номенклатура=СтрокаТоваров.Номенклатура;
			НоваяСтрокаТоваров.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
			ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрокаТоваров);
		Иначе
			НоваяСтрокаТоваров=СтрокиОборудования[0];
		КонецЕсли; 
		НоваяСтрокаТоваров.Количество = СтрокаТоваров.Количество/?(обЗначениеНеЗаполнено(НоваяСтрокаТоваров.Коэффициент),1,НоваяСтрокаТоваров.Коэффициент);
		Если ИзменятьЦену Тогда
			НоваяСтрокаТоваров.СуммаВсего=обПересчет(СтрокаТоваров.СуммаПродажиУпр,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли; 
		ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаТоваров);
	КонецЦикла;
КонецПроцедуры

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Автомобили.Итог("СуммаВсего")+Товары.Итог("СуммаВсего");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		//Параметры для подстановки или создания договора контрагента
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
		ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
		ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомиссионером И
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбирать договор комиссии !",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Возврат Ложь;
			КонецЕсли; 
		КонецЕсли;
		#Если Клиент Тогда
		Если (ЭтаФорма<>Неопределено) И
			 (НЕ ДоговорВзаиморасчетов.Пустая()) И
			 (ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.СКомиссионером) Тогда
			Если Автомобили.Количество()>0 Тогда
				Если Вопрос("Перерассчитать сумму комиссионного вознаграждения?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
					//Пройдемся по ТЧ и рассчитаем сумму комиссионного вознаграждения
					Для Каждого СтрокаТЧ Из Автомобили Цикл
						ОбработкаРеквизита("РассчитатьВознаграждение",СтрокаТЧ);
					КонецЦикла;
					Для Каждого СтрокаТЧ Из Товары Цикл
						ОбработкаРеквизита("РассчитатьВознаграждение",СтрокаТЧ);
					КонецЦикла;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		#КонецЕсли
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	АвтомобилиОтданныеОстатки.ДокументПередачи
		                      |ИЗ
		                      |	РегистрНакопления.АвтомобилиОтданные.Остатки(&НаМомент) КАК АвтомобилиОтданныеОстатки
		                      |ГДЕ
		                      |	АвтомобилиОтданныеОстатки.Автомобиль = &Автомобиль
		                      |	И АвтомобилиОтданныеОстатки.Контрагент = &Контрагент
		                      |	И АвтомобилиОтданныеОстатки.ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов");
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),Новый Граница(Дата,ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("Автомобиль",ТекСтрока.Автомобиль);
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекСтрока.ДокументПередачи=Выборка.ДокументПередачи;
		Иначе
			ТекСтрока.ДокументПередачи=Неопределено;
		КонецЕсли;
		Рез=ОбработкаРеквизита("Автомобили.ДокументПередачи",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобили.Количество" ИЛИ Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
		Иначе
			//НДС в цену не включен - Получим сумму НДС
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		Если ТекСтрока.Количество<>0 Тогда
			ТекСтрока.Цена=ТекСтрока.Сумма/ТекСтрока.Количество;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		//Получим новую сумму НДС как процент от суммы всего
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			ТекСтрока.Сумма=ТекСтрока.СуммаВсего;
		Иначе
			//НДС в цену не включен
			ТекСтрока.Сумма=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.ДокументПередачи" Тогда
		Рез=Истина;
		Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДокументПередачи) Тогда
			СтруктураОтбора=Новый Структура("Автомобиль");
			СтруктураОтбора.Автомобиль=ТекСтрока.Автомобиль;
			ПоискПоДокументуПередачи=ТекСтрока.ДокументПередачи.Автомобили.НайтиСтроки(СтруктураОтбора);
			Если ПоискПоДокументуПередачи.Количество()>0 Тогда
				Если ТипЗнч(ТекСтрока.ДокументПередачи)=Тип("ДокументСсылка.ИнвентаризацияАвтомобилей") Тогда
					СуммаПередачи=ПоискПоДокументуПередачи[0].Сумма;
				Иначе
					СуммаПередачи=ПоискПоДокументуПередачи[0].СуммаВсего;
				КонецЕсли;
				Если ТипЗнч(ТекСтрока.ДокументПередачи)=Тип("ДокументСсылка.ВводОстатковАвтомобилей") Тогда
					СтруктураОтбора=Новый Структура("ИдентификаторАвтомобиля",ПоискПоДокументуПередачи[0].ИдентификаторАвтомобиля);
					ПоискПоДокументуПередачи=ТекСтрока.ДокументПередачи.Опции.НайтиСтроки(СтруктураОтбора);
					Для каждого СтрокаОпций Из ПоискПоДокументуПередачи Цикл
						СуммаПередачи=СуммаПередачи+СтрокаОпций.СуммаВсего;
					КонецЦикла; 
				КонецЕсли; 
				СуммаПередачи=обПересчет(СуммаПередачи,ТекСтрока.ДокументПередачи.ВалютаДокумента,ТекСтрока.ДокументПередачи.КурсДокумента,ВалютаДокумента,КурсДокумента);
				Если ТекСтрока.СуммаВсего<>СуммаПередачи Тогда
					ТекСтрока.СуммаВсего=СуммаПередачи;
					Рез=ОбработкаРеквизита("Автомобили.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		ДобавитьОборудованиеАвтомобиля(ТекСтрока.Автомобиль,ТекСтрока.ДокументПередачи,Истина);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ПолучитьРасчетноеКоличество" Тогда
		//Получение расчетного количество
		СтруктураОтбора=Новый Структура("Контрагент,ДоговорВзаиморасчетов,Автомобиль");
		СтруктураОтбора.Контрагент=Контрагент;
		СтруктураОтбора.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		СтруктураОтбора.Автомобиль=ТекСтрока.Автомобиль;
		МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
		ТаблицаОстатков=РегистрыНакопления.АвтомобилиОтданные.Остатки(МоментВремени,СтруктураОтбора,,"Количество");
		Если ТаблицаОстатков.Количество()>0 Тогда
			ТекСтрока.Количество=ТаблицаОстатков.Итог("Количество");
		Иначе
			ТекСтрока.Количество=1;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Автомобили.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="РассчитатьВознаграждение" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда     
			Если не ДоговорВзаиморасчетов.ПроцентКомиссионногоВознаграждения = 0 тогда  //ЧалапАю БизТех 13.02.2025
				
			иначе
				ТекСтрока.Вознаграждение=(ТекСтрока.СуммаВсего*ДоговорВзаиморасчетов.ПроцентКомиссионногоВознаграждения)/100;
			КонецЕсли;
			//<<ЧалапАю БизТех 13.02.2025
			//ТекСтрока.Вознаграждение = (ТекСтрока.СуммаПродажи - ТекСтрока.Сумма);      //тут нет суммы продажи
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДСРасчетная;
			ТекСтрока.СуммаНДС = (ТекСтрока.Вознаграждение*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка);
			//ЧалапАю БизТех 13.02.2025>>
		Иначе
			ТекСтрока.Вознаграждение=0;
		КонецЕсли;
		Возврат Истина;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		//А вот тут мы загрузим те цены, по которым оборудование было отдано на комиссию
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ПартииТоваровОтданные.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
		|ГДЕ
		|	ПартииТоваровОтданные.ВидДвижения = &ВидДвиженияПриход
		|	И ПартииТоваровОтданные.ДокументПередачи = &ДокументПередачи
		|	И ПартииТоваровОтданные.Номенклатура = &Номенклатура
		|	И ПартииТоваровОтданные.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";
		Запрос.УстановитьПараметр("ВидДвиженияПриход",ВидДвиженияНакопления.Приход);
		//Найдем документ, согласно которого был продан автомобиль
		СтрокаАвтомобиля=Автомобили.Найти(ТекСтрока.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаАвтомобиля=Неопределено Тогда
			Запрос.УстановитьПараметр("ДокументПередачи",Неопределено);
		Иначе
			Запрос.УстановитьПараметр("ДокументПередачи",СтрокаАвтомобиля.ДокументПередачи);
		КонецЕсли; 
		Запрос.УстановитьПараметр("Номенклатура",ТекСтрока.Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
			Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
				СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
				КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Иначе
				КурсУпр        = КурсВалютыУпр;
			КонецЕсли;
			ТекСтрока.СуммаВсего=обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Лев(Имя,7)="Товары." Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ОтчетКомиссионераЗаАвтомобили"
// Возвращает сформированный табличный документ:
Функция ПечатьОтчетКомиссионераЗаАвтомобили(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ОтчетКомиссионераЗаАвтомобили");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеКомитента		= спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.ПредставлениеКомиссионера	= спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//подготовим дополнительную таблицу
	ТаблицаТоваровПоАвтомобилям = ЭтотОбъект.Товары.Выгрузить();
	ТаблицаТоваровПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка строки ТЧ
		СтрокаТабличнойЧастиТовары = ТаблицаТоваровПоАвтомобилям.Найти(СтрокаТабличнойЧасти.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиТовары <> Неопределено Тогда
			СтрокаТабличнойЧасти.Сумма		= СтрокаТабличнойЧасти.Сумма		+ СтрокаТабличнойЧастиТовары.Сумма;
			СтрокаТабличнойЧасти.СуммаНДС	= СтрокаТабличнойЧасти.СуммаНДС		+ СтрокаТабличнойЧастиТовары.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаВсего	= СтрокаТабличнойЧасти.СуммаВсего	+ СтрокаТабличнойЧастиТовары.СуммаВсего;
		КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("Вознаграждение", Формат(СтрокаТабличнойЧасти.Вознаграждение,ФорматВыводаСуммы));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ-12"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.ПодразделениеКомпании, СтруктураПредставления2, ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикОрганизация",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = ""; 
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы", Права,,ЭтотОбъект);
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	// в новой ПФ не выводится
	ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ВыборкаСтрок = ЭтотОбъект.Автомобили;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	
	// инициализация итогов по странице
	ИтогоКоличествоНаСтранице = 0;
	ИтогоСуммаНаСтранице      = 0;
	ИтогоНДСНаСтранице        = 0;
	ИтогоСуммаСНДСНаСтранице  = 0;

	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		// Для переноса последней страницы необходимо посчитать количество итогов по НДС
		СуммаВсегоАвтомобиля=СтрокаТоваров.СуммаВсего;
		СуммаНДСАвтомобиля=СтрокаТоваров.СуммаНДС;
		Для каждого СтрокаТЧ Из Товары Цикл
			Если СтрокаТЧ.ИдентификаторАвтомобиля=СтрокаТоваров.ИдентификаторАвтомобиля Тогда
				СуммаВсегоАвтомобиля=СуммаВсегоАвтомобиля+СтрокаТЧ.СуммаВсего;
				СуммаНДСАвтомобиля=СуммаНДСАвтомобиля+СтрокаТЧ.СуммаНДС;
			КонецЕсли;
			// итоги по ставкам НДС
			Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.СтавкаНДС) Тогда
				Если СоответствиеИтоговПоставкамНДС[СтрокаТЧ.СтавкаНДС]=Неопределено Тогда
					СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТЧ.СтавкаНДС,0);
				КонецЕсли;
				СоответствиеИтоговПоставкамНДС[СтрокаТЧ.СтавкаНДС]=СоответствиеИтоговПоставкамНДС[СтрокаТЧ.СтавкаНДС]+СтрокаТЧ.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС]=Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС]=СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС]+СуммаНДСАвтомобиля;
		КонецЕсли;
		
		Если ИспользоватьРазбиениеНаСтраницы И КоличествоСтрокНаТекущейСтранице>0 Тогда
			// Проверим, помещаются ли нужные области на странице
			Если СтрокаТоваров.НомерСтроки = КоличествоСтрок Тогда
				// только на последнем шаге, когда нужно вывести все
				МассивОбластей = Новый Массив();
				МассивОбластей.Добавить(ОбластьСтрока);
				МассивОбластей.Добавить(ОбластьИтоги);
				МассивОбластей.Добавить(ОбластьВсего);
				//Для Каждого СтрокаИтоговНДС Из СоответствиеИтоговПоставкамНДС Цикл
				//	МассивОбластей.Добавить(ОбластьВсегоСтавкаНДС);				
				//КонецЦикла;
				МассивОбластей.Добавить(ОбластьПодвал);
			Иначе
				МассивОбластей = Новый Массив();			
				МассивОбластей.Добавить(ОбластьСтрока);
				МассивОбластей.Добавить(ОбластьИтоги);
			КонецЕсли; 
			Попытка
				Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
					ОбластьИтоги.Параметры.ИтогКоличествоПоСтранице = ИтогоКоличествоНаСтранице;
					ОбластьИтоги.Параметры.ИтогСуммыПоСтранице      = ИтогоСуммаНаСтранице;
					ОбластьИтоги.Параметры.ИтогНДСПоСтранице        = ИтогоНДСНаСтранице;
					ОбластьИтоги.Параметры.ИтогСуммыСНДСПоСтранице  = ИтогоСуммаСНДСНаСтранице;
					ТабДокумент.Вывести(ОбластьИтоги);

					// очистим итоги по странице
					ИтогоКоличествоНаСтранице = 0;
					ИтогоСуммаНаСтранице      = 0;
					ИтогоНДСНаСтранице        = 0;
					ИтогоСуммаСНДСНаСтранице  = 0;

					НомерСтраницы = НомерСтраницы + 1;
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
					ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
					КоличествоСтрокНаТекущейСтранице=0;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;

		//заполняем данные строки
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.Номенклатура=СтрокаТоваров.Автомобиль;
		ОбластьСтрока.Параметры.ТоварНаименование=спПолучитьНаименование(СтрокаТоваров.Автомобиль);
		ОбластьСтрока.Параметры.Код = СтрокаТоваров.Автомобиль.VIN;
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование="шт";
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ="шт";
		ОбластьСтрока.Параметры.КоличествоВОдномМесте=1;
		ОбластьСтрока.Параметры.КоличествоМест=1;
		ОбластьСтрока.Параметры.СтавкаНДС = СтрокаТоваров.СтавкаНДС;
		
		// Выполним перерасчет суммы и цены по регламентированной валюте
		СуммаВсегоАвтомобиля = Окр(обПересчет(СуммаВсегоАвтомобиля, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДСАвтомобиля   = Окр(обПересчет(СуммаНДСАвтомобиля, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		
		СуммаАвтомобиляБезНДС=СуммаВсегоАвтомобиля - СуммаНДСАвтомобиля;
		ОбластьСтрока.Параметры.ЦенаБезНДС = СуммаАвтомобиляБезНДС;
		ОбластьСтрока.Параметры.СуммаБезНДС = СуммаАвтомобиляБезНДС;
		ОбластьСтрока.Параметры.СуммаНДС = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", СуммаНДСАвтомобиля);
		ОбластьСтрока.Параметры.СуммаВсего = СуммаВсегоАвтомобиля;
		
		ТабДокумент.Вывести(ОбластьСтрока);
		КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;

		// увеличим итоги по странице
		ИтогоКоличествоНаСтранице = ИтогоКоличествоНаСтранице + СтрокаТоваров.Количество;
		ИтогоСуммаНаСтранице      = ИтогоСуммаНаСтранице      + СуммаАвтомобиляБезНДС;
		ИтогоНДСНаСтранице        = ИтогоНДСНаСтранице        + СуммаНДСАвтомобиля;
		ИтогоСуммаСНДСНаСтранице  = ИтогоСуммаСНДСНаСтранице  + СуммаВсегоАвтомобиля;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаАвтомобиляБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДСАвтомобиля;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсегоАвтомобиля;
		
	КонецЦикла;
	
	// Выводим итоги по последней странице
	ОбластьИтоги.Параметры.ИтогКоличествоПоСтранице = ИтогоКоличествоНаСтранице;
	ОбластьИтоги.Параметры.ИтогСуммыПоСтранице      = ИтогоСуммаНаСтранице;
	ОбластьИтоги.Параметры.ИтогНДСПоСтранице        = ИтогоНДСНаСтранице;
	ОбластьИтоги.Параметры.ИтогСуммыСНДСПоСтранице  = ИтогоСуммаСНДСНаСтранице;
	ТабДокумент.Вывести(ОбластьИтоги);
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = ВыборкаСтрок.Итог("Количество");
	ОбластьВсего.Параметры.ИтогСуммы      = ИтогоСумма;
	ОбластьВсего.Параметры.ИтогНДС        = ИтогоНДС;
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = ИтогоСуммаСНДС;
	ТабДокумент.Вывести(ОбластьВсего);
	
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "+СокрЛП(ЭлементСоответствия.Ключ);
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
	//	ТабДокумент.Вывести(ОбластьВсегоСтавкаНДС);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьМакета = ОбластьПодвал;
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьМакета.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьМакета.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьМакета.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьМакета.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьМакета.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьМакета.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
   	Товары.Очистить();
	// при вводе на основании реализации автомобилей заполним документ передачи
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И 
		(Основание.ХозОперация=Справочники.ХозОперации.РеализацияАвтомобилейКомиссия ИЛИ
		 Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковАвтомобилейПереданныхНаКомиссию) Тогда
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			СтрокаТЧ.ДокументПередачи=Основание;
			ОбработкаРеквизита("Автомобили.ДокументПередачи",СтрокаТЧ);
		КонецЦикла;
	КонецЕсли;
	//Пройдемся по ТЧ и рассчитаем сумму комиссионного вознаграждения
	Для Каждого СтрокаТЧ Из Автомобили Цикл
		ОбработкаРеквизита("РассчитатьВознаграждение",СтрокаТЧ);
	КонецЦикла;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;         
	
	
	//<<ЧалапАю БизТех 13.02.2025
	тзАвтомобили = Автомобили.Выгрузить();  
	Автомобили.Очистить();
	
	Для каждого Стр из тзАвтомобили цикл   
		//ДатаПродажи = НайдемДатуПродажи(Стр.Автомобиль,Стр.ДокументПередачи.Дата);    
		ДанныеПродажи3Лицу = БТ_СлужебныеФункции.НайдемДатуПродажи(Стр.Автомобиль,Стр.ДокументПередачи.Дата);      
		
		ЗапросПостКомиссии = Новый Запрос( "ВЫБРАТЬ ПЕРВЫЕ 1
		                       |	ПоступлениеАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		                       |	ПоступлениеАвтомобилейАвтомобили.Сумма КАК Сумма,
		                       |	ПоступлениеАвтомобилейАвтомобили.СтавкаНДС КАК СтавкаНДС,
		                       |	ПоступлениеАвтомобилейАвтомобили.СуммаНДС КАК СуммаНДС,
		                       |	МИНИМУМ(ПоступлениеАвтомобилейАвтомобили.Ссылка.Дата) КАК Дата
		                       |ИЗ
		                       |	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
		                       |ГДЕ
		                       |	ПоступлениеАвтомобилейАвтомобили.Ссылка.Дата > &Дата
		                       |	И ПоступлениеАвтомобилейАвтомобили.Автомобиль = &Автомобиль
		                       |	И ПоступлениеАвтомобилейАвтомобили.Ссылка.Проведен
		                       |
		                       |СГРУППИРОВАТЬ ПО
		                       |	ПоступлениеАвтомобилейАвтомобили.Автомобиль,
		                       |	ПоступлениеАвтомобилейАвтомобили.Сумма,
		                       |	ПоступлениеАвтомобилейАвтомобили.СтавкаНДС,
		                       |	ПоступлениеАвтомобилейАвтомобили.СуммаНДС");
		ЗапросПостКомиссии.УстановитьПараметр("Дата",Стр.ДокументПередачи.Дата);
		ЗапросПостКомиссии.УстановитьПараметр("Автомобиль",Стр.Автомобиль);  
		
		//ЗапросПостКомиссии = новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		//                                  |	ПоступлениеАвтомобилейАвтомобили.Ссылка КАК Ссылка,
		//                                  |	ПоступлениеАвтомобилейАвтомобили.Цена КАК Цена
		//                                  |ИЗ
		//                                  |	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
		//                                  |ГДЕ
		//                                  |	ПоступлениеАвтомобилейАвтомобили.Ссылка.Проведен
		//                                  |	И ПоступлениеАвтомобилейАвтомобили.Ссылка.Организация = &Организация
		//                                  |	И ПоступлениеАвтомобилейАвтомобили.Автомобиль = &Автомобиль
		//                                  |
		//                                  |УПОРЯДОЧИТЬ ПО
		//                                  |	ПоступлениеАвтомобилейАвтомобили.Ссылка.Дата УБЫВ");
		//ЗапросПостКомиссии.УстановитьПараметр("Организация",Организация);
		//ЗапросПостКомиссии.УстановитьПараметр("Автомобиль",Стр.Автомобиль);    
		//Выборка = ЗапросПостКомиссии.Выполнить().Выбрать(); 
		//Если Выборка.Следующий() Тогда
		//	СуммаПоступленияКомисии = Выборка.Цена;
		//иначе
		//	СуммаПоступленияКомисии = 0;
		//КонецЕсли;
		
		
		РезультатПостКомиссии = ЗапросПостКомиссии.Выполнить().Выгрузить();
		если  РезультатПостКомиссии.Количество() = 0 тогда
			СуммаПоступленияКомисии = 0
		иначе
			СуммаПоступленияКомисии = РезультатПостКомиссии[0].Сумма;
		КонецЕсли;
		//СуммаПоступленияКомисии = Стр.ДокументПередачи.Автомобили.Найти( Стр.Автомобиль, "Автомобиль").СуммаВсего;
		если не ДанныеПродажи3Лицу = Неопределено тогда
			ДатаПродажи = ДанныеПродажи3Лицу.Дата;
			СуммаПродажи3Лицу = ДанныеПродажи3Лицу.СуммаПродажи3Лицу;
		//Если не ДатаПродажи = Неопределено тогда
			НайденныйОтчетКомитента = БТ_СлужебныеФункции.НайтиОтчетКомитента(Стр.Автомобиль, Стр.ДокументПередачи.Дата);   
			НайденныйОтчетКомиссионера = БТ_СлужебныеФункции.НайтиОтчетКомиссионера(Стр.Автомобиль, Стр.ДокументПередачи.Дата);
			Если не НайденныйОтчетКомитента = Неопределено           //добавляем авто только если есть отчет комитенту
				и НайденныйОтчетКомиссионера = Неопределено тогда     //и чтобы не было в других отчетах комисиионера
				НоваяСтрока = Автомобили.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);  
				НоваяСтрока.Сумма = СуммаПродажи3Лицу;
				НоваяСтрока.СуммаВсего = СуммаПродажи3Лицу;
				НоваяСтрока.Цена = СуммаПродажи3Лицу;  
				НоваяСтрока.Вознаграждение = СуммаПродажи3Лицу- СуммаПоступленияКомисии;
				НоваяСтрока.СуммаНДС = окр(НоваяСтрока.Вознаграждение*НоваяСтрока.СтавкаНДС.Ставка/(100+НоваяСтрока.СтавкаНДС.Ставка),2);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//ЧалапАю БизТех 13.02.2025>>
КонецПроцедуры     

//Функция НайдемДатуПродажи(Автомобиль, ДатаПередачиНаКомиссию)  //ЧалапАю БизТех 13.02.2025 
//	ПараметрыАвто = новый Структура;
//	
//	Запрос = Новый Запрос("ВЫБРАТЬ
//	                      |	РеализацияАвтомобилейАвтомобили.Ссылка КАК Ссылка,
//	                      |	РеализацияАвтомобилейАвтомобили.Ссылка.Дата КАК Дата,
//	                      |	РеализацияАвтомобилейАвтомобили.Сумма КАК СуммаПродажи3Лицу
//	                      |ИЗ
//	                      |	Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
//	                      |ГДЕ
//	                      |	РеализацияАвтомобилейАвтомобили.Автомобиль = &Автомобиль
//	                      |	И РеализацияАвтомобилейАвтомобили.Ссылка.Проведен
//	                      |	И РеализацияАвтомобилейАвтомобили.Ссылка.Дата > &Дата");   //дата продажи 3 лицу больше чем дата передачи на комиссию
//	Запрос.УстановитьПараметр("Дата", ДатаПередачиНаКомиссию);
//	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);  
//	Результат = Запрос.Выполнить().Выбрать();
//	Если Результат.Следующий() тогда    
//		ПараметрыАвто.Вставить("Дата",Результат.Дата);
//		ПараметрыАвто.Вставить("СуммаПродажи3Лицу",Результат.СуммаПродажи3Лицу);
//		Возврат ПараметрыАвто; //Результат.Дата
//	иначе
//		Возврат Неопределено
//	КонецЕсли;  
//КонецФункции

//Функция НайтиОтчетКомитента(Автомобиль, ДатаПродажи3Лицу)   //ЧалапАю БизТех 13.02.2025
//	Запрос = Новый Запрос("ВЫБРАТЬ
//	                      |	ОтчетКомитентуЗаАвтомобилиАвтомобили.Ссылка КАК Ссылка
//	                      |ИЗ
//	                      |	Документ.ОтчетКомитентуЗаАвтомобили.Автомобили КАК ОтчетКомитентуЗаАвтомобилиАвтомобили
//	                      |ГДЕ
//	                      |	ОтчетКомитентуЗаАвтомобилиАвтомобили.Ссылка.Проведен
//	                      |	И ОтчетКомитентуЗаАвтомобилиАвтомобили.Ссылка.Дата > &Дата
//	                      |	И ОтчетКомитентуЗаАвтомобилиАвтомобили.Автомобиль = &Автомобиль");
//	Запрос.УстановитьПараметр("Дата", ДатаПродажи3Лицу);
//	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
//	Результат = Запрос.Выполнить().Выбрать();
//	Если Результат.Следующий() тогда
//		Возврат Результат.Ссылка
//	иначе
//		Возврат Неопределено
//	КонецЕсли;
//КонецФункции


Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	////Обновим оборудование автомобилей
	//Для каждого СтрокаАвтомобилей Из Автомобили Цикл
	//	ДобавитьОборудованиеАвтомобиля(СтрокаАвтомобилей.Автомобиль,СтрокаАвтомобилей.ДокументПередачи,Ложь);
	//КонецЦикла; 
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И НЕ ЭтоНовый() И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		// прошлый момент времени
		Запрос = Новый Запрос("ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
	КонецЕсли;
	
	// подсчитаем сумму вознаграждения
	СуммаВознаграждения=Автомобили.Итог("Вознаграждение")+Товары.Итог("Вознаграждение");
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проведем взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
	НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента-СуммаВознаграждения;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	//Отданные на комиссию автомобили
	НаборЗаписейАвтомобилиОтданные=Движения.АвтомобилиОтданные;
	НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
	НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	НаборЗаписейАвтомобилиОтданные.ОтчетКомиссионера=Истина;
	Отказ=НЕ НаборЗаписейАвтомобилиОтданные.Расход() ИЛИ Отказ; 
	// продажи автомобилей
	Если НЕ Отказ Тогда
		НаборЗаписейПродажи=Движения.ПродажиАвтомобилей;
		НаборЗаписейПродажи.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПродажи.СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка();
		НаборЗаписейПродажи.ДокументПродажи=Ссылка;
		НаборЗаписейПродажи.Сторно=Ложь;
		НаборЗаписейПродажи.Покупатель=Контрагент;
		НаборЗаписейПродажи.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		НаборЗаписейПродажи.ПодразделениеКомпании=ПодразделениеКомпании;
		НаборЗаписейПродажи.Комиссия=Истина;
		Отказ=НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
	КонецЕсли;
	Если НЕ Отказ Тогда
		НаборЗаписейАвтомобилиОтданные.Записать();
	КонецЕсли; 
	
	// партии
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

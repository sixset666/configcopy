Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЕстьNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	                      |ИЗ
	                      |	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Дата, ДоговорВзаиморасчетов = &Договор) КАК ВзаиморасчетыКомпанииОстатки");
	//Запрос.УстановитьПараметр("Дата", МоментВремени());     
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Запрос.УстановитьПараметр("Договор", ДоговорВзаиморасчетов);
	
	ОстатокДолга = СуммаДокумента;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОстатокДолга = ОстатокДолга + Выборка.СуммаОстаток;
	КонецЦикла;	 
	
	ЭМ_ПодписалОплату = обПолучитьЗначениеСвойства(ЭтотОбъект, ПланыВидовХарактеристик.СвойстваОбъектов.ЭМ_ПодписалОплату,Справочники.Пользователи.ПустаяСсылка());
	Если ЗначениеЗаполнено(ЭМ_ПодписалОплату) Тогда
		ОплатаПодписана = Истина;
	Иначе
		ОплатаПодписана = Ложь;
	КонецЕсли;	
	Если ОстатокДолга > 0  и Не ОплатаПодписана и не РольДоступна("ВосстПослед") Тогда  
		Сообщить("Долг по договору составляет " + ОстатокДолга + ". 
			|Необходимо подписать оплату.",СтатусСообщения.Важное);  
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БТ_ПредоплаченноеТО.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.БТ_ПредоплаченноеТО КАК БТ_ПредоплаченноеТО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БТ_ПакетыПредоплаченногоТО.ТаблицаТО КАК БТ_ПакетыПредоплаченногоТОТаблицаТО
		|		ПО БТ_ПредоплаченноеТО.ПакетПредоплаченногоТО = БТ_ПакетыПредоплаченногоТОТаблицаТО.Ссылка
		|ГДЕ
		|	БТ_ПредоплаченноеТО.Проведен
		|	И БТ_ПредоплаченноеТО.Ссылка <> &Ссылка
		|	И БТ_ПредоплаченноеТО.Контрагент = &Контрагент
		|	И БТ_ПредоплаченноеТО.Автомобиль = &Автомобиль
		|	И БТ_ПакетыПредоплаченногоТОТаблицаТО.НомерТО В(&НомерТО)
		|
		|СГРУППИРОВАТЬ ПО
		|	БТ_ПредоплаченноеТО.Ссылка";
	
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("НомерТО", ПакетПредоплаченногоТО.ТаблицаТО.ВыгрузитьКолонку("НомерТО"));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	СписокДокументов = "";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокДокументов = СписокДокументов + ?(ПустаяСтрока(СписокДокументов),"",Символы.ПС) + ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Если Не ПустаяСтрока(СписокДокументов) Тогда 
		Сообщить("На данный автомобиль и номеру ТО из пакета уже существует ранее выданный пакет ТО.
			| " + СписокДокументов); 
		
		Отказ = Истина;
	КонецЕсли;	
	
    Запрос = Новый Запрос("ВЫБРАТЬ
                          |	БТ_ПакетыПредоплаченногоТОТаблицаТО.НомерТО КАК НомерТО,
                          |	БТ_ПакетыПредоплаченногоТОТаблицаТО.НомерСтроки КАК НомерСтроки
                          |ПОМЕСТИТЬ ВТ_НомераТО
                          |ИЗ
                          |	Справочник.БТ_ПакетыПредоплаченногоТО.ТаблицаТО КАК БТ_ПакетыПредоплаченногоТОТаблицаТО
                          |ГДЕ
                          |	БТ_ПакетыПредоплаченногоТОТаблицаТО.Ссылка = &СсылкаПакет
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |ВЫБРАТЬ
                          |	ВТ_НомераТО.НомерТО КАК НомерТО,
                          |	ЗаявкаНаРемонт.Ссылка КАК Ссылка,
                          |	ВТ_НомераТО.НомерСтроки КАК НомерСтроки
                          |ИЗ
                          |	ВТ_НомераТО КАК ВТ_НомераТО
                          |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
                          |		ПО ВТ_НомераТО.НомерТО = ЗаявкаНаРемонт.БТ_НомерТО
                          |			И (НЕ ЗаявкаНаРемонт.ПометкаУдаления)
                          |			И (ЗаявкаНаРемонт.Автомобиль = &Автомобиль)
                          |			И (ЗаявкаНаРемонт.Заказчик = &Заказчик)
                          |			И (ЗаявкаНаРемонт.ДокументОснование ССЫЛКА Документ.БТ_ПредоплаченноеТО)
                          |ГДЕ
                          |	(ЗаявкаНаРемонт.Ссылка ЕСТЬ NULL
                          |			ИЛИ ЗаявкаНаРемонт.ДокументОснование <> &Ссылка)"); 
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СсылкаПакет", ПакетПредоплаченногоТО);
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Запрос.УстановитьПараметр("Заказчик", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			Сообщить("По ТО "+Выборка.НомерТО+" уже создан документ " +Выборка.Ссылка);
			Продолжить;
		КонецЕсли;	
		
		СоздатьЗаявкуНаРемонт(Выборка.НомерТО, Выборка.НомерСтроки);
	КонецЦикла; 
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	//Запрос = Новый Запрос;
	//Запрос.Текст = 
	//	"ВЫБРАТЬ
	//	|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
	//	|	-ВзаиморасчетыКомпанииОстатки.СуммаОстаток КАК СуммаОстаток
	//	|ИЗ
	//	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Дата, ДоговорВзаиморасчетов = &Договор) КАК ВзаиморасчетыКомпанииОстатки";
	//
	//Запрос.УстановитьПараметр("Дата", МоментВремени());
	//Запрос.УстановитьПараметр("Договор", ДоговорВзаиморасчетов);
	//
	//РезультатЗапроса = Запрос.Выполнить();
	//
	//ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	//
	//Движения.ВзаиморасчетыКомпании.Записывать = Истина;
	//ОсталосьРаспределить = СуммаДокумента;
	//Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	//	Если ОсталосьРаспределить <=0 Тогда 
	//		Прервать;
	//	КонецЕсли;
	//	
	//	НовоеДвижение = Движения.ВзаиморасчетыКомпании.ДобавитьПриход();
	//	НовоеДвижение.Период = Дата;
	//	НовоеДвижение.Контрагент = Контрагент; 
	//	НовоеДвижение.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
	//	НовоеДвижение.Сделка = ВыборкаДетальныеЗаписи.Сделка;
	//	
	//	Сумма = Мин(ОсталосьРаспределить, ВыборкаДетальныеЗаписи.СуммаОстаток);
		
	//	НовоеДвижение.Сумма = Сумма; 
	//	НовоеДвижение.СуммаБаз = Сумма;
	//	НовоеДвижение.СуммаУпр = Сумма; 
	//	
	//	ОсталосьРаспределить = ОсталосьРаспределить - Сумма;
	//КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

КонецПроцедуры

Процедура СоздатьЗаявкуНаРемонт(НомерТО, НомерСтроки) 
	
	Док = Документы.ЗаявкаНаРемонт.СоздатьДокумент();
	док.Дата = ТекущаяДатаСеанса();
	Док.ХозОперация = Справочники.ХозОперации.ПланРемонта;
	Док.Контрагент = Контрагент;   
	Док.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
	Док.Автомобиль = Автомобиль;
	Док.ДокументОснование = Ссылка;
	Док.БТ_НомерТО = НомерТО;   
	док.ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер, ТекущаяДата());
	
	Док.ПодразделениеКомпании = Подразделение;
	Док.Организация = Организация;
	Док.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	Док.Автор = Автор;
	Док.VIN = АВтомобиль.VIN;
	Док.Цех = НайтиЦех();
	Док.ВидРемонта = Справочники.ВидыРемонта.НайтиПоНаименованию("Техническое обслуживание (ТО)");
	Док.ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
	Док.ТипЦенРабот = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
	Док.КурсДокумента = 1;
	Док.КурсВалютыУпр = 1;
	Док.Заказчик = Контрагент;
	Док.УстановитьНовыйНомер(); 
	Док.Мастер = Автор.Сотрудник;   
	Док.ДатаНачала = ДобавитьМесяц(Док.Дата, ?(ПакетПредоплаченногоТО.ТаблицаТО.Количество() = 0, 0, Цел(ПакетПредоплаченногоТО.СрокДействия * НомерСтроки/ПакетПредоплаченногоТО.ТаблицаТО.Количество())));
	Док.ДатаОкончания = Док.ДатаНачала + 1; 
	Док.ПричинаОбращения = НомерТО;
	Док.Записать();
	
	Сообщить("Создан документ " + Док+" ("+НомерТО+")");
КонецПроцедуры	 

Функция НайтиЦех()
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Цеха.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.Цеха КАК Цеха
	                      |ГДЕ
	                      |	Цеха.Подразделение = &Подразделение
	                      |	И Цеха.Организация = &Организация
	                      |	И Цеха.Родитель <> &Родитель");	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Родитель", Справочники.Цеха.НайтиПоКоду("ЦБ000093"));   //цех не находит
	
	ВЫборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;	
КонецФункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если не ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		КурсДокумента = 1;
		КурсВалютыУпр = 1;
	КонецЕсли;	 
	
	Если Не ОбПраво("ОформлениеПакетовИЦенПредоплаченногоТО") Тогда 
		Сообщить("Нет доступа");
		Отказ = Истина;
	КонецЕсли; 
	
	ХозОперация = Справочники.ХозОперации.ПредоплаченноеТО;
	
	Если Товары.Количество() = 0 Тогда
		НоваяСтрока = Товары.Добавить();
		Если не Справочники.Номенклатура.НайтиПоНаименованию("Предоплаченное ТО") = Неопределено тогда 
			НоваяСтрока.Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию("Предоплаченное ТО")
		иначе
			Сообщить("В спрваочнике Номенклатура не найдена номенклатура с наименованием Предоплаченное ТО");
			Возврат
		КонецЕсли;
	Иначе     
		НоваяСтрока = Товары[0];
	КонецЕсли;	
		
	НоваяСтрока.Количество = 1;
	НоваяСтрока.Коэффициент = 1;
	
	НоваяСтрока.Цена = СуммаДокумента;
	НоваяСтрока.Сумма = СуммаДокумента;
	НоваяСтрока.СуммаВсего = СуммаДокумента; 
	НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
	НоваяСтрока.ЕдиницаИзмерения = НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;

	
КонецПроцедуры

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("Подразделение",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

Процедура ПриКопировании(ОбъектКопирования)
	ДополнительныеСвойства.Вставить("Копирование", Истина);
КонецПроцедуры


Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;							// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ВводИзАРМРесепшен;						// Переменная хранит флаг, был ли введен данный документ из АРМ Ресепшен
Перем РегистрацияЗвонковИКонтактов;				// Переменная хранит структуру при вводе на основании из АРМ Ресепшн
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
	ОбязательныеРеквизиты.Вставить("Тема", 1);
	ОбязательныеРеквизиты.Вставить("ВидСобытия", 1);
	ОбязательныеРеквизиты.Вставить("Приоритет", 1);
	ОбязательныеРеквизиты.Вставить("Состояние", 1);
	ОбязательныеРеквизиты.Вставить("ТипСообщения", 1);
	ОбязательныеРеквизиты.Вставить("ДатаНачала", 1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Если Состояние = Перечисления.СостоянияСобытий.Отменено Тогда
		ОбязательныеРеквизиты.Вставить("ПричинаОтмены", 1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	// проверим что Дата окончания не меньше даты начала
	Если НЕ обЗначениеНеЗаполнено(ДатаОкончания) И ДатаОкончания < ДатаНачала Тогда
		дкДобавитьОшибку(Ошибки,"Дата окончания события не может быть меньше даты начала!");
		Возврат Ложь;	
	КонецЕсли;
	
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
КонецФункции // ПроверитьКорректность()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Событие", "Событие", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Рез=Истина;
	
	//ОБРАБОТКА ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
		
	Если Имя="ДатаНачала" Тогда
		Если ДатаОкончания<=ДатаНачала Тогда
			ДатаОкончания = ДатаНачала + 600;
		КонецЕсли;
		
	ИначеЕсли Найти(Имя,"Товары.")=1 Тогда
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитатьЦену",Ложь);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
		
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		//Проставим количество
		Если ТекСтрока.Количество<>1 Тогда
			ТекСтрока.Количество=1;
		КонецЕсли;
		
		// заметки
		#Если Клиент Тогда
			дкСообщитьОЗаметках(ЭтотОбъект,ТекСтрока.Автомобиль,ЭтаФорма);
		#КонецЕсли
		
	Иначе 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьСобытие(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("Событие");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаДокумента");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если Пользователи.Количество() > 0 Тогда
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("ПустаяСтрока"));
		Область = Макет.ПолучитьОбласть("СтрокаПользователи");
		Для Каждого ТекСтрокаПользователи Из Пользователи Цикл
			Если ТекСтрокаПользователи.НомерСтроки = 1 Тогда
				Область.Параметры.НазваниеСтроки = "Пользователи:";
			Иначе
				Область.Параметры.НазваниеСтроки = " ";
			КонецЕсли;
			Область.Параметры.СтрокаПользователей = ТекСтрокаПользователи.Пользователь;
			ТабДокумент.Вывести(Область);
		КонецЦикла;
	КонецЕсли;	
	
	Если СторонниеЛица.Количество() > 0 Тогда
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаСторонниеЛица"));
		Область = Макет.ПолучитьОбласть("СтрокаСторонниеЛица");
		Для Каждого ТекСтрокаСторонниеЛица Из СторонниеЛица Цикл
			Если  обЗначениеНеЗаполнено(ТекСтрокаСторонниеЛица.Контрагент)Тогда
				Область.Параметры.Контрагент = " ";
				Область.Параметры.КонтрагентСсылка = Неопределено;
			Иначе
				Область.Параметры.Контрагент = спПолучитьНаименование(ТекСтрокаСторонниеЛица.Контрагент);
				Область.Параметры.КонтрагентСсылка = ТекСтрокаСторонниеЛица.Контрагент;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекСтрокаСторонниеЛица.КонтактноеЛицо)Тогда
				ТекущееКонтактноеЛицо = " ";
			Иначе
				ТекущееКонтактноеЛицо = ТекСтрокаСторонниеЛица.КонтактноеЛицо;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекСтрокаСторонниеЛица.КонтактнаяИнформация)Тогда
				ТекущаяКонтактнаяИнформация = " ";
			Иначе	
				ТекущаяКонтактнаяИнформация = СокрЛП(ТекСтрокаСторонниеЛица.КонтактнаяИнформация);
			КонецЕсли;
			
			Область.Параметры.КонтактноеЛицо = ТекущееКонтактноеЛицо;
			Область.Параметры.КонтактнаяИнформация = ТекущаяКонтактнаяИнформация;
			ТабДокумент.Вывести(Область);	
		КонецЦикла;
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("ВерхняяГраница"));
	КонецЕсли;	
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСодержание");
	Если ФорматТекста = Перечисления.ФорматТекста.ПростойТекст Тогда
		ОбластьМакета.Параметры.Содержание = Содержание;			
	Иначе			
		ОбластьМакета.Параметры.Содержание = обПолучитьТекст(Содержание);
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаРезультат");
	Если ФорматТекста = Перечисления.ФорматТекста.ПростойТекст Тогда
		ОбластьМакета.Параметры.Результат = Результат;			
	Иначе			
		ОбластьМакета.Параметры.Результат = обПолучитьТекст(Результат);
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
	
КонецФункции// ПечатьСобытие(ТабДокумент) Экспорт

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // ПечатьСобытие()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	ТипЗначенияОснование = ТипЗнч(Основание);
	
	Если ТипЗначенияОснование=Тип("Структура") Тогда	
		ИмяСтруктуры	= Неопределено;
		Основание.Свойство("Имя", ИмяСтруктуры);
		Если ИмяСтруктуры="РегистрацияЗвонковИКонтактов" Тогда
			
			ВводИзАРМРесепшен = Истина;
			
			ВидСобытия = Основание.ВидКонтакта;
			
			Основание.Свойство("ТипСообщение", ТипСообщения);
			Если ТипСообщения.Пустая() Тогда
				ТипСообщения = Перечисления.ТипыСообщений.Входящее;
			КонецЕсли;
			
			ДатаНачала	= Основание.ДатаДень;
			Тема		= Основание.Примечание;
			ИсточникИнформации = Основание.Реклама;
			
			//Если ЗначениеЗаполнено(Основание.Сотрудник) Тогда
			//	СтрокаПользователь				= Пользователи.Добавить();
			//	СтрокаПользователь.Пользователь	= Справочники.Пользователи.НайтиПоРеквизиту("Сотрудник",Основание.Сотрудник);
			//КонецЕсли;
			
			Если ЗначениеЗаполнено(Основание.Участник) Тогда
				ПользовательУчастник = Справочники.Пользователи.НайтиПоРеквизиту("Сотрудник",Основание.Участник);
				Если НЕ ПользовательУчастник = Неопределено И ЗначениеЗаполнено(ПользовательУчастник) Тогда
					Организация           = ПользовательУчастник.Организация;
					ПодразделениеКомпании = ПользовательУчастник.Подразделение;
				КонецЕсли;
				Если Пользователи.Найти(Справочники.Пользователи.НайтиПоРеквизиту("Сотрудник",Основание.Участник), "Пользователь") = Неопределено Тогда
					СтрокаПользователь				= Пользователи.Добавить();
					СтрокаПользователь.Пользователь	= ПользовательУчастник;
				КонецЕсли;
			КонецЕсли;
			
			Контрагент						= Основание.Контрагент;
			СторонниеЛицаСтрока				= СторонниеЛица.Добавить();
			СторонниеЛицаСтрока.Контрагент	= Основание.Контрагент;
			Тема							= Основание.ПредметОбращения;
			Содержание						= Основание.Примечание;
			Если ЗначениеЗаполнено(Основание.Модель) Тогда
				МодельСтрока = Автомобили.Добавить();
				МодельСтрока.Автомобиль = Основание.Модель;
				ОбработкаРеквизита("Автомобили.Автомобиль",МодельСтрока,,Новый Структура("ТабличнаяЧасть",Автомобили));
			КонецЕсли;
			
			Состояние             = Перечисления.СостоянияСобытий.НеОбработано;
			КодГорода             = Основание.КодГорода;
			КодРегиона            = Основание.КодРегиона;
			ВнутреннийНомер       = Основание.ВнутреннийНомер;
			ПредставлениеТелефона = Основание.ПредставлениеТелефона;
			НомерТелефона         = Основание.НомерТелефона;
			ВидТелефона           = Основание.ВидТелефона;
			
			//найдем основной номер телефона
			//ТекстЗапроса = "ВЫБРАТЬ
			//			   |	КонтактнаяИнформация.Представление
			//			   |ИЗ
			//			   |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			//			   |ГДЕ
			//			   |	КонтактнаяИнформация.Объект.Ссылка = &Контрагент
			//			   |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = &Истина
			//			   |	И КонтактнаяИнформация.Тип = &ТипКИ";
			//Запрос			= Новый Запрос();
			//Запрос.Текст	= ТекстЗапроса;
			//Запрос.УстановитьПараметр("Контрагент",Основание.Контрагент);
			//Запрос.УстановитьПараметр("Истина",Истина);
			//Запрос.УстановитьПараметр("ТипКИ",Перечисления.ТипыКонтактнойИнформации.Телефон);
			//РезультатЗапроса		= Запрос.Выполнить();
			//Выборка = РезультатЗапроса.Выбрать();
			//КонтактнаяИнформация	= "";
			//Пока Выборка.Следующий() Цикл
			//	КонтактнаяИнформация	= Выборка.представление;
			//	Прервать;
			//КонецЦикла;
			//Если ЗначениеЗаполнено(Основание.ПредставлениеТелефона) Тогда
			//	КонтактнаяИнформация		= Основание.ПредставлениеТелефона;
			//КонецЕсли;
			//СторонниеЛицаСтрока.ВладелецКИ				= Основание.Контрагент;
			//СторонниеЛицаСтрока.КонтактнаяИнформация	= КонтактнаяИнформация;
			//СторонниеЛицаСтрока.ТипКонтактнойИнформации	= Перечисления.ТипыКонтактнойИнформации.Телефон;
			
			РегистрацияЗвонковИКонтактов = Основание;
		КонецЕсли;
		
		//не требуется вызов дкОбработкаЗаполнения()
		Возврат;	
		
	ИначеЕсли ТипЗначенияОснование=Тип("СправочникСсылка.Контрагенты") Тогда
		
		// вызов обработчиков из дкОбработкаЗаполнения()
		дкОбработкаЗаполненияПоУмолчанию(ЭтотОбъект,Основание,Ложь,Ложь);
		
		Если Основание.ЭтоГруппа Тогда 
			Возврат;
		КонецЕсли;
		
		// вызов обработчиков из дкОбработкаЗаполнения()
		дкДокументыНаОснованииКонтрагентов(ЭтотОбъект,Основание,Результат);
		
		//заполним реквизит контрагент или реквизит контактное лицо
		Если Основание.Ссылка.ВидКонтрагента = Перечисления.ВидыКонтрагентов.КонтактноеЛицо Тогда
			КонтактноеЛицо = Основание.Ссылка;
		Иначе
			Контрагент	= Основание.Ссылка;
			СторонниеЛицаСтрока = СторонниеЛица.Добавить();
			СторонниеЛицаСтрока.Контрагент = Основание.Ссылка;
		КонецЕсли;
		//найдем основной номер телефона
		ТекстЗапроса = "ВЫБРАТЬ
		               |	КонтактнаяИнформация.Представление
		               |ИЗ
		               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		               |ГДЕ
		               |	КонтактнаяИнформация.Объект.Ссылка = &Контрагент
		               |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = &Истина
		               |	И КонтактнаяИнформация.Тип = &ТипКИ";
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Контрагент",Основание.Ссылка);
		Запрос.УстановитьПараметр("Истина",Истина);
		Запрос.УстановитьПараметр("ТипКИ",Перечисления.ТипыКонтактнойИнформации.Телефон);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		КонтактнаяИнформация = "";
		Пока Выборка.Следующий() Цикл
			КонтактнаяИнформация = Выборка.представление;
			Прервать;
		КонецЦикла;
		Если НЕ Основание.Ссылка.ВидКонтрагента = Перечисления.ВидыКонтрагентов.КонтактноеЛицо Тогда
			СторонниеЛицаСтрока.ВладелецКИ = Основание.Ссылка;
			СторонниеЛицаСтрока.КонтактнаяИнформация = КонтактнаяИнформация;
			СторонниеЛицаСтрока.ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
		КонецЕсли;
		
		Если Пользователи.Количество() = 0 Тогда
			СтрокаПользователь = Пользователи.Добавить();
			СтрокаПользователь.Пользователь = ПараметрыСеанса.Пользователь;
		КонецЕсли;
		
		//не требуется вызов дкОбработкаЗаполнения()
		Возврат;
		
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗначенияОснование=Тип("ДокументСсылка.Событие") Тогда 	
		ВидСобытия = Основание.ВидСобытия;
		Приоритет = Основание.Приоритет;
		Состояние = Основание.Состояние;
		ФорматТекста = Основание.ФорматТекста;
		ДокументОснование = Основание.Ссылка;
		СторонниеЛица.Загрузить(Основание.СторонниеЛица.Выгрузить());
		Пользователи.Загрузить(Основание.Пользователи.Выгрузить());
		ПереносСтроки = ?(Основание.ФорматТекста = Перечисления.ФорматТекста.HTML,"<BR>",Символы.ПС);
		Содержание = ПереносСтроки + "---------- Исходное событие ----------" + ПереносСтроки;
		Содержание = Содержание + "Дата: " + Основание.Дата + ПереносСтроки;
	 	Содержание = Содержание + "Тема: " + Основание.Тема + ПереносСтроки + ПереносСтроки;
		ТелоHTML = обПолучитьТелоHTML(Основание.Содержание);
		Если НЕ ПустаяСтрока(ТелоHTML) Тогда
			Содержание = Содержание + ТелоHTML;	
		КонецЕсли;	
		Результат = ПереносСтроки +  "------- Достигнутые результаты -------" + ПереносСтроки;
		
		ТелоHTML = обПолучитьТелоHTML(Основание.Результат);
		Если НЕ ПустаяСтрока(ТелоHTML) Тогда
			Результат = Результат + ТелоHTML;
		КонецЕсли;	
		Тема = Основание.Тема;			
		ТипСообщения = ?(Основание.ТипСообщения = Перечисления.ТипыСообщений.Входящее,Перечисления.ТипыСообщений.Исходящее, Перечисления.ТипыСообщений.Входящее);
		
	ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.ЭлектронноеПисьмо") Тогда
		Тема = Основание.Тема;
		Если Основание.СтатусПисьма = Перечисления.СтатусыСообщений.Входящее Тогда
			ТипСообщения = Перечисления.ТипыСообщений.Входящее;
		Иначе
			ТипСообщения = Перечисления.ТипыСообщений.Исходящее;
		КонецЕсли;
		ВидСобытия = Перечисления.ВидыСобытий.ЭлектронноеПисьмо;				
		ФорматТекста = Основание.ФорматТекста;
		Содержание = Основание.ТекстПисьма;				
		Результат = "";
		
	ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда	
		
		Если Основание.ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобильИзменение Тогда
			ВидСобытия	= Перечисления.ВидыСобытий.ЗаменаАвтомобиля;
			
		ИначеЕсли Основание.ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобильПереуступка Тогда
			ВидСобытия	= Перечисления.ВидыСобытий.ПереуступкаАвтомобиля;
			
		ИначеЕсли Основание.ХозОперация=Справочники.ХозОперации.ЗаказНаАвтомобильОтмена Тогда
			ВидСобытия	= Перечисления.ВидыСобытий.ОтказОтАвтомобиля;
			
		Иначе
			ВидСобытия	= Перечисления.ВидыСобытий.ВыдачаАвтомобиля;	
		КонецЕсли;	
		ТипСообщения	= Перечисления.ТипыСообщений.Исходящее;	
        Тема			= Строка(Основание);		
		ДатаНачала		= ТекущаяДата();
		ОбработкаРеквизита("ДатаНачала");
				
		КонтрагентОснования = Основание.Заказчик;		
		Если (КонтрагентОснования<>Неопределено) И (ТипЗнч(КонтрагентОснования)=Тип("СправочникСсылка.Контрагенты")) Тогда 			
			Если СторонниеЛица.Найти(КонтрагентОснования, "Контрагент")=Неопределено Тогда
				НоваяСтрока				= СторонниеЛица.Добавить();
				НоваяСтрока.Контрагент	= КонтрагентОснования;
				Контрагент				= КонтрагентОснования;
			КонецЕсли; 				
		КонецЕсли;
		
		МестоВыдачиАвтомобиля	= Перечисления.МестаВыдачиАвтомобилей.Салон;
		
		ЗаказНаАвтомобиль	= Основание;
		ОбработкаРеквизита("ЗаказНаАвтомобиль");
		
		АвторВыдачиАвтомобиля	= Основание.Автор;
		ОбработкаРеквизита("АвторВыдачиАвтомобиля");
		
		Если обЗначениеНеЗаполнено(Автомобили) Тогда
			Автомобиль	= Основание.Автомобиль;
			ОбработкаРеквизита("Автомобиль");
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.РабочийЛист") ИЛИ
		ТипЗначенияОснование=Тип("ДокументСсылка.РабочийЛистКредитногоОтдела") ИЛИ
		ТипЗначенияОснование=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда

		ВидСобытия			= Перечисления.ВидыСобытий.Прочее;	
		ТипСообщения		= Перечисления.ТипыСообщений.Исходящее;	
		Тема				= Строка(Основание);
		
		
		Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
			ДатаНачала		= ТекущаяДата();
			ОбработкаРеквизита("ДатаНачала");
			ДатаОкончания	= ДатаНачала+600;
		КонецЕсли;			
		
		// Попытаемся определить контрагента.
		Контрагент	= Неопределено;
		Если ТипЗначенияОснование=Тип("ДокументСсылка.РабочийЛист") Тогда
			Контрагент			= Основание.Контрагент;
			Автомобиль			= Основание.Автомобиль;
			Менеджер			= Основание.Менеджер;
			ИсточникИнформации 	= Основание.Реклама;
			
		ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.РабочийЛистКредитногоОтдела") Тогда
			Контрагент	= Основание.Клиент;
			Менеджер	= Основание.МенеджерКредитногоОтдела;
			
		ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
			Контрагент	= Основание.Страхователь;
			Менеджер	= Основание.МенеджерОтделаСтрахования;
		КонецЕсли;
		
		Если (Контрагент<>Неопределено) И (ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты")) Тогда 			
			Если СторонниеЛица.Найти(КонтрагентОснования, "Контрагент")=Неопределено Тогда
				НоваяСтрока				= СторонниеЛица.Добавить();
				НоваяСтрока.Контрагент	= Контрагент;
			КонецЕсли; 				
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
		ВидСобытия = Перечисления.ВидыСобытий.ВыдачаАвтомобиля;
		
		КонтрагентОснования = Основание.Заказчик;		
		Если (КонтрагентОснования<>Неопределено) И (ТипЗнч(КонтрагентОснования)=Тип("СправочникСсылка.Контрагенты")) Тогда 			
			Если СторонниеЛица.Найти(КонтрагентОснования, "Контрагент")=Неопределено Тогда
				НоваяСтрока				= СторонниеЛица.Добавить();
				НоваяСтрока.Контрагент	= КонтрагентОснования;
				Контрагент				= КонтрагентОснования;
			КонецЕсли; 				
		КонецЕсли;
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Телефоны = киПолучитьСписокТелефоновОбъекта(Контрагент);
			Если Телефоны<>Неопределено И Телефоны.Количество()>0 Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект,Телефоны[0].Значение);
			КонецЕсли;
		КонецЕсли;
		
		Если Основание.Автомобили.Количество() = 1 Тогда
			Тема = "Выдача автомобиля " + Основание.Автомобили[0].Автомобиль + ", контрагент "+ Контрагент;
			Автомобиль = Основание.Автомобили[0].Автомобиль;
			ОбработкаРеквизита("Автомобиль");
			ЗаказНаАвтомобиль = Основание.Автомобили[0].ЗаказНаАвтомобиль;
			ОбработкаРеквизита("ЗаказНаАвтомобиль");
		Иначе 
			Тема = "Выдача автомобиля контрагенту " + Контрагент;
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.РазукомплектацияАвтомобилей") Тогда
		Для Каждого СтрТовар Из Основание.Опции Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
			НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмерения;
			НоваяСтрока.Коэффициент = СтрТовар.Коэффициент;
			НоваяСтрока.Количество = СтрТовар.Количество;
		КонецЦикла;
		
	ИначеЕсли ТипЗначенияОснование=Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда		
		
		ВидСобытия 				= Перечисления.ВидыСобытий.ТелефонныйЗвонок;
		Автор 					= Основание.Автор;
		Менеджер 				= Автор.Сотрудник;
		ДокументОснование 		= Основание.Ссылка;
		Приоритет 				= Перечисления.Важность.Средняя;
		Состояние 				= Перечисления.СостоянияСобытий.Запланировано;
		ДатаНачала 				= Основание.Дата;
		ДатаОкончания 			= Основание.Дата + Основание.сфпДлительностьЗвонка;
		Продолжительность 		= Основание.сфпДлительностьЗвонка / 3600;
		НомерТелефона 			= Основание.АбонентКакСвязаться;
		ОригинальныйТелефонныйНомер	= Основание.АбонентКакСвязаться;	
		
		Если Основание.Входящий Тогда
			ТипСообщения = Перечисления.ТипыСообщений.Входящее;
		Иначе
			ТипСообщения = Перечисления.ТипыСообщений.Исходящее;
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(Основание.АбонентКонтакт) Тогда
			Если ТипЗнч(Основание.АбонентКонтакт) = Тип("СправочникСсылка.Организации") Тогда
				Организация = Основание.АбонентКонтакт;
			ИначеЕсли ТипЗнч(Основание.АбонентКонтакт) = Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				ПодразделениеКомпании = Основание.АбонентКонтакт;
			ИначеЕсли ТипЗнч(Основание.АбонентКонтакт) = Тип("СправочникСсылка.Сотрудники") Тогда
				ВнутреннийНомер = сфпСофтФонПроСервер.сфпТекущийВнутреннийНомер(Основание.АбонентКонтакт);
			ИначеЕсли ТипЗнч(Основание.АбонентКонтакт) = Тип("СправочникСсылка.Контрагенты") Тогда
				Ведущий = РаботаСКонтактнымиЛицами.ПолучитьВедущегоКонтрагента(Основание.АбонентКонтакт);
				Если ЗначениеЗаполнено(Ведущий) Тогда
					Контрагент = Ведущий;
					КонтактноеЛицо = Основание.АбонентКонтакт;
				Иначе
					Контрагент = Основание.АбонентКонтакт;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	Иначе // Все остальные документы.
		Если ВидСобытия.Пустая() Тогда
			ВидСобытия = Перечисления.ВидыСобытий.Прочее;
		КонецЕсли;
		
		Если ТипСообщения.Пустая() Тогда
			ТипСообщения = Перечисления.ТипыСообщений.Исходящее;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
			ДатаНачала = ТекущаяДата();
			ОбработкаРеквизита("ДатаНачала");
		КонецЕсли;
		
		Если ПустаяСтрока(Тема) Тогда
			Тема = Строка(Основание);
		КонецЕсли;
		
		// Попытаемся определить контрагента.
		КонтрагентОснования = Неопределено;
		Попытка
			КонтрагентОснования = Основание.Заказчик;
		Исключение
			Попытка
				КонтрагентОснования = Основание.Контрагент;
			Исключение
				Попытка
					КонтрагентОснования = Основание.ВладелецТовара;
				Исключение
					Попытка
						КонтрагентОснования = Основание.ОпрашиваемоеЛицо;
					Исключение
					КонецПопытки;
				КонецПопытки;
			КонецПопытки;
		КонецПопытки;
		
		Если (КонтрагентОснования<>Неопределено) И (ТипЗнч(КонтрагентОснования)=Тип("СправочникСсылка.Контрагенты")) Тогда
			Если СторонниеЛица.Найти(КонтрагентОснования, "Контрагент")=Неопределено Тогда
				НоваяСтрока				= СторонниеЛица.Добавить();
				НоваяСтрока.Контрагент	= КонтрагентОснования;
				Контрагент				= КонтрагентОснования;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	//Свернем ТЧ товаров, т.к. в документе-основании могут быть дубли строк
	Товары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,ХарактеристикаНоменклатуры","Количество");
	
	Если НЕ ВводИзАРМРесепшен И Пользователи.Количество() = 0 Тогда
		СтрокаПользователь = Пользователи.Добавить();
		СтрокаПользователь.Пользователь = ПараметрыСеанса.Пользователь;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	ВидСледующегоСобытия = Неопределено;
	ДатаСледующегоСобытия = Неопределено;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Состояние = Перечисления.СостоянияСобытий.Завершено Тогда
		Если НЕ ЗначениеЗаполнено(ДатаФактическогоЗавершения) Тогда
			#Если Клиент Тогда
			ДатаФактическогоЗавершения=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
			#Иначе
			ДатаФактическогоЗавершения=ТекущаяДата();
			#КонецЕсли
		КонецЕсли;
	Иначе
		ДатаФактическогоЗавершения = Неопределено;
	КонецЕсли;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// Попытка заполнения времени планируемой выдачи автомобияя на основании даты
	ПланируемоеВремяВыдачиАвтомобиля	= Дата(1,1,1,0,0,0);
	Если НЕ обЗначениеНеЗаполнено(ПланируемаяДатаВыдачиАвтомобиля) Тогда
		ПланГод		= Год(ПланируемаяДатаВыдачиАвтомобиля);
		ПланМесяц	= Месяц(ПланируемаяДатаВыдачиАвтомобиля);
		ПланДень	= День(ПланируемаяДатаВыдачиАвтомобиля);
		ПланЧас		= Час(ПланируемаяДатаВыдачиАвтомобиля);
		ПланМинута	= Минута(ПланируемаяДатаВыдачиАвтомобиля);
		ПланСекунда	= Секунда(ПланируемаяДатаВыдачиАвтомобиля);
		ПланируемоеВремяВыдачиАвтомобиля= Дата(1, 1, 1, ПланЧас, ПланМинута, ПланСекунда);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ Тогда
		Если НЕ орЖурналСостояний(Ссылка,Состояние) Тогда
			Отказ=Истина;
			дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			Возврат;
		КонецЕсли; 
	КонецЕсли; 
	
	//anton
	//Если РегистрацияЗвонковИКонтактов <> Неопределено И РегистрацияЗвонковИКонтактов.Имя = "РегистрацияЗвонковИКонтактов" Тогда
	//	НаборЗаписей = РегистрыСведений.РегистрацияЗвонковИКонтактов.СоздатьНаборЗаписей(); 
	//	НаборЗаписей.Отбор.Автор.Установить(РегистрацияЗвонковИКонтактов.АвторКонтакта);
	//	НаборЗаписей.Отбор.ДатаДень.Установить(РегистрацияЗвонковИКонтактов.ДатаДень); 
	//	НаборЗаписей.Отбор.ДатаВремя.Установить(РегистрацияЗвонковИКонтактов.ДатаВремя); 
	//	НаборЗаписей.Отбор.Сотрудник.Установить(РегистрацияЗвонковИКонтактов.Сотрудник);
	//	НаборЗаписей.Отбор.Клиент.Установить(РегистрацияЗвонковИКонтактов.Контрагент);
	//	НаборЗаписей.Прочитать();
	//	НаборЗаписей[0].ДокументСобытие = Ссылка;
	//	НаборЗаписей.Записать();
	//КонецЕсли;
	//anton
	
	#Если Клиент Тогда
		Оповестить("ДокументСобытиеПриЗаписи", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Заполним График работы ресурсов
	
	// Очистим регистр
	НаборЗаписейГрафикРаботыРесурсов = РегистрыСведений.ГрафикРаботыРесурсов.СоздатьНаборЗаписей();
	НаборЗаписейГрафикРаботыРесурсов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейГрафикРаботыРесурсов.ОтменаПроведения();
	
	НаборЗаписей = Новый ТаблицаЗначений;
	НаборЗаписей.Колонки.Добавить("Ресурс1");
	НаборЗаписей.Колонки.Добавить("Ресурс2");
	НаборЗаписей.Колонки.Добавить("Объект");
	НаборЗаписей.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НаборЗаписей.Колонки.Добавить("НачалоРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("КонецРабочегоВремени", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("Продолжительность", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	НаборЗаписей.Колонки.Добавить("КоличествоНатуральныхЕдиниц", Новый ОписаниеТипов("Число"));
	НаборЗаписей.Колонки.Добавить("НапомнитьЗа", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	
	// SHMI ++
	НаборЗаписей.Колонки.Добавить("Авторабота");
	// SHMI --
	
	// Перебираем пользователей
	Для Каждого СтрокаПользователи Из Пользователи Цикл
		Если НЕ обЗначениеНеЗаполнено(ДатаОкончания) Тогда
			Если НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала) = 0 Тогда
				// Начало окончание в одном дне
				СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
				СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаПользователи.Пользователь;
				СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
				СтрокаГрафикаРаботыРесурсов.Дата = НачалоДня(ДатаНачала);
				СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
				СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101' + (ДатаОкончания - НачалоДня(ДатаОкончания));
				СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени - СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени);
			Иначе
				Разница = (НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала)) / 60 / 60 / 24;
				ТекущийДень = НачалоДня(ДатаНачала);
				Пока Разница >= 0 Цикл
					СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
					СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаПользователи.Пользователь;
					СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
					СтрокаГрафикаРаботыРесурсов.Дата = ТекущийДень;					
					
					// Установим НачалоРабочегоВремени 
					Если ТекущийДень = НачалоДня(ДатаНачала) Тогда
						СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
					Иначе
						СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101';
					КонецЕсли;
					
					// Установим КонецРабочегоВремени
					Если ТекущийДень = НачалоДня(ДатаОкончания) Тогда
						СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101' + (ДатаОкончания - НачалоДня(ДатаОкончания));
					Иначе
						СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени = '00010101235959';
					КонецЕсли;
					
					// Посчитаем Продолжительность
					СтрокаГрафикаРаботыРесурсов.Продолжительность = '00010101' + (СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени - СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени);
					ТекущийДень = ТекущийДень + 1*60*60*24;
					Разница = Разница - 1;
				КонецЦикла;
			КонецЕсли;
		Иначе
			// Дата окончания не заполнена
			СтрокаГрафикаРаботыРесурсов = НаборЗаписей.Добавить();
			СтрокаГрафикаРаботыРесурсов.Ресурс1 = СтрокаПользователи.Пользователь;
			СтрокаГрафикаРаботыРесурсов.Объект = Ссылка;
			СтрокаГрафикаРаботыРесурсов.Дата = НачалоДня(ДатаНачала);
			СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени = '00010101' + (ДатаНачала - НачалоДня(ДатаНачала));
			СтрокаГрафикаРаботыРесурсов.КонецРабочегоВремени  = СтрокаГрафикаРаботыРесурсов.НачалоРабочегоВремени;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписейГрафикРаботыРесурсов.НаборЗаписей = НаборЗаписей;
	НаборЗаписейГрафикРаботыРесурсов.Проведение();
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)
	// Очистим регистр ГрафикРаботыРесурсов
	НаборЗаписейГрафикРаботыРесурсов = РегистрыСведений.ГрафикРаботыРесурсов.СоздатьНаборЗаписей();
	НаборЗаписейГрафикРаботыРесурсов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейГрафикРаботыРесурсов.ОтменаПроведения();
КонецПроцедуры // ОбработкаУдаленияПроведения()


ВводИзАРМРесепшен = Ложь;

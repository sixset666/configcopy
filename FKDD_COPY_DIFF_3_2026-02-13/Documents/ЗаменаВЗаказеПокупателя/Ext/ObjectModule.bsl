// Модуль документа СнятиеРезервовЗаказовПокупателя

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	//ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары = Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	//ОбязательныеРеквизиты.Вставить("СрокПоставки",1);
	//ОбязательныеРеквизиты.Вставить("ВидОплаты",1);
	ОбязательныеРеквизиты.Вставить("Замены",           ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("ТоварыЗаменители", ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения характеристики номенклатуры
//                                                                
// Параметры:
//  СтрокаТовары - строка табличной части документа - ссылка на строку.
//  Ошибки       - строка - содержит описание ошибки.
//
// Возвращаемое значение:
//  Булево 
//
Функция ПроверитьЗаполнениеХарактеристики(СтрокаТовары, Ошибки="") Экспорт
	
	УчетВедется = СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ  СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2;
	РучноеСписаниеХарактеристик = СтрокаТовары.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ
																СтрокаТовары.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик.Пустая();
	Если УчетВедется И РучноеСписаниеХарактеристик И обЗначениеНеЗаполнено(СтрокаТовары.ХарактеристикаНоменклатуры) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьЗаполнениеХарактеристики()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗаменаВЗаказеПокупателя","Замена в заказе покупателя",Истина);
	Возврат СписокХозОпераций;
	
КонецФункции // ПолучитьСписокХозОпераций()

#Если Клиент Тогда
	
//Выполняет пересчет значений в таблице товаров
//
// Параметры:
// Проверка	– Булево – Флаг закрытия интервалов.
//
// Возвращаемое значение:
// Булево – Результат закрытия.
//
Процедура ПересчетТаблицы(ФормаЦен, ТаблицаТоваров, ПараметрыИзменений = Неопределено) Экспорт
	
	ТребуетсяПересчет = Ложь;
	ИзменилиЦену      = Ложь;
	ИзменилиВалюту    = Ложь;
	ИзменилиСкидку    = Ложь;
	ОтключитьСкидки   = Ложь;
	Если ТипЗнч(ПараметрыИзменений) = Тип("Структура") Тогда
		
		Если ПараметрыИзменений.Свойство("ТребуетсяПересчет") Тогда
			ТребуетсяПересчет = ПараметрыИзменений.ТребуетсяПересчет;
		КонецЕсли;
		
		Если ПараметрыИзменений.Свойство("ИзменилиЦену") Тогда
			ИзменилиЦену = ПараметрыИзменений.ИзменилиЦену;
		КонецЕсли;
		
		Если ПараметрыИзменений.Свойство("ИзменилиВалюту") Тогда
			ИзменилиВалюту = ПараметрыИзменений.ИзменилиВалюту;
		КонецЕсли;
		
		Если ПараметрыИзменений.Свойство("ИзменилиСкидку") Тогда
			ИзменилиСкидку = ПараметрыИзменений.ИзменилиСкидку;
		КонецЕсли;
		
		ОтключитьСкидки = Истина;
		
	КонецЕсли;
	
	ОбнулятьСуммовыеПоказатели = Неопределено;
	Для Каждого СтрокаЗамены Из ТаблицаТоваров.Строки Цикл
		
		Для Каждого ТекСтрока Из СтрокаЗамены.Строки Цикл
			
			Если ТребуетсяПересчет Тогда
				// если изменились, тип цен, валюта или курс, то получим цену
				Если ИзменилиЦену Тогда
					Цена = обПолучитьЦену(ФормаЦен.НовыйТипЦен, ТекСтрока.Номенклатура,?(ЭтоНовый(), Дата, МоментВремени()),, ФормаЦен.НоваяВалюта, ФормаЦен.НовыйКурс, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
					Если Цена = 0 Тогда
						Если ОбнулятьСуммовыеПоказатели = Неопределено Тогда // Если вопрос еще не задавался, то зададим его.
							Ответ = Вопрос("В табличной части присутствуют товары с нулевой ценой для выбранного типа цен <" + ФормаЦен.НовыйТипЦен + ">.
							|Обнулить цены для данных позиций?", РежимДиалогаВопрос.ДаНет);
							Если Ответ = КодВозвратаДиалога.Нет Тогда
								ОбнулятьСуммовыеПоказатели = Ложь;
							Иначе
								ОбнулятьСуммовыеПоказатели = Истина;
							КонецЕсли;
						КонецЕсли;
						
						Если ОбнулятьСуммовыеПоказатели Тогда
							ТекСтрока.Цена = 0;
						ИначеЕсли ИзменилиВалюту Тогда
							// Если обнулять цену не нужно, и при этом изменилась валюта,
							//то выполним пересчет значения старой цены.
							ТекСтрока.Цена = обПересчет(ТекСтрока.Цена, ФормаЦен.СтараяВалюта, ФормаЦен.СтарыйКурс, ФормаЦен.НоваяВалюта, ФормаЦен.НовыйКурс);
						КонецЕсли;
					КонецЕсли;
				Иначе // Значит сменилась только валюта и необходимо выполнить пересчет.
					ТекСтрока.Цена = обПересчет(ТекСтрока.Цена, ФормаЦен.СтараяВалюта, ФормаЦен.СтарыйКурс, ФормаЦен.НоваяВалюта, ФормаЦен.НовыйКурс);
				КонецЕсли;
				
				Если ИзменилиСкидку Тогда
					//Если скидка была изменена, сотрем старое значение скидки
					ТекСтрока.ПроцентСкидки = 0;
				КонецЕсли;
			КонецЕсли;
			
			Если ОтключитьСкидки Тогда
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("НеРассчитыватьСкидки", Истина);
				ОбработкаРеквизита("Товары.Цена", ТекСтрока, ФормаЦен.ВладелецФормы, ДопПараметры);
			Иначе
				ОбработкаРеквизита("Товары.Цена", ТекСтрока, ФормаЦен.ВладелецФормы, ДопПараметры);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "ДокументОснование" Тогда
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Контрагент = ?(обЗначениеНеЗаполнено(ДокументОснование), Неопределено, ДокументОснование.ПодразделениеПолучатель);
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
			ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Истина;
			СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
			Если ЭтаФорма = Неопределено Тогда
				Для Каждого ТекСтрока Из ТоварыЗаменители Цикл
					ТекСтрока.СкидкаНаТовар = СкидкаНаценка;
					//ТекСтрока.СтавкаНДС     = Справочники.СтавкиНДС.БезНДС;
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
					ОбработкаРеквизита("Товары.СтавкаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
				КонецЦикла;
			Иначе
				Для Каждого СтрокаРодитель Из ЭтаФорма.Товары.Строки Цикл
					Для Каждого СтрокаЗаменитель Из СтрокаРодитель.Строки Цикл
						СтрокаЗаменитель.СкидкаНаТовар = СкидкаНаценка;
						//СтрокаЗаменитель.СтавкаНДС     = Справочники.СтавкиНДС.БезНДС;
						СтрокаЗаменитель.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
						ОбработкаРеквизита("Товары.СтавкаНДС", СтрокаЗаменитель, ЭтаФорма, ДопПараметры);
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			ОбработкаРеквизита("РассчитатьСкидки", , ЭтаФорма, ДопПараметры);
		Иначе
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				Контрагент    = ДокументОснование.Контрагент;
				СкидкаНаценка = ДокументОснование.СкидкаНаценка;
			КонецЕсли;
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Ложь;
			ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Ложь;
		КонецЕсли;
		
	ИначеЕсли Имя = "Организация" Тогда
		
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
		ОсвобожденОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		// Организация может являются плательщиком НДС, а может и нет.. надо обработать таблицу
		// однако ее может не быть, либо в ней не отражается НДС
		// Помимо этого необходимо учесть особенность документа "Поступление товаров", 
		// т.к. в этом случае контрагент (поставщик) может не является платильщиком НДС.
		// (если контрагент - частное лицо, не смотрим на флаг, проставляем ставку БезНДС)
		Если ЭтаФорма = Неопределено Тогда
			Для Каждого СтрокаТоваров Из ТоварыЗаменители Цикл
				Если ОсвобожденОтНДС Тогда
					СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				Иначе
					СтрокаТоваров.СтавкаНДС = СтрокаТоваров.Номенклатура.СтавкаНДС;
				КонецЕсли;
				ОбработкаРеквизита("Товары.Цена", СтрокаТоваров, ЭтаФорма, ДопПараметры);
			КонецЦикла;
		Иначе
			//ТаблицаТоваров = Товары;
			Для Каждого СтрокаЗамен Из ЭтаФорма.Товары.Строки Цикл
				Для Каждого СтрокаТоваров Из СтрокаЗамен.Строки Цикл
					Если ОсвобожденОтНДС Тогда
						СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
					Иначе
						СтрокаТоваров.СтавкаНДС = СтрокаТоваров.Номенклатура.СтавкаНДС;
					КонецЕсли;
					ОбработкаРеквизита("Товары.Цена", СтрокаТоваров, ЭтаФорма, ДопПараметры);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя = "ПодразделениеКомпании" Тогда
		
		Рез = Истина;
		
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
		#Если Клиент Тогда
			// Если произошло интерактивное изменение подразделения компании, то, возможно, необходимо пересчитать цены,
			Если НЕ ЭтаФорма = Неопределено Тогда
				// В табличной части "Товары" может не быть реквизита "Цена".
				// Зададим вопрос пользователю, хочет ли он пересчитать цены.
				Если Вопрос("Пересчитать цены в табличной части в соответствии с выбранным подразделением?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
					
					Попытка
						МоментЦены = ?(ЭтоНовый(), Дата, МоментВремени());
					Исключение
						МоментЦены = Неопределено;
					КонецПопытки;
					
					Для Каждого СтрокаЗамены Из ЭтаФорма.Товары.Строки Цикл
						Для Каждого ТекСтрока Из СтрокаЗамены.Строки Цикл
							// Определим, есть ли в ТЧ реквизиты "ХарактеристикаНоменклатуры" и "ЕдиницаИзмерения".
							ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
							ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
							ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, МоментЦены,, ВалютаДокумента, КурсДокумента, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании);
							Рез = ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		#КонецЕсли
		
		// Изменение подразделения приводит к необходимости пересчета скидок документа (как "шапочных", так и товарных). 		
		// Если имя переменной модуля документа, являющейся ссылкой на обработку по расчету скидок, отличается
		// стандартного, тогда альтернативное имя будет передано через дополнительные параметры.
		Попытка
			ИмяПеременнойОбработки = ДопПараметры.ИмяПеременнойОбработки;
		Исключение
			ИмяПеременнойОбработки = "ОбработкаРасчетСкидок";
		КонецПопытки;
		
		Попытка // Получаем ссылку на обработку по расчету скидок. Если такой переменной в модуле нет, то документ не поддерживает расчет скидок.
			ОбработкаРасчетСкидок.ПодразделениеКомпании = ПодразделениеКомпании;
			Рез = ОбработкаРеквизита("РассчитатьСкидки", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Исключение
		КонецПопытки;
		
	ИначеЕсли Имя = "ТипЦен" Тогда
		
		ТребуетсяПересчет = Истина;
		Если ДопПараметры <> Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет", ТребуетсяПересчет) Тогда
				ТребуетсяПересчет = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ТребуетсяПересчет Тогда
			Возврат Истина;
		КонецЕсли;
		
		
		// пересчитываем
		Если ЭтаФорма = Неопределено Тогда
			Для Каждого ТекСтрока Из ТоварыЗаменители Цикл
				// Если изменились, тип цен, валюта или курс, то получим цену.
				Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Ссылка,,ВалютаДокумента, КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
				Если Цена <> 0 Тогда
					ТекСтрока.Цена = Цена;
					ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма, ДопПараметры);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого СтрокаЗамен Из ЭтаФорма.Товары.Строки Цикл
				Для Каждого ТекСтрока Из СтрокаЗамен.Строки Цикл
					// Если изменились, тип цен, валюта или курс, то получим цену.
					Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Ссылка,,ВалютаДокумента, КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
					Если Цена <> 0 Тогда
						ТекСтрока.Цена = Цена;
						ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма, ДопПараметры);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя = "СкидкаНаценка" Тогда
		
		// Выбрана ручная скидка, необходимо пересчитать скидки в табличной части.
		// Если имя переменной модуля документа, являющейся ссылкой на обработку по расчету скидок, отличается
		// стандартного, тогда альтернативное имя будет передано через дополнительные параметры.
		
		ОбработкаРасчетСкидок.СкидкаНаценка = ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСкидкаНаценка];
		// Смена скидки\наценки влияет на видимость некоторых колонок в табличной части.
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				дкУправлениеДоступностьюКолонокСкидок(ЭтаФорма, ОбработкаРасчетСкидок.СкидкаНаценка, "Товары");
			КонецЕсли;
		#КонецЕсли
		
		Рез = ОбработкаРеквизита("РассчитатьСкидки", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез; 
		
	ИначеЕсли Имя = "РассчитатьСкидки" И НЕ ЭтаФорма = Неопределено Тогда
		
		ТоварыЗаменители.Очистить();
		Для Каждого СтрокаРодитель Из ЭтаФорма.Товары.Строки Цикл
			Для Каждого СтрокаЗаменитель Из СтрокаРодитель.Строки Цикл
				НоваяСтрока = ТоварыЗаменители.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗаменитель);
			КонецЦикла;
		КонецЦикла;
		
		//Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		//	НеРассчитыватьСкидки = Истина;
		//Иначе
			Попытка
				НеРассчитыватьСкидки = ДопПараметры.НеРассчитыватьСкидки;
			Исключение 
				НеРассчитыватьСкидки = Ложь;
			КонецПопытки;
		//КонецЕсли;
		
		// Подбираем подходяющую скидку. Если скидка не изменилась, то функция вернет Ложь.
		Если (Не НеРассчитыватьСкидки) И ОбработкаРасчетСкидок.ПодобратьСкидку(ЭтотОбъект) Тогда
			ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСкидкаНаценка]   = ОбработкаРасчетСкидок.ТекущаяСкидкаНаценка;
			ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаЗначениеСкидкиНаценки] = ОбработкаРасчетСкидок.ТекущееЗначениеСкидки;
			ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСуммаСкидкиНаценки] = ОбработкаРасчетСкидок.ТекущаяСуммаСкидки;
			
			// Подобралась другая "шапочная" скидка, значит, возможно, придется заблокировать работу с некоторыми колонками 
			// в зависимости от типа скидки.
			#Если Клиент Тогда
				дкУправлениеДоступностьюКолонокСкидок(ЭтаФорма, ОбработкаРасчетСкидок.ТекущаяСкидкаНаценка, "Товары");
			#КонецЕсли
			
			// Применяем скидку к табличной части. Пересчитываем строчные скидки.
			ОбработкаРасчетСкидок.ПрименитьСкидку(ЭтотОбъект);
			
			// Получаем сумму всех скидок документа (с учетом строчных).
			СуммаИтого = 0;
			Попытка	// Документ может поддерживать только шапочные скидки, а значит в ТЧ не будет реквизита "СуммаСкидкиСтроки".
				СуммаИтого = СуммаИтого + ЭтотОбъект[ОбработкаРасчетСкидок.ИмяТабличнойЧасти].Итог("СуммаСкидкиСтроки")
			Исключение
			КонецПопытки;
			ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСуммаСкидкиНаценки] = ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСуммаСкидкиНаценки] + СуммаИтого;
			
			СтрокаРодитель      = Неопределено;
			ИдентификаторСтроки = Неопределено;
			ИндексСтроки        = 0;
			СтрокаЗамены = "ПроцентСкидки, СуммаСкидки, СуммаНДС, СуммаВсего, СкидкаНаТовар, ПроцентСкидкиСтроки, СуммаСкидкиСтроки";
			Для Каждого СтрокаЗаменитель Из ТоварыЗаменители Цикл
				
				Если ИдентификаторСтроки <> СтрокаЗаменитель.ИдентификаторСтроки Тогда
					ИдентификаторСтроки = СтрокаЗаменитель.ИдентификаторСтроки;
					СтрокаРодитель      = ЭтаФорма.Товары.Строки.Найти(ИдентификаторСтроки, "ИдентификаторСтроки", Ложь);
					ИндексСтроки        = 0;
				КонецЕсли;
				
				Если СтрокаРодитель = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаЗаменНаФорме = СтрокаРодитель.Строки[ИндексСтроки];
				СтрокаЗаменНаФорме.ПроцентСкидки       = СтрокаЗаменитель.ПроцентСкидки;
				СтрокаЗаменНаФорме.СуммаСкидки         = СтрокаЗаменитель.СуммаСкидки;
				СтрокаЗаменНаФорме.СкидкаНаТовар       = СтрокаЗаменитель.СкидкаНаТовар;
				СтрокаЗаменНаФорме.ПроцентСкидкиСтроки = СтрокаЗаменитель.ПроцентСкидкиСтроки;
				СтрокаЗаменНаФорме.СуммаСкидкиСтроки   = СтрокаЗаменитель.СуммаСкидкиСтроки;
				СтрокаЗаменНаФорме.СуммаНДС            = СтрокаЗаменитель.СуммаНДС;
				СтрокаЗаменНаФорме.СуммаВсего          = СтрокаЗаменитель.СуммаВсего;
				
				ИндексСтроки = ИндексСтроки + 1;
			КонецЦикла;
			
			#Если Клиент Тогда
				// Формируем строку для отображения на форме.
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			#КонецЕсли 
		Иначе // Просто пересчитываем значение суммы скидки для текущей строки.
			Если ТекСтрока <> Неопределено Тогда									
				// Подбираем скидку для текущей строки (скидка на товар).
				Если (НЕ НеРассчитыватьСкидки) И (Не ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки) Тогда
					ОбработкаРасчетСкидок.ПодобратьСтрочнуюСкидку(ЭтотОбъект, ТекСтрока);
					ТекСтрока.СкидкаНаТовар  = ОбработкаРасчетСкидок.ТекущаяСкидкаСтроки;
					ТекСтрока.ПроцентСкидкиСтроки = ОбработкаРасчетСкидок.ТекущийПроцентСкидкиСтроки;
					ТекСтрока.СуммаСкидкиСтроки = ОбработкаРасчетСкидок.ТекущаяСуммаСкидкиСтроки;
				КонецЕсли;
				
				// Рассчитываем "шапочную" скидку для текущей строки. За одно рассчитаем общую сумму скидки на документ.
				Если ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСкидкаНаценка].СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная Тогда
					ОбработкаРеквизита(ОбработкаРасчетСкидок.ИмяТабличнойЧасти + ".СуммаСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);
				Иначе
					ОбработкаРеквизита(ОбработкаРасчетСкидок.ИмяТабличнойЧасти + ".ПроцентСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);
				КонецЕсли;
			ИначеЕсли (НЕ НеРассчитыватьСкидки) Тогда
				// Если произошла смена типа скидки, но значение скидки не изменилась, то пересчитывать ТЧ нет необходимости, но
				// сохранить новый тип скидки нужно.
				ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСкидкаНаценка] = ОбработкаРасчетСкидок.ТекущаяСкидкаНаценка;
				
				СуммаИтого = 0;
				СуммаИтого = ЭтотОбъект[ОбработкаРасчетСкидок.ИмяТабличнойЧасти].Итог("СуммаСкидки");
				
				Попытка // Документ может поддерживать только шапочные скидки, а значит в ТЧ не будет реквизита "СуммаСкидкиСтроки".
					СуммаИтого = СуммаИтого + ЭтотОбъект[ОбработкаРасчетСкидок.ИмяТабличнойЧасти].Итог("СуммаСкидкиСтроки")
				Исключение
				КонецПопытки;
				ЭтотОбъект[ОбработкаРасчетСкидок.ИмяРеквизитаСуммаСкидкиНаценки] = СуммаИтого;
			КонецЕсли;
		КонецЕсли;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,,ДопПараметры);
		
	ИначеЕсли Найти(Имя, "ТоварыЗаменители.") = 1 Тогда
		ОбработкаРеквизита(СтрЗаменить(Имя, "ТоварыЗаменители.", "Товары."), ТекСтрока, ЭтаФорма, ДопПараметры);
	ИначеЕсли Имя = "Товары.Номенклатура" ИЛИ Имя = "Товары.ХарактеристикаНоменклатуры" ИЛИ Имя = "Товары.ЕдиницаИзмерения" Тогда
		
		Если НЕ ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		
		Если ТипЗнч(ТекСтрока) = Тип("СтрокаДереваЗначений") Тогда
			Если ТекСтрока.Родитель = Неопределено Тогда
				ДопПараметры.Вставить("ТабличнаяЧастьТовары", ЭтаФорма.Товары.Строки);
			Иначе
				ДопПараметры.Вставить("ТабличнаяЧастьТовары", ТекСтрока.Родитель.Строки);
			КонецЕсли;
		Иначе
			ИмяТЧ = Метаданные.НайтиПоТипу(ТипЗнч(ТекСтрока)).Имя;
			ДопПараметры.Вставить("ТабличнаяЧастьТовары", ЭтотОбъект[ИмяТЧ]);
		КонецЕсли;
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Если ТипЗнч(ТекСтрока) = Тип("СтрокаДереваЗначений") И ТекСтрока.Родитель = Неопределено Тогда
			ТекСтрока.Цена = 0;
			Рез = ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.СтавкаНДС" Тогда
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") И ЗначениеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "КорректировкаЗаказаПокупателя"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗаменаВЗаказеПокупателя(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("ЗаменаВЗаказеПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект, Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//определяем есть ли скидки
	ЕстьСкидка = Ложь;
	
	Если ТоварыЗаменители.Итог("СуммаСкидки") <> 0 ИЛИ ТоварыЗаменители.Итог("СуммаСкидкиСтроки") <> 0 Тогда
		ЕстьСкидка = Истина;
	КонецЕсли;
	
	//Если Не ЕстьСкидка Тогда
	//	ОбластьСкидка = Макет.Область("Скидка");
	//	Макет.Область("Товар").ШиринаКолонки = Макет.Область("Товар").ШиринаКолонки + ОбластьСкидка.ШиринаКолонки;
	//	Макет.УдалитьОбласть(ОбластьСкидка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	//КонецЕсли;
	
	//вывод образца заполнения платёжного поручения
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета");
	ОбластьМакета.Параметры.БанкПолучателя						= Организация.ОсновнойБанковскийСчет.Банк;
	ОбластьМакета.Параметры.БанкПолучателяПредставление			= спПолучитьПредставление(Организация,Новый Структура("Банк"));
	ОбластьМакета.Параметры.БИКБанкаПолучателя					= спПолучитьПредставление(Организация,Новый Структура("БИК"));
	ОбластьМакета.Параметры.СчетБанкаПолучателя					= спПолучитьПредставление(Организация,Новый Структура("КоррСчет"));
	ОбластьМакета.Параметры.ИНН									= спПолучитьПредставление(Организация,Новый Структура("ИНН"));
	ОбластьМакета.Параметры.КПП									= спПолучитьКППДляПечати(Организация, ПодразделениеКомпании);
	ОбластьМакета.Параметры.Получатель							= Организация;
	ОбластьМакета.Параметры.ПолучательПредставление				= спПолучитьПредставление(Организация,Новый Структура("Наименование"));
	ОбластьМакета.Параметры.СчетПолучателя						= спПолучитьПредставление(Организация,Новый Структура("БанковскийСчет"));
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтруктураПредставления=Новый Структура(
		"ИНН,КПП,Наименование,АдресЮридический,ТелефонРабочий",
		"ИНН ","КПП ","","","тел.: "
	);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация,
		СтруктураПредставления, ДополнительныеПараметры);
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = дкПолучитьПредставление(ДокументОснование);
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета.Параметры.ДокументОснованиеПредставление	= "<заказ не выбран>"
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ОбластьМакета.Параметры.Контрагент = ДокументОснование.Контрагент;
		ОбластьМакета.Параметры.КонтрагентПредставление	= спПолучитьПредставление(ДокументОснование.Контрагент,СтруктураПредставления);
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		ОбластьМакета.Параметры.Контрагент = ДокументОснование.ПодразделениеПолучатель;
		ОбластьМакета.Параметры.КонтрагентПредставление	= спПолучитьНаименование(ДокументОснование.ПодразделениеПолучатель);
	КонецЕсли;

	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета            = Макет.ПолучитьОбласть("Строка");
	ОбластьШапкаЗаменителей  = Макет.ПолучитьОбласть("ШапкаЗаменителей");
	ОбластьСтрокаЗаменителей = Макет.ПолучитьОбласть("СтрокаЗаменителей");
	ОбластьПодвал            = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, СуммаСкидки, СуммаНДС, СуммаВсего", ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = Замены;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти, ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		МассивЗаменителей = ТоварыЗаменители.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки));
		
		мсвДопОбластиПодвала = Неопределено;
		Если МассивЗаменителей.Количество() > 0 Тогда
			ТабДокВрем = Новый ТабличныйДокумент;
			ТабДокВрем.ПолеСверху = 0;
			ТабДокВрем.ПолеСнизу  = 0;
			ТабДокВрем.ПолеСлева  = 0;
			ТабДокВрем.ПолеСправа = 0;
			ТабДокВрем.Вывести(ОбластьШапкаЗаменителей);
			Для Каждого СтрокаЗаменителей Из МассивЗаменителей Цикл
				СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаЗаменителей, ЭтотОбъект);
				ОбластьСтрокаЗаменителей.Параметры.Заполнить(СтруктураСтроки);
				ТабДокВрем.Вывести(ОбластьСтрокаЗаменителей);
			КонецЦикла;
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ТабДокВрем);
		КонецЕсли;
		
		//доп. области
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			Если мсвДопОбластиПодвала = Неопределено Тогда
				мсвДопОбластиПодвала = Новый Массив;
			КонецЕсли;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, СуммаСкидки, СуммаНДС, СуммаВсего", ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		Если МассивЗаменителей.Количество()> 0 Тогда
			ТабДокумент.Вывести(ОбластьШапкаЗаменителей);
			Для Каждого СтрокаЗаменителей Из МассивЗаменителей Цикл
				//заполняем данные строки
				СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаЗаменителей, ЭтотОбъект);
				ОбластьСтрокаЗаменителей.Параметры.Заполнить(СтруктураСтроки);
				ТабДокумент.Вывести(ОбластьСтрокаЗаменителей);
				//добавляем итоги
				дкДобавитьИтогиПоСтранице(СтрокаЗаменителей, СтруктураИтоговПоСтранице);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогоПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ТоварыЗаменители.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ТоварыЗаменители.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ЕстьСкидка Тогда
		СуммаСкидки = ТоварыЗаменители.Итог("СуммаСкидки");
		СуммаСкидки = СуммаСкидки + ТоварыЗаменители.Итог("СуммаСкидкиСтроки");
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СуммаСкидки, ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ТоварыЗаменители.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего, ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительОрганизация");
	Исполнитель.ИсполнительОрганизацияПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительОрганизация),"","/ "+ Исполнитель.ИсполнительОрганизацияПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик    = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикКонтрагент");
	Заказчик.ЗаказчикКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикКонтрагент),"","/ "+ Заказчик.ЗаказчикКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЗаменаВЗаказеПокупателя()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Контрагент=Основание.Контрагент;
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Ложь;
			ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Ложь;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Контрагент=Основание.ПодразделениеПолучатель;
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
			ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки       = Истина;
			//СкидкаНаценка     = Справочники.ТипыСкидок.ПустаяСсылка();
		КонецЕсли;
		//А теперь откорректируем ТЧ на предмет того, что в ней должны остаться только текущие резервы
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		             |	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток-ЗаказыПокупателейОстатки.РезервОстаток,0) КАК Количество,
		             |	ЗаказыПокупателейОстатки.СкладКомпании КАК МестоРазмещения,
		             |	ЗаказыПокупателейОстатки.Заказ КАК ЗаказПокупателя,
		             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		             |ИЗ
		             |	РегистрНакопления.ЗаказыПокупателей.Остатки(&НаМомент,Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		             |ГДЕ
		             |	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток-ЗаказыПокупателейОстатки.РезервОстаток, 0) > 0
		             |";
		Запрос.УстановитьПараметр("НаМомент",ТекущаяДата());
		Запрос.УстановитьПараметр("Заказ",Основание);
		Замены.Загрузить(Запрос.Выполнить().Выгрузить());
		Для Каждого СтрокаТоваров Из Замены Цикл
			ОбработкаРеквизита("Товары.Номенклатура", СтрокаТоваров);
			СтрокаТоваров.ИдентификаторСтроки = Новый УникальныйИдентификатор;
			Если (НЕ СтрокаТоваров.Коэффициент = 0) И (НЕ СтрокаТоваров.Коэффициент = 1) Тогда
				СтрокаТоваров.Количество = Окр(СтрокаТоваров.Количество/СтрокаТоваров.Коэффициент, 3);
				ОбработкаРеквизита("Товары.Количество", СтрокаТоваров);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	//Проверим корректность заполнения табличной части
	//Для Каждого СтрокаТоваров Из Замены Цикл
	//	Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
	//		Если ТипЗнч(СтрокаТоваров.ЗаказПокупателя)<>Тип("ДокументСсылка.ЗаказПокупателя") Тогда
	//			Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Вид заказа не соответствует контрагенту.");
	//			Отказ=Истина;
	//		Иначе
	//			Если Контрагент<>СтрокаТоваров.ЗаказПокупателя.Контрагент Тогда
	//				Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Контрагент заказа не соответствует контрагенту документа.");
	//				Отказ=Истина;
	//			КонецЕсли; 
	//		КонецЕсли; 
	//	ИначеЕсли ТипЗнч(Контрагент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
	//		Если ТипЗнч(СтрокаТоваров.ЗаказПокупателя)<>Тип("ДокументСсылка.ЗаказВнутренний") Тогда
	//			Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Вид заказа не соответствует контрагенту.");
	//			Отказ=Истина;
	//		Иначе
	//			Если Контрагент<>СтрокаТоваров.ЗаказПокупателя.ПодразделениеПолучатель Тогда
	//				Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Контрагент заказа не соответствует контрагенту документа.");
	//				Отказ=Истина;
	//			КонецЕсли; 
	//		КонецЕсли; 
	//	КонецЕсли;
	//КонецЦикла; 
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Заказ ИЗ РегистрНакопления.ЗаказыПокупателей ГДЕ Регистратор = &Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДокументТовары.Ссылка.ДокументОснование КАК Заказ,
	|	ДокументТовары.Ссылка.ДокументОснование.СкладКомпании КАК СкладЗаказа,
	|	ДокументТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.Количество*ДокументТовары.Коэффициент КАК Количество
	|ИЗ
	|	Документ.ЗаменаВЗаказеПокупателя.Замены КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка = &Ссылка
	|");
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	РезультатЗапросаПоТоварам = Запрос.Выполнить().Выгрузить();
	
	// Снимаем резервы по заказам (если таковые были)
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект  = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
	НаборЗаписейЗаказыПокупателей.Заказ           = ДокументОснование;
	НаборЗаписейЗаказыПокупателей.ВидОперации     = Перечисления.ВидыОперацийЗаказов.Замена;
	НаборЗаписейЗаказыПокупателей.СкладКомпании   = Неопределено;
	Отказ = НЕ НаборЗаписейЗаказыПокупателей.КорректировкаСписаниемЗаказаПокупателя() ИЛИ Отказ;
	
	//Снимаем распределение заказов покупателя
	РезультатЗакрытияЗаказов = НаборЗаписейЗаказыПокупателей.Выгрузить();
	РезультатЗакрытияЗаказов.Свернуть("Номенклатура, ХарактеристикаНоменклатуры", "Заказано");
	
	КолонкаКоличества = РезультатЗакрытияЗаказов.Колонки.Найти("Заказано");
	КолонкаКоличества.Имя = "Количество";
	
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗакрытияЗаказов;
	НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = ДокументОснование;
	НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
	Отказ = НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
		//Корректировка распределения других заказов, на внутреннем заказе
		НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
		НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
		НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗакрытияЗаказов;
		НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = Неопределено;
		НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = ДокументОснование;
		Отказ=НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаменаВЗаказеПокупателяТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЗаменаВЗаказеПокупателяТовары.Номенклатура КАК Номенклатура,
	|	ЗаменаВЗаказеПокупателяТовары.Количество * ЗаменаВЗаказеПокупателяТовары.Коэффициент КАК Количество,
	|	ЗаменаВЗаказеПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаменаВЗаказеПокупателяТовары.СуммаВсего КАК Сумма
	|ИЗ
	|	Документ.ЗаменаВЗаказеПокупателя.ТоварыЗаменители КАК ЗаменаВЗаказеПокупателяТовары
	|ГДЕ
	|	ЗаменаВЗаказеПокупателяТовары.Ссылка = &Ссылка И 
	|	ЗаменаВЗаказеПокупателяТовары.Количество > 0";
	
	// проводим заказ покупателя
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
	НаборЗаписейЗаказыПокупателей.Контрагент                = Контрагент;
	НаборЗаписейЗаказыПокупателей.Заказ                     = ДокументОснование;
	НаборЗаписейЗаказыПокупателей.СкладКомпании             = ДокументОснование.СкладКомпании;
	НаборЗаписейЗаказыПокупателей.ВидОперации               = Перечисления.ВидыОперацийЗаказов.Замена;
	НаборЗаписейЗаказыПокупателей.Заказывать                = Истина;
	НаборЗаписейЗаказыПокупателей.Резервировать             = Ложь;
	Отказ = НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		СуммаКорректировки = НаборЗаписейЗаказыПокупателей.Итог("Сумма");
		ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, ДокументОснование, СуммаКорректировки);
		Если ТаблицаДолгов.Количество() > 0 Тогда
			СтрокаДолга = ТаблицаДолгов[0];
			СуммаЗаказа       = Формат(СтрокаДолга.СуммаЗаказа,       "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(СтрокаДолга.Валюта);
			Предоплата        = Формат(СтрокаДолга.Предоплата,        "ЧЦ=15; ЧДЦ=2; ЧН=0") + " " + СокрЛП(СтрокаДолга.Валюта);
			ПроцентПредоплаты = Формат(СтрокаДолга.ПроцентПредоплаты, "ЧЦ=15; ЧДЦ=2; ЧН=0") + "%";
			
			Сообщить("Стоимость заказа после замены составит: " + СуммаЗаказа + ", внесена предоплата " + Предоплата + ", установленный процент предоплаты " + ПроцентПредоплаты + ".", СтатусСообщения.Информация);
			
		КонецЕсли;
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.ИмяТабличнойЧасти = "ТоварыЗаменители";
#КонецЕсли

// Модуль документа ВводОстатковТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	ОбязательныеТовары.Вставить("Количество",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	Если ХозОперация <> Справочники.ХозОперации.ВводОстатковТоваров Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	КонецЕсли;
	Если ХозОперация<>Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если НЕ ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;	
			Если ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию 
				или ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхИПроданных Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВводОстатковТоваров","Ввод остатков товаров",Истина);
	Если Константы.СтратегияСписанияПартийТоваровПоДатам.Получить() <> Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
		СписокХозОпераций.Добавить("ВводОстатковТоваровПринятыхНаКомиссию","Ввод остатков товаров принятых на комиссию",Ложь);
	КонецЕсли;
	СписокХозОпераций.Добавить("ВводОстатковТоваровПринятыхИПроданных","Ввод принятых на комиссию и проданных товаров",Ложь);
	СписокХозОпераций.Добавить("ВводОстатковТоваровПереданныхНаКомиссию","Ввод остатков товаров переданных на комиссию",Ложь);
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по колонке "СуммаВсего".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Товары.Итог("СуммаВсего");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию ИЛИ
			 ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию ИЛИ
			 ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПринятыхИПроданных Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			Если ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
				ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			Иначе
				ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			КонецЕсли;
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;		
	ИначеЕсли Имя="СкладКомпании" Тогда
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма);
					Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",СтрокаТоваров,ЭтаФорма) И Рез;
				КонецЦикла;
			КонецЕсли;
			Для Каждого СтрокаТоваров Из Товары Цикл
				Если обЗначениеНеЗаполнено(СтрокаТоваров.Номенклатура) Тогда
					СтрокаТоваров.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
				Иначе
					СтрокаТоваров.Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(СтрокаТоваров.Номенклатура,СкладКомпании);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого стрТовары Из Товары Цикл
				стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЦикла;
			Рез = Истина;
		КонецЕсли;
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("Контрагент",Контрагент);
        КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Если НЕ обЗначениеНеЗаполнено(Контрагент) И Контрагент.ОсвобожденОтНДС Тогда
			Запрос=Новый Запрос("
			|ВЫБРАТЬ ПЕРВЫЕ 1 СпрСтавкиНДС.Ссылка КАК СтавкаБезНДС
			|ИЗ Справочник.СтавкиНДС КАК СпрСтавкиНДС
			|ГДЕ СпрСтавкиНДС.Ставка=0");
			РезультатСтавкаБезНДС=Запрос.Выполнить();
			Если РезультатСтавкаБезНДС.Пустой() Тогда
				ТекСтрока.СтавкаНДС=Неопределено;
			Иначе
				ТекСтрока.СтавкаНДС=РезультатСтавкаБезНДС.Выгрузить().Получить(0).СтавкаБезНДС;
			КонецЕсли;
			ОбработкаРеквизита("Товары.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
		КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл)); 
		ТекСтрока.ЦенаРозничная=ТекЦенаЗакупки+(ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
		Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
			ТекСтрока.ЦенаРозничная=ТекСтрока.ЦенаРозничная*(1+(ТекСтрока.СтавкаНДС.Ставка/100));
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если ((ТекСтрока.Количество*ТекСтрока.Коэффициент)=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=ТекСтрока.СуммаРозничная/(ТекСтрока.Количество*ТекСтрока.Коэффициент);
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена=0 Тогда
			ТекСтрока.ПроцентНаценки=0;
		Иначе
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл)); 
			ТекСтрока.ПроцентНаценки=((ТекСтрока.ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если ХозОперация<>Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
			Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
				Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
					ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную цену (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  ШапкаДокумента - Документ.Ссылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента)
	ТекстЗапроса="
		|ВЫБРАТЬ
		|	ОстаткиТоваров.Номенклатура,
		|	ОстаткиТоваров.Ссылка КАК Партия,
		|	ОстаткиТоваров.ХарактеристикаНоменклатуры,
		|   ОстаткиТоваров.СуммаНДС КАК СуммаНДС,
		|	ОстаткиТоваров.Сумма КАК СуммаВсего,
		|	0 КАК СуммаРозн,
		|	ОстаткиТоваров.Сумма КАК Сумма,
		|	ОстаткиТоваров.Количество*ОстаткиТоваров.Коэффициент КАК Количество
		|ИЗ
		|	Документ.ВводОстатковТоваров.Товары КАК ОстаткиТоваров
		|ГДЕ
		|	  ОстаткиТоваров.Ссылка = &Ссылка
		|	И ОстаткиТоваров.Количество > 0
		|";
		
	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	Запрос.Текст=ТекстЗапроса;
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Документ.Ссылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - Выборка по шапке.
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СтатьяОприходованияТМЦ КАК СтатьяОприходованияТМЦ
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведений баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;

	// определим вид проводки
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваров ИЛИ
		 ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию Тогда
		// Приходуем товар
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейПартии.ЕстьСтавкаНДС=Истина;
		Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваров Тогда
			НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
		Иначе
			НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
		КонецЕсли; 
		НаборЗаписейПартии.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейПартии.РежимПроведения=Режим;
		Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
		
		Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваров Тогда
			// Доходы и расходы
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяОприходованияТМЦ;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= НаборЗаписейПартии.Итог("СуммаУпр");
			НаборЗаписейДиР.Расход					= 0;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		
	ИначеЕсли ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
		// Отдаем товар на комиссию
	   	// получим результат запроса по товарной таблице
		Запрос=Новый Запрос("
			|ВЫБРАТЬ
			|	ВводОстатковТоваровТовары.Номенклатура КАК Номенклатура,
			|	ВводОстатковТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	&Ссылка КАК Партия,
			|	ВводОстатковТоваровТовары.ГТД КАК ГТД,
			|	Сумма(ВводОстатковТоваровТовары.СуммаНДС) КАК СуммаНДС,
			|	Сумма(ВводОстатковТоваровТовары.Сумма) КАК Сумма,
			|	Сумма(ВводОстатковТоваровТовары.СуммаВсего) КАК СуммаВсего,
			|	Сумма(ВводОстатковТоваровТовары.Количество*ВводОстатковТоваровТовары.Коэффициент) КАК Количество
			|ИЗ
			|	Документ.ВводОстатковТоваров.Товары КАК ВводОстатковТоваровТовары
			|ГДЕ
			|	ВводОстатковТоваровТовары.Ссылка=&Ссылка И
			|	ВводОстатковТоваровТовары.Номенклатура.ВидНоменклатуры<>&Услуга
			|СГРУППИРОВАТЬ ПО
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры,
			|	ГТД
			|");
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		//Проведем по регистру отданных на реализацию товаров
		НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартииОтданные.Контрагент=ШапкаДокумента.Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам=Запрос.Выполнить();
		НаборЗаписейПартииОтданные.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейПартииОтданные.ПередаватьНаКомиссиюВесьТовар=Истина;
		НаборЗаписейПартииОтданные.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейПартииОтданные.РежимПроведения=Режим;
		Отказ=НЕ НаборЗаписейПартииОтданные.Приход() ИЛИ Отказ;
		
		// Доходы и расходы
		НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяОприходованияТМЦ;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.ВУпрВалюте				= Истина;
		НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
		НаборЗаписейДиР.Доход					= НаборЗаписейПартииОтданные.Итог("СуммаУпр");
		НаборЗаписейДиР.Расход					= 0;
		Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
	ИначеЕсли ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхИПроданных Тогда
		
		ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
			КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр = ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;	
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		Для каждого СтрокаТоваров Из ШапкаДокумента.Ссылка.Товары Цикл
			НоваяЗапись = НаборЗаписейРеализованныеТовары.Добавить();
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор = ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент = ШапкаДокумента.Контрагент;
			НоваяЗапись.Номенклатура = СтрокаТоваров.Номенклатура;
			НоваяЗапись.ДоговорВзаиморасчетов = ШапкаДокумента.ДоговорВзаиморасчетов;
			НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
			НоваяЗапись.ДокументПередачи =  ШапкаДокумента.Ссылка;
			НоваяЗапись.ГТД = СтрокаТоваров.ГТД;
			НоваяЗапись.ХозОперация = ШапкаДокумента.ХозОперация;
			НоваяЗапись.Количество = СтрокаТоваров.Количество;
			НоваяЗапись.СуммаРегл = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ШапкаДокумента.Дата), 2);
			НоваяЗапись.СуммаУпр = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр), 2);
			НоваяЗапись.СуммаПродажи = НоваяЗапись.СуммаУпр;
			НоваяЗапись.СуммаПродажиРегл = НоваяЗапись.СуммаРегл;
		КонецЦикла; 
		
		// БАЛАНС: Увеличим доход на сумму из реализованных товаров
		// подготовим таблицу движений в разрезе подразделений реализованных товаров
		ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
		ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
		ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение",ОписаниеТипов);
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			Если БалансВедетсяПоПодразделениям Тогда
				СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
			Иначе
				СтрокаСписания.Подразделение = ПодразделениеКомпании;  
			КонецЕсли;
		КонецЦикла;	
		ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
		
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Доход                  = 0;
			НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;	
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЦикла;
	КонецЕсли;
	
	 // если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	// двигаем границу последовательности партий
	ДвигаемГраницу = (ХозОперация=Справочники.ХозОперации.ВводОстатковТоваров ИЛИ 
		ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию И 
		Режим<>РежимПроведенияДокумента.Оперативный);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Ввод остатков товаров"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВводОстатковТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВводОстатковТоваров");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеКонтрагента = спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");

	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВводОстатковТоваров()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если ЭтоНовый() Тогда
		Если Основание<>Неопределено Тогда
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПриходныйСкладскойОрдер") Тогда
				Если (НЕ обЗначениеНеЗаполнено(ДокументОснование.ВладелецТовара)) И 
					 (ТипЗнч(ДокументОснование.ВладелецТовара)=Тип("СправочникСсылка.Контрагенты")) Тогда
					 ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию;
					 Контрагент=ДокументОснование.ВладелецТовара;
				Иначе
					 ХозОперация=Справочники.ХозОперации.ВводОстатковТоваров;
				КонецЕсли;
				ОбработкаРеквизита("Контрагент");
			КонецЕсли;
		КонецЕсли; 
		СтатьяОприходованияТМЦ = Справочники.СтатьиДоходовИРасходов.ВводОстатковТоваров;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию И ЗначениеЗаполнено(СкладКомпании) Тогда
		СкладКомпании = Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию Тогда
		Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
			СтратегияСписанияПартийПоДатам = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
		Иначе
			СтратегияСписанияПартийПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
		КонецЕсли;
		
		Если СтратегияСписанияПартийПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
			Сообщить("При стратегии списания партий по среднему нельзя приходывать комиссионный товар.",СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	Если ХозОперация=Справочники.ХозОперации.ВводОстатковТоваров ИЛИ
		 ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию Тогда
		// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
		РезультатЗапросаПоТоварам=ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект);
		
		// Приходуем товар
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.Контрагент=Контрагент;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.Резервировать=Ложь;
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
		Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;

		// если приходуем товар на розничный склад, то установим розничные цены на этот товар
		Если НЕ Отказ И СкладКомпании.Розничный И ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
			НаборЗаписейЦены=Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейЦены.Контрагент=Неопределено;
			НаборЗаписейЦены.ИмяРеквизитаЦена="ЦенаРозничная";
			НаборЗаписейЦены.ТипЦен=СкладКомпании.ТипЦенРозничнойТорговли;
			НаборЗаписейЦены.ПодразделениеКомпании = СкладКомпании.Подразделение;
			НаборЗаписейЦены.УстанавливатьЦеныУслуг=Ложь;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
		// установим цены контрагентов, если, конечно контрагент имеется
		Если НЕ Отказ И ТипЦен.РегистрироватьЦеныПоПриходу И (НЕ обЗначениеНеЗаполнено(Контрагент)) И НЕ ТипЦен.Рассчитывается Тогда
			НаборЗаписейЦены=Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейЦены.Контрагент=Контрагент;
			НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
			НаборЗаписейЦены.ТипЦен=ТипЦен;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
		// установим закупочные цены контрагентов, если, конечно контрагент имеется
		Если НЕ Отказ И ПодразделениеКомпании.ФормироватьЗакупочнуюЦену И НЕ Справочники.ТипыЦен.ОсновнойТипЦенЗакупки.Рассчитывается Тогда
			НаборЗаписейЦены=Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейЦены.Контрагент=Неопределено;
			НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
			НаборЗаписейЦены.ТипЦен=Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
		//установим нормативные цены если это разрешено
		Если НЕ Отказ И ПодразделениеКомпании.ФормироватьНормативнуюЦену И НЕ Справочники.ТипыЦен.НормативнаяЦена.Рассчитывается Тогда
			НаборЗаписейЦены=Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейЦены.Контрагент=Неопределено;
			НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
			НаборЗаписейЦены.ТипЦен=Справочники.ТипыЦен.НормативнаяЦена;
			Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
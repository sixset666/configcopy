// Модуль документа Рассылка

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;							// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;				// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура отправки электронной рассылки
//
Процедура ЭлектроннаяРассылкаОтправить() Экспорт
	
	ЕстьОшибки	= Ложь;
	Если обЗначениеНеЗаполнено(УчетнаяЗапись) Тогда
			Сообщить("Не указана учетная запись отправителя!",СтатусСообщения.Важное);
			ЕстьОшибки = Истина;
	КонецЕсли;
		
	Если обЗначениеНеЗаполнено(Тема) Тогда
		Сообщить("Не указана тема письма!",СтатусСообщения.Важное);
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
	Состояние("Выполняется создание электронных писем ...");
	#КонецЕсли
	Для Каждого Адресат Из Получатели  Цикл
		Если (НЕ Адресат.Отказ)И (НЕ обЗначениеНеЗаполнено(Адресат.Получатель)) Тогда	
			ЭлектронноеПисьмо	= Документы.ЭлектронноеПисьмо.СоздатьДокумент();
			ЭлектронноеПисьмо.Заполнить(Неопределено);
			ЭлектронноеПисьмо.ХозОперация				= Справочники.ХозОперации.ЭлектронноеПисьмо;
			ЭлектронноеПисьмо.ДокументОснование			= Ссылка;
			ЭлектронноеПисьмо.УчетнаяЗапись				= УчетнаяЗапись;
			ЭлектронноеПисьмо.ОтправительПредставление	= СокрЛП(ОтправительПредставление);			
			
			стрПолучатель								= ЭлектронноеПисьмо.Получатели.Добавить();
			стрПолучатель.АдресЭлектроннойПочты			= Адресат.АдресЭлектроннойПочты;
			стрПолучатель.СсылкаНаОбъект				= Адресат.Получатель;
			стрПолучатель.КодГруппыАдреса				= Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
			стрПолучатель.Представление					= Адресат.Получатель.Наименование;
			ЭлектронноеПисьмо.Тема						= СокрЛП(Тема);
			ЭлектронноеПисьмо.ТекстПисьма				= СформироватьСообщениеПоШаблону(ТекстПисьма,Адресат.Получатель);
			ЭлектронноеПисьмо.ФорматТекста				= ФорматТекста;
			ЭлектронноеПисьмо.Комментарий				= Строка(Ссылка);
			// Свойство устарело и более не поддерживается.
			//ЭлектронноеПисьмо.УведомитьОДоставке 		= УведомитьОДоставке;
			ЭлектронноеПисьмо.УведомитьОДоставке 		= Ложь;
			ЭлектронноеПисьмо.УведомитьОПрочтении 		= УведомитьОПрочтении;
			ЭлектронноеПисьмо.ЕстьВложения				= ?(ПрикрепленныеФайлы.Количество()>1,Истина, Ложь);
			//Перенос вложений
			ЭлектронноеПисьмо.ПрикрепленныеФайлы.Загрузить(ПрикрепленныеФайлы.Выгрузить());
			//
			ЭлектронноеПисьмо.ПолучателиПредставление	= ЭлектронноеПисьмо.ПредставлениеПолучателей();
			ЭлектронноеПисьмо.ГруппаПисем				= Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие",Истина,,УчетнаяЗапись);
			Попытка
				ЭлектронноеПисьмо.Записать();
			Исключение
				Сообщить(ОписаниеОшибки());
				Возврат;
			КонецПопытки;
			Адресат.ЭлектронноеПисьмо = ЭлектронноеПисьмо.Ссылка; 
			#Если Клиент Тогда
			Состояние("Выполняется рассылка почты для: " + Адресат.Получатель);
			#КонецЕсли
			//Отправка
			Если (НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтправитьВнешнимКлиентом")) И
				(эпПолучитьДоступныеУчетныеЗаписи("ТРАНСПОРТ").НайтиПоЗначению(УчетнаяЗапись) = Неопределено) Тогда
			#Если Клиент Тогда
			Предупреждение("У пользователя нет прав на отправку писем в данной учетной записи!
			|Обратитесь к администратору.",, "Внимание!");
			#КонецЕсли
			Возврат;
		КонецЕсли;
	
		// отправим письмо по эл. почте
		
		// на случай провала отправки все равно запишем письмо в папку исходящее	
		ЭлектронноеПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.Исходящее;
		ЭлектронноеПисьмо.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие",Истина,,ЭлектронноеПисьмо.УчетнаяЗапись);
		Если НЕ ЭлектронноеПисьмо.ДополнительныеСвойства.Свойство("ОтправитьВнешнимКлиентом") тогда
			Если ЭлектронноеПисьмо.УчетнаяЗапись=Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка и обЗначениеНеЗаполнено(УчетнаяЗапись.SMTPСервер) Тогда
				#Если Клиент Тогда
				МассивПисем = Новый Массив;
				МассивПисем.Добавить(ЭлектронноеПисьмо); 			
				Состояние("Отправка письма через стандартный сервер SMTP");
				мсвОшибки = Новый Массив;
				Если зфОтправитьПисьмаЧерезСтандартныйSMTP(ЭлектронноеПисьмо.ОтправительПредставление, МассивПисем,, мсвОшибки) Тогда
				Иначе
					Если мсвОшибки.Количество()<>0 Тогда
						Для каждого ТекОшибка Из мсвОшибки Цикл 
							Сообщить("" + ТекОшибка, СтатусСообщения.Важное);
						КонецЦикла;	
					КонецЕсли;				
				КонецЕсли;
				Оповестить("ОбновитьДеревоГрупп", "Пересчитать");
				#КонецЕсли
			Иначе			
				СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
				СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(ЭлектронноеПисьмо.УчетнаяЗапись, СтруктураРежимовОбмена);
				Если СоединениеСПочтовымСервером <> Неопределено Тогда
					Если эпОтправитьПисьмо(ЭлектронноеПисьмо, СоединениеСПочтовымСервером) Тогда
						эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
					Иначе
						эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
					КонецЕсли;	
				КонецЕсли;
			КонецЕсли;
		Иначе // отправляем через внешний клиент
			#Если Клиент Тогда
			ПочтовоеСообщение = Новый ПочтовоеСообщение();
			ПочтовоеСообщение.Отправитель = ЭлектронноеПисьмо.ОтправительПредставление; 
			ПочтовоеСообщение.Тема        = ЭлектронноеПисьмо.Тема;
			ПочтовоеСообщение.Текст       = ЭлектронноеПисьмо.ТекстПисьма;
			
			// Заполняем получателей.
			Для Каждого ТекПолучатель Из ЭлектронноеПисьмо.Получатели Цикл 
				Если ТекПолучатель.КодГруппыАдреса=Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому Тогда
					ПочтовоеСообщение.Получатели.Добавить(ТекПолучатель.АдресЭлектроннойПочты);	
				ИначеЕсли ТекПолучатель.КодГруппыАдреса=Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии Тогда
					ПочтовоеСообщение.Копии.Добавить(ТекПолучатель.АдресЭлектроннойПочты);	
				Иначе
					ПочтовоеСообщение.СлепыеКопии.Добавить(ТекПолучатель.АдресЭлектроннойПочты);
				КонецЕсли;
			КонецЦикла;
			
			// Добавляем вложения.
			Для Каждого ТекФайл Из ЭлектронноеПисьмо.ПрикрепленныеФайлы Цикл  	
				ПочтовоеСообщение.Вложения.Добавить(ТекФайл.Файл.Получить(), ТекФайл.Имя);		
			КонецЦикла;
			
			// Пытаемся отправить.
			ЭлектронноеПисьмо.СообщениеОтправлено = Ложь;
			Почта = Новый Почта();
			Попытка
				Почта.Подключиться();
			Исключение
				СтруктураОшибки = ИнформацияОбОшибке();
				Сообщить("Ошибка при подключении к почтовому серверу! Причина: " + СтруктураОшибки.Причина, СтатусСообщения.Важное);
				Почта = "";
				ПочтовоеСообщение = "";
				Возврат;
			КонецПопытки;
			
			Попытка
				Почта.Послать(ПочтовоеСообщение);
				ЭлектронноеПисьмо.СообщениеОтправлено = Истина;
			Исключение
				СтруктураОшибки = ИнформацияОбОшибке();
				Сообщить("Ошибка при отправке сообщения! Причина: " + СтруктураОшибки.Причина, СтатусСообщения.Важное);
				Возврат;
			КонецПопытки;
			
			Почта = "";
			ПочтовоеСообщение = "";
			
			Если ЭлектронноеПисьмо.СообщениеОтправлено Тогда
				Сообщить("Электронное письмо отправлено!", СтатусСообщения.Информация);	
			КонецЕсли;
			#КонецЕсли
		КонецЕсли;
	
		#Если Клиент Тогда
		Оповестить("ОбновитьДеревоГрупп", "Пересчитать");
		#КонецЕсли
		КонецЕсли;
		//---	
	КонецЦикла;
	ЭтотОбъект.Записать();
КонецПроцедуры

// Процедура для отправления СМС-сообщения
//
Процедура РассылкаSMSОтправить() Экспорт
	Если обЗначениеНеЗаполнено(ТекстПисьма) Тогда
		#Если Клиент Тогда
		Предупреждение("Не указан текст сообщения!");
		#КонецЕсли
		Возврат;
	КонецЕсли;
	#Если Клиент Тогда
	Состояние("Создаются СМС...");
	#КонецЕсли
	Для Каждого Адресат Из Получатели  Цикл
		Если (НЕ Адресат.Отказ)И (НЕ обЗначениеНеЗаполнено(Адресат.Телефон)) Тогда
			Если НЕ Адресат.Получатель.СогласиеНаПолучениеSMS Тогда
				#Если Клиент Тогда
					Сообщить("Контрагент "+Адресат.Получатель+" не дал согласие на получение SMS!", СтатусСообщения.Важное);
				#КонецЕсли
				Продолжить;
			КонецЕсли;
			СМС	= Документы.SMS.СоздатьДокумент();
			СМС.Заполнить(Неопределено);
			СМС.Организация				= Организация;
			СМС.ПодразделениеКомпании   = ПодразделениеКомпании; 
			СМС.ХозОперация				= Справочники.ХозОперации.SMS;
			СМС.ДокументОснование		= Ссылка;
			СМС.НомерОтправителя		= НомерОтправителяSMS;
			СМС.Входящее_Исходящее		= Перечисления.ТипыСообщений.Исходящее;
			стрПолучатель				= СМС.Получатели.Добавить();
			стрПолучатель.Контрагент	= Адресат.Получатель;
			стрПолучатель.НомерТелефона	= Адресат.Телефон;
			СМС.ТекстСообщения			= СформироватьСообщениеПоШаблону(ТекстПисьма,Адресат.Получатель);
			СМС.НачалоОтправки 			= ТекущаяДата();
			Попытка
				СМС.Записать();
			Исключение
				Сообщить(ОписаниеОшибки());
				Возврат;
			Конецпопытки;
			Адресат.SMS	= СМС.Ссылка;
			СМС.ОтправитьSMS();
		КонецЕсли;
	КонецЦикла;
	ЭтотОбъект.Записать();
КонецПроцедуры

// Функция формирования сообщения
//
Функция СформироватьСообщениеПоШаблону(Шаблон,Контрагент)
	Возврат зфЗащищенныеФункцииСервер.РассылкаСформироватьСообщениеПоШаблону(Шаблон,Контрагент);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы получателей
	ОбязательныеПолучатели=Новый Структура();
	ОбязательныеПолучатели.Вставить("Получатель",3);
	ОбязательныеПолучатели.Вставить("АдресЭлектроннойПочты",2);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Тема",1);
	ОбязательныеРеквизиты.Вставить("Реклама",1);
	Если ХозОперация = Справочники.ХозОперации.ЭлектроннаяРассылка Тогда
		ОбязательныеРеквизиты.Вставить("УчетнаяЗапись",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Получатели",ОбязательныеПолучатели);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЭлектроннаяРассылка","Электронная рассылка",Истина);
	СписокХозОпераций.Добавить("ПочтоваяРассылка","Почтовая рассылка",Ложь);
	СписокХозОпераций.Добавить("РассылкаSMS","Рассылка SMS",Ложь);
	СписокХозОпераций.Добавить("Телемаркетинг","Телемаркетинг",Ложь);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя = "Анкета" Тогда
		Если НЕ обЗначениеНеЗаполнено(Анкета) Тогда
			Тема = Анкета.НаименованиеАнкеты;	
		КонецЕсли;
	ИначеЕсли Имя = "Получатели.Получатель" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Получатель) Тогда
			ТекСтрока.Отказ = обПолучитьЗначениеСвойства(ТекСтрока.Получатель,ПланыВидовХарактеристик.СвойстваОбъектов.ОтказОтРассылки,ЛОЖЬ);
			Если ХозОперация = Справочники.ХозОперации.ЭлектроннаяРассылка Тогда
				ТекСтрока.АдресЭлектроннойПочты = киПолучитьПредставлениеКИ(ТекСтрока.Получатель,Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий);
			ИначеЕсли ХозОперация = Справочники.ХозОперации.РассылкаSMS Тогда
				ТекСтрока.Телефон = киПолучитьПредставлениеКИ(ТекСтрока.Получатель,Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
			ИначеЕсли ХозОперация = Справочники.ХозОперации.Телемаркетинг Тогда
				ТекСтрока.Телефон = киПолучитьПредставлениеКИ(ТекСтрока.Получатель,Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
			ИначеЕсли ХозОперация = Справочники.ХозОперации.ПочтоваяРассылка Тогда
				ТекСтрока.Адрес = киПолучитьПредставлениеКИ(ТекСтрока.Получатель,Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Имя = "ПолучателиТелемаркетинг.Получатель" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Получатель) Тогда
			ТекСтрока.Отказ = обПолучитьЗначениеСвойства(ТекСтрока.Получатель,ПланыВидовХарактеристик.СвойстваОбъектов.ОтказОтРассылки,ЛОЖЬ);
			ТекСтрока.Телефон = киПолучитьПредставлениеКИ(ТекСтрока.Получатель,Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
		Иначе	
			ТекСтрока.Телефон = "";
		КонецЕсли;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
//
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
 Возврат;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события установки нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события ввода на основании
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;

	Если ТипЗнч(Основание)=Тип("ДокументСсылка.Событие") Тогда
		Реклама	= Основание.ИсточникИнформации;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Состояние			= Перечисления.СостоянияРассылок.ВРаботе;
		НомерОтправителяSMS	= Константы.смсИмяПользователяПоУмолчанию.Получить();
	КонецЕсли;

	Если обЗначениеНеЗаполнено(ФорматТекста) Тогда
		ФорматТекста=Перечисления.ФорматТекста.ПростойТекст;
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	// Надо обновить состояние документа
	Если НЕ Отказ Тогда
		Если НЕ орЖурналСостояний(Ссылка) Тогда
			Отказ=Истина;
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

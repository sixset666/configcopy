// Модуль документа СписаниеАктивов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ВалютаКурсДок Экспорт;        // Кэш валюты и курса документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Получает процент амортизации для актива по его эксплуатационным параметрам
//
// Параметры:
//  Актив                   – СправочникСсылка.ПрочиеАктивы                   – Прочий актив (амортизируемый),
//	ТипЭксплуатации         – СправочникСсылка.ТипыЭксплуатации               – Тип эксплуатации прочего актива,
//  БалансоваяСтоимость     – Число                                           – Балансовая стоимость актива,
//	НакопленнаяАмортизация  – Число                                           – Уже накопленная сумма амортизации актива
//
// Возвращаемое значение:
//	ПроцентАмортизации - Число - Процент амортизации актива
Функция ПолучитьПроцентАмортизации(Актив, ТипЭксплуатации, БалансоваяСтоимость, НакопленнаяАмортизация)
	// по умолчанию пусть будет всё по нулям
	ПроцентАмортизации = 0;
		
	// установлен флажок "консервирование" - не начисляем
	Если ТипЭксплуатации.Консервирование Тогда
		Возврат ПроцентАмортизации;
	КонецЕсли;
	
	// получим все необходимые параметры и коэффициенты
	ДатаВвода              = Актив.ДатаВводаВЭксплуатацию;
	СрокИспользования      = Актив.СрокПолезногоИспользования;
	КоэффициентАмортизации = ?(Актив.КоэффициентУскорения = 0, 1, Актив.КоэффициентУскорения);
	
	// посмотрим, если срок использования истек,
	// то нужно полностью самортизировать актив, независимо от его текущей стоимости
	Если ДобавитьМесяц(ДатаВвода, СрокИспользования) < Дата Тогда
		Возврат (1 - НакопленнаяАмортизация / БалансоваяСтоимость) * 100;
	КонецЕсли;
		
	// и собственно расчет по методам
	
	//ЛИНЕЙНЫЙ
	Если ТипЭксплуатации.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизации.Линейный Тогда
		ПроцентАмортизации = 100 / СрокИспользования;
					
	//УМЕНЬШАЕМОГО ОСТАТКА
	ИначеЕсли ТипЭксплуатации.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизации.УменьшаемогоОстатка Тогда
		Если НакопленнаяАмортизация / БалансоваяСтоимость < 0.8 Тогда
			ПроцентАмортизации = КоэффициентАмортизации * (1 - НакопленнаяАмортизация / БалансоваяСтоимость) /СрокИспользования * 100;
		Иначе
			ПроцентАмортизации = 100 / СрокИспользования;
		КонецЕсли;	
						
	//ПО СУММЕ ЧИСЕЛ ЛЕТ
	ИначеЕсли ТипЭксплуатации.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизации.ПоСуммеЧиселЛет Тогда
		СрокИспользованияЛет = Окр(СрокИспользования /12);
		СуммаЧиселЛет = Окр((1 + СрокИспользованияЛет) * СрокИспользованияЛет /2);
		НомерТекущегоГода = Год(Дата) - Год(ДатаВвода);
		Если Месяц(Дата) > Месяц(ДатаВвода) Тогда
			НомерТекущегоГода = НомерТекущегоГода + 1;
		ИначеЕсли (Месяц(Дата) = Месяц(ДатаВвода)) И (День(КонецМесяца(Дата)) > День(ДатаВвода)) Тогда
			НомерТекущегоГода = НомерТекущегоГода + 1;
		КонецЕсли;
		ОсталосьЛет = СрокИспользованияЛет - НомерТекущегоГода;
		ПроцентАмортизации = 100 * (ОсталосьЛет + 1) /СуммаЧиселЛет /12;
				
	//ДРУГОЙ
	Иначе
		//1) 100-процентный при вводе - не начисляем,
		//2) по объему выработки делаем после ввода, собственно, выработки в ТЧ Активы
	КонецЕсли;
	
	//мы не можем позволить отрицательную остаточную стоимость
	Если БалансоваяСтоимость - НакопленнаяАмортизация - (ПроцентАмортизации * БалансоваяСтоимость / 100) < 0 Тогда
		ПроцентАмортизации = (1 - НакопленнаяАмортизация / БалансоваяСтоимость) * 100;
	КонецЕсли;
	
	Возврат ПроцентАмортизации;
КонецФункции // ПолучитьПроцентАмортизации()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы активов
	ОбязательныеАктивы = Новый Структура();
	ОбязательныеАктивы.Вставить("ПрочийАктив", 3);
	ОбязательныеАктивы.Вставить("СуммаАмортизации", 1);
						
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента", 1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
    ОбязательныеРеквизиты.Вставить("Активы", ОбязательныеАктивы);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только в случае
	// если все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
		
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
//
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Амортизация", "Амортизация", Истина);
		
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()	

// Возвращает суммы амортизации сгруппированные по
// статьям доходов и расходов из ТЧ Активы
//
// Возвращаемое значение:
// возвращает таблицу значений
Функция ПолучитьРезультатЗапросаПоСтатьям(ШапкаДокумента)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА АмортизацияАктивы.СтатьяРасходовПоАмортизации = &ПустаяСсылка
	               |			ТОГДА &Амортизация
	               |		ИНАЧЕ АмортизацияАктивы.СтатьяРасходовПоАмортизации
	               |	КОНЕЦ КАК СтатьяРасходовПоАмортизации,
	               |	СУММА(АмортизацияАктивы.СуммаАмортизации) КАК СуммаАмортизации
	               |ИЗ
	               |	Документ.Амортизация.Активы КАК АмортизацияАктивы
	               |ГДЕ
	               |	АмортизацияАктивы.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА АмортизацияАктивы.СтатьяРасходовПоАмортизации = &ПустаяСсылка
	               |			ТОГДА &Амортизация
	               |		ИНАЧЕ АмортизацияАктивы.СтатьяРасходовПоАмортизации
	               |	КОНЕЦ";
	Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("Амортизация", Справочники.СтатьиДоходовИРасходов.Амортизация);
	Запрос.УстановитьПараметр("ПустаяСсылка", Справочники.СтатьиДоходовИРасходов.ПустаяСсылка());
	
	Возврат Запрос.Выполнить();
КонецФункции //ПолучитьРезультатЗапросаПоСтатьям()

// сформирует свой результат запроса по активам
//
// Возвращаемое значение:
//  Возвращает таблицу значений, содержащую остатки по активу.
//
Функция ПолучитьРезультатЗапросаПоАктивам()
	//получаем ТЗ остатков активов и ТЗ ТЧ
	НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
	НаборЗаписейЭксплуатация.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЭксплуатация.ДатаДвижений = КонецМесяца(Дата);
	НаборЗаписейЭксплуатация.ПодразделениеКомпании = ПодразделениеКомпании;
	ТаблОстаткиАктивов = дкПолучитьОстаткиАктивов(ПодразделениеКомпании, , Активы.ВыгрузитьКолонку("ПрочийАктив"), КонецМесяца(Дата));
	ТаблАктивы = Активы.Выгрузить();
	
	//добавляем амортизацию
	Для каждого ТекСтрокаАктива Из ТаблОстаткиАктивов Цикл
		СтрокаАктива = ТаблАктивы.Найти(ТекСтрокаАктива.Актив, "ПрочийАктив");
		Если СтрокаАктива = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ТекСтрокаАктива.СуммаОбслуживания  = 0;
		ТекСтрокаАктива.БалансоваяСтоимость = 0;
		ТекСтрокаАктива.Амортизация = СтрокаАктива.СуммаАмортизации;
	КонецЦикла;
	
	Возврат ТаблОстаткиАктивов;
КонецФункции // ПолучитьРезультатЗапросаПоАктивам()

// Расчет общей суммы амортизации активов
//
// Возвращаемое значение:
//  Возвращает итог по клонке "СуммаАмортизации".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Активы.Итог("СуммаАмортизации");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, 
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "Дата" Тогда
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		//пересчитаем ТЧ
		Для каждого ТекСтрока Из Активы Цикл
			ОбработкаРеквизита("Активы.Актив", ТекСтрока, ЭтаФорма);
		КонецЦикла;
		Возврат Истина;
	
	ИначеЕсли Имя = "ПодразделениеКомпании" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
				
		#Если Клиент Тогда
		//здесь будет перезаполнена ТЧ Активы
		флПерезаполнить = Истина;
		
		Если Вопрос("Перезаполнить активами текущего подразделения <"+СокрЛП(ПодразделениеКомпании)+"> ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			флПерезаполнить = Ложь;
		КонецЕсли;
		
		Если флПерезаполнить Тогда
			Активы.Очистить();
			Результат = дкКоманднаяПанельАктивыЗаполнение(ЭтаФорма, Активы, Новый Структура("Имя", "ЗаполнитьАктивамиПодразделения"));
			Если ТипЗнч(Результат) = Тип("ТаблицаЗначений") Тогда
				ЗаполнитьАктивамиПодразделения(Результат, ЭтаФорма);
			КонецЕсли;	
		КонецЕсли;	
		#КонецЕсли
	    Возврат Истина;
		
	ИначеЕсли Имя = "ВалютаДокумента" И (ВалютаКурсДок.Валюта <> ВалютаДокумента ИЛИ ВалютаКурсДок.Курс <> КурсДокумента) Тогда
		ВалютаКурсДок.Валюта = ВалютаДокумента;
		ВалютаКурсДок.Курс = КурсДокумента;
		Возврат Истина;
		
	ИначеЕсли Имя = "Активы.Актив" Тогда
		// получим остаточную стоимость из регистра
		ДатаРасчета = ?(Дата = Дата("00010101"), ТекущаяДата(), Дата);
		МоментВремени = ?(Проведен, Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая), КонецМесяца(ДатаРасчета));
		ТаблАктив = дкПолучитьОстаткиАктивовНеАмортизированных(МоментВремени, ПодразделениеКомпании, , ТекСтрока.ПрочийАктив);
		Если ТаблАктив.Количество() = 0 Тогда
			#Если Клиент Тогда
				Сообщить("Прочий актив """ + СокрЛП(ТекСтрока.ПрочийАктив) + """. Подразделение """ + СокрЛП(ПодразделениеКомпании) + """. Не обнаружен или полностью самортизирован", СтатусСообщения.Внимание);
			#КонецЕсли
			ТекСтрока.ПрочийАктив = Справочники.ПрочиеАктивы.ПустаяСсылка();
			ТекСтрока.ТипЭксплуатации = Справочники.ТипыЭксплуатации.ПустаяСсылка();
			ТекСтрока.БалансоваяСтоимость    = 0;
			ТекСтрока.НакопленнаяАмортизация = 0;
			ТекСтрока.ПроцентАмортизации     = 0;
		Иначе
			ТекСтрока.ТипЭксплуатации = ТаблАктив[0].ТипЭксплуатации;
			ТекСтрока.СтатьяРасходовПоАмортизации = ТекСтрока.ТипЭксплуатации.СтатьяРасходовПоАмортизации;
			Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				ТекСтрока.БалансоваяСтоимость    = ТаблАктив[0].БалансоваяСтоимостьРегл;
				ТекСтрока.НакопленнаяАмортизация = ТаблАктив[0].АмортизацияРегл;
			Иначе
				ТекСтрока.БалансоваяСтоимость    = обПересчет(ТаблАктив[0].БалансоваяСтоимость, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), МоментВремени, КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
				ТекСтрока.НакопленнаяАмортизация = обПересчет(ТаблАктив[0].Амортизация, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(КурсВалютыУпр), МоментВремени, КурсВалютыУпр), ВалютаДокумента, КурсДокумента);
			КонецЕсли; 
			ТекСтрока.ПроцентАмортизации = ПолучитьПроцентАмортизации(ТекСтрока.ПрочийАктив, ТекСтрока.ТипЭксплуатации, ТекСтрока.БалансоваяСтоимость, ТекСтрока.НакопленнаяАмортизация);
		КонецЕсли;
		Возврат ОбработкаРеквизита("Активы.ПроцентАмортизации", ТекСтрока);
				
	ИначеЕсли Имя = "Активы.ПроцентАмортизации" Тогда
		ТекСтрока.СуммаАмортизации = ТекСтрока.БалансоваяСтоимость * ТекСтрока.ПроцентАмортизации / 100;
		ТекСтрока.ОстаточнаяСтоимость = ТекСтрока.БалансоваяСтоимость - ТекСтрока.НакопленнаяАмортизация - ТекСтрока.СуммаАмортизации;
		Возврат Истина;
		
	ИначеЕсли Имя = "Активы.СуммаАмортизации" Тогда
		Если ТекСтрока.БалансоваяСтоимость = 0 Тогда
			ТекСтрока.СуммаАмортизации   = 0;
			ТекСтрока.ПроцентАмортизации = 0;
		Иначе
			ТекСтрока.ПроцентАмортизации = ТекСтрока.СуммаАмортизации / ТекСтрока.БалансоваяСтоимость * 100;
		КонецЕсли;
		ТекСтрока.ОстаточнаяСтоимость = ТекСтрока.БалансоваяСтоимость - ТекСтрока.НакопленнаяАмортизация - ТекСтрока.СуммаАмортизации;
		Возврат Истина;
		
	ИначеЕсли Имя = "Активы.ОстаточнаяСтоимость" Тогда
		СуммаАмортизации = ТекСтрока.БалансоваяСтоимость - ТекСтрока.НакопленнаяАмортизация - ТекСтрока.ОстаточнаяСтоимость;
		Если СуммаАмортизации >= 0 Тогда
			ТекСтрока.СуммаАмортизации = СуммаАмортизации;
			Возврат ОбработкаРеквизита("Активы.СуммаАмортизации", ТекСтрока);
		Иначе
			ТекСтрока.ОстаточнаяСтоимость = ТекСтрока.БалансоваяСтоимость - ТекСтрока.НакопленнаяАмортизация - ТекСтрока.СуммаАмортизации;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя = "Активы.Выработка" Тогда
		Если ТекСтрока.ПрочийАктив.Пустая() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если ТекСтрока.ПрочийАктив.РесурсВыработки = 0 Тогда
			Сообщить("Прочий актив """ + СокрЛП(ТекСтрока.ПрочийАктив) + """. Не указан ресурс выработки (карточка актива)!");
			ПроцентАмортизации  = 0;
			ТекСтрока.Выработка = 0;
		Иначе
			ПроцентАмортизации = ТекСтрока.Выработка / ТекСтрока.ПрочийАктив.РесурсВыработки;
		КонецЕсли;
				
		ТекСтрока.ПроцентАмортизации  = ПроцентАмортизации * 100;
		Возврат ОбработкаРеквизита("Активы.ПроцентАмортизации", ТекСтрока);
		
	ИначеЕсли Имя = "Активы.ТипЭксплуатации" Тогда
		Если ТекСтрока.ПрочийАктив.Пустая() Тогда
			Возврат Ложь;
		Иначе
			ТекСтрока.СтатьяРасходовПоАмортизации = ТекСтрока.ТипЭксплуатации.СтатьяРасходовПоАмортизации;
			Возврат Истина;
		КонецЕсли;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
// Заполнение ТЧ Активы активами подразделения, которым нужна амортизация
// Параметры
// Результат 	- ТаблицаЗначений 	- Результат работы функции дкКоманднаяПанельАктивыЗаполнение().
// ЭтаФорма 	- Форма 			- Ссылка на форму документа.
Процедура ЗаполнитьАктивамиПодразделения(Результат, ЭтаФорма) Экспорт
	ВсегоСтрок = Результат.Количество();
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПрочиеАктивыВЭксплуатации.ПрочийАктив КАК ПрочийАктив
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	|ГДЕ
	|	ПрочиеАктивыВЭксплуатации.Период МЕЖДУ &НачПериода И &КонПериода И 
	|	(ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ВводВЭксплуатацию ИЛИ 
	|	ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ВводВЭксплуатациюАвтомобилей ИЛИ 
	|	ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.Амортизация ИЛИ 
	|	(ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ВводОстатковПрочихАктивов И 
	|	(ПрочиеАктивыВЭксплуатации.ПрочийАктив.ДатаВводаВЭксплуатацию МЕЖДУ &НачПериода И &КонПериода ИЛИ 
	|	 ПрочиеАктивыВЭксплуатации.ПрочийАктив.ДатаВводаВЭксплуатацию = ДатаВремя(1, 1, 1)))) И 
	|	ПрочиеАктивыВЭксплуатации.ПрочийАктив В (&ВыбранныеАктивы)
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеАктивыВЭксплуатации.ПрочийАктив";
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ВыбранныеАктивы", Результат.ВыгрузитьКолонку("Актив"));
	Запрос.УстановитьПараметр("НачПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонецМесяца(Дата)));
	// сюда выгружаются те активы, которым не нужна амортизация
	РезультатПроверкиАктивов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПрочийАктив");
	
	Для Каждого СтрокаАктив Из Результат Цикл
		// в тч добавляются только нужные активы
		Если РезультатПроверкиАктивов.Найти(СтрокаАктив.Актив) = Неопределено Тогда
			НоваяСтрока = Активы.Добавить();
			НоваяСтрока.ПрочийАктив = СтрокаАктив.Актив;
			ОбработкаРеквизита("Активы.Актив", НоваяСтрока, ЭтаФорма);						
			Состояние("Загружено " + СокрЛП(НоваяСтрока.НомерСтроки) + " из " + СокрЛП(ВсегоСтрок));
		КонецЕсли;	
	КонецЦикла;	
	
	дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);

КонецПроцедуры
#КонецЕсли

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Амортизация"
//
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  Возвращает сформированный табличный документ
//
Функция ПечатьАмортизация(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Амортизация");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеПодразделения = спПолучитьНаименование(ПодразделениеКомпании);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,ОстаточнаяСтоимость",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Активы;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.АктивНаименование		= спПолучитьНаименование(СтрокаТабличнойЧасти.ПрочийАктив);
		ОбластьМакета.Параметры.Код						= СтрокаТабличнойЧасти.ПрочийАктив.ИнвентарныйНомер;
		ОбластьМакета.Параметры.БалансоваяСтоимость		= Формат(СтрокаТабличнойЧасти.БалансоваяСтоимость,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.НакопленнаяАмортизация	= Формат(СтрокаТабличнойЧасти.НакопленнаяАмортизация,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаАмортизации		= Формат(СтрокаТабличнойЧасти.СуммаАмортизации,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ОстаточнаяСтоимость		= Формат(СтрокаТабличнойЧасти.ОстаточнаяСтоимость,ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,ОстаточнаяСтоимость",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги	
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОстаточнаяСтоимость = ВыборкаТабличнойЧасти.Итог("ОстаточнаяСтоимость");
	ОбластьПодвал.Параметры.ОстаточнаяСтоимость = Формат(ОстаточнаяСтоимость,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(ОстаточнаяСтоимость,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАмортизация()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	//обновим информацию по активам при копировании
	Для каждого ТекСтрока Из Активы Цикл
		ОбработкаРеквизита("Активы.Актив", ТекСтрока);
	КонецЦикла;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	РезультатЗапросаПоАктивам = ПолучитьРезультатЗапросаПоАктивам();
	
	// ни одного амортизируемого актива нет
	Если РезультатЗапросаПоАктивам.Количество() = 0 Тогда
		РезультатОбработки = "Подразделение " + ПодразделениеКомпании + ". Ни один из амортизируемых активов не обнаружен.";
		Отказ = Истина;
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	// если части активов нет
	ИначеЕсли РезультатЗапросаПоАктивам.Количество() < Активы.Количество() Тогда
		РезультатОбработки = "";
		Для каждого ТекАктив Из Активы Цикл
			СтрокаАктива = РезультатЗапросаПоАктивам.НайтиСтроки(Новый Структура("Актив", ТекАктив.ПрочийАктив));
			Если СтрокаАктива.Количество() = 0 Тогда
				РезультатОбработки = РезультатОбработки + "Подразделение " + ПодразделениеКомпании + ". Актив " + СокрЛП(ТекАктив.ПрочийАктив) + " не обнаружен.
				|";
			КонецЕсли;
		КонецЦикла;
		РезультатОбработки = Лев(РезультатОбработки, СтрДлина(РезультатОбработки)-1);
		Отказ = Истина;
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	КонецЕсли;
		
	// начисляем амортизацию в эксплуатации прочих активов
	НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
	НаборЗаписейЭксплуатация.ДатаДвижений              = Неопределено;
	НаборЗаписейЭксплуатация.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейЭксплуатация.ПодразделениеКомпании     = ПодразделениеКомпании;
	НаборЗаписейЭксплуатация.ЭтоАмортизация            = Истина;
	НаборЗаписейЭксплуатация.РезультатЗапросаПоАктивам = РезультатЗапросаПоАктивам;
	Отказ = НЕ НаборЗаписейЭксплуатация.Приход() ИЛИ Отказ;
	
	// начисляем расход по амортизации
	РезультатЗапросаПоСтатьям = ПолучитьРезультатЗапросаПоСтатьям(ЭтотОбъект).Выгрузить();
	НаборЗаписейДиР = Движения.ДоходыИРасходы;
	Для Каждого Строка Из РезультатЗапросаПоСтатьям Цикл
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.Подразделение          = ПодразделениеКомпании;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Строка.СтатьяРасходовПоАмортизации;
		НаборЗаписейДиР.ВУпрВалюте             = Ложь;
		НаборЗаписейДиР.Расход                 = Строка.СуммаАмортизации;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЦикла;	
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// инициализация кэша валюты и курса документа
ВалютаКурсДок = Новый Структура("Валюта, Курс", ВалютаДокумента, КурсДокумента);
// Модуль документа СнятиеРаспределенияЗаказовПокупателя

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Заполняет табличную часть данными
//
// Параметры:
// Основание	– ДокументСсылка – Ссылка на документ-основание.
//
Процедура ЗаполнитьТабличнуюЧасть(Основание = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаМомент", ?(Ссылка.Пустая(), Дата, Новый Граница(МоментВремени(), ВидГраницы.Исключая)));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	ТекстОтбора = "";
	ТекстПараметров = "";
	Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика Тогда
		Если Основание = Неопределено Тогда
			ТекстОтбора = "ГДЕ
			|	ВЫРАЗИТЬ(ЗаказыРаспределениеОстатки.ЗаказПоставщика КАК Документ.ЗаказПоставщику).Контрагент = &Контрагент";
		КонецЕсли;
		ТекстПараметров = "ЗаказПоставщика ССЫЛКА Документ.ЗаказПоставщику";
	Иначе
		Если Основание = Неопределено Тогда
			ТекстОтбора = "ГДЕ
			|	ВЫРАЗИТЬ(ЗаказыРаспределениеОстатки.ЗаказПоставщика КАК Документ.ЗаказВнутренний).ПодразделениеПолучатель = &Контрагент";
		КонецЕсли;
		ТекстПараметров = "ЗаказПоставщика ССЫЛКА Документ.ЗаказВнутренний";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика Тогда
				ТекстПараметров = ТекстПараметров + " И 
				|	ЗаказПокупателя = &Основание";
			Иначе
				ТекстПараметров = ТекстПараметров + " И 
				|	ЗаказПоставщика = &Основание";
			КонецЕсли;
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика Тогда
				ТекстПараметров = ТекстПараметров + " И 
				|	ЗаказПоставщика = &Основание";
			КонецЕсли;
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ТекстПараметров = ТекстПараметров + " И 
			|	ЗаказПокупателя = &Основание";
		КонецЕсли;
		Запрос.УстановитьПараметр("Основание", Основание);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказыРаспределениеОстатки.ЗаказПокупателя,
	|	ЗаказыРаспределениеОстатки.ЗаказПоставщика,
	|	ЗаказыРаспределениеОстатки.Номенклатура,
	|	ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры,
	|	ЗаказыРаспределениеОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗаказыРаспределение.Остатки(&НаМомент,
	|	" + ТекстПараметров + ") КАК ЗаказыРаспределениеОстатки
	|	" + ТекстОтбора + "
	|";
	
	Товары.Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
		Если (НЕ НоваяСтрока.Коэффициент = 0) И (НЕ НоваяСтрока.Коэффициент = 1) Тогда
			НоваяСтрока.Количество = Окр(НоваяСтрока.Количество/НоваяСтрока.Коэффициент, 3);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ЗаказПоставщика",3);
	ОбязательныеТовары.Вставить("ЗаказПокупателя",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	//ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);	
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыСостава.Вставить("ЗаказПоставщика", КонтрольПоПодразделению);
				КонтролируемыеРеквизитыСостава.Вставить("ЗаказПокупателя");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыСостава);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СнятиеРаспределенияПоставщика","Снятие распределения поставщика",   Истина);
	СписокХозОпераций.Добавить("СнятиеРаспределенияВнутреннего","Снятие распределения внутреннего", Ложь);
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  СуммаДокумента - число - сумма документа.
//
Функция РассчитатьСуммуВсего()Экспорт
	
	Возврат 0;
	
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ХозОперация" Тогда
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика Тогда
			Если ТипЗнч(Контрагент)<>Тип("СправочникСсылка.Контрагенты") Тогда
				Контрагент = Справочники.Контрагенты.ПустаяСсылка();
				Рез = ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.СнятиеРаспределенияВнутреннего Тогда
			Если ТипЗнч(Контрагент)<>Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				Контрагент = Справочники.ПодразделенияКомпании.ПустаяСсылка();
				Рез = ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя="Контрагент" Тогда
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		ЗаполнитьТабличнуюЧасть(ДокументОснование);
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика И обЗначениеНеЗаполнено(ТекСтрока.ЗаказПоставщика) Тогда
					ТекСтрока.ЗаказПоставщика = ДокументОснование;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				Если обЗначениеНеЗаполнено(ТекСтрока.ЗаказПокупателя) Тогда
					ТекСтрока.ЗаказПокупателя = ДокументОснование;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказВнутренний") Тогда
				Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика И обЗначениеНеЗаполнено(ТекСтрока.ЗаказПокупателя)  Тогда
					ТекСтрока.ЗаказПокупателя = ДокументОснование;
				ИначеЕсли ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика И обЗначениеНеЗаполнено(ТекСтрока.ЗаказПоставщика)  Тогда
					ТекСтрока.ЗаказПоставщика = ДокументОснование;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "СнятиеРаспределенияЗаказовПокупателя"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьСнятиеРаспределенияЗаказовПокупателя(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("СнятиеРаспределенияЗаказовПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка          = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("ЗаказПокупателя", дкПолучитьПредставление(СтрокаТабличнойЧасти.ЗаказПокупателя));
		СтруктураСтроки.Вставить("ЗаказПоставщика", дкПолучитьПредставление(СтрокаТабличнойЧасти.ЗаказПоставщика));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	// Выводим представления и расшифровки подписей
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьСнятиеРаспределенияЗаказовПокупателя()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		Если ТипЗнч(Основание)=Тип("СправочникСсылка.Контрагенты") Тогда
			Контрагент  = Основание;
			ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			Контрагент  = Основание.Контрагент;
			ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Контрагент  = Справочники.Контрагенты.ПустаяСсылка();
			ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Контрагент  = Основание.ПодразделениеПолучатель;
			ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияВнутреннего;
		КонецЕсли;
		
		//А теперь откорректируем ТЧ
		ЗаполнитьТабличнуюЧасть(Основание);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим корректность заполнения табличной части
	Для Каждого СтрокаТоваров Из Товары Цикл
		
		Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
			Если ТипЗнч(СтрокаТоваров.ЗаказПоставщика)<>Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Вид заказа не соответствует контрагенту.");
				Отказ = Истина;
			Иначе
				Если Контрагент<>СтрокаТоваров.ЗаказПоставщика.Контрагент Тогда
					Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Контрагент заказа не соответствует контрагенту документа.");
					Отказ=Истина;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Контрагент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
			Если ТипЗнч(СтрокаТоваров.ЗаказПоставщика)<>Тип("ДокументСсылка.ЗаказВнутренний") Тогда
				Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Вид заказа не соответствует контрагенту.");
				Отказ=Истина;
			Иначе
				Если Контрагент<>СтрокаТоваров.ЗаказПоставщика.ПодразделениеПолучатель Тогда
					Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Контрагент заказа не соответствует контрагенту документа.");
					Отказ=Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикам.ЗаказПоставщику КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|	ГДЕ
		|		ЗаказыПоставщикам.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения           = Режим;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = Неопределено;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект            = ЭтотОбъект;
	Отказ = НЕ НаборЗаписейРаспределениеЗаказов.СнятиеРаспределения() ИЛИ Отказ;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Если КорректировкаЗаказа Тогда
		
		Если ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика Тогда
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура КАК Номенклатура,
			|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ДокументТовары.ЗаказПоставщика КАК ЗаказПоставщику,
			|	СУММА(ДокументТовары.Количество*ДокументТовары.Коэффициент) КАК Количество
			|ИЗ
			|	Документ.СнятиеРаспределенияЗаказовПокупателя.Товары КАК ДокументТовары
			|ГДЕ
			|	ДокументТовары.Ссылка=&Ссылка И 
			|	ДокументТовары.Количество>0
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.ХарактеристикаНоменклатуры,
			|	ДокументТовары.ЗаказПоставщика
			|";
			
			// Снимаем резервы по заказам (если таковые были)
			НаборЗаписейЗаказыПоставщикам = Движения.ЗаказыПоставщикам;
			НаборЗаписейЗаказыПоставщикам.РежимПроведения           = Режим;
			НаборЗаписейЗаказыПоставщикам.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейЗаказыПоставщикам.РезультатЗапросаПоТоварам = Запрос.Выполнить();
			НаборЗаписейЗаказыПоставщикам.ЗаказПоставщику           = Неопределено;
			Отказ = НЕ НаборЗаписейЗаказыПоставщикам.КорректировкаСписаниемЗаказаПоставщика() ИЛИ Отказ;
		Иначе
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура КАК Номенклатура,
			|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ДокументТовары.ЗаказПоставщика КАК Заказ,
			|	ДокументТовары.ЗаказПоставщика.СкладКомпании КАК СкладЗаказа,
			|	СУММА(ДокументТовары.Количество*ДокументТовары.Коэффициент) КАК Количество
			|ИЗ
			|	Документ.СнятиеРаспределенияЗаказовПокупателя.Товары КАК ДокументТовары
			|ГДЕ
			|	ДокументТовары.Ссылка = &Ссылка И 
			|	ДокументТовары.Количество>0
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.ХарактеристикаНоменклатуры,
			|	ДокументТовары.ЗаказПоставщика
			|";
			
			// Снимаем резервы по заказам (если таковые были)
			НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
			НаборЗаписейЗаказыПокупателей.РежимПроведения           = Режим;
			НаборЗаписейЗаказыПокупателей.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
			НаборЗаписейЗаказыПокупателей.Заказ                     = Неопределено;
			НаборЗаписейЗаказыПокупателей.ВидОперации               = Перечисления.ВидыОперацийЗаказов.КорректировкаЗаказа;
			НаборЗаписейЗаказыПокупателей.СкладКомпании             = Неопределено;
			Отказ = НЕ НаборЗаписейЗаказыПокупателей.КорректировкаСписаниемЗаказаПокупателя() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ЗаказыРаспределение.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыРаспределение.Выгрузить();
			ТаблицаЗаказов.Свернуть("ЗаказПокупателя","");
			НаборЗаписейГраницыЗаказы.Заказ            = ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПокупателя");
			НаборЗаписейГраницыЗаказы.МоментВремени    = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл         = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ            = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени    = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл         = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
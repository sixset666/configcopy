//Процедура заполнения предопределённых элементов справочника
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы() Экспорт
	
	// Получим макет
	Макет = РегистрыСведений.ПорядокЗакрытияБалансовыхСтатей.ПолучитьМакет("НастройкиПоУмолчанию");
	Если Макет = Неопределено Тогда
		Сообщить("Не обнаружен макет начальных настроек!",СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	// Добавление статей
	НаборЗаписей = РегистрыСведений.ПорядокЗакрытияБалансовыхСтатей.СоздатьНаборЗаписей();
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		// Считаем из макета Статья, Порядок
		СтрСтатья = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрПорядок = СокрЛП(Макет.Область(НомерСтроки,2).Текст);
		
		Отказ = Ложь;
		Статья = Неопределено;
		Порядок = Неопределено;
		
		// Получим статью
		Попытка
			Статья = Справочники.СтатьиДоходовИРасходов[СтрСтатья];
		Исключение
			Отказ = Истина;
		КонецПопытки;
		
		// Получим порядок
		Попытка
			Порядок = Число(СтрПорядок);
		Исключение
			Отказ = Истина;
		КонецПопытки;
		
		// Добавим запись
		Если НЕ Отказ Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Статья  = Статья;
			НоваяЗапись.Порядок = Порядок;
		КонецЕсли;
	КонецЦикла;
	
	// Сохраним порядок распределения статей доходов и расходов
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

// Модуль набора записей регистра "СкидкиСтроки"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ДокументОбъект			Экспорт;	// Документ объект
Перем РезультатЗапросаПоСкидкам	Экспорт;	// РезультатЗапроса или ТаблицаЗначений.
Перем ПодразделениеКомпании		Экспорт;	// Подразделение компании

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция выполняет отмену проведения
Функция ОтменаПроведения() Экспорт
	// проверим значения ключевых объектов
	Отказ = ((ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.НазначениеСкидокСтроки")) ИЛИ ДокументОбъект.ЭтоНовый());
	
	// приступим к очистки движений
	Если НЕ Отказ Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	*
		|ИЗ
		|	РегистрСведений.СкидкиСтроки КАК СкидкиСтроки
		|ГДЕ
		|	СкидкиСтроки.РегистраторСкидки = &РегистраторСкидки";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("РегистраторСкидки", ДокументОбъект.Ссылка);
		
		Запись = РегистрыСведений.СкидкиСтроки.СоздатьМенеджерЗаписи();
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Попытка
				Запись.Прочитать();
				Запись.Удалить();
			Исключение
				Отказ = ИСТИНА;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Отказ;
КонецФункции

// Функция устанавливает скидки
Функция УстановитьСкидки() Экспорт
	Отказ = ЛОЖЬ;
	
	// проверим значения ключевых объектов
	Если (ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.НазначениеСкидокСтроки")) ИЛИ ДокументОбъект.ЭтоНовый() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если (ТипЗнч(ПодразделениеКомпании) <> Тип("СправочникСсылка.ПодразделенияКомпании")) ИЛИ ПодразделениеКомпании.Пустая() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	
	// получаем таблицу товаров
	Если (РезультатЗапросаПоСкидкам = Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("ТаблицаЗначений")) Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоСкидкам) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоСкидкам = РезультатЗапросаПоСкидкам.Выгрузить();
	КонецЕсли;
	
	// если были обнаружены ошибки, то движения делать не будем
	Если НЕ Отказ Тогда
		// Осуществляем запись данных в регистр
		ТабЗаписей = ПолучитьДаныеПоСкидкам(ДокументОбъект.Ссылка);
		НужнаПроверка = ТабЗаписей.Количество() > 0;
		стрОшибок = "";
		
		Для Каждого ТекСтрока Из РезультатЗапросаПоСкидкам Цикл
			// регистр СкидкиСтроки 
			НоваяЗапись = Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
			НоваяЗапись.Период				= ДокументОбъект.ДатаНачалаДействия;
			НоваяЗапись.Подразделение		= ПодразделениеКомпании;
			НоваяЗапись.Действует			= ИСТИНА;
			НоваяЗапись.РегистраторСкидки	= ДокументОбъект.Ссылка;
			НоваяЗапись.Свойство			= ?(обЗначениеНеЗаполнено(НоваяЗапись.Свойство), Неопределено, НоваяЗапись.Свойство);
			НоваяЗапись.ИдентификаторСкидки	= ТекСтрока.ИдентификаторСкидки;
			НоваяЗапись.СкидкаНаТовары		= ТекСтрока.СкидкаНаТовары; 
			НоваяЗапись.СкидкаНаРаботы		= ТекСтрока.СкидкаНаРаботы; 
			НоваяЗапись.МоментИзменения		= ТекущаяДата(); //TruA
			НоваяЗапись.РучнаяСкидка		= ТекСтрока.Скидка.РучнаяСкидка;
			
			Если НужнаПроверка Тогда
				стрПоиск = Новый Структура;
				стрПоиск.Вставить("Подразделение");
				стрПоиск.Вставить("РучнаяСкидка");
				стрПоиск.Вставить("Объект");
				стрПоиск.Вставить("НачВремя");
				стрПоиск.Вставить("КонВремя");
				стрПоиск.Вставить("ДниНедели");
				стрПоиск.Вставить("ЭтоПодарок");
				стрПоиск.Вставить("ДисконтнаяКарта");
				стрПоиск.Вставить("ОтСуммыНакопленияНаКарте");
				стрПоиск.Вставить("ЗалОбслуживания");
				стрПоиск.Вставить("Свойство");
				стрПоиск.Вставить("ОтСуммыСтроки");
				стрПоиск.Вставить("ОтКоличества");
				стрПоиск.Вставить("Правило");
				стрПоиск.Вставить("Скидка");
				стрПоиск.Вставить("СкидкаНаТовары");
				стрПоиск.Вставить("СкидкаНаРаботы");
				
				ЗаполнитьЗначенияСвойств(стрПоиск,НоваяЗапись);
				
				РезПоиска = ТабЗаписей.НайтиСтроки(стрПоиск);
				Если РезПоиска.Количество() > 0 Тогда
					Отказ = Истина;
					дкДобавитьОшибку(стрОшибок, "Для скидки <"+ТекСтрока.Скидка+"> уже есть запись на дату """+ Формат(ДокументОбъект.ДатаНачалаДействия,"ДЛФ=DDT") +""".");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Произведем запись набора (т.к. автоматической записи по окончанию транзакции не будет)
		Если Отказ Тогда
			дкСообщитьРезультат(стрОшибок, "Важное&Скидки (на товарную строку документа):", ДокументОбъект);//ОписаниеОшибки()
		Иначе
			Попытка
				Записать(ЛОЖЬ);
			Исключение
				Отказ = ИСТИНА;
				дкСообщитьРезультат("В процессе записи данных в регистр возникла ошибка.", "Важное&Скидки (на товарную строку документа):", ДокументОбъект);//ОписаниеОшибки()
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	// Очистим основные переменные
	ДокументОбъект				= Неопределено;
	РезультатЗапросаПоСкидкам	= Неопределено;
	ПодразделениеКомпании		= Неопределено;
	
	Возврат Отказ;
КонецФункции

// Отменить скидки
Функция ОтменитьСкидки() Экспорт
	Отказ = ЛОЖЬ;
	
	// проверим значения ключевых объектов
	Если (ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.НазначениеСкидокСтроки")) ИЛИ ДокументОбъект.ЭтоНовый() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если (ТипЗнч(ПодразделениеКомпании) <> Тип("СправочникСсылка.ПодразделенияКомпании")) ИЛИ ПодразделениеКомпании.Пустая() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	
	// получаем таблицу товаров
	Если (РезультатЗапросаПоСкидкам = Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("ТаблицаЗначений")) Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоСкидкам) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоСкидкам = РезультатЗапросаПоСкидкам.Выгрузить();
	КонецЕсли;
	
	// Осуществляем запись данных в регистр
	Для Каждого ТекСтрока Из РезультатЗапросаПоСкидкам Цикл
		Если НЕ ТекСтрока.СкидкаУстановлена Тогда
			Отказ = ИСТИНА;
			дкСообщитьРезультат("Скидка """+СокрЛП(ТекСтрока.Скидка)+""" ранее не была установлена. Отмена скидки не возможно!", "Важное&Скидки (на товарную строку документа):", ДокументОбъект, ТекСтрока.Скидка);
			Продолжить;
		КонецЕсли;
		
		// регистр СкидкиСтроки
		НоваяЗапись = Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
		НоваяЗапись.Период				= ДокументОбъект.ДатаНачалаДействия;
		НоваяЗапись.Подразделение		= ПодразделениеКомпании;
		НоваяЗапись.Действует			= ЛОЖЬ;
		НоваяЗапись.РегистраторСкидки	= ДокументОбъект.Ссылка;
		НоваяЗапись.ИдентификаторСкидки = ТекСтрока.ИдентификаторСкидки; //TruA
		НоваяЗапись.СкидкаНаТовары		= ТекСтрока.СкидкаНаТовары;
		НоваяЗапись.СкидкаНаРаботы		= ТекСтрока.СкидкаНаРаботы;
		НоваяЗапись.МоментИзменения		= ТекущаяДата(); //TruA
	КонецЦикла;
	
	// если были обнаружены ошибки, то движения записывать не будем
	Если НЕ Отказ Тогда
		// Произведем запись набора (т.к. автоматической записи по окончанию транзакции не будет)
		Попытка
			Записать(ЛОЖЬ);
		Исключение
			Отказ = ИСТИНА;
			дкСообщитьРезультат("В процессе записи данных в регистр возникла ошибка.", "Важное&Скидки (на товарную строку документа):", ДокументОбъект);//ОписаниеОшибки()
		КонецПопытки;
	КонецЕсли;
	
	// Очистим основные переменные
	ДокументОбъект				= Неопределено;
	РезультатЗапросаПоСкидкам	= Неопределено;
	ПодразделениеКомпании		= Неопределено;
	
	Возврат Отказ;
КонецФункции

// получение скидок на дуту
Функция ПолучитьДаныеПоСкидкам(ДокСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СкидкиСтроки.Подразделение,
	|	СкидкиСтроки.РучнаяСкидка,
	|	СкидкиСтроки.Объект,
	|	СкидкиСтроки.НачВремя,
	|	СкидкиСтроки.КонВремя,
	|	СкидкиСтроки.ДниНедели,
	|	СкидкиСтроки.ЭтоПодарок,
	|	СкидкиСтроки.ДисконтнаяКарта,
	|	СкидкиСтроки.ОтСуммыНакопленияНаКарте,
	|	СкидкиСтроки.ЗалОбслуживания,
	|	СкидкиСтроки.Свойство,
	|	СкидкиСтроки.ОтСуммыСтроки,
	|	СкидкиСтроки.ОтКоличества,
	|	СкидкиСтроки.Правило,
	|	СкидкиСтроки.Скидка,
	|	СкидкиСтроки.СкидкаНаТовары,
	|	СкидкиСтроки.СкидкаНаРаботы
	|ИЗ
	|	РегистрСведений.СкидкиСтроки КАК СкидкиСтроки
	|ГДЕ
	|	СкидкиСтроки.Период = &ДатаНачалаДействия
	|	И СкидкиСтроки.Скидка В
	|			(ВЫБРАТЬ
	|				НазначениеСкидокСтроки.Скидка
	|			ИЗ
	|				Документ.НазначениеСкидокСтроки.Скидки КАК НазначениеСкидокСтроки
	|			ГДЕ
	|				НазначениеСкидокСтроки.Ссылка = &ДокСсылка
	|			СГРУППИРОВАТЬ ПО
	|						НазначениеСкидокСтроки.Скидка)";
	
	Запрос.УстановитьПараметр("ДокСсылка", ДокСсылка);
	Запрос.УстановитьПараметр("ДатаНачалаДействия",ДокСсылка.ДатаНачалаДействия);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

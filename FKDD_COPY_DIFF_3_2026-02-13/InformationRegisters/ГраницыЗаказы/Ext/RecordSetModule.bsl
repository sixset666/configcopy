Перем Заказ Экспорт;   // Документ.ЗаказПокупателя ИЛИ Документ.ЗаказВнутренний ИЛИ Документ.ЗаказПоставщику ИЛИ Массив. Содержит заказы по которым сдвигается граница последовательности
Перем ЗаказБыл Экспорт;   // Документ.ЗаказПокупателя ИЛИ Документ.ЗаказВнутренний ИЛИ Документ.ЗаказПоставщику ИЛИ Массив. Содержит заказы по которым сдвигается граница последовательности
Перем МоментВремени Экспорт;   // Дата-время документа
Перем МоментВремениБыл Экспорт;   // Дата-время документа до изменения
Перем ДокументСсылка Экспорт;  // Ссылка на документ

// проверяет, надо ли двигать границу последовательности и если надо - сдвигает
Функция ИзменитьГраницу(БезПроверок=Ложь) Экспорт
	// если заказ один, то преобразуем его в массив
	МассивЗаказовНовые = Новый Массив();
	МассивЗаказовСтарые = Новый Массив();
	// фильтр на заказы
	МассивЗаказовФильтр = Новый Массив();
	Если ТипЗнч(Заказ)<>Тип("Массив") Тогда
		МассивЗаказовНовые.Добавить(Заказ);
		МассивЗаказовФильтр.Добавить(Заказ);
	Иначе
		Для Каждого ТекЗаказ Из Заказ Цикл
			МассивЗаказовНовые.Добавить(ТекЗаказ);
			МассивЗаказовФильтр.Добавить(ТекЗаказ);
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(ЗаказБыл) И ТипЗнч(ЗаказБыл)<>Тип("Массив") И МассивЗаказовНовые.Найти(ЗаказБыл)=Неопределено Тогда
		МассивЗаказовСтарые.Добавить(ЗаказБыл);
		МассивЗаказовФильтр.Добавить(ЗаказБыл);
	ИначеЕсли ЗначениеЗаполнено(ЗаказБыл) Тогда
		Для Каждого ТекЗаказ Из ЗаказБыл Цикл
			Если МассивЗаказовНовые.Найти(ТекЗаказ)=Неопределено Тогда
				МассивЗаказовСтарые.Добавить(ТекЗаказ);
				МассивЗаказовФильтр.Добавить(ТекЗаказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// получаем текущие границы
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Заказ КАК Заказ,
	|	МоментВремени КАК МоментВремени,
	|	ДокументСсылка КАК ДокументСсылка
	|ИЗ
	|	РегистрСведений.ГраницыЗаказы
	|ГДЕ
	|	Заказ В(&Заказы)
	|");
	Запрос.УстановитьПараметр("Заказы",МассивЗаказовФильтр);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	МоментВремениДокумента = ?(ТипЗнч(МоментВремени)=Тип("МоментВремени") И ДокументСсылка<>Неопределено, Новый МоментВремени(Мин(ДокументСсылка.Дата, МоментВремени.Дата), ДокументСсылка), Новый МоментВремени(МоментВремени, ДокументСсылка));
	Если ЗначениеЗаполнено(МоментВремениБыл) Тогда
		МоментВремениДокумента = ?(МоментВремениДокумента.Сравнить(МоментВремениБыл)=1,МоментВремениБыл,МоментВремениДокумента);
	КонецЕсли;
	// идем по новым заказам и смотрим надо ли сдвинуть границу
	Для Каждого СледующийЗаказ Из МассивЗаказовНовые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(СледующийЗаказ,"Заказ");
	
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениДокумента) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыЗаказы.СоздатьМенеджерЗаписи();
			НоваяЗапись.Заказ = СледующийЗаказ;
			НоваяЗапись.МоментВремени = МоментВремениДокумента.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениДокумента.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// теперь тот-же проход, но по старым заказам
	Для Каждого СледующийЗаказ Из МассивЗаказовСтарые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(СледующийЗаказ,"Заказ");
		
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениБыл) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыЗаказы.СоздатьМенеджерЗаписи();
			НоваяЗапись.Заказ = СледующийЗаказ;
			НоваяЗапись.МоментВремени = МоментВремениБыл.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениБыл.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// убираем циклические ссылки
	ДокументСсылка = Неопределено;
	
	Возврат Истина;
КонецФункции

// вызывается при непосредственном удалении проведенного документа
// проверяет есть ли граница с таким документом, если есть, то очищает ссылку
Процедура УбратьСсылку(Ссылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Заказ КАК Заказ,
	|	МоментВремени КАК МоментВремени 
	|ИЗ
	|	РегистрСведений.ГраницыЗаказы 
	|ГДЕ
	|	ДокументСсылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = РегистрыСведений.ГраницыЗаказы.СоздатьМенеджерЗаписи();
			НоваяЗапись.Заказ = Выборка.Заказ;
			НоваяЗапись.МоментВремени = Выборка.МоментВремени;
			НоваяЗапись.ДокументСсылка = Неопределено;
			НоваяЗапись.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

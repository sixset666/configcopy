
Функция ПодставитьПараметрыВСтроку(СтрокаСПараметрами, Параметр1 = Неопределено, Параметр2 = Неопределено, Параметр3 = Неопределено, Параметр4 = Неопределено)
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%1", Параметр1);
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%2", Параметр2);
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%3", Параметр3);
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%4", Параметр4);
	Возврат СтрокаСПараметрами;
КонецФункции

//Процедура заполняет артикул а также преобразует артикул для поиска
Процедура ЗаполнитьАртикулДляПоиска() Экспорт 
	
	#Если Клиент Тогда
	Состояние(НСтр("ru='Проверка упущенного спроса.'"));
	#КонецЕсли
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УпущенныйСпрос.Производитель КАК Производитель,
		|	УпущенныйСпрос.Период,
		|	УпущенныйСпрос.АртикулДляПоиска,
		|	УпущенныйСпрос.Количество,
		|	УпущенныйСпрос.КоличествоОстаток,
		|	УпущенныйСпрос.Автор,
		|	УпущенныйСпрос.Комментарий,
		|	УпущенныйСпрос.Номенклатура,
		|	УпущенныйСпрос.Причина,
		|	УпущенныйСпрос.ИсточникЗапроса
		|ИЗ
		|	РегистрСведений.УпущенныйСпрос КАК УпущенныйСпрос
		|ГДЕ
		|	УпущенныйСпрос.Артикул = """"
		|ИТОГИ ПО
		|	Производитель";
	
	Результат = Запрос.Выполнить();
				   
	Если Результат.Пустой() Тогда
		#Если Клиент Тогда
		Сообщить(НСтр("ru='Обновление регистра сведений ""Упущенный спрос"" не требуется'"));
		#КонецЕсли
		Возврат;
	КонецЕсли;
		
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Упущенный спрос""'"));
	#КонецЕсли
	
	ВыборкаПроизводителей = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	РазмерВыборки = ВыборкаПроизводителей.Количество();
	СчВсего = 0;
	ПроцентПрогресса = 0;
	
	Пока ВыборкаПроизводителей.Следующий()Цикл
		
		ПроизводительНаименование = ?(обЗначениеНеЗаполнено(ВыборкаПроизводителей.Производитель), "<Не указан>", ВыборкаПроизводителей.Производитель.Наименование);
		
		СчВсего = СчВсего + 1;
		#Если Клиент Тогда
		// Прогресс устроим по производителям
		Если Цел(СчВсего*100/РазмерВыборки) > ПроцентПрогресса Тогда
			ПроцентПрогресса = Цел(СчВсего*100/РазмерВыборки);
			Состояние(ПодставитьПараметрыВСтроку(НСтр("ru='Обновление регистра сведений ""Упущенный спрос"": %1%, производитель %2'"),
												 Строка(ПроцентПрогресса),
												 ПроизводительНаименование));
		КонецЕсли;
		#КонецЕсли
		
		НачатьТранзакцию();		
		
		НаборЗаписей = РегистрыСведений.УпущенныйСпрос.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Производитель.Установить(ВыборкаПроизводителей.Производитель, Истина);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		
		#Если Клиент Тогда
		Если НаборЗаписей.Количество() > 20000 Тогда
			// Будем сообщать только о существенных производителях
			Сообщить(ПодставитьПараметрыВСтроку(НСтр("ru='Для производителя %1 в регистре найдено %2 записей. Обновление может занять длительное время. Подождите!'"),
												ПроизводительНаименование,
												Строка(НаборЗаписей.Количество())));
		КонецЕсли;
		#КонецЕсли
											
		// Если набор записей содержит меньше 100000 записей,
		// то отредактируем и запишем сразу весь набор. Это еще более менее терпимо по нагрузке на сервер
		// Если записей больше, то очистим набор и построчно создадим новый
		
		Если НаборЗаписей.Количество() > 100000 Тогда
			
			// Весь набор нельзя удалять, так как могут быть записи, которых в запрос не попало!
			НаборЗаписей = Неопределено;
			//НаборЗаписей.Очистить();
			//НаборЗаписей.Записать(Истина);
			
			ДетальнаяВыборка = ВыборкаПроизводителей.Выбрать();
			
			СчЦикла = 0;
			
			Пока  ДетальнаяВыборка.Следующий() Цикл
				
				СчЦикла = СчЦикла + 1;
				
				Артикул = СокрЛП(ДетальнаяВыборка.АртикулДляПоиска);
				АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, ДетальнаяВыборка.Производитель); 
				Если ПустаяСтрока(АртикулДляПоиска)Тогда
					АртикулДляПоиска = СокрЛП(Артикул);
				КонецЕсли;
				
				РабочийНабор = РегистрыСведений.УпущенныйСпрос.СоздатьНаборЗаписей();
				РабочийНабор.Отбор.Производитель.Установить(ДетальнаяВыборка.Производитель,	Истина);
				РабочийНабор.Отбор.Период.		 Установить(ДетальнаяВыборка.Период,		Истина);
				
				Если СокрП(ДетальнаяВыборка.АртикулДляПоиска) <> АртикулДляПоиска Тогда
					// Артикул изменился, надо удалить старую запись и создать новую
					РабочийНабор.Отбор.АртикулДляПоиска.Установить(ДетальнаяВыборка.АртикулДляПоиска,	Истина);
					РабочийНабор.ОбменДанными.Загрузка = Истина;
					РабочийНабор.Записать(Истина);
				КонецЕсли;
				
				РабочийНабор.Отбор.АртикулДляПоиска.Установить(АртикулДляПоиска, Истина);
				
				НоваяЗапись = РабочийНабор.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ДетальнаяВыборка);
				НоваяЗапись.Артикул          = Артикул;
				НоваяЗапись.АртикулДляПоиска = АртикулДляПоиска;
				
				РабочийНабор.ОбменДанными.Загрузка = Истина;
				РабочийНабор.Записать(Истина);
				
				Если СчЦикла >= 10000 Тогда
					ЗафиксироватьТранзакцию();
					НачатьТранзакцию();
					СчЦикла = 0;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Для Каждого Запись ИЗ НаборЗаписей Цикл 
				Артикул			 = Запись.АртикулДляПоиска;
				АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, Запись.Производитель);
				
				Запись.Артикул = Артикул;
				Если НЕ ПустаяСтрока(АртикулДляПоиска)Тогда
					Запись.АртикулДляПоиска = АртикулДляПоиска;
				КонецЕсли;	
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Упущенный спрос"" завершено.'"));
	#КонецЕсли
	
КонецПроцедуры //ЗаполнитьАртикулДляПоиска()

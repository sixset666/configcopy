
Функция ПодставитьПараметрыВСтроку(СтрокаСПараметрами, Параметр1 = Неопределено, Параметр2 = Неопределено, Параметр3 = Неопределено, Параметр4 = Неопределено)
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%1", Параметр1);
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%2", Параметр2);
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%3", Параметр3);
	СтрокаСПараметрами = СтрЗаменить(СтрокаСПараметрами, "%4", Параметр4);
	Возврат СтрокаСПараметрами;
КонецФункции

//Процедура заполняет артикул, артикул замены, преобразует артикул для поиска и артикул замены для поиска
Процедура ЗаполнитьАртикулДляПоиска() Экспорт 
	
	#Если Клиент Тогда
	Состояние(НСтр("ru='Проверка замен.'"));
	#КонецЕсли
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Замены.АртикулДляПоиска,
		|	Замены.АртикулЗаменыДляПоиска,
		|	Замены.Производитель КАК Производитель,
		|	Замены.Группа,
		|	Замены.Количество,
		|	Замены.ДатаЗамены,
		|	Замены.ДатаОбновления,
		|	Замены.Описание,
		|	Замены.Артикул,
		|	Замены.АртикулЗамены
		|ИЗ
		|	РегистрСведений.Замены КАК Замены
		|ГДЕ
		|	(Замены.Артикул = """"
		|			ИЛИ Замены.АртикулЗамены = """")
		|ИТОГИ ПО
		|	Производитель";
				   
	Результат = Запрос.Выполнить();			   
	
	Если Результат.Пустой() Тогда
		#Если Клиент Тогда
		Сообщить(НСтр("ru='Обновление регистра сведений ""Замены"" не требуется'"));
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Замены""'"));
	#КонецЕсли
	
	Выборка = Результат.Выбрать();
	
	РазмерВыборки = Выборка.Количество();
	
	ВыборкаПроизводителей = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СчВсего	= 0;
	СчЦикла	= 0;
	ПроцентПрогресса = 0;
	
	Пока ВыборкаПроизводителей.Следующий() Цикл
		
		// Каждого производителя в отдельной транзакции
		НачатьТранзакцию();
		
		ПроизводительНаименование = ?(обЗначениеНеЗаполнено(ВыборкаПроизводителей.Производитель), "<Не указан>", ВыборкаПроизводителей.Производитель.Наименование);
		#Если Клиент Тогда
		Состояние(ПодставитьПараметрыВСтроку(НСтр("ru='Обновление регистра сведений ""Замены"": %1%, производитель %2'"),
												  Строка(ПроцентПрогресса),
												  ПроизводительНаименование));
		#КонецЕсли
		СчВсего = СчВсего + 1;
		Выборка = ВыборкаПроизводителей.Выбрать();
		
		НаборЗаписей = РегистрыСведений.Замены.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Производитель.Установить(ВыборкаПроизводителей.Производитель, Истина);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		
		Если НаборЗаписей.Количество() > 20000 Тогда
			// Сообщим про существенного производителя
			// Большой набор, запись может быть долгой!
			#Если Клиент Тогда
			Сообщить(ПодставитьПараметрыВСтроку(НСтр("ru='Для производителя %1 в регистре найдено %2 записей. Обновление может занять длительное время. Подождите!'"),
												ПроизводительНаименование,
												Строка(НаборЗаписей.Количество())));
			#КонецЕсли
		КонецЕсли;
		
		Если НаборЗаписей.Количество() > 100000 Тогда
			// Будем записывать поштучно!
			
			НаборЗаписей = Неопределено;
			
			РабочийНабор = РегистрыСведений.Замены.СоздатьНаборЗаписей();
			РабочийНабор.Отбор.Производитель.Установить(ВыборкаПроизводителей.Производитель, Истина);
			
			Пока Выборка.Следующий()Цикл
				СчВсего = СчВсего + 1;
				СчЦикла = СчЦикла + 1;
				#Если Клиент Тогда
				Если Цел(СчВсего*100/РазмерВыборки) > ПроцентПрогресса Тогда
					ПроцентПрогресса = Цел(СчВсего*100/РазмерВыборки);
					Состояние(ПодставитьПараметрыВСтроку(НСтр("ru='Обновление регистра сведений ""Замены"": %1%, производитель %2'"),
															  Строка(ПроцентПрогресса),
															  ПроизводительНаименование));
				КонецЕсли;
				#КонецЕсли
				
				РабочийНабор.Отбор.АртикулДляПоиска.		Установить(Выборка.АртикулДляПоиска,		Истина);
				РабочийНабор.Отбор.АртикулЗаменыДляПоиска.	Установить(Выборка.АртикулЗаменыДляПоиска,	Истина);
				РабочийНабор.Прочитать();
				Если РабочийНабор.Количество() = 0 Тогда
					// Запись уже удалена
					Продолжить;
				КонецЕсли;
				
				АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Выборка.АртикулДляПоиска, Выборка.Производитель); 
				Если ПустаяСтрока(АртикулДляПоиска)Тогда
					АртикулДляПоиска = СокрЛП(Выборка.АртикулДляПоиска);
				КонецЕсли;
				
				АртикулЗаменыДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Выборка.АртикулЗаменыДляПоиска, Выборка.Производитель);
				Если ПустаяСтрока(АртикулЗаменыДляПоиска)Тогда
					АртикулЗаменыДляПоиска = СокрЛП(Выборка.АртикулЗаменыДляПоиска);
				КонецЕсли;
				
				// Если Артикулы для поиска и Артикул замены для поиска оказались равны,
				// то такая запись не имеет смысла и мы удалим ее
				// даже если одинаковые артикулы ранее были в регистре (например, "  0567" и "0567"),
				// то на самом деле они не имели смысла!
				Если АртикулДляПоиска = АртикулЗаменыДляПоиска Тогда
					РабочийНабор.Очистить();
					РабочийНабор.ОбменДанными.Загрузка = Истина;
					РабочийНабор.Записать(Истина);
					Продолжить;
				КонецЕсли;
				
				// Если Артикулы для поиска изменились, поищем может новая пара уже есть в регистре?
				Если СокрП(Выборка.АртикулДляПоиска) <> АртикулДляПоиска ИЛИ СокрП(Выборка.АртикулЗаменыДляПоиска) <> АртикулЗаменыДляПоиска Тогда
					Менеджер = РегистрыСведений.Замены.СоздатьМенеджерЗаписи();
					Менеджер.АртикулДляПоиска		= АртикулДляПоиска;
					Менеджер.АртикулЗаменыДляПоиска	= АртикулЗаменыДляПоиска;
					Менеджер.Производитель			= Выборка.Производитель;
					Менеджер.Прочитать();
					Если Менеджер.Выбран() > 0 Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				Если СокрП(Выборка.АртикулДляПоиска) = АртикулДляПоиска И СокрП(Выборка.АртикулЗаменыДляПоиска) = АртикулЗаменыДляПоиска Тогда
					// Артикулы для поиска остались без изменений
					// заполним реквизиты для записи
					РабочийНабор[0].Артикул			= СокрЛП(Выборка.АртикулДляПоиска);
					РабочийНабор[0].АртикулЗамены	= СокрЛП(Выборка.АртикулЗаменыДляПоиска);
					
				Иначе
					// Удалим старую запись
					РабочийНабор.Очистить();
					РабочийНабор.ОбменДанными.Загрузка = Истина;
					РабочийНабор.Записать(Истина);
					
					// Добавим новую запись
					РабочийНабор.Отбор.АртикулДляПоиска.		Установить(АртикулДляПоиска,		Истина);
					РабочийНабор.Отбор.АртикулЗаменыДляПоиска.	Установить(АртикулЗаменыДляПоиска,	Истина);
					НоваяСтрока = РабочийНабор.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					НоваяСтрока.АртикулДляПоиска		= АртикулДляПоиска;
					НоваяСтрока.АртикулЗаменыДляПоиска	= АртикулЗаменыДляПоиска;
					НоваяСтрока.Артикул					= СокрЛП(Выборка.АртикулДляПоиска);
					НоваяСтрока.АртикулЗамены			= СокрЛП(Выборка.АртикулЗаменыДляПоиска);
					
				КонецЕсли;
				
				РабочийНабор.ОбменДанными.Загрузка = Истина;
				РабочийНабор.Записать(Истина);
				
				Если СчЦикла > 10000 Тогда
					ЗафиксироватьТранзакцию();
					НачатьТранзакцию();
					СчЦикла = 0;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			// Попытаемся записать все через большой набор
			тзНаборЗаписей = НаборЗаписей.Выгрузить();
			тзНаборЗаписей.Индексы.Добавить("АртикулДляПоиска,АртикулЗаменыДляПоиска");
		
			Пока Выборка.Следующий()Цикл
				СчВсего = СчВсего + 1;
				#Если Клиент Тогда
				Если Цел(СчВсего*100/РазмерВыборки) > ПроцентПрогресса Тогда
					ПроцентПрогресса = Цел(СчВсего*100/РазмерВыборки);
					Состояние(ПодставитьПараметрыВСтроку(НСтр("ru='Обновление регистра сведений ""Замены"": %1%, производитель %2'"),
															  Строка(ПроцентПрогресса),
															  ПроизводительНаименование));
				КонецЕсли;
				#КонецЕсли
				
				НайденныеСтроки = тзНаборЗаписей.НайтиСтроки(Новый Структура("АртикулДляПоиска, АртикулЗаменыДляПоиска", Выборка.АртикулДляПоиска, Выборка.АртикулЗаменыДляПоиска));
				Если НайденныеСтроки.Количество() = 0 Тогда
					// Таких строк уже нет, они могли ранее быть удалены
					Продолжить;
				КонецЕсли;
				
				АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Выборка.АртикулДляПоиска, Выборка.Производитель); 
				Если ПустаяСтрока(АртикулДляПоиска)Тогда
					АртикулДляПоиска = СокрЛП(Выборка.АртикулДляПоиска);
				КонецЕсли;
				
				АртикулЗаменыДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Выборка.АртикулЗаменыДляПоиска, Выборка.Производитель);
				Если ПустаяСтрока(АртикулЗаменыДляПоиска)Тогда
					АртикулЗаменыДляПоиска = СокрЛП(Выборка.АртикулЗаменыДляПоиска);
				КонецЕсли;
				
				Если АртикулДляПоиска = АртикулЗаменыДляПоиска Тогда
					// Если преобразованные переменные АртикулДляПоиска и АртикулЗаменыДляПоиска оказались равны,
					// то такая запись не имеет смысла и мы удалим ее
					// даже если одинаковые артикулы были в регистре (например, "  0567" и "0567"), то на самом деле они не имели смысла!
					Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
						тзНаборЗаписей.Удалить(НайденнаяСтрока);
					КонецЦикла;
					Продолжить;
				КонецЕсли;
				
				ПерваяСтрока = Истина;
				Если СокрП(Выборка.АртикулДляПоиска) <> АртикулДляПоиска ИЛИ СокрП(Выборка.АртикулЗаменыДляПоиска) <> АртикулЗаменыДляПоиска Тогда
					// Артикулы изменились, поищем новую пару в таблице
					НайденныеСтрокиНовые = тзНаборЗаписей.НайтиСтроки(Новый Структура("АртикулДляПоиска, АртикулЗаменыДляПоиска", АртикулДляПоиска, АртикулЗаменыДляПоиска));
					Если НайденныеСтрокиНовые.Количество() > 0 Тогда
						// Нужная пара уже есть в регистре
						// Все старые строки будем удалять,
						// рано или поздно по выборке дойдем до этой строки и преобразуем ее или эта строка уже преобразована!
						ПерваяСтрока = Ложь;
					КонецЕсли;
				КонецЕсли;
				
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					Если ПерваяСтрока Тогда
						ПерваяСтрока = Ложь;
						НайденнаяСтрока.Артикул					= СокрЛП(Выборка.АртикулДляПоиска);
						НайденнаяСтрока.АртикулЗамены			= СокрЛП(Выборка.АртикулЗаменыДляПоиска);
						НайденнаяСтрока.АртикулДляПоиска		= АртикулДляПоиска;
						НайденнаяСтрока.АртикулЗаменыДляПоиска	= АртикулЗаменыДляПоиска;
					Иначе
						// Могли быть строки с артикулом вида " 0567" и "0567"
						// ранее они считались разными артикулами, теперь одинаковые
						тзНаборЗаписей.Удалить(НайденнаяСтрока);
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			
			НаборЗаписей.Загрузить(тзНаборЗаписей);
			НаборЗаписей.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
		
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Замены"" завершено.'"));
	#КонецЕсли
	
КонецПроцедуры //ЗаполнитьАртикулДляПоиска()

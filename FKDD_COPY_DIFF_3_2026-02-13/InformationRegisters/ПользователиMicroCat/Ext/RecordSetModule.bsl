/////////////////////////////////////////////////////////////////////////
//Блок авторизации

// Выполняет авторизацию пользователя, и возвращает статус авторизации
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусАвторизации   - Строка, содержащая статус авторизации
//	"OK" - Авторизация прошла успешно
//  "INVU" - Пользователь не найден
//  "INVL" - Логин указан не верно
//	"INVP" - Пароль указан не верно
//
Функция СтатусАвторизацииПользователяMicroCat(Пользователь,Логин,Пароль) Экспорт 

	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();
	
	Если Количество()=0 Тогда
		Возврат "INVU";
	КонецЕсли;
	
	Если Логин<>ЭтотОбъект[0].Логин Тогда
		Возврат "INVL";
	КонецЕсли;
	
	Если Пароль<>ЭтотОбъект[0].Пароль Тогда
		Возврат "INVP";
	КонецЕсли;
	
	Возврат "OK";

КонецФункции // СтатусАвторизацииПользователяMicroCat()

// Смена старого пароля пользователя MicroCat, на новый
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя, при авторизации из MicroCat(15 символов)
//  ПарольСтарый  - Строка - Старый пароль пользователя MicroCat
//  ПарольНовый  - Строка - Новый пароль пользователя MicroCat
//
// Возвращаемое значение:
//  СтатусСменаПароля   - Строка, содержащая статус авторизации
//	"OK" - Авторизация прошла успешно
//  "INVU" - Пользователь не найден
//  "INVL" - Логин указан не верно
//	"INVP" - Пароль указан не верно
//
Функция СменитьПарольПользователяMicroCat(Пользователь,Логин,ПарольСтарый,ПарольНовый) Экспорт 

	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();
	
	Если ПарольСтарый<>ЭтотОбъект[0].Пароль Тогда
		Возврат "INVP";
	КонецЕсли;
	
	ЭтотОбъект[0].Пользователь=Пользователь;
	ЭтотОбъект[0].Логин=Логин;
	ЭтотОбъект[0].Пароль=ПарольНовый;
	Записать();
	
	Возврат "OK";

КонецФункции // СменитьПарольПользователяMicroCat()

// Восстановление утерянного пароля пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусВосстановитьПароль   - Строка, содержащая статус восстановления пароля
//	"OK" - Пароль успешно восстановлен
//  "INVU" - Логин указан не верно
//	"INVL" - Пароль указан не верно
//  Неопределено - Нарушение прав доступа
//
Функция ВосстановитьПарольПользователяMicroCat(Пользователь,Логин,Пароль) Экспорт 

	Если НЕ ПравоДоступа("Администрирование", Метаданные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();
	
	Если Количество()=0 Тогда
		Возврат "INVU";
	КонецЕсли;
	
	Если Логин<>ЭтотОбъект[0].Логин Тогда
		Возврат "INVL";
	КонецЕсли;
	
	ЭтотОбъект[0].Пользователь=Пользователь;
	ЭтотОбъект[0].Логин=Логин;
	ЭтотОбъект[0].Пароль=Пароль;
	Записать();
	
	Возврат "OK";

КонецФункции // ВосстановитьПарольПользователяMicroCat()

// Добавляет нового пользователя в список пользователей MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусНовыйПользователь   - Строка, содержащая статус добавления нового пользователя MicroCat
//	"OK" - Пользователь добавлен
//  "UOK" - Пользователь уже имеется в базе
//  Неопределено - Нарушение прав доступа
//
Функция ДобавитьПользователяMicroCat(Пользователь,Логин,Пароль) Экспорт

	Если НЕ ПравоДоступа("Администрирование", Метаданные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();
	
	Если Количество()>0 Тогда
		Возврат "UOK";
	КонецЕсли;
	
	НовыйПользователь=ЭтотОбъект.Добавить();
	НовыйПользователь.Пользователь=Пользователь;
	НовыйПользователь.Логин=Логин;
	НовыйПользователь.Пароль=Пароль;
	Записать();
	
	Возврат "OK";

КонецФункции // ДобавитьПользователяMicroCat()

// Возвращает логин пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//
// Возвращаемое значение:
//  ЛогинПользователя   - Строка, содержащая логин пользователя MicroCat, иначе пустая строка
//
Функция ПолучитьЛогинПользователяMicroCat(Пользователь) Экспорт

	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();
	
	Если Количество()>0 Тогда
		Возврат ЭтотОбъект[0].Логин;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции // ДобавитьПользователяMicroCat()

/////////////////////////////////////////////////////////////////////////
// Блок настроек параметров пользователя

// Устанавливает параметры пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  СтруктураПараметров  - Структура - Параметры пользователя MicroCat
//
// Возвращаемое значение:
//  ПараметрыПользователя   - Структура, содержащая параметры пользователя MicroCat
//
Функция ЗаписатьПараметрыПользователяMicroCat(Пользователь,СтруктураПараметров) Экспорт
	
	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();
	
	Если Количество()<>0 Тогда
		ЭтотОбъект[0].ПараметрыПользователя=Новый ХранилищеЗначения(СтруктураПараметров, Новый СжатиеДанных(9));
		
		Записать();
		
		Возврат Истина;
		
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции // ДобавитьПользователяMicroCat()

// Устанавливает параметры пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//
// Возвращаемое значение:
//  ПараметрыПользователя   - Структура, содержащая параметры пользователя MicroCat
//
Функция ПолучитьПараметрыПользователяMicroCat(Пользователь) Экспорт
	
	Отбор.Пользователь.Установить(Пользователь);
	Прочитать();	
	
	Если Количество()<>0 Тогда
		Возврат ЭтотОбъект[0].ПараметрыПользователя;
	КонецЕсли;
	Возврат Неопределено;
	
КонецФункции // ДобавитьПользователяMicroCat()


Перем ДокументОбъект Экспорт;        // Документ объект
Перем ПериодЗаписей Экспорт;        // период записей
Перем ТаблицаПрослеживаемыхТоваров Экспорт; // РезультатЗапроса или ТаблицаЗначений. Устанавливается если документ имеет "необычную" ТЧ

// Добавление новой операции
Функция ДобавитьОперациюПрослеживаемости() Экспорт
	
	Если ТаблицаПрослеживаемыхТоваров = Неопределено
		ИЛИ ТаблицаПрослеживаемыхТоваров.Количество() = 0 Тогда
		ТаблицаПрослеживаемыхТоваров = Неопределено;
		ПериодЗаписей                = Неопределено;
		ДокументОбъект 				 = Неопределено;
		Возврат Истина;
	КонецЕсли;
	
	Если ПериодЗаписей = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(ПериодЗаписей) Тогда
		ПериодЗаписей = ДокументОбъект.Дата;
	КонецЕсли;
	
	Для Каждого СтрокаОперации Из ТаблицаПрослеживаемыхТоваров Цикл
		
		Если СтрокаОперации.КоличествоПрослеживаемости = 0
			И СтрокаОперации.СуммаБезНДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = Добавить();
		НоваяЗапись.Период = ПериодЗаписей;
		НоваяЗапись.Регистратор = ДокументОбъект.Ссылка;
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаОперации);
		
	КонецЦикла;
	
	Попытка
		Записать(Ложь);
	Исключение
		дкСообщитьРезультат("В процессе записи данных в регистр возникла ошибка.", "Операции прослеживаемых товаров", ДокументОбъект);//ОписаниеОшибки()
		ТаблицаПрослеживаемыхТоваров = Неопределено;
		ПериодЗаписей                = Неопределено;
		ДокументОбъект 				 = Неопределено;
		Возврат Ложь;
	КонецПопытки;
	
	// скинем переменные
	ТаблицаПрослеживаемыхТоваров = Неопределено;
	ПериодЗаписей                = Неопределено;
	
	// убиваем циклическую ссылку
	ДокументОбъект               = Неопределено;
	
	Возврат Истина;
	
КонецФункции

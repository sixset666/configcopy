
//Процедура заполняет артикул, дату записи и преобразует артикул для поиска
Процедура ЗаполнитьАртикулДляПоиска() Экспорт 
	
	#Если Клиент Тогда
	Состояние(НСтр("ru='Проверка прайс-листов контрагентов.'"));
	#КонецЕсли
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрайсЛистыКонтрагентов.Производитель КАК Производитель,
		|	ПрайсЛистыКонтрагентов.ПрайсЛист КАК ПрайсЛист,
		|	ПрайсЛистыКонтрагентов.ДатаЗаписи
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
		|ГДЕ
		|	НЕ ПрайсЛистыКонтрагентов.АртикулДляПоиска = """"
		|	И ПрайсЛистыКонтрагентов.Артикул = """"
		|	И ПрайсЛистыКонтрагентов.ДатаЗаписи = ДАТАВРЕМЯ(1, 1, 1)";
				   
	Результат = Запрос.Выполнить();
				   
	Если Результат.Пустой() Тогда
		#Если Клиент Тогда
		Сообщить(НСтр("ru='Обновление регистра сведений ""Прайс-листы контрагентов"" не требуется'"));
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Прайс-листы контрагентов""'"));
	#КонецЕсли
	
	Выборка = Результат.Выбрать(); 
	
	РазмерВыборки	= Выборка.Количество();
	РазмерПорции	= Окр(100/РазмерВыборки);
	СчВыборки		= 0;
	ПроцентПрогресса = 0;
	
	Пока Выборка.Следующий()Цикл
		СчВыборки = СчВыборки + 1;
		
		НачатьТранзакцию();
		
		НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрайсЛист.	 Установить(Выборка.ПрайсЛист,		Истина);
		НаборЗаписей.Отбор.ДатаЗаписи.	 Установить(Выборка.ДатаЗаписи,		Истина);
		НаборЗаписей.Отбор.Производитель.Установить(Выборка.Производитель,	Истина);
		
		НаборЗаписей.Прочитать();
		
		ТабНабора = НаборЗаписей.Выгрузить();
		
		НаборЗаписей.Очистить();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
		
		ПорцияЗаписей = ТабНабора.СкопироватьКолонки();
		ПорцияЗаписей.Индексы.Добавить("АртикулДляПоиска,Номенклатура");
		
		ДатаЗаписи = ТекущаяДата();
		
		РазмерНабора  = ТабНабора.Количество();
		СчНабора = 0;
		
		Пока ТабНабора.Количество() > 0 Цикл 
			Если ДатаЗаписи > ТекущаяДата() Тогда
				ДатаЗаписи = ДатаЗаписи + 1;
			Иначе
				ДатаЗаписи = ТекущаяДата();
			КонецЕсли;
			
			Сч = 0;
			Пока ТабНабора.Количество() > Сч Цикл
				
				СчНабора = СчНабора + 1;
				
				#Если Клиент Тогда
				Если Цел(Окр(СчНабора * РазмерПорции / РазмерНабора) + (РазмерПорции * (СчВыборки-1))) > ПроцентПрогресса Тогда
					ПроцентПрогресса = Цел(Окр(СчНабора * РазмерПорции / РазмерНабора) + (РазмерПорции * (СчВыборки-1)));
					Состояние(НСтр("ru='Обновление регистра сведений ""Прайс-листы контрагентов"": '") + Строка(ПроцентПрогресса)+"%");
				КонецЕсли;
				#КонецЕсли
				
				Если ПорцияЗаписей.Количество() > 10000 Тогда
					НаборЗаписей.Отбор.ДатаЗаписи.Установить(ДатаЗаписи, Истина);
					НаборЗаписей.Загрузить(ПорцияЗаписей);
					НаборЗаписей.ОбменДанными.Загрузка = Истина;
					НаборЗаписей.Записать();
					Если ДатаЗаписи > ТекущаяДата() Тогда
						ДатаЗаписи = ДатаЗаписи + 1;
					Иначе
						ДатаЗаписи = ТекущаяДата();
					КонецЕсли;
					ПорцияЗаписей.Очистить();
					Сч = 0;
					Продолжить;
				КонецЕсли;
				
				ИсходнаяСтрока = ТабНабора[Сч];
				
				Артикул			 = ИсходнаяСтрока.АртикулДляПоиска;
				АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, ИсходнаяСтрока.Производитель);
				
				Если ПустаяСтрока(АртикулДляПоиска)Тогда
					АртикулДляПоиска = Артикул;
				КонецЕсли;
				
				Если ПорцияЗаписей.НайтиСтроки(Новый Структура("АртикулДляПоиска,Номенклатура", АртикулДляПоиска, ИсходнаяСтрока.Номенклатура)).Количество() > 0 Тогда
					Сч = Сч + 1;
					СчНабора = СчНабора - 1;
					Продолжить;
				КонецЕсли;
				
				СтрокаПорции = ПорцияЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПорции, ИсходнаяСтрока);
				СтрокаПорции.ДатаЗаписи = ДатаЗаписи;
				СтрокаПорции.АртикулДляПоиска = АртикулДляПоиска;
				СтрокаПорции.Артикул = Артикул;
				ТабНабора.Удалить(ИсходнаяСтрока);
				
			КонецЦикла;
			    				
			Если ПорцияЗаписей.Количество() > 0 Тогда
				НаборЗаписей.Отбор.ДатаЗаписи.Установить(ДатаЗаписи, Истина);
				НаборЗаписей.Загрузить(ПорцияЗаписей);
				НаборЗаписей.ОбменДанными.Загрузка = Истина;
				НаборЗаписей.Записать();
			КонецЕсли;
			
			ПорцияЗаписей.Очистить();
			
		КонецЦикла;
					
		ЗафиксироватьТранзакцию();
	КонецЦикла;
	
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Прайс-листы контрагентов"" завершено.'"));
	#КонецЕсли
	
КонецПроцедуры //ЗаполнитьАртикулДляПоиска()

//Возвращает данные из прайс-листов по переданной номенклатуре
Функция ПолучитьПрайсЛистыКонтрагентов(Артикул_Номенклатура, Производитель=Неопределено, Поставщик=Неопределено, ДатаЦен=Неопределено, ПодразделениеКомпании=Неопределено) Экспорт 
	
	Если ДатаЦен = Неопределено Тогда
		ДатаЦен = ТекущаяДата();
	КонецЕсли;
	
	Контрагент = Неопределено;
	ПрайсЛистКонтрагента = Неопределено;
	
	Если ТипЗнч(Поставщик) = Тип("СправочникСсылка.Контрагенты") Тогда
		Контрагент = Поставщик;
	ИначеЕсли ТипЗнч(Поставщик) = Тип("СправочникСсылка.ПрайсЛистыКонтрагентов") Тогда
		ПрайсЛистКонтрагента = Поставщик;
	ИначеЕсли ТипЗнч(Поставщик) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента);
	Запрос.УстановитьПараметр("Дата", ДатаЦен);
	Если ТипЗнч(Производитель) = Тип("Строка") Тогда
		Запрос.УстановитьПараметр("ПроизводительВПрайсЛисте", Производитель);
	КонецЕсли;
	
	ЭтоВебПрайсЛист = ЗначениеЗаполнено(ПрайсЛистКонтрагента) И ПрайсЛистКонтрагента.ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ВебПрайсЛист;
	
	Если ТипЗнч(Артикул_Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПравилаЗагрузки.ПрайсЛист,
		|	ВЫРАЗИТЬ(ПравилаЗагрузки.ОбъектПравила КАК СТРОКА(32)) КАК КлючСтроки
		|ПОМЕСТИТЬ втПравилаЗагрузки
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
		|		ПО ПравилаЗагрузки.ПрайсЛист = ЗапрещенныеПодразделения.ПрайсЛист
		|			И ("+?(ЗначениеЗаполнено(ПодразделениеКомпании), "ЗапрещенныеПодразделения.ПодразделениеКомпании = &ПодразделениеКомпании", "ЛОЖЬ")+"
		|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
		|ГДЕ
		|	(ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура)) = &Номенклатура
		|	И ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки)
		|	И ПравилаЗагрузки.ВидПравила = ЗНАЧЕНИЕ(Перечисление.ВидыПравилЗагрузки.ПрисвоитьЗначение)
		|	И ПравилаЗагрузки.ИмяРеквизитаПрайсЛиста = ""Номенклатура""
		|	"+?(ЗначениеЗаполнено(Контрагент), "И ПравилаЗагрузки.ПрайсЛист.Владелец = &Контрагент", "")+"
		|	"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПравилаЗагрузки.ПрайсЛист = &ПрайсЛист", "")+"
		|	И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрайсЛистыКонтрагентов.Артикул,
		|	ПрайсЛистыКонтрагентов.Производитель,
		|	ПрайсЛистыКонтрагентов.Наименование,
		|	ПрайсЛистыКонтрагентов.НаименованиеИностранное,
		|	ПрайсЛистыКонтрагентов.Количество,
		|	ПрайсЛистыКонтрагентов.ПрайсЛист,
		|	ПрайсЛистыКонтрагентов.Цена КАК Цена,
		|	ПрайсЛистыКонтрагентов.Валюта,
		|	ПрайсЛистыКонтрагентов.СрокПоставки,
		|	ПрайсЛистыКонтрагентов.СрокПоставкиМинимальный,
		|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика,
		|	ПрайсЛистыКонтрагентов.ТегПозиции
		|ПОМЕСТИТЬ втПозицииПрайсЛистов
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентов"+?(ЭтоВебПрайсЛист И ТипЗнч(Производитель) = Тип("Строка"), "Временный", "")+" КАК ПрайсЛистыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
		|		ПО ПрайсЛистыКонтрагентов.ПрайсЛист = ЗапрещенныеПодразделения.ПрайсЛист
		|			И ("+?(ЗначениеЗаполнено(ПодразделениеКомпании), "ЗапрещенныеПодразделения.ПодразделениеКомпании = &ПодразделениеКомпании", "ЛОЖЬ")+"
		|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.АртикулДляПоиска <> """"
		|	И ПрайсЛистыКонтрагентов.АртикулДляПоиска = &АртикулДляПоиска
		|	И ПрайсЛистыКонтрагентов.Производитель = &Производитель
		|	"+?(ЗначениеЗаполнено(Контрагент), "И ПрайсЛистыКонтрагентов.ПрайсЛист.Владелец = &Контрагент", "")+"
		|	"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист", "")+"
		|	"+?(ТипЗнч(Производитель) = Тип("Строка"), "И ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте = &ПроизводительВПрайсЛисте", "")+"
		|	И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ПрайсЛистыКонтрагентов.Артикул,
		|	ПрайсЛистыКонтрагентов.Производитель,
		|	ПрайсЛистыКонтрагентов.Наименование,
		|	ПрайсЛистыКонтрагентов.НаименованиеИностранное,
		|	ПрайсЛистыКонтрагентов.Количество,
		|	ПрайсЛистыКонтрагентов.ПрайсЛист,
		|	ПрайсЛистыКонтрагентов.Цена,
		|	ПрайсЛистыКонтрагентов.Валюта,
		|	ПрайсЛистыКонтрагентов.СрокПоставки,
		|	ПрайсЛистыКонтрагентов.СрокПоставкиМинимальный,
		|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика,
		|	ПрайсЛистыКонтрагентов.ТегПозиции
		|ИЗ
		|	втПравилаЗагрузки КАК ПравилаЗагрузки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентов"+?(ЭтоВебПрайсЛист И ТипЗнч(Производитель) = Тип("Строка"), "Временный", "")+" КАК ПрайсЛистыКонтрагентов
		|		ПО ПравилаЗагрузки.ПрайсЛист = ПрайсЛистыКонтрагентов.ПрайсЛист
		|			И ПравилаЗагрузки.КлючСтроки = ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втПравилаЗагрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПозицииПрайсЛистов.ПрайсЛист,
		|	ПозицииПрайсЛистов.Артикул,
		|	ПозицииПрайсЛистов.Производитель,
		|	ПозицииПрайсЛистов.Наименование,
		|	ПозицииПрайсЛистов.ТегПозиции,
		|	ПозицииПрайсЛистов.Количество,
		|	ПозицииПрайсЛистов.Цена КАК ЦенаВПрайсЛисте,
		|	ПозицииПрайсЛистов.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) / 100 КАК Цена,
		|	ПозицииПрайсЛистов.Валюта,
		|	ПозицииПрайсЛистов.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1) / 100 КАК ЦенаРег,
		|	ПозицииПрайсЛистов.СрокПоставки,
		|	ПозицииПрайсЛистов.СрокПоставкиМинимальный,
		|	ПозицииПрайсЛистов.ПрайсЛист.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ПозицииПрайсЛистов.ПрайсЛист.СтавкаНДС КАК СтавкаНДС,
		|	ПозицииПрайсЛистов.НаименованиеИностранное,
		|	ПозицииПрайсЛистов.КлючСтрокиПоставщика
		|ИЗ
		|	втПозицииПрайсЛистов КАК ПозицииПрайсЛистов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						втПозицииПрайсЛистов.ПрайсЛист
		|					ИЗ
		|						втПозицииПрайсЛистов)) КАК Скидки
		|		ПО (НЕ Скидки.Отменена)
		|			И ПозицииПрайсЛистов.ПрайсЛист = Скидки.ПрайсЛист
		|			И ПозицииПрайсЛистов.ТегПозиции = Скидки.ТегПозиции
		|			И ПозицииПрайсЛистов.Производитель = Скидки.Производитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						втПозицииПрайсЛистов.ПрайсЛист
		|					ИЗ
		|						втПозицииПрайсЛистов)
		|					И ТегПозиции <> """"
		|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|					) КАК СкидкиТегПозиции
		|		ПО (НЕ СкидкиТегПозиции.Отменена)
		|			И ПозицииПрайсЛистов.ПрайсЛист = СкидкиТегПозиции.ПрайсЛист
		|			И ПозицииПрайсЛистов.ТегПозиции = СкидкиТегПозиции.ТегПозиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						втПозицииПрайсЛистов.ПрайсЛист
		|					ИЗ
		|						втПозицииПрайсЛистов)
		|					И ТегПозиции = """"
		|					И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|					) КАК СкидкиПроизводитель
		|		ПО (НЕ СкидкиПроизводитель.Отменена)
		|			И ПозицииПрайсЛистов.ПрайсЛист = СкидкиПроизводитель.ПрайсЛист
		|			И ПозицииПрайсЛистов.Производитель = СкидкиПроизводитель.Производитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ПрайсЛист В
		|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|							втПозицииПрайсЛистов.ПрайсЛист
		|						ИЗ
		|							втПозицииПрайсЛистов)
		|					И ТегПозиции = """"
		|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|					) КАК СкидкиБазовые
		|		ПО (НЕ СкидкиБазовые.Отменена)
		|			И ПозицииПрайсЛистов.ПрайсЛист = СкидкиБазовые.ПрайсЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
		|		ПО ПозицииПрайсЛистов.Валюта = КурсыВалют.Валюта
		|УПОРЯДОЧИТЬ ПО
		|	ЦенаРег, СрокПоставкиМинимальный
		|";
		
		Запрос.УстановитьПараметр("Номенклатура", Артикул_Номенклатура);
		Запрос.УстановитьПараметр("АртикулДляПоиска", Артикул_Номенклатура.АртикулДляПоиска);
		Запрос.УстановитьПараметр("Производитель", Артикул_Номенклатура.Производитель);
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПрайсЛистыКонтрагентов.ПрайсЛист,
		|	ПрайсЛистыКонтрагентов.Артикул,
		|	ПрайсЛистыКонтрагентов.Производитель,
		|	ПрайсЛистыКонтрагентов.Наименование,
		|	ПрайсЛистыКонтрагентов.ТегПозиции,
		|	ПрайсЛистыКонтрагентов.Количество,
		|	ПрайсЛистыКонтрагентов.Цена КАК ЦенаВПрайсЛисте,
		|	ПрайсЛистыКонтрагентов.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) / 100 КАК Цена,
		|	ПрайсЛистыКонтрагентов.Валюта,
		|	ПрайсЛистыКонтрагентов.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1) / 100 КАК ЦенаРег,
		|	ПрайсЛистыКонтрагентов.СрокПоставки,
		|	ПрайсЛистыКонтрагентов.СрокПоставкиМинимальный КАК СрокПоставкиМинимальный,
		|	ПрайсЛистыКонтрагентов.ПрайсЛист.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ПрайсЛистыКонтрагентов.ПрайсЛист.СтавкаНДС КАК СтавкаНДС,
		|	ПрайсЛистыКонтрагентов.НаименованиеИностранное,
		|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентов"+?(ЭтоВебПрайсЛист И ТипЗнч(Производитель) = Тип("Строка"), "Временный", "")+" КАК ПрайсЛистыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
		|		ПО ПрайсЛистыКонтрагентов.ПрайсЛист = ЗапрещенныеПодразделения.ПрайсЛист
		|			И ("+?(ЗначениеЗаполнено(ПодразделениеКомпании), "ЗапрещенныеПодразделения.ПодразделениеКомпании = &ПодразделениеКомпании", "ЛОЖЬ")+"
		|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ИСТИНА
		|					"+?(ЗначениеЗаполнено(Контрагент), "И ПрайсЛист.Владелец = &Контрагент", "")+"
		|					"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПрайсЛист = &ПрайсЛист", "")+"
		|					) КАК Скидки
		|		ПО (НЕ Скидки.Отменена)
		|			И ПрайсЛистыКонтрагентов.ПрайсЛист = Скидки.ПрайсЛист
		|			И ПрайсЛистыКонтрагентов.ТегПозиции = Скидки.ТегПозиции
		|			И ПрайсЛистыКонтрагентов.Производитель = Скидки.Производитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ИСТИНА
		|					"+?(ЗначениеЗаполнено(Контрагент), "И ПрайсЛист.Владелец = &Контрагент", "")+"
		|					"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПрайсЛист = &ПрайсЛист", "")+"
		|					И ТегПозиции <> """"
		|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|					) КАК СкидкиТегПозиции
		|		ПО (НЕ СкидкиТегПозиции.Отменена)
		|			И ПрайсЛистыКонтрагентов.ПрайсЛист = СкидкиТегПозиции.ПрайсЛист
		|			И ПрайсЛистыКонтрагентов.ТегПозиции = СкидкиТегПозиции.ТегПозиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ИСТИНА
		|					"+?(ЗначениеЗаполнено(Контрагент), "И ПрайсЛист.Владелец = &Контрагент", "")+"
		|					"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПрайсЛист = &ПрайсЛист", "")+"
		|					И ТегПозиции = """"
		|					И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|					) КАК СкидкиПроизводитель
		|		ПО (НЕ СкидкиПроизводитель.Отменена)
		|			И ПрайсЛистыКонтрагентов.ПрайсЛист = СкидкиПроизводитель.ПрайсЛист
		|			И ПрайсЛистыКонтрагентов.Производитель = СкидкиПроизводитель.Производитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|				&Дата,
		|				ИСТИНА
		|					"+?(ЗначениеЗаполнено(Контрагент), "И ПрайсЛист.Владелец = &Контрагент", "")+"
		|					"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПрайсЛист = &ПрайсЛист", "")+"
		|					И ТегПозиции = """"
		|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
		|			) КАК СкидкиБазовые
		|		ПО (НЕ СкидкиБазовые.Отменена)
		|			И ПрайсЛистыКонтрагентов.ПрайсЛист = СкидкиБазовые.ПрайсЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
		|		ПО ПрайсЛистыКонтрагентов.Валюта = КурсыВалют.Валюта
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.АртикулДляПоиска <> """"
		|	И ПрайсЛистыКонтрагентов.АртикулДляПоиска В (&СписокАртикулов)
		|	"+?(Производитель <> Неопределено, "И ПрайсЛистыКонтрагентов.Производитель = &Производитель", "")+"
		|	"+?(ЗначениеЗаполнено(Контрагент), "И ПрайсЛистыКонтрагентов.ПрайсЛист.Владелец = &Контрагент", "")+"
		|	"+?(ЗначениеЗаполнено(ПрайсЛистКонтрагента), "И ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист", "")+"
		|	"+?(ТипЗнч(Производитель) = Тип("Строка"), "И ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте = &ПроизводительВПрайсЛисте", "")+"
		|	И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
		|УПОРЯДОЧИТЬ ПО
		|	ЦенаРег, СрокПоставкиМинимальный
		|";
		
		АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул_Номенклатура);
		СписокАртикулов = Новый СписокЗначений;
		СписокАртикулов.Добавить(Артикул_Номенклатура);
		Если АртикулДляПоиска <> Артикул_Номенклатура Тогда
			СписокАртикулов.Добавить(АртикулДляПоиска);
		КонецЕсли;
		Запрос.УстановитьПараметр("СписокАртикулов", СписокАртикулов);
		Запрос.УстановитьПараметр("Производитель", Производитель);
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//Маршрутизирует выполнение процесса переноса записей прайс-листа в наиболее оптимальную процедуру
Функция ОбновитьЗаписиВОсновномРегистре(ПрайсЛистКонтрагента, ПорцияЗаписи = Неопределено, КлючСтрокиПоставщика = Неопределено) Экспорт
	Если ПорцияЗаписи = Неопределено Тогда
		ПорцияЗаписи = 9999;
	КонецЕсли;
	Если ТипЗнч(КлючСтрокиПоставщика) = Тип("Строка")
		ИЛИ (ТипЗнч(КлючСтрокиПоставщика) = Тип("Массив") И КлючСтрокиПоставщика.Количество() <= ПорцияЗаписи) Тогда
		Результат = ОбновитьЗаписиПоКлючуСтроки(ПрайсЛистКонтрагента, КлючСтрокиПоставщика);
	Иначе
		Результат = ОбновитьЗаписиВсе(ПрайсЛистКонтрагента, ПорцияЗаписи);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

//Выполняет перенос подходящих записей в постоянный регистр по переданным ключам строк
Функция ОбновитьЗаписиПоКлючуСтроки(ПрайсЛистКонтрагента, КлючСтрокиПоставщика)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Временный.ПрайсЛист КАК ПрайсЛист,
	|	Временный.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	МАКСИМУМ(Временный.АртикулДляПоиска) КАК АртикулДляПоиска,
	|	МАКСИМУМ(Временный.Производитель) КАК Производитель,
	|	МАКСИМУМ(Временный.Наименование) КАК Наименование,
	|	МАКСИМУМ(Временный.НаименованиеИностранное) КАК НаименованиеИностранное,
	|	МАКСИМУМ(Временный.Цена) КАК Цена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(Временный.Количество КАК БУЛЕВО)) = ИСТИНА
	|				ТОГДА 0
	|			ИНАЧЕ Временный.Количество
	|		КОНЕЦ) КАК Количество,
	|	МАКСИМУМ(Временный.ЗапретПродажи) КАК ЗапретПродажи,
	|	МАКСИМУМ(Временный.ЗапретЗакупки) КАК ЗапретЗакупки,
	|	МАКСИМУМ(Временный.КратностьПоставок) КАК КратностьПоставок,
	|	МАКСИМУМ(Временный.ТегПозиции) КАК ТегПозиции,
	|	МАКСИМУМ(Временный.Валюта) КАК Валюта,
	|	МАКСИМУМ(Временный.СрокПоставкиМинимальный) КАК СрокПоставкиМинимальный,
	|	МАКСИМУМ(Временный.СрокПоставкиМаксимальный) КАК СрокПоставкиМаксимальный,
	|	МАКСИМУМ(Временный.СрокПоставки) КАК СрокПоставки,
	|	МАКСИМУМ(Временный.Артикул) КАК Артикул,
	|	МАКСИМУМ(Временный.СнятаСПроизводства) КАК СнятаСПроизводства,
	|	МАКСИМУМ(Временный.Вес) КАК Вес,
	|	МАКСИМУМ(Временный.Объем) КАК Объем,
	|	МАКСИМУМ(Временный.ПроизводительВПрайсЛисте) КАК ПроизводительВПрайсЛисте,
	|	Временный.ПрайсЛист.Владелец КАК Контрагент
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК Временный
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
	|		ПО Временный.ПрайсЛист = ПравилаЗагрузки.ПрайсЛист
	|			И Временный.КлючСтрокиПоставщика = ПравилаЗагрузки.ОбъектПравила
	|			И (ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки))
	|			И (ПравилаЗагрузки.Значение ССЫЛКА Справочник.Номенклатура)
	|			И ((ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура)) <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ЗапретЗагрузкиПроизводитель
	|		ПО Временный.ПрайсЛист = ЗапретЗагрузкиПроизводитель.ПрайсЛист
	|			И (Временный.ПроизводительВПрайсЛисте = (ВЫРАЗИТЬ(ЗапретЗагрузкиПроизводитель.ОбъектПравила КАК СТРОКА(100))))
	|			И (ЗапретЗагрузкиПроизводитель.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.ПроизводительВПрайсЛисте))
	|			И (ЗапретЗагрузкиПроизводитель.ВидПравила = ЗНАЧЕНИЕ(Перечисление.ВидыПравилЗагрузки.НеЗагружать))
	|			И (Временный.Производитель = (ВЫРАЗИТЬ(ЗапретЗагрузкиПроизводитель.Значение КАК Справочник.Производители)))
	|ГДЕ
	|	Временный.ПрайсЛист = &ПрайсЛист
	|	И Временный.КлючСтрокиПоставщика В(&КлючиСтрок)
	|	И ((ВЫРАЗИТЬ(Временный.Количество КАК БУЛЕВО)) = ИСТИНА
	|			ИЛИ (ВЫРАЗИТЬ(Временный.Количество КАК ЧИСЛО(15, 3))) > 0)
	|	И Временный.ЗапретЗагрузки = ЛОЖЬ
	|	И (Временный.ПрайсЛист.ВидПрайсЛиста = ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ПрайсЛистПоставщика)
	|			ИЛИ Временный.ПрайсЛист.ВидПрайсЛиста = ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ОстаткиДляКаталогаПредложений))
	|	И (Временный.АртикулДляПоиска <> """"
	|				И (НЕ Временный.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
	|					ИЛИ Временный.ПроизводительВПрайсЛисте = """")
	|			ИЛИ НЕ ПравилаЗагрузки.Значение ЕСТЬ NULL )
	|	И ЗапретЗагрузкиПроизводитель.ПрайсЛист ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Временный.ПрайсЛист,
	|	Временный.КлючСтрокиПоставщика
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючСтрокиПоставщика";
	
	СписокЗначений = Новый СписокЗначений;
	
	Если ТипЗнч(КлючСтрокиПоставщика) = Тип("Массив") Тогда
		СписокЗначений.ЗагрузитьЗначения(КлючСтрокиПоставщика);
	Иначе
		СписокЗначений.Добавить(КлючСтрокиПоставщика);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента);
	Запрос.УстановитьПараметр("КлючиСтрок", СписокЗначений);
	
	ДатаЗаписиПорцииДанных = ТекущаяДата();
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Менеджер = РегистрыСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи.СоздатьМенеджерЗаписи();
		Менеджер.ПрайсЛист = ПрайсЛистКонтрагента;
		Менеджер.ДатаЗаписи = ДатаЗаписиПорцииДанных;
		Менеджер.Записать();
		
		НачатьТранзакцию();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентов");
		ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛистКонтрагента);
		ЭлементБлокировки.УстановитьЗначение("ДатаЗаписи", ДатаЗаписиПорцииДанных);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛистКонтрагента, Истина);
		НаборЗаписей.Отбор.ДатаЗаписи.Установить(ДатаЗаписиПорцииДанных, Истина);
		
		КодПредложения = 0;
		
		ВыборкаЗаписей = Результат.Выбрать();
		Пока ВыборкаЗаписей.Следующий() Цикл
			НоваяСтрока = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗаписей); 
			КодПредложения = КодПредложения + 1;
			НоваяСтрока.ДатаЗаписи = ДатаЗаписиПорцииДанных;
			НоваяСтрока.КодПредложения = КодПредложения;
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			#Если Клиент Тогда
				Сообщить(ОписаниеОшибки());
			#КонецЕсли
			Возврат Ложь;
		КонецПопытки;
		
	КонецЕсли;
	
	//Удалим из регистра "ПрайсЛистыКонтрагентов" старые записи
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.ПрайсЛист,
	|	ПрайсЛистыКонтрагентов.Номенклатура,
	|	ПрайсЛистыКонтрагентов.АртикулДляПоиска,
	|	ПрайсЛистыКонтрагентов.Производитель,
	|	ПрайсЛистыКонтрагентов.ДатаЗаписи,
	|	ПрайсЛистыКонтрагентов.КодПредложения
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|ГДЕ
	|	ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист
	|	И ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика В(&КлючиСтрок)
	|	И ПрайсЛистыКонтрагентов.ДатаЗаписи <> &ДатаЗаписи";
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента);
	Запрос.УстановитьПараметр("КлючиСтрок", СписокЗначений);
	Запрос.УстановитьПараметр("ДатаЗаписи", ДатаЗаписиПорцииДанных);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Результат;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ПрайсЛист",        "ПрайсЛист");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ДатаЗаписи",       "ДатаЗаписи");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("КодПредложения",   "КодПредложения");
	Блокировка.Заблокировать();
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрайсЛист.Установить       (ПрайсЛистКонтрагента);
		НаборЗаписей.Отбор.ДатаЗаписи.Установить      (Выборка.ДатаЗаписи);
		НаборЗаписей.Отбор.КодПредложения.Установить  (Выборка.КодПредложения);
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОтменитьТранзакцию();
			#Если Клиент Тогда
				Сообщить(ОписаниеОшибки());
			#КонецЕсли
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
	//Очистим регистр "ПрайсЛистыКонтрагентовНедоступныеЗаписи"
	НачатьТранзакцию();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи");
	ЭлементБлокировки.УстановитьЗначение("ПрайсЛист",  ПрайсЛистКонтрагента);
	ЭлементБлокировки.УстановитьЗначение("ДатаЗаписи", ДатаЗаписиПорцииДанных);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛистКонтрагента, Истина);
	НаборЗаписей.Отбор.ДатаЗаписи.Установить(ДатаЗаписиПорцииДанных, Истина);
	Попытка
		НаборЗаписей.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		#Если Клиент Тогда
			Сообщить(ОписаниеОшибки());
		#КонецЕсли
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции

//Выполняет перенос всех подходящих записей прайс-листа из временного в постоянный регистр
Функция ОбновитьЗаписиВсе(ПрайсЛистКонтрагента, ПорцияЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ "+Формат(ПорцияЗаписи, "ЧГ=0")+"
	|	Временный.ПрайсЛист,
	|	Временный.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	МАКСИМУМ(Временный.АртикулДляПоиска) КАК АртикулДляПоиска,
	|	МАКСИМУМ(Временный.Производитель) КАК Производитель,
	|	МАКСИМУМ(Временный.Наименование) КАК Наименование,
	|	МАКСИМУМ(Временный.НаименованиеИностранное) КАК НаименованиеИностранное,
	|	МАКСИМУМ(Временный.Цена) КАК Цена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(Временный.Количество КАК БУЛЕВО)) = ИСТИНА
	|				ТОГДА 0
	|			ИНАЧЕ Временный.Количество
	|		КОНЕЦ) КАК Количество,
	|	МАКСИМУМ(Временный.ЗапретПродажи) КАК ЗапретПродажи,
	|	МАКСИМУМ(Временный.ЗапретЗакупки) КАК ЗапретЗакупки,
	|	МАКСИМУМ(Временный.КратностьПоставок) КАК КратностьПоставок,
	|	МАКСИМУМ(Временный.ТегПозиции) КАК ТегПозиции,
	|	МАКСИМУМ(Временный.Валюта) КАК Валюта,
	|	МАКСИМУМ(Временный.СрокПоставкиМинимальный) КАК СрокПоставкиМинимальный,
	|	МАКСИМУМ(Временный.СрокПоставкиМаксимальный) КАК СрокПоставкиМаксимальный,
	|	МАКСИМУМ(Временный.СрокПоставки) КАК СрокПоставки,
	|	МАКСИМУМ(Временный.Артикул) КАК Артикул,
	|	МАКСИМУМ(Временный.СнятаСПроизводства) КАК СнятаСПроизводства,
	|	МАКСИМУМ(Временный.Вес) КАК Вес,
	|	МАКСИМУМ(Временный.Объем) КАК Объем,
	|	МАКСИМУМ(Временный.ПроизводительВПрайсЛисте) КАК ПроизводительВПрайсЛисте,
	|	Временный.ПрайсЛист.Владелец КАК Контрагент
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК Временный
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
	|		ПО Временный.ПрайсЛист = ПравилаЗагрузки.ПрайсЛист
	|			И Временный.КлючСтрокиПоставщика = ПравилаЗагрузки.ОбъектПравила
	|			И (ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки))
	|			И (ПравилаЗагрузки.Значение ССЫЛКА Справочник.Номенклатура)
	|			И ((ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура)) <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ЗапретЗагрузкиПроизводитель
	|		ПО Временный.ПрайсЛист = ЗапретЗагрузкиПроизводитель.ПрайсЛист
	|			И (Временный.ПроизводительВПрайсЛисте = (ВЫРАЗИТЬ(ЗапретЗагрузкиПроизводитель.ОбъектПравила КАК СТРОКА(100))))
	|			И (ЗапретЗагрузкиПроизводитель.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.ПроизводительВПрайсЛисте))
	|			И (ЗапретЗагрузкиПроизводитель.ВидПравила = ЗНАЧЕНИЕ(Перечисление.ВидыПравилЗагрузки.НеЗагружать))
	|			И (Временный.Производитель = (ВЫРАЗИТЬ(ЗапретЗагрузкиПроизводитель.Значение КАК Справочник.Производители)))
	|ГДЕ
	|	Временный.ПрайсЛист = &ПрайсЛист
	|	И Временный.КлючСтрокиПоставщика > &КлючСтрокиПоставщика
	|	И ((ВЫРАЗИТЬ(Временный.Количество КАК БУЛЕВО)) = ИСТИНА
	|			ИЛИ (ВЫРАЗИТЬ(Временный.Количество КАК ЧИСЛО(15, 3))) > 0)
	|	И Временный.ЗапретЗагрузки = ЛОЖЬ
	|	И (Временный.ПрайсЛист.ВидПрайсЛиста = ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ПрайсЛистПоставщика)
	|			ИЛИ Временный.ПрайсЛист.ВидПрайсЛиста = ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ОстаткиДляКаталогаПредложений))
	|	И (Временный.АртикулДляПоиска <> """"
	|				И (НЕ Временный.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
	|					ИЛИ Временный.ПроизводительВПрайсЛисте = """")
	|			ИЛИ НЕ ПравилаЗагрузки.Значение ЕСТЬ NULL )
	|	И ЗапретЗагрузкиПроизводитель.ПрайсЛист ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Временный.ПрайсЛист,
	|	Временный.КлючСтрокиПоставщика
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючСтрокиПоставщика";
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента);
	
	ДатаЗаписиПорцииДанных = ТекущаяДата();
	КлючСтрокиПоставщика = "";
	
	Пока Истина Цикл
		
		Запрос.УстановитьПараметр("КлючСтрокиПоставщика", КлючСтрокиПоставщика);
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		Менеджер = РегистрыСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи.СоздатьМенеджерЗаписи();
		Менеджер.ПрайсЛист = ПрайсЛистКонтрагента;
		Менеджер.ДатаЗаписи = ДатаЗаписиПорцииДанных;
		Менеджер.Записать();
		
		НачатьТранзакцию();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентов");
		ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛистКонтрагента);
		ЭлементБлокировки.УстановитьЗначение("ДатаЗаписи", ДатаЗаписиПорцииДанных);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛистКонтрагента);
		НаборЗаписей.Отбор.ДатаЗаписи.Установить(ДатаЗаписиПорцииДанных);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		
		КодПредложения = 0;
		
		ВыборкаЗаписей = Результат.Выбрать();
		Пока ВыборкаЗаписей.Следующий() Цикл
			НоваяСтрока = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗаписей); 
			КодПредложения = КодПредложения + 1;
			НоваяСтрока.КодПредложения = КодПредложения;
			НоваяСтрока.ДатаЗаписи = ДатаЗаписиПорцииДанных;
			КлючСтрокиПоставщика = ВыборкаЗаписей.КлючСтрокиПоставщика;
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			#Если Клиент Тогда
				Сообщить(ОписаниеОшибки());
			#КонецЕсли
			Возврат Ложь;
		КонецПопытки;
		
		Если ТекущаяДата() <= ДатаЗаписиПорцииДанных Тогда
			ДатаЗаписиПорцииДанных = ДатаЗаписиПорцииДанных + 1;
		Иначе
			ДатаЗаписиПорцииДанных = ТекущаяДата();
		КонецЕсли;
		
	КонецЦикла;
	
	//Удалим из регистра "ПрайсЛистыКонтрагентов" старые записи
	//Получим все даты записей из регистра "ПрайсЛистыКонтрагентов",
	//которых нет в "ПрайсЛистыКонтрагентовНедоступныеЗаписи"
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПрайсЛистыКонтрагентов.ДатаЗаписи
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
	|		ПО НедоступныеЗаписи.ПрайсЛист = ПрайсЛистыКонтрагентов.ПрайсЛист
	|			И НедоступныеЗаписи.ДатаЗаписи = ПрайсЛистыКонтрагентов.ДатаЗаписи
	|ГДЕ
	|	ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист
	|	И НедоступныеЗаписи.ДатаЗаписи ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентов");
		ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛистКонтрагента);
		ЭлементБлокировки.УстановитьЗначение("ДатаЗаписи", Выборка.ДатаЗаписи);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛистКонтрагента);
		НаборЗаписей.Отбор.ДатаЗаписи.Установить(Выборка.ДатаЗаписи);
		Попытка
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			#Если Клиент Тогда
				Сообщить(ОписаниеОшибки());
			#КонецЕсли
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	//Очистим регистр "ПрайсЛистыКонтрагентовНедоступныеЗаписи"
	НачатьТранзакцию();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи");
	ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛистКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛистКонтрагента, Истина);
	Попытка
		НаборЗаписей.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		#Если Клиент Тогда
			Сообщить(ОписаниеОшибки());
		#КонецЕсли
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции


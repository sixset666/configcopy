Перем ЗаказНаряд Экспорт;      // Документ.ЗаказНаряд ИЛИ Массив. Содержит заказ-наряды по которым сдвигается граница последовательности
Перем МоментВремени Экспорт;   // Дата-время документа
Перем ДокументСсылка Экспорт;  // Ссылка на документ
Перем ЗаказНарядБыл Экспорт; 	// Документ.ЗаказНаряд ИЛИ Массив. Содержит заказ-наряды до изменения
Перем МоментВремениБыл Экспорт;   // Дата-время документа до изменения

// проверяет, надо ли двигать границу последовательности и если надо - сдвигает
Функция ИзменитьГраницу(БезПроверок=Ложь) Экспорт
	// если склад компании один, то преобразуем его в массив
	МассивЗаказНарядовНовые = Новый Массив();
	МассивЗаказНарядовСтарые = Новый Массив();
	// фильтр на Заказ-наряды
	МассивЗаказНарядовФильтр = Новый Массив();
	
	Если ТипЗнч(ЗаказНаряд)<>Тип("Массив") Тогда
		МассивЗаказНарядовНовые.Добавить(ЗаказНаряд);
		МассивЗаказНарядовФильтр.Добавить(ЗаказНаряд);
	Иначе
		Для Каждого ЗН Из ЗаказНаряд Цикл
			МассивЗаказНарядовНовые.Добавить(ЗН);
			МассивЗаказНарядовФильтр.Добавить(ЗН);
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаказНарядБыл) И ТипЗнч(ЗаказНарядБыл)<>Тип("Массив") И МассивЗаказНарядовНовые.Найти(ЗаказНарядБыл)=Неопределено Тогда
		МассивЗаказНарядовСтарые.Добавить(ЗаказНарядБыл);
		МассивЗаказНарядовФильтр.Добавить(ЗаказНарядБыл);
	ИначеЕсли ЗначениеЗаполнено(ЗаказНарядБыл) Тогда
		Для Каждого ЗН Из ЗаказНарядБыл Цикл
			Если МассивЗаказНарядовНовые.Найти(ЗН)=Неопределено Тогда
				МассивЗаказНарядовСтарые.Добавить(ЗН);
				МассивЗаказНарядовФильтр.Добавить(ЗН);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// получаем текущую границу
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ЗаказНаряд КАК ЗаказНаряд,
	|	МоментВремени КАК МоментВремени,
	|	ДокументСсылка КАК ДокументСсылка
	|ИЗ
	|	РегистрСведений.ГраницыПроизводство
	|ГДЕ
	|	ЗаказНаряд В(&ЗаказНаряды)
	|");
	Запрос.УстановитьПараметр("ЗаказНаряды",МассивЗаказНарядовФильтр);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	МоментВремениДокумента = ?(ТипЗнч(МоментВремени)=Тип("МоментВремени") И ДокументСсылка<>Неопределено, Новый МоментВремени(Мин(ДокументСсылка.Дата, МоментВремени.Дата), ДокументСсылка), Новый МоментВремени(МоментВремени, ДокументСсылка));
	Если ЗначениеЗаполнено(МоментВремениБыл) Тогда
		МоментВремениДокумента = ?(МоментВремениДокумента.Сравнить(МоментВремениБыл)=1,МоментВремениБыл,МоментВремениДокумента);
	КонецЕсли;
	
	// идем по новым заказ-нарядам и смотрим надо ли сдвинуть границу
	Для Каждого ЗН Из МассивЗаказНарядовНовые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(ЗН,"ЗаказНаряд");
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениДокумента) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыПроизводство.СоздатьМенеджерЗаписи();
			НоваяЗапись.ЗаказНаряд = ЗН;
			НоваяЗапись.МоментВремени = МоментВремениДокумента.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениДокумента.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЗН Из МассивЗаказНарядовСтарые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(ЗН,"ЗаказНаряд");
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениБыл) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыПроизводство.СоздатьМенеджерЗаписи();
			НоваяЗапись.ЗаказНаряд = ЗН;
			НоваяЗапись.МоментВремени = МоментВремениБыл.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениБыл.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// убираем циклические ссылки
	ДокументСсылка = Неопределено;
	
	Возврат Истина;
КонецФункции

// вызывается при непосредственном удалении проведенного документа
// проверяет есть ли граница с таким документом, если есть, то очищает ссылку
Процедура УбратьСсылку(Ссылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ЗаказНаряд,
	|	МоментВремени
	|ИЗ
	|	РегистрСведений.ГраницыПроизводство 
	|ГДЕ
	|	ДокументСсылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = РегистрыСведений.ГраницыПроизводство.СоздатьМенеджерЗаписи();
			НоваяЗапись.ЗаказНаряд = Выборка.ЗаказНаряд;
			НоваяЗапись.МоментВремени = Выборка.МоментВремени;
			НоваяЗапись.ДокументСсылка = Неопределено;
			НоваяЗапись.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Перем ДокументОбъект Экспорт; // документ, выполняющий изменение состояния
Перем Период Экспорт; // период фиксирования даты изменения статуса
Перем Организация Экспорт; // СправочникСсылка.Организации
Перем Состояние Экспорт; // Перечисления.СостоянияКодовМаркировки
Перем РезультатЗапросаПоКодамМаркировки Экспорт; // Список кодов маркировки к изменению статуса
Перем ПроверятьВыводИзОборота Экспорт;
Перем ПроверятьВводВОборот Экспорт;
Перем РежимПроведения Экспорт; // Режим проведения документа оперативный/неоперативный
Перем УдалятьДвижения Экспорт; // Признак удаления ранее записанных движений
Перем ОчищатьЗаписи Экспорт; // Признак очистки записей до

Функция ТаблицаКодовМаркировки() Экспорт
	
	ВидДок = ДокументОбъект.Метаданные().Имя;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДокументТовары.ИдентификаторТовара КАК ИдентификаторТовара,
	               |	ДокументТовары.Номенклатура КАК Номенклатура,
	               |	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ПОМЕСТИТЬ ТаблицаТоваров
	               |ИЗ
	               |	Документ.#ВидДокумента.Товары КАК ДокументТовары
	               |ГДЕ
	               |	ДокументТовары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДокументКодыМаркировки.ИдентификаторТовара КАК ИдентификаторТовара,
	               |	ДокументКодыМаркировки.КодМаркировки КАК КодМаркировки
	               |ПОМЕСТИТЬ ТаблицаКодовМаркировки
	               |ИЗ
	               |	Документ.#ВидДокумента.КодыМаркировки КАК ДокументКодыМаркировки
	               |ГДЕ
	               |	ДокументКодыМаркировки.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаКодовМаркировки.КодМаркировки КАК КодМаркировки
	               |ИЗ
	               |	ТаблицаТоваров КАК ТаблицаТоваров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКодовМаркировки КАК ТаблицаКодовМаркировки
	               |		ПО ТаблицаТоваров.ИдентификаторТовара = ТаблицаКодовМаркировки.ИдентификаторТовара";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВидДокумента", ВидДок);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СостояниеКодовМаркировки() Экспорт
	
	ВсеОК = Истина;
	
	Если УдалятьДвижения Тогда
		ОтменаПроведения();
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапросаПоКодамМаркировки) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоКодамМаркировки = РезультатЗапросаПоКодамМаркировки.Выгрузить();
	ИначеЕсли НЕ ТипЗнч(РезультатЗапросаПоКодамМаркировки) = Тип("ТаблицаЗначений") Тогда
		РезультатЗапросаПоКодамМаркировки = ТаблицаКодовМаркировки();
	КонецЕсли;
	
	Если ПроверятьВыводИзОборота
		И РегистрыСведений.СостоянияКодовМаркировки.КодМаркировкиЗапрещенДляВывода(РезультатЗапросаПоКодамМаркировки, ДокументОбъект, РежимПроведения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПроверятьВводВОборот 
		И РегистрыСведений.СостоянияКодовМаркировки.КодМаркировкиВведенВОборот(РезультатЗапросаПоКодамМаркировки, ДокументОбъект, РежимПроведения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// отберем набор записей по документу
	Отбор.ДокументОснование.Значение = ДокументОбъект.Ссылка;
	Отбор.ДокументОснование.Использование = Истина;
	ТаблицаЗаписей = ЭтотОбъект.Выгрузить();
	Если ОчищатьЗаписи Тогда
		ТаблицаЗаписей.Очистить();
	КонецЕсли;
	
	Если Период = Неопределено Тогда
		Период = ДокументОбъект.Дата;
	КонецЕсли;
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	Для Каждого ТекущаяСтрока Из РезультатЗапросаПоКодамМаркировки Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			дкСообщитьРезультат("Код маркировки <"+СокрЛП(ТекущаяСтрока.КодМаркировки)+"> не разобран. Проверьте его корректность.","Важное&Состояние кодов маркировки:",ДокументОбъект);
			ВсеОК = Ложь;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаЗаписей.Добавить();
		НоваяСтрока.Период = Период;
		НоваяСтрока.ДокументОснование = ДокументОбъект.Ссылка;
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.Номенклатура = ТекущаяСтрока.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатуры;
		НоваяСтрока.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		НоваяСтрока.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		НоваяСтрока.Состояние = Состояние;
		НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировки;
	КонецЦикла;
	
	Если НЕ ВсеОК Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтотОбъект.Загрузить(ТаблицаЗаписей);
	
	Попытка
		Записать();
	Исключение
		дкСообщитьРезультат("Ошибка записи состояния кодов маркировки","Важное&Состояние кодов маркировки:",ЭтотОбъект);
		ДокументОбъект = Неопределено;
		Возврат Ложь;
	КонецПопытки; 
	
	ДокументОбъект = Неопределено;
	Возврат Истина;
	
КонецФункции // СостояниеКодовМаркировки()

// отмена проведения документа
Функция ОтменаПроведения() Экспорт
	Отбор.ДокументОснование.Значение=ДокументОбъект.Ссылка;
	Отбор.ДокументОснование.Использование = Истина;
	ТаблицаЗаписей = ЭтотОбъект.Выгрузить();
	ТаблицаЗаписей.Очистить();
	
	Попытка
		Записать();
	Исключение
		дкСообщитьРезультат("Ошибка записи состояния кодов маркировки", "Не удалось записать состояние кодов маркировки.",ЭтотОбъект);
		Возврат Ложь;
	КонецПопытки; 
	
	Возврат Истина;
КонецФункции

Процедура ПередЗаписью(Отказ, Замещение)
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	Для Каждого Строка Из ЭтотОбъект Цикл
		ОбъектШК.РазобратьСтрокуШтрихкодаGS1(Строка.КодМаркировки, Истина);
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////
//Инициализация переменных модуля набора записей

ДокументОбъект = Неопределено;
Период = Неопределено;
РезультатЗапросаПоКодамМаркировки = Неопределено;
ПроверятьВыводИзОборота = Ложь;
ПроверятьВводВОборот = Ложь;
УдалятьДвижения = Истина;
ОчищатьЗаписи = Истина;


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Заполняем реквизиты из Параметров
	Если Параметры.Свойство("ВидЗаявки") Тогда
		ВидЗаявки  	= Параметры.ВидЗаявки;
	КонецЕсли;
	Если Параметры.Свойство("Роль") Тогда
		Роль      	= Параметры.Роль;
	КонецЕсли;
	Если Параметры.Свойство("Согласующий") Тогда
		Согласующий	= Параметры.Согласующий;
	КонецЕсли;
	Если Параметры.Свойство("Приоритет") Тогда
		Приоритет  	= Параметры.Приоритет;
	КонецЕсли;
	
	//Определяем Какую схему нам надо 
	СКД = ПолучитьНужнуюСхему(ВидЗаявки, Роль);
	Если СКД = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	АдресВременногоХранилищаСхемы = ПоместитьВоВременноеХранилище(СКД,?(АдресВременногоХранилищаСхемы <> "", АдресВременногоХранилищаСхемы, Новый УникальныйИдентификатор()));
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВременногоХранилищаСхемы));
	
	Если Параметры.Свойство("УсловиеОтбораКомпановка") Тогда
		Если ТипЗнч(Параметры.УсловиеОтбораКомпановка) = Тип("ХранилищеЗначения") Тогда
			УсловиеОтбораКомпановка = Параметры.УсловиеОтбораКомпановка.Получить();
		Иначе 
			АдресВременногоХранилищаЗначений = Параметры.УсловиеОтбораКомпановка;
			УсловиеОтбораКомпановкаХранЗнч  = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаЗначений);
			УсловиеОтбораКомпановка = УсловиеОтбораКомпановкаХранЗнч.Получить();	
			ВозвращатьВоВременноеХранилище = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловиеОтбораКомпановка) Тогда
		НастройкиПоУмолчанию = СКД.НастройкиПоУмолчанию;
		
		СкопироватьСтрокуОтбора(УсловиеОтбораКомпановка.Отбор.Элементы, НастройкиПоУмолчанию);
		
		КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиПоУмолчанию);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	КонецЕсли;
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.Полное);		
	
	ТипСтр = Неопределено;
	
	Если ВидЗаявки = Перечисления.БТ_ВидыЗаявок.ЗаявкаНаСогласованиеЗаявокДДС Тогда
		ТипСтр = "БизнесПроцессСсылка.БТ_СогласованиеЗаявокДДС";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипСтр) Тогда
		ОписТип = Новый ОписаниеТипов(ТипСтр);
		ПроверочнаяЗаявка = ОписТип.ПривестиЗначение();
		Элементы.ПроверочнаяЗаявка.ВыбиратьТип = Ложь;
	КонецЕсли;	
	
КонецПроцедуры    

Процедура СкопироватьСтрокуОтбора(Откуда, Куда)
	
	Если ТипЗнч(Откуда) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
		Если ТипЗнч(Куда) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда 
			ЭлементОтбора = Куда.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));		
		Иначе 
			ЭлементОтбора = Куда.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));		
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭлементОтбора, Откуда, "ВидСравнения, Использование, ЛевоеЗначение, ПравоеЗначение");
	ИначеЕсли ТипЗнч(Откуда) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда 
		Если ТипЗнч(Куда) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда 
			ЭлементОтбора = Куда.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));		
		Иначе 
			ЭлементОтбора = Куда.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));		
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭлементОтбора, Откуда, "Использование, ТипГруппы");
		Для Каждого СтрокаОтбора Из Откуда.Элементы Цикл
			СкопироватьСтрокуОтбора(СтрокаОтбора, ЭлементОтбора);
		КонецЦикла;
	Иначе 	
		Для Каждого СтрокаОтбора Из Откуда Цикл
			СкопироватьСтрокуОтбора(СтрокаОтбора, Куда);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьНужнуюСхему(ВидЗаявки, Роль)
	
	Если ВидЗаявки = Перечисления.БТ_ВидыЗаявок.ЗаявкаНаСогласованиеЗаявокДДС Тогда
		Возврат РегистрыСведений.БТ_АдресатыЗаявок.ПолучитьМакет("ЗаявкаНаСогласованиеЗаявокДДС");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьПравило(Команда)
	
	ПроверитьПравилоС();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПравилоС()
	
	СКД = ПолучитьНужнуюСхему(ВидЗаявки, Роль);
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ЗаявкаПараметр = Настройки.ПараметрыДанных.Элементы.Найти("Заявка");
	
	Если ЗаявкаПараметр = Неопределено Тогда
		
		СКД.Параметры.Ссылка.Значение = ПроверочнаяЗаявка;
		ЗаявкаПараметр = Настройки.ПараметрыДанных.Элементы.Найти("Ссылка");
		ЗаявкаПараметр.Значение			= ПроверочнаяЗаявка;
		ЗаявкаПараметр.Использование	= Истина;     

	Иначе

		СКД.Параметры.Заявка.Значение 	= ПроверочнаяЗаявка;
		ЗаявкаПараметр.Значение			= ПроверочнаяЗаявка;
		ЗаявкаПараметр.Использование	= Истина;     

	КонецЕсли;
	
	КомпоновщикМакета	= Новый КомпоновщикМакетаКомпоновкиДанных;
	ТабДокОписаниеЗаявкиДанныеРасшифровки	= Новый ДанныеРасшифровкиКомпоновкиДанных;
	МакетКомпоновки		= КомпоновщикМакета.Выполнить(СКД, Настройки, ТабДокОписаниеЗаявкиДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ТабДокОписаниеЗаявкиДанныеРасшифровки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ОписаниеЗаявки.Очистить();
	ПроцессорВывода.УстановитьДокумент(ОписаниеЗаявки);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	ОписаниеЗаявки.ОтображатьСетку		= Ложь;
	ОписаниеЗаявки.ОтображатьЗаголовки	= Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверочнаяЗаявкаПриИзменении(Элемент)
	
	Элементы.ФормаПроверитьПравило.Доступность = ЗначениеЗаполнено(ПроверочнаяЗаявка);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьУсловие(Команда)
	
	#Если ВебКлиент Тогда
		
		Сообщить("форма открыта в ВебКлиенте! Если были произведены изменения, то они не сохранятся.");
		
	#Иначе
		
		ОповеститьОВыборе(ХранилищеСУсловием());
		
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция ХранилищеСУсловием()
	
	СтруктураПараметров = Новый Структура;    
	
	Если ВозвращатьВоВременноеХранилище Тогда  
		
		СтруктураПараметров.Вставить("Хранилище", ПоместитьВоВременноеХранилище(Новый ХранилищеЗначения(КомпоновщикНастроек.ПолучитьНастройки()),АдресВременногоХранилищаЗначений));
		СтруктураПараметров.Вставить("НастройкиОтбора", КомпоновщикНастроек.ПолучитьНастройки());
		
	Иначе
		
		СтруктураПараметров.Вставить("Хранилище", Новый ХранилищеЗначения(КомпоновщикНастроек.ПолучитьНастройки()));
		СтруктураПараметров.Вставить("НастройкиОтбора", КомпоновщикНастроек.ПолучитьНастройки());
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции






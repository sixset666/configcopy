
Функция СформироватьСтруктуруЖурналаЗагрузки() Экспорт
	
	СтруктураЖурнала = Новый Структура;
	
	ЛогЗагрузки = Новый Структура;
	ЛогЗагрузки.Вставить("Кратко", "");
	ЛогЗагрузки.Вставить("Старт", "Старт загрузки: " + Формат(ТекущаяДата(), "ДФ='yyyy.MM.dd ЧЧ:мм:сс'"));
	ЛогЗагрузки.Вставить("Финиш", "");
	
	ИнформацияОЗагрузке = Новый Структура;
	
	СтруктураЖурнала.Вставить("Период", ТекущаяДата());
	СтруктураЖурнала.Вставить("Статус", Перечисления.СтатусыЗагрузкиПрайсЛистов.ПустаяСсылка()); 
	СтруктураЖурнала.Вставить("СтрокаПодключения", "");
	СтруктураЖурнала.Вставить("ДатаФайла", Дата("00010101"));
	СтруктураЖурнала.Вставить("ХешФайла", "");
	СтруктураЖурнала.Вставить("Примечание", "");
	СтруктураЖурнала.Вставить("ЛогЗагрузки", ЛогЗагрузки);
	СтруктураЖурнала.Вставить("ИнформацияОЗагрузке", ИнформацияОЗагрузке);
	СтруктураЖурнала.Вставить("ИдентификаторФоновогоЗадания", "");
	СтруктураЖурнала.Вставить("Автор", "");
	
	Возврат СтруктураЖурнала;
КонецФункции //СформироватьСтруктуруЖурналаЗагрузки()

Процедура СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, ПрайсЛист)Экспорт 
	
	Если ЖурналЗагрузки.Статус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Завершена Тогда
		//НЕОБХОДИМО ДОБАВИТЬ ФОРМИРОВАНИЕ СТАТИСТИКИ ДЛЯ ЗАПИСИ В РЕГИСТР !!!!!!
	КонецЕсли;
	
	Менеджер = РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Менеджер, ЖурналЗагрузки);
	Менеджер.ПрайсЛист = ПрайсЛист;
	Менеджер.ЛогЗагрузки = Новый ХранилищеЗначения(ЖурналЗагрузки.ЛогЗагрузки);
	Менеджер.ИнформацияОЗагрузке = Новый ХранилищеЗначения(ЖурналЗагрузки.ИнформацияОЗагрузке);
	Попытка
		Менеджер.Автор = ПараметрыСеанса.Пользователь;
	Исключение
		Менеджер.Автор = Справочники.Пользователи.Робот; // Фоновое задание
	КонецПопытки;
	
	Менеджер.Записать();
	
КонецПроцедуры //СохранитьЖурналЗагрузкиПрайсЛиста()

Процедура УдалитьЗаписиСтаршеДаты(ПрайсЛист=Неопределено, Дата) Экспорт 
	 //Получим ключи всех записей старше переданной даты
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	ЖурналЗагрузки.Период,
	 |	ЖурналЗагрузки.ПрайсЛист
	 |ИЗ
	 |	РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки КАК ЖурналЗагрузки
	 |ГДЕ
	 |	ЖурналЗагрузки.Период < &Дата
	 |	"+?(обЗначениеНеЗаполнено(ПрайсЛист), "", "И ЖурналЗагрузки.ПрайсЛист = &ПрайсЛист")+"
	 |";
	 
	 Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛист);
	 Запрос.УстановитьПараметр("Дата", Дата);
	 
	 Результат = Запрос.Выполнить();
	 Если Результат.Пустой() Тогда
		 Возврат;
	 КонецЕсли;
	 
	 НачатьТранзакцию();
	 
	 Блокировка = Новый БлокировкаДанных;
	 ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки");
	 ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	 ЭлементБлокировки.ИсточникДанных = Результат;
	 ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Период", "Период");
	 ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ПрайсЛист", "ПрайсЛист");
	 Блокировка.Заблокировать();
	 
	 Выборка = Результат.Выбрать();
	 КоличествоЗаписей = Выборка.Количество();
	 СЧ = 0;
	 Пока Выборка.Следующий() Цикл
		 СЧ = СЧ + 1;
		 #Если Клиент Тогда
			 Состояние(НСтр("ru='Очистка журнала загрузки: '") + Строка(Цел(СЧ*100/КоличествоЗаписей))+"%");
		 #КонецЕсли
		 
		 НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СоздатьНаборЗаписей();
		 НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		 НаборЗаписей.Отбор.ПрайсЛист.Установить(Выборка.ПрайсЛист);
		 
		 НаборЗаписей.Прочитать();
		 НаборЗаписей.Очистить();
		 Попытка
			 НаборЗаписей.Записать();
		 Исключение
			 ОтменитьТранзакцию();
			 #Если Клиент Тогда
				 ТекстСообщения = НСтр("ru='Не удалось очистить журнал загрузки прайс-листов по пречине: '") + ИнформацияОбОшибке().Описание;
				 Сообщить(ТекстСообщения,СтатусСообщения.Важное);
			 #КонецЕсли
			 Прервать;
		 КонецПопытки;
	 КонецЦикла;
	 Если ТранзакцияАктивна()Тогда
		 ЗафиксироватьТранзакцию();
		 #Если Клиент Тогда
			 ТекстСообщения = НСтр("ru='Записи журнала загрузки прайс-листов старше '") + Формат(Дата,"ДЛФ=DD") + НСтр("ru=' успешно удалены.'");
			 Сообщить(ТекстСообщения);
		 #КонецЕсли
	 КонецЕсли;	 
	 
КонецПроцедуры //УдалитьЗаписиСтаршеДаты()
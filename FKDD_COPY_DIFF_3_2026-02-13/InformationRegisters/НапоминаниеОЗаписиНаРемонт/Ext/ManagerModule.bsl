
// Процедура - Создать изменить записи из заявки на ремонт
//
// Параметры:
//  Основание	 - ДокументСсылка.ЗаявкаНаРемонт - Заявка на ремонт
//
Процедура СоздатьИзменитьИзЗаявкиНаРемонт(Основание) Экспорт 
	
	// Вычислим время отправки
	ВремяОтправки = Основание.ДатаНачала - Основание.ВидРемонта.ОтправлятьЗаВремя*60*60;
	
	// Если время отправки прошло, то все равно отправим смс
	Если ВремяОтправки < ТекущаяДата() Тогда
		ВремяОтправки = ТекущаяДата(); 		
	КонецЕсли;
	
	// Создадим набор
	Запись = СоздатьНаборЗаписей();
	// Удалим старые записи
	Запись.Отбор.ЗаявкаНаРемонт.Установить(Основание);	
	Запись.Очистить();
	// Добавим новую
	НоваяЗапись = Запись.Добавить();
	НоваяЗапись.ЗаявкаНаРемонт = Основание;
	НоваяЗапись.ДатаЗапланированнойОтправки = ВремяОтправки;	
	НоваяЗапись.ДатаНачалаРемонта = Основание.ДатаНачала;
	// Запишем
	Запись.Записать();
	
КонецПроцедуры

// Процедура - Удалить из заявки на ремонт
//
// Параметры:
//  Основание	 - ДокументСсылка.ЗаявкаНаРемонт - Заявка на ремонт
//
Процедура УдалитьИзЗаявкиНаРемонт(Основание) Экспорт 
	
	Запись = СоздатьНаборЗаписей();
	Запись.Отбор.ЗаявкаНаРемонт.Установить(Основание);	
	Запись.Записать();
	
КонецПроцедуры

// Процедура - Изменить после отправки из рег задания
//
// Параметры:
//  Основание	 - ДокументСсылка.ЗаявкаНаРемонт - Заявка на ремонт
//  ДокументСМС	 - ДокументСсылка.SMS			 - Документ, созданый в регламентном задании.
//
Процедура ИзменитьПослеОтправкиИзРегЗадания(Основание, ДокументСМС) Экспорт 
	
	Запись = СоздатьНаборЗаписей();
	Запись.Отбор.ЗаявкаНаРемонт.Установить(Основание);	
	Запись.Прочитать();
	Для каждого ТекЗапись Из Запись Цикл
		ТекЗапись.ДокументSMS = ДокументСМС;
		ТекЗапись.ДатаРеальнойОтправки = ДокументСМС.НачалоОтправки;
	КонецЦикла;
	Запись.Записать();

КонецПроцедуры
	

	


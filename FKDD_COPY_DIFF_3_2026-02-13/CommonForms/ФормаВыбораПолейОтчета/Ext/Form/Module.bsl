
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Событие") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Вставить содержимое обработчика.
	Если Параметры.Свойство("СхемаКомпоновки") Тогда
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Параметры.СхемаКомпоновки));
		СхемаКомпоновки = Параметры.СхемаКомпоновки;
	ИначеЕсли Параметры.Свойство("КомпоновщикНастроекИзНоменклатуры") Тогда
		КомпоновщикНастроек = Параметры.КомпоновщикНастроекИзНоменклатуры;
	КонецЕсли;
	
	Если Параметры.Свойство("НастройкиОтчета") Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Параметры.НастройкиОтчета);
	КонецЕсли;
	
	Событие = Параметры.Событие;
	Если Событие = "Реквизиты" ИЛИ Событие = "Ресурсы" ИЛИ Событие = "Показатель" Тогда
		Элементы.ГруппаПоляВыбора.ТекущаяСтраница = Элементы.ГруппаВыбор;
	ИначеЕсли Событие = "Отбор" Тогда
		Элементы.ГруппаПоляВыбора.ТекущаяСтраница = Элементы.ГруппаОтбор;
		ВидыСравненияОтбора.Очистить();
		Для Каждого ТекущийВидСравнения Из ВидСравненияКомпоновкиДанных Цикл
			ВидыСравненияОтбора.Добавить(ТекущийВидСравнения, ПолучитьПолноеИмяПредопределенногоЗначения(ТекущийВидСравнения));
		КонецЦикла;
	ИначеЕсли Событие = "Порядок" Тогда
		Элементы.ГруппаПоляВыбора.ТекущаяСтраница = Элементы.ГруппаПорядок;
	ИначеЕсли Событие = "ПоляГруппировки" ИЛИ Событие = "Точки" ИЛИ Событие = "Серии" Тогда
		Элементы.ГруппаПоляВыбора.ТекущаяСтраница = Элементы.ГруппаПоляГруппировки;
		Если (НЕ Параметры.Свойство("ЕстьРеквизиты")) ИЛИ (НЕ Параметры.ЕстьРеквизиты) Тогда
			Элементы.ПанельРеквизитов.Видимость = Ложь;
			Элементы.ДополнительныеПоля.Видимость = Ложь;
		КонецЕсли;
		
		Если Параметры.Свойство("АдресТаблицы") Тогда
			ЗаполнитьКоллекциюИзХранилища(Параметры.АдресТаблицы);
		КонецЕсли;
	КонецЕсли;
	
	Если Событие = "Точки" ИЛИ Событие = "Серии" Тогда
		Элементы.ПанельРеквизитов.Видимость   = Ложь;
		Элементы.ДополнительныеПоля.Видимость = Ложь;
		Элементы.ТипГруппировки.Видимость     = Ложь;
	КонецЕсли;
	
	// Получим настройки для текущей строки структуры настроек
	СтрокаНастроек = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока = КомпоновщикНастроек.Настройки.ПолучитьИдентификаторПоОбъекту(СтрокаНастроек);
	
	Параметры.Свойство("ТекущееПоле", ТекущееПоле);
	
	Если Параметры.Свойство("ГруппировкиСтруктуры") Тогда
		ГруппировкиСтруктуры.ЗагрузитьЗначения(Параметры.ГруппировкиСтруктуры);
	КонецЕсли;
	
	Если Параметры.Свойство("ПоляВыбора") Тогда
		Для Каждого ТекущаяСтрока Из Параметры.ПоляВыбора Цикл
			НоваяСтрока = СтрокаНастроек.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			НоваяСтрока.Использование = ТекущаяСтрока.Использование;
			НоваяСтрока.Поле          = ТекущаяСтрока.Поле;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("ПоляГруппировки") Тогда
		Для Каждого ТекущаяСтрока Из Параметры.ПоляГруппировки Цикл
			НоваяСтрока = СтрокаНастроек.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			НоваяСтрока.Использование  = ТекущаяСтрока.Использование;
			НоваяСтрока.Поле           = ТекущаяСтрока.Поле;
			НоваяСтрока.ТипГруппировки = ТекущаяСтрока.ТипГруппировки;
			НоваяСтрока.ТипДополнения  = ТекущаяСтрока.ТипДополнения;
			НоваяСтрока.НачалоПериода  = ТекущаяСтрока.НачалоПериода;
			НоваяСтрока.КонецПериода   = ТекущаяСтрока.КонецПериода;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("ПоляПорядка") Тогда
		Для Каждого ТекущаяСтрока Из Параметры.ПоляПорядка Цикл
			НоваяСтрока = СтрокаНастроек.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
			НоваяСтрока.Использование     = ТекущаяСтрока.Использование;
			НоваяСтрока.Поле              = ТекущаяСтрока.Поле;
			НоваяСтрока.ТипУпорядочивания = ТекущаяСтрока.ТипУпорядочивания;
		КонецЦикла;
	КонецЕсли;
	
	Параметры.Свойство("ВидСравнения",   ВидСравненияОтбора);
	Параметры.Свойство("ТипГруппировки", ТипГруппировки);
	
	Если ТипГруппировки = "" Тогда
		ТипГруппировки = "ТипГруппировкиКомпоновкиДанных.Элементы";
	КонецЕсли;
	
	Если ВидСравненияОтбора = "" Тогда
		ВидСравненияОтбора = "ВидСравненияКомпоновкиДанных.Равно";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОграничения(ДоступныеПоляВыбора, ОграниченияИспользованияПолей, УстановленоОграничение = Ложь)
	
	Для Каждого ТекПоле Из ДоступныеПоляВыбора Цикл
		
		Если ТекПоле.Папка Тогда
			ПустаяГруппа = Ложь;
			УстановитьОграничения(ТекПоле.Элементы, ОграниченияИспользованияПолей, ПустаяГруппа);
			Если НЕ ПустаяГруппа Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		
		УстановленоОграничение = Истина;
		
		НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
		НовоеОграничение.Поле        = ТекПоле.Поле;
		НовоеОграничение.Доступность = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТипЗнч(ТекущееПоле) = Тип("ПолеКомпоновкиДанных") Тогда
		Если Событие = "Реквизиты" ИЛИ Событие = "Ресурсы" ИЛИ Событие = "Показатель" Тогда
			ДоступноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(ТекущееПоле);
			Если НЕ ДоступноеПоле = Неопределено Тогда
				ИдентификаторСтроки = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
				Элементы.ДоступныеПоляВыбора.ТекущаяСтрока = ИдентификаторСтроки;
			КонецЕсли;
		ИначеЕсли Событие = "Отбор" Тогда
			ДоступноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(ТекущееПоле);
			Если НЕ ДоступноеПоле = Неопределено Тогда
				ИдентификаторСтроки = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
				Элементы.ДоступныеПоляОтбора.ТекущаяСтрока = ИдентификаторСтроки;
			КонецЕсли;
		ИначеЕсли Событие = "Порядок" Тогда
			ДоступноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.НайтиПоле(ТекущееПоле);
			Если НЕ ДоступноеПоле = Неопределено Тогда
				ИдентификаторСтроки = КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
				Элементы.ДоступныеПоляПорядка.ТекущаяСтрока = ИдентификаторСтроки;
			КонецЕсли;
		ИначеЕсли Событие = "ПоляГруппировки" ИЛИ Событие = "Точки" ИЛИ Событие = "Серии" Тогда
			ДоступноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.НайтиПоле(ТекущееПоле);
			Если НЕ ДоступноеПоле = Неопределено Тогда
				ИдентификаторСтроки = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
				Элементы.ДоступныеПоляГруппировок.ТекущаяСтрока = ИдентификаторСтроки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Событие = "Реквизиты" Тогда
		ДоступныеПоляВыбора = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора;
		ОграниченияИспользованияПолей = Элементы.ДоступныеПоляВыбора.ОграниченияИспользования;
		
		Для Каждого ЭлементСписка Из ГруппировкиСтруктуры Цикл
			
			ПолеГруппировки = ЭлементСписка.Значение;
			ПоляВетки       = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(ПолеГруппировки);
			
			РодительГруппировки = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.НайтиПоле(ПолеГруппировки);
			Пока НЕ РодительГруппировки = Неопределено Цикл
				Если РодительГруппировки.Родитель = Неопределено Тогда
					Для Каждого ТекПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
						
						Если НЕ ГруппировкиСтруктуры.НайтиПоЗначению(ТекПоле.Поле) = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
						НовоеОграничение.Поле        = ТекПоле.Поле;
						НовоеОграничение.Доступность = Ложь;
					КонецЦикла;
				Иначе
					Для Каждого ТекПоле Из РодительГруппировки.Родитель.Элементы Цикл
						
						Если НЕ ГруппировкиСтруктуры.НайтиПоЗначению(ТекПоле.Поле) = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
						НовоеОграничение.Поле        = ТекПоле.Поле;
						НовоеОграничение.Доступность = Ложь;
					КонецЦикла;
				КонецЕсли;
				РодительГруппировки = РодительГруппировки.Родитель;
			КонецЦикла;
			
		КонецЦикла;
		
	ИначеЕсли Событие = "Ресурсы" ИЛИ Событие = "Показатель" Тогда
		
		ДоступныеПоляВыбора = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора;
		ОграниченияИспользованияПолей = Элементы.ДоступныеПоляВыбора.ОграниченияИспользования;
		
		УстановитьОграничения(ДоступныеПоляВыбора.Элементы, ОграниченияИспользованияПолей);
		
	//ИначеЕсли Событие = "Отбор" Тогда
	//	ДоступныеПоляВыбора = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора;
	//	ОграниченияИспользованияПолей = Элементы.ДоступныеПоляОтбора.ОграниченияИспользования;
	ИначеЕсли Событие = "Порядок" Тогда
		
		ДоступныеПоляПорядка          = КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка;
		ОграниченияИспользованияПолей = Элементы.ДоступныеПоляПорядка.ОграниченияИспользования;
		
		Для Каждого ЭлементСписка Из ГруппировкиСтруктуры Цикл
			
			ПолеГруппировки = ЭлементСписка.Значение;
			
			ПоляВетки = КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.НайтиПоле(ПолеГруппировки);
			
			РодительГруппировки = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.НайтиПоле(ПолеГруппировки);
			Пока НЕ РодительГруппировки = Неопределено Цикл
				Если РодительГруппировки.Родитель = Неопределено Тогда
					Для Каждого ТекПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.Элементы Цикл
						
						Если НЕ ГруппировкиСтруктуры.НайтиПоЗначению(ТекПоле.Поле) = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
						НовоеОграничение.Поле        = ТекПоле.Поле;
						НовоеОграничение.Доступность = Ложь;
					КонецЦикла;
				Иначе
					Для Каждого ТекПоле Из РодительГруппировки.Родитель.Элементы Цикл
						
						Если НЕ ГруппировкиСтруктуры.НайтиПоЗначению(ТекПоле.Поле) = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						НовоеОграничение = ОграниченияИспользованияПолей.Добавить();
						НовоеОграничение.Поле        = ТекПоле.Поле;
						НовоеОграничение.Доступность = Ложь;
					КонецЦикла;
				КонецЕсли;
				РодительГруппировки = РодительГруппировки.Родитель;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

//&НаКлиенте
//Функция ПолучитьПредставлениеЭлементаОтбора(ПредставлениеПоля)
//	
//	ТекущийВидСравнения = ПредопределенноеЗначение(ВидСравненияОтбора);
//	
//	// Формируем массив описания частей отбора
//	ЭлементыПредставленияОтбора = Новый Массив;
//	
//	ЭлементыПредставленияОтбора.Добавить(Строка(ПредставлениеПоля));
//	
//	ПредставлениеСравнения = "";
//	// Производим формирование секции выдов сравнения
//	Если ТекущийВидСравнения=Неопределено ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.Равно ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.Содержит ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.ВИерархии ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.ВСписке ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
//		// Формирование представлений для прямых видов условий производить не будем
//		
//	ИначеЕсли ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.Больше Тогда
//		ПредставлениеСравнения = " >";
//	ИначеЕсли ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
//		ПредставлениеСравнения = " >=";
//	ИначеЕсли ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.Меньше Тогда
//		ПредставлениеСравнения = " <";
//	ИначеЕсли ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
//		ПредставлениеСравнения = " <=";
//	ИначеЕсли ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.НеРавно ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.НеВИерархии ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.НеВСписке ИЛИ ТекущийВидСравнения=ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
//		ПредставлениеСравнения = " НЕ";
//	Иначе
//		ПредставлениеСравнения = " "+Строка(ТекущийВидСравнения);
//	КонецЕсли;
//	
//	Если ОтчетыКлиентСервер.ЭтоВебКлиент() Тогда
//		ЭлементыПредставленияОтбора.Добавить(ПредставлениеСравнения);
//	Иначе
//		ЭлементыПредставленияОтбора.Добавить(" ");
//		ЭлементыПредставленияОтбора.Добавить(Новый ФорматированнаяСтрока(ПредставлениеСравнения, Новый Шрифт(Элементы.ДоступныеПоляОтбора.Шрифт,,,Истина)));
//	КонецЕсли;
//	
//	// Возвращаем сформированное представление текущего элемента отбора
//	Возврат Новый ФорматированнаяСтрока(ЭлементыПредставленияОтбора);
//	
//КонецФункции // ПолучитьПредставлениеЭлементаОтбора()

&НаКлиенте
Функция ПолучитьЗаголовокПоля(ДоступноеПоле)
	
	ЗаголовокПоля = ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПоле);
	
	//Если Событие = "Отбор" Тогда
	//	ЗаголовокПоля = ПолучитьПредставлениеЭлементаОтбора(ЗаголовокПоля);
	//Иначе
	Если Событие = "ПоляГруппировки" И (НЕ ТипГруппировки = "ТипГруппировкиКомпоновкиДанных.Элементы") Тогда
		ЗаголовокПоля = ЗаголовокПоля + " " + Элементы.ТипГруппировки.СписокВыбора.НайтиПоЗначению(ТипГруппировки).Представление;
	КонецЕсли;
	
	Возврат ЗаголовокПоля;
	
КонецФункции

&НаКлиенте
Процедура Выбрать(Команда)
	
	Результат = Неопределено;
	Если Событие = "Реквизиты" ИЛИ Событие = "Показатель" ИЛИ Событие = "Ресурсы" Тогда
		ИдентификаторОбъекта = Элементы.ДоступныеПоляВыбора.ТекущаяСтрока;
		
		Если ИдентификаторОбъекта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(ИдентификаторОбъекта);
		
		Если ВыбранноеПоле = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		Результат = Новый Структура("Поле, Заголовок, Событие, Папка", ВыбранноеПоле.Поле, ЗаголовокПоля, Событие, Ложь);
		
		Если ВыбранноеПоле.Папка Тогда
			Результат.Вставить("Папка", Истина);
			
			АдресТаблицы = ПоместитьДоступныеПоляВыбораВХранилище(ИдентификаторОбъекта);
			Результат.Вставить("АдресТаблицы", АдресТаблицы);
		КонецЕсли;
		
	ИначеЕсли Событие = "Отбор" Тогда
		ИдентификаторОбъекта = Элементы.ДоступныеПоляОтбора.ТекущаяСтрока;
		
		Если ИдентификаторОбъекта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.ПолучитьОбъектПоИдентификатору(ИдентификаторОбъекта);
		
		Если ВыбранноеПоле = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ВыбранноеПоле.Папка Тогда
			Если Элементы.ДоступныеПоляВыбора.Развернут(ИдентификаторОбъекта) Тогда
				Элементы.ДоступныеПоляВыбора.Свернуть(ИдентификаторОбъекта);
			Иначе
				Элементы.ДоступныеПоляВыбора.Развернуть(ИдентификаторОбъекта);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		Если Элементы.ВидСравненияОтбора.СписокВыбора.НайтиПоЗначению(ВидСравненияОтбора) = Неопределено Тогда
			ВидСравненияОтбора = Элементы.ВидСравненияОтбора.СписокВыбора[0].Значение;
		КонецЕсли;
		
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		
		Результат = Новый Структура("Поле, Заголовок, ВидСравнения, Событие", ВыбранноеПоле.Поле, ЗаголовокПоля, ВидСравненияОтбора, Событие);
		
	ИначеЕсли Событие = "Порядок" Тогда
		ИдентификаторОбъекта = Элементы.ДоступныеПоляПорядка.ТекущаяСтрока;
		
		Если ИдентификаторОбъекта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.ПолучитьОбъектПоИдентификатору(ИдентификаторОбъекта);
		
		Если ВыбранноеПоле = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ВыбранноеПоле.Папка Тогда
			Если Элементы.ДоступныеПоляПорядка.Развернут(ИдентификаторОбъекта) Тогда
				Элементы.ДоступныеПоляПорядка.Свернуть(ИдентификаторОбъекта);
			Иначе
				Элементы.ДоступныеПоляПорядка.Развернуть(ИдентификаторОбъекта);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		Результат = Новый Структура("Поле, Заголовок, Событие", ВыбранноеПоле.Поле, ЗаголовокПоля, Событие);
		
	ИначеЕсли Событие = "ПоляГруппировки" ИЛИ Событие = "Точки" ИЛИ Событие = "Серии" Тогда
		ИдентификаторОбъекта = Элементы.ДоступныеПоляГруппировок.ТекущаяСтрока;
		
		Если ИдентификаторОбъекта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьОбъектПоИдентификатору(ИдентификаторОбъекта);
		
		Если ВыбранноеПоле = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ВыбранноеПоле.Папка Тогда
			Если Элементы.ДоступныеПоляГруппировок.Развернут(ИдентификаторОбъекта) Тогда
				Элементы.ДоступныеПоляГруппировок.Свернуть(ИдентификаторОбъекта);
			Иначе
				Элементы.ДоступныеПоляГруппировок.Развернуть(ИдентификаторОбъекта);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		Результат = Новый Структура("Поле, Заголовок, ТипГруппировки, Событие", ВыбранноеПоле.Поле, ЗаголовокПоля, ТипГруппировки, Событие);
		
		АдресТаблицы = ПоместитьДополнительныеПоляВХранилище();
		Результат.Вставить("АдресТаблицы", АдресТаблицы);
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПоляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Результат = Неопределено;
	
	СтандартнаяОбработка = Ложь;
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока);
	Если НЕ ВыбранноеПоле = Неопределено Тогда
		Если ВыбранноеПоле.Папка Тогда
			Если Элементы.ДоступныеПоляВыбора.Развернут(ВыбраннаяСтрока) Тогда
				Элементы.ДоступныеПоляВыбора.Свернуть(ВыбраннаяСтрока);
			Иначе
				Элементы.ДоступныеПоляВыбора.Развернуть(ВыбраннаяСтрока);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		
		Результат = Новый Структура("Поле, Заголовок, Событие, Папка", ВыбранноеПоле.Поле, ЗаголовокПоля, Событие, Ложь);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПоляПорядкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Результат = Неопределено;
	
	СтандартнаяОбработка = Ложь;
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока);
	Если НЕ ВыбранноеПоле = Неопределено Тогда
		Если ВыбранноеПоле.Папка Тогда
			Если Элементы.ДоступныеПоляПорядка.Развернут(ВыбраннаяСтрока) Тогда
				Элементы.ДоступныеПоляПорядка.Свернуть(ВыбраннаяСтрока);
			Иначе
				Элементы.ДоступныеПоляПорядка.Развернуть(ВыбраннаяСтрока);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		
		Результат = Новый Структура("Поле, Заголовок, Событие", ВыбранноеПоле.Поле, ЗаголовокПоля, Событие);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПоляГруппировокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Результат = Неопределено;
	
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока);
	Если ВыбранноеПоле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеПоле.Папка Тогда
		Если Элементы.ДоступныеПоляГруппировок.Развернут(ВыбраннаяСтрока) Тогда
			Элементы.ДоступныеПоляГруппировок.Свернуть(ВыбраннаяСтрока);
		Иначе
			Элементы.ДоступныеПоляГруппировок.Развернуть(ВыбраннаяСтрока);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
	
	Результат = Новый Структура("Поле, Заголовок, ТипГруппировки, Событие", ВыбранноеПоле.Поле, ЗаголовокПоля, ТипГруппировки, Событие);
	
	АдресТаблицы = ПоместитьДополнительныеПоляВХранилище();
	Результат.Вставить("АдресТаблицы", АдресТаблицы);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПоляОтбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Результат = Неопределено;
	
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока);
	Если НЕ ВыбранноеПоле = Неопределено Тогда
		Если ВыбранноеПоле.Папка Тогда
			Если Элементы.ДоступныеПоляОтбора.Развернут(ВыбраннаяСтрока) Тогда
				Элементы.ДоступныеПоляОтбора.Свернуть(ВыбраннаяСтрока);
			Иначе
				Элементы.ДоступныеПоляОтбора.Развернуть(ВыбраннаяСтрока);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		Если Элементы.ВидСравненияОтбора.СписокВыбора.НайтиПоЗначению(ВидСравненияОтбора) = Неопределено Тогда
			ВидСравненияОтбора = Элементы.ВидСравненияОтбора.СписокВыбора[0].Значение;
		КонецЕсли;
		
		ЗаголовокПоля = ПолучитьЗаголовокПоля(ВыбранноеПоле);
		
		Результат = Новый Структура("Поле, Заголовок, ВидСравнения, Событие", ВыбранноеПоле.Поле, ЗаголовокПоля, ВидСравненияОтбора, Событие);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#Область РедактированиеРеквизитов

&НаСервере
Процедура ЗаполнитьЭлементыГруппыИзХранилища(АдресТаблицы, ТекущаяСтрока)
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	КоллекцияФормы = ДополнительныеПоля.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ОбъектКоллекции = ПолучитьИзВременногоХранилища(АдресТаблицы);
	
	ОтчетыСервер.ЗаполнитьНастройкиОтчета(ЭтаФорма, КоллекцияФормы.ПолучитьЭлементы(), ОбъектКоллекции.Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ТекущаяСтрока = Элементы.ДоступныеПоляГруппировок.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	Если ВыбранноеПоле = Неопределено ИЛИ ВыбранноеПоле.Папка Тогда
		Возврат;
	КонецЕсли;
	
	МассивГруппировок = Новый Массив;
	МассивГруппировок.Добавить(ВыбранноеПоле.Поле);
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("СхемаКомпоновки",        СхемаКомпоновки);
	СтруктураПараметров.Вставить("Событие",               "Реквизиты");
	СтруктураПараметров.Вставить("ГруппировкиСтруктуры",   МассивГруппировок);
	СтруктураПараметров.Вставить("ИдентификаторВладельца", УникальныйИдентификатор);
	
	ОписаниеОповещенияВыбораПоля = Новый ОписаниеОповещения("ОповещенияНовоеПолеВыбора", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораПолейОтчета", СтруктураПараметров, Элемент,,,, ОписаниеОповещенияВыбораПоля, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещенияНовоеПолеВыбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДополнительныеПоля.ТекущиеДанные;
	
	ЭлементыПоказателей = ДополнительныеПоля.ПолучитьЭлементы();
	
	НовоеПоле = ЭлементыПоказателей.Добавить();
	НовоеПоле.Использование = Истина;
	НовоеПоле.Заголовок     = РезультатЗакрытия.Заголовок;
	НовоеПоле.Поле          = РезультатЗакрытия.Поле;
	НовоеПоле.Папка         = РезультатЗакрытия.Папка;
	
	ТекущаяСтрока = НовоеПоле.ПолучитьИдентификатор();
	
	Если РезультатЗакрытия.Папка Тогда
		ЗаполнитьЭлементыГруппыИзХранилища(РезультатЗакрытия.АдресТаблицы, ТекущаяСтрока);
	КонецЕсли;
	
	Элементы.ДополнительныеПоля.ТекущаяСтрока = ТекущаяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ДоступныеПоляГруппировок.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	Если ВыбранноеПоле = Неопределено ИЛИ ВыбранноеПоле.Папка Тогда
		Возврат;
	КонецЕсли;
	
	МассивГруппировок = Новый Массив;
	МассивГруппировок.Добавить(ВыбранноеПоле.Поле);
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("СхемаКомпоновки",      СхемаКомпоновки);
	Если НЕ Элементы.ДополнительныеПоля.ТекущиеДанные = Неопределено Тогда
		СтруктураПараметров.Вставить("ТекущееПоле",      Элементы.ДополнительныеПоля.ТекущиеДанные.Поле);
	КонецЕсли;
	СтруктураПараметров.Вставить("Событие",               "Реквизиты");
	СтруктураПараметров.Вставить("ГруппировкиСтруктуры",   МассивГруппировок);
	СтруктураПараметров.Вставить("ИдентификаторВладельца", УникальныйИдентификатор);
	
	ОписаниеОповещенияВыбораПоля = Новый ОписаниеОповещения("ОповещенияВыбораПоля", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораПолейОтчета", СтруктураПараметров, Элемент,,,, ОписаниеОповещенияВыбораПоля, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещенияВыбораПоля(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
		
	Если РезультатЗакрытия.Событие = "Реквизиты" Тогда
		
		ТекущиеДанные = Элементы.ДополнительныеПоля.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТекущиеДанные.Заголовок     = РезультатЗакрытия.Заголовок;
		ТекущиеДанные.Поле          = РезультатЗакрытия.Поле;
		ТекущиеДанные.Папка         = РезультатЗакрытия.Папка;
		ТекущиеДанные.Использование = Истина;
		
		ТекущаяСтрока = Элементы.ДополнительныеПоля.ТекущаяСтрока;
		
		Если РезультатЗакрытия.Папка Тогда
			ЗаполнитьЭлементыГруппыИзХранилища(РезультатЗакрытия.АдресТаблицы, ТекущаяСтрока);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
// Функция помещает коллекцию формы во временное хранилище и возвращает адрес 
//
Функция ПоместитьДополнительныеПоляВХранилище()
	
	Результат = "";
	
	ОбъектКоллекции = ДанныеФормыВЗначение(ДополнительныеПоля, Тип("ДеревоЗначений"));
	
	Если ОбъектКоллекции.Строки.Количество()>0 Тогда
		Результат = ПоместитьВоВременноеХранилище(ОбъектКоллекции, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПоместитьДополнительныеПоляВХранилище()

&НаСервере
// Функция помещает коллекцию доступных полей во временное хранилище и возвращает адрес 
//
Функция ПоместитьДоступныеПоляВыбораВХранилище(ИдентификаторОбъекта)
	
	Результат = "";
	
	ДеревоПолей = Новый ДеревоЗначений;
	ДеревоПолей.Колонки.Добавить("Использование");
	ДеревоПолей.Колонки.Добавить("Поле");
	ДеревоПолей.Колонки.Добавить("Заголовок");
	ДеревоПолей.Колонки.Добавить("Папка");
	ДеревоПолей.Колонки.Добавить("Расположение");
	
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(ИдентификаторОбъекта);
	
	ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ЭтаФорма, ДеревоПолей.Строки, ВыбранноеПоле.Элементы);
	
	Если ДеревоПолей.Строки.Количество()> 0 Тогда
		Результат = ПоместитьВоВременноеХранилище(ДеревоПолей, ИдентификаторВладельца);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПоместитьДополнительныеПоляВХранилище()

&НаКлиенте
Процедура ПодборРевизитов(Команда)
	
	ТекущаяСтрока = Элементы.ДоступныеПоляГруппировок.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	Если ВыбранноеПоле = Неопределено ИЛИ ВыбранноеПоле.Папка Тогда
		Возврат;
	КонецЕсли;
	
	МассивГруппировок = Новый Массив;
	МассивГруппировок.Добавить(ВыбранноеПоле.Поле);
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("СхемаКомпоновки", СхемаКомпоновки);
	СтруктураПараметров.Вставить("Событие",        "Реквизиты");
	СтруктураПараметров.Вставить("ИдентификаторВладельца", УникальныйИдентификатор);
	
	Если НЕ Элементы.ДополнительныеПоля.ТекущиеДанные = Неопределено Тогда
		СтруктураПараметров.Вставить("ТекущееПоле",          Элементы.ДополнительныеПоля.ТекущиеДанные.Поле);
	КонецЕсли;
	
	АдресТаблицы = ПоместитьДополнительныеПоляВХранилище();
	СтруктураПараметров.Вставить("АдресТаблицы", АдресТаблицы);
	
	СтруктураПараметров.Вставить("ГруппировкиСтруктуры", МассивГруппировок);
	
	ОписаниеОповещенияВыбораПоля = Новый ОписаниеОповещения("ОповещениеПодбораПолей", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораПолейОтчета", СтруктураПараметров, ЭтаФорма,,,,ОписаниеОповещенияВыбораПоля, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
// Процедура заполняет коллекцию формы из временного хранилища
//
Процедура ЗаполнитьКоллекциюИзХранилища(АдресТаблицы)
	
	КоллекцияФормы = ДополнительныеПоля.ПолучитьЭлементы();
	КоллекцияФормы.Очистить();
	
	Если Не ЭтоАдресВременногоХранилища(АдресТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектКоллекции = ПолучитьИзВременногоХранилища(АдресТаблицы);
	ОбъектКоллекции = ОбъектКоллекции.Строки;
	
	ОтчетыСервер.ЗаполнитьНастройкиОтчета(ЭтаФорма, КоллекцияФормы, ОбъектКоллекции);
	
КонецПроцедуры // ЗаполнитьКоллекциюИзХранилища()

&НаКлиенте
Процедура ОповещениеПодбораПолей(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатЗакрытия.Событие = "Реквизиты" Тогда
		
		ЗаполнитьКоллекциюИзХранилища(РезультатЗакрытия.АдресТаблицы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОчистить(Команда)
	
	ДополнительныеПоля.ПолучитьЭлементы().Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидыСравнения(ДоступныеВидыСравнения)
	
	СписокВыбора = Элементы.ВидСравненияОтбора.СписокВыбора;
	СписокВыбора.Очистить();
	
	Для Каждого ДоступноеСравнения Из ДоступныеВидыСравнения Цикл
		СписокВыбора.Добавить(ПолучитьПолноеИмяПредопределенногоЗначения(ДоступноеСравнения), Строка(ДоступноеСравнения));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПоляОтбораПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.ДоступныеПоляОтбора.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДоступноеПолеОтбора = КомпоновщикНастроек.Настройки.Отбор.ДоступныеПоляОтбора.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	
	Если ДоступноеПолеОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Элементы.ВидСравненияОтбора.СписокВыбора;
	СписокВыбора.Очистить();
	
	Для Каждого ДоступноеСравнения Из ДоступноеПолеОтбора.ДоступныеВидыСравнения Цикл
		
		ПолеПоиска = ВидыСравненияОтбора.НайтиПоЗначению(ДоступноеСравнения.Значение);
		Если ПолеПоиска = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВыбора.Добавить(ПолеПоиска.Представление, Строка(ПолеПоиска.Значение));
		
	КонецЦикла;
	
КонецПроцедуры

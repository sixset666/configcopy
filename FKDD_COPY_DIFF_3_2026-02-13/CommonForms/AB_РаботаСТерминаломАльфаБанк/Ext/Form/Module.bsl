&НаКлиенте
Перем Ожидание;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сумма = 0;
	НаименованиеСвободногоСтатуса = Строка(Перечисления.AB_СтатусыТерминаловАльфабанк.Открыт);
	ТекущийДокумент = ЭтаФорма.Параметры.ТекущийДокумент;
	ЭтаФорма.Заголовок = ТекущийДокумент;
	ОрганизацияДокумента = ТекущийДокумент.Организация;
	Таймаут = Константы.AB_СрокЖизниСессии.Получить();
	Если НЕ ЗначениеЗаполнено(Таймаут) Тогда
		Таймаут = 600;
	КонецЕсли;
	
	ОбновитьТаблицуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуНаСервере()
	
	ТекущийПользователь = ПараметрыСеанса.Пользователь;
	ОрганизацияД = ТекущийДокумент.Организация;
	ПодразделениеД = ТекущийДокумент.ПодразделениеКомпании;
	
	МассивПодразделений = Новый Массив;
	МассивПодразделений.Добавить(ПодразделениеД);
	Пока ЗначениеЗаполнено(ПодразделениеД.Родитель) Цикл
		МассивПодразделений.Добавить(ПодразделениеД.Родитель);
		ПодразделениеД = ПодразделениеД.Родитель;
	КонецЦикла;
	
	
	
	AB_ОбщийМодульАльфаБанкСервер.ОбработатьТранзакции(ТекущийТерминал,ТекущийДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	AB_ТерминалыАльфабанк.Ссылка КАК Ссылка,
		|	AB_ТерминалыАльфабанк.ID КАК ID,
		|	AB_ТерминалыАльфабанк.Наименование КАК Наименование,
		|	AB_ТерминалыАльфабанк.Организация КАК Организация,
		|	AB_ТерминалыАльфабанк.ПодразделениеКомпании КАК ПодразделениеКомпании
		|ПОМЕСТИТЬ Терминалы
		|ИЗ
		|	Справочник.AB_ТерминалыАльфабанк КАК AB_ТерминалыАльфабанк
		|ГДЕ
		|	AB_ТерминалыАльфабанк.ПометкаУдаления = ЛОЖЬ
		|	И AB_ТерминалыАльфабанк.Организация = &Организация
		|	И AB_ТерминалыАльфабанк.ПодразделениеКомпании В(&ПодразделениеКомпании)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Терминал КАК Терминал,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Пользователь КАК Пользователь,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Статус КАК Статус,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Период КАК Период,
		|	AB_РаботаСТерминаломАльфаБанкСрезПоследних.Документ КАК Документ
		|ПОМЕСТИТЬ СтатусТерминалов
		|ИЗ
		|	РегистрСведений.AB_РаботаСТерминаломАльфаБанк.СрезПоследних(
		|			,
		|			Статус = ЗНАЧЕНИЕ(Перечисление.AB_СтатусыТерминаловАльфабанк.Открыт)
		|				ИЛИ Статус = ЗНАЧЕНИЕ(Перечисление.AB_СтатусыТерминаловАльфабанк.Занят)) КАК AB_РаботаСТерминаломАльфаБанкСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Терминалы.Наименование КАК ТерминалН,
		|	СтатусТерминалов.Статус КАК Статус,
		|	СтатусТерминалов.Период КАК ВремяСтатуса,
		|	Терминалы.Организация КАК Организация,
		|	СтатусТерминалов.Пользователь КАК Пользователь,
		|	СУММА(AB_РаботаСТерминаломАльфаБанк.Сумма) КАК Сумма,
		|	Терминалы.Ссылка КАК Терминал,
		|	Терминалы.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	СтатусТерминалов.Документ КАК Документ
		|ИЗ
		|	Терминалы КАК Терминалы
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтатусТерминалов КАК СтатусТерминалов
		|		ПО Терминалы.Ссылка = СтатусТерминалов.Терминал
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.AB_РаботаСТерминаломАльфаБанк КАК AB_РаботаСТерминаломАльфаБанк
		|		ПО (СтатусТерминалов.Период < AB_РаботаСТерминаломАльфаБанк.Период)
		|			И Терминалы.Ссылка = AB_РаботаСТерминаломАльфаБанк.Терминал
		|
		|СГРУППИРОВАТЬ ПО
		|	Терминалы.Наименование,
		|	СтатусТерминалов.Статус,
		|	СтатусТерминалов.Период,
		|	Терминалы.Организация,
		|	СтатусТерминалов.Пользователь,
		|	Терминалы.Ссылка,
		|	Терминалы.ПодразделениеКомпании,
		|	СтатусТерминалов.Документ";
	
	Запрос.УстановитьПараметр("Организация",ОрганизацияД);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",МассивПодразделений);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ТекстНеЗанят = "Не занят терминал." + Символы.ПС + "Выберите свободный терминал и нажмите <Начать работу с терминалом>";
	
	ТекущийТерминал = Неопределено;
	
	Для Каждого СтрокаТерминал Из РезультатЗапроса Цикл
		Если СтрокаТерминал.Пользователь = ТекущийПользователь И СтрокаТерминал.Статус = Перечисления.AB_СтатусыТерминаловАльфабанк.Занят Тогда
			ТекущийТерминал = СтрокаТерминал.Терминал;
			Сумма = СтрокаТерминал.Сумма;
			ТекущийДокументИзСессии = СтрокаТерминал.Документ;
			ВремяОткрытияСессии = СтрокаТерминал.ВремяСтатуса;
		КонецЕсли;
	КонецЦикла;

	Если НЕ значениезаполнено(ТекущийТерминал) Тогда
		Элементы.ТекущийТерминал.Заголовок = ТекстНеЗанят;
		Элементы.ТекущийТерминал.ЦветТекста = новый Цвет(51, 153, 102);
		Элементы.ЗакончитьРаботу.Доступность = ложь;
	Иначе
		Элементы.НачатьРаботу.Доступность = ложь;
		ТекстТекущегоТерминала = "Текущим пользователем занят терминал <" + ТекущийТерминал.Наименование + ">" 
		+ Символы.ПС + "Оплачиваемый документ  - <" + ТекущийДокумент + ">"
		+ Символы.ПС + "Текущая принятая сумма - " + Сумма + " рублей";
		Элементы.ТекущийТерминал.Заголовок = ТекстТекущегоТерминала;
		Элементы.ТекущийТерминал.ЦветТекста = новый Цвет(172, 35, 15);		
	КонецЕсли;
	
	ЭтотОбъект.ТаблицаТерминалов.Загрузить(РезультатЗапроса);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицу(Команда=Неопределено)
	Ожидание = 10;
	
	// Запомним выбранную строку по терминалу
	ТекущийВыделенныйТерминал = Неопределено;
	Если Элементы.ТаблицаТерминалов.ТекущиеДанные <> Неопределено Тогда
		ТекущийВыделенныйТерминал = Элементы.ТаблицаТерминалов.ТекущиеДанные.Терминал;
	КонецЕсли;
	
	ОбновитьТаблицуНаСервере();
	
	// Выделим запомненную строку по терминалу
	Если ЗначениеЗаполнено(ТекущийВыделенныйТерминал) Тогда
		МассивСтрок = ТаблицаТерминалов.НайтиСтроки(Новый Структура("Терминал", ТекущийВыделенныйТерминал));
		Если МассивСтрок.Количество() Тогда
			Элементы.ТаблицаТерминалов.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура Автообновление()
	Если Ожидание = 0 Тогда
		Ожидание = 10;
		ЭтаФорма.Элементы.Обновить.Заголовок = "Обновить ("+Строка(Ожидание)+"с)";
		ОбновитьТаблицу();
	Иначе
		Ожидание = Ожидание - 1;
		ЭтаФорма.Элементы.Обновить.Заголовок = "Обновить ("+Строка(Ожидание)+"с)";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АвтообновлениеТаймаута()
	Если ЗначениеЗаполнено(ТекущийТерминал) И ЗначениеЗаполнено(ВремяОткрытияСессии) Тогда
		СессияОткрыта = ТекущаяДата() - ВремяОткрытияСессии;
		ОсталосьВремени = Таймаут - СессияОткрыта;
		Если ОсталосьВремени < 0 Тогда
			ОтключитьОбработчикОжидания("АвтообновлениеТаймаута");
			Элементы.ЗакончитьРаботу.Заголовок = "Автоматически завершаем работу по таймауту";
			ЗакончитьРаботу();
		Иначе
			Элементы.ЗакончитьРаботу.Заголовок = "Закончить работу с терминалом (" + Строка(ОсталосьВремени) + ")";
		КонецЕсли;
	Иначе
		Элементы.ЗакончитьРаботу.Заголовок = "Закончить работу с терминалом";
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура НачатьРаботуНаСервере(Терминал)
	Отказ = Ложь;
	Если ТекущийДокумент.AB_ЧерезАльфаБанк = Истина Тогда
		Сообщить("Документ уже оплачен. Закройте окно.");
		Отказ = Истина;
	КонецЕсли;
	Если Не Отказ Тогда
		Сумма = 0;
		AB_ОбщийМодульАльфаБанкСервер.НачатьРаботуСТерминалом(Терминал,ПараметрыСеанса.Пользователь,ТекущийДокумент);
		ОбновитьТаблицуНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьРаботу(Команда)
	// Запомним выбранную строку по терминалу
	ТекущийВыделенныйТерминал = Неопределено;
	Если Элементы.ТаблицаТерминалов.ТекущиеДанные <> Неопределено Тогда
		ТекущийВыделенныйТерминал = Элементы.ТаблицаТерминалов.ТекущиеДанные.Терминал;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элементы.ТаблицаТерминалов.ТекущиеДанные.Терминал) Тогда
		НачатьРаботуНаСервере(Элементы.ТаблицаТерминалов.ТекущиеДанные.Терминал);	
	КонецЕсли;
	
	// Выделим запомненную строку по терминалу
	Если ЗначениеЗаполнено(ТекущийВыделенныйТерминал) Тогда
		МассивСтрок = ТаблицаТерминалов.НайтиСтроки(Новый Структура("Терминал", ТекущийВыделенныйТерминал));
		Если МассивСтрок.Количество() Тогда
			Элементы.ТаблицаТерминалов.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗакончитьРаботуНаСервере(Терминал)
	AB_ОбщийМодульАльфаБанкСервер.ЗакончитьРаботуСТерминалом(Терминал,ПараметрыСеанса.Пользователь,ТекущийДокумент,Сумма);
	ОбновитьТаблицуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРаботу(Команда=Неопределено) Экспорт 
	
	// Если завершают работу покажем принятую сумму и подтверждение
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос(Новый ФорматированнаяСтрока("Сумма принятых средств - "+ Сумма +" руб." + Символы.ПС + Символы.ПС + "Вы точно хотите завершить работу с терминалом?", Новый Шрифт("Times New Roman", 20,Истина)), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
	    Возврат;
	КонецЕсли;

	
	ЗакончитьРаботуНаСервере(ТекущийТерминал);
	Если Сумма <> 0 Тогда
		Если ЭтаФорма.ВладелецФормы <> Неопределено Тогда
			ОповеститьОбИзменении(ТекущийДокумент);
			ЭтаФорма.ВладелецФормы.ТолькоПросмотр = Ложь;
			ЭтаФорма.ВладелецФормы.ЭлементыФормы.СуммаДокумента.Доступность = Ложь;
			//ЭтаФорма.ВладелецФормы.ОбновитьОтображениеДанных();
			ЭтаФорма.ВладелецФормы.Прочитать();
			ЭтаФорма.ВладелецФормы.Активизировать();
			ЭтаФорма.Закрыть();
			Сообщить("Закончили работу с АльфаБанк. Проведите документ.");
		Иначе
			ЭтаФорма.Закрыть();
			Сообщить("Закончили работу с АльфаБанк. Проведите документ. Документ - <"+ТекущийДокумент+">");
		КонецЕсли;
	Иначе
		Если ЭтаФорма.ВладелецФормы <> Неопределено Тогда
			ЭтаФорма.ВладелецФормы.ТолькоПросмотр = Ложь;
			ЭтаФорма.ВладелецФормы.Активизировать();
			ЭтаФорма.Закрыть();
			Сообщить("Закончили работу с АльфаБанк. Изменения не внесены.");
		Иначе
			ЭтаФорма.Закрыть();
			Сообщить("Закончили работу с АльфаБанк. Изменения не внесены. Документ - <"+ТекущийДокумент+">");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Ожидание = 10;
	ПодключитьОбработчикОжидания("Автообновление",1,ложь);
	ПодключитьОбработчикОжидания("АвтообновлениеТаймаута",1,ложь);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТерминаловПриАктивизацииСтроки(Элемент)
	Если ЗначениеЗаполнено(ТекущийТерминал) Тогда
		Элементы.НачатьРаботу.Доступность = ложь;
		Элементы.ЗакончитьРаботу.Доступность = Истина;
	Иначе
		Если ТипЗНЧ(Элементы.ТаблицаТерминалов.ТекущиеДанные) = Тип("ДанныеФормыЭлементКоллекции") Тогда
			Если Строка(Элементы.ТаблицаТерминалов.ТекущиеДанные.Статус) = НаименованиеСвободногоСтатуса Тогда
				Элементы.НачатьРаботу.Доступность = Истина;
			ИначеЕсли Строка(Элементы.ТаблицаТерминалов.ТекущиеДанные.Статус) = "" Тогда
				Элементы.НачатьРаботу.Доступность = Истина;
			Иначе
				Элементы.НачатьРаботу.Доступность = Ложь;
			КонецЕсли;
			Элементы.ЗакончитьРаботу.Доступность = ложь;
		Иначе
			Элементы.ЗакончитьРаботу.Доступность = ложь;
			Элементы.НачатьРаботу.Доступность = ложь;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

Перем РежимПроведения Экспорт;		// Режим проведения документа оперативный/неоперативный

Перем ДокументОбъект Экспорт; // документ выполняюший движения
Перем РезультатЗапросаПоТоварам Экспорт; // если неопределен, то в документе стандартная таблица
Перем РезультатЗапросаПоЗаказам Экспорт; // если неопределен, то в документе стандартная таблица
Перем ЗаказПоставщику Экспорт; // Заказ поставщику
Перем ЗаказОснование Экспорт; // Заказ основание
Перем Контрагент Экспорт; // Ссылка на контрагента
Перем ПоБазовомуКоличеству Экспорт; // Булево. Ложь - количество товаров будет раcсчитываться как "Количество*Коэффициент", Истина - количество будет браться из реквизита "КоличествоБазовое"

// Возвращает результат запроса по таблице товаров
Функция ПолучитьТаблицуТоваров()
	// получим результат запроса по товарной таблице
    ВидДок=ДокументОбъект.Метаданные().Имя;
	ПоБазовомуКоличеству=?(ПоБазовомуКоличеству=Неопределено,Ложь,ПоБазовомуКоличеству);
	Запрос=Новый Запрос();
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.Количество"+?(ПоБазовомуКоличеству,"Базовое","*ДокументТовары.Коэффициент")+" КАК Количество,
	|	ДокументТовары.СуммаВсего КАК СуммаВсего
	|ИЗ
	|	Документ."+ВидДок+".Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка=&Ссылка
	//|	И ДокументТовары.Количество<>0
	|";
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ЗаказПоставщику",ЗаказПоставщику);
	Запрос.УстановитьПараметр("Контрагент",Контрагент);
	Возврат Запрос.Выполнить();
КонецФункции

// Формирует движения по регистру приход (регистрируем заказ поставщика)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция Приход() Экспорт
	ВсеОК=Истина;
	
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	// получим таблицу товарного состава
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоТоварам=ПолучитьТаблицуТоваров();
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	Если ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ВалютаДоговора = ЗаказПоставщику.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	Иначе
		ВалютаДоговора = ДокументОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	КонецЕсли;
	
	//Перебираем строки товаров
	Для Каждого СтрокаТоваров Из РезультатЗапросаПоТоварам Цикл
		//Заказ=?(ЗаказПоставщику=Неопределено,СтрокаТоваров.ЗаказПоставщику,ЗаказПоставщику);
		Заказ=ЗаказПоставщику;
		//КАгент=?(Контрагент=Неопределено,СтрокаТоваров.Контрагент,Контрагент);
		КАгент=Контрагент;
		// добавляем 
		НоваяЗапись=Добавить();
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период=ДокументОбъект.Дата;
		НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
		НоваяЗапись.Контрагент=КАгент;
		НоваяЗапись.ЗаказПоставщику=Заказ;
		НоваяЗапись.Номенклатура=СтрокаТоваров.Номенклатура;
		НоваяЗапись.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
		// определяемся с хоз. операцией
		НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
		// количество
		НоваяЗапись.Заказано=СтрокаТоваров.Количество;
		// суммы
		НоваяЗапись.Сумма=Окр(?(СтрокаТоваров.СуммаВсего=NULL,0,обПересчет((СтрокаТоваров.СуммаВсего/СтрокаТоваров.Количество)*НоваяЗапись.Заказано,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаДоговора,ДокументОбъект.Дата)),2);
		НоваяЗапись.СуммаУпр=Окр(?(СтрокаТоваров.СуммаВсего=NULL,0,обПересчет((СтрокаТоваров.СуммаВсего/СтрокаТоваров.Количество)*НоваяЗапись.Заказано,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,?(обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр), ДокументОбъект.Дата, ДокументОбъект.КурсВалютыУпр))),2);
	КонецЦикла;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	
	Возврат Истина;
КонецФункции

// Формирует движения по регистру приход (корректируем заказ поставщика)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция КорректировкаЗаказаПоставщика() Экспорт
	
	ВсеОК = Истина;
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	// Получим таблицу товарного состава
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоТоварам=ПолучитьТаблицуТоваров();
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	//Получение остатков по заказу поставщику
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.ЗаказаноОстаток, 0) КАК Количество,
		|	ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) КАК МинимальныйОстаток,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.СуммаОстаток,0) КАК СуммаОстаток,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.СуммаУпрОстаток,0) КАК СуммаУпрОстаток
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&Момент,
		|			ЗаказПоставщику = &ЗаказПоставщику И 
		|			Номенклатура В (&Номенклатура) И 
		|			ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПоставщикамОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ЗаказыРаспределение.Остатки(&Момент,
		|			ЗаказПоставщика = &ЗаказПоставщику И 
		|			Номенклатура В (&Номенклатура) И 
		|			ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыРаспределениеОстатки
		|ПО
		|	ЗаказыПоставщикамОстатки.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки
		|");
	Запрос.УстановитьПараметр("Момент",                     ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, ДокументОбъект.МоментВремени()));
	Запрос.УстановитьПараметр("ЗаказПоставщику",            ЗаказПоставщику);
	Запрос.УстановитьПараметр("Номенклатура",               РезультатЗапросаПоТоварам.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", РезультатЗапросаПоТоварам.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПоставщикам");
	ЗначенияБлокировки = Новый Соответствие;
	ОписаниеИсточника  = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЗначенияБлокировки.Вставить("ЗаказПоставщику", ЗаказПоставщику);
	
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	ВыборкаОстатковТоваров = Запрос.Выполнить().Выбрать();
	СтруктураОтбораОстатковТоваров = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	
	ВремКонтрагент = ЗаказПоставщику.Контрагент;
	ВалютаДоговора = ЗаказПоставщику.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(, ДокументОбъект).Имя;
	// Спишем заказанный товар с регистра заказов покупателей
	Для Каждого СтрокаТоваров Из РезультатЗапросаПоТоварам Цикл
		
		Количество = СтрокаТоваров.Количество;
		ВыборкаОстатковТоваров.Сбросить();
		СтруктураОтбораОстатковТоваров.Номенклатура               = СтрокаТоваров.Номенклатура;
		СтруктураОтбораОстатковТоваров.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
		
		Если ВыборкаОстатковТоваров.НайтиСледующий(СтруктураОтбораОстатковТоваров) Тогда
			КоличествоОстаток  = ВыборкаОстатковТоваров.Количество;
			МинимальныйОстаток = ВыборкаОстатковТоваров.МинимальныйОстаток;
			СуммаОстаток       = ВыборкаОстатковТоваров.СуммаОстаток;
			СуммаУпрОстаток    = ВыборкаОстатковТоваров.СуммаУпрОстаток;
		Иначе
			КоличествоОстаток  = 0;
			МинимальныйОстаток = 0;
			СуммаОстаток       = 0;
			СуммаУпрОстаток    = 0;
		КонецЕсли;
		
		ЗначениеКорректировки = Количество - КоличествоОстаток;
		СуммаПоДокументу      = обПересчет(СтрокаТоваров.СуммаВсего, ДокументОбъект.ВалютаДокумента, ДокументОбъект.КурсДокумента, ВалютаДоговора, ДокументОбъект.Дата);
		
		Если ЗначениеКорректировки = 0 И (СуммаПоДокументу - СуммаОстаток) = 0 Тогда
			Продолжить;
		ИначеЕсли МинимальныйОстаток > Количество Тогда
			//Попытка снятия с заказа большего количества, чем заказано
			НаименованиеЕдиницы = СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения);
			Если обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат(СокрЛП(ЗаказПоставщику)+": ["+дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""". Снимается с заказа "+Формат(-ЗначениеКорректировки,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Заказано "+Формат(КоличествоОстаток,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Запрещено корректировать "+Формат(МинимальныйОстаток, "ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Превышение "+Формат(МинимальныйОстаток-Количество,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы,"Важное&Заказы поставщикам:",ДокументОбъект,СтрокаТоваров.Номенклатура);
			Иначе
				дкСообщитьРезультат(СокрЛП(ЗаказПоставщику)+": ["+дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТоваров.ХарактеристикаНоменклатуры)+""". Снимается с заказа "+Формат(-ЗначениеКорректировки,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Заказано "+Формат(КоличествоОстаток,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Запрещено корректировать "+Формат(МинимальныйОстаток, "ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Превышение "+Формат(МинимальныйОстаток-Количество,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы,"Важное&Заказы поставщикам:",ДокументОбъект,СтрокаТоваров.Номенклатура);
			КонецЕсли;
			ВсеОК=Ложь;
		КонецЕсли;
		
		Если ВсеОК Тогда
			// Создаем запись регистра заказов покупателей
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период                     = ДокументОбъект.Дата;
			НоваяЗапись.Регистратор                = ДокументОбъект.Ссылка;
			НоваяЗапись.Контрагент                 = ВремКонтрагент;
			НоваяЗапись.ЗаказПоставщику            = ЗаказПоставщику;
			НоваяЗапись.Номенклатура               = СтрокаТоваров.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
			// Определяемся с хоз. операцией
			НоваяЗапись.ХозОперация                = ДокументОбъект.ХозОперация;
			// Количество
			НоваяЗапись.Заказано                   = ЗначениеКорректировки;
			
			Если Количество = 0 Тогда
				НоваяЗапись.Сумма                  = -СуммаОстаток;
				НоваяЗапись.СуммаУпр               = -СуммаУпрОстаток;
			Иначе
				СуммаУпрСуммаПоДокументу           = обПересчет(СтрокаТоваров.СуммаВсего, ДокументОбъект.ВалютаДокумента, ДокументОбъект.КурсДокумента, ВалютаУпр,?(обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр), ДокументОбъект.Дата, ДокументОбъект.КурсВалютыУпр));
				НоваяЗапись.Сумма                  = СуммаПоДокументу - СуммаОстаток;
				НоваяЗапись.СуммаУпр               = СуммаУпрСуммаПоДокументу - СуммаУпрОстаток;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	// Убиваем циклическую ссылку
	ДокументОбъект = Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Формирует движения по регистру приход (корректируем заказ поставщика)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция КорректировкаСписаниемЗаказаПоставщика() Экспорт
	
	ВсеОК = Истина;
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	// Получим таблицу товарного состава
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоТоварам=ПолучитьТаблицуТоваров();
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	ЗаказПоставщикаВТаблице = (ЗаказПоставщику = Неопределено);
	
	//Получение остатков по заказу поставщику
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.Контрагент КАК Контрагент,
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.ЗаказаноОстаток, 0) КАК Количество,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.СуммаОстаток,0) КАК СуммаОстаток,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.СуммаУпрОстаток,0) КАК СуммаУпрОстаток
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&Момент,
		|			ЗаказПоставщику В (&ЗаказПоставщику) И 
		|			Номенклатура В (&Номенклатура) И 
		|			ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПоставщикамОстатки
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки
		|");
	Запрос.УстановитьПараметр("Момент",                     ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, ДокументОбъект.МоментВремени()));
	Запрос.УстановитьПараметр("ЗаказПоставщику",            РезультатЗапросаПоТоварам.ВыгрузитьКолонку("ЗаказПоставщику"));
	Запрос.УстановитьПараметр("Номенклатура",               РезультатЗапросаПоТоварам.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", РезультатЗапросаПоТоварам.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПоставщикам");
	
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ДокументОбъект.Дата));
	
	ОписаниеИсточника  = Новый Соответствие;
	ОписаниеИсточника.Вставить("ЗаказПоставщику", "ЗаказПоставщику");
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	ВыборкаОстатковТоваров = Запрос.Выполнить().Выбрать();
	СтруктураОтбораОстатковТоваров = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, ЗаказПоставщику");
	
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(, ДокументОбъект).Имя;
	// Спишем заказанный товар с регистра заказов покупателей
	Для Каждого СтрокаТоваров Из РезультатЗапросаПоТоварам Цикл
		
		Количество = СтрокаТоваров.Количество;
		ВыборкаОстатковТоваров.Сбросить();
		СтруктураОтбораОстатковТоваров.Номенклатура               = СтрокаТоваров.Номенклатура;
		СтруктураОтбораОстатковТоваров.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
		СтруктураОтбораОстатковТоваров.ЗаказПоставщику        = СтрокаТоваров.ЗаказПоставщику;
		
		Если ВыборкаОстатковТоваров.НайтиСледующий(СтруктураОтбораОстатковТоваров) Тогда
			КоличествоОстаток = ВыборкаОстатковТоваров.Количество;
			СуммаОстаток      = ВыборкаОстатковТоваров.СуммаОстаток;
			СуммаУпрОстаток   = ВыборкаОстатковТоваров.СуммаУпрОстаток;
			ВремКонтрагент    = ВыборкаОстатковТоваров.Контрагент;
		Иначе
			КоличествоОстаток = 0;
			СуммаОстаток      = 0;
			СуммаУпрОстаток   = 0;
			ВремКонтрагент    = СтрокаТоваров.ЗаказПоставщику.Контрагент;
		КонецЕсли;
		
		Если Количество > КоличествоОстаток Тогда
			//Попытка снятия с заказа большего количества, чем заказано
			НаименованиеЕдиницы = СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения);
			Если обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат(СокрЛП(СтрокаТоваров.ЗаказПоставщику)+": ["+дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""". Снимается с заказа "+Формат(Количество,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Заказано "+Формат(КоличествоОстаток,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Превышение "+Формат(Количество - КоличествоОстаток,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы,"Важное&Заказы поставщикам:",ДокументОбъект,СтрокаТоваров.Номенклатура);
			Иначе
				дкСообщитьРезультат(СокрЛП(СтрокаТоваров.ЗаказПоставщику)+": ["+дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТоваров.ХарактеристикаНоменклатуры)+""". Снимается с заказа "+Формат(Количество,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Заказано "+Формат(КоличествоОстаток,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы+". Превышение "+Формат(Количество-КоличествоОстаток,"ЧДЦ=3; ЧН=0,00")+" "+НаименованиеЕдиницы,"Важное&Заказы поставщикам:",ДокументОбъект,СтрокаТоваров.Номенклатура);
			КонецЕсли;
			ВсеОК=Ложь;
		КонецЕсли;
		
		Если ВсеОК И Количество<>0 Тогда
			// Создаем запись регистра заказов покупателей
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период                     = ДокументОбъект.Дата;
			НоваяЗапись.Регистратор                = ДокументОбъект.Ссылка;
			НоваяЗапись.Контрагент                 = ВремКонтрагент;
			НоваяЗапись.ЗаказПоставщику            = СтрокаТоваров.ЗаказПоставщику;
			НоваяЗапись.Номенклатура               = СтрокаТоваров.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
			// Определяемся с хоз. операцией
			НоваяЗапись.ХозОперация                = ДокументОбъект.ХозОперация;
			// Количество
			НоваяЗапись.Заказано                   = -Количество;
			
			Если Количество = КоличествоОстаток Тогда
				НоваяЗапись.Сумма                  = -СуммаОстаток;
				НоваяЗапись.СуммаУпр               = -СуммаУпрОстаток;
			Иначе
				НоваяЗапись.Сумма                  = -Окр(СуммаОстаток/Количество, 2);
				НоваяЗапись.СуммаУпр               = -Окр(СуммаУпрОстаток/Количество, 2);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Убиваем циклическую ссылку
	ДокументОбъект = Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Формирует движения по регистру расход (снимаем заказ поставщика)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция ЗакрытиеЗаказовПоставщику() Экспорт
	ВсеОК=Истина;
	
	// получим таблицу товарного состава
	Если (РезультатЗапросаПоТоварам=Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		РезультатЗапросаПоТоварам=ПолучитьТаблицуТоваров();
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоТоварам)=Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам.Выгрузить();
	КонецЕсли;
	
	Если РезультатЗапросаПоЗаказам=Неопределено Тогда
		// получим не закрытые заказы поставщикам, нашего контрагента
		Запрос=Новый Запрос("
		|ВЫБРАТЬ * ИЗ(
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Контрагент КАК Контрагент,
		|	ВЫБОР КОГДА ЗаказыПоставщикамОстатки.ЗаказПоставщику=&ЗаказОснование ТОГДА 0 ИНАЧЕ 1 КОНЕЦ КАК ЗаказОснование,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЗаказыПоставщикамОстатки.СуммаОстаток КАК Сумма,
		|	ЗаказыПоставщикамОстатки.СуммаУпрОстаток КАК СуммаУпр,
		|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК Заказано
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&Момент,
		|		Номенклатура В (&Номенклатура)
		|		И (ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) ИЛИ ХарактеристикаНоменклатуры=&ПустаяХарактеристика)
		|   	"+?(Контрагент=Неопределено,"","И Контрагент=&Контрагент")+"
		|		"+?(ЗаказПоставщику=Неопределено,"","И ЗаказПоставщику=&ЗаказПоставщику")+"
		|	) КАК ЗаказыПоставщикамОстатки
		|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ЗаказыПоставщикам.Остатки
		|) КАК ЗаказыПоставщикамОстатки
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказыПоставщикамОстатки.ЗаказОснование Возр,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.Дата,
		|	ЗаказыПоставщикамОстатки.Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры.Сортировка Возр
		|");
		Запрос.УстановитьПараметр("Момент",?(РежимПроведения=РежимПроведенияДокумента.Оперативный,Неопределено,ДокументОбъект.МоментВремени()));
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("ЗаказПоставщику",ЗаказПоставщику);
		Запрос.УстановитьПараметр("ЗаказОснование",ЗаказОснование);
		Запрос.УстановитьПараметр("Номенклатура",РезультатЗапросаПоТоварам.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",РезультатЗапросаПоТоварам.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
		Запрос.УстановитьПараметр("ПустаяХарактеристика",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		
		// Наложим блокировку на считываемые данные
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПоставщикам");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ДокументОбъект.Дата));
		Если Контрагент <> Неопределено Тогда
			ЗначенияБлокировки.Вставить("Контрагент", Контрагент);
		КонецЕсли;
		Если ЗаказПоставщику <> Неопределено Тогда
			ЗначенияБлокировки.Вставить("ЗаказПоставщику", ЗаказПоставщику);
		КонецЕсли;
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", РезультатЗапросаПоТоварам);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		
		РезультатЗапросаПоЗаказам = Запрос.Выполнить();
		РезультатЗапросаПоЗаказам = РезультатЗапросаПоЗаказам.Выгрузить();
	КонецЕсли; 
	
	ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	РучнойРежим = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание;
	ПустойРежим = Перечисления.РежимыАвтоСписанияХарактеристик.ПустаяСсылка();
	Для Каждого СтрокаПоЗаказам Из РезультатЗапросаПоЗаказам Цикл
		
		РучноеСписание = Истина;
		АвтоСписаниеХарактеристик = СтрокаПоЗаказам.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик;
		Если СтрокаПоЗаказам.ХарактеристикаНоменклатуры = ПустаяХарактеристика И 
			(НЕ АвтоСписаниеХарактеристик = ПустойРежим) И 
			(НЕ АвтоСписаниеХарактеристик = РучнойРежим) Тогда
			Автосписание = Ложь;
		КонецЕсли;
		
		НадоЗакрыть = СтрокаПоЗаказам.Заказано;
		Сумма       = СтрокаПоЗаказам.Сумма;
		СуммаУпр    = СтрокаПоЗаказам.СуммаУпр;
		СтруктураОтбора = Новый Структура("Номенклатура");
		СтруктураОтбора.Номенклатура=СтрокаПоЗаказам.Номенклатура;
		Если РучноеСписание Тогда
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаПоЗаказам.ХарактеристикаНоменклатуры);
		КонецЕсли;
		ВыборкаТовары = РезультатЗапросаПоТоварам.НайтиСтроки(СтруктураОтбора);
		Для Каждого Товар ИЗ ВыборкаТовары Цикл
			// Если все закрыли, то выходим
			Если НадоЗакрыть=0 Тогда
				Прервать;
			КонецЕсли;
			//Если товара 0 продолжаем
			Если Товар.Количество=0 Тогда
				Продолжить;
			КонецЕсли;
			// Закрываем заказ
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения     = ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период          = ДокументОбъект.Дата;
			НоваяЗапись.Регистратор     = ДокументОбъект.Ссылка;
			НоваяЗапись.Контрагент      = Контрагент;
			НоваяЗапись.ЗаказПоставщику = СтрокаПоЗаказам.ЗаказПоставщику;
			НоваяЗапись.Номенклатура    = Товар.Номенклатура;
			Если РучноеСписание Тогда
				НоваяЗапись.ХарактеристикаНоменклатуры = Товар.ХарактеристикаНоменклатуры;
			КонецЕсли;
			НоваяЗапись.ХозОперация     = ДокументОбъект.ХозОперация;
			// Количество
			Если НадоЗакрыть > Товар.Количество Тогда
				НоваяЗапись.Заказано    = Товар.Количество;
				// Суммы
				НоваяЗапись.Сумма       = Окр(Сумма/НадоЗакрыть*Товар.Количество, 2);
				НоваяЗапись.СуммаУпр    = Окр(СуммаУпр/НадоЗакрыть*Товар.Количество, 2);
				
				РезультатЗапросаПоТоварам.Удалить(Товар);
			Иначе
				НоваяЗапись.Заказано    = НадоЗакрыть;
				// Суммы
				НоваяЗапись.Сумма       = Сумма;
				НоваяЗапись.СуммаУпр    = СуммаУпр;
				
				Товар.Количество = Товар.Количество-НоваяЗапись.Заказано;
			КонецЕсли;
			НадоЗакрыть = НадоЗакрыть - НоваяЗапись.Заказано;
			Сумма       = Сумма       - НоваяЗапись.Сумма;
			СуммаУпр    = СуммаУпр    - НоваяЗапись.СуммаУпр;
		КонецЦикла;
		
	КонецЦикла;
	
	РезультатЗапросаПоТоварам=Неопределено;
	РезультатЗапросаПоЗаказам=Неопределено;
	// Убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	
	Возврат ВсеОк;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

////////////////////////////////////////////////////////////////////////
//Инициализация переменных модуля набора записей
РежимПроведения=РежимПроведенияДокумента.Оперативный;

ДокументОбъект=Неопределено; // !!! Обязательный к заполнению перед началом проведения
РезультатЗапросаПоТоварам=Неопределено;
РезультатЗапросаПоЗаказам=Неопределено;
ЗаказПоставщику=Неопределено;
Контрагент=Неопределено;
ПоБазовомуКоличеству=Ложь;

// Модуль набора записей регистра "КассыККМ"

Перем РежимПроведения Экспорт;                  // Режим проведения документа оперативный/неоперативный
Перем ДокументОбъект Экспорт;                   // Документ, осуществляющий движение по регистру
Перем КассаККМ Экспорт;                         // Ссылка на кассу
Перем СпособОплаты Экспорт;                     // Способ оплаты
Перем Сумма Экспорт;                            // Сумма
Перем РасчетОстатковНа Экспорт;                 // Дата расчета остатков
Перем СуммаДоходаРасходаКурсовыхРазниц Экспорт; // Сумма дохода расхода курсовых разниц

// Функция возвращает остаток наличных
Функция ПолучитьОстатокНаличных(СпособОплаты = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КассыККМОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.КассыККМ.Остатки(&НаМомент, КассаККМ = &КассаККМ И СпособОплаты = &СпособОплаты) КАК КассыККМОстатки";
	Запрос.УстановитьПараметр("НаМомент",?(РасчетОстатковНа=Неопределено,?(РежимПроведения=РежимПроведенияДокумента.Оперативный,Неопределено,ДокументОбъект.МоментВремени()),РасчетОстатковНа));
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	Запрос.УстановитьПараметр("СпособОплаты",СпособОплаты);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СуммаОстатка=?(Выборка.Сумма=NULL,0,Выборка.Сумма);
	Иначе
		СуммаОстатка=0;
	КонецЕсли;
	Возврат СуммаОстатка;
КонецФункции // ПолучитьОстатокНаличных()

// Функция возвращает остаток безразличных
Функция ПолучитьОстатокБезналичных(Поступление,СпособОплаты = Неопределено ) Экспорт
	Если РасчетОстатковНа=Неопределено Тогда
		ДатаРасчета=ДокументОбъект.МоментВремени().Дата;
	Иначе
		Если ТипЗнч(РасчетОстатковНа)=Тип("Дата") Тогда
			ДатаРасчета=РасчетОстатковНа;
		ИначеЕсли ТипЗнч(РасчетОстатковНа)=Тип("МоментВремени") Тогда
			ДатаРасчета=РасчетОстатковНа.Дата;
		Иначе
			ДатаРасчета=ДокументОбъект.МоментВремени().Дата;
		КонецЕсли; 
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(КассыККМ.Сумма) КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.КассыККМ КАК КассыККМ
	               |ГДЕ
	               |	КассыККМ.Период <= &НаМомент И
	               |	КассыККМ.КассаККМ = &КассаККМ И
	               |	КассыККМ.СпособОплаты = &СпособОплаты И
	               |	КассыККМ.ВидДвижения = &ВидДвижения И
	               |	(КассыККМ.Сумма "+?(Поступление,">","<")+" 0)";
	Запрос.УстановитьПараметр("НаМомент",ДатаРасчета);
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	Запрос.УстановитьПараметр("СпособОплаты",СпособОплаты);
	Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Приход);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СуммаПрихода=?(Выборка.Сумма=NULL,0,Выборка.Сумма);
	Иначе
		СуммаПрихода=0;
	КонецЕсли;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(КассыККМ.Сумма) КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.КассыККМ КАК КассыККМ
	               |ГДЕ
	               |	КассыККМ.Период <= &НаМомент И
	               |	КассыККМ.КассаККМ = &КассаККМ И
	               |	КассыККМ.СпособОплаты = &СпособОплаты И
	               |	КассыККМ.ВидДвижения = &ВидДвижения И
	               |	(КассыККМ.Сумма "+?(Поступление,">","<")+" 0)";
	Запрос.УстановитьПараметр("НаМомент",ДатаРасчета);
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	Запрос.УстановитьПараметр("СпособОплаты",СпособОплаты);
	Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Расход);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СуммаРасхода=?(Выборка.Сумма=NULL,0,Выборка.Сумма);
	Иначе
		СуммаРасхода=0;
	КонецЕсли;
	Возврат СуммаПрихода-СуммаРасхода;
КонецФункции // ПолучитьОстатокБезналичныхПоступление()

// Формирует движения по приходу (поступление денежных средств в кассу ККМ)
// Возвращаемое значение: Булево. Истина - все нормально, Ложь - чего-то не так.
Функция Приход() Экспорт
	КассаККМ=?(КассаККМ=Неопределено,ДокументОбъект.КассаККМ,КассаККМ);
	
	СуммаВВалютеКассы=обПересчет(Сумма,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,КассаККМ.ВалютаДенежныхСредств,ДокументОбъект.Дата);
	
	Результат=Истина;
	Если СпособОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
		//При приходе проверка осуществляется только по наличной оплате
		Если СуммаВВалютеКассы<0 Тогда
			//И если сумма прихода отрицательная, т.е. производиться сторнирование (возвратный чек)
			Если (НЕ обПраво("РазрешитьОтрицательныеОстаткиПоКассе",ДокументОбъект.Права))
				 Тогда			
				 //Проверка осуществляется при запрете отрицательных остатков по кассе и при оперативном проведении
				 СуммаОстатка=ПолучитьОстатокНаличных(СпособОплаты);
				 Если СуммаОстатка<(-СуммаВВалютеКассы) Тогда
					дкСообщитьРезультат("Нет прав на отрицательные суммовые остатки по <"+СокрЛП(КассаККМ)+">","Внимание&Кассы ККМ:",ДокументОбъект);
					Результат=Ложь;	
				 КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	
	Если Результат Тогда
		НоваяЗапись=Добавить();
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период=ДокументОбъект.Дата;
		НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
		НоваяЗапись.КассаККМ=КассаККМ;
		НоваяЗапись.СпособОплаты=СпособОплаты;
		НоваяЗапись.Сумма=Окр(СуммаВВалютеКассы,2);
		НоваяЗапись.СуммаУпр=Окр(обПересчет(Сумма,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),?(обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр), ДокументОбъект.Дата, ДокументОбъект.КурсВалютыУпр)),2);
		НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
	КонецЕсли;
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	
	Возврат Результат;
КонецФункции

// Формирует движения по расходу (расход денежных средств из кассы ККМ)
// Возвращаемое значение: Булево. Истина - все нормально, Ложь - чего-то не так.
Функция Расход() Экспорт
	КассаККМ=?(КассаККМ=Неопределено,ДокументОбъект.КассаККМ,КассаККМ);
	
	СуммаВВалютеКассы=обПересчет(Сумма,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,КассаККМ.ВалютаДенежныхСредств,ДокументОбъект.Дата);
	
	Результат=Истина;
	Если СпособОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
		Если СуммаВВалютеКассы>0 Тогда
			//При расходе проверка изъятия денежных средств только при положительной сумме расхода
			Если (НЕ обПраво("РазрешитьОтрицательныеОстаткиПоКассе",ДокументОбъект.Права))
				 Тогда			
				 //Проверка осуществляется при запрете отрицательных остатков по кассе и при оперативном проведении
				 СуммаОстатка=ПолучитьОстатокНаличных(СпособОплаты);
				 Если СуммаОстатка<СуммаВВалютеКассы Тогда
					дкСообщитьРезультат("Нет прав на отрицательные суммовые остатки по <"+СокрЛП(КассаККМ)+">","Внимание&Кассы ККМ:",ДокументОбъект);
					Результат=Ложь;	
				 КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
	ИначеЕсли СпособОплаты.Объект <> Перечисления.ТипыОплатыВРознице.Наличные Тогда
		//Проверка при расходе по безналичной оплате
		Если СуммаВВалютеКассы>0 Тогда
			//Если сумма больше нуля происходит проверка изъятия безналичных платежей
			Если (НЕ обПраво("РазрешитьОтрицательныеОстаткиПоКассе",ДокументОбъект.Права))
				 Тогда			
				 //Проверка осуществляется при запрете отрицательных остатков по кассе и при оперативном проведении
				 СуммаОстатка=ПолучитьОстатокБезналичных(Истина,СпособОплаты );
				 Если СуммаОстатка<СуммаВВалютеКассы Тогда
					дкСообщитьРезультат("Нет прав на отрицательные суммовые остатки по <"+СокрЛП(КассаККМ)+">","Внимание&Кассы ККМ:",ДокументОбъект);
					Результат=Ложь;	
				 КонецЕсли; 
			КонецЕсли;
		ИначеЕсли СуммаВВалютеКассы<0 Тогда
			//Если сумма меньше нуля происходит проверка изъятия безналичных платежей на возврат
			Если (НЕ обПраво("РазрешитьОтрицательныеОстаткиПоКассе",ДокументОбъект.Права))
				 Тогда			
				 //Проверка осуществляется при запрете отрицательных остатков по кассе и при оперативном проведении
				 СуммаОстатка=ПолучитьОстатокБезналичных(Ложь, СпособОплаты);
				 Если (-СуммаОстатка)<(-СуммаВВалютеКассы) Тогда
					дкСообщитьРезультат("Нет прав на отрицательные суммовые остатки по <"+СокрЛП(КассаККМ)+">","Внимание&Кассы ККМ:",ДокументОбъект);
					Результат=Ложь;	
				 КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	
	Если Результат Тогда
		// расходуем денежные средства
		НоваяЗапись=Добавить();
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период=ДокументОбъект.Дата;
		НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
		НоваяЗапись.КассаККМ=КассаККМ;
		НоваяЗапись.СпособОплаты=СпособОплаты;
		НоваяЗапись.Сумма=Окр(СуммаВВалютеКассы,2);
		НоваяЗапись.СуммаУпр=Окр(обПересчет(Сумма,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),?(обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр), ДокументОбъект.Дата, ДокументОбъект.КурсВалютыУпр)),2);
		НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
	КонецЕсли;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	
	Возврат Результат;
КонецФункции

// списывает курсовые разницы
Функция СписаниеКурсовыхРазниц() Экспорт
	// вспомогательные переменные
	СуммаДоходаРасходаКурсовыхРазниц=0;
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ДокументОбъект.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ДокументОбъект.КурсВалютыУпр;
	КонецЕсли;	
	
	СтрокаФильтра = "";
	Если ДополнительныеСвойства.Свойство("ФильтрПоПодразделению") Тогда
		СтрокаФильтра = ", КассаККМ.Подразделение=&ПодразделениеКомпании";	
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоОрганизации") Тогда
		СтрокаФильтра = ", КассаККМ.Организация=&Организация";
	КонецЕсли;

	// получаем выборку
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|   КассыККМОстатки.КассаККМ КАК КассаККМ,
	|   КассыККМОстатки.КассаККМ.ВалютаДенежныхСредств КАК Валюта,
	|   КассыККМОстатки.СпособОплаты КАК СпособОплаты,
	|	ВЫБОР КОГДА КассыККМОстатки.СуммаОстаток ЕСТЬ NULL ТОГДА 0 ИНАЧЕ КассыККМОстатки.СуммаОстаток КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА КассыККМОстатки.СуммаУпрОстаток ЕСТЬ NULL ТОГДА 0 ИНАЧЕ КассыККМОстатки.СуммаУпрОстаток КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность,0)=0 ТОГДА
	|			КурсыВалютСрезПоследних.Курс
	|		ИНАЧЕ
	|			КурсыВалютСрезПоследних.Курс/КурсыВалютСрезПоследних.Кратность
	|	КОНЕЦ КАК Курс
	|ИЗ
	|	РегистрНакопления.КассыККМ.Остатки(&Момент" + СтрокаФильтра + ") КАК КассыККМОстатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Момент) КАК КурсыВалютСрезПоследних
	|	ПО КассыККМОстатки.КассаККМ.ВалютаДенежныхСредств=КурсыВалютСрезПоследних.Валюта");
	Запрос.УстановитьПараметр("Момент",?(РежимПроведения=РежимПроведенияДокумента.Оперативный,Неопределено,ДокументОбъект.МоментВремени()));
	
	// Если нужно добавим фильтр.
	Если ДополнительныеСвойства.Свойство("ФильтрПоПодразделению") Тогда
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ДополнительныеСвойства.ФильтрПоПодразделению);
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоОрганизации") Тогда
		Запрос.УстановитьПараметр("Организация", ДополнительныеСвойства.ФильтрПоОрганизации);	
	КонецЕсли;
	
	РезультатЗапросаКурсовые = Запрос.Выполнить();
	
	// Если необходимо, заполним вспомогательную таблицу движений, которую 
	// можно использовать во "внешнем" коде.
	Если ДополнительныеСвойства.Свойство("ТаблицаДвижений") Тогда
		ЗаполнитьТаблицуДвижений = Истина;
	Иначе
		ЗаполнитьТаблицуДвижений = Ложь;
	КонецЕсли;

	// идем по выборке
	Выборка=РезультатЗапросаКурсовые.Выбрать();
	Пока Выборка.Следующий() Цикл
		СуммаПереоценки=Окр(обПересчет(Выборка.Сумма,Выборка.Валюта,Выборка.Курс,ВалютаУпр,КурсУпр)-Выборка.СуммаУпр,2);
		Если СуммаПереоценки=0 Тогда Продолжить; КонецЕсли; // переоценивать нечего
		// переоцениваем
		НоваяЗапись=Добавить();
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период=ДокументОбъект.Дата;
		НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
		НоваяЗапись.КассаККМ=Выборка.КассаККМ;
		НоваяЗапись.СпособОплаты=Выборка.СпособОплаты;
		НоваяЗапись.Сумма=0;
		НоваяЗапись.СуммаУпр=Окр(СуммаПереоценки,2);
		НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
		// доходы/расходы
		СуммаДоходаРасходаКурсовыхРазниц=СуммаДоходаРасходаКурсовыхРазниц+НоваяЗапись.СуммаУпр;
		
		// Заполняем вспомогательную таблицу движений.
		Если ЗаполнитьТаблицуДвижений Тогда
			НоваяСтрока = ДополнительныеСвойства.ТаблицаДвижений.Добавить();
			НоваяСтрока.Подразделение      = Выборка.КассаККМ.Подразделение;
			НоваяСтрока.КурсоваяРазницаУпр = НоваяЗапись.СуммаУпр;
		КонецЕсли;
	КонецЦикла;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	
	// все ОК
	Возврат Истина;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

РежимПроведения=РежимПроведенияДокумента.Оперативный;
ДокументОбъект=Неопределено; // !!! Обязательный к заполнению перед началом проведения
КассаККМ=Неопределено;
СпособОплаты=Неопределено; // !!! Обязательный к заполнению перед началом проведения
Сумма=0; // !!! Обязательный к заполнению перед началом проведения
РасчетОстатковНа=Неопределено;
СуммаДоходаРасходаКурсовыхРазниц=0;

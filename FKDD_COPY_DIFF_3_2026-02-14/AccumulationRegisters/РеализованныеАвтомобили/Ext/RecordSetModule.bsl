Перем Сторно Экспорт; // Признак сторно записи
Перем ВУпрВалюте Экспорт; // если Истина, то суммы переданы в валюте упр. учета и пересчитывать их не надо
Перем ДокументОбъект Экспорт; // Документ объект выполняющий движения
Перем Контрагент Экспорт; // Ссылка на контрагента
Перем ДоговорВзаиморасчетов Экспорт; // Ссылка на договор взаимо расчетов
Перем РезультатЗапросаПоАвтомобилям Экспорт; // Результат запроса по автомобилям
Перем Списание Экспорт; // Булево. Истина - проводим складское списание автомобилей.
//Перем СтоимостьРеализованныхАвтомобилей Экспорт;	//кэш для последующих проводок DORA
Перем ЗаписыватьДвижения Экспорт; // Булево. Истина - движения записываются сразу по окончании выполнения процедуры. Ложь - движения записываются при записи документа.

// Возврат реализованных автомобилей.
//// Возвращает Истина - все ОК, Ложь - чего-то не так
Функция ВозвратАвтомобилей()
	ВсеОК=Истина;
	
	// Получим результат проведения возвратного документа по партионному регистру.
	// Нас будут интересовать только возвращенные комиссионные автомобили
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ОстаткиАвтомобилейОбороты.Автомобиль КАК Автомобиль,
	|	ОстаткиАвтомобилейОбороты.Партия КАК ДокументПередачи,
	|	ОстаткиАвтомобилейОбороты.ГТД КАК ГТД,
	|	ОстаткиАвтомобилейОбороты.КоличествоРасход*(-1) КАК Количество
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей.Обороты(&Момент,&Момент,Регистратор,СтатусПартии=&СтатусПартии) КАК ОстаткиАвтомобилейОбороты
	|ГДЕ
	|	ОстаткиАвтомобилейОбороты.Регистратор=&Ссылка
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ОстаткиАвтомобилей.Обороты
	|";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	Момент=Новый Граница(ДокументОбъект.МоментВремени(),ВидГраницы.Включая);
	Запрос.УстановитьПараметр("Момент",Момент);
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ОстаткиАвтомобилей");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ДокументОбъект.Дата)); 
	ЗначенияБлокировки.Вставить("СтатусПартии", Перечисления.СтатусыПартий.ТоварПринятыйКомиссия); 
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
	// Получим таблицу извращенных автомобилей
	ТаблицаВозвращенныхАвтомобилей=Запрос.Выполнить().Выгрузить();
	// Если таблица пустая, то возвращать нечего
	Если ТаблицаВозвращенныхАвтомобилей.Количество()=0 Тогда 
		// Убиваем циклическую ссылку
		ДокументОбъект=Неопределено;
		Контрагент=Неопределено;
		ДоговорВзаиморасчетов=Неопределено;
		РезультатЗапросаПоАвтомобилям=Неопределено;
		Возврат Истина; 
	КонецЕсли;
	
	// Теперь соберем данные из текущего регистра и будем возвращать в порядке ЛИФО
	ТекстЗапроса="ВЫБРАТЬ
	             |	РеализованныеАвтомобилиОбороты.Контрагент КАК Контрагент,
	             |	РеализованныеАвтомобилиОбороты.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	             |	РеализованныеАвтомобилиОбороты.Автомобиль КАК Автомобиль,
	             |	РеализованныеАвтомобилиОбороты.ДокументПередачи КАК ДокументПередачи,
	             |	РеализованныеАвтомобилиОбороты.ГТД КАК ГТД,
	             |	РеализованныеАвтомобилиОбороты.КоличествоПриход КАК Количество,
	             |	РеализованныеАвтомобилиОбороты.СуммаУпрПриход КАК СуммаУпр,
	             |	РеализованныеАвтомобилиОбороты.СуммаПродажиПриход КАК СуммаПродажи,
	             |	РеализованныеАвтомобилиОбороты.СуммаПродажиРеглПриход КАК СуммаПродажиРегл,
	             |	РеализованныеАвтомобилиОбороты.СуммаРеглПриход КАК СуммаРегл
	             |ИЗ
	             |	РегистрНакопления.РеализованныеАвтомобили.Обороты(,,,Автомобиль В (&Автомобиль) И ДокументПередачи В (&ДокументПередачи)) КАК РеализованныеАвтомобилиОбороты
	             |
	             |ДЛЯ ИЗМЕНЕНИЯ
	             |	РегистрНакопления.РеализованныеАвтомобили.Обороты
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ДокументПередачи УБЫВ";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Автомобиль",ТаблицаВозвращенныхАвтомобилей.ВыгрузитьКолонку("Автомобиль"));
	Запрос.УстановитьПараметр("ДокументПередачи",ТаблицаВозвращенныхАвтомобилей.ВыгрузитьКолонку("ДокументПередачи"));
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "РеализованныеАвтомобили");
	ЗначенияБлокировки = Новый Соответствие;
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаВозвращенныхАвтомобилей);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Автомобиль", "Автомобиль");
	ОписаниеИсточника.Вставить("ДокументПередачи", "ДокументПередачи");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	// Получим таблицу реализованных автомобилей
	ТаблицаРеализованныхАвтомобилей=Запрос.Выполнить().Выгрузить();

	// Создаем структуру отбора
	СтруктураОтбора=Новый Структура("Автомобиль");
	Для Каждого СтрокаАвтомобиль Из ТаблицаВозвращенныхАвтомобилей Цикл
		// Получим строки таблицы партий с нашим автомобилем
		СтруктураОтбора.Автомобиль=СтрокаАвтомобиль.Автомобиль;
		МассивСтрок=ТаблицаРеализованныхАвтомобилей.НайтиСтроки(СтруктураОтбора);
		// Теперь возвращаем
		НадоВернуть=СтрокаАвтомобиль.Количество;
		ВГраница=МассивСтрок.ВГраница();
		Для Сч=0 По ВГраница Цикл
			СтрокаТаблицы=МассивСтрок[Сч];
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=ДокументОбъект.Дата;
			НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
			НоваяЗапись.Контрагент=СтрокаТаблицы.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов=СтрокаТаблицы.ДоговорВзаиморасчетов;
			НоваяЗапись.ДокументПередачи=СтрокаТаблицы.ДокументПередачи;
			НоваяЗапись.Автомобиль=СтрокаТаблицы.Автомобиль;
			НоваяЗапись.ГТД=СтрокаТаблицы.ГТД;
			НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
			НоваяЗапись.Количество=-Мин(НадоВернуть,СтрокаТаблицы.Количество);
			НоваяЗапись.СуммаУпр=Окр(-?(СтрокаТаблицы.Количество<=НадоВернуть,СтрокаТаблицы.СуммаУпр,(СтрокаТаблицы.СуммаУпр/СтрокаТаблицы.Количество)*НадоВернуть),2);
			НоваяЗапись.СуммаПродажи=Окр(-?(СтрокаТаблицы.Количество<=НадоВернуть,СтрокаТаблицы.СуммаПродажи,(СтрокаТаблицы.СуммаПродажи/СтрокаТаблицы.Количество)*НадоВернуть),2);
			НоваяЗапись.СуммаРегл=Окр(-?(СтрокаТаблицы.Количество<=НадоВернуть,СтрокаТаблицы.СуммаРегл,(СтрокаТаблицы.СуммаРегл/СтрокаТаблицы.Количество)*НадоВернуть),2);
			НоваяЗапись.СуммаПродажиРегл=Окр(-?(СтрокаТаблицы.Количество<=НадоВернуть,СтрокаТаблицы.СуммаПродажиРегл,(СтрокаТаблицы.СуммаПродажиРегл/СтрокаТаблицы.Количество)*НадоВернуть),2);
			// Уменьшаем количество которое надо вернуть
			НадоВернуть=НадоВернуть-(-НоваяЗапись.Количество);
			// Удалим ненужные строки
			ТаблицаРеализованныхАвтомобилей.Удалить(СтрокаТаблицы);
			Если НадоВернуть<=0 Тогда Прервать; КонецЕсли;
		КонецЦикла;
		// Проверим чего осталось
		Если НадоВернуть>0 Тогда
			// Не удалось вернуть автомобиль
			дкСообщитьРезультат("Автомобиль """+СокрЛП(СтрокаАвтомобиль.Автомобиль)+""" вернуть не удалось, так как автомобиль не был взят на комиссию !","Важное&Реализованные автомобили:",ДокументОбъект,СтрокаАвтомобиль.Автомобиль);
			ВсеОК=Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Запись движений
	ЗаписыватьДвижения=?(ЗаписыватьДвижения=Неопределено,Истина,ЗаписыватьДвижения);
	Если ВсеОК И ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// Убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	Контрагент=Неопределено;
	ДоговорВзаиморасчетов=Неопределено;
	РезультатЗапросаПоАвтомобилям=Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Формирует движения по регистру приход (реализация комиссионных автомобилей)
//// Возвращаемое значение: Булево. Истина - все хорошо, Ложь - чего-то не так.
Функция Приход() Экспорт
	Сторно=?(Сторно=Неопределено,Ложь,Сторно);
	// проверим на возврат
	Если Сторно Тогда
		Возврат ВозвратАвтомобилей();
	КонецЕсли;
	
	Если РезультатЗапросаПоАвтомобилям=Неопределено Тогда
		// получим таблицу автомобилей
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокАвтомобили.Автомобиль КАК Автомобиль,
		|	СУММА(ДокАвтомобили.Количество) КАК Количество,
		|	СУММА(ДокАвтомобили.СуммаВсего) КАК СуммаПродажи
		|ИЗ
		|	Документ."+ДокументОбъект.Метаданные().Имя+".Автомобили КАК ДокАвтомобили
		|ГДЕ
		|	ДокАвтомобили.Ссылка=&Ссылка
		|СГРУППИРОВАТЬ
		|	ПО ДокАвтомобили.Автомобиль");
		Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
		РезультатЗапросаПоАвтомобилям=Запрос.Выполнить();
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗапросаПоАвтомобилям)=Тип("РезультатЗапроса") Тогда
		ТаблицаАвтомобилей=РезультатЗапросаПоАвтомобилям.Выгрузить();
	Иначе
		ТаблицаАвтомобилей=РезультатЗапросаПоАвтомобилям;
	КонецЕсли;
	
	// получим проданные автомобили
	ТекстЗапроса="ВЫБРАТЬ
	|	ОбъединенныйЗапрос.Автомобиль КАК Автомобиль,
	|	ОбъединенныйЗапрос.Партия КАК Партия,
	|	ОбъединенныйЗапрос.СтатусПартии КАК СтатусПартии,
	|	ОбъединенныйЗапрос.ГТД КАК ГТД,
	|	ОбъединенныйЗапрос.Контрагент КАК Контрагент,
	|	ОбъединенныйЗапрос.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	СУММА(ОбъединенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ОбъединенныйЗапрос.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ОбъединенныйЗапрос.Сумма) КАК Сумма
	|ИЗ(
	|ВЫБРАТЬ
	|	ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
	|	ОстаткиАвтомобилей.Партия КАК Партия,
	|	ОстаткиАвтомобилей.СтатусПартии КАК СтатусПартии,
	|	ОстаткиАвтомобилей.ГТД КАК ГТД,
	|	ОстаткиАвтомобилей.Партия.ДоговорВзаиморасчетов.Владелец КАК Контрагент,
	|	ОстаткиАвтомобилей.Партия.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ОстаткиАвтомобилей.Количество КАК Количество,
	|	ОстаткиАвтомобилей.СуммаУпр КАК СуммаУпр,
	|	ОстаткиАвтомобилей.Сумма    КАК Сумма
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор=&Ссылка
	|	И ОстаткиАвтомобилей.СтатусПартии=&СтатусПартии
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
	|	КомплектацияАвтомобилей.Партия КАК Партия,
	|	КомплектацияАвтомобилей.СтатусПартии КАК СтатусПартии,
	|	КомплектацияАвтомобилей.ГТД КАК ГТД,
	|	КомплектацияАвтомобилей.Партия.ДоговорВзаиморасчетов.Владелец КАК Контрагент,
	|	КомплектацияАвтомобилей.Партия.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	0 КАК Количество,
	|	КомплектацияАвтомобилей.СуммаУпр КАК СуммаУпр,
	|	КомплектацияАвтомобилей.Сумма    КАК Сумма
	|ИЗ
	|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	|ГДЕ
	|	КомплектацияАвтомобилей.Регистратор = &Ссылка
	|	И КомплектацияАвтомобилей.СтатусПартии = &СтатусПартии
	|	И КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции
	|) КАК ОбъединенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ОбъединенныйЗапрос.Автомобиль,
	|	ОбъединенныйЗапрос.Партия,
	|	ОбъединенныйЗапрос.СтатусПартии,
	|	ОбъединенныйЗапрос.ГТД,
	|	ОбъединенныйЗапрос.Контрагент,
	|	ОбъединенныйЗапрос.ДоговорВзаиморасчетов";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	ТаблицаСписанныхАвтомобилей=Запрос.Выполнить().Выгрузить();
	СтруктураОтбора=Новый Структура("Автомобиль");
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	Попытка
		КурсУпр=ДокументОбъект.КурсВалютыУпр;
	Исключение
		КурсУпр=Неопределено;
	КонецПопытки; 
	Если КурсУпр=Неопределено ИЛИ обЗначениеНеЗаполнено(КурсУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ДокументОбъект.Дата);
		КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	
	// идем по выборке автомобилей
	Для Каждого СтрокаАвтомобиля Из ТаблицаАвтомобилей Цикл
		НадоЗафиксировать=СтрокаАвтомобиля.Количество;
		СтруктураОтбора.Автомобиль=СтрокаАвтомобиля.Автомобиль;
		// идем по выборке списанных автомобилей
		МассивНайденныхСтрок=ТаблицаСписанныхАвтомобилей.НайтиСтроки(СтруктураОтбора);
		Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
			ТекСтрока=МассивНайденныхСтрок[Сч];
			
			// зафиксируем продажу комиссионного автомобиля
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=ДокументОбъект.Дата;
			НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
			НоваяЗапись.ДокументПередачи=ТекСтрока.Партия;
			НоваяЗапись.Контрагент=ТекСтрока.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов=ТекСтрока.ДоговорВзаиморасчетов;
			НоваяЗапись.Автомобиль=ТекСтрока.Автомобиль;
			НоваяЗапись.ГТД=ТекСтрока.ГТД;
			НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
			НоваяЗапись.Количество=Мин(ТекСтрока.Количество,НадоЗафиксировать);
			НоваяЗапись.СуммаУпр=Окр(ТекСтрока.СуммаУпр,2);
			НоваяЗапись.СуммаРегл=Окр(ТекСтрока.Сумма,2);
			Если Списание Тогда
				НоваяЗапись.СуммаПродажи=Окр(ТекСтрока.СуммаУпр,2);
				НоваяЗапись.СуммаПродажиРегл=Окр(ТекСтрока.Сумма,2);
			Иначе
				Если ВУпрВалюте Тогда
					НоваяЗапись.СуммаПродажи=Окр((СтрокаАвтомобиля.СуммаПродажи/СтрокаАвтомобиля.Количество)*НоваяЗапись.Количество,2);
					НоваяЗапись.СуммаПродажиРегл=Окр(обПересчет(НоваяЗапись.СуммаПродажи,ВалютаУпр,КурсУпр,ВалютаРегл,ДокументОбъект.Дата),2);
				ИначеЕсли ДокументОбъект.ВалютаДокумента = ВалютаРегл Тогда
					НоваяЗапись.СуммаПродажиРегл=Окр((СтрокаАвтомобиля.СуммаПродажи/СтрокаАвтомобиля.Количество)*НоваяЗапись.Количество,2);
					НоваяЗапись.СуммаПродажи=Окр(обПересчет(НоваяЗапись.СуммаПродажиРегл,ВалютаРегл,ДокументОбъект.Дата,ВалютаУпр,КурсУпр),2);
				Иначе
					НоваяЗапись.СуммаПродажи=Окр(обПересчет((СтрокаАвтомобиля.СуммаПродажи/СтрокаАвтомобиля.Количество)*НоваяЗапись.Количество,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр),2);
					НоваяЗапись.СуммаПродажиРегл=Окр(обПересчет((СтрокаАвтомобиля.СуммаПродажи/СтрокаАвтомобиля.Количество)*НоваяЗапись.Количество,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,ДокументОбъект.Дата),2);
				КонецЕсли; 
			КонецЕсли;
			//<<Павличенко 12.02.19
			//СебестоимостьРавнаПродаже
			//Если НЕ НоваяЗапись.ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж Тогда
			//	НоваяЗапись.СуммаРегл = НоваяЗапись.СуммаПродажиРегл;
			//	НоваяЗапись.СуммаУпр  = НоваяЗапись.СуммаПродажи;
			//КонецЕсли;
			//>>Павличенко 12.02.19
			НадоЗафиксировать=НадоЗафиксировать-НоваяЗапись.Количество;
			// удалим ненужные строки
			ТаблицаСписанныхАвтомобилей.Удалить(ТекСтрока);
		КонецЦикла;
	КонецЦикла;
	
	// все ОК
	ЗаписыватьДвижения=?(ЗаписыватьДвижения=Неопределено,Истина,ЗаписыватьДвижения);
	Если ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	Контрагент=Неопределено;
	ДоговорВзаиморасчетов=Неопределено;
	РезультатЗапросаПоАвтомобилям=Неопределено;
	
	Возврат Истина;
КонецФункции

// Формирует движения по регистру расход (отчет комитенту за реализованные автомобили)
// Возвращаемое значение: Булево. Истина - все хорошо, Ложь - чего-то не так.
Функция Расход() Экспорт
	ВсеОК=Истина;
	
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
	Попытка
		КурсУпр=ДокументОбъект.КурсВалютыУпр;
	Исключение
		КурсУпр=Неопределено;
	КонецПопытки; 
	Если КурсУпр=Неопределено ИЛИ обЗначениеНеЗаполнено(КурсУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ДокументОбъект.Дата);
		КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	// получаем таблицу автомобилей
	Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ОтчетКомитентуАвтомобили.Автомобиль КАК Автомобиль,
		|	ОтчетКомитентуАвтомобили.ДокументПоступления КАК ДокументПередачи,
		|	ОтчетКомитентуАвтомобили.Количество КАК Количество,
		|	ОтчетКомитентуАвтомобили.Вознаграждение КАК Вознаграждение,
		|	ОтчетКомитентуАвтомобили.СуммаВсего КАК Сумма
		|ИЗ
		|	Документ.ОтчетКомитентуЗаАвтомобили.Автомобили КАК ОтчетКомитентуАвтомобили
		|ГДЕ
		|	ОтчетКомитентуАвтомобили.Ссылка=&Ссылка
		|");
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	РезультатЗапросаПоАвтомобилям=Запрос.Выполнить();
	тзАвтомобили=РезультатЗапросаПоАвтомобилям.Выгрузить();
	// получаем таблицу реализованных автомобилей, за которые еще не отчитались
	Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	РеализованныеАвтомобилиОстатки.Контрагент КАК Контрагент,
		|	РеализованныеАвтомобилиОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	РеализованныеАвтомобилиОстатки.Автомобиль КАК Автомобиль,
		|	РеализованныеАвтомобилиОстатки.ДокументПередачи КАК ДокументПередачи,
		|	РеализованныеАвтомобилиОстатки.ГТД КАК ГТД,
		|	РеализованныеАвтомобилиОстатки.КоличествоОстаток КАК Количество,
		|	РеализованныеАвтомобилиОстатки.СуммаУпрОстаток КАК СуммаУпр,
		|	РеализованныеАвтомобилиОстатки.СуммаПродажиОстаток КАК СуммаПродажи,
		|	РеализованныеАвтомобилиОстатки.СуммаРеглОстаток КАК СуммаРегл,
		|	РеализованныеАвтомобилиОстатки.СуммаПродажиРеглОстаток КАК СуммаПродажиРегл
		|ИЗ
		|	РегистрНакопления.РеализованныеАвтомобили.Остатки(&Момент) КАК РеализованныеАвтомобилиОстатки
		|ГДЕ
		|	РеализованныеАвтомобилиОстатки.ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов
		|	И РеализованныеАвтомобилиОстатки.Автомобиль В (&Автомобиль)
		|	И РеализованныеАвтомобилиОстатки.ДокументПередачи В (&ДокументПередачи)
		|");
	Запрос.УстановитьПараметр("Момент",ДокументОбъект.МоментВремени());
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Автомобиль",тзАвтомобили.ВыгрузитьКолонку("Автомобиль"));
	Запрос.УстановитьПараметр("ДокументПередачи",тзАвтомобили.ВыгрузитьКолонку("ДокументПередачи"));
	РезультатЗапросаПоРеализованнымАвтомобилям=Запрос.Выполнить();
	// закрываем реализацию
	СтруктураОтбора=Новый Структура("Контрагент,ДоговорВзаиморасчетов,Автомобиль,ДокументПередачи");
	ТаблицаРеализации=РезультатЗапросаПоРеализованнымАвтомобилям.Выгрузить();
	ВыборкаАвтомобили=РезультатЗапросаПоАвтомобилям.Выбрать();
	СтоимостьРеализованныхАвтомобилей=0;
	Пока ВыборкаАвтомобили.Следующий() Цикл
		// заполним структуру отбора
		СтруктураОтбора.Контрагент=Контрагент; СтруктураОтбора.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		СтруктураОтбора.Автомобиль=ВыборкаАвтомобили.Автомобиль; СтруктураОтбора.ДокументПередачи=ВыборкаАвтомобили.ДокументПередачи;
		// фильтруем выборку
		МассивНайденныхСтрок=ТаблицаРеализации.НайтиСтроки(СтруктураОтбора);
		// теперь списываем
		НадоСписать=ВыборкаАвтомобили.Количество;
		Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
			ТекСтрока=МассивНайденныхСтрок[Сч];
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период=ДокументОбъект.Дата;
			НоваяЗапись.Регистратор=ДокументОбъект.Ссылка;
			НоваяЗапись.ДокументПередачи=ТекСтрока.ДокументПередачи;
			НоваяЗапись.Контрагент=Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
			НоваяЗапись.Автомобиль=ВыборкаАвтомобили.Автомобиль;
			НоваяЗапись.ГТД=ТекСтрока.ГТД;
			НоваяЗапись.Количество=Мин(НадоСписать,ТекСтрока.Количество);
			НоваяЗапись.СуммаУпр=Окр(?(ТекСтрока.СуммаУпр=NULL,0,?(ТекСтрока.Количество<=НадоСписать,ТекСтрока.СуммаУпр,ТекСтрока.СуммаУпр/ТекСтрока.Количество*НадоСписать)),2);
			НоваяЗапись.СуммаРегл=Окр(?(ТекСтрока.СуммаУпр=NULL,0,?(ТекСтрока.Количество<=НадоСписать,ТекСтрока.СуммаРегл,ТекСтрока.СуммаРегл/ТекСтрока.Количество*НадоСписать)),2);
			СтоимостьРеализованныхАвтомобилей=СтоимостьРеализованныхАвтомобилей+НоваяЗапись.СуммаУпр;
			НоваяЗапись.СуммаПродажи=Окр(?(ТекСтрока.СуммаПродажи=NULL,0,?(ТекСтрока.Количество<=НадоСписать,ТекСтрока.СуммаПродажи,ТекСтрока.СуммаПродажи/ТекСтрока.Количество*НадоСписать)),2);
			НоваяЗапись.СуммаПродажиРегл=Окр(?(ТекСтрока.СуммаПродажиРегл=NULL,0,?(ТекСтрока.Количество<=НадоСписать,ТекСтрока.СуммаПродажиРегл,ТекСтрока.СуммаПродажиРегл/ТекСтрока.Количество*НадоСписать)),2);
			НоваяЗапись.Вознаграждение=обПересчет(ВыборкаАвтомобили.Вознаграждение/ВыборкаАвтомобили.Количество*НадоСписать,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр);
			НоваяЗапись.ХозОперация=ДокументОбъект.ХозОперация;
			// уменьшаем количество которое надо списать
			НадоСписать=НадоСписать-НоваяЗапись.Количество;
			// удалим ненужные строки
			ТаблицаРеализации.Удалить(ТекСтрока);
			Если НадоСписать<=0 Тогда Прервать; КонецЕсли;
		КонецЦикла;
		// если после списания остались автомобили, то значит их мы на комиссию не брали. ругаемся
		Если НадоСписать>0 Тогда
			дкСообщитьРезультат("Автомобиль """+СокрЛП(ВыборкаАвтомобили.Автомобиль)+""" в количестве "+СокрЛП(ВыборкаАвтомобили.Количество)+" не брался на комиссию.","Важное&Реализованные автомобили:",ДокументОбъект,ВыборкаАвтомобили.Автомобиль);
			ВсеОК=Ложь;
		КонецЕсли;
	КонецЦикла;
	
	//Все ОК
	ЗаписыватьДвижения=?(ЗаписыватьДвижения=Неопределено,Истина,ЗаписыватьДвижения);
	Если ЗаписыватьДвижения Тогда Записать(); КонецЕсли;
	
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	Контрагент=Неопределено;
	ДоговорВзаиморасчетов=Неопределено;
	РезультатЗапросаПоАвтомобилям=Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

// сбросим переменные
РезультатЗапросаПоАвтомобилям=Неопределено;
Списание=Ложь;
ВУпрВалюте=Ложь;

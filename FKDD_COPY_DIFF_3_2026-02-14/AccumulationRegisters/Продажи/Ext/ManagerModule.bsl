// Модуль менеджера регистра накоплений "Продажи"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИБ

// Обработчик обновления на версию 5.1.41.05
//
// Перебираются все записи регистра Продажи с отбором по регистратору. В качестве регистратора
// выступают документы "Реализация товаров", "Корректировка реализации товаров", "Корректировка движений" и "Перенос истории".
// Затем устанавливается значение Неопределено для реквизита "СкладКомпании", если выполняются условия:
//  * Значение Вида номенклатуры у реквизита "Номенклатура" равно Услуга;
//  * Заполнено значение для реквизита "СкладКомпани" и Тип значения равен Типу "СправочникСсылка.СкладыКомпании"
//
Процедура ОчиститьСкладыДляЗаписейУслуг() Экспорт
	
	ДокументыТребующиеОбработкуДвижений = ДокументыТребующиеОчисткуСкладаДляДвиженийУслуг();
		
	Если ДокументыТребующиеОбработкуДвижений.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВидНоменклатурыУслуга = Перечисления.ВидыНоменклатуры.Услуга;
	ТипСкладыКомпании = Тип("СправочникСсылка.СкладыКомпании");
	
	// Обойдем регистраторы
	Для Каждого ДокументТребующийОбработки Из ДокументыТребующиеОбработкуДвижений Цикл
	
		ОчиститьСкладыДляУслуг(ДокументТребующийОбработки, ВидНоменклатурыУслуга, ТипСкладыКомпании);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновлений

Функция ДокументыТребующиеОчисткуСкладаДляДвиженийУслуг()
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Продажи.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	(Продажи.Регистратор ССЫЛКА Документ.РеализацияТоваров
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.КорректировкаРеализации
		|			ИЛИ Продажи.Регистратор ССЫЛКА Документ.Корректировка)
		|	И Продажи.СкладКомпании ССЫЛКА Справочник.СкладыКомпании
		|	И Продажи.СкладКомпании <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|	И Продажи.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|	И Продажи.Активность = ИСТИНА";
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Новый Массив;
		
	КонецЕсли;
	
	Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	Возврат Результат;
	
КонецФункции

Процедура ОчиститьСкладыДляУслуг(Регистратор, ВидНоменклатурыУслуга, ТипСкладыКомпании)
	
	// Выполним изменение набора записей для регистратора
	НаборЗаписей = РегистрыНакопления.Продажи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Значение = Регистратор;
	НаборЗаписей.Прочитать();
	
    МассивНоменклатуры = НаборЗаписей.ВыгрузитьКолонку("Номенклатура");
	ЗапросВидовНоменклатуры = Новый Запрос();
	ЗапросВидовНоменклатуры.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	                                |	Номенклатура.Ссылка КАК Номенклатура,
	                                |	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	                                |ИЗ
	                                |	Справочник.Номенклатура КАК Номенклатура
	                                |ГДЕ
	                                |	Номенклатура.Ссылка В(&Ссылка)";
	ЗапросВидовНоменклатуры.УстановитьПараметр("Ссылка", МассивНоменклатуры);
	ТаблицаВидовНоменклатуры = ЗапросВидовНоменклатуры.Выполнить().Выгрузить();
	
	Для Каждого Запись Из НаборЗаписей Цикл
		
		СтрокаВидаНоменклатуры = ТаблицаВидовНоменклатуры.Найти(Запись.Номенклатура, "Номенклатура");
		Если СтрокаВидаНоменклатуры.ВидНоменклатуры <> ВидНоменклатурыУслуга Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверяем на заполненность склада
		Если ЗначениеЗаполнено(Запись.СкладКомпании)
			И ТипЗнч(Запись.СкладКомпании) = ТипСкладыКомпании Тогда
			
			Запись.СкладКомпании = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

// Модуль набора записей регистра "ВзаиморасчетыКомпании"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Контрагент Экспорт;				// Контрагент, взаиморасчеты с которым мы фиксируем
Перем ДоговорВзаиморасчетов Экспорт;	// договор взаиморасчетов с контрагентом
Перем Сделка Экспорт;					// Ссылка на документ-сделку, по которой надо зафиксировать/списать долги. 
									 	// Если сделка неопределена, то долг "вешаются" на документ-регистратор
Перем ВзаиморасчетыСПокупателем Экспорт;// Документ
Перем АвтоЗакрытиеСделок Экспорт;		// Булево. Истина - Сначала будут закрыты все долги/авансы контрагента по указанному договору в порядке ФИФО.
										// Если Неопределено, то берется из договора
Перем СписатьНераспределеннуюСуммуПоСделке Экспорт; // Булево. Указывает на способ списания нераспределенной суммы, после погашения долгов по сделке и автораспределения.
										//Истина - нераспределенная сумма будет записана на сделку, иначе на документ регистратор
Перем Сумма Экспорт;					// Сумма взаиморасчетов
Перем Валюта Экспорт;					// если неопределено, то сумма в валюте документа
Перем КурсВзаиморасчетов Экспорт;		// Курс валюты взаиморасчетов, если не ноль, то в расчетах используется он
Перем ДокументОбъект Экспорт;			// Исходный документ ( подлежит обнулению в любой функции)
Перем СделкиКонтрагента Экспорт;		// Сделки контрагента
Перем ПриходРасход Экспорт;				// действие (приход/расход)
Перем СуммаДоходаРасходаСуммовыхРазниц Экспорт;	// доходы/расходы от суммовых разниц
Перем ШапкаДокумента Экспорт; 			// Выборка или строка таблицы значений, в которой содержаться необходимые данные о шапке документа
Перем РежимПроведения Экспорт;		    // Режим проведения документа оперативный/неоперативный
Перем РежимКорректировки Экспорт;       // Включение режима корректировки

Перем МоментВремени Экспорт; // Момент времени документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// возвращает параметр для использования в функции обПересчет в качестве курса валюты взаиморасчетов
Функция ПолучитьПараметрКурсВзаиморасчетов(ВалютаДоговора, КурсДокумента)
	
	Если ЗначениеЗаполнено(КурсВзаиморасчетов) Тогда
		// если заполнена экспортная переменная КурсВзаиморасчетов то она и определяет курс
		ПараметрКурсРасчетов = КурсВзаиморасчетов;
	Иначе
		Если дкДокументЕстьРеквизитШапки("КурсВалютыВзаиморасчетов", ДокументОбъект.Метаданные().Имя) Тогда
			Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыВзаиморасчетов) Тогда
				// если курс не заполнен, возвращаем дату документа
				Если Валюта = ВалютаДоговора Тогда
					ПараметрКурсРасчетов = КурсДокумента;
				Иначе
					ПараметрКурсРасчетов = ШапкаДокумента.Дата;
				КонецЕсли;
			Иначе
				ПараметрКурсРасчетов = ШапкаДокумента.КурсВалютыВзаиморасчетов;
			КонецЕсли;
		Иначе
			// если у документа нет реквизита "КурсВалютыВзаиморасчетов" возвращаем дату документа
			Если Валюта = ВалютаДоговора Тогда
				ПараметрКурсРасчетов = КурсДокумента;
			Иначе
				ПараметрКурсРасчетов = ШапкаДокумента.Дата;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ПараметрКурсРасчетов;
КонецФункции

// проверка корректности заполнения обязательных данных
Функция ДанныеКорректны()
	// заполненность
	Если обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) ИЛИ обЗначениеНеЗаполнено(Контрагент) Тогда
		// не указан ни договор ни контрагент. так нельзя
		дкСообщитьРезультат("Не достаточно параметров для списания долга ! Не указаны договор или контрагент.","Важное&Взаиморасчеты компании:",ДокументОбъект);
		Возврат Ложь;
	КонецЕсли;
	// соответствие договора контрагенту
	Если ДоговорВзаиморасчетов.Владелец<>Контрагент Тогда
		дкСообщитьРезультат("Договор взаиморасчетов <"+СокрЛП(ДоговорВзаиморасчетов)+"> не соответствует контрагенту <"+СокрЛП(Контрагент)+">!","Важное&Взаиморасчеты компании:",ДокументОбъект);
		Возврат Ложь;
	КонецЕсли;
	// соответствие сделки контрагенту
	Если СделкиКонтрагента<>Неопределено И СделкиКонтрагента Тогда
		Попытка
			Если Сделка.Контрагент<>Контрагент Тогда
				Сделка=Неопределено;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли; 
	
	Возврат Истина;
КонецФункции

// возвращает таблицу сделок по которым есть долги
// ТолькоОстаткиПоСделке - Булево. Истина - получаем только долги по указанной сделке
Функция ПолучитьНеЗакрытыеСделки(ТолькоОстаткиПоСделке = Ложь, Приход = Неопределено)
	
	// Получим фильтр на сделки
	Если ТолькоОстаткиПоСделке Тогда
		СтрОтбор       = "(Сделка=&Сделка)";
		СтрОтборФизТаб = "";
	ИначеЕсли ЗначениеЗаполнено(Сделка) Тогда
		СтрОтбор = "(Сделка<>&Сделка)";
		СтрОтборФизТаб = "ГДЕ
		|	ВзаиморасчетыКомпании.Сделка<>&Сделка";
	Иначе
		СтрОтбор = "";
		СтрОтборФизТаб = "";
	КонецЕсли;
	
	Если СделкиКонтрагента = Неопределено ИЛИ СделкиКонтрагента Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			СтрОтбор = СтрОтбор + ?(ПустаяСтрока(СтрОтбор), "", " И ") + "Контрагент = &Контрагент";
			СтрОтборФизТаб = СтрОтборФизТаб + ?(ПустаяСтрока(СтрОтборФизТаб), "ГДЕ ", " И ") + "
			|	ВзаиморасчетыКомпании.Контрагент = &Контрагент";
		КонецЕсли;
		Если ЗначениеЗаполнено(ДоговорВзаиморасчетов) Тогда
			СтрОтбор = СтрОтбор + ?(ПустаяСтрока(СтрОтбор), "", " И ") + "ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов";
			СтрОтборФизТаб = СтрОтборФизТаб + ?(ПустаяСтрока(СтрОтборФизТаб), "ГДЕ ", " И ") + "
			|	ВзаиморасчетыКомпании.ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов";
		КонецЕсли;
	КонецЕсли; 

	// Уустановим, есть ли у сделки документ основание
	Если ТолькоОстаткиПоСделке ИЛИ ПустаяСтрока(СтрОтборФизТаб) Тогда
		Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
		|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК Сумма
		// Подлежит исправлению YABS
		// Сделки будем закрывать исходя из суммы, согласно валюты договора
		//|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
		//|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток, 0) КАК СуммаБаз
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Момент, "+СтрОтбор+") КАК ВзаиморасчетыКомпанииОстатки
		|ГДЕ
		|	ВзаиморасчетыКомпанииОстатки.СуммаОстаток"+?(Приход,"<",">")+"0
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки" + ?(ТолькоОстаткиПоСделке, "", "
		|УПОРЯДОЧИТЬ ПО
		|	Сделка.Дата Возр,
		|	Сделка.МоментВремени Возр") + "
		|");
	Иначе
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ВзаиморасчетыКомпании.Сделка КАК Сделка,
		|	МИНИМУМ(ВзаиморасчетыКомпании.Период) КАК Период
		|ПОМЕСТИТЬ
		|	ТаблицаСортировки
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
		|"+СтрОтборФизТаб+"
		|СГРУППИРОВАТЬ ПО
		|	ВзаиморасчетыКомпании.Сделка
		|;
		|
		|ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
		|	ТаблицаСортировки.Период КАК Период,
		|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК Сумма
		// Подлежит исправлению YABS
		// Сделки будем закрывать исходя из суммы, согласно валюты договора
		//|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
		//|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток, 0) КАК СуммаБаз
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Момент, "+СтрОтбор+") КАК ВзаиморасчетыКомпанииОстатки
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаСортировки КАК ТаблицаСортировки
		|ПО
		|	ВзаиморасчетыКомпанииОстатки.Сделка = ТаблицаСортировки.Сделка
		|ГДЕ
		|	ВзаиморасчетыКомпанииОстатки.СуммаОстаток"+?(Приход,"<",">")+"0
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки
		|УПОРЯДОЧИТЬ ПО
		|	Период Возр,
		|	Сделка Возр
		|");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Контрагент",            Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("Сделка",                Сделка);
	Запрос.УстановитьПараметр("Момент",                ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени));

	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ВзаиморасчетыКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	
	Если СделкиКонтрагента=Неопределено ИЛИ СделкиКонтрагента Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ЗначенияБлокировки.Вставить("Контрагент",            Контрагент);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ЗначенияБлокировки.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		КонецЕсли;
	КонецЕсли;
	Если ТолькоОстаткиПоСделке Тогда
		ЗначенияБлокировки.Вставить("Сделка", Сделка);
	КонецЕсли;
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Формирует движения по приходу (уменьшение нашего долга / увеличение долга контрагента)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция Приход() Экспорт
	
	Если ШапкаДокумента = Неопределено Тогда
		ШапкаДокумента = ДокументОбъект;
		Если МоментВремени = Неопределено Тогда
			МоментВремени = ШапкаДокумента.МоментВремени();
		КонецЕсли;
	Иначе
		Попытка
			Если МоментВремени = Неопределено Тогда
				МоментВремени = ШапкаДокумента.МоментВремени;
			КонецЕсли;
		Исключение
			Если МоментВремени = Неопределено Тогда
				МоментВремени = ДокументОбъект.МоментВремени();
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	// получим ускоряющие переменные
	ВалютаДоговора = ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	ВалютаУпр      = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаБаз      = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаБаз, ШапкаДокумента.Дата);
	КурсБаз        = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Если Валюта = Неопределено Тогда 
		Валюта = ШапкаДокумента.ВалютаДокумента; Курс=ШапкаДокумента.КурсДокумента;
	Иначе
		СтруктураКурса = обКурсДляВалюты(Валюта,ШапкаДокумента.Дата);
		Курс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	
	//Курс - это курс документа
	ПараметрКурсВзаиморасчетов = ПолучитьПараметрКурсВзаиморасчетов(ВалютаДоговора,Курс);
	
	// проверки
	Если НЕ ДанныеКорректны() Тогда
		МоментВремени     = Неопределено;
		// убиваем циклическую ссылку
		ДокументОбъект    = Неопределено;
		ШапкаДокумента    = Неопределено;
		СделкиКонтрагента = Неопределено;
		ПриходРасход      = Неопределено;
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Истина;
	Если ЗначениеЗаполнено(Сделка) И (НЕ ВзаиморасчетыСПокупателем = Истина) Тогда
		АвтоЗакрытиеСделок = Ложь;
	ИначеЕсли АвтоЗакрытиеСделок = Неопределено Тогда
		Попытка
			АвтоЗакрытиеСделок = ДоговорВзаиморасчетов.АвтоЗакрытиеСделок;
		Исключение
			АвтоЗакрытиеСделок = обПраво("АвтоЗакрытиеСделок", ДокументОбъект.Права, , ДокументОбъект);
		КонецПопытки;
	КонецЕсли;
	
	// Для документов отгрузки или если указана максимальная сумма кредита нужно рассчитать остатки взаиморасчетов
	СуммаДолга = 0;
	Если (ШапкаДокумента.ХозОперация.Родитель=Справочники.ХозОперации.ОтгрузкаТМЦ) ИЛИ 
		(ШапкаДокумента.ХозОперация.Родитель=Справочники.ХозОперации.ОтгрузкаАвтомобилей) ИЛИ 
		(обПраво("ПроверкаМаксимальногоПревышенияКредитаКонтрагента", ДокументОбъект.Права,,ДокументОбъект)
		И НЕ ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита) Тогда
		// получим остатки взаиморасчетов по договору
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Момент, 
		|							Контрагент = &Контрагент И ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов) КАК ВзаиморасчетыКомпанииОстатки
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки
		|");
		Если (РежимПроведения=НЕОПРЕДЕЛЕНО) ИЛИ (РежимПроведения=РежимПроведенияДокумента.Неоперативный) Тогда
			Если ТипЗнч(МоментВремени)=Тип("Граница") Тогда
				НаМомент = Новый Граница(МоментВремени.Значение,ВидГраницы.Исключая);
			Иначе
				НаМомент = Новый Граница(МоментВремени,ВидГраницы.Исключая);
			КонецЕсли;
		Иначе
			НаМомент = НЕОПРЕДЕЛЕНО;
		КонецЕсли;
		
		// Наложим блокировку на считываемые данные
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ВзаиморасчетыКомпании");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период",                Новый Диапазон(, ШапкаДокумента.Дата));
		ЗначенияБлокировки.Вставить("Контрагент",            Контрагент);
		ЗначенияБлокировки.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов); 
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
		
		Запрос.УстановитьПараметр("Момент",                НаМомент);
		Запрос.УстановитьПараметр("Контрагент",            Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			// Долг пересчитаем в валюту договора взиморасчетов (так как контроль будет в этой валюте)
			СуммаДолга = Выборка.СуммаОстаток;
		КонецЕсли;
	КонецЕсли;
	
	// Пересчитаем сумму документа в валюту договора взаиморасчетов
	СуммаСделки = обПересчет(Сумма, Валюта, Курс, ВалютаДоговора, ПараметрКурсВзаиморасчетов);
	
	// Для документов отгрузки проверим нет ли долгов по взаиморасчетам
	Если (ШапкаДокумента.ХозОперация.Родитель = Справочники.ХозОперации.ОтгрузкаТМЦ) ИЛИ 
		(ШапкаДокумента.ХозОперация.Родитель = Справочники.ХозОперации.ОтгрузкаАвтомобилей) Тогда
		// Проверим есть ли долг по договору
		Если СуммаДолга+СуммаСделки > 0 Тогда
			// Проверим есть ли право на отгрузку при наличии задолженности
			РазрешитьОтгрузкуВДолг = обПраво("РазрешитьОтгрузкуВДолг",ДокументОбъект.Права,,ДокументОбъект);
			Если РазрешитьОтгрузкуВДолг И СуммаДолга > 0 Тогда
				Если обПраво("ВыводитьСообщенияПроверкиПравДоступа",ДокументОбъект.Права,,ДокументОбъект) Тогда
					дкСообщитьРезультат("Зафиксирована отгрузка контрагенту при наличии задолженности по договору взаиморасчетов.","Информация&Взаиморасчеты компании:",ДокументОбъект);
				КонецЕсли;
			ИначеЕсли НЕ РазрешитьОтгрузкуВДолг Тогда
				СообщениеОбОшибке = ?(СуммаДолга <= 0, "Нет прав производить отгрузку без предварительной оплаты.",
									"Нет прав производить отгрузку при наличии задолженности по договору взаиморасчетов.");
				дкСообщитьРезультат(СообщениеОбОшибке,"Внимание&Взаиморасчеты компании:", ДокументОбъект);
				Результат=Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; // Документ отгрузки ТМЦ
	// Проверим на превышение максимальной суммы кредита по договору взаиморасчетов
	//<<Павличенко 09.09.2020
	//Если обПраво("ПроверкаМаксимальногоПревышенияКредитаКонтрагента",ДокументОбъект.Права,,ДокументОбъект) И НЕ ДокументОбъект.АвтоматСоздание Тогда //bizteh (проверить АвтоматСоздание)
	Если обПраво("ПроверкаМаксимальногоПревышенияКредитаКонтрагента",ДокументОбъект.Права,,ДокументОбъект) Тогда
	//>>Павличенко 09.09.2020
	// Проверим превысим ли мы максимальный кредит при проведении этого документа
		Если НЕ ДоговорВзаиморасчетов.ОтменаКонтроляСуммыКредита И ((СуммаДолга+СуммаСделки) > ДоговорВзаиморасчетов.МаксимальныйКредит) Тогда
			// Проверим есть ли право на превышение максимального кредита
			Если обПраво("РазрешитьПревышениеМаксимальногоКредитаКонтрагента",ДокументОбъект.Права,,ДокументОбъект) Тогда
				Если обПраво("ВыводитьСообщенияПроверкиПравДоступа",ДокументОбъект.Права,,ДокументОбъект) Тогда
					дкСообщитьРезультат("Зафиксировано превышение максимальной суммы кредита по договору взаиморасчетов.","Информация&Взаиморасчеты компании:",ДокументОбъект);
				КонецЕсли;
			Иначе
				дкСообщитьРезультат("Нет прав на превышение максимальной суммы кредита по договору взаиморасчетов.","Внимание&Взаиморасчеты компании:",ДокументОбъект);
				Результат=Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Подлежит исправлению YABS
	// Сделки будем закрывать исходя из суммы, согласно валюты договора
	Если Валюта = Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
		ИмяРесурсаСуммы = "СуммаУпр";
	ИначеЕсли Валюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ИмяРесурсаСуммы = "СуммаБаз";
	Иначе
		ИмяРесурсаСуммы = "Сумма";
	КонецЕсли; 
	
	// Суммы
	ПолнаяРаспределяемаяСумма = Сумма;
	ПолнаяСуммаСделки         = 0;
	
	ПогашениеКредиторскойЗадолженности = Перечисления.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности;
	
	// 1. Если указана сделка, то сначала закроем ее долги/авансы
	Если ЗначениеЗаполнено(Сделка) И Метаданные.РегистрыНакопления.ВзаиморасчетыКомпании.Измерения.Сделка.Тип.СодержитТип(ТипЗнч(Сделка)) Тогда
		РезультатДолгиПоСделке = ПолучитьНеЗакрытыеСделки(Истина, ?(ПриходРасход=Неопределено, Истина, ПриходРасход));
		Выборка = РезультатДолгиПоСделке.Выбрать();
		// Пройдемся по выборке
		Пока Выборка.Следующий() Цикл
			ВалютаОстаткаСуммыПлатежа = ВалютаДоговора;
			КурсОстаткаСуммыПлатежа   = ПараметрКурсВзаиморасчетов;
			
			ОстатокСуммыПлатежа = обПересчет(Сумма, Валюта, Курс, ВалютаОстаткаСуммыПлатежа, КурсОстаткаСуммыПлатежа);
			// Определим по какой сумме считаем все остальное для избежания двойного пересчета и как следствия ошибки округления
			Если Выборка.Сумма*?(Выборка.Сумма<0,-1,1)<ОстатокСуммыПлатежа Тогда
				СуммаДляПересчета = Окр(Выборка.Сумма*?(Выборка.Сумма<0,-1,1),2);
			Иначе
				ВалютаОстаткаСуммыПлатежа=Валюта;
				КурсОстаткаСуммыПлатежа=Курс;
				СуммаДляПересчета=Сумма;
			КонецЕсли;
			Если СуммаДляПересчета = 0 Тогда
				Продолжить;
			КонецЕсли; 
			// Закрываем
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения           = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период                = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор           = ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент            = Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка                = Выборка.Сделка;
			НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
			НоваяЗапись.ВидОперации           = ПогашениеКредиторскойЗадолженности;
			НоваяЗапись.Сумма                 = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаДоговора,ПараметрКурсВзаиморасчетов),2);
			НоваяЗапись.СуммаУпр              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр)),2);
			НоваяЗапись.СуммаБаз              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаБаз,КурсБаз),2);
			// Подлежит исправлению YABS
			// Сделки будем закрывать исходя из суммы, согласно валюты договора
			Если ИмяРесурсаСуммы="СуммаУпр" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр),ШапкаДокумента.Дата,ШапкаДокумента.КурсВалютыУпр),Валюта,Курс);
			ИначеЕсли ИмяРесурсаСуммы="СуммаБаз" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаБаз,КурсБаз,Валюта,Курс);
			Иначе
				СуммаЗакрылиДок = обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,Валюта,Курс);
			КонецЕсли;
			
			// Уменьшим сумму
			Сумма = Сумма - СуммаЗакрылиДок;
			// Подсчитаем полную сумму
			ПолнаяСуммаСделки = ПолнаяСуммаСделки + СуммаЗакрылиДок;
			
			//Если НеучтеннаяСуммаСделки<>Неопределено Тогда
			//	НеучтеннаяСуммаСделки = НеучтеннаяСуммаСделки - НоваяЗапись.Сумма;
			//КонецЕсли;
			
			// Если сделка закрыта, спишем курсовую разницу по ресурсу СуммаБаз
			ОстатокПоВзаиморасчетам = НоваяЗапись.Сумма-Выборка.Сумма*?(Выборка.Сумма<0,-1,1);
			Если (?(ОстатокПоВзаиморасчетам<0, -ОстатокПоВзаиморасчетам, ОстатокПоВзаиморасчетам))<0.01 Тогда
				СписаниеКурсовойРазницыБаз(НоваяЗапись.ДоговорВзаиморасчетов, НоваяЗапись.Контрагент, НоваяЗапись.Сделка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// 2. Если после закрытия долгов по указанной сделке осталась еще не распределенная сумма,
	//    и надо произвести автоматическое закрытие сделок, то закроем остальные сделки в порядке ФИФО
	Если АвтоЗакрытиеСделок И Сумма>=0.01 Тогда
		РезультатНеЗакрытыеСделки = ПолучитьНеЗакрытыеСделки(,?(ПриходРасход=Неопределено,Истина,ПриходРасход));
		Выборка = РезультатНеЗакрытыеСделки.Выбрать();
		// Пройдемся по выборке
		Пока Выборка.Следующий() Цикл
			// Надо ли закрывать данную сделку ?
			ВалютаОстаткаСуммыПлатежа=ВалютаДоговора;
			КурсОстаткаСуммыПлатежа=ПараметрКурсВзаиморасчетов;
			
			ОстатокСуммыПлатежа = обПересчет(Сумма,Валюта,Курс,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа);
			// Определим по какой сумме считаем все остальное для избежания двойного пересчета и как следствия ошибки округления
			Если Выборка.Сумма*?(Выборка.Сумма<0,-1,1)<ОстатокСуммыПлатежа Тогда
				СуммаДляПересчета = Окр(Выборка.Сумма*?(Выборка.Сумма<0,-1,1),2);
			Иначе
				ВалютаОстаткаСуммыПлатежа=Валюта;
				КурсОстаткаСуммыПлатежа=Курс;
				СуммаДляПересчета=Сумма;
			КонецЕсли;
			Если СуммаДляПересчета = 0 Тогда
				Продолжить;
			КонецЕсли;
			// Зачтем авансы и долги
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения           = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период                = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор           = ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент            = Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка                = Выборка.Сделка;
			НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
			НоваяЗапись.ВидОперации           = ПогашениеКредиторскойЗадолженности;
			НоваяЗапись.Сумма                 = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаДоговора,ПараметрКурсВзаиморасчетов),2);
			НоваяЗапись.СуммаУпр              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр)),2);
			НоваяЗапись.СуммаБаз              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаБаз,КурсБаз),2);
			// Сделки будем закрывать исходя из суммы, согласно валюты договора
			Если ИмяРесурсаСуммы = "СуммаУпр" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр),ШапкаДокумента.Дата,ШапкаДокумента.КурсВалютыУпр),Валюта,Курс);
			ИначеЕсли ИмяРесурсаСуммы = "СуммаБаз" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаБаз,КурсБаз,Валюта,Курс);
			Иначе
				СуммаЗакрылиДок = обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,Валюта,Курс);
			КонецЕсли;
			
			// Уменьшим сумму
			Сумма = Сумма-СуммаЗакрылиДок;
			// подсчитаем полную сумму
			ПолнаяСуммаСделки = ПолнаяСуммаСделки + СуммаЗакрылиДок;
			
			// Если сделка закрыта, спишем курсовую разницу по ресурсу СуммаБаз
			ОстатокПоВзаиморасчетам = НоваяЗапись.Сумма-Выборка.Сумма*?(Выборка.Сумма<0,-1,1);
			Если (?(ОстатокПоВзаиморасчетам<0,-ОстатокПоВзаиморасчетам,ОстатокПоВзаиморасчетам))<0.01 Тогда
				СписаниеКурсовойРазницыБаз(НоваяЗапись.ДоговорВзаиморасчетов,НоваяЗапись.Контрагент,НоваяЗапись.Сделка);
			КонецЕсли;
			// Если распределили всю сумму, то выходим
			Если Сумма<0.01 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// 3. Если осталась нераспределенная сумма...
	СуммаОстаток    = обПересчет(Сумма, Валюта, Курс, ВалютаДоговора, ПараметрКурсВзаиморасчетов);
	СуммаОстатокАбс = СуммаОстаток;
	Если СуммаОстаток < 0 Тогда
		СуммаОстатокАбс = -СуммаОстаток;
	КонецЕсли;
	Если СуммаОстатокАбс>=0.01 Тогда
		//СписатьНераспределеннуюСуммуПоСделке = ?(СписатьНераспределеннуюСуммуПоСделке = Неопределено, Ложь, СписатьНераспределеннуюСуммуПоСделке);
		//Если (НЕ обЗначениеНеЗаполнено(Сделка)) И (НеучтеннаяСуммаСделки<>Неопределено ИЛИ СписатьНераспределеннуюСуммуПоСделке = Истина) Тогда
		Если ЗначениеЗаполнено(Сделка) И ((НЕ ВзаиморасчетыСПокупателем = Истина) ИЛИ РежимКорректировки) Тогда
			СделкаПереплаты = Сделка;
		Иначе
			СделкаПереплаты = ШапкаДокумента.Ссылка;
		КонецЕсли;
		Если СделкаПереплаты<>ШапкаДокумента.Ссылка И СуммаОстатокАбс>0 И НЕ РежимКорректировки Тогда
			//Проверили, что закрываемая сделка не есть сам документ движения
			//Посмотрим, запрещена ли по данной сделке переплата. и если запрещена, то падаем
			Попытка
				Если ПланыВидовХарактеристик.ТипыСделок[СделкаПереплаты.Метаданные().Имя].ЗапретитьПереплатуПоСделкам Тогда
					// Переплата запрещена.
					дкСообщитьРезультат("Распределяемая сумма "+Формат(ПолнаяРаспределяемаяСумма,"ЧДЦ=2; ЧН=0,00")+" превышает незакрытую сумму сделки "+Формат(ПолнаяСуммаСделки,"ЧЦ=15; ЧДЦ=2; ЧН=0,00")+" на "+Формат(Сумма,"ЧЦ=15; ЧДЦ=2; ЧН=0,00")+" !","Внимание&Взаиморасчеты компании:",ДокументОбъект);
					Результат=Ложь;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		//Если Результат И (НЕ обЗначениеНеЗаполнено(Сделка)) И СделкаПереплаты<>Сделка Тогда
		//	дкСообщитьРезультат("Сумма переплаты "+Формат(СуммаОстатокАбс,"ЧДЦ=2; ЧН=0,00")+" "+СокрЛП(ДоговорВзаиморасчетов.ВалютаВзаиморасчетов)+" распределена на сделку <"+СокрЛП(СделкаПереплаты)+">!","Внимание&Взаиморасчеты компании:",ДокументОбъект);
		//КонецЕсли; 
		
		// Вешаем оставшуюся сумму либо на сделку (если она указана), либо на сам документ
		НоваяЗапись = Добавить();
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период                = ШапкаДокумента.Дата;
		НоваяЗапись.Регистратор           = ШапкаДокумента.Ссылка;
		НоваяЗапись.Контрагент            = Контрагент;
		НоваяЗапись.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
		НоваяЗапись.Сделка                = СделкаПереплаты;
		НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
		НоваяЗапись.ВидОперации           = Перечисления.ВидыОперацийВзаиморасчетов.НачислениеДебиторскойЗадолженности;
		НоваяЗапись.Сумма				  = Окр(СуммаОстаток,2);
		НоваяЗапись.СуммаУпр			  = Окр(обПересчет(Сумма,Валюта,Курс,ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр)),2);
		НоваяЗапись.СуммаБаз			  = Окр(обПересчет(Сумма,Валюта,Курс,ВалютаБаз,КурсБаз),2);
	КонецЕсли;
	
	// Если списываем, суммовые разницы первичным документом, то спишем их
	Если обПраво("РежимСписанияСуммовыхРазниц",ДокументОбъект.Права,,ДокументОбъект)=Перечисления.РежимыРегламентныхОпераций.ПриПроведенииДокумента Тогда
		СписаниеСуммовыхРазниц();
	КонецЕсли;
	
	// Убиваем циклическую ссылку
	МоментВремени                        = Неопределено;
	ДокументОбъект                       = Неопределено;
	АвтоЗакрытиеСделок                   = Неопределено;
	СписатьНераспределеннуюСуммуПоСделке = Неопределено;
	ШапкаДокумента                       = Неопределено;
	Валюта                               = Неопределено;
	СделкиКонтрагента                    = Неопределено;
	ПриходРасход                         = Неопределено;
	
	Возврат Результат;
КонецФункции

// Формирует движения по расходу (увеличение нашего долга)
// Возвращаемое значение: Булево. Истина - все ОК, Ложь - чего-то не так.
Функция Расход() Экспорт
	
	Если ШапкаДокумента = Неопределено Тогда
		ШапкаДокумента = ДокументОбъект;
		Если МоментВремени = Неопределено Тогда
			МоментВремени = ШапкаДокумента.МоментВремени();
		КонецЕсли;
	Иначе
		Попытка
			Если МоментВремени = Неопределено Тогда
				МоментВремени = ШапкаДокумента.МоментВремени;
			КонецЕсли;
		Исключение
			Если МоментВремени = Неопределено Тогда
				МоментВремени = ДокументОбъект.МоментВремени();
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	// получим ускоряющие переменные	
	ВалютаДоговора = ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаБаз = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаБаз, ШапкаДокумента.Дата);
	КурсБаз = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Если Валюта = Неопределено Тогда 
		Валюта = ШапкаДокумента.ВалютаДокумента;
		Курс   = ШапкаДокумента.КурсДокумента;
	Иначе
		СтруктураКурса = обКурсДляВалюты(Валюта, ШапкаДокумента.Дата);
		Курс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	
	//Курс - это курс документа
	ПараметрКурсВзаиморасчетов = ПолучитьПараметрКурсВзаиморасчетов(ВалютаДоговора,Курс);
	
	//Если (ЗначениеЗаполнено(Сделка)) И (НЕ ВзаиморасчетыСПокупателем = Неопределено) Тогда
	//	НеучтеннаяСуммаСделки = орПолучитьОстатокПоСделке2(Сделка,МоментВремени,ВзаиморасчетыСПокупателем,ДоговорВзаиморасчетов,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ПараметрКурсВзаиморасчетов,,Истина);
	//Иначе
	//	НеучтеннаяСуммаСделки = Неопределено;
	//КонецЕсли;
	
	// проверки
	Если НЕ ДанныеКорректны() Тогда 
		// убиваем циклическую ссылку
		МоментВремени     = Неопределено;
		ДокументОбъект    = Неопределено;
		ШапкаДокумента    = Неопределено;
		СделкиКонтрагента = Неопределено;
		ПриходРасход      = Неопределено;
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Истина;
	
	Если ЗначениеЗаполнено(Сделка) И ВзаиморасчетыСПокупателем = Истина Тогда
		АвтоЗакрытиеСделок = Ложь;
	ИначеЕсли АвтоЗакрытиеСделок = Неопределено Тогда
		Попытка 
			АвтоЗакрытиеСделок = ДоговорВзаиморасчетов.АвтоЗакрытиеСделок;
		Исключение
			АвтоЗакрытиеСделок = обПраво("АвтоЗакрытиеСделок", ДокументОбъект.Права,, ДокументОбъект);
		КонецПопытки;
	КонецЕсли;
	
	// Подлежит исправлению YABS
	// Сделки будем закрывать исходя из суммы, согласно валюты договора
	Если Валюта = Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
		ИмяРесурсаСуммы = "СуммаУпр";
	ИначеЕсли Валюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ИмяРесурсаСуммы = "СуммаБаз";
	Иначе
		ИмяРесурсаСуммы = "Сумма";
	КонецЕсли; 
	
	// суммы
	ПолнаяРаспределяемаяСумма = Сумма;
	ПолнаяСуммаСделки         = 0;
	
	// 1. Если указана сделка, то сначала закроем ее долги/авансы
	ПогашениеДебиторскойЗадолженности = Перечисления.ВидыОперацийВзаиморасчетов.ПогашениеДебиторскойЗадолженности;
	Если ЗначениеЗаполнено(Сделка) И Метаданные.РегистрыНакопления.ВзаиморасчетыКомпании.Измерения.Сделка.Тип.СодержитТип(ТипЗнч(Сделка)) Тогда
		РезультатДолгиПоСделке = ПолучитьНеЗакрытыеСделки(Истина, ?(ПриходРасход = Неопределено, Ложь, ПриходРасход));
		
		Выборка = РезультатДолгиПоСделке.Выбрать();
		// пройдемся по выборке
		Пока Выборка.Следующий() Цикл
			// Подлежит исправлению YABS
			// Сделки будем закрывать исходя из суммы, согласно валюты договора
			//Если ИмяРесурсаСуммы="СуммаУпр" Тогда
			//	ВалютаОстаткаСуммыПлатежа=ВалютаУпр;
			//	КурсОстаткаСуммыПлатежа=?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр),
			//	ШапкаДокумента.Дата,ШапкаДокумента.КурсВалютыУпр);
			//ИначеЕсли ИмяРесурсаСуммы="СуммаБаз" Тогда
			//	ВалютаОстаткаСуммыПлатежа=ВалютаБаз;
			//	КурсОстаткаСуммыПлатежа=КурсБаз;
			//Иначе
			ВалютаОстаткаСуммыПлатежа = ВалютаДоговора;
			КурсОстаткаСуммыПлатежа   = ПараметрКурсВзаиморасчетов;
			//КонецЕсли;
			ОстатокСуммыПлатежа = обПересчет(Сумма, Валюта, Курс, ВалютаОстаткаСуммыПлатежа, КурсОстаткаСуммыПлатежа);
			// определим по какой сумме считаем все остальное для избежания двойного пересчета и как следствия ошибки округления
			Если Выборка.Сумма*?(Выборка.Сумма<0,-1,1)<ОстатокСуммыПлатежа Тогда
				СуммаДляПересчета = Окр(Выборка.Сумма*?(Выборка.Сумма<0,-1,1),2);
			Иначе
				ВалютаОстаткаСуммыПлатежа=Валюта;
				КурсОстаткаСуммыПлатежа=Курс;
				СуммаДляПересчета=Сумма;
			КонецЕсли;
			
			Если СуммаДляПересчета=0 Тогда
				Продолжить;
			КонецЕсли;
			
			// закрываем
			НоваяЗапись=Добавить();
			НоваяЗапись.ВидДвижения           = ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период                = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор           = ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент            = Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка                = Выборка.Сделка;
			НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
			НоваяЗапись.ВидОперации           = ПогашениеДебиторскойЗадолженности;
			НоваяЗапись.Сумма                 = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаДоговора,ПараметрКурсВзаиморасчетов),2);
			НоваяЗапись.СуммаУпр              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр)),2);
			НоваяЗапись.СуммаБаз              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаБаз,КурсБаз),2);
			// Подлежит исправлению YABS
			// Сделки будем закрывать исходя из суммы, согласно валюты договора
			Если ИмяРесурсаСуммы      ="СуммаУпр" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр),ШапкаДокумента.Дата,ШапкаДокумента.КурсВалютыУпр),Валюта,Курс);
			ИначеЕсли ИмяРесурсаСуммы = "СуммаБаз" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаБаз,КурсБаз,Валюта,Курс);
			Иначе
				СуммаЗакрылиДок = обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,Валюта,Курс);
			КонецЕсли;
			
			// уменьшим сумму
			Сумма = Сумма-СуммаЗакрылиДок;
			
			// подсчитаем полную сумму
			ПолнаяСуммаСделки = ((ПолнаяСуммаСделки+Сумма)=(Сумма-СуммаЗакрылиДок));
			
			//Если НеучтеннаяСуммаСделки<>Неопределено Тогда
			//	НеучтеннаяСуммаСделки=НеучтеннаяСуммаСделки-НоваяЗапись.Сумма;
			//КонецЕсли; 
			
			// если сделка закрыта, спишем курсовую разницу по ресурсу СуммаБаз
			ОстатокПоВзаиморасчетам = НоваяЗапись.Сумма-Выборка.Сумма*?(Выборка.Сумма<0,-1,1);
			Если (?(ОстатокПоВзаиморасчетам<0,-ОстатокПоВзаиморасчетам,ОстатокПоВзаиморасчетам))<0.01 Тогда
				СписаниеКурсовойРазницыБаз(НоваяЗапись.ДоговорВзаиморасчетов, НоваяЗапись.Контрагент, НоваяЗапись.Сделка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// 2. Если после закрытия долгов по указанной сделке осталась еще не распределенная сумма,
	//    и надо произвести автоматическое закрытие сделок, то закроем остальные сделки в порядке ФИФО
	Если АвтоЗакрытиеСделок И Сумма>=0.01 Тогда
		
		РезультатНеЗакрытыеСделки = ПолучитьНеЗакрытыеСделки(,?(ПриходРасход=Неопределено,Ложь,ПриходРасход));
		Выборка = РезультатНеЗакрытыеСделки.Выбрать();
		// пройдемся по выборке
		Пока Выборка.Следующий() Цикл
			// Подлежит исправлению YABS
			// Сделки будем закрывать исходя из суммы, согласно валюты договора
			//Если ИмяРесурсаСуммы="СуммаУпр" Тогда
			//	ВалютаОстаткаСуммыПлатежа=ВалютаУпр;
			//	КурсОстаткаСуммыПлатежа=?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр),
			//	ШапкаДокумента.Дата,ШапкаДокумента.КурсВалютыУпр);
			//ИначеЕсли ИмяРесурсаСуммы="СуммаБаз" Тогда
			//	ВалютаОстаткаСуммыПлатежа=ВалютаБаз;
			//	КурсОстаткаСуммыПлатежа=КурсБаз;
			//Иначе
			ВалютаОстаткаСуммыПлатежа=ВалютаДоговора;
			КурсОстаткаСуммыПлатежа=ПараметрКурсВзаиморасчетов;
			//КонецЕсли; 
			ОстатокСуммыПлатежа=обПересчет(Сумма,Валюта,Курс,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа);
			// определим по какой сумме считаем все остальное для избежания двойного пересчета и как следствия ошибки округления
			Если Выборка.Сумма*?(Выборка.Сумма<0,-1,1)<ОстатокСуммыПлатежа Тогда
				СуммаДляПересчета = Окр(Выборка.Сумма*?(Выборка.Сумма<0,-1,1),2);
			Иначе
				ВалютаОстаткаСуммыПлатежа=Валюта;
				КурсОстаткаСуммыПлатежа=Курс;
				СуммаДляПересчета = Сумма;
			КонецЕсли;
			
			Если СуммаДляПересчета<=0 Тогда
				Продолжить;
			КонецЕсли;
			// зачтем авансы и долги
			НоваяЗапись = Добавить();
			НоваяЗапись.ВидДвижения           = ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период                = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор           = ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент            = Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка                = Выборка.Сделка;
			НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
			НоваяЗапись.ВидОперации           = ПогашениеДебиторскойЗадолженности;
			НоваяЗапись.Сумма                 = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаДоговора,ПараметрКурсВзаиморасчетов),2);
			НоваяЗапись.СуммаУпр              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр)),2);
			НоваяЗапись.СуммаБаз              = Окр(обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,ВалютаБаз,КурсБаз),2);
			// Подлежит исправлению YABS
			// Сделки будем закрывать исходя из суммы, согласно валюты договора
			Если ИмяРесурсаСуммы      = "СуммаУпр" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр),ШапкаДокумента.Дата,ШапкаДокумента.КурсВалютыУпр),Валюта,Курс);
			ИначеЕсли ИмяРесурсаСуммы = "СуммаБаз" Тогда
				СуммаЗакрылиДок = обПересчет(НоваяЗапись[ИмяРесурсаСуммы],ВалютаБаз,КурсБаз,Валюта,Курс);
			Иначе
				СуммаЗакрылиДок = обПересчет(СуммаДляПересчета,ВалютаОстаткаСуммыПлатежа,КурсОстаткаСуммыПлатежа,Валюта,Курс);
			КонецЕсли;
			
			// уменьшим сумму
			Сумма = Сумма-СуммаЗакрылиДок;
			// подсчитаем полную сумму
			ПолнаяСуммаСделки = ПолнаяСуммаСделки + СуммаЗакрылиДок;
			
			// если сделка закрыта, спишем курсовую разницу по ресурсу СуммаБаз
			ОстатокПоВзаиморасчетам = НоваяЗапись.Сумма-Выборка.Сумма*?(Выборка.Сумма<0,-1,1);
			Если (?(ОстатокПоВзаиморасчетам<0,-ОстатокПоВзаиморасчетам,ОстатокПоВзаиморасчетам))<0.01 Тогда
				СписаниеКурсовойРазницыБаз(НоваяЗапись.ДоговорВзаиморасчетов,НоваяЗапись.Контрагент,НоваяЗапись.Сделка);
			КонецЕсли;
			
			// если распределили всю сумму, то выходим
			Если Сумма<0.01 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// 3. Если осталась нераспределенная сумма...
	СуммаОстаток = обПересчет(Сумма, Валюта, Курс, ВалютаДоговора, ПараметрКурсВзаиморасчетов);
	СуммаОстатокАбс = СуммаОстаток;
	Если СуммаОстаток < 0 Тогда
		СуммаОстатокАбс = -СуммаОстаток;
	КонецЕсли;
	Если СуммаОстатокАбс>=0.01 Тогда
		// СписатьНераспределеннуюСуммуПоСделке Испол
		//СписатьНераспределеннуюСуммуПоСделке = ?(СписатьНераспределеннуюСуммуПоСделке=Неопределено, Ложь, СписатьНераспределеннуюСуммуПоСделке);
		//Если (ЗначениеЗаполнено(Сделка)) И (НеучтеннаяСуммаСделки<>Неопределено ИЛИ СписатьНераспределеннуюСуммуПоСделке=Истина) Тогда
		//	СделкаПереплаты=Сделка;
		//Иначе
		//	СделкаПереплаты=ШапкаДокумента.Ссылка;
		//КонецЕсли;
		Если ЗначениеЗаполнено(Сделка) И ВзаиморасчетыСПокупателем = Истина Тогда
			СделкаПереплаты = Сделка;
		Иначе
			СделкаПереплаты = ШапкаДокумента.Ссылка;
		КонецЕсли;
		Если СделкаПереплаты<>ШапкаДокумента.Ссылка И СуммаОстатокАбс>0 Тогда
			//Проверили, что закрываемая сделка не есть сам документ движения
			//посмотрим, запрещена ли по данной сделке переплата. и если запрещена, то падаем
			Попытка
				Если ПланыВидовХарактеристик.ТипыСделок[СделкаПереплаты.Метаданные().Имя].ЗапретитьПереплатуПоСделкам Тогда
					// переплата запрещена.
					дкСообщитьРезультат("Распределяемая сумма "+Формат(ПолнаяРаспределяемаяСумма,"ЧДЦ=2; ЧН=0,00")+" превышает незакрытую сумму сделки "+Формат(ПолнаяСуммаСделки,"ЧДЦ=2; ЧН=0,00")+" на "+Формат(Сумма,"ЧДЦ=2; ЧН=0,00")+" !","Внимание&Взаиморасчеты компании:",ДокументОбъект);
					Результат=Ложь;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		//Если Результат И (ЗначениеЗаполнено(Сделка)) И СделкаПереплаты<>Сделка Тогда
		//	дкСообщитьРезультат("Сумма переплаты "+Формат(СуммаОстатокАбс,"ЧДЦ=2; ЧН=0,00")+" "+СокрЛП(ДоговорВзаиморасчетов.ВалютаВзаиморасчетов)+" распределена на сделку <"+СокрЛП(СделкаПереплаты)+">!","Внимание&Взаиморасчеты компании:",ДокументОбъект);
		//КонецЕсли; 
		
		// вешаем оставшуюся сумму либо на сделку (если она указана), либо на сам документ
		НоваяЗапись = Добавить();
		НоваяЗапись.ВидДвижения           = ВидДвиженияНакопления.Расход;
		НоваяЗапись.Период                = ШапкаДокумента.Дата;
		НоваяЗапись.Регистратор           = ШапкаДокумента.Ссылка;
		НоваяЗапись.Контрагент            = Контрагент;
		НоваяЗапись.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
		НоваяЗапись.Сделка                = СделкаПереплаты;
		НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
		НоваяЗапись.ВидОперации           = Перечисления.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности;
		НоваяЗапись.Сумма				  = Окр(СуммаОстаток,2);
		НоваяЗапись.СуммаУпр			  = Окр(обПересчет(Сумма,Валюта,Курс,ВалютаУпр,?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр)),2);
		НоваяЗапись.СуммаБаз			  = Окр(обПересчет(Сумма,Валюта,Курс,ВалютаБаз,КурсБаз),2);
	КонецЕсли;
	
	// если списываем, суммовые разницы первичным документом, то спишем их
	Если обПраво("РежимСписанияСуммовыхРазниц",ДокументОбъект.Права,,ДокументОбъект)=Перечисления.РежимыРегламентныхОпераций.ПриПроведенииДокумента Тогда
		СписаниеСуммовыхРазниц();
	КонецЕсли;

	// убиваем циклическую ссылку
	МоментВремени                        = Неопределено;
	ДокументОбъект                       = Неопределено;
	ШапкаДокумента                       = Неопределено;
	АвтоЗакрытиеСделок                   = Неопределено;
	СписатьНераспределеннуюСуммуПоСделке = Неопределено;
	Валюта                               = Неопределено;
	СделкиКонтрагента                    = Неопределено;
	ПриходРасход                         = Неопределено;
	
	Возврат Результат;
КонецФункции

// Списывает суммовые разницы
Функция СписаниеСуммовыхРазниц() Экспорт
	
	Если ШапкаДокумента=Неопределено Тогда 
		ШапкаДокумента=ДокументОбъект; 
		Если МоментВремени=Неопределено Тогда
			МоментВремени=ШапкаДокумента.МоментВремени();
		КонецЕсли; 
	Иначе
		Попытка
			Если МоментВремени=Неопределено Тогда
				МоментВремени=ШапкаДокумента.МоментВремени;
			КонецЕсли; 
		Исключение
			Если МоментВремени=Неопределено Тогда
				МоментВремени=ДокументОбъект.МоментВремени();
			КонецЕсли; 
		КонецПопытки;
	КонецЕсли;
	
	// запишем предыдущие движения иначе остатки будут неверны в запросе
	Записать();
	// определимся, как списываем, регламентно или первичным документом
	Регламентно=(обПраво("РежимСписанияСуммовыхРазниц",ДокументОбъект.Права,,ДокументОбъект)=Перечисления.РежимыРегламентныхОпераций.Регламентно);
	СуммаДоходаРасходаСуммовыхРазниц=0;
	
	// если списываем не регламентно, то установим фильтры по контрагенту и договору
	СтрокаФильтра="";
	Если НЕ Регламентно Тогда
		СтрокаФильтра=",Контрагент=&Контрагент И ДоговорВзаиморасчетов=&ДоговорВзаиморасчетов";		
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоПодразделению") Тогда	
		СтрокаФильтра = ", ДоговорВзаиморасчетов.Подразделение=&ПодразделениеКомпании"; 
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоОрганизации") Тогда
		СтрокаФильтра = ", ДоговорВзаиморасчетов.Организация=&Организация";
	КонецЕсли;
	
	// получим таблицу суммовых разниц
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
	|	ВзаиморасчетыКомпанииОстатки.СуммаОстаток КАК Сумма,
	|	ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток КАК СуммаУпр,
	|	ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток КАК СуммаБаз
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Момент"+СтрокаФильтра+") КАК ВзаиморасчетыКомпанииОстатки
	|ГДЕ
	|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток,0)=0
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки
	|";
	Запрос = Новый Запрос(ТекстЗапроса);
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ВзаиморасчетыКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ШапкаДокумента.Дата));
	
	Если НЕ Регламентно Тогда
		Если ТипЗнч(МоментВремени)=Тип("Граница") Тогда
			ГраницаРасчетов=Новый Граница(МоментВремени.Значение,ВидГраницы.Включая);
		Иначе
			ГраницаРасчетов=Новый Граница(МоментВремени,ВидГраницы.Включая);
		КонецЕсли;
		Запрос.УстановитьПараметр("Момент",ГраницаРасчетов);
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		
		ЗначенияБлокировки.Вставить("Контрагент", Контрагент);
		ЗначенияБлокировки.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов); 
	Иначе
		Запрос.УстановитьПараметр("Момент",МоментВремени);
	КонецЕсли;
	
	// Если нужно добавим фильтр.
	Если ДополнительныеСвойства.Свойство("ФильтрПоПодразделению") Тогда
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ДополнительныеСвойства.ФильтрПоПодразделению);
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоОрганизации") Тогда
		Запрос.УстановитьПараметр("Организация", ДополнительныеСвойства.ФильтрПоОрганизации);	
	КонецЕсли;
	
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
	
	РезультатЗапросаСуммовыеРазницы = Запрос.Выполнить(); СуммаДохода=0; СуммаРасхода=0;
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ШапкаДокумента.КурсВалютыУпр;
	КонецЕсли;	
	
	// Если необходимо, заполним вспомогательную таблицу движений, которую 
	// можно использовать во "внешнем" коде.
	Если ДополнительныеСвойства.Свойство("ТаблицаДвижений") Тогда
		ЗаполнитьТаблицуДвижений = Истина;
	Иначе
		ЗаполнитьТаблицуДвижений = Ложь;
	КонецЕсли;
	
	// Закрываем	
	Выборка=РезультатЗапросаСуммовыеРазницы.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаУпр<>0 Тогда
			НоваяЗапись=Добавить();
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент=Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка=Выборка.Сделка;
			Если Выборка.СуммаУпр>0 Тогда
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
				НоваяЗапись.СуммаУпр=Окр(Выборка.СуммаУпр,2);
				СуммаДоходаРасходаСуммовыхРазниц=СуммаДоходаРасходаСуммовыхРазниц-НоваяЗапись.СуммаУпр;
			Иначе
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.СуммаУпр=Окр(-Выборка.СуммаУпр,2);
				СуммаДоходаРасходаСуммовыхРазниц=СуммаДоходаРасходаСуммовыхРазниц+НоваяЗапись.СуммаУпр;
			КонецЕсли;
			НоваяЗапись.Сумма=0;
			НоваяЗапись.СуммаБаз=0;
			НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
			НоваяЗапись.ВидОперации=Перечисления.ВидыОперацийВзаиморасчетов.СписаниеКурсовойРазницыУпрВалюты;
			
			// Заполняем вспомогательную таблицу движений.
			Если ЗаполнитьТаблицуДвижений Тогда
				НоваяСтрока = ДополнительныеСвойства.ТаблицаДвижений.Добавить();
				НоваяСтрока.Подразделение      = Выборка.ДоговорВзаиморасчетов.Подразделение;
				НоваяСтрока.СуммоваяРазницаУпр = ?(Выборка.СуммаУпр<0, НоваяЗапись.СуммаУпр, -НоваяЗапись.СуммаУпр);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.СуммаБаз<>0 Тогда
			НоваяЗапись=Добавить();
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗапись.Контрагент=Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка=Выборка.Сделка;
			Если Выборка.СуммаБаз>0 Тогда
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
				НоваяЗапись.СуммаБаз=Окр(Выборка.СуммаБаз,2);
			Иначе
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.СуммаБаз=Окр(-Выборка.СуммаБаз,2);
			КонецЕсли;
			НоваяЗапись.Сумма=0;
			НоваяЗапись.СуммаУпр=0;
			НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
			НоваяЗапись.ВидОперации=Перечисления.ВидыОперацийВзаиморасчетов.СписаниеКурсовойРазницыБазВалюты;
		КонецЕсли;
	КонецЦикла;
	
	// Убиваем циклическую ссылку
	МоментВремени  = Неопределено;
	ДокументОбъект = Неопределено;
	ШапкаДокумента = Неопределено;
	
	Возврат Истина;
	
КонецФункции

// Списывает курсовые разницы
Функция СписаниеКурсовыхРазниц() Экспорт
	Если ШапкаДокумента=Неопределено Тогда 
		ШапкаДокумента=ДокументОбъект; 
		Если МоментВремени=Неопределено Тогда
			МоментВремени=ШапкаДокумента.МоментВремени();
		КонецЕсли; 
	Иначе
		Попытка
			Если МоментВремени=Неопределено Тогда
				МоментВремени=ШапкаДокумента.МоментВремени;
			КонецЕсли; 
		Исключение
			Если МоментВремени=Неопределено Тогда
				МоментВремени=ДокументОбъект.МоментВремени();
			КонецЕсли; 
		КонецПопытки;
	КонецЕсли;
	
	// запишем предыдущие движения иначе остатки будут неверны в запросе
	Записать();
	// Вспомогательные переменные
	СуммаДоходаРасходаСуммовыхРазниц=0;
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ШапкаДокумента.КурсВалютыУпр;
	КонецЕсли;	
	
	СтрокаФильтра = "";
	Если ДополнительныеСвойства.Свойство("ФильтрПоПодразделению") Тогда	
		СтрокаФильтра = ", ДоговорВзаиморасчетов.Подразделение=&ПодразделениеКомпании"; 
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоОрганизации") Тогда
		СтрокаФильтра = ", ДоговорВзаиморасчетов.Организация=&Организация";
	КонецЕсли; 
	
	// Получаем выборку
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА КурсыВалютСрезПоследних.Кратность = 0 ТОГДА
	|			КурсыВалютСрезПоследних.Курс
	|		ИНАЧЕ
	|			КурсыВалютСрезПоследних.Курс/КурсыВалютСрезПоследних.Кратность
	|	КОНЕЦ КАК Курс
	|ПОМЕСТИТЬ
	|	КурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Момент) КАК КурсыВалютСрезПоследних
	|;
	|
	|ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток,0) КАК Сумма,
	|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток,0) КАК СуммаУпр,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&Момент" + СтрокаФильтра + ") КАК ВзаиморасчетыКомпанииОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	|ПО
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки
	|");
	Если ТипЗнч(МоментВремени)=Тип("Граница") Тогда
		ГраницаРасчетов = Новый Граница(МоментВремени.Значение,ВидГраницы.Включая);
	Иначе
		ГраницаРасчетов = Новый Граница(МоментВремени,ВидГраницы.Включая);
	КонецЕсли;
	Запрос.УстановитьПараметр("Момент",ГраницаРасчетов);
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ВзаиморасчетыКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ШапкаДокумента.Дата));
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
	
	// Если нужно добавим фильтр.
	Если ДополнительныеСвойства.Свойство("ФильтрПоПодразделению") Тогда
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ДополнительныеСвойства.ФильтрПоПодразделению);
	ИначеЕсли ДополнительныеСвойства.Свойство("ФильтрПоОрганизации") Тогда
		Запрос.УстановитьПараметр("Организация", ДополнительныеСвойства.ФильтрПоОрганизации);	
	КонецЕсли;
	
	РезультатЗапросаКурсовые=Запрос.Выполнить();
	
	// Если необходимо, заполним вспомогательную таблицу движений, которую 
	// можно использовать во "внешнем" коде.
	Если ДополнительныеСвойства.Свойство("ТаблицаДвижений") Тогда
		ЗаполнитьТаблицуДвижений = Истина;
	Иначе
		ЗаполнитьТаблицуДвижений = Ложь;
	КонецЕсли;
	
	// Идем по выборке
	Выборка=РезультатЗапросаКурсовые.Выбрать();
	Пока Выборка.Следующий() Цикл
		СуммаПереоценки=Окр(обПересчет(Выборка.Сумма,Выборка.ВалютаВзаиморасчетов,Выборка.Курс,ВалютаУпр,КурсУпр)-Выборка.СуммаУпр,2);
		Если СуммаПереоценки=0 Тогда Продолжить; КонецЕсли; // переоценивать нечего
		// Переоцениваем
		НоваяЗапись=Добавить();
		НоваяЗапись.Период=ШапкаДокумента.Дата;
		НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
		НоваяЗапись.Контрагент=Выборка.Контрагент;
		НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
		НоваяЗапись.Сделка=Выборка.Сделка;
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
		НоваяЗапись.СуммаУпр=СуммаПереоценки;
		НоваяЗапись.Сумма=0;
		НоваяЗапись.СуммаБаз=0;
		// Доходы/расходы
		СуммаДоходаРасходаСуммовыхРазниц=СуммаДоходаРасходаСуммовыхРазниц+НоваяЗапись.СуммаУпр;
		// Определяемся с хоз. операцией
		НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
		НоваяЗапись.ВидОперации=Перечисления.ВидыОперацийВзаиморасчетов.СписаниеКурсовойРазницыУпрВалюты;
		
		// Заполняем вспомогательную таблицу движений.
		Если ЗаполнитьТаблицуДвижений Тогда
			НоваяСтрока = ДополнительныеСвойства.ТаблицаДвижений.Добавить();
			НоваяСтрока.Подразделение      = Выборка.ДоговорВзаиморасчетов.Подразделение;
			НоваяСтрока.КурсоваяРазницаУпр = НоваяЗапись.СуммаУпр;
		КонецЕсли;
	КонецЦикла;
	
	// Убиваем циклическую ссылку
	МоментВремени=Неопределено;
	ДокументОбъект=Неопределено;
	ШапкаДокумента=Неопределено;
	
	// Все ОК
	Возврат Истина;
КонецФункции

// Списание курсовой разницы на ресурсе СуммаБаз при закрытии сделки
Функция СписаниеКурсовойРазницыБаз(ПараметрДоговор=Неопределено,ПараметрКонтрагент=Неопределено,ПараметрСделка=Неопределено) Экспорт
	// Запишем предыдущие движения иначе остатки будут неверны в запросе
	Записать();
	ОтборКонтрагент=?(обЗначениеНеЗаполнено(ПараметрКонтрагент),?(обЗначениеНеЗаполнено(Контрагент),ДоговорВзаиморасчетов.Владелец,Контрагент),ПараметрКонтрагент);
	ОтборСделка=?(обЗначениеНеЗаполнено(ПараметрСделка),?(обЗначениеНеЗаполнено(Сделка),ШапкаДокумента.Ссылка,Сделка),ПараметрСделка);
	ОтборДоговор=?(обЗначениеНеЗаполнено(ПараметрДоговор),ДоговорВзаиморасчетов,ПараметрДоговор);
	
	ВалютаБаз=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаБаз, ШапкаДокумента.Дата);
	КурсБаз = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	// Получаем выборку
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА КурсыВалютСрезПоследних.Кратность = 0 ТОГДА
	|			КурсыВалютСрезПоследних.Курс
	|		ИНАЧЕ
	|			КурсыВалютСрезПоследних.Курс/КурсыВалютСрезПоследних.Кратность
	|	КОНЕЦ КАК Курс
	|ПОМЕСТИТЬ
	|	КурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Момент) КАК КурсыВалютСрезПоследних
	|;
	|
	|ВЫБРАТЬ
	|	ВзаиморасчетыКомпанииОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ВзаиморасчетыКомпанииОстатки.Сделка КАК Сделка,
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК Сумма,
	|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток, 0) КАК СуммаБаз,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
	|		&Момент,
	|		Контрагент = &Контрагент И 
	|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И 
	|		Сделка = &Сделка) КАК ВзаиморасчетыКомпанииОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	|ПО
	|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки
	|");
	Если ТипЗнч(МоментВремени)=Тип("Граница") Тогда
		ГраницаРасчетов=Новый Граница(МоментВремени.Значение,ВидГраницы.Включая);
	Иначе
		ГраницаРасчетов=Новый Граница(МоментВремени,ВидГраницы.Включая);
	КонецЕсли;
	Запрос.УстановитьПараметр("Момент",ГраницаРасчетов);
	Запрос.УстановитьПараметр("Контрагент",ОтборКонтрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ОтборДоговор);
	Запрос.УстановитьПараметр("Сделка",ОтборСделка);
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ВзаиморасчетыКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, ШапкаДокумента.Дата));
	ЗначенияБлокировки.Вставить("Контрагент", ОтборКонтрагент);
	ЗначенияБлокировки.Вставить("ДоговорВзаиморасчетов", ОтборДоговор);
	ЗначенияБлокировки.Вставить("Сделка", ОтборСделка);
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
	
	РезультатЗапросаКурсовые=Запрос.Выполнить();
	Выборка=РезультатЗапросаКурсовые.Выбрать();
	СуммаПереоценки=0;
	Пока Выборка.Следующий() Цикл
		СуммаПереоценки=СуммаПереоценки+обПересчет(Выборка.Сумма,Выборка.ВалютаВзаиморасчетов,Выборка.Курс,ВалютаБаз,КурсБаз)-Выборка.СуммаБаз;
	КонецЦикла;
	
	// Делаем корректирующее движение, если есть курсовая разница
	СуммаПереоценки=Окр(СуммаПереоценки,2);
	Если СуммаПереоценки<>0 Тогда
		НоваяЗапись=Добавить();
		НоваяЗапись.Период=ШапкаДокумента.Дата;
		НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
		НоваяЗапись.Контрагент=ОтборКонтрагент;
		НоваяЗапись.ДоговорВзаиморасчетов=ОтборДоговор;
		НоваяЗапись.Сделка=ОтборСделка;
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
		НоваяЗапись.Сумма=0;
		НоваяЗапись.СуммаУпр=0;
		НоваяЗапись.СуммаБаз=СуммаПереоценки;
		НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
		НоваяЗапись.ВидОперации=Перечисления.ВидыОперацийВзаиморасчетов.СписаниеКурсовойРазницыБазВалюты;
	КонецЕсли;
	Возврат Истина;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

РежимПроведения = НЕОПРЕДЕЛЕНО;
Валюта = Неопределено;
КурсВзаиморасчетов = 0;
СуммаДоходаРасходаСуммовыхРазниц=0;
ВзаиморасчетыСПокупателем=Неопределено;
МоментВремени=Неопределено;
РежимКорректировки=Ложь;

Перем ДокументОбъект Экспорт;
Перем РезультатЗапросаПоАвтомобилям Экспорт;
Перем ПодразделениеКомпании Экспорт;

Функция ПолучитьТаблицуАвтомобилей(ТаблицаСписания)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвтомобилиДляТестДрайваОстатки.ПодразделениеКомпании,
	|	АвтомобилиДляТестДрайваОстатки.Модель,
	|	АвтомобилиДляТестДрайваОстатки.Автомобиль,
	|	АвтомобилиДляТестДрайваОстатки.Актив,
	|	АвтомобилиДляТестДрайваОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.АвтомобилиДляТестДрайва.Остатки КАК АвтомобилиДляТестДрайваОстатки
	|ГДЕ
	|	АвтомобилиДляТестДрайваОстатки.Автомобиль В(&СписокАвто)
	|	И АвтомобилиДляТестДрайваОстатки.Актив В(&СписокАктивов)
	|	И АвтомобилиДляТестДрайваОстатки.Автомобиль <> &ПустойАвтомобиль";
	
	Запрос.УстановитьПараметр("СписокАвто", ТаблицаСписания.ВыгрузитьКолонку("Автомобиль"));
	Запрос.УстановитьПараметр("СписокАктивов", ТаблицаСписания.ВыгрузитьКолонку("ПрочийАктив"));
	Запрос.УстановитьПараметр("ПустойАвтомобиль", Справочники.Автомобили.ПустаяСсылка());
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция Приход() Экспорт
	Если ТипЗнч(РезультатЗапросаПоАвтомобилям) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоАвтомобилям = РезультатЗапросаПоАвтомобилям.Выгрузить();
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из РезультатЗапросаПоАвтомобилям Цикл
		НоваяЗапись = Добавить();
		НоваяЗапись.Регистратор 		  = ДокументОбъект.Ссылка;
		НоваяЗапись.Период     			  = ДокументОбъект.Дата;
		НоваяЗапись.ВидДвижения           = ВидДвиженияНакопления.Приход;
		НоваяЗапись.ПодразделениеКомпании = ПодразделениеКомпании;
		НоваяЗапись.Модель				  = СтрокаТЧ.Автомобиль.Модель;
		НоваяЗапись.Автомобиль 			  = СтрокаТЧ.Автомобиль;
		НоваяЗапись.Актив                 = СтрокаТЧ.Актив;
		
		НоваяЗапись.Количество            = 1;
		//НоваяЗапись.МаксимальнаяПродолжительностьЭксплуатации = ДобавитьМесяц(ДокументОбъект.Дата,СтрокаТЧ.МаксимальнаяПродолжительностьЭксплуатации);
		НоваяЗапись.МаксимальнаяПродолжительностьЭксплуатации = ДокументОбъект.Дата + (СтрокаТЧ.МаксимальнаяПродолжительностьЭксплуатации *60*60*24);
		НоваяЗапись.МаксимальныйПробег                        = СтрокаТЧ.МаксимальныйПробег;
		НоваяЗапись.ХозОперация                               = ДокументОбъект.ХозОперация;
	КонецЦикла;
КонецФункции

Функция Расход() Экспорт
	Если ТипЗнч(РезультатЗапросаПоАвтомобилям) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоАвтомобилям = РезультатЗапросаПоАвтомобилям.Выгрузить();
	КонецЕсли;
	
	ТаблицаОстатковАвто = ПолучитьТаблицуАвтомобилей(РезультатЗапросаПоАвтомобилям);
	
	Для Каждого стрАвто Из РезультатЗапросаПоАвтомобилям Цикл
		
		Если обЗначениеНеЗаполнено(стрАвто.Автомобиль) Тогда
			Продолжить;
		КонецЕсли;
		
		стрПоиска = Новый Структура("Актив,Автомобиль",стрАвто.ПрочийАктив,стрАвто.Автомобиль);
		МассивНайденных = ТаблицаОстатковАвто.НайтиСтроки(стрПоиска);
		
		Для Каждого стрМассива Из МассивНайденных Цикл
			НоваяСтрока = Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,стрМассива);
			НоваяСтрока.Регистратор = ДокументОбъект.Ссылка;
			НоваяСтрока.Период = ДокументОбъект.Дата;
			НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
			НоваяСтрока.ХозОперация = ДокументОбъект.ХозОперация;
		КонецЦикла;
	КонецЦикла;
КонецФункции

Функция Перемещение() Экспорт
	Расход();
	
	ТаблицаСписанныхАвто = ЭтотОбъект.Выгрузить();
	Для Каждого СписанноеАвто Из ТаблицаСписанныхАвто Цикл
		НоваяЗапись = Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись,СписанноеАвто);
		НоваяЗапись.Период      = ДокументОбъект.Дата;
		НоваяЗапись.Регистратор = ДокументОбъект.Ссылка;
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяЗапись.ПодразделениеКомпании = ПодразделениеКомпании;
		НоваяЗапись.ХозОперация = ДокументОбъект.ХозОперация;
	КонецЦикла;
КонецФункции

ДокументОбъект = Неопределено;
РезультатЗапросаПоАвтомобилям = Неопределено;
ПодразделениеКомпании = Неопределено;
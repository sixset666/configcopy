// Модуль набора записей регистра "ГТДПартийТоваровКомпании"

Перем ДокументОбъект Экспорт;				// Документ выполняющий движения
Перем ШапкаДокумента Экспорт; 				// Выборка или строка таблицы значений, в которой содержаться необходимые данные о шапке документа
Перем ГраницаРасчетаОстатков Экспорт; 		// граница на которую надо рассчитывать остатки 
Перем ТаблицаПартий Экспорт;				// Таблица движений по партия товаров компании
Перем ТаблицаТовары Экспорт; 				// если результат запроса (или таблица значений) неопределен, то подразумевается, что в документе есть таблица "Товары". И наименования колонок стандартные.
Перем СкладКомпании Экспорт;				// Склад компании
Перем МассивПартий Экспорт; 				// Массив. Если надо установить фильтр на партии, то заполняем этот массив
Перем РежимПроведения Экспорт; 				// Режим проведения документа оперативный/неоперативный
Перем ТаблицаВозвратТоваров Экспорт;		// Таблица возвратов товаров с указанием партии


// Функция получения остатков ГТД
Функция ПолучитьОстаткиГТДПартий(ТаблицаПартий)
	Если ГраницаРасчетаОстатков=Неопределено Тогда 
		Если РежимПроведения=РежимПроведенияДокумента.Оперативный Тогда
			ГраницаРасчетаОстатков=Неопределено;
		Иначе
			ГраницаРасчетаОстатков=ШапкаДокумента.МоментВремени; 
		КонецЕсли;
	КонецЕсли;
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ГТДПартийТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	             |	ГТДПартийТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	             |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	ГТДПартийТоваровКомпанииОстатки.Партия КАК Партия,
	             |	ГТДПартийТоваровКомпанииОстатки.ГТД КАК ГТД,
	             |	СУММА(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток) КАК Количество
	             |ИЗ
	             |	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	             |			&Момент,
	             |			"+?(СкладКомпании=Неопределено,"СкладКомпании В (&СкладКомпании)","СкладКомпании=&СкладКомпании")+"
	             |				И Номенклатура В (&Номенклатура)
	             |				И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	             |				И Партия В (&Партия)) КАК ГТДПартийТоваровКомпанииОстатки
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ГТДПартийТоваровКомпанииОстатки.СкладКомпании,
	             |	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
	             |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	             |	ГТДПартийТоваровКомпанииОстатки.Партия,
	             |	ГТДПартийТоваровКомпанииОстатки.ГТД";
	Запрос.УстановитьПараметр("Момент",ГраницаРасчетаОстатков);
	Если СкладКомпании=Неопределено Тогда
		Запрос.УстановитьПараметр("СкладКомпании",ТаблицаПартий.ВыгрузитьКолонку("СкладКомпании"));
	Иначе
		Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
	КонецЕсли; 
	Запрос.УстановитьПараметр("Номенклатура",ТаблицаПартий.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТаблицаПартий.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.УстановитьПараметр("Партия",ТаблицаПартий.ВыгрузитьКолонку("Партия"));
	
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ГТДПартийТоваровКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	
	Попытка
		ДатаГраницы = ГраницаРасчетаОстатков.Дата;
	Исключение
		Попытка
			ДатаГраницы = ГраницаРасчетаОстатков.Значение.Дата;
		Исключение
			ДатаГраницы = Неопределено;
		КонецПопытки;
	КонецПопытки;
	
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(,ДатаГраницы)); 
	Если СкладКомпании<>Неопределено Тогда
		ЗначенияБлокировки.Вставить("СкладКомпании", СкладКомпании); 
	КонецЕсли; 
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаПартий);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("Партия", "Партия");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	ТаблицаГТД=Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаГТД;
КонецФункции // ПолучитьОстаткиГТДПартий() 

// Добавление новой записи в набор записей регистра
//
// Параметры
//  ЗаписьПартий  - РегистрНакопленияЗапись - Запись регистра накопления партий товаров
//  ГТД  - СправочникСсылка.ГТД - ГТД партии
//
Процедура ДобавитьЗапись(ЗаписьПартий,ГТД) Экспорт
	Если НЕ обЗначениеНеЗаполнено(ГТД) Тогда
		Если ТипЗнч(ЗаписьПартий)=Тип("РегистрНакопленияЗапись.ПартииТоваровКомпании") Тогда
			Если ЗаписьПартий.Количество<>0 Тогда
				НоваяЗапись=Добавить();
				
				НоваяЗапись.ВидДвижения					= ЗаписьПартий.ВидДвижения;
				НоваяЗапись.Период						= ЗаписьПартий.Период;
				НоваяЗапись.Регистратор					= ЗаписьПартий.Регистратор;
				
				НоваяЗапись.СкладКомпании				= ЗаписьПартий.СкладКомпании;
				НоваяЗапись.Номенклатура				= ЗаписьПартий.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры	= ЗаписьПартий.ХарактеристикаНоменклатуры;
				НоваяЗапись.Партия						= ЗаписьПартий.Партия;
				НоваяЗапись.ГТД							= ГТД;
				
				НоваяЗапись.Количество					= ЗаписьПартий.Количество;
				
				НоваяЗапись.ХозОперация					= ЗаписьПартий.ХозОперация;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры // ДобавитьЗапись()

// Формирует движения по регистру расход (уменьшение запаса по ГТД)
// Возвращаемое значение: Булево. Истина - все окей, Ложь - чего-то не так.
Функция Расход() Экспорт
	ВсеОК=Истина;
	
	// Получим права пользователя
	ОтрицательныеОстаткиРазрешены=(обПраво("РазрешитьОтрицательныеСкладскиеОстатки",ДокументОбъект.Права,,ДокументОбъект)<>Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	Если ОтрицательныеОстаткиРазрешены Тогда
		ВыводитьКритическиеСообщения=обПраво("ВыводитьСообщенияПроверкиПравДоступа",ДокументОбъект.Права,,ДокументОбъект);
	Иначе
		ВыводитьКритическиеСообщения=Ложь;
	КонецЕсли;
	
	// Списываем ГТД партий
	ТаблицаГТДПартий=ПолучитьОстаткиГТДПартий(ТаблицаПартий);
	ТаблицаПартий.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,Партия","Количество");
	
	//Отсортируем таблицу товаров по ГТД, чтобы сначала списать товары с указанными ГТД
	ЕстьПартия=(ТаблицаТовары.Колонки.Найти("Партия")<>Неопределено);
	ЕстьГТД=(ТаблицаТовары.Колонки.Найти("ГТД")<>Неопределено);
	СтрокаСортировки="";
	Если ЕстьПартия Тогда
		СтрокаСортировки=СтрокаСортировки+", Партия Убыв";
	КонецЕсли;
	Если ЕстьГТД Тогда
		СтрокаСортировки=СтрокаСортировки+", ГТД Убыв";
	КонецЕсли;
	Если НЕ ПустаяСтрока(СтрокаСортировки) Тогда
		СтрокаСортировки=Сред(СтрокаСортировки,3);
		ТаблицаТовары.Сортировать(СтрокаСортировки);
	КонецЕсли; 
	
	Права=ДокументОбъект.Права;
	ИмяКолонкиКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ДокументОбъект).Имя;
	ВозвратТоваров=(ДокументОбъект.Метаданные()=Метаданные.Документы.ВозвратПоставщику);
	
	Если ТаблицаВозвратТоваров <> Неопределено Тогда
		ТаблицаВозвратТоваров.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,Партия","Количество");
	КонецЕсли;
	
	Для Каждого СтрокаТовар Из ТаблицаТовары Цикл
		// Получим строки таблицы партий с нашим товаром
		СтруктураОтбора=Новый Структура("Номенклатура",СтрокаТовар.Номенклатура);
		Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
		КонецЕсли; 
		Если ЕстьПартия И НЕ обЗначениеНеЗаполнено(СтрокаТовар.Партия) Тогда
			СтруктураОтбора.Вставить("Партия",СтрокаТовар.Партия);
		КонецЕсли; 
		МассивНайденныхПартий=ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
		Для СчПартий=0 По МассивНайденныхПартий.ВГраница() Цикл
			//Идем по списанным партия
			СтрокаПартия=МассивНайденныхПартий[СчПартий];
			СтрокаПартия.Количество=?(ВозвратТоваров,-СтрокаПартия.Количество,СтрокаПартия.Количество);
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаПартия.ХарактеристикаНоменклатуры);
			СтруктураОтбора.Вставить("Партия",СтрокаПартия.Партия);
			Если ЕстьГТД И НЕ обЗначениеНеЗаполнено(СтрокаТовар.ГТД) Тогда
				СтруктураОтбора.Вставить("ГТД",СтрокаТовар.ГТД);
			КонецЕсли; 
			МассивНайденныхГТД=ТаблицаГТДПартий.НайтиСтроки(СтруктураОтбора);
			Для СчГТД=0 По МассивНайденныхГТД.ВГраница() Цикл
				
				СтрокаВозврат = Неопределено;
				Если ТаблицаВозвратТоваров <> Неопределено Тогда
					МассивВозвратов = ТаблицаВозвратТоваров.НайтиСтроки(СтруктураОтбора);
					Если МассивВозвратов.Количество() > 0 Тогда
						СтрокаВозврат = МассивВозвратов[0];
					КонецЕсли;
				КонецЕсли;
				// Был полный возврат товара.
				Если НЕ СтрокаВозврат = Неопределено И СтрокаПартия.Количество = 0 Тогда
					СтрокаПартия.Количество = СтрокаВозврат.Количество;
				КонецЕсли;
				
				//Списываем ГТД партии
				СтрокаГТД=МассивНайденныхГТД[СчГТД];
				НоваяЗапись=Добавить();
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
				НоваяЗапись.Период=ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗапись.СкладКомпании=СтрокаГТД.СкладКомпании;
				НоваяЗапись.Номенклатура=СтрокаГТД.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры=СтрокаГТД.ХарактеристикаНоменклатуры;
				НоваяЗапись.Партия=СтрокаГТД.Партия;
				НоваяЗапись.ГТД=СтрокаГТД.ГТД;
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗапись.Количество=Мин(СтрокаГТД.Количество,СтрокаПартия.Количество,СтрокаТовар.Количество);
				
				Если НЕ СтрокаВозврат = Неопределено Тогда
					КоличествоВозврат = Мин(СтрокаГТД.Количество,СтрокаПартия.Количество,СтрокаВозврат.Количество);
				Иначе
					КоличествоВозврат = 0;
				КонецЕсли;
				
				СтрокаГТД.Количество=СтрокаГТД.Количество-НоваяЗапись.Количество;
				СтрокаПартия.Количество=СтрокаПартия.Количество-НоваяЗапись.Количество;
				
				Если КоличествоВозврат > 0 Тогда
					НоваяЗаписьВозврат = Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗаписьВозврат, НоваяЗапись);
					НоваяЗаписьВозврат.Количество = -КоличествоВозврат;
					СтрокаГТД.Количество=СтрокаГТД.Количество + КоличествоВозврат;
					СтрокаПартия.Количество=СтрокаПартия.Количество + КоличествоВозврат;
					Если КоличествоВозврат = СтрокаВозврат.Количество Тогда
						ТаблицаВозвратТоваров.Удалить(СтрокаВозврат);
					Иначе
						СтрокаВозврат.Количество = СтрокаВозврат.Количество - КоличествоВозврат;
					КонецЕсли;
					СчГТД = СчГТД - 1;
				КонецЕсли;
				
				СтрокаТовар.Количество=СтрокаТовар.Количество-НоваяЗапись.Количество;
				Если ВозвратТоваров Тогда
					НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
					НоваяЗапись.Количество=-НоваяЗапись.Количество;
				КонецЕсли; 
				Если СтрокаГТД.Количество=0 Тогда
					ТаблицаГТДПартий.Удалить(СтрокаГТД);
				КонецЕсли; 
				Если СтрокаПартия.Количество=0 Тогда
					Прервать;
				КонецЕсли; 
				Если СтрокаТовар.Количество=0 Тогда
					Прервать;
				КонецЕсли; 
			КонецЦикла;
			Если СтрокаПартия.Количество=0 Тогда
				ТаблицаПартий.Удалить(СтрокаПартия);
			Иначе
				СтрокаПартия.Количество=?(ВозвратТоваров, -1, 1) * СтрокаПартия.Количество;
			КонецЕсли; 
			Если СтрокаТовар.Количество=0 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		//Если еще остались ГТД для списания, то возможно уходим в минус
		Если СтрокаТовар.Количество>0 И ЕстьГТД И НЕ обЗначениеНеЗаполнено(СтрокаТовар.ГТД) Тогда
			//Если ВыводитьКритическиеСообщения Тогда
				Если обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" не распределился по ГТД <"+СтрокаТовар.ГТД+"> !","Важное&ГТД партий товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				Иначе
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ДокументОбъект,Истина)+"] Товар """+СокрЛП(СтрокаТовар.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТовар.ХарактеристикаНоменклатуры)+""" не распределился по ГТД <"+СтрокаТовар.ГТД+"> !","Важное&ГТД партий товаров компании:",ДокументОбъект,СтрокаТовар.Номенклатура);
				КонецЕсли; 
			//КонецЕсли; 
			Если НЕ ОтрицательныеОстаткиРазрешены Тогда
				ВсеОК=Ложь;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	// обнуляем переменные
	ТаблицаТовары=Неопределено;
	ТаблицаПартий=Неопределено;
	ГраницаРасчетаОстатков=Неопределено;
	СкладКомпании=Неопределено;
	ВозвратТоваров=Неопределено;
	// убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	ШапкаДокумента=Неопределено;
	
	Возврат ВсеОК;
КонецФункции

// Предопределенная процедура при записи
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()


ТаблицаВозвратТоваров=Неопределено;
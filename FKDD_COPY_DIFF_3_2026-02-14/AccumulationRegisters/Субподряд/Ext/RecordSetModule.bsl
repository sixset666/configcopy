Перем ДокументОбъект Экспорт;            // документ объект
Перем ДатаВыполнения Экспорт;			 // дата выполнения субподряда
Перем РезультатЗапросаПоРаботам Экспорт; // результат запроса по работам

//Фиксируем движения по субподрядным работам
Функция Движение(РеализацияПоСубподряду) Экспорт
	Результат = Истина;
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл, ДокументОбъект.Дата);
	КурсРегл  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ДокументОбъект.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр, ДокументОбъект.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ДокументОбъект.КурсВалютыУпр;
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из РезультатЗапросаПоРаботам Цикл
		НоваяЗапись=Добавить();
		НоваяЗапись.Период = ?(ДатаВыполнения=Неопределено,ДокументОбъект.Дата,ДатаВыполнения);
		НоваяЗапись.Регистратор = ДокументОбъект.Ссылка;
		НоваяЗапись.ПодразделениеКомпании = ДокументОбъект.ПодразделениеКомпании;
		НоваяЗапись.Контрагент = СтрокаТЧ.Контрагент;
		НоваяЗапись.ДоговорВзаиморасчетов = СтрокаТЧ.ДоговорВзаиморасчетов;
		НоваяЗапись.ЗаказНаряд = СтрокаТЧ.ЗаказНаряд;
		НоваяЗапись.Работа = СтрокаТЧ.Работа;
		
		Если РеализацияПоСубподряду Тогда
			НоваяЗапись.Сумма = обПересчет(СтрокаТЧ.СуммаВсего,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл);
			НоваяЗапись.СуммаНДС = обПересчет(СтрокаТЧ.СуммаНДС,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл);
			НоваяЗапись.СуммаУпр = обПересчет(СтрокаТЧ.СуммаВсего,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр);
			НоваяЗапись.Себестоимость = 0;
			НоваяЗапись.СуммаНДСВходящий = 0;
			НоваяЗапись.СебестоимостьУпр = 0;
		Иначе
			НоваяЗапись.Сумма = 0;
			НоваяЗапись.СуммаНДС = 0;
			НоваяЗапись.СуммаУпр = 0;
			НоваяЗапись.Себестоимость = обПересчет(СтрокаТЧ.СуммаВсего,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл);
			НоваяЗапись.СуммаНДСВходящий = обПересчет(СтрокаТЧ.СуммаНДС,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаРегл,КурсРегл);
			НоваяЗапись.СебестоимостьУпр = обПересчет(СтрокаТЧ.СуммаВсего,ДокументОбъект.ВалютаДокумента,ДокументОбъект.КурсДокумента,ВалютаУпр,КурсУпр);
		КонецЕсли; 
		
		НоваяЗапись.ХозОперация = ДокументОбъект.ХозОперация;
	КонецЦикла; 
	
	// Убиваем циклическую ссылку
	ДокументОбъект=Неопределено;
	ДатаВыполнения=Неопределено;
	ЗаказНаряд=Неопределено;
	Контрагент=Неопределено;
	ДоговорВзаиморасчетов=Неопределено;
	РезультатЗапросаПоРаботам=Неопределено;

	Возврат Результат;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
	
КонецПроцедуры	//	ПриЗаписи()

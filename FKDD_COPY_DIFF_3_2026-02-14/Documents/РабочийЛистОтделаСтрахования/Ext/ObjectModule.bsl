// Модуль документа РабочийЛистОтделаСтрахования

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;					// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;		// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     	// Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/атрикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт;	// Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
Перем СписокСтатусов Экспорт;			// Список статусов рабочих листов

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА


// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Возврат Результат;	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РабочийЛистОтделаСтрахования","Рабочий лист отдела страхования",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Страхователь" Тогда
		Результат	= дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		#Если Клиент Тогда
		КодРегиона = "";
		КодГорода = "";
		НомерТелефона = "";
		ВнутреннийНомер = "";
		ПредставлениеТелефона = "";
		ВидТелефона = Неопределено;
		Если ЗначениеЗаполнено(Страхователь) Тогда
			Телефоны = киПолучитьСписокТелефоновОбъекта(Страхователь);
			Если Телефоны<>Неопределено И Телефоны.Количество()>0 Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект,Телефоны[0].Значение);
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		
		Возврат Результат;

	ИначеЕсли Имя="ВариантыСтрахования.ПрограммаСтрахования" Тогда
		Результат	= Истина;
		// Вид страхования и страховую компанию следует получить из программы страхования
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ПрограммаСтрахования) Тогда
			ТекСтрока.ВидСтрахования	= ТекСтрока.ПрограммаСтрахования.ВидСтрахования;
			ТекСтрока.Страховщик		= ТекСтрока.ПрограммаСтрахования.СтраховаяКомпания;
			ТекСтрока.ПроцентКомиссии	= ТекСтрока.ПрограммаСтрахования.Вознаграждение; 
			Комитент 					= ТекСтрока.ПрограммаСтрахования.СтраховаяКомпания;  //БизТех 11.08.2025
		КонецЕсли;
		Возврат Результат;
	ИначеЕсли Имя="ВариантыСтрахования.ВидСтрахования" Тогда
		Результат	= Истина;
		// Программа страхования должна соответствовать виду страхования
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ВидСтрахования) И
			НЕ обЗначениеНеЗаполнено(ТекСтрока.ПрограммаСтрахования) И
			Текстрока.ВидСтрахования<>ТекСтрока.ПрограммаСтрахования.ВидСтрахования Тогда
			ТекСтрока.ПрограммаСтрахования	= Справочники.ПрограммыСтрахования.ПустаяСсылка();
			//misha
			ТекСтрока.БланкПолиса	= Справочники.ТТ_БланкиКСО.ПустаяСсылка();
			//misha
		КонецЕсли;
		Возврат Результат;
	ИначеЕсли Имя="ВариантыСтрахования.Страховщик" Тогда
		Результат	= Истина;
		// Страховщик должна соответствовать виду страхования
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Страховщик) И
			НЕ обЗначениеНеЗаполнено(ТекСтрока.ПрограммаСтрахования) И
			Текстрока.Страховщик<>ТекСтрока.ПрограммаСтрахования.СтраховаяКомпания Тогда
			ТекСтрока.ПрограммаСтрахования	= Справочники.ПрограммыСтрахования.ПустаяСсылка();
		КонецЕсли;
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.СтраховаяСумма" Тогда
		Результат	= Истина;
		
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры 			= Новый Структура;
		КонецЕсли;
		Если ТекСтрока.Тариф>0 Тогда
			ДопПараметры.Вставить("РассчитыватьСуммуПремии",Ложь);
			ТекСтрока.СуммаПремии	= ТекСтрока.СтраховаяСумма*ТекСтрока.Тариф/100;
			Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаПремии",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		Если ТекСтрока.СуммаБрутто=0 Тогда
			ТекСтрока.СуммаБрутто	= ТекСтрока.СтраховаяСумма;
			Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаБрутто",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			ТекСтрока.СуммаНетто	= 0;
		КонецЕсли;
		
		Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаСкидкиСтроки",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.Тариф" Тогда	
		Результат	= Истина;
		
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры 			= Новый Структура;
		КонецЕсли;
		Если ТекСтрока.СтраховаяСумма>0 Тогда
			ДопПараметры.Вставить("РассчитыватьСуммуПремии",Ложь);
			ТекСтрока.СуммаПремии	= ТекСтрока.СтраховаяСумма*ТекСтрока.Тариф/100;
			Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаПремии",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.СуммаНетто" Тогда
		Результат	= Истина;
		
		ТекСтрока.СуммаКомиссии	= ТекСтрока.СуммаБрутто - ТекСтрока.СуммаНетто - ТекСтрока.СуммаСкидкиСтроки;
		
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.СуммаБрутто" Тогда
		Результат	= Истина;
		
		ТекСтрока.СуммаНетто	= ТекСтрока.СуммаБрутто - Окр(ТекСтрока.СуммаБрутто*ТекСтрока.ПроцентКомиссии/100,2);
		Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаНетто",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
		ТекСтрока.СуммаКомиссии = ТекСтрока.СуммаБрутто - ТекСтрока.СуммаНетто - ТекСтрока.СуммаСкидкиСтроки;
		
		РассчитыватьСуммуПремии	= Истина;
		Если ТипЗнч(ДопПараметры)=Тип("Структура") И ДопПараметры.Свойство("РассчитыватьСуммуПремии") Тогда
			РассчитыватьСуммуПремии	= ДопПараметры.РассчитыватьСуммуПремии;	
		КонецЕсли;
		
		Если РассчитыватьСуммуПремии Тогда
			ТекСтрока.СуммаПремии	= ТекСтрока.СуммаБрутто - ТекСтрока.СуммаСкидкиСтроки;
		КонецЕсли;
	
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.ПроцентКомиссии" Тогда
		Результат	= Истина;
		Если ТекСтрока.ПроцентКомиссии>100 Тогда
			ТекСтрока.ПроцентКомиссии	= 100;
		КонецЕсли;
		Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаБрутто",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.СуммаКомиссии" Тогда
		Результат				= Истина;
		ТекСтрока.СуммаБрутто 	= ТекСтрока.СуммаКомиссии + ТекСтрока.СуммаНетто + ТекСтрока.СуммаСкидкиСтроки;
		Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаБрутто",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.СуммаПремии" Тогда
		Результат				= Истина;
		ТекСтрока.СуммаБрутто	= ТекСтрока.СуммаПремии + ТекСтрока.СуммаСкидкиСтроки;
		ТекСтрока.СуммаНетто	= 0;
		Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаБрутто",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.ПроцентСкидкиСтроки" Тогда	
		Результат	= Истина;
		#Если Клиент Тогда 			
		// Скидка не может быть выше 100%
		Если ТекСтрока.ПроцентСкидкиСтроки > 100 Тогда
			ТекСтрока.ПроцентСкидки = 100;	
		КонецЕсли;
		
		СуммаБезСкидки				= ТекСтрока.СуммаБрутто;
		ТекСтрока.СуммаСкидкиСтроки	= Окр(СуммаБезСкидки * ТекСтрока.ПроцентСкидкиСтроки / 100, 2);
		
		Если ТипЗнч(ДопПараметры) <> Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитыватьПроцент", Ложь);
			
		Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаСкидкиСтроки",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;			
		#КонецЕсли
		Возврат Результат;
		
	ИначеЕсли Имя="ВариантыСтрахования.СуммаСкидкиСтроки" Тогда
		Результат	= Истина;
		#Если Клиент Тогда		 						
			// Проверим дополнительные параметры, возможно пересчитывать процент не нужно. 			
			Попытка // Определим необходимость обратного расчета процента скидки.
				ПересчитыватьПроцент = ДопПараметры.ПересчитыватьПроцент;
			Исключение 
				ПересчитыватьПроцент = Истина;
			КонецПопытки;
			
			Если ПересчитыватьПроцент Тогда
				СуммаБезСкидки = ТекСтрока.СуммаБрутто;
				ТекСтрока.ПроцентСкидкиСтроки = ?(СуммаБезСкидки = 0, 0, Окр(ТекСтрока.СуммаСкидкиСтроки * 100 / СуммаБезСкидки,2));
			КонецЕсли;
			Результат=ОбработкаРеквизита("ВариантыСтрахования.СуммаБрутто",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		#КонецЕсли				
		Возврат Результат;
		
	Иначе	
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Функция возвращает сотрудника на основании переданного пользователя
//
Функция ПолучитьСотрудникаПоПользователю(ТекущийПользователь) Экспорт
	ТекущийСотрудник	= Справочники.Сотрудники.ПустаяСсылка();
	
	Если НЕ обЗначениеНеЗаполнено(ТекущийПользователь) И НЕ ПустаяСтрока(ТекущийПользователь.Наименование) Тогда
		Если обЗначениеНеЗаполнено(ТекущийПользователь.Сотрудник) Тогда
			ТекущийСотрудник	= Справочники.Сотрудники.НайтиПоНаименованию(ТекущийПользователь.Наименование, Истина);
			Если обЗначениеНеЗаполнено(ТекущийСотрудник) Тогда
				// Создадим нового сотрудника
				СотрудникОбъект	= Справочники.Сотрудники.СоздатьЭлемент();
				СотрудникОбъект.УстановитьНовыйКод();
				СотрудникОбъект.Наименование	= ТекущийПользователь.Наименование;
				СотрудникОбъект.Организация= ПараметрыСеанса.Организация;
				СотрудникОбъект.Подразделение	= ПараметрыСеанса.ПодразделениеКомпании;
				СотрудникОбъект.Должность		= Справочники.Должности.Неопределена;
				Попытка
					СотрудникОбъект.Записать();
					ТекущийСотрудник	= СотрудникОбъект.Ссылка;
				Исключение КонецПопытки;
			КонецЕсли;
		Иначе
			ТекущийСотрудник	= ТекущийПользователь.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущийСотрудник;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события при установке новго номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)

	ХозОперация	= Справочники.ХозОперации.РабочийЛистОтделаСтрахования;
	
	Если обЗначениеНеЗаполнено(ТипЦен) Тогда
    	ТипЦен=обПраво("ОсновнойТипЦенПродажи", Права,,ЭтотОбъект)
	КонецЕсли; 
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	// Ввод на основании рабочего листа
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.РабочийЛист") Тогда
		МенеджерОтделаСтрахования	= Основание.Менеджер;
		ПричинаОтказа				= Основание.ПричинаОтказа;
		ВидОплаты					= Основание.ВидОплаты;
		
		ВидТелефона 				= Основание.ВидТелефона;
		КодРегиона					= Основание.КодРегиона;
		КодГорода					= Основание.КодГорода;	
		НомерТелефона				= Основание.НомерТелефона;
		ВнутреннийНомер				= Основание.ВнутреннийНомер;
		ПредставлениеТелефона		= Основание.ПредставлениеТелефона;	
		
		Страхователь				= Основание.Контрагент;
		
		Если НЕ обЗначениеНеЗаполнено(Основание.Автомобиль) Тогда
			НоваяСтрока						= ВариантыСтрахования.Добавить();
			НоваяСтрока.ОбъектСтрахования	= Основание.Автомобиль;
		КонецЕсли;
//{{MRG[ <-> ]
//		ВидПервичногоКонтакта = Перечисления.ВидыПервичногоКонтакта.СмежныйОтдел;
//	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.Событие") Тогда
//		Страхователь = Основание.Контрагент;
//}}MRG[ <-> ]
КонецЕсли;  

//БизТех Аляль 28.12.2024г <<       
Если  ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда   
	Страхователь					= Основание.Контрагент; 
	МенеджерОтделаСтрахования		= Основание.Менеджер;  
	Менеджер                    	= Основание.Автор.Сотрудник;
	БТ_ДокЗН 						= Основание; 
	ПредставлениеТелефона			= Основание.Контрагент.ОсновнойТелефон;
	ДокументОснование				= Основание.ДокументОснование;
	СтраховаяКомпания 				= НайтиКонтрагента(Основание.Организация.ИНН, Основание.Организация.КПП);  
	Комитент						= СтраховаяКомпания;
	
	НоваяСтрока						= ВариантыСтрахования.Добавить();
	НоваяСтрока.ОбъектСтрахования	= Основание.Автомобиль.Модель;	
	НоваяСтрока.ВидСтрахования 		= Перечисления.ВидыСтрахования.ДополнительноеОборудование; 
	НоваяСтрока.СуммаБрутто         = Основание.СуммаДокумента;  
	НоваяСтрока.СтоимостьАвтомобиля = Основание.ДокументОснование.СуммаДокумента;
	
	Если ЗначениеЗаполнено(СтраховаяКомпания) Тогда
		НоваяСтрока.Страховщик      		= СтраховаяКомпания;
		НоваяСтрока.ПрограммаСтрахования 	= Справочники.ПрограммыСтрахования.НайтиПоРеквизиту("СтраховаяКомпания", СтраховаяКомпания); 
		ОбработкаРеквизита("ВариантыСтрахования.ПрограммаСтрахования", НоваяСтрока)
	КонецЕсли; 
//	ТекСтрока	= ЭлементыФормы.ВариантыСтрахования.ТекущаяСтрока;
	ОбработкаРеквизита("ВариантыСтрахования.СуммаБрутто", НоваяСтрока);
КонецЕсли; 
//БизТех >>
КонецПроцедуры // ОбработкаЗаполнения()

Функция НайтиКонтрагента(ИНН, КПП)
	
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.КПП = &КПП
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("ИНН", СокрЛП(ИНН));
		Запрос.УстановитьПараметр("КПП", СокрЛП(КПП));
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		Иначе
			Возврат Неопределено;
		КонецЕсли;

КонецФункции
// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)  
	//БизТех Аляль 30.06.2025г проверка ДоговораКомитента
    Если ЭтоНовый() или НЕ ЗначениеЗаполнено(ДоговорКомитента) Тогда
		Запрос = Новый запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ДоговорыВзаиморасчетов.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.ДоговорыВзаиморасчетов КАК ДоговорыВзаиморасчетов
		               |ГДЕ
		               |	ДоговорыВзаиморасчетов.Владелец = &Владелец
		               |	И ДоговорыВзаиморасчетов.ПризнакАгента = &ПризнакАгента
		               |	И ДоговорыВзаиморасчетов.ДатаНачала <= &ДатаДок
		               |	И (ДоговорыВзаиморасчетов.ДатаКонца >= &ДатаДок
		               |			ИЛИ ДоговорыВзаиморасчетов.ДатаКонца = ДАТАВРЕМЯ(1, 1, 1))
		               |	И ДоговорыВзаиморасчетов.Организация = &Организация";     
		Запрос.УстановитьПараметр("Владелец", ЭтотОбъект.ВариантыСтрахования[0].Страховщик);  
		Запрос.УстановитьПараметр("Организация", ЭтотОбъект.Организация);
		Запрос.УстановитьПараметр("ПризнакАгента", Перечисления.ПризнакиАгента.Агент);
		Запрос.УстановитьПараметр("ДатаДок", ЭтотОбъект.Дата);
		Рез = Запрос.Выполнить().Выбрать();
		Если Рез.Следующий() Тогда
			ДоговорКомитента = Рез.Ссылка;
		Иначе
			Сообщить("Внимание! Не найден действующий договор комитента с признаком Агент по Страховой компании! Документ не записан! Создайте новый договор комитента!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	//->Diginova _Акт сверки кред,страх_ 07.12.2021
	Записи = РегистрыСведений.DN_АВ_КредитноСтраховыеСделки.СоздатьНаборЗаписей();
	Записи.Отбор.Регистратор.Установить(Ссылка);
	Записи.Прочитать();
	Если Записи.Количество() тогда
		Для Каждого СтрокаЗаписи из Записи цикл
			Если ЗначениеЗаполнено(СтрокаЗаписи.АктСверки) тогда
				Сообщить("Для модификации документа, распроведите " + СтрокаЗаписи.АктСверки + " в котором содержится данный кредитный лист");
				Отказ = Истина; 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//<-Diginova _FlaiM_
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Обработчик события удаления проведения
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события проведения
//
Процедура ОбработкаПроведения(Отказ, Режим)

	//Bizteh++ 14.10.2022 убираем обмен с Оптима МК, т.к. с 01.10.2022 КСО в базе ФКДД
	//->Diginova _Настройка базы_ 05.01.2022  
	//Если Не Отказ и Константы.DN_ЗапуститьОбмен.Получить() тогда
	//	УИД = Ссылка.УникальныйИдентификатор();
	//	Отправлен = DN_HTTP_Обмен.ОтправитьОбъект(Ссылка,УИД);
	//КонецЕсли;
	//<-Diginova _FlaiM_    
	//Bizteh--
	ЭтотОбъект.Записать();  //Bizteh 09.02.2023 для записи Договора КСО  
	// БизТех 04.04.2023 проверка принадлежности договора страхователю <<
	Если ЗначениеЗаполнено(ЭтотОбъект.ДоговорКСО) Тогда
		Если ЭтотОбъект.Страхователь<>ЭтотОбъект.ДоговорКСО.Владелец Тогда
			Сообщить("Договор КСО не принадлежит страхователю! Сначала очистите поле ""Договор КСО"".");
			Отказ = Истина;  
		КонецЕсли;
	КонецЕсли; 
	//>>

	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Проведение по регистру сведений Журнал событий
	Если НЕ Отказ Тогда
		Если НЕ орЖурналСостояний(Ссылка, Статус) Тогда
			Отказ=Истина;
			Возврат;
		КонецЕсли; 
	КонецЕсли;	
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	//misha
	Для Каждого Стр ИЗ ВариантыСтрахования Цикл
		Движение = Движения.ТТ_СкладБланковКСО.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = СкладБланкаКСО;
		Движение.Бланк = Стр.БланкПолиса;
		Движение.ВидСтрахования = Стр.ВидСтрахования;
		Движение.Количество = 1;
	КонецЦикла;
	
	//->Diginova _Акт сверки кред,страх_ 30.11.2021
	Если НЕ Отказ тогда
		
		Движения.DN_АВ_КредитноСтраховыеСделки.Записывать = Истина;
		Движения.DN_АВ_КредитноСтраховыеСделки.Очистить();
		
		Для Каждого Строка из ВариантыСтрахования Цикл
					
			Запись = Движения.DN_АВ_КредитноСтраховыеСделки.Добавить();	
			Запись.Период = Дата;
			Запись.Статус = Статус;
			Запись.ХозОперация = Справочники.ХозОперации.АктСверкиСтраховой;
			Запись.Организация = Организация;
			Запись.ПодразделенияКомпании = ПодразделениеКомпании;
			Запись.Программа = Строка.ПрограммаСтрахования;
			Запись.Контрагент = Строка.Страховщик;
			Запись.ДоговорыВзаиморасчетов = Строка.ПрограммаСтрахования.Договор;
			Запись.АВЛиста = Строка.СуммаКомиссии;
					
		КонецЦикла;
	КонецЕсли;
	//<-Diginova _FlaiM_
	
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;

// Список статусов рабочего листа
СписокСтатусов	= Новый СписокЗначений;
ЗапросСтатусов		= Новый Запрос();
		ЗапросСтатусов.Текст= "ВЫБРАТЬ
		                      |	СтатусыРабочегоЛиста.Ссылка КАК Ссылка,
		                      |	СтатусыРабочегоЛиста.Наименование КАК Наименование
		                      |ИЗ
		                      |	Справочник.СтатусыРабочегоЛиста КАК СтатусыРабочегоЛиста
		                      |ГДЕ
		                      |	СтатусыРабочегоЛиста.ИспользоватьВРабочемЛистеОтделаСтрахования
		                      |	И НЕ СтатусыРабочегоЛиста.ПометкаУдаления
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	СтатусыРабочегоЛиста.ПорядокВОтчете УБЫВ";
ВыборкаСтатусов	= ЗапросСтатусов.Выполнить().Выбрать();
Пока ВыборкаСтатусов.Следующий() Цикл
	СписокСтатусов.Добавить(ВыборкаСтатусов.Ссылка);	
КонецЦикла;

#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли

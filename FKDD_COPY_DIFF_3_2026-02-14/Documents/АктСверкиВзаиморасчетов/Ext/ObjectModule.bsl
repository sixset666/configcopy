// Модуль документа АктСверкиВзаиморасчетов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

#Если Клиент Тогда

// Производит очистку табличной части
//
Функция ОчиститьТабличнуюЧасть(ЭтаФорма) Экспорт
	
	Если (ПоДаннымОрганизации.Количество()>0 ИЛИ ПоДаннымКонтрагента.Количество()>0) И Вопрос("Все строки из таблицы будут удалены !
		|Продолжить ?",РежимДиалогаВопрос.ДаНет,,,"Очистка таблицы")<>КодВозвратаДиалога.Да Тогда 
		Возврат Ложь;
	КонецЕсли;
	ПоДаннымОрганизации.Очистить();
	ПоДаннымКонтрагента.Очистить();
	СуммаДокументаПриход = 0;
	СуммаДокументаРасход = 0;
	ОстатокНаНачало		 = 0;
	ЭтаФорма.ЭлементыФормы.ОстатокНаНачалоКонтрагент.Значение = 0;
	ЭтаФорма.ЭлементыФормы.ОстатокНаКонец.Значение = 0;
	ЭтаФорма.ЭлементыФормы.ОстатокНаКонецКонтрагент.Значение = 0;
	ЭтаФорма.ЭлементыФормы.Расхождение.Значение = 0;
	ЭтаФорма.ЭлементыФормы.РасхождениеКонтрагент.Значение = 0;
	
	Возврат Истина;
КонецФункции //ОчиститьТабличнуюЧасть()

#КонецЕсли					 

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныйСостав=Новый Структура(); 
	//anton
	//ОбязательныйСостав.Вставить("Дата", 3);
	//ОбязательныйСостав.Вставить("Документ",3); 
	//anton
	ОбязательныйСостав.Вставить("Представление", 2);
	ОбязательныйСостав.Вставить("ДоговорВзаиморасчетов", ?(обЗначениеНеЗаполнено(ДоговорВзаиморасчетов),3,2));
	ОбязательныйСостав.Вставить("Сделка", 2);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	Если Не обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов", 1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);                                            
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);                                           
	ОбязательныеРеквизиты.Вставить("ДатаОкончания",1);                                         
	ОбязательныеРеквизиты.Вставить("ПоДаннымОрганизации",ОбязательныйСостав);                  
	ОбязательныеРеквизиты.Вставить("ПоДаннымКонтрагента",ОбязательныйСостав);                  
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
				
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению",
	// то дополнительно осуществляется проверка соответствия подразделений.
	
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует); 
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации); 		
		
		Если БалансВедетсяПоОрганизации	Или Не КонтрольОтсутствует Тогда
			// Выясним, ведется ли контроль по организации и подразделению, или только по организации.
			КонтрольВедетсяПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);			
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			// Контроль организации договора из шапки документа для баланса не интересен.
			Если Не КонтрольОтсутствует Тогда
				КонтролируемыеРеквизитыШапки = Новый Структура();
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",  КонтрольВедетсяПоПодразделению);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			КонецЕсли;
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			КонтролируемыеРеквизитыСостава.Вставить("ДоговорВзаиморасчетов", КонтрольВедетсяПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("ПоДаннымОрганизации",КонтролируемыеРеквизитыСостава);
			ТабличныеЧасти.Вставить("ПоДаннымКонтрагента",КонтролируемыеРеквизитыСостава);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("АктСверкиВзаиморасчетов", "Акт сверки взаиморасчетов",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка			- Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма				- Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="" Тогда
		//Для приватной обработки изменения реквизитов
		
	ИначеЕсли Имя="КурсДокумента" Тогда
		СуммаДокументаПриход = 0;
		СуммаДокументаРасход = 0;
		ИспользоватьДоговорШапки = Не обЗначениеНеЗаполнено(ДоговорВзаиморасчетов);
		Для Каждого ТекСтрока Из ПоДаннымОрганизации Цикл
			ТекВалюта = ?(ИспользоватьДоговорШапки, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов);
			СуммаДокументаПриход = СуммаДокументаПриход + обПересчет(ТекСтрока.УвеличениеДолга, ТекВалюта, Дата, ВалютаДокумента, КурсДокумента);
			СуммаДокументаРасход = СуммаДокументаРасход + обПересчет(ТекСтрока.УменьшениеДолга, ТекВалюта, Дата, ВалютаДокумента, КурсДокумента);
		КонецЦикла;
		
		СуммаДокументаПриходК = 0;
		СуммаДокументаРасходК = 0;
		Для Каждого ТекСтрока Из ПоДаннымКонтрагента Цикл
			ТекВалюта = ?(ИспользоватьДоговорШапки, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов);
			СуммаДокументаПриходК = СуммаДокументаПриходК + обПересчет(ТекСтрока.УвеличениеДолга, ТекВалюта, Дата, ВалютаДокумента, КурсДокумента);
			СуммаДокументаРасходК = СуммаДокументаРасходК + обПересчет(ТекСтрока.УменьшениеДолга, ТекВалюта, Дата, ВалютаДокумента, КурсДокумента);
		КонецЦикла;
		
		ОстатокНаКонец = СуммаДокументаПриход -  СуммаДокументаРасход+ОстатокНаНачало;
		ОстатокНаКонецКонтрагент = СуммаДокументаПриходК - СуммаДокументаРасходК-ОстатокНаНачало;
		Расхождение = ОстатокНаКонец + ОстатокНаКонецКонтрагент;
		
		#Если Клиент Тогда
			ЭтаФорма.ЭлементыФормы.ОстатокНаНачалоКонтрагент.Значение = - ОстатокНаНачало;
			ЭтаФорма.ЭлементыФормы.ОстатокНаКонец.Значение = ОстатокНаКонец;
			ЭтаФорма.ЭлементыФормы.ОстатокНаКонецКонтрагент.Значение = ОстатокНаКонецКонтрагент;
			ЭтаФорма.ЭлементыФормы.Расхождение.Значение = Расхождение;
			ЭтаФорма.ЭлементыФормы.РасхождениеКонтрагент.Значение = -Расхождение;
		#КонецЕсли
		
		Возврат ИСТИНА;
		
	ИначеЕсли Имя="Организация" Тогда	
		#Если Клиент Тогда
			Если ЭтаФорма.ОрганизацияПодразделение[0].Значение <> Организация Тогда
				Если Не ОчиститьТабличнуюЧасть(ЭтаФорма) Тогда
					Организация = ЭтаФорма.ОрганизацияПодразделение[0].Значение;
				КонецЕсли;
			КонецЕсли;
		#КонецЕсли
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ИСТИНА;
		
	ИначеЕсли Имя="Контрагент" Тогда
		ДоговорВзаиморасчетовИзменен=Ложь;
		Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов)) И ДоговорВзаиморасчетов.Владелец<>Контрагент Тогда
			ДоговорВзаиморасчетов=Неопределено;
			ДоговорВзаиморасчетовИзменен=Истина;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент,, ЭтотОбъект);
			ДоговорВзаиморасчетовИзменен = Истина;
		КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ДоговорВзаиморасчетовИзменен Тогда
			Рез=дкОбработкаРеквизита(ЭтотОбъект,"ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Документ" Тогда
		// Сформируем строку представления
		Если обЗначениеНеЗаполнено(ТекСтрока.Документ) Тогда
			ТекСтрока.Представление = "";
			ТекСтрока.Дата = '00010101';
		Иначе
			МетаданныеТекДокумента = ТекСтрока.Документ.Метаданные();
			НомерДокумента = ?(МетаданныеТекДокумента.Реквизиты.Найти("ВхДокНомер") <> Неопределено И Не обЗначениеНеЗаполнено(ТекСтрока.Документ.ВхДокНомер), ТекСтрока.Документ.ВхДокНомер, ТекСтрока.Документ.Номер);
			ДатаДокумента  = ?(МетаданныеТекДокумента.Реквизиты.Найти("ВхДокДата") <> Неопределено И Не обЗначениеНеЗаполнено(ТекСтрока.Документ.ВхДокДата), ТекСтрока.Документ.ВхДокДата, ТекСтрока.Документ.Дата);
			ТекСтрока.Представление = МетаданныеТекДокумента.Синоним + " № " + НомерДокумента + " от " + Формат(ДатаДокумента, "ДФ='дд ММ гггг'");
			ТекСтрока.Дата = ТекСтрока.Документ.Дата;
			
			Если обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) И обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
				Попытка
					ТекСтрока.ДоговорВзаиморасчетов = ТекСтрока.Документ.ДоговорВзаиморасчетов;
				Исключение
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли; 
		Возврат ИСТИНА;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
	
	// Формирует печатную форму "КорректировкаДолга"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
Функция ПечатьАктСверки(ТабДокумент) Экспорт
	
	Макет  = ПолучитьМакет("АктСверки");
	// Установим ориентацию страницы для нормального разбиения на страницы
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Настроим макет
	ОтобразитьСделку=Истина;
	ОтобразитьСделкуКонтрагента=Истина;
	МассивПустыхСсылок= Новый Массив;
	МассивПустыхСсылок.Добавить(ПланыВидовХарактеристик.ТипыСделок.ПустаяСсылка());
	МассивПустыхСсылок.Добавить(Неопределено);
	Для Каждого Документ Из Документы Цикл
		МассивПустыхСсылок.Добавить(Документ.ПустаяСсылка());	
	КонецЦикла;	
		
	// Определяем есть ли сделки по организации
	ЗапросПоСделке= Новый Запрос;
	ЗапросПоСделке.Текст="ВЫБРАТЬ
	                     |	ЕСТЬNULL(АктСверкиВзаиморасчетовПоДаннымОрганизации.Сделка, 0) КАК Поле1
	                     |ИЗ
	                     |	Документ.АктСверкиВзаиморасчетов.ПоДаннымОрганизации КАК АктСверкиВзаиморасчетовПоДаннымОрганизации
	                     |ГДЕ
	                     |	АктСверкиВзаиморасчетовПоДаннымОрганизации.Ссылка = &ДокументСсылка
	                     |	И (НЕ АктСверкиВзаиморасчетовПоДаннымОрганизации.Сделка В (&МассивПустыхСсылок))";
	
						 
	ЗапросПоСделке.УстановитьПараметр("ДокументСсылка",Ссылка);
	ЗапросПоСделке.УстановитьПараметр("МассивПустыхСсылок",МассивПустыхСсылок);
	РезультатЗапроса=ЗапросПоСделке.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбластьСделка = Макет.Область("СделкаПоОрганизации");
		ОбластьСтрокаТЧ=Макет.Область("Строка");
		ОтобразитьСделку=Ложь;
		Макет.УдалитьОбласть(ОбластьСделка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	
	// Определяем есть ли сделки по контрагенту
	ЗапросПоСделке.Текст="ВЫБРАТЬ
	                     |	АктСверкиВзаиморасчетовПоДаннымКонтрагента.Сделка
	                     |ИЗ
	                     |	Документ.АктСверкиВзаиморасчетов.ПоДаннымКонтрагента КАК АктСверкиВзаиморасчетовПоДаннымКонтрагента
	                     |ГДЕ
	                     |	АктСверкиВзаиморасчетовПоДаннымКонтрагента.Ссылка = &ДокументСсылка
	                     |	И (НЕ АктСверкиВзаиморасчетовПоДаннымКонтрагента.Сделка В (&МассивПустыхСсылок))";
						 
	ЗапросПоСделке.УстановитьПараметр("ДокументСсылка",Ссылка);
	ЗапросПоСделке.УстановитьПараметр("МассивПустыхСсылок",МассивПустыхСсылок);

	РезультатЗапроса=ЗапросПоСделке.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбластьСделка = Макет.Область("СделкаПоКонтрагенту");
		ОбластьСтрокаТЧ=Макет.Область("Строка");
		ОтобразитьСделкуКонтрагента=Ложь;
		Макет.УдалитьОбласть(ОбластьСделка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;

	// Строим печатную форму
	ОбластьЗаголовок    	= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьНачОстатки	   	= Макет.ПолучитьОбласть("НачОстатки");
	ОбластьИтогоПоСтранице	= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьСтрока	      	= Макет.ПолучитьОбласть("Строка");
	ОбластьОборотыИтог  	= Макет.ПолучитьОбласть("ОборотыИтог");
	ОбластьКонОстатки   	= Макет.ПолучитьОбласть("КонОстатки");
	ОбластьПодвал       	= Макет.ПолучитьОбласть("Подвал");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	НазваниеОрганизации = спПолучитьПредставление(Организация);
	НаименованиеКонтрагента = спПолучитьПредставление(Контрагент);
	
	ПредставительОрганизации = дкОтветственноеЛицо(ЭтотОбъект, "ПредставительОрганизации");
	ПредставительКонтрагента = дкОтветственноеЛицо(ЭтотОбъект, "ПредставительКонтрагента");
	
	Если обЗначениеНеЗаполнено(ПредставительОрганизации.ПредставительОрганизации) Тогда
		ДолжностьПредставителяОрганизации = "";
		ИмяПредставителяОрганизации = "";
	Иначе
		ДолжностьПредставителяОрганизации = ПредставительОрганизации.ПредставительОрганизацииДолжность;
		ИмяПредставителяОрганизации = ПредставительОрганизации.ПредставительОрганизацииПредставление;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ПредставительКонтрагента.ПредставительКонтрагента) Тогда
		ДолжностьПредставителяКонтрагента = "";
		ИмяПредставителяКонтрагента = "";
	Иначе
		ДолжностьПредставителяКонтрагента = ПредставительКонтрагента.ПредставительКонтрагентаДолжность;
		ИмяПредставителяКонтрагента = ПредставительКонтрагента.ПредставительКонтрагентаПредставление;
	КонецЕсли;
	
	ДатаНачалаПериода = ?(обЗначениеНеЗаполнено(ДатаНачала) И ПоДаннымОрганизации.Количество()>0,ПоДаннымОрганизации[0].Дата, ДатаНачала);
	ТекстЗаголовка = "взаимных расчетов за период с " + Формат(ДатаНачалаПериода, "ДФ=dd.MM.yyyy") + " по " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy") +  " между " + НазваниеОрганизации + " и " + НаименованиеКонтрагента;
	Если не обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
		ТекстЗаголовка = ТекстЗаголовка + Символы.ПС + " по договору " + СокрЛП(ДоговорВзаиморасчетов.Наименование);
	КонецЕсли;
	ОбластьЗаголовок.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	СтрЗаголовокТаблица = "Мы, нижеподписавшиеся, "
	+ ?(ДолжностьПредставителяОрганизации<>"",ДолжностьПредставителяОрганизации,"_______________________")+" "
	+ НазваниеОрганизации 
	+ " " + ?(ИмяПредставителяОрганизации<>"",ИмяПредставителяОрганизации,"__________________________________________") + ", с одной стороны, "
	+ "и " + ?(ДолжностьПредставителяКонтрагента<>"",ДолжностьПредставителяКонтрагента,"___________________________") 
	+ " " + НаименованиеКонтрагента + " " 
	+ ?(обЗначениеНеЗаполнено(ИмяПредставителяКонтрагента),"_____________________________________________",ИмяПредставителяКонтрагента) + ", с другой стороны, "
	+ "составили настоящий акт сверки в том, что состояние взаимных расчетов по данным учета следующее:";
	
	ОбластьЗаголовок.Параметры.СтрЗаголовокТаблица = СтрЗаголовокТаблица;
	
	ТабДокумент.Вывести(ОбластьЗаголовок);
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	//теперь выводим шапку
	ОбластьШапкаТаблицы.Параметры.НазваниеОрганизации = СокрЛП(Организация.НаименованиеПолное);
	ОбластьШапкаТаблицы.Параметры.НаименованиеКонтрагента = СокрЛП(Контрагент.НаименованиеПолное);
	ОбластьШапкаТаблицы.Параметры.ВалютаДокумента = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьНачОстатки.Параметры.СуммаНачальныйОстатокДт = ?(ОстатокНаНачало > 0, ОстатокНаНачало, 0);
	ОбластьНачОстатки.Параметры.СуммаНачальныйОстатокКт = ?(ОстатокНаНачало < 0, -ОстатокНаНачало, 0);
	ОбластьНачОстатки.Параметры.ВалютаДокумента = ВалютаДокумента;
	
	ТабДокумент.Вывести(ОбластьНачОстатки);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТаблицаДанныхОрганизации.Дата КАК Дата,
	               |	ТаблицаДанныхОрганизации.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ТаблицаДанныхОрганизации.Документ КАК Документ,
	               |	ТаблицаДанныхОрганизации.Представление КАК ДокументПредставление,
	               |	ТаблицаДанныхОрганизации.Сделка.Представление КАК Сделка,
	               |	ВЫБОР
	               |		КОГДА ТаблицаДанныхОрганизации.Ссылка.ДоговорВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.ДоговорыВзаиморасчетов.ПустаяСсылка)
	               |			ТОГДА ТаблицаДанныхОрганизации.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов
	               |		ИНАЧЕ ТаблицаДанныхОрганизации.Ссылка.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов
	               |	КОНЕЦ КАК Валюта,
	               |	ТаблицаДанныхОрганизации.УвеличениеДолга КАК СуммаОборотДт,
	               |	ТаблицаДанныхОрганизации.УменьшениеДолга КАК СуммаОборотКт,
	               |	ТаблицаДанныхОрганизации.НомерСтроки КАК НомерСтроки,
	               |	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаКонтр,
	               |	"""" КАК ДоговорВзаиморасчетовКонтр,
	               |	"""" КАК ДокументКонтр,
	               |	"""" КАК ДокументПредставлениеКонтр,
	               |	"""" КАК СделкаКонтр,
	               |	"""" КАК ВалютаКонтр,
	               |	0 КАК СуммаОборотДтКонтр,
	               |	0 КАК СуммаОборотКтКонтр
	               |ИЗ
	               |	Документ.АктСверкиВзаиморасчетов.ПоДаннымОрганизации КАК ТаблицаДанныхОрганизации
	               |ГДЕ
	               |	ТаблицаДанныхОрганизации.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ДАТАВРЕМЯ(1, 1, 1),
	               |	"""",
	               |	"""",
	               |	"""",
	               |	"""",
	               |	"""",
	               |	0,
	               |	0,
	               |	ТаблицаДанныхКонтрагента.НомерСтроки,
	               |	ТаблицаДанныхКонтрагента.Дата,
	               |	ТаблицаДанныхКонтрагента.ДоговорВзаиморасчетов,
	               |	ТаблицаДанныхКонтрагента.Документ,
	               |	ТаблицаДанныхКонтрагента.Представление,
	               |	ТаблицаДанныхКонтрагента.Сделка.Представление,
	               |	ВЫБОР
	               |		КОГДА ТаблицаДанныхКонтрагента.Ссылка.ДоговорВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.ДоговорыВзаиморасчетов.ПустаяСсылка)
	               |			ТОГДА ТаблицаДанныхКонтрагента.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов
	               |		ИНАЧЕ ТаблицаДанныхКонтрагента.Ссылка.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов
	               |	КОНЕЦ,
	               |	ТаблицаДанныхКонтрагента.УвеличениеДолга,
	               |	ТаблицаДанныхКонтрагента.УменьшениеДолга
	               |ИЗ
	               |	Документ.АктСверкиВзаиморасчетов.ПоДаннымКонтрагента КАК ТаблицаДанныхКонтрагента
	               |ГДЕ
	               |	ТаблицаДанныхКонтрагента.Ссылка = &Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки
	               |ИТОГИ
	               |	МАКСИМУМ(Дата),
	               |	МАКСИМУМ(ДоговорВзаиморасчетов),
				   
				   
				   
				   
	               |	МАКСИМУМ(Документ),
	               |	МАКСИМУМ(ДокументПредставление),
	               |	МАКСИМУМ(Сделка),
	               |	МАКСИМУМ(Валюта),
	               |	СУММА(СуммаОборотДт),
	               |	СУММА(СуммаОборотКт),
	               |	МАКСИМУМ(ДатаКонтр),
	               |	МАКСИМУМ(ДоговорВзаиморасчетовКонтр),
	               |	МАКСИМУМ(ДокументКонтр),
	               |	МАКСИМУМ(ДокументПредставлениеКонтр),
	               |	МАКСИМУМ(СделкаКонтр),
	               |	МАКСИМУМ(ВалютаКонтр),
	               |	СУММА(СуммаОборотДтКонтр),
	               |	СУММА(СуммаОборотКтКонтр)
	               |ПО
	               |	НомерСтроки";                                                            
	
	//доп. области
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьОборотыИтог);
	мсвДопОбластиПодвала.Добавить(ОбластьКонОстатки);
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	Запрос = Новый Запрос(ТекстЗапроса);                                         
	Запрос.УстановитьПараметр("Ссылка", Ссылка);                                 
	
	СтруктураИтоговПоСтранице = Новый Структура("СуммаОборотДт, СуммаОборотКт, СуммаОборотДтКонтр, СуммаОборотКтКонтр", 0, 0, 0, 0);
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоличествоСтрок = Выборка.Количество();
	НомерСтроки = 1;
	Пока Выборка.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		Если ОтобразитьСделку Тогда ОбластьСтрока.Параметры.Сделка=Лев(Выборка.Сделка,СтрДлина(Выборка.Сделка)-8); КонецЕсли;
		Если ОтобразитьСделкуКонтрагента Тогда ОбластьСтрока.Параметры.СделкаКонтр=Лев(Выборка.СделкаКонтр,СтрДлина(Выборка.СделкаКонтр)-8); КонецЕсли;
		ОбластьСтрока.Параметры.СуммаОборотДт = Формат(Выборка.СуммаОборотДТ,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаОборотДтКонтр = Формат(Выборка.СуммаОборотДТКонтр,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаОборотКТ = Формат(Выборка.СуммаОборотКТ,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаОборотКтКонтр = Формат(Выборка.СуммаОборотКТКонтр,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.Дата=Формат(Выборка.Дата, "ДФ=dd.MM.yyyy");
		ОбластьСтрока.Параметры.ДатаКонтр=Формат(Выборка.ДатаКонтр, "ДФ=dd.MM.yyyy");
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТаблицы, ОбластьИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, ?(НомерСтроки = КоличествоСтрок, мсвДопОбластиПодвала, Неопределено));
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("СуммаОборотДт,СуммаОборотКт,СуммаОборотДтКонтр,СуммаОборотКтКонтр");
		СтруктураСтрокиИтогов.СуммаОборотДт      = Выборка.СуммаОборотДт;
		СтруктураСтрокиИтогов.СуммаОборотКт      = Выборка.СуммаОборотКт;
		СтруктураСтрокиИтогов.СуммаОборотДтКонтр = Выборка.СуммаОборотДтКонтр;
		СтруктураСтрокиИтогов.СуммаОборотКтКонтр = Выборка.СуммаОборотКтКонтр;
		
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			СтруктураИтоговПоСтранице = Новый Структура("СуммаОборотДт, СуммаОборотКт, СуммаОборотДтКонтр, СуммаОборотКтКонтр", 0, 0, 0, 0);
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	ОбластьОборотыИтог.Параметры.СуммаОборотДт   = Формат(СуммаДокументаПриход,ФорматВыводаСуммы);
	ОбластьОборотыИтог.Параметры.СуммаОборотКт   = Формат(СуммаДокументаРасход,ФорматВыводаСуммы);
	ОбластьОборотыИтог.Параметры.ВалютаДокумента = ВалютаДокумента;
	
	Если СверкаСогласована тогда
		ОбластьОборотыИтог.Параметры.СуммаОборотДтКонтр   = Формат(СуммаДокументаПриходК,ФорматВыводаСуммы);
		ОбластьОборотыИтог.Параметры.СуммаОборотКтКонтр   = Формат(СуммаДокументаРасходК,ФорматВыводаСуммы);
		ОбластьОборотыИтог.Параметры.ВалютаДокументаКонтр = ВалютаДокумента;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьОборотыИтог);
	
	ОстатокНаКонец  = ОстатокНаНачало + СуммаДокументаПриход - СуммаДокументаРасход;
	ОстатокНаКонецК = СуммаДокументаПриходК - СуммаДокументаРасходК - ОстатокНаНачало;
	ОбластьКонОстатки.Параметры.СуммаКонечныйОстатокДт = Формат(?(ОстатокНаКонец>0, ОстатокНаКонец,0),ФорматВыводаСуммы);
	ОбластьКонОстатки.Параметры.СуммаКонечныйОстатокКт = Формат(?(ОстатокНаКонец<0,-ОстатокНаКонец,0),ФорматВыводаСуммы);
	ОбластьКонОстатки.Параметры.ВалютаДокумента		   = ВалютаДокумента;
	
	Если СверкаСогласована тогда
		ОбластьКонОстатки.Параметры.СуммаКонечныйОстатокДтКонтр = Формат(?(ОстатокНаКонецК>0, ОстатокНаКонецК,0),ФорматВыводаСуммы);
		ОбластьКонОстатки.Параметры.СуммаКонечныйОстатокКтКонтр = Формат(?(ОстатокНаКонецК<0,-ОстатокНаКонецК,0),ФорматВыводаСуммы);
		ОбластьКонОстатки.Параметры.ВалютаДокументаКонтр		= ВалютаДокумента;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьКонОстатки);
	
	// Результаты сверки
	Если ПоДаннымОрганизации.Количество() = 0 и ОстатокНаНачало = 0 Тогда
		РезультатыСверки = "<сверка не проведена>";
	ИначеЕсли обЗначениеНеЗаполнено(ДатаОкончания) Тогда
		РезультатыСверки = "<не указана дата сверки>";
	ИначеЕсли обЗначениеНеЗаполнено(Контрагент) Тогда
		РезультатыСверки = "<не указан контрагент>";
	Иначе
		РезультатыСверки = "на " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy") + " задолженность ";
		
		Если ОстатокНаКонец > 0 Тогда
			РезультатыСверки = РезультатыСверки + "в пользу " + СокрЛП(Организация.НаименованиеПолное) + " " + обЧислоПрописью(ОстатокНаКонец, ВалютаДокумента);
		ИначеЕсли ОстатокНаКонец < 0 Тогда
			РезультатыСверки = РезультатыСверки + "в пользу " + СокрЛП(Контрагент.НаименованиеПолное) + " " + обЧислоПрописью(-ОстатокНаКонец, ВалютаДокумента);
		Иначе
			РезультатыСверки = РезультатыСверки + "отсутствует.";
		КонецЕсли;
	КонецЕсли;
	ОбластьПодвал.Параметры.РезультатыСверки = РезультатыСверки;
	
	Если СверкаСогласована тогда
		ОбластьПодвал.Параметры.ПоДаннымКонтрагента = "По данным " + СокрЛП(Контрагент);
		
		Если ПоДаннымКонтрагента.Количество() = 0 и ОстатокНаНачало = 0 Тогда
			РезультатыСверки = "<сверка не проведена>";
		ИначеЕсли обЗначениеНеЗаполнено(ДатаОкончания) Тогда
			РезультатыСверки = "<не указана дата сверки>";
		ИначеЕсли обЗначениеНеЗаполнено(Контрагент) Тогда
			РезультатыСверки = "<не указан контрагент>";
		Иначе
			РезультатыСверки = "на " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy") + " задолженность ";
			Если ОстатокНаКонецК < 0 Тогда
				РезультатыСверки = РезультатыСверки + "в пользу " + СокрЛП(Организация.НаименованиеПолное) + " " + обЧислоПрописью(-ОстатокНаКонецК, ВалютаДокумента);
			ИначеЕсли ОстатокНаКонецК > 0 Тогда
				РезультатыСверки = РезультатыСверки + "в пользу " + СокрЛП(Контрагент.НаименованиеПолное) + " " + обЧислоПрописью(ОстатокНаКонецК, ВалютаДокумента);
			Иначе
				РезультатыСверки = РезультатыСверки + "отсутствует.";
			КонецЕсли;
		КонецЕсли;
		
		ОбластьПодвал.Параметры.РезультатыСверкиК = РезультатыСверки;
		Если ОстатокНаКонец+ОстатокНаКонецК<>0 Тогда
			ИтогСверки = "В результате сверки выявлено расхождение информации о состоянии расчетов в размере "+обЧислоПрописью(?(ОстатокНаКонец+ОстатокНаКонецК>0,1,-1)*(ОстатокНаКонец+ОстатокНаКонецК),  ВалютаДокумента);
			ОбластьПодвал.Параметры.ИтогСверки = Символы.ПС+ИтогСверки+Символы.ПС+" ";
		КонецЕсли; 
	КонецЕсли;
	
	ОбластьПодвал.Параметры.НазваниеОрганизации 	= СокрЛП(Организация.НаименованиеПолное);
	ОбластьПодвал.Параметры.НаименованиеКонтрагента = СокрЛП(Контрагент.НаименованиеПолное);
	ОбластьПодвал.Параметры.Организация				= СокрЛП(Организация.НаименованиеПолное);
	ОбластьПодвал.Параметры.Контрагент				= СокрЛП(Контрагент.НаименованиеПолное);
	ОбластьПодвал.Параметры.ДолжностьПредставление	= ?(обЗначениеНеЗаполнено(ДолжностьПредставителяОрганизации),"________________", СокрЛП(ДолжностьПредставителяОрганизации));
	ОбластьПодвал.Параметры.Должность 				= ДолжностьПредставителяОрганизации;
	ОбластьПодвал.Параметры.ДолжностьПредставлениеК = ?(обЗначениеНеЗаполнено(ДолжностьПредставителяКонтрагента),"________________", СокрЛП(ДолжностьПредставителяКонтрагента));
	ОбластьПодвал.Параметры.ДолжностьК				= ДолжностьПредставителяКонтрагента;
	ОбластьПодвал.Параметры.ФИОПредставителя  		= "("+?(обЗначениеНеЗаполнено(ИмяПредставителяОрганизации),"_______________________", ИмяПредставителяОрганизации)+")";
	ОбластьПодвал.Параметры.Представитель			= ПредставительОрганизации.ПредставительОрганизации;
	ОбластьПодвал.Параметры.ФИОПредставителяК 		= "("+?(обЗначениеНеЗаполнено(ИмяПредставителяКонтрагента),"_______________________", ИмяПредставителяКонтрагента)+")";
	ОбластьПодвал.Параметры.ПредставительК			= ПредставительКонтрагента.ПредставительКонтрагента;
	
	//зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ТолькоПросмотр = Истина;
	
	ТабДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАктСверки()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ДатаОкончания) Тогда
		ДатаОкончания = ТекущаяДата();
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = НачалоМесяца(ДатаОкончания);
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
		Для Каждого ТекСтрока Из ПоДаннымОрганизации Цикл
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
				ТекСтрока.ДоговорВзаиморасчетов = Неопределено;
			КонецЕсли; 
		КонецЦикла;
		Для Каждого ТекСтрока Из ПоДаннымКонтрагента Цикл
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
				ТекСтрока.ДоговорВзаиморасчетов = Неопределено;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли
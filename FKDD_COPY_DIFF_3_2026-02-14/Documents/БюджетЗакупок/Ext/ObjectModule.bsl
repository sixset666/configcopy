// Модуль документа БюджетЗакупок

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;		// Имя реквизита кода номенклатуры
Перем ВалютаУпр;					// Валюта управленческого учета
Перем ВалютаРег;					// Валюта регламентированного учета 
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОсновнаяСтавкаНДС Экспорт;	// Основная ставка НДС организации

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары = Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("СценарийПланирования", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);  	
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки = "", ДопРеквизиты = Неопределено, Заполнение = Истина, Уникальность = Истина) Экспорт
	
	Результат = Истина;	
	
	// Запретим запись с невыбранным сценарием планирования
	Если обЗначениеНеЗаполнено(СценарийПланирования) ИЛИ обЗначениеНеЗаполнено(СценарийПланирования.Периодичность) Тогда
		дкДобавитьОшибку(Ошибки, "Не выбран сценарий планирования, либо у выбранного сценария не выбрана периодичность планирования.");		
		Результат = Ложь;		
	КонецЕсли;
	
	// проверим правильность введенного подразделения хоз.операции.
	Результат = плПроверитьКорректностьВыбораПодразделенияИХозОперации(ХозОперация, ПодразделениеКомпании) И Результат;
	Если НЕ Результат Тогда
		дкДобавитьОшибку(Ошибки, "Для данного подразделения нельзя вводить данный вид бюджета.");		
	КонецЕсли;  
	
	Результат = (дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И Результат);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты = Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока = Неопределено, ЭтаФорма = Неопределено, ДопПараметры = Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "СценарийПланирования" Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		плПолучитьДатыПланируемогоПериода(Параметры);
		ДатаПланирования = Параметры.ДатаНачала;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			Результат=дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
			Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		Если ТекСтрока.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """ + СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
			#КонецЕсли
			ТекСтрока.Номенклатура = Неопределено;
			Возврат Ложь;
		КонецЕсли;
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Дата, Справочники.Контрагенты.ПустаяСсылка(), Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли Имя = "Товары.Цена" Тогда
		СтараяСуммаУпр = ТекСтрока.СуммаВсегоУпр;
		НоваяСуммаУпр  = ТекСтрока.Количество * ТекСтрока.Цена;
		
		ТекСтрока.СуммаВсегоУпр = НоваяСуммаУпр;
		
		// Рассчитаем НДС по ставке из справочника по актуальному курсу
		// Получим ставку
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС.Ставка; 
		Иначе
			СтавкаНДС = ОсновнаяСтавкаНДС.Ставка;
		КонецЕсли;
		
		// Получим курсы упр. и рег. валюты на дату документа
		КоэфПересчета      = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС / 100;	
		
	ИначеЕсли Имя = "Товары.Количество" Тогда
		ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма);
				
	ИначеЕсли Имя = "Товары.СуммаВсегоУпр" Тогда
		ХОПоНоменклатуре    = (ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре);
		
		СтавкаНДС           = ?(ХОПоНоменклатуре, ТекСтрока.Номенклатура.СтавкаНДС.Ставка, ОсновнаяСтавкаНДС.Ставка);
		КоэфПересчета       = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС  = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС / 100;			
		
		Если ХОПоНоменклатуре Тогда
			ТекСтрока.Цена = ?(ТекСтрока.Количество=0,ТекСтрока.СуммаВсегоУпр,ТекСтрока.СуммаВсегоУпр/ТекСтрока.Количество);			
		КонецЕсли;
		
	ИначеЕсли Имя="Услуги.Номенклатура" Тогда 
		
		Если ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
			#КонецЕсли
			ТекСтрока.Номенклатура=Неопределено;
			Возврат Ложь; 
		КонецЕсли;
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Дата, Справочники.Контрагенты.ПустаяСсылка(), ВалютаУпр,,,, ПодразделениеКомпании);
			ОбработкаРеквизита("Услуги.Цена", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли Имя="Услуги.Цена" Тогда
		ТекСтрока.СуммаВсегоУпр = ТекСтрока.Количество * ТекСтрока.Цена;
		// Рассчитаем НДС по ставке из справочника по актуальному курсу
		// Получим ставку
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС.Ставка;
		Иначе
			СтавкаНДС = ОсновнаяСтавкаНДС.Ставка;
		КонецЕсли;
		
		// Получим курсы упр. и рег. валюты на дату документа
		КоэфПересчета      = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС/100;	
		
	ИначеЕсли Имя="Услуги.Количество" Тогда
		ОбработкаРеквизита("Услуги.Цена"           , ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя="Услуги.СуммаВсегоУпр" Тогда
		ХОПоНоменклатуре    = (ХозОперация=Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре);
		
		СтавкаНДС           = ?(ХОПоНоменклатуре, ТекСтрока.Номенклатура.СтавкаНДС.Ставка, ОсновнаяСтавкаНДС.Ставка);
		КоэфПересчета       = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС  = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС/100;			
		
		Если ХОПоНоменклатуре Тогда
			ТекСтрока.Цена = ?(ТекСтрока.Количество=0,ТекСтрока.СуммаВсегоУпр,ТекСтрока.СуммаВсегоУпр/ТекСтрока.Количество);			
		КонецЕсли;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("БюджетЗакупокПоКатегориям", "По категориям", Истина);
	СписокХозОпераций.Добавить("БюджетЗакупокПоНоменклатуре", "По номенклатуре", Ложь);	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// формирование строки по которой заполняли в последний раз
//
Процедура СформироватьСтрокуПоследнегоЗаполнения(ЭтаФорма, СпособЗаполнения = -1) Экспорт
	
	Если СпособЗаполнения = -1 Тогда
		СпособЗаполнения = СпособПоследнегоЗаполнения;
	Иначе
		СпособПоследнегоЗаполнения = СпособЗаполнения;
	КонецЕсли;	
	
	Если СпособЗаполнения = -2 Тогда
		ЭтаФорма.ЭлементыФормы.НадписьПараметры.Заголовок = "";	
		Возврат;
	КонецЕсли;
		
	Текст = "";
	
	Если СпособЗаполнения = 0 Тогда
		
		Текст = "Тип анализа: " + СокрЛП(ТипАнализа);
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			Текст = Текст + "Способ учета стратег. мин. остатков: " + СокрЛП(СпособУчетаОстатков) + Символы.ПС;
		КонецЕсли;
		Текст = Текст + " (Коэф.роста=" + СокрЛП(КоэффициентРоста) + ";  Коэф.сез.=" + СокрЛП(КоэффициентСезонности) + ")" + Символы.ПС;
		Текст = Текст + "Показатель планирования: " + ?(ПоказательПланирования, "Количество", "Сумма (упр.)") + Символы.ПС;
		Текст = Текст + "Периоды планирования: " + плВывестиПредставлениеПериода(ЭтаФорма, СценарийПланирования.Периодичность, ДатаПланирования);
		
	ИначеЕсли СпособЗаполнения = 1 Тогда
		
		Текст = Текст + "Тип анализа: " + СокрЛП(ТипАнализа) + Символы.ПС;
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			Текст = Текст + "Способ учета стратег. мин. остатков: " + СокрЛП(СпособУчетаОстатков) + Символы.ПС;
		КонецЕсли;
		
		Текст = Текст + "Модель: " + СокрЛП(МодельПрогнозирования);
		
		Если МодельПрогнозирования = Перечисления.МоделиПрогнозирования.Сглаживанием Тогда
			
			Текст = Текст + "; Постоянная уровня: " + СокрЛП(Параметр1) + "; Постоянная тренда: "+ СокрЛП(Параметр2);
			Если ((СценарийПланирования.Периодичность = Перечисления.ПериодичностьПланирования.Месяц) И
				(КоличествоПериодов >= 24) ) 
				или				
				((СценарийПланирования.Периодичность = Перечисления.ПериодичностьПланирования.Квартал) И
				(КоличествоПериодов >= 8)) Тогда  
				Текст = Текст + "; Постоянная сезонности: " + СокрЛП(Параметр3);
			КонецЕсли;
			
		ИначеЕсли МодельПрогнозирования = Перечисления.МоделиПрогнозирования.Полиномиальный Тогда
			Текст = Текст + "; Степень полинома: " + СокрЛП(Параметр1);
		КонецЕсли;
		
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			Текст = Текст + Символы.ПС + "Показатель планирования:  " + ?(ПоказательПланирования,"Количество","Сумма (упр.)");
		КонецЕсли;
		Текст = Текст + Символы.ПС + "Периоды планирования: " + плВывестиПредставлениеПериода(ЭтаФорма, СценарийПланирования.Периодичность, ДатаПланирования);
		
	ИначеЕсли СпособЗаполнения = 2 Тогда		
		Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
			Текст = "";
		Иначе
			Текст = Текст + "Док.основание:  " + ДокументОснование;
				Если ХозОперация = Справочники.ХозОперации.БюджетПродажПоНоменклатуре И ДокументОснование.ХозОперация = Справочники.ХозОперации.БюджетПродажПоКатегориям Тогда
					Текст = Текст + Символы.ПС + "Метод распределения суммы продаж категорий:  " + СокрЛП(МетодыРаспределенияКатегорий);
					Текст = Текст + Символы.ПС + "Изменяемый показатель:  " + ?(ПараметрУправленияРаспределением = 0, "Количество", "Цена");
				КонецЕсли;
		КонецЕсли; 			
	КонецЕсли;   	
	ЭтаФорма.ЭлементыФормы.НадписьПараметры.Заголовок = Текст;     		
КонецПроцедуры // СформироватьСтрокуПоследнегоЗаполнения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда  	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьБюджетЗакупок(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("БюджетЗакупок");
	
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоКатегориям Тогда
		ОбластьТовар	= Макет.Область(3,Макет.Область("Товар").Право);
		ОбластьКоличество	= Макет.Область("Количество");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКоличество.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКоличество,ТипСмещенияТабличногоДокумента.ПоВертикали);
		ОбластьЦена	= Макет.Область("Цена");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЦена.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЦена,ТипСмещенияТабличногоДокумента.ПоВертикали);
		ОбластьКод	= Макет.Область("Код");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	
	//ЗАГОЛОВОК
	ОбластьЗаголовка = ПолучитьЗаполненнуюОбластьТабличногоДокумента("Заголовок", Макет); 
	ТабДокумент.Вывести(ОбластьЗаголовка);	
	
	// установим следующий номер страницы
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;  	

	// ТЕПЕРЬ ШАПКА	
	ОбластьШапкаТаблицы = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ШапкаТаблицы", Макет);
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ШапкаТаблицы", Макет, , ("Страница: " + НомерСтраницы));
	
	// ИТОГИ НА СТРАНИЦЕ
	УчетКоличества    = (ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре);
	СтруктураИтоговПоСтранице = Новый Структура;
	
	СтруктураИтоговПоСтранице.Вставить("СуммаВсегоУпр",0);
	
	СтруктураИтоговПоСтранице.Вставить("СуммаНДС",0);
	
	// Получаем табл. документ (итоги на странице) с заполненным параметрами. Иначе никак. 
	Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
		ОбластьМакетаИтогоПоСтранице = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ИтогоПоСтранице", Макет, Права, СтруктураИтоговПоСтранице);
	Иначе
		ОбластьМакетаИтогоПоСтранице = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ИтогоПоСтраницеБезКоличества", Макет, Права, СтруктураИтоговПоСтранице);
	КонецЕсли;
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	// ИТОГ ПО ДОКУМЕНТУ	
	СтруктураИтогов = Новый Структура;
	СтруктураИтогов.Вставить("ИтогоСуммаВсегоУпр", ВыборкаТабличнойЧасти.Итог("СуммаВсегоУпр"));
	
	СтруктураИтогов.Вставить("ИтогоСуммаНДС",     ВыборкаТабличнойЧасти.Итог("СуммаНДС"));
	СтруктураИтогов.Вставить("КоличествоПозиций", ВыборкаТабличнойЧасти.Количество());
	
	Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
		ОбластьПодвал = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ПодвалПоНоменклатуре", Макет, Права, СтруктураИтогов); 			
	Иначе
		ОбластьПодвал = ПолучитьЗаполненнуюОбластьТабличногоДокумента("Подвал", Макет, Права, СтруктураИтогов);
	КонецЕсли;	
	
	//перебор строк 	
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета = ПолучитьЗаполненнуюОбластьТабличногоДокумента("Строка", Макет, Права, СтрокаТабличнойЧасти); 
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество() -1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;		
		
		// Получаем табл. документ (итоги на странице) с заполненным параметрами. Иначе никак. 
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			ОбластьМакетаИтогоПоСтранице = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ИтогоПоСтранице", Макет, Права, СтруктураИтоговПоСтранице);         	
		Иначе
			ОбластьМакетаИтогоПоСтранице = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ИтогоПоСтраницеБезКоличества", Макет, Права, СтруктураИтоговПоСтранице);         	
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			// обнулим структуру
			Для Каждого стрСтруктуры Из СтруктураИтоговПоСтранице Цикл
				СтруктураИтоговПоСтранице.Вставить(стрСтруктуры.Ключ, 0);		
			КонецЦикла;
			НомерСтраницыПред = НомерСтраницы;
			//заполним параметры шапки таблицы для следующего листа  
			ОбластьШапкаТаблицы = ПолучитьЗаполненнуюОбластьТабличногоДокумента("ШапкаТаблицы", Макет, Права,("Страница: " + НомерСтраницы));	
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);		
	КонецЦикла;  	
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);  	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьБюджетЗакупок()

// Заполняет параметры переданной области таблицы для следующего листа
//
// Параметры:
//  ИмяОбласти        - Строка - Имя области для заполнения,
//  Макет             - Макет,
//  ПраваПользователя - Соответствие, где ключ - значение доступа, значение - флаг доступа
//  ДопПараметр       - Структура - передает дополнительные параметры.
//
Функция ПолучитьЗаполненнуюОбластьТабличногоДокумента(ИмяОбласти, Макет, ПраваПользователя="", ДопПараметр="")
	
	Если ИмяОбласти="Заголовок" Тогда 		
		ОбластьЗаголовка = Новый ТабличныйДокумент;
		МакетШапки     = Макет.ПолучитьОбласть(ИмяОбласти+"|Товар");	
		МакетШапки.Параметры.Заполнить(ЭтотОбъект); 		
		ТекстЗаголовка = ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy");  		
		МакетШапки.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		ПредставлениеПериодаПланирования = плПолучитьДатыПланируемогоПериода(Параметры);		
		
		МакетШапки.Параметры.СценарийПланирования = СценарийПланирования;
		МакетШапки.Параметры.ПредставлениеПериодаПланирования = ПредставлениеПериодаПланирования;
		ОбластьЗаголовка.Вывести(МакетШапки);
		
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			ОбластьЗаголовка.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти+"|Код") );
			ОбластьЗаголовка.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|Количество") );
			ОбластьЗаголовка.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|Цена") );
		КонецЕсли;  
		
		ОбластьЗаголовка.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|СуммаУпр")); 
		ОбластьЗаголовка.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|НДС"));
		Возврат ОбластьЗаголовка;
		
	ИначеЕсли ИмяОбласти = "ШапкаТаблицы" Тогда
		ОбластьШапкаТаблицы = Новый ТабличныйДокумент;  
		МакетШапкиТовар     = Макет.ПолучитьОбласть(ИмяОбласти + "|Товар");		
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			МакетШапкиТовар.Параметры.Товар = "Товар"; 
		Иначе
			МакетШапкиТовар.Параметры.Товар = "Типы номенклатуры";
		КонецЕсли; 	
		ОбластьШапкаТаблицы.Вывести(МакетШапкиТовар );
		
		Если ХозОперация=Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			ОбластьШапкаТаблицы.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|Код") );
			ОбластьШапкаТаблицы.Присоединить( Макет.ПолучитьОбласть(ИмяОбласти + "|Количество") );
			ОбластьШапкаТаблицы.Присоединить( Макет.ПолучитьОбласть(ИмяОбласти + "|Цена") );
		КонецЕсли;  	
		ОбластьШапкаТаблицы.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|СуммаУпр")); 
		МакетШапкиТовар = Макет.ПолучитьОбласть(ИмяОбласти + "|НДС");
		Если ДопПараметр <> "" Тогда
			МакетШапкиТовар.Параметры.НомерСтраницы = ДопПараметр;
		КонецЕсли;  		
		ОбластьШапкаТаблицы.Присоединить(МакетШапкиТовар); 
		Возврат ОбластьШапкаТаблицы;
		
	ИначеЕсли ИмяОбласти = "Строка" Тогда
		ОбластьСтроки        = Новый ТабличныйДокумент;
		СтрокаТабличнойЧасти = ДопПараметр;
		//форматы вывода
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", ПраваПользователя,,ЭтотОбъект);
		ФорматВыводаСуммы 	   = обПраво("ФорматВыводаСуммы",      ПраваПользователя,,ЭтотОбъект);
		ВыводитьПроизводителя  = обПраво("ВыводитьПроизводителяВПечатныхФормах",ПраваПользователя,,ЭтотОбъект);
		
		МакетСтроки	         = Макет.ПолучитьОбласть(ИмяОбласти + "|Товар");
		МакетСтроки.Параметры.НомерСтроки       = СтрокаТабличнойЧасти.НомерСтроки;
		МакетСтроки.Параметры.ТоварНаименование = СтрокаТабличнойЧасти.Номенклатура.Наименование;
		ОбластьСтроки.Вывести(МакетСтроки); 		
		
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|Код");
			МакетСтроки.Параметры.Код = дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,, ЭтотОбъект, ВыводитьПроизводителя);
			ОбластьСтроки.Присоединить(МакетСтроки);
			
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|Количество");
			МакетСтроки.Параметры.Количество = Формат(СтрокаТабличнойЧасти.Количество, ФорматВыводаКоличества);
			ОбластьСтроки.Присоединить(МакетСтроки);
			
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|Цена");
			МакетСтроки.Параметры.Цена = Формат(СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы);
			ОбластьСтроки.Присоединить(МакетСтроки);    
		КонецЕсли;  		
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|СуммаУпр");
		МакетСтроки.Параметры.СуммаУпр = Формат(СтрокаТабличнойЧасти.СуммаВсегоУпр, ФорматВыводаСуммы);
		ОбластьСтроки.Присоединить(МакетСтроки);   
						
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|НДС");
		МакетСтроки.Параметры.СуммаНДС = Формат(СтрокаТабличнойЧасти.СуммаНДС, ФорматВыводаСуммы);
		ОбластьСтроки.Присоединить(МакетСтроки);   
		Возврат ОбластьСтроки;
		
	ИначеЕсли ИмяОбласти = "ИтогоПоСтранице" ИЛИ ИмяОбласти = "ИтогоПоСтраницеБезКоличества" Тогда
		ОбластьИтогов             = Новый ТабличныйДокумент;
		СтруктураИтоговПоСтранице = ДопПараметр;
		
		//форматы вывода
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", ПраваПользователя,,ЭтотОбъект);
		ФорматВыводаСуммы 	   = обПраво("ФорматВыводаСуммы",      ПраваПользователя,,ЭтотОбъект); 
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|Товар");
		Если НЕ ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			МакетСтроки.Параметры.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		КонецЕсли;
		ОбластьИтогов.Вывести(МакетСтроки);
		
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|Код");
			МакетСтроки.Параметры.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			ОбластьИтогов.Присоединить(МакетСтроки);
			ОбластьИтогов.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|Количество")); 
			МакетСтроки =  Макет.ПолучитьОбласть(ИмяОбласти + "|Цена");
			ОбластьИтогов.Присоединить(МакетСтроки);    
		КонецЕсли; 
		
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|СуммаУпр");
		МакетСтроки.Параметры.СуммаВсегоУпр = Формат(СтруктураИтоговПоСтранице.СуммаВсегоУпр, ФорматВыводаСуммы);
		ОбластьИтогов.Присоединить(МакетСтроки);   
		                                          				
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|НДС");
		МакетСтроки.Параметры.СуммаНДС = Формат(СтруктураИтоговПоСтранице.СуммаНДС, ФорматВыводаСуммы);
		ОбластьИтогов.Присоединить(МакетСтроки);   
		Возврат ОбластьИтогов; 
		
	ИначеЕсли ИмяОбласти = "Подвал" ИЛИ ИмяОбласти = "ПодвалПоНоменклатуре" Тогда
		ОбластьПодвала = Новый ТабличныйДокумент;
		СтруктураИтогов = ДопПараметр;
		//форматы вывода
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", ПраваПользователя,,ЭтотОбъект);
		ФорматВыводаСуммы 	   = обПраво("ФорматВыводаСуммы",      ПраваПользователя,,ЭтотОбъект); 
		
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти+"|Товар");
		Если ХозОперация=Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			МакетСтроки.Параметры.СуммаПрописью     = "Всего запланировано позиций номенклатуры " + СтруктураИтогов.КоличествоПозиций+" на сумму " + обЧислоПрописью(СтруктураИтогов.ИтогоСуммаВсегоУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		Иначе
			МакетСтроки.Параметры.СуммаПрописью     = "Всего запланировано типов номенклатуры " + СтруктураИтогов.КоличествоПозиций+" на сумму " + обЧислоПрописью(СтруктураИтогов.ИтогоСуммаВсегоУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		КонецЕсли; 
		МакетСтроки.Параметры.Автор = Автор;
		Если ИмяОбласти = "Подвал" Тогда
			МакетСтроки.Параметры.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить();			
		КонецЕсли;
		ОбластьПодвала.Вывести(МакетСтроки); 		
		
		Если ХозОперация = Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|Код"); 		
			ОбластьПодвала.Присоединить(МакетСтроки);
			
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|Количество");
			ОбластьПодвала.Присоединить(МакетСтроки);  
			
			МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|Цена");
			МакетСтроки.Параметры.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить();	
			ОбластьПодвала.Присоединить(МакетСтроки);    
		КонецЕсли;  		
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|СуммаУпр");
		МакетСтроки.Параметры.ИтогоСуммаВсегоУпр = Формат(СтруктураИтогов.ИтогоСуммаВсегоУпр, ФорматВыводаСуммы);
		ОбластьПодвала.Присоединить(МакетСтроки);   
		
		МакетСтроки = Макет.ПолучитьОбласть(ИмяОбласти + "|НДС");
		МакетСтроки.Параметры.ИтогоСуммаНДС = Формат(СтруктураИтогов.ИтогоСуммаНДС, ФорматВыводаСуммы);
		ОбластьПодвала.Присоединить(МакетСтроки); 		
		
		Возврат ОбластьПодвала;
	КонецЕсли;
	
КонецФункции // ПолучитьЗаполненнуюОбластьТабличногоДокумента()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы = "", КоличествоЭкземпляров = 0, НаПринтер = Ложь, Документ = Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)   	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , , ,"10111") Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли; 
	
	// Собственно само проведение
	НаборЗаписей = Движения.БюджетЗакупок;
	НаборЗаписей.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписей.ДокументОбъект        = Ссылка;
	НаборЗаписей.РежимПроведения       = Режим;
	
	Отказ = НЕ НаборЗаписей.Приход();
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;  
	
	дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);  	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
ВалютаРег = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
ОсновнаяСтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

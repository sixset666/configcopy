// Модуль документа

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеОпции=Новый Структура();
	ОбязательныеОпции.Вставить("Модель",3);
	ОбязательныеОпции.Вставить("ВариантКомплектации",2);
	ОбязательныеОпции.Вставить("Опция",3);
	ОбязательныеОпции.Вставить("Цена",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Опции",ОбязательныеОпции);
	ОбязательныеРеквизиты.Вставить("ДатаНачалаДействия",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только
	// в случае если все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
	
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;	

	Возврат Результат
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УстановкаЦенНаОпции","Установка цен на опции",Истина);

	Возврат СписокХозОпераций;
КонецФункции

//Заполнения базового типа цен в зависимости от источника базовых цен
Процедура ЗаполнитьБазовуюЦену(СтрокаОпций)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("ВалютаДокумента",ВалютаДокумента);
	ДокументОбъектСтруктура.Вставить("КурсДокумента",КурсДокумента);
	ДокументОбъектСтруктура.Вставить("РасчетЦенОт",РасчетЦенОт);
	ДокументОбъектСтруктура.Вставить("БазовыйТипЦен",БазовыйТипЦен);
	ДокументОбъектСтруктура.Вставить("ДокументОснование",ДокументОснование);
	//ДокументОбъектСтруктура.Вставить("СкладКомпании",СкладКомпании);
	ДокументОбъектСтрока=Новый Структура();
	Для каждого РеквизитТабличнойЧасти Из ЭтотОбъект.Метаданные().ТабличныеЧасти.Опции.Реквизиты Цикл
		ДокументОбъектСтрока.Вставить(РеквизитТабличнойЧасти.Имя,СтрокаОпций[РеквизитТабличнойЧасти.Имя]);
	КонецЦикла; 
	зфЗащищенныеФункцииСервер.ИзменениеЦенОпцийЗаполнитьБазовуюЦену(ДокументОбъектСтруктура,ДокументОбъектСтрока);
	Для каждого РеквизитТабличнойЧасти Из ДокументОбъектСтрока Цикл
		СтрокаОпций[РеквизитТабличнойЧасти.Ключ]=РеквизитТабличнойЧасти.Значение;
	КонецЦикла; 
КонецПроцедуры

//Заполнения базового типа цен в зависимости от источника базовых цен
Процедура ЗаполнитьБазовыеЦены() Экспорт
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Для каждого СтрокаОпции Из Опции Цикл
		ЗаполнитьБазовуюЦену(СтрокаОпции);
	КонецЦикла; 
КонецПроцедуры

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	Если Имя="ВалютаДокумента" Тогда
		//Попытка
		//	ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		//Исключение
		//	ТребуетсяПересчет=Ложь;
		//КонецПопытки; 
		//Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
		//	ЗаполнитьБазовыеЦены();
		//	Для каждого СтрокаТЧ Из Опции Цикл
		//		НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
		//		Если НоваяЦена<>СтрокаТЧ.Цена Тогда
		//			СтрокаТЧ.Цена=НоваяЦена;
		//			Результат=ОбработкаРеквизита("Опции.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		//		КонецЕсли;
		//	КонецЦикла; 
		//КонецЕсли;
		
		Для Каждого ТекСтрока Из Опции Цикл
			ЗаполнитьБазовуюЦену(ТекСтрока);
			ОбработкаРеквизита("Опции.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЦикла;
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Результат;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ОПЦИИ"
	ИначеЕсли Имя="Опции.Модель" ИЛИ Имя="Опции.ВариантКомплектации" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Модель) Тогда Возврат Ложь; КонецЕсли;
		Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.ВариантКомплектации)) И
			 (ТекСтрока.ВариантКомплектации.Владелец<>ТекСтрока.Модель) Тогда
			 ТекСтрока.ВариантКомплектации=Неопределено;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.Опция" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда Возврат Ложь; КонецЕсли;
		//получим базовую цену
		ЗаполнитьБазовуюЦену(ТекСтрока);
		Если ПроцентНаценки<>0 Тогда
			ТекСтрока.ПроцентНаценки = ПроцентНаценки;
		Иначе
			ТекСтрока.ПроцентНаценки = БазовыйТипЦен.ПроцентСкидкиНаценки;
		КонецЕсли;
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+((ТекСтрока.ЦенаБазовая*ТекСтрока.ПроцентНаценки)/100);
		ТекСтрока.СуммаНаценки = ТекСтрока.Цена - ТекСтрока.ЦенаБазовая;
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.Цена" Тогда	
		ТекСтрока.ПроцентНаценки=?(ТекСтрока.ЦенаБазовая=0,0,((ТекСтрока.Цена-ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100);
		ТекСтрока.СуммаНаценки = ТекСтрока.Цена - ТекСтрока.ЦенаБазовая;
		Возврат Истина;

	ИначеЕсли Имя="Опции.ПроцентНаценки" Тогда	
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+((ТекСтрока.ЦенаБазовая*ТекСтрока.ПроцентНаценки)/100);
		ТекСтрока.СуммаНаценки = ТекСтрока.Цена - ТекСтрока.ЦенаБазовая;

		//округляем
		НоваяЦена = ТекСтрока.Цена;
		ДельтаОкругления = ?(ОкруглятьДо=0,0,НоваяЦена/ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления<>ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел+1)*ОкруглятьДо;
		КонецЕсли; 
		Если НоваяЦена <> ТекСтрока.Цена Тогда
			ТекСтрока.Цена = НоваяЦена;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.СуммаНаценки" Тогда	
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+ТекСтрока.СуммаНаценки;
		ТекСтрока.ПроцентНаценки = ?(ТекСтрока.ЦенаБазовая=0,0,((ТекСтрока.Цена - ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100);
		
		//округляем
		НоваяЦена = ТекСтрока.Цена;
		ДельтаОкругления = ?(ОкруглятьДо=0,0,НоваяЦена/ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления<>ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел+1)*ОкруглятьДо;
		КонецЕсли; 
		Если НоваяЦена <> ТекСтрока.Цена Тогда
			ТекСтрока.Цена = НоваяЦена;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.ПроцентНаценки" Тогда	
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+((ТекСтрока.ЦенаБазовая*ТекСтрока.ПроцентНаценки)/100);
		ТекСтрока.СуммаНаценки = ТекСтрока.Цена-ТекСтрока.ЦенаБазовая;
		
		//округляем
		НоваяЦена = ТекСтрока.Цена;
		ДельтаОкругления = ?(ОкруглятьДо=0,0,НоваяЦена/ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления<>ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел+1)*ОкруглятьДо;
		КонецЕсли; 
		Если НоваяЦена <> ТекСтрока.Цена Тогда
			ТекСтрока.Цена = НоваяЦена;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="Опции.СуммаНаценки" Тогда	
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+ТекСтрока.СуммаНаценки;
		ТекСтрока.ПроцентНаценки = ?(ТекСтрока.ЦенаБазовая=0,0,((ТекСтрока.Цена - ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100);

		//округляем
		НоваяЦена = ТекСтрока.Цена;
		ДельтаОкругления = ?(ОкруглятьДо=0,0,НоваяЦена/ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления<>ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел+1)*ОкруглятьДо;
		КонецЕсли; 
		Если НоваяЦена <> ТекСтрока.Цена Тогда
			ТекСтрока.Цена = НоваяЦена;
		КонецЕсли; 
		
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ИзменениеЦенОпций"
// Возвращает сформированный табличный документ:
Функция ПечатьИзменениеЦенОпций(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИзменениеЦенОпций");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ОбластьШапкаТаблицы.Параметры.МодельВариантКомплектации = "Модель";
	Таб = ЭтотОбъект.Опции.Выгрузить();
	Таб.Свернуть("ВариантКомплектации");
	Если Таб.Количество()>0 Тогда
		Если НЕ обЗначениеНеЗаполнено(Таб[0].ВариантКомплектации) Тогда
			ОбластьШапкаТаблицы.Параметры.МодельВариантКомплектации = "Модель / Вариант комплектации"	
		КонецЕсли;
	КонецЕсли;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.ПредставлениеТипаЦен		= спПолучитьНаименование(ТипЦен);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Опции;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		// найдём старую цену
		СтруктураОтбора=Новый Структура("ТипЦен,Опция,Модель,ВариантКомплектации",ТипЦен,СтрокаТабличнойЧасти.Опция,СтрокаТабличнойЧасти.Модель,СтрокаТабличнойЧасти.ВариантКомплектации);
		МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),Новый Граница(Ссылка.МоментВремени(),ВидГраницы.Исключая));
		СтруктураЦен=РегистрыСведений.ЦеныОпций.ПолучитьПоследнее(МоментВремени,СтруктураОтбора);
		ЦенаСтарая=СтруктураЦен.Цена;
		
		ВалютаТипаЦены = обВалютаТипаЦены(СтрокаТабличнойЧасти.Опция, ТипЦен);
		Если Не ВалютаТипаЦены = ВалютаДокумента Тогда
			ЦенаСтарая=обПересчет(ЦенаСтарая,ВалютаТипаЦены,Дата,ВалютаДокумента,КурсДокумента);	
		КонецЕсли;
		СтруктураСтроки.Вставить("ЦенаСтарая",Формат(ЦенаСтарая,ФорматВыводаСуммы));
		НетВарианта = обЗначениеНеЗаполнено(СтрокаТабличнойЧасти.ВариантКомплектации);
		СтруктураСтроки.Вставить("МодельВариантКомплектации",?(НетВарианта,спПолучитьНаименование(СтрокаТабличнойЧасти.Модель),спПолучитьНаименование(СтрокаТабличнойЧасти.Модель)+" / "+спПолучитьНаименование(СтрокаТабличнойЧасти.ВариантКомплектации)));		
		СтруктураСтроки.Вставить("Модель_ВариантКомплектации",?(НетВарианта,СтрокаТабличнойЧасти.Модель,СтрокаТабличнойЧасти.ВариантКомплектации));
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы,,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;

	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество();
	
	// Выводим представления и расшифровки подписей
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ПредседательКомиссии.ПредседательКомиссииПредставление = ?(обЗначениеНеЗаполнено(ПредседательКомиссии.ПредседательКомиссии),"","/ "+ ПредседательКомиссии.ПредседательКомиссииПредставление);
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/ "+ ГлавныйБухгалтер.ГлавныйБухгалтерПредставление);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	ДатаНачалаДействия = Дата;
	Если ЭтоНовый() Тогда
		ОкруглятьДо=1.00;
	КонецЕсли; 
	
	Если Основание<>Неопределено И ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеАвтомобилей")
		ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковАвтомобилей") Тогда
		
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеАвтомобилей") Тогда
			СтрДокумент="ПоступлениеАвтомобилей";
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковАвтомобилей") Тогда
			СтрДокумент="ВводОстатковАвтомобилей"
		КонецЕсли;
		
		РасчетЦенОт=1;
		ТипЦен=обПраво("ОсновнойТипЦенПродажиАвтомобилей",Права,,ЭтотОбъект);
		Если ТипЦен.Рассчитывается Тогда
			ПроцентНаценки=ТипЦен.ПроцентСкидкиНаценки;
		КонецЕсли; 
		БазовыйТипЦен=Основание.ТипЦен;
		Опции.Очистить();
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументАвтомобили.Автомобиль.Модель КАК Модель,
		|	ДокументАвтомобили.Автомобиль.ВариантКомплектации КАК ВариантКомплектации,
		|	ДокументОпции.Опция КАК Опция
		|ИЗ
		|	Документ."+СтрДокумент+".Опции КАК ДокументОпции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ."+СтрДокумент+".Автомобили КАК ДокументАвтомобили
		|		ПО (ДокументАвтомобили.Ссылка = &Ссылка)
		|			И ДокументОпции.ИдентификаторАвтомобиля = ДокументАвтомобили.ИдентификаторАвтомобиля
		|ГДЕ
		|	ДокументОпции.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",ДокументОснование);
		Опции.Загрузить(Запрос.Выполнить().Выгрузить());
		ЗаполнитьБазовыеЦены();
		Для Каждого СтрокаОпции Из Опции Цикл
			СтрокаОпции.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации,Опция",СтрокаОпции.Модель,СтрокаОпции.ВариантКомплектации,СтрокаОпции.Опция),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			СтрокаОпции.ПроцентНаценки=?(СтрокаОпции.ЦенаБазовая=0,0,((СтрокаОпции.Цена*100)/СтрокаОпции.ЦенаБазовая)-100);
			СтрокаОпции.СуммаНаценки = СтрокаОпции.Цена - СтрокаОпции.ЦенаБазовая;
		КонецЦикла;
		ДатаНачалаДействия = Основание.Дата;
	КонецЕсли;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Отказ = ТипЦен.Рассчитывается;
	Если Отказ Тогда
		РезультатОбработки = "Тип цен не должен быть рассчитываемым!";
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		// устанавливаем цены
		НаборЗаписейЦены = Движения.ЦеныОпций;
		НаборЗаписейЦены.ДокументОбъект	= ЭтотОбъект;
		НаборЗаписейЦены.ТипЦен			= ТипЦен;
		НаборЗаписейЦены.ДатаНачалаДействия = ДатаНачалаДействия;
		Отказ=НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Модуль документа Жалоба клиента

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/атрикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПолучитьЧасыИМинутыТекстом(ДатаВычитаемое,ДатаУменьшаемое,УжеСреагировали = Ложь) Экспорт
	ВремяЧислом = ДатаУменьшаемое - ДатаВычитаемое;
	Если ВремяЧислом > 0 Тогда
		Если УжеСреагировали Тогда
			Возврат ("Разобрана вовремя");
		Иначе
			Результат = "Осталось: ";
		КонецЕсли;
	Иначе
		ВремяЧислом = - ВремяЧислом;
		Если УжеСреагировали Тогда
			Результат = "С опозданием на: ";
		Иначе
			Результат = "Просрочено на: ";
		КонецЕсли;
	КонецЕсли;
	ЧислоЧасов = Цел(ВремяЧислом/60/60) ;
	ЧислоМинут = Цел((ВремяЧислом - ЧислоЧасов*60*60)/60) ;
	Если ЧислоЧасов > 0 Тогда 
		Результат = Результат + ЧислоЧасов+" ч.";
	КонецЕсли;
	Если ЧислоМинут > 0 Тогда 
		Результат = Результат + ЧислоМинут+" мин." ;
	КонецЕсли;
	Возврат Результат;         
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы категорий
	ОбязательныеКатегории=Новый Структура();
	ОбязательныеКатегории.Вставить("Категория",3);	
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	//Rarus agar {
	Если    Состояние = Перечисления.СостояниеЖалобКлиентов.Разобрана 
		Или Состояние = Перечисления.СостояниеЖалобКлиентов.Проверена
		Или Состояние = Перечисления.СостояниеЖалобКлиентов.Закрыта
		Тогда
		ОбязательныеРеквизиты.Вставить("Ответственный",1);
		ОбязательныеРеквизиты.Вставить("Сотрудник",1);
		ОбязательныеРеквизиты.Вставить("МнениеКлиента",1);
		ОбязательныеРеквизиты.Вставить("МнениеОтветственного",1);
		ОбязательныеРеквизиты.Вставить("МнениеСотрудника",1);
	КонецЕсли;
	//Rarus agar }
	ОбязательныеРеквизиты.Вставить("Состояние",1);
	ОбязательныеРеквизиты.Вставить("Суть",1);
	ОбязательныеРеквизиты.Вставить("Категории",ОбязательныеКатегории);	
		
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверять на корректность реквизитов по организации будем только в случае если все нормально с остальными рекизитами
	// Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// Проведем проверку соотвествия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соотвествия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;     	
	
	// Проверка состояний 
	// Прочитаем набор записей журнала состояний документа
	НаборЗаписейЖурналСостояний = РегистрыСведений.ЖурналСостояний.СоздатьНаборЗаписей();
	НаборЗаписейЖурналСостояний.Отбор.Объект.Значение      = Ссылка;
	НаборЗаписейЖурналСостояний.Отбор.Объект.Использование = Истина;
	НаборЗаписейЖурналСостояний.Прочитать();	
	
	// Выгрузим в таблицу значений для использования индекса строки
	ТаблицаСостояний = НаборЗаписейЖурналСостояний.Выгрузить();

	// Поиск строки с текущим состоянием
	СтрокаТаблицыЗначений = ТаблицаСостояний.Найти(Состояние);	
	Если СтрокаТаблицыЗначений <> Неопределено Тогда // Строка найдена
		// Индекс строки с текущим состоянием
		ИндексСтрокиТаблицыЗначений = ТаблицаСостояний.Индекс(СтрокаТаблицыЗначений);
		
		// Если текущее состояние не последнее, т.е. было уже установлено ранее,
		// то надо запретить запись
		Если (ИндексСтрокиТаблицыЗначений + 1) <> ТаблицаСостояний.Количество() Тогда
			дкДобавитьОшибку(Ошибки, "Состояние """+СокрЛП(Состояние)+""" уже было установлено "+СтрокаТаблицыЗначений.Период+" пользователем """+СтрокаТаблицыЗначений.Автор+""".");
			Результат = Ложь;
		КонецЕсли;
	ИначеЕсли Перечисления.СостояниеЖалобКлиентов.Индекс(Состояние) > 0 Тогда
		// Иначе надо проверить, не "перескочили" ли через какое-то состояние
		Для Индекс = 0 По Перечисления.СостояниеЖалобКлиентов.Индекс(Состояние)-1 Цикл
			ЗначениеПеречисления = Перечисления.СостояниеЖалобКлиентов.Получить(Индекс);
			
			НайденнаяСтрокаТаблицыЗначений = ТаблицаСостояний.Найти(ЗначениеПеречисления);
			Если НайденнаяСтрокаТаблицыЗначений = Неопределено Тогда
				дкДобавитьОшибку(Ошибки, "Состояние """+СокрЛП(ЗначениеПеречисления)+""" еще не было установлено.");
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЖалобаКлиента","Жалоба клиента",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя = "Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	ИначеЕсли Имя = "Контрагент" Тогда
		Если обЗначениеНеЗаполнено(Контрагент) Тогда 
			// если контрагент не указан то очистим договор
			Попытка
				ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			Исключение КонецПопытки; 
			Возврат Истина;
		Иначе
			
			ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент,, ЭтотОбъект, ДопПараметры);
			
			// Заполнение списка выбора контактной информации
			Если ЭтаФорма <> Неопределено Тогда
				ЗаполнитьСписокВыбораПоляВводаКИ(ЭтаФорма.ЭлементыФормы.КИКонтрагента.СписокВыбора, Контрагент);
			КонецЕсли;	
		КонецЕсли; 
		
		Возврат Истина;	
	ИначеЕсли Имя = "ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	ИначеЕсли Имя = "КЛКонтрагента" Тогда
		//Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Заполнение списка выбора контактной информации
		Если ЭтаФорма <> Неопределено Тогда
			ЗаполнитьСписокВыбораПоляВводаКИ(ЭтаФорма.ЭлементыФормы.КИКЛКонтрагента.СписокВыбора, КЛКонтрагента);
		КонецЕсли;  		
		Возврат Истина;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
//
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
 Возврат;
КонецПроцедуры

// Заполняет список выбора поля ввода контактной информацией
//
Процедура ЗаполнитьСписокВыбораПоляВводаКИ(СписокВыбора, ВладелецКИ) Экспорт
	МассивЗначений=СписокВыбора.ВыгрузитьЗначения();
	зфЗащищенныеФункцииСервер.ЖалобаКлиентаЗаполнитьСписокВыбораПоляВводаКИ(МассивЗначений,ВладелецКИ);
	СписокВыбора.ЗагрузитьЗначения(МассивЗначений);
КонецПроцедуры // ЗаполнитьСписокВыбораПоляВводаКИ()
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ЖалобаКлиента"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЖалобаКлиента(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ЖалобаКлиента");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок"); 
	
	ОбластьМакета.Параметры.ТекстЗаголовка = Ссылка;
	
	// Получение состояний жалобы из регистра сведений
	НаборЗаписейЖурналСостояний = РегистрыСведений.ЖурналСостояний.СоздатьНаборЗаписей();
	НаборЗаписейЖурналСостояний.Отбор.Объект.Значение      = Ссылка;
	НаборЗаписейЖурналСостояний.Отбор.Объект.Использование = Истина;
	НаборЗаписейЖурналСостояний.Прочитать();	
	
	ТаблицаСостояний = НаборЗаписейЖурналСостояний.Выгрузить();
	
	// Зафиксирована
	НайденнаяСтрокаСостояний = ТаблицаСостояний.Найти(Перечисления.СостояниеЖалобКлиентов.Зафиксирована);
	Если НайденнаяСтрокаСостояний <> Неопределено Тогда
		ОбластьМакета.Параметры.Зафиксирована = ""+НайденнаяСтрокаСостояний.Период+" ("+НайденнаяСтрокаСостояний.Автор+")";
	КонецЕсли;
	
	// Первый контакт
	МассивВидов = Новый Массив();
	МассивВидов.Добавить("Событие");
	
	МассивПодчиненныхСобытий = дкПолучитьМассивПодчиненных(Ссылка, МассивВидов);	
	Если  (МассивПодчиненныхСобытий <> Неопределено) 
		И (МассивПодчиненныхСобытий.Количество() > 0)
		Тогда
		ОбластьМакета.Параметры.ПервыйКонтакт = ""+МассивПодчиненныхСобытий[0].Дата+" ("+МассивПодчиненныхСобытий[0].Автор+")";
	КонецЕсли;	
	
	//Rarus agar {
	// Дата и время реакции
	ОбластьМакета.Параметры.ДатаИВремяРеакции = ДатаИВремяРеакции;
	//Rarus agar }
	
	// Разобрана
	НайденнаяСтрокаСостояний = ТаблицаСостояний.Найти(Перечисления.СостояниеЖалобКлиентов.Разобрана);
	Если НайденнаяСтрокаСостояний <> Неопределено Тогда
		ОбластьМакета.Параметры.Разобрана = ""+НайденнаяСтрокаСостояний.Период+" ("+НайденнаяСтрокаСостояний.Автор+")";
	КонецЕсли;
	
	//Rarus agar {
	// Дата и время разбора
	ОбластьМакета.Параметры.ДатаИВремяРазбора = ДатаИВремяРазбора;
	//Rarus agar }
	
	// Проверена
	НайденнаяСтрокаСостояний = ТаблицаСостояний.Найти(Перечисления.СостояниеЖалобКлиентов.Проверена);
	Если НайденнаяСтрокаСостояний <> Неопределено Тогда
		ОбластьМакета.Параметры.Проверена = ""+НайденнаяСтрокаСостояний.Период+" ("+НайденнаяСтрокаСостояний.Автор+")";
	КонецЕсли;
	
	// Закрыта
	НайденнаяСтрокаСостояний = ТаблицаСостояний.Найти(Перечисления.СостояниеЖалобКлиентов.Закрыта);
	Если НайденнаяСтрокаСостояний <> Неопределено Тогда
		ОбластьМакета.Параметры.Закрыта = ""+НайденнаяСтрокаСостояний.Период+" ("+НайденнаяСтрокаСостояний.Автор+")";
	КонецЕсли;    	
	
	// Контрагент
	ОбластьМакета.Параметры.Контрагент = спПолучитьПредставление(Контрагент)+?(ПустаяСтрока(КИКонтрагента),"",", "+СокрЛП(КИКонтрагента));
	
	// Контактное лицо контрагента
	ОбластьМакета.Параметры.КЛКонтрагента = СокрЛП(КЛКонтрагента)+?(ПустаяСтрока(КИКЛКонтрагента),"",", "+СокрЛП(КИКЛКонтрагента));
	
	//Rarus agar {
	// Документ-основание жалобы
	ОбластьМакета.Параметры.ДокументОснование = ДокументОснование;
	//Rarus agar }
	
	// Ответственный
	ОбластьМакета.Параметры.Ответственный = СокрЛП(Ответственный);
	
	// Категория жалобы
	СтрокаКатегорийЖалобы = "";
	Для Каждого СтрокаТабличнойЧасти Из Категории Цикл
		СтрокаКатегорийЖалобы = СтрокаКатегорийЖалобы + СокрЛП(СтрокаТабличнойЧасти.Категория)+"; ";
	КонецЦикла;	
    // Надо отрезать последнюю точку с запятой
	СтрокаКатегорийЖалобы = Лев(СтрокаКатегорийЖалобы, СтрДлина(СтрокаКатегорийЖалобы)-2);
	
	ОбластьМакета.Параметры.КатегорияЖалобы = СтрокаКатегорийЖалобы;
	
	//Rarus agar {
	// Клиент удовлетворен
	ОбластьМакета.Параметры.КлиентУдовлетворен = Формат(КлиентУдовлетворен, "БЛ='НЕ удовлетворен'; БИ=удовлетворен");
		
	// Жалоба справедлива
	ОбластьМакета.Параметры.ЖалобаСправедлива = Формат(ЖалобаСправедлива, "БЛ='НЕ справедлива'; БИ=справедлива");	
	//Rarus agar }
	
	// Суть жалобы
	ОбластьМакета.Параметры.СутьЖалобы = СокрЛП(Суть);
	
    // Мнение клиента
	ОбластьМакета.Параметры.МнениеКлиентаЗаголовок	= "Мнение клиента ("+СокрЛП(Контрагент)+")";
	ОбластьМакета.Параметры.МнениеКлиента			= МнениеКлиента;	
	
	// Мнение сотрудника
	ОбластьМакета.Параметры.МнениеСотрудникаЗаголовок = "Мнение сотрудника ("+СокрЛП(Сотрудник)+")";
	ОбластьМакета.Параметры.МнениеСотрудника = МнениеСотрудника;
	
	// Мнение ответственного
	ОбластьМакета.Параметры.МнениеОтветственногоЗаголовок = "Мнение ответственного ("+СокрЛП(Ответственный)+")";
	ОбластьМакета.Параметры.МнениеОтветственного = МнениеОтветственного;
	
	//Rarus agar {
	// Действия по исправлению 
	ОбластьМакета.Параметры.ДействияПоИсправлению = СокрЛП(ДействияПоИсправлению);
	
	// Действия по предотвращению
	ОбластьМакета.Параметры.ДействияПоПредотвращению = СокрЛП(ДействияПоПредотвращению);
	//Rarus agar }
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЖалобаКлиента()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события при установке нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()
  
// Обработчик события заполнения на основании
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.Опрос") Тогда
		Контрагент=Основание.ОпрашиваемоеЛицо;
		ОбработкаРеквизита("Контрагент");
	КонецЕсли; 
		
	#Если Клиент Тогда
	зфЖалобаКлиентаОбработкаЗаполнения(ЭтотОбъект,Основание);
	#Иначе
	зфЗащищенныеФункцииСервер.ЖалобаКлиентаОбработкаЗаполнения(ЭтотОбъект,Основание);
	#КонецЕсли
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события копирования
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		// Надо обновить надписи дат состояний
		НаборЗаписейЖурналСостояний = РегистрыСведений.ЖурналСостояний.СоздатьНаборЗаписей();
		НаборЗаписейЖурналСостояний.Отбор.Объект.Значение      = Ссылка;
		НаборЗаписейЖурналСостояний.Отбор.Объект.Использование = Истина;
		НаборЗаписейЖурналСостояний.Прочитать();	
		
		ТаблицаСостояний = НаборЗаписейЖурналСостояний.Выгрузить();	
		// Зафиксирована
		НайденнаяСтрокаСостояний = ТаблицаСостояний.Найти(Перечисления.СостояниеЖалобКлиентов.Зафиксирована);
		Если   НайденнаяСтрокаСостояний = Неопределено 
			И  Состояние = Перечисления.СостояниеЖалобКлиентов.Зафиксирована
			Тогда 		
			Если ДатаИВремяРеакции = '00010101' Тогда
				ДатаИВремяРеакции = ТекущаяДата()+60*60*обПраво(ПредопределенноеЗначение("ПланВидовХарактеристик.ПраваИНастройки.ВремяРеакцииНаЖалобу"));
			КонецЕсли;
			Если ДатаИВремяРазбора = '00010101' Тогда
				ДатаИВремяРазбора = ТекущаяДата() +60*60*обПраво(ПредопределенноеЗначение("ПланВидовХарактеристик.ПраваИНастройки.ВремяРазбораЖалобы"));
			КонецЕсли;     		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Надо обновить состояние документа
	Если НЕ Отказ Тогда
		Если НЕ орЖурналСостояний(Ссылка) Тогда
			Отказ=Истина;
		КонецЕсли; 
	КонецЕсли;
	
	#Если Клиент Тогда
		Оповестить("ОбновитьДеревоСобытий", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


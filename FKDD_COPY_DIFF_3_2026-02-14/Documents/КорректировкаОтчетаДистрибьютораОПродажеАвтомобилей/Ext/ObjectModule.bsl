Процедура ЗаполнитьТекВозмещение(Стр) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ТТ_ОстаткиВозмещенийОстатки.СуммаОстаток) КАК СуммаОстаток,
		|	СУММА(ТТ_ОстаткиВозмещенийОстатки.СуммаНДСОстаток) КАК СуммаНДСОстаток
		|ИЗ
		|	РегистрНакопления.ТТ_ОстаткиВозмещений.Остатки(&Дата1, ) КАК ТТ_ОстаткиВозмещенийОстатки
		|ГДЕ
		|	ТТ_ОстаткиВозмещенийОстатки.Автомобиль = &Автомобиль
		|	И ТТ_ОстаткиВозмещенийОстатки.ТипСкидки = &ТипСкидки";
	
	Запрос.УстановитьПараметр("Автомобиль", Стр.Автомобиль);
	Запрос.УстановитьПараметр("Дата1", ЭтотОбъект.Дата);
	Запрос.УстановитьПараметр("ТипСкидки", Стр.Скидка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Стр.ТекущееВозмещение = ВыборкаДетальныеЗаписи.СуммаОстаток;
		Стр.ТекущаяСуммаНДС = ВыборкаДетальныеЗаписи.СуммаНДСОстаток;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
	Дата = ТекущаяДата();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетДистрибьютораОПродажеАвтомобилей") Тогда
		Скидки.Очистить();
		// Заполнение шапки
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		ДокументОснование=ДанныеЗаполнения;
		ЭтотОбъект.УстановитьНовыйНомер();
		Для Каждого Стр из ДанныеЗаполнения.Скидки Цикл
			НовСтр = Скидки.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр,Стр);
			ЗаполнитьТекВозмещение(НовСтр);
		КонецЦикла;
		
	КонецЕсли;
	
	
	
	Если обЗначениеНеЗаполнено(Организация) Тогда
		Организация=ПараметрыСеанса.Организация;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		ПодразделениеКомпании=ПараметрыСеанса.ПодразделениеКомпании;		
	КонецЕсли;
	
	Автор=ПараметрыСеанса.Пользователь;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ХозОперация=Справочники.ХозОперации.КорректировкаОтчетаНаВозмещениеДистрибьютора;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	НаборЗаписей = РегистрыСведений.тт_Возмещения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	
	Для каждого Стр из Скидки Цикл
		
		Если обЗначениеНеЗаполнено(Стр.Автомобиль) Тогда
			Сообщить("В строке № "+Стр.НомерСтроки+" не заполнен Автомобиль! Запись прервана.");
			Отказ=Истина;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(Стр.Скидка) Тогда
			Сообщить("В строке № "+Стр.НомерСтроки+" не заполнен Тип скидки! Запись прервана.");
			Отказ=Истина;
		КонецЕсли;
		
		Если Стр.Скидка.Организация <> Организация Тогда
			
			Сообщить("Организация документа не соответствует Организации программы возмещения "+Стр.Скидка+". Автомобиль "+Стр.Автомобиль);
			
			Если НЕ РольДоступна("ПолныеПрава") Тогда
				Отказ=Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Стр.ТекущееВозмещение <> 0 Тогда
			//Проверка, на наличие корректируемых сумм
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СУММА(ТТ_ОстаткиВозмещенийОстатки.СуммаОстаток) КАК СуммаОстаток
			|ИЗ
			|	РегистрНакопления.ТТ_ОстаткиВозмещений.Остатки(
			|			&Момент,
			|			Автомобиль = &Автомобиль
			|				И ТипСкидки = &ТипСкидки) КАК ТТ_ОстаткиВозмещенийОстатки";
			
			Запрос.УстановитьПараметр("Автомобиль", Стр.Автомобиль);
			Запрос.УстановитьПараметр("Момент", Новый Граница(ЭтотОбъект.Дата, ВидГраницы.Исключая));
			Запрос.УстановитьПараметр("ТипСкидки", Стр.Скидка);
			
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() ИЛИ РезультатЗапроса.Выгрузить()[0].Суммаостаток = 0 Тогда
				Сообщить("Нет остатков сумм возмещения по программе "+Стр.Скидка+". Автомобиль: "+Стр.Автомобиль);
				Отказ=Истина;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		//Формирование движений 
		Движения.ТТ_ОстаткиВозмещений.Записывать=Истина;
		Движ=Движения.ТТ_ОстаткиВозмещений.Добавить();	
		Движ.Период=ЭтотОбъект.Дата;
		дВиж.ВидДвижения=ВидДвиженияНакопления.Приход;
		Движ.Автомобиль=Стр.Автомобиль;
		Движ.Поставщик = Стр.Скидка.Контрагент;
		Движ.ТипСкидки=Стр.Скидка;
		Движ.Сумма= -1 * (Стр.ТекущееВозмещение - Стр.СуммаВозмещения);
		Движ.СуммаНДС= -1 * (Стр.ТекущаяСуммаНДС - Стр.СуммаНДС);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Автомобиль = Стр.Автомобиль;
		НоваяЗапись.СтатусВозмещения = Справочники.ТТ_СтатусыВозмещений.КоррОтчет;
		НоваяЗапись.Скидка = Стр.Скидка;
		НоваяЗапись.Период = Ссылка.Дата;
		НоваяЗапись.СуммаВозмещения = -1 * (Стр.ТекущееВозмещение - Стр.СуммаВозмещения);
		НоваяЗапись.СуммаНДС = -1 * (Стр.ТекущаяСуммаНДС - Стр.СуммаНДС);
		
	КонецЦикла;
	
	НаборЗаписей.Записать(ИСТИНА);
	
КонецПроцедуры

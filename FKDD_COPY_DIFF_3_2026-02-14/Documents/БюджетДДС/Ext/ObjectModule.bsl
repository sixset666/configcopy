// Модуль документа БюджетДДС

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ВалютаУпр;					// Валюта управленческого учета
Перем ВалютаРег;					// Валюта регламентированного учета 
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеРеквизитыТабЧасти = Новый Структура();
	ОбязательныеРеквизитыТабЧасти.Вставить("СтатьяДДС",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("СценарийПланирования", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);  	
	ОбязательныеРеквизиты.Вставить("СтатьиДДС", ОбязательныеРеквизитыТабЧасти);
		
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки = "", ДопРеквизиты = Неопределено, Заполнение = Истина, Уникальность = Истина) Экспорт
	
	Результат = Истина;	
	
	// Запретим запись с невыбранным сценарием планирования
	Если обЗначениеНеЗаполнено(СценарийПланирования) ИЛИ обЗначениеНеЗаполнено(СценарийПланирования.Периодичность) Тогда
		дкДобавитьОшибку(Ошибки, "Не выбран сценарий планирования, либо у выбранного сценария не выбрана периодичность планирования.");		
		Результат = Ложь;		
	КонецЕсли;
	
	//БизТех Закомментировано <<
	//// проверим правильность введенного подразделения хоз.операции.
	//Результат = плПроверитьКорректностьВыбораПодразделенияИХозОперации(ХозОперация, ПодразделениеКомпании) И Результат;
	//Если НЕ Результат Тогда
	//	дкДобавитьОшибку(Ошибки, "Для данного подразделения нельзя вводить данный вид бюджета.");		
	//КонецЕсли;
	//>>
	
	Если ИспользоватьРаздельныеГрафикиПогашения Тогда
		// Проверим на уникальность периодов для каждой статьи ДДС
		Параметры = Новый Структура;	
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		
		тзВрем = ГрафикРаспределенияДС.Выгрузить();
		тзВрем.Колонки.Добавить("Уникальность", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)) ); 
		тзВрем.ЗаполнитьЗначения(1, "Уникальность");
		
		тзВрем.Свернуть("СтатьяДДС, Период", "Уникальность, Процент");
		Для Каждого ТекСтр Из тзВрем Цикл
			Если ТекСтр.Уникальность>1 Тогда
				Параметры.Вставить("Действие", ТекСтр.Период);
				ТекстОшибки = "У статьи <" + ТекСтр.СтатьяДДС + "> период <" + плПолучитьДатыПланируемогоПериода(Параметры) +  "> не уникален.";			
				дкДобавитьОшибку(Ошибки, ТекстОшибки);
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		тзВрем.Свернуть("СтатьяДДС", "Процент");
		// Проверим сумму процентов по каждой статье
		Для Каждого ТекСтр Из тзВрем Цикл
			Если ТекСтр.Процент > 100 Тогда 						
				ТекстОшибки = "У статьи <" + ТекСтр.СтатьяДДС + "> сумма процентов по всем периодам превышает 100%";
				дкДобавитьОшибку(Ошибки, ТекстОшибки);
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		// Проверим уникальность периодов (смещений)		
		тзВрем = ГрафикРаспределенияДС.Выгрузить();
		тзВрем.Колонки.Добавить("Уникальность", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)) ); 
        тзВрем.ЗаполнитьЗначения(1, "Уникальность");
		тзВрем.Свернуть("Период", "Уникальность, Процент");
		Для Каждого ТекСтр Из тзВрем Цикл
			Если ТекСтр.Уникальность>1 Тогда
				Параметры.Вставить("Действие", ТекСтр.Период);
				ТекстОшибки = "Период <" + плПолучитьДатыПланируемогоПериода(Параметры) +  "> не уникален.";			
				дкДобавитьОшибку(Ошибки, ТекстОшибки);
				Результат = Ложь;   				
			КонецЕсли;
		КонецЦикла;
		
		// Проверим процент
		ИтогПроцент = тзВрем.Итог("Процент");
		
		Если ИтогПроцент > 100 Тогда
			ТекстОшибки = "Сумма процентов по всем периодам превышает 100%";
			дкДобавитьОшибку(Ошибки, ТекстОшибки);
			Результат = Ложь;	
		КонецЕсли;    		
	КонецЕсли;      
	
	Результат = (дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И Результат);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты = Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа.
//  Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя, ТекСтрока=Неопределено, ЭтаФорма=Неопределено, ДопПараметры=Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СценарийПланирования" Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		плПолучитьДатыПланируемогоПериода(Параметры); 	
		ДатаПланирования = Параметры.ДатаНачала;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "СТАТЬИ ДДС"
	ИначеЕсли Имя = "СтатьиДДС.СтатьяДДС" Тогда
		Если ТекСтрока.БазовыйБюджет <> Неопределено Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.СтатьяДДС) Тогда
				Если ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Приход Тогда
					Если ТекСтрока.БазовыйБюджет.Метаданные().Имя = "БюджетЗакупок" Тогда
						ТекСтрока.БазовыйБюджет = Неопределено;	
					КонецЕсли;
				ИначеЕсли ТекСтрока.СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Расход Тогда
					Если ТекСтрока.БазовыйБюджет.Метаданные().Имя = "БюджетПродаж" Тогда
						ТекСтрока.БазовыйБюджет = Неопределено;	
					КонецЕсли;
				Иначе
					ТекСтрока.БазовыйБюджет = Неопределено;	
				КонецЕсли;
			КонецЕсли;
			
			ОбработкаРеквизита("СтатьиДДС.БазовыйБюджет", ТекСтрока);
		КонецЕсли;
		
		// Возможно появились строки призраки, их нужно аннигилировать.
		мсвСтрокиДляУдаления = Новый Массив;
		Для Каждого ТекСтр Из ГрафикРаспределенияДС Цикл
			Если СтатьиДДС.Найти(ТекСтр.СтатьяДДС, "СтатьяДДС") = Неопределено Тогда
				мсвСтрокиДляУдаления.Добавить(ТекСтр);		
			КонецЕсли;
		КонецЦикла;
		
		// Непосредственно само удаление
		Для Каждого ТекСтр Из мсвСтрокиДляУдаления Цикл
			ГрафикРаспределенияДС.Удалить(ТекСтр);
		КонецЦикла;
		
	ИначеЕсли Имя="СтатьиДДС.БазоваяСуммаУпр" Тогда
		// Пересчитаем распределения
		Если ИспользоватьРаздельныеГрафикиПогашения Тогда
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("СтатьяДДС", ТекСтрока.СтатьяДДС);
			мсвСтрок = ГрафикРаспределенияДС.НайтиСтроки(СтруктураПоиска);
			Для Каждого ТекСтрокаГрафика Из мсвСтрок Цикл   		
				ОбработкаРеквизита("ГрафикРаспределенияДС.Процент", ТекСтрокаГрафика, ЭтаФорма, ТекСтрока.БазоваяСуммаУпр);	
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя = "СтатьиДДС.БазовыйБюджет" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.БазовыйБюджет) Тогда
			ТекСтрока.БазоваяСуммаУпр = ТекСтрока.БазовыйБюджет.СуммаДокумента; 			
		Иначе
			ТекСтрока.БазоваяСуммаУпр = 0; 
		КонецЕсли;
		ОбработкаРеквизита("СтатьиДДС.БазоваяСуммаУпр", ТекСтрока);
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ГРАФИК РАСПРЕДЕЛЕНИЯ ДЕНЕЖНЫХ СРЕДСТВ" 	
	ИначеЕсли Имя = "ГрафикРаспределенияДС.Процент" Тогда
		Если ИспользоватьРаздельныеГрафикиПогашения Тогда
			ТекСтрока.СуммаПогашенияУпр = ТекСтрока.Процент * ДопПараметры / 100;
		КонецЕсли;
		
	ИначеЕсли Имя = "ГрафикРаспределенияДС.СуммаПогашенияУпр" Тогда
		Если ДопПараметры = 0 Тогда
			ТекСтрока.Процент = 0;
		Иначе
			ТекСтрока.Процент = ТекСтрока.СуммаПогашенияУпр / ДопПараметры * 100;	
		КонецЕсли;		
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);		
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("БюджетДДС", "Бюджет движения денежных средств", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда  	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// ПечатьБюджетДДССГрафиками - Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьБюджетДДССГрафиками(ТабДокумент) Экспорт
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Для начала, получим данные в удобном виде
	Запрос = Новый Запрос;
	Если ИспользоватьРаздельныеГрафикиПогашения Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС КАК СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ КАК Период,
		|	СУММА(БюджетДДСГрафикРаспределенияДС.СуммаПогашенияУпр) КАК СуммаУпр,
		|	СУММА(БюджетДДСГрафикРаспределенияДС.Процент) КАК Процент
		|ИЗ
		|	Документ.БюджетДДС.ГрафикРаспределенияДС КАК БюджетДДСГрафикРаспределенияДС
		|ГДЕ
		|	БюджетДДСГрафикРаспределенияДС.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС.Наименование,
		|	Период
		|ИТОГИ
		|	СУММА(СуммаУпр),
		|	СУММА(Процент)
		|ПО
		|	СтатьяДДС,
		|	Период";
	Иначе
		 Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетДДССтатьиДДС.СтатьяДДС КАК СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ КАК Период,
		|	СУММА(БюджетДДСГрафикРаспределенияДС.Процент) КАК Процент,
		|	СУММА(БюджетДДССтатьиДДС.БазоваяСуммаУпр * БюджетДДСГрафикРаспределенияДС.Процент / 100) КАК СуммаУпр
		|ИЗ
		|	Документ.БюджетДДС.ГрафикРаспределенияДС КАК БюджетДДСГрафикРаспределенияДС,
		|	Документ.БюджетДДС.СтатьиДДС КАК БюджетДДССтатьиДДС
		|ГДЕ
		|	БюджетДДСГрафикРаспределенияДС.Ссылка = &Ссылка
		|	И БюджетДДССтатьиДДС.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ,
		|	БюджетДДССтатьиДДС.СтатьяДДС
		|
		|УПОРЯДОЧИТЬ ПО
		|	БюджетДДССтатьиДДС.СтатьяДДС.Наименование,
		|	Период
		|ИТОГИ
		|	СУММА(Процент),
		|	СУММА(СуммаУпр)
		|ПО
		|	СтатьяДДС,
		|	Период"; 		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка",           Ссылка);
	Запрос.УстановитьПараметр("ДатаПланирования", ДатаПланирования);
	Запрос.УстановитьПараметр("Периодичность",    Строка(СценарийПланирования.Периодичность));
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПериодов  = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	
	Параметры = Новый Структура;
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	СписокПериодов = Новый СписокЗначений;
	Пока ВыборкаПериодов.Следующий() Цикл
		Параметры.Вставить("ДатаИзПериода", ВыборкаПериодов.Период);
		СписокПериодов.Добавить(ВыборкаПериодов.Период, плПолучитьДатыПланируемогоПериода(Параметры));			
	КонецЦикла;
	СписокПериодов.СортироватьПоЗначению();
	
	// Получаем макет
	Макет = ПолучитьМакет("БюджетДДССГрафиками");      
	
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	// Заголовок
	Параметры.Очистить();
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	Параметры.Вставить("ДатаИзПериода", ДатаПланирования);                                                 		
	ПредставлениеПериода = плПолучитьДатыПланируемогоПериода(Параметры);
	
	Параметры.Очистить(); 	 
	Параметры.Вставить("Область", "Заголовок");		
	Параметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	Параметры.Вставить("СценарийПланирования",  СценарийПланирования);
	Параметры.Вставить("ТекстЗаголовка",        ХозОперация.Наименование + " № " + дкПолучитьНомерДляПечати(ЭтотОбъект) + " от " + Формат(Дата, "ДФ=dd.MM.yyyy"));
	Параметры.Вставить("ПериодПланирования",    ПредставлениеПериода);     	
    Параметры.Вставить("Список",                СписокПериодов);
	
	ОбластьМакета = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	ТабДокумент.Вывести(ОбластьМакета);

	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;

	// Готовим и выводим шапку
	Параметры.Очистить();
	Параметры.Вставить("Область", ?(ИспользоватьРаздельныеГрафикиПогашения, "ШапкаТаблицыРаздельныеГрафики", "ШапкаТаблицы") );	
	
	// Подготовим список
	мсвВрем = Новый Массив;	
	Для Каждого ТекПериод Из СписокПериодов Цикл	
		СтруктураВрем = Новый Структура;
		СтруктураВрем.Вставить("Период", ТекПериод.Представление);
		мсвВрем.Добавить(СтруктураВрем);
	КонецЦикла;
	Параметры.Вставить("Список", мсвВрем);
	
	ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	// Подготовим шапку для дальнейшего использования, если строки не влезут на страницу
	Параметры.Вставить("ТекстЗаголовка", ХозОперация.Наименование+" № " + дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy"));
	ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// Подготовим подвал страницы
	Параметры.Вставить("Область", "ПодвалСтраницы");
	ОбластьПодвалаСтраницыТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// Ну теперь строки пойдут.
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	Параметры.Очистить();
	Параметры.Вставить("Область", ?(ИспользоватьРаздельныеГрафикиПогашения, "СтрокаТаблицыРаздельныеГрафики", "Строка"));
	
	НомерСтроки = 1;
	ВыборкаСтатей = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяДДС");
	Пока ВыборкаСтатей.Следующий() Цикл
		
		// Подготавливаем список
		мсвВрем = Новый Массив;
		ВыборкаПериодов = ВыборкаСтатей.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
		Пока ВыборкаПериодов.Следующий() Цикл
			СтруктураВрем = Новый Структура;
			СтруктураВрем.Вставить("СуммаПериодаУпр", Формат(ВыборкаПериодов.СуммаУпр, ФорматВыводаСуммы));
			СтруктураВрем.Вставить("Процент",         "" + ?(ВыборкаПериодов.Процент = NULL, "", ?(ВыборкаПериодов.Процент = 0, "", "" + ВыборкаПериодов.Процент + "%") ));
			мсвВрем.Добавить(СтруктураВрем);
		КонецЦикла;
		Параметры.Вставить("Список",      мсвВрем); 
		Параметры.Вставить("СтатьяДДС",   ВыборкаСтатей.СтатьяДДС);
		Параметры.Вставить("НомерСтроки", НомерСтроки);
		
		ОбластьСтрокиТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);
		НомерСтраницы        = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокиТаблицы, ОбластьШапкаТаблицы, ОбластьПодвалаСтраницыТаблицы, НомерСтраницы, Права);
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Если НомерСтраницы > 2 Тогда
		ТабДокумент.Вывести(ОбластьПодвалаСтраницыТаблицы);
	КонецЕсли;
	
	// Завершаем печать статей
	Если НЕ Параметры.Свойство("Список") Тогда
		Параметры.Вставить("Список", Новый Массив);		
    КонецЕсли;
	Параметры.Вставить("Область", "ПодвалСтатей");
	ОбластьПодвалаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
    НомерСтраницы = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалаТаблицы, , , НомерСтраницы, Права);

	// Подготовим подвал документа
	Параметры.Вставить("Область", "Подвал");
	Параметры.Вставить("Автор",   Автор);	
	ОбластьПодвалаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// График выведем
	Если НЕ ИспользоватьРаздельныеГрафикиПогашения Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность); //Для получения представления периода
		
		ТабГрафикРаспределения = Новый ТабличныйДокумент;
		ТабГрафикРаспределения.Вывести(Макет.ПолучитьОбласть("ШапкаГрафика"));
		ТабГрафикРаспределения.Вывести(Макет.ПолучитьОбласть("Начало"));
		
		ВыборкаСтатей = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяДДС");
		Если ВыборкаСтатей.Следующий() Тогда
			ВыборкаПериодов = ВыборкаСтатей.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
			Пока ВыборкаПериодов.Следующий() Цикл
				ТекОбл = Макет.ПолучитьОбласть("ПериодГрафика");
				Параметры.Вставить("ДатаИзПериода", ВыборкаПериодов.Период); 
				ТекОбл.Параметры.Период  = плПолучитьДатыПланируемогоПериода(Параметры);
				ТекОбл.Параметры.Процент = "" + ВыборкаПериодов.Процент + "%";
				ТабГрафикРаспределения.Присоединить(ТекОбл);		
			КонецЦикла;
		КонецЕсли;
		ТабГрафикРаспределения.Присоединить(Макет.ПолучитьОбласть("ОкончаниеГрафика"));	
		ВывестиГоризонтальнуюОбласть(ТабДокумент, ТабГрафикРаспределения, , , НомерСтраницы, Права);
	КонецЕсли;

	// Выводим подвал
    НомерСтраницы = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалаТаблицы, , , НомерСтраницы, Права);
                  	
	Возврат ТабДокумент; 
КонецФункции // ПечатьБюджетДДССГрафиками()

// ПечатьБюджетДДСБезГрафиков - Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьБюджетДДСБезГрафиков(ТабДокумент) Экспорт
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Для начала, получим данные в удобном виде
	Запрос = Новый Запрос;
	Если ИспользоватьРаздельныеГрафикиПогашения Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС КАК СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ КАК Период,
		|	СУММА(БюджетДДСГрафикРаспределенияДС.СуммаПогашенияУпр) КАК СуммаУпр,
		|	СУММА(БюджетДДСГрафикРаспределенияДС.Процент) КАК Процент
		|ИЗ
		|	Документ.БюджетДДС.ГрафикРаспределенияДС КАК БюджетДДСГрафикРаспределенияДС
		|ГДЕ
		|	БюджетДДСГрафикРаспределенияДС.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	БюджетДДСГрафикРаспределенияДС.СтатьяДДС.Наименование,
		|	Период
		|ИТОГИ
		|	СУММА(СуммаУпр),
		|	СУММА(Процент)
		|ПО
		|	СтатьяДДС,
		|	Период";
	Иначе
		 Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетДДССтатьиДДС.СтатьяДДС КАК СтатьяДДС,
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ КАК Период,
		|	СУММА(БюджетДДСГрафикРаспределенияДС.Процент) КАК Процент,
		|	СУММА(БюджетДДССтатьиДДС.БазоваяСуммаУпр * БюджетДДСГрафикРаспределенияДС.Процент / 100) КАК СуммаУпр
		|ИЗ
		|	Документ.БюджетДДС.ГрафикРаспределенияДС КАК БюджетДДСГрафикРаспределенияДС,
		|	Документ.БюджетДДС.СтатьиДДС КАК БюджетДДССтатьиДДС
		|ГДЕ
		|	БюджетДДСГрафикРаспределенияДС.Ссылка = &Ссылка
		|	И БюджетДДССтатьиДДС.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА &Периодичность = ""Год""
		|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, ГОД, БюджетДДСГрафикРаспределенияДС.Период)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Периодичность = ""Квартал""
		|					ТОГДА ДОБАВИТЬКДАТЕ(&ДатаПланирования, КВАРТАЛ, БюджетДДСГрафикРаспределенияДС.Период)
		|				ИНАЧЕ ДОБАВИТЬКДАТЕ(&ДатаПланирования, МЕСЯЦ, БюджетДДСГрафикРаспределенияДС.Период)
		|			КОНЕЦ
		|	КОНЕЦ,
		|	БюджетДДССтатьиДДС.СтатьяДДС
		|
		|УПОРЯДОЧИТЬ ПО
		|	БюджетДДССтатьиДДС.СтатьяДДС.Наименование,
		|	Период
		|ИТОГИ
		|	СУММА(Процент),
		|	СУММА(СуммаУпр)
		|ПО
		|	СтатьяДДС,
		|	Период"; 		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка",           Ссылка);
	Запрос.УстановитьПараметр("ДатаПланирования", ДатаПланирования);
	Запрос.УстановитьПараметр("Периодичность",    Строка(СценарийПланирования.Периодичность));
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПериодов  = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	
	Параметры = Новый Структура;
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	СписокПериодов = Новый СписокЗначений;
	Пока ВыборкаПериодов.Следующий() Цикл
		Параметры.Вставить("ДатаИзПериода", ВыборкаПериодов.Период);
		СписокПериодов.Добавить(ВыборкаПериодов.Период, плПолучитьДатыПланируемогоПериода(Параметры));			
	КонецЦикла;
	СписокПериодов.СортироватьПоЗначению();
	
	// Получаем макет
	Макет = ПолучитьМакет("БюджетДДСБезГрафиков");      
	
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	// Заголовок
	Параметры.Очистить();
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	Параметры.Вставить("ДатаИзПериода", ДатаПланирования);                                                 		
	ПредставлениеПериода = плПолучитьДатыПланируемогоПериода(Параметры);
	
	Параметры.Очистить(); 	 
	Параметры.Вставить("Область", "Заголовок");		
	Параметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	Параметры.Вставить("СценарийПланирования",  СценарийПланирования);
	Параметры.Вставить("ТекстЗаголовка",        ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy"));
	Параметры.Вставить("ПериодПланирования",    ПредставлениеПериода);     	
    Параметры.Вставить("Список",                СписокПериодов);
	
	ОбластьМакета = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	ТабДокумент.Вывести(ОбластьМакета);

	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;

	// Готовим и выводим шапку
	Параметры.Очистить();
	Параметры.Вставить("Область", "ШапкаТаблицы");	
	
	// Подготовим список
	мсвВрем = Новый Массив;	
	Для Каждого ТекПериод Из СписокПериодов Цикл	
		СтруктураВрем = Новый Структура;
		СтруктураВрем.Вставить("Период", ТекПериод.Представление);
		мсвВрем.Добавить(СтруктураВрем);
	КонецЦикла;
	Параметры.Вставить("Список", мсвВрем);
	
	ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	// Подготовим шапку для дальнейшего использования, если строки не влезут на страницу
	Параметры.Вставить("ТекстЗаголовка", ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy"));
	ОбластьШапкаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// Подготовим подвал страницы
	Параметры.Вставить("Область", "ПодвалСтраницы");
	ОбластьПодвалаСтраницыТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	// Подготовим подвал документа
	Параметры.Вставить("Область", "Подвал");
	Параметры.Вставить("Автор",   Автор);
	ОбластьПодвалаТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);	
	
	
	// Ну теперь строки пойдут.
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	Параметры.Очистить();
	Параметры.Вставить("Область", "Строка");
	
	НомерСтроки = 1;
	ВыборкаСтатей = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяДДС");
	Пока ВыборкаСтатей.Следующий() Цикл
		
		// Подготавливаем список
		мсвВрем = Новый Массив;
		ВыборкаПериодов = ВыборкаСтатей.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
		Пока ВыборкаПериодов.Следующий() Цикл
			СтруктураВрем = Новый Структура;
			СтруктураВрем.Вставить("СуммаПериодаУпр", Формат(ВыборкаПериодов.СуммаУпр, ФорматВыводаСуммы));
			СтруктураВрем.Вставить("Процент",         "" + ?(ВыборкаПериодов.Процент = NULL, "", ?(ВыборкаПериодов.Процент=0, "", "" + ВыборкаПериодов.Процент + "%") ));
			мсвВрем.Добавить(СтруктураВрем);
		КонецЦикла;
		Параметры.Вставить("Список",      мсвВрем); 
		Параметры.Вставить("СтатьяДДС",   ВыборкаСтатей.СтатьяДДС);
		Параметры.Вставить("НомерСтроки", НомерСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если НомерСтроки = ВыборкаСтатей.Количество() Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвалаТаблицы);
		КонецЕсли;
		
		ОбластьСтрокиТаблицы = СформироватьОбластьТабличногоДокумента(Макет, Параметры);
		НомерСтраницы        = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокиТаблицы, ОбластьШапкаТаблицы, ОбластьПодвалаСтраницыТаблицы,
			НомерСтраницы, Права, мсвДопОбластиПодвала);
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Если НомерСтраницы > 2 Тогда
		ТабДокумент.Вывести(ОбластьПодвалаСтраницыТаблицы);
	КонецЕсли;
	
	// Выводим подвал
	НомерСтраницы = ВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалаТаблицы, , , НомерСтраницы, Права);
		
	Возврат ТабДокумент; 
КонецФункции // ПечатьБюджетДДСБезГрафиков()

// СформироватьОбластьТабличногоДокумента - Формирует область табличного документа.
//
// Параметры:
//  Макет     - макет, переданный для формирования табличного документа.
//  Параметры - структура, передает параметры заполнения области табличного документа.
//
// Возвращаемое значение:
//  ТабДок - Возвращает табличный документ.
//
Функция СформироватьОбластьТабличногоДокумента(Знач Макет, Знач Параметры)
	ТабДок = Новый ТабличныйДокумент;
	ТекОблПериод = Макет.ПолучитьОбласть("" + Параметры.Область + "|СтатьяДДС"); 	
	ЗаполнитьЗначенияСвойств(ТекОблПериод.Параметры, Параметры);		
	ТабДок.Вывести(ТекОблПериод);
	
	Для Каждого ТекПериод Из Параметры.Список Цикл
		ТекОблПериод = Макет.ПолучитьОбласть("" + Параметры.Область + "|Период");
		ЗаполнитьЗначенияСвойств(ТекОблПериод.Параметры, ТекПериод);
		ТабДок.Присоединить(ТекОблПериод);
	КонецЦикла;
	
	ТабДок.Присоединить(Макет.ПолучитьОбласть("" + Параметры.Область + "|Граница"));
	Возврат ТабДок;
КонецФункции // СформироватьОбластьТабличногоДокумента()

// ВывестиГоризонтальнуюОбласть - выводит переданные области табличного документа
// 
// Параметры:
//  ТабличныйДокумент       - табличный документ.
//  ОбластьСтроки           - табличный документ, содержит область строки,
//  ОбластьШапкиСтраницы    - табличный документ, содержит область Шапки,
//  ОбластьПодвалаСтраницы  - табличный документ, содержит область Подвала,
//  НомерСтраницы           - число номер текущей страницы,
//  Право                   - переменная, в которой кэшируются права пользователя
//  мсвДопОбластиПодвала    - массив.
//
//  Возвращаемое значение:
//   НомерСтраницы          - число, номер страницы.
//
Функция ВывестиГоризонтальнуюОбласть(ТабличныйДокумент, ОбластьСтроки, ОбластьШапкиСтраницы, ОбластьПодвалаСтраницы=Неопределено, НомерСтраницы, Право, мсвДопОбластиПодвала=Неопределено)
	ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы",Права,,ЭтотОбъект);
	
	//если не разбиваем, выводим и все
	Если НЕ ИспользоватьРазбиениеНаСтраницы Тогда
		ТабличныйДокумент.Вывести(ОбластьСтроки);
		Возврат НомерСтраницы;
	КонецЕсли;
	
	МассивОбластейДляПроверки = Новый Массив;
	МассивОбластейДляПроверки.Добавить(ОбластьСтроки);
	Если ОбластьПодвалаСтраницы <> Неопределено Тогда
		МассивОбластейДляПроверки.Добавить(ОбластьПодвалаСтраницы);
	КонецЕсли;
	
	Попытка
		РезультатПроверки = ТабличныйДокумент.ПроверитьВывод(МассивОбластейДляПроверки);
		Если НЕ РезультатПроверки Тогда
			Если ОбластьПодвалаСтраницы <> Неопределено Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвалаСтраницы);
			КонецЕсли;
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			НомерСтраницы = НомерСтраницы + 1;
			
			Если ОбластьШапкиСтраницы <> Неопределено Тогда
				ТабличныйДокумент.Вывести(ОбластьШапкиСтраницы);
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ТабличныйДокумент.Вывести(ОбластьСтроки);
		
	Возврат НомерСтраницы; 	
КонецФункции // ВывестиГоризонтальнуюОбласть()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы = "", КоличествоЭкземпляров = 0, НаПринтер = Ложь, Документ = Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)  	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , , ,"10111") Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда 
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли; 
	
	// Собственно само проведение
	НаборЗаписей = Движения.БюджетДДС;
	НаборЗаписей.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписей.ДокументОбъект        = Ссылка;
	НаборЗаписей.РежимПроведения       = Режим;
	
	Отказ = НЕ НаборЗаписей.Приход();
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);  	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
ВалютаРег = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
   
// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

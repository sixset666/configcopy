
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Объект.Автор = ПараметрыСеанса.Пользователь;
		Объект.ПодразделениеКомпании = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
		Объект.Организация = Справочники.Организации.ОсновнаяОрганизация;
		Объект.ПериодРасчета = НачалоМесяца(ТекущаяДата());

	КонецЕсли;	
	
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Объект.ПериодРасчета);

КонецПроцедуры

&НаКлиенте
Процедура ПериодРасчетаПриИзменении(Элемент)
	
	Мотивация.ДатаКакМесяцПодобратьДатуПоТексту(ДатаПланирования, Объект.ПериодРасчета);
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Объект.ПериодРасчета);

КонецПроцедуры

&НаКлиенте
Процедура ПериодРасчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораИзСпискаПредставленияПериодаРегистрации(ДатаПланирования, СтандартнаяОбработка, Объект.ПериодРасчета);

КонецПроцедуры

&НаКлиенте
Процедура ПериодРасчетаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Объект.ПериодРасчета = ДобавитьМесяц(Объект.ПериодРасчета, Направление);
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Объект.ПериодРасчета);

КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораИзСпискаПредставленияПериодаРегистрации(ПериодПланирования, СтандартнаяОбработка, ПериодРегистрации, НачальноеЗначение = Неопределено, МинГод=Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если НачальноеЗначение = Неопределено Тогда
		НачальноеЗначение = ПериодРегистрации;
	КонецЕсли; 
	
	СписокВыбора = Новый СписокЗначений;
	НачалоТекущегоГода = НачалоГода(НачальноеЗначение);
	НачалоПрошлогоГода = НачалоГода(НачалоТекущегоГода - 1);
	
	Если НЕ МинГод=Неопределено Тогда
		Если НачалоПрошлогоГода<МинГод Тогда
			НачалоПрошлогоГода =МинГод;
		КонецЕсли;
	КонецЕсли;
	
	СписокВыбора.Добавить(НачалоПрошлогоГода, (Формат(НачалоПрошлогоГода, "ДФ='yyyy'") + "..."));
	НачалоМесяцаЗаполнения = НачалоТекущегоГода;
	ЭлементПоУмолчанию = Неопределено;
	Для а = 1 По 12 Цикл
		Если НЕ МинГод=Неопределено Тогда
			
			Если НачалоМесяцазаполнения<МинГод Тогда
				НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ДобавленныйЭлемент = СписокВыбора.Добавить(НачалоМесяцаЗаполнения, Мотивация.ДатаКакМесяцПредставление(НачалоМесяцаЗаполнения));
		Если НачальноеЗначение = НачалоМесяцаЗаполнения Тогда
			ЭлементПоУмолчанию = ДобавленныйЭлемент;
		КонецЕсли; 
		
		НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
	КонецЦикла;
	НачалоСледующегоГода = КонецГода(НачалоТекущегоГода) + 1;
	СписокВыбора.Добавить(НачалоСледующегоГода, (Формат(НачалоСледующегоГода, "ДФ='yyyy'") + "..."));
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ПериодПланирования",ПериодПланирования);
	ДопПараметры.Вставить("МинГод",МинГод);
	ДопПараметры.Вставить("НачальноеЗначение",НачальноеЗначение);
	ДопПараметры.Вставить("СтандартнаяОбработка",СтандартнаяОбработка);
	ДопПараметры.Вставить("ПериодРегистрации",ПериодРегистрации);
	
    ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПериодаРегистрацииНачалоВыбораЗавершение", ЭтотОбъект, ДопПараметры), СписокВыбора, ПериодПланирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодаРегистрацииНачалоВыбораЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	ИначеЕсли Год(ВыбранныйЭлемент.Значение) <> Год(ДополнительныеПараметры.НачальноеЗначение) Тогда
		НачалоВыбораИзСпискаПредставленияПериодаРегистрации(ДополнительныеПараметры.ПериодПланирования, ДополнительныеПараметры.СтандартнаяОбработка, ДополнительныеПараметры.ПериодРегистрации, ВыбранныйЭлемент.Значение, ДополнительныеПараметры.МинГод);
		Возврат;
	КонецЕсли;
	
	Объект.ПериодРасчета = ВыбранныйЭлемент.Значение;
	ДатаПланирования  = Мотивация.ДатаКакМесяцПредставление(Объект.ПериодРасчета);

КонецПроцедуры


&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Объект.ПериодРасчета);

КонецПроцедуры


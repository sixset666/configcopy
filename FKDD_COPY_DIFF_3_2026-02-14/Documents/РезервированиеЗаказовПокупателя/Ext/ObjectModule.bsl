// Модуль документа РезервированиеЗаказовПокупателя

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("МестоРазмещения",3);
	ОбязательныеТовары.Вставить("ЗаказПокупателя",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	//ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			КонтролируемыеРеквизитыСостава.Вставить("МестоРазмещения", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыСостава.Вставить("ЗаказПокупателя");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыСостава);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);		
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РезервированиеПодЗаказПокупателя","Резервирование под заказ покупателя",Истина);
	СписокХозОпераций.Добавить("РезервированиеПодВнутреннийЗаказ","Резервирование под внутренний заказ",Ложь);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  СуммаДокумента - число - сумма документа.
//
Функция РассчитатьСуммуВсего()Экспорт
	Если СуммаДокумента<>0 Тогда СуммаДокумента=0; КонецЕсли; 
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ХозОперация=Справочники.ХозОперации.РезервированиеПодЗаказПокупателя Тогда
			Если ТипЗнч(Контрагент)<>Тип("СправочникСсылка.Контрагенты") Тогда
				Контрагент = Справочники.Контрагенты.ПустаяСсылка();
				Рез=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.РезервированиеПодВнутреннийЗаказ Тогда
			Если ТипЗнч(Контрагент)<>Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				Контрагент = Справочники.ПодразделенияКомпании.ПустаяСсылка();
				Рез=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "РезервированиеЗаказовПокупателя"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРезервированиеЗаказовПокупателя(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РезервированиеЗаказовПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
			
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка			= ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("ЗаказПокупателя", дкПолучитьПредставление(СтрокаТабличнойЧасти.ЗаказПокупателя));
		СтруктураСтроки.Вставить("МестоРазмещения", СтрокаТабличнойЧасти.МестоРазмещения);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"Исполнитель");
	Исполнитель.ИсполнительПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.Исполнитель),"","/ "+ Исполнитель.ИсполнительПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик    = дкОтветственноеЛицо(ЭтотОбъект,"Заказчик");
	Заказчик.ЗаказчикПредставление = ?(обЗначениеНеЗаполнено(Заказчик.Заказчик),"","/ "+ Заказчик.ЗаказчикПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРезервированиеЗаказовПокупателя()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			
			Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				Контрагент = Основание.Контрагент;
				ХозОперация = Справочники.ХозОперации.РезервированиеПодЗаказПокупателя;
				ВидДокумента = "ЗаказПокупателя";
			ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") Тогда	
				Контрагент = Основание.ПодразделениеПолучатель;
				ХозОперация = Справочники.ХозОперации.РезервированиеПодВнутреннийЗаказ;
				ВидДокумента = "ЗаказВнутренний";
			КонецЕсли; 
			//А теперь откорректируем ТЧ на предмет того, что в ней должны остаться только текущие
			//заказы с учетом резервов и распределения
			//еще в ТЧ может быть указано где должен быть зарезервирован товар!
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗаказыПокупателейОстатки.Заказ КАК ЗаказПокупателя,
			|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
			|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ЗаказПокупателяТовары.Поставщик ССЫЛКА Справочник.СкладыКомпании
			|			ТОГДА ЗаказПокупателяТовары.Поставщик
			|		ИНАЧЕ &МестоРазмещения
			|	КОНЕЦ КАК МестоРазмещения,
			|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) КАК Количество
			|ИЗ
			|	РегистрНакопления.ЗаказыПокупателей.Остатки(&НаМомент, Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(&НаМомент, ЗаказПокупателя = &Заказ) КАК ЗаказыРаспределениеОстатки
			|		ПО ЗаказыПокупателейОстатки.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
			|			И ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ."+ВидДокумента+".Товары КАК ЗаказПокупателяТовары
			|		ПО ЗаказыПокупателейОстатки.Заказ = ЗаказПокупателяТовары.Ссылка
			|			И ЗаказыПокупателейОстатки.Номенклатура = ЗаказПокупателяТовары.Номенклатура
			|			И ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказПокупателяТовары.ХарактеристикаНоменклатуры
			|ГДЕ
			|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) > 0
			|	И (ЗаказПокупателяТовары.Поставщик ССЫЛКА Справочник.СкладыКомпании
			|			ИЛИ ЗаказПокупателяТовары.Поставщик = НЕОПРЕДЕЛЕНО)";
			Запрос.УстановитьПараметр("НаМомент",        ТекущаяДата());
			Запрос.УстановитьПараметр("Заказ",           Основание);
			Запрос.УстановитьПараметр("МестоРазмещения", Основание.СкладКомпании);
			Товары.Загрузить(Запрос.Выполнить().Выгрузить());
			Для Каждого СтрокаТоваров Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров);
				Если (НЕ СтрокаТоваров.Коэффициент = 0) И (НЕ СтрокаТоваров.Коэффициент = 1) Тогда
					СтрокаТоваров.Количество = Окр(СтрокаТоваров.Количество/СтрокаТоваров.Коэффициент, 3);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим корректность заполнения табличной части
	Для Каждого СтрокаТоваров Из Товары Цикл
		Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
			Если ТипЗнч(СтрокаТоваров.ЗаказПокупателя)<>Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Вид заказа не соответствует контрагенту.");
				Отказ=Истина;
			Иначе
				Если Контрагент<>СтрокаТоваров.ЗаказПокупателя.Контрагент Тогда
					Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Контрагент заказа не соответствует контрагенту документа.");
					Отказ=Истина;
				КонецЕсли; 
			КонецЕсли; 
		ИначеЕсли ТипЗнч(Контрагент)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
			Если ТипЗнч(СтрокаТоваров.ЗаказПокупателя)<>Тип("ДокументСсылка.ЗаказВнутренний") Тогда
				Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Вид заказа не соответствует контрагенту.");
				Отказ=Истина;
			Иначе
				Если Контрагент<>СтрокаТоваров.ЗаказПокупателя.ПодразделениеПолучатель Тогда
					Сообщить("Строка "+СокрЛП(СтрокаТоваров.НомерСтроки)+": Контрагент заказа не соответствует контрагенту документа.");
					Отказ=Истина;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла; 
	
	дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПоставщика
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	//Формирование запроса по табличной части товаров
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокументТовары.ЗаказПокупателя КАК Заказ,
	|	ДокументТовары.МестоРазмещения КАК СкладКомпании,
	|	0 КАК Количество,
	|	ДокументТовары.Количество*ДокументТовары.Коэффициент КАК Резерв
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка=&Ссылка И ДокументТовары.Количество>0
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	// Корректируем заказ покупателя
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
	НаборЗаписейЗаказыПокупателей.Контрагент = Контрагент;
	НаборЗаписейЗаказыПокупателей.Заказ = Неопределено;
	НаборЗаписейЗаказыПокупателей.СкладКомпании = Неопределено;
	НаборЗаписейЗаказыПокупателей.Заказывать = Ложь;
	НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
	
	//Снимаем распределение заказов покупателя
	РезультатЗакрытияЗаказов=НаборЗаписейЗаказыПокупателей.Выгрузить();
	РезультатЗакрытияЗаказов.Свернуть("Заказ,Контрагент,Номенклатура,ХарактеристикаНоменклатуры","Резерв");
	Запрос=Новый Запрос;
	//Запрос.МенеджерВременныхТаблиц=Новый МенеджерВременныхТаблиц();
	Запрос.Текст="
	|ВЫБРАТЬ
	|	РезультатЗакрытияЗаказов.Контрагент КАК Контрагент,
	|	РезультатЗакрытияЗаказов.Заказ КАК Заказ,
	|	РезультатЗакрытияЗаказов.Номенклатура КАК Номенклатура,
	|	РезультатЗакрытияЗаказов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РезультатЗакрытияЗаказов.Резерв
	|ПОМЕСТИТЬ
	|	РезультатЗакрытияЗаказов
	|ИЗ
	|	&РезультатЗакрытияЗаказов КАК РезультатЗакрытияЗаказов
	|;
	|
	|ВЫБРАТЬ
	|	РезультатЗакрытияЗаказов.Контрагент КАК Контрагент,
	|	РезультатЗакрытияЗаказов.Заказ КАК Заказ,
	|	РезультатЗакрытияЗаказов.Номенклатура КАК Номенклатура,
	|	РезультатЗакрытияЗаказов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	((ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток,0) -
	|			ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток,0)) -
	|		РезультатЗакрытияЗаказов.Резерв) -
	|	ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток,0) КАК Количество
	|ИЗ
	|	РезультатЗакрытияЗаказов КАК РезультатЗакрытияЗаказов
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(&НаМомент,
	|		Контрагент В (&Контрагент) И 
	|		Заказ В (&Заказ) И 
	|		Номенклатура В (&Номенклатура) И 
	|		ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	|	) КАК ЗаказыПокупателейОстатки
	|ПО
	|	РезультатЗакрытияЗаказов.Контрагент                 = ЗаказыПокупателейОстатки.Контрагент И
	|	РезультатЗакрытияЗаказов.Заказ                      = ЗаказыПокупателейОстатки.Заказ И
	|	РезультатЗакрытияЗаказов.Номенклатура               = ЗаказыПокупателейОстатки.Номенклатура И
	|	РезультатЗакрытияЗаказов.ХарактеристикаНоменклатуры = ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыРаспределение.Остатки(&НаМомент,
	|		ЗаказПокупателя В (&Заказ) И
	|		Номенклатура В (&Номенклатура) И
	|		ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	|	) КАК ЗаказыРаспределениеОстатки
	|ПО
	|	РезультатЗакрытияЗаказов.Заказ=ЗаказыРаспределениеОстатки.ЗаказПокупателя И
	|	РезультатЗакрытияЗаказов.Номенклатура=ЗаказыРаспределениеОстатки.Номенклатура И
	|	РезультатЗакрытияЗаказов.ХарактеристикаНоменклатуры=ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	|";
	
	Запрос.УстановитьПараметр("НаМомент",МоментВремени());
	Запрос.УстановитьПараметр("Контрагент",РезультатЗакрытияЗаказов.ВыгрузитьКолонку("Контрагент"));
	Запрос.УстановитьПараметр("Заказ",РезультатЗакрытияЗаказов.ВыгрузитьКолонку("Заказ"));
	Запрос.УстановитьПараметр("Номенклатура",РезультатЗакрытияЗаказов.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",РезультатЗакрытияЗаказов.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.УстановитьПараметр("РезультатЗакрытияЗаказов",РезультатЗакрытияЗаказов);
	
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = Запрос.Выполнить().Выгрузить();
	НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = "Заказ";
	НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
	НаборЗаписейРаспределениеЗаказов.Контрагент = Контрагент;
	НаборЗаписейРаспределениеЗаказов.ПоБазовомуКоличеству=Истина;
	НаборЗаписейРаспределениеЗаказов.ПорядокЗакрытияЗаказовПоставщику="ЗаказыРаспределениеОстатки.ЗаказПокупателя.Дата ВОЗР";
	Отказ=НЕ НаборЗаписейРаспределениеЗаказов.КорректировкаРаспределения() ИЛИ Отказ;
	
	// Корректировка резервирования товаров на складе
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам;
	НаборЗаписейОстатки.СкладКомпании=Неопределено;
	НаборЗаписейОстатки.ДвиженияПоРознице=Ложь;
	Отказ = НЕ НаборЗаписейОстатки.Зарезервировать() ИЛИ Отказ;
	
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыПокупателей.Количество()>0 ИЛИ Движения.ЗаказыРаспределение.Количество()>0));
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для Каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
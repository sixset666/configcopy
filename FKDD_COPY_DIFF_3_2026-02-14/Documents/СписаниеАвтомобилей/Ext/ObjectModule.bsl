// Модуль документа СписаниеАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ВалютаУпр;                    // Валюта управленческого учета
Перем ВедетсяБалансПоПодразделению Экспорт;		//Признак ведения баланса по подразделениям

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка наличия резервов на автомобиль
//
// Параметры
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль()
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("МетаданныеИмя",ЭтотОбъект.Метаданные().Имя);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
		ОбязательныеАвтомобили.Вставить("ДокументПередачи",3);
	КонецЕсли;

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СтатьяСписанияТМЦ",1);
	Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
    Иначе
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
		ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
		Если НЕ ВедетсяБалансПоПодразделению Тогда
			ОбязательныеРеквизиты.Вставить("ПодразделениеЗатрат",1);
		КонецЕсли; 
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Иначе
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыАвтомобили = Новый Структура();
				КонтролируемыеРеквизитыАвтомобили.Вставить("ДокументПередачи");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Автомобили",КонтролируемыеРеквизитыАвтомобили);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И Результат;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СписаниеАвтомобилей","Списание автомобилей",Истина);
	СписокХозОпераций.Добавить("СписаниеАвтомобилейОтданныхНаКомиссию","Списание автомобилей отданных на комиссию");
	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СкладКомпании.Подразделение КАК ПодразделениеСклада,	
	|	Док.ПодразделениеЗатрат			КАК ПодразделениеЗатрат,	
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДоговорВзаиморасчетов.Подразделение КАК ПодразделениеДоговора, 
	|	Док.СтатьяСписанияТМЦ КАК СтатьяСписанияТМЦ
	|
	|ИЗ
	|	Документ.СписаниеАвтомобилей КАК Док
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Режим - режим проведения (оперативный/неоперативный)
// ДокументСсылка - ссылка на документ который надо допровести по партиям
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
		
	// определим способ ведения баланса
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилей Тогда
		//Спишем оборудование автомобиля
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	СписаниеАвтомобилейАвтомобили.Автомобиль
		|ИЗ
		|	Документ.СписаниеАвтомобилей.Автомобили КАК СписаниеАвтомобилейАвтомобили
		|ГДЕ
		|	СписаниеАвтомобилейАвтомобили.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		ВыборкаАвтомобилей=Запрос.Выполнить().Выбрать();
		НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
		Пока ВыборкаАвтомобилей.Следующий() Цикл
			//Спишем оборудование по документу
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ВыборкаАвтомобилей.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
			//Спишем опции, установленное производителем
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ВыборкаАвтомобилей.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
		КонецЦикла;
		Если НЕ Отказ Тогда
			НаборЗаписейКомплектацияАвтомобилей.Записать();
		КонецЕсли; 
		Если НЕ НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
			// Зачитываем принятые на реализацию товары, которые были списаны
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
					|	КомплектацияАвтомобилей.Партия,
					|	КомплектацияАвтомобилей.Автомобиль,
					|	КомплектацияАвтомобилей.Номенклатура,
					|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
					|	КомплектацияАвтомобилей.ГТД,
					|	СУММА(КомплектацияАвтомобилей.Количество) КАК Количество,
					|	СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр,
					|	СУММА(КомплектацияАвтомобилей.СуммаПродажиУпр) КАК СуммаПродажиУпр
					|ИЗ
					|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
					|ГДЕ
					|	КомплектацияАвтомобилей.Регистратор = &Регистратор И 
					|	КомплектацияАвтомобилей.СтатусПартии = &СтатусПартииТоварПринятыйКомиссия И 
					|	КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура
					|СГРУППИРОВАТЬ ПО
					|	КомплектацияАвтомобилей.Партия,
					|	КомплектацияАвтомобилей.Автомобиль,
					|	КомплектацияАвтомобилей.Номенклатура,
					|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
					|	КомплектацияАвтомобилей.ГТД
					|";
			Запрос.УстановитьПараметр("Регистратор",                       ШапкаДокумента.Ссылка);
			Запрос.УстановитьПараметр("СтатусПартииТоварПринятыйКомиссия", Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
			ВыборкаКомплектацияАвтомобилей  = Запрос.Выполнить().Выбрать();
			НаборЗаписейРеализованныеТовары = Движения.РеализованныеТовары;
			ОпцияПроизводителя   = Справочники.Номенклатура.ОпцияПроизводителя;
			ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			Пока ВыборкаКомплектацияАвтомобилей.Следующий() Цикл
				НоваяЗапись = НаборЗаписейРеализованныеТовары.Добавить();
				НоваяЗапись.ВидДвижения          = ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период               = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор          = ШапкаДокумента.Ссылка;
				НоваяЗапись.ДокументПередачи     = ВыборкаКомплектацияАвтомобилей.Партия;
				НоваяЗапись.Автомобиль           = ВыборкаКомплектацияАвтомобилей.Автомобиль;
				Если ТипЗнч(ВыборкаКомплектацияАвтомобилей.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
					НоваяЗапись.Номенклатура               = ВыборкаКомплектацияАвтомобилей.Номенклатура;
					НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаКомплектацияАвтомобилей.ХарактеристикаНоменклатуры;
				Иначе
					НоваяЗапись.Номенклатура               = ОпцияПроизводителя;
					НоваяЗапись.ХарактеристикаНоменклатуры = ПустаяХарактеристика;
				КонецЕсли; 
				НоваяЗапись.Контрагент            = ВыборкаКомплектацияАвтомобилей.Партия.Контрагент;
				НоваяЗапись.ДоговорВзаиморасчетов = ВыборкаКомплектацияАвтомобилей.Партия.ДоговорВзаиморасчетов;
				НоваяЗапись.ГТД                   = ВыборкаКомплектацияАвтомобилей.ГТД;
				НоваяЗапись.ХозОперация           = ШапкаДокумента.ХозОперация;
				НоваяЗапись.Количество            = ВыборкаКомплектацияАвтомобилей.Количество;
				НоваяЗапись.СуммаУпр              = ВыборкаКомплектацияАвтомобилей.СуммаУпр;
				НоваяЗапись.СуммаПродажи          = ВыборкаКомплектацияАвтомобилей.СуммаПродажиУпр;
			КонецЦикла; 
			НаборЗаписейРеализованныеТовары.Записать();
		КонецЕсли;
		// Доходы и расходы
		СуммаРасходов=0;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор
		|	И КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СуммаРасходов=СуммаРасходов+Выборка.СуммаУпр;
		КонецЕсли; 
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		|ГДЕ
		|	ОстаткиАвтомобилей.Регистратор = &Регистратор
		|	И ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СуммаРасходов=СуммаРасходов+Выборка.СуммаУпр;
		КонецЕсли; 
		Если СуммаРасходов<>0 Тогда
			// Доходы и расходы
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение		= ШапкаДокумента.ПодразделениеСклада;
			Иначе 
				НаборЗаписейДиР.Подразделение		= ШапкаДокумента.ПодразделениеЗатрат;
			КонецЕсли;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			Иначе
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ШапкаДокумента.СтатьяСписанияТМЦ;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Расход					= СуммаРасходов;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		//Запрос=Новый Запрос;
		//Запрос.Текст="ВЫБРАТЬ
		//			|	ОбъединенныйЗапрос.Подразделение КАК Подразделение,
		//			|	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаАвтомобилейУпр), 0) КАК СуммаАвтомобилейУпр,
		//			|	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаОборудованияУпр), 0) КАК СуммаОборудованияУпр
		//			|ИЗ(
		//			|ВЫБРАТЬ
		//			|	РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
		//			|	ЕСТЬNULL(СУММА(РеализованныеАвтомобили.СуммаУпр), 0) КАК СуммаАвтомобилейУпр,
		//			|	0 КАК СуммаОборудованияУпр
		//			|ИЗ
		//			|	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
		//			|ГДЕ
		//			|	РеализованныеАвтомобили.Регистратор = &Регистратор
		//			|СГРУППИРОВАТЬ ПО
		//			|	РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение
		//			|
		//			|ОБЪЕДИНИТЬ ВСЕ
		//			|
		//			|ВЫБРАТЬ
		//			|	РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение,
		//			|	0,
		//			|	ЕСТЬNULL(СУММА(РеализованныеТовары.СуммаУпр),0)
		//			|ИЗ
		//			|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
		//			|ГДЕ
		//			|	РеализованныеТовары.Регистратор = &Регистратор
		//			|СГРУППИРОВАТЬ ПО
		//			|	РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение
		//			|) КАК ОбъединенныйЗапрос
		//			|СГРУППИРОВАТЬ ПО
		//			|	ОбъединенныйЗапрос.Подразделение";
		//Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		//// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		//Если ВедетсяБалансПоПодразделению Тогда
		//	Выборка=Запрос.Выполнить().Выбрать();
		//	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		//	Пока Выборка.Следующий() Цикл
		//		СуммаАвтомобилейУпр  = Выборка.СуммаАвтомобилейУпр;
		//		СуммаОборудованияУпр = Выборка.СуммаОборудованияУпр;
		//		Если СуммаАвтомобилейУпр <> 0 Тогда
		//			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		//			НаборЗаписейДоходыИРасходы.Подразделение          = Выборка.Подразделение;
		//			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
		//			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		//			НаборЗаписейДоходыИРасходы.Доход                  = СуммаАвтомобилейУпр;
		//			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		//			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		//		КонецЕсли;
		//		Если СуммаОборудованияУпр <> 0 Тогда
		//			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		//			НаборЗаписейДоходыИРасходы.Подразделение          = Выборка.Подразделение;
		//			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		//			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		//			НаборЗаписейДоходыИРасходы.Доход                  = СуммаОборудованияУпр;
		//			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		//			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		//		КонецЕсли;
		//	КонецЦикла;
		//Иначе
		//	ТаблицаСебестоимости = Запрос.Выполнить().Выгрузить();
		//	СуммаАвтомобилейУпр  = ТаблицаСебестоимости.Итог("СуммаАвтомобилейУпр");
		//	СуммаОборудованияУпр = ТаблицаСебестоимости.Итог("СуммаОборудованияУпр");
		//	Если СуммаАвтомобилейУпр <> 0 Тогда
		//		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		//		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		//		НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ПодразделениеЗатрат;
		//		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
		//		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		//		НаборЗаписейДоходыИРасходы.Доход                  = СуммаАвтомобилейУпр;
		//		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		//		Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		//	КонецЕсли;
		//	Если СуммаОборудованияУпр <> 0 Тогда
		//		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		//		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		//		НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ПодразделениеЗатрат;
		//		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		//		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		//		НаборЗаписейДоходыИРасходы.Доход                  = СуммаОборудованияУпр;
		//		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		//		Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		//	КонецЕсли;
		//КонецЕсли;
		
	ИначеЕсли ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
		Если НЕ НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
			// списываем оборудование, отданное на комиссию
			НаборЗаписейПартииОтданные = Движения.ПартииТоваровОтданные;
			НаборЗаписейПартииОтданные.ДокументОбъект        = ЭтотОбъект;
			НаборЗаписейПартииОтданные.Контрагент            = ШапкаДокумента.Контрагент;
			НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов = ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейПартииОтданные.ШапкаДокумента        = ШапкаДокумента;
			НаборЗаписейПартииОтданные.ЕстьАвтомобиль        = Истина;
			Запрос = Новый Запрос;
			Запрос.Текст = "
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	СписаниеАвтомобилей.ДокументПередачи,
					|	СписаниеАвтомобилей.Автомобиль
					|ПОМЕСТИТЬ
					|	ТаблицаАвтомобилей
					|ИЗ
					|	Документ.СписаниеАвтомобилей.Автомобили КАК СписаниеАвтомобилей
					|ГДЕ
					|	СписаниеАвтомобилей.Ссылка = &Ссылка
					|;
					|
					|ВЫБРАТЬ
					|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
					|	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
					|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|	КомплектацияАвтомобилей.Партия КАК Партия,
					|	КомплектацияАвтомобилей.ГТД КАК ГТД,
					|	КомплектацияАвтомобилей.Регистратор КАК ДокументПередачи,
					|	СУММА(ВЫБОР
					|		КОГДА КомплектацияАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			-КомплектацияАвтомобилей.Количество
					|		ИНАЧЕ
					|			КомплектацияАвтомобилей.Количество
					|	КОНЕЦ) КАК Количество,
					|	СУММА(ВЫБОР
					|		КОГДА КомплектацияАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			-КомплектацияАвтомобилей.СуммаНДС
					|		ИНАЧЕ
					|			КомплектацияАвтомобилей.СуммаНДС
					|	КОНЕЦ) КАК СуммаНДС,
					|	СУММА(ВЫБОР
					|		КОГДА КомплектацияАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			-КомплектацияАвтомобилей.СуммаУпр
					|		ИНАЧЕ
					|			КомплектацияАвтомобилей.СуммаУпр
					|	КОНЕЦ) КАК СуммаВсего
					|ИЗ
					|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
					|ГДЕ
					|	КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура И 
					|	(КомплектацияАвтомобилей.Автомобиль, КомплектацияАвтомобилей.Регистратор) В 
					|	(ВЫБРАТЬ
					|		ТаблицаАвтомобилей.Автомобиль,
					|		ТаблицаАвтомобилей.ДокументПередачи
					|	ИЗ
					|		ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
					|		
					|СГРУППИРОВАТЬ ПО
					|	КомплектацияАвтомобилей.Автомобиль,
					|	КомплектацияАвтомобилей.Номенклатура,
					|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
					|	КомплектацияАвтомобилей.Партия,
					|	КомплектацияАвтомобилей.ГТД,
					|	КомплектацияАвтомобилей.Регистратор";
			Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
			НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам = Запрос.Выполнить();
			НаборЗаписейПартииОтданные.РезультатЗапросаПоПартиям = НаборЗаписейПартииОтданные.ПолучитьТаблицуПартийОтданных(Ложь);
			Отказ = НЕ НаборЗаписейПартииОтданные.Расход() ИЛИ Отказ;
			// списываем опции производителя, отданное на комиссию
			НаборЗаписейПартииОтданные = Движения.ПартииТоваровОтданные;
			НаборЗаписейПартииОтданные.ДокументОбъект        = ЭтотОбъект;
			НаборЗаписейПартииОтданные.Контрагент            = ШапкаДокумента.Контрагент;
			НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов = ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейПартииОтданные.ШапкаДокумента        = ШапкаДокумента;
			НаборЗаписейПартииОтданные.ЕстьАвтомобиль        = Истина;
			Запрос = Новый Запрос;
			Запрос.Текст = "
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	СписаниеАвтомобилей.ДокументПередачи,
					|	СписаниеАвтомобилей.Автомобиль
					|ПОМЕСТИТЬ
					|	ТаблицаАвтомобилей
					|ИЗ
					|	Документ.СписаниеАвтомобилей.Автомобили КАК СписаниеАвтомобилей
					|ГДЕ
					|	СписаниеАвтомобилей.Ссылка = &Ссылка
					|;
					|
					|ВЫБРАТЬ
					|	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
					|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
					|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|	КомплектацияАвтомобилей.Партия КАК Партия,
					|	КомплектацияАвтомобилей.ГТД КАК ГТД,
					|	КомплектацияАвтомобилей.Регистратор КАК ДокументПередачи,
					|	СУММА(КомплектацияАвтомобилей.Количество) КАК Количество,
					|	СУММА(КомплектацияАвтомобилей.СуммаНДС) КАК СуммаНДС,
					|	СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаВсего
					|ИЗ
					|	РегистрНакопления.ПартииТоваровОтданные КАК КомплектацияАвтомобилей
					|ГДЕ
					|	КомплектацияАвтомобилей.Номенклатура = &ОпцияПроизводителя И 
					|	(КомплектацияАвтомобилей.Регистратор,КомплектацияАвтомобилей.Автомобиль) В 
					|	(ВЫБРАТЬ
					|		ТаблицаАвтомобилей.ДокументПередачи,
					|		ТаблицаАвтомобилей.Автомобиль
					|	ИЗ
					|		ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
					|		
					|СГРУППИРОВАТЬ ПО
					|	КомплектацияАвтомобилей.Номенклатура,
					|	КомплектацияАвтомобилей.Автомобиль,
					|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
					|	КомплектацияАвтомобилей.Партия,
					|	КомплектацияАвтомобилей.ГТД,
					|	КомплектацияАвтомобилей.Регистратор
					|";
			Запрос.УстановитьПараметр("Ссылка",             ШапкаДокумента.Ссылка);
			Запрос.УстановитьПараметр("ОпцияПроизводителя", Справочники.Номенклатура.ОпцияПроизводителя);
			НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам = Запрос.Выполнить();
			НаборЗаписейПартииОтданные.РезультатЗапросаПоПартиям = НаборЗаписейПартииОтданные.ПолучитьТаблицуПартийОтданных(Ложь);
			Отказ=НЕ НаборЗаписейПартииОтданные.Расход() ИЛИ Отказ;
			Если НЕ Отказ Тогда
				НаборЗаписейПартииОтданные.Записать();
			КонецЕсли; 
			// Доходы и расходы
			СуммаРасходов=0;
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
						 |	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаСебестоимостиУпр), 0) КАК СуммаСебестоимостиУпр
						 |ИЗ(
						 |ВЫБРАТЬ
						 |	ЕСТЬNULL(СУММА(АвтомобилиОтданные.СуммаСебестоимостиУпр), 0) КАК СуммаСебестоимостиУпр
						 |ИЗ
						 |	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
						 |ГДЕ
						 |	АвтомобилиОтданные.Регистратор = &Регистратор
						 |	И АвтомобилиОтданные.ВидДвижения = &ВидДвижения
						 |
						 |ОБЪЕДИНИТЬ ВСЕ
						 |
						 |ВЫБРАТЬ
						 |	ЕСТЬNULL(СУММА(ПартииТоваровОтданные.СуммаСебестоимостиУпр),0)
						 |ИЗ
						 |	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
						 |ГДЕ
						 |	ПартииТоваровОтданные.Регистратор = &Регистратор
						 |	И ПартииТоваровОтданные.ВидДвижения = &ВидДвижения
						 |) КАК ОбъединенныйЗапрос
						 |";
			Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
			Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Расход);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СуммаРасходов=СуммаРасходов+Выборка.СуммаСебестоимостиУпр;
			КонецЕсли;
			Если СуммаРасходов<>0 Тогда
				НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
				// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
				Если ВедетсяБалансПоПодразделению Тогда
					НаборЗаписейДиР.Подразделение		= ШапкаДокумента.ПодразделениеДоговора;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяСписанияТМЦ;
				НаборЗаписейДиР.ВУпрВалюте				= Истина;
				НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
				НаборЗаписейДиР.Расход					= СуммаРасходов;
				Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по автомобилям
// Режим - число, 0-возвращает излишки, 1-недостачи
Функция ПолучитьРезультатЗапросаПоАвтомобилям()
    ТекстЗапроса="
	|ВЫБРАТЬ
	|	СписаниеАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	|	СписаниеАвтомобилейАвтомобили.Количество КАК Количество
	|ИЗ
	|	Документ.СписаниеАвтомобилей.Автомобили КАК СписаниеАвтомобилейАвтомобили
    |ГДЕ
	|	СписаниеАвтомобилейАвтомобили.Ссылка=&Ссылка
 	|";

	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст=ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	СуммаВсего=0;
	Если Проведен Тогда
		Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
			КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр        = КурсВалютыУпр;
		КонецЕсли;
		Запрос=Новый Запрос;
		Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилей Тогда
			Запрос.Текст="ВЫБРАТЬ
			             |	ОбъединенныйЗапрос.Автомобиль,
			             |	СУММА(ОбъединенныйЗапрос.Сумма) КАК Сумма
			             |ИЗ (
			             |ВЫБРАТЬ
			             |	ОстаткиАвтомобилей.Автомобиль,
			             |	ОстаткиАвтомобилей.СуммаУпр КАК Сумма
			             |ИЗ
			             |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			             |ГДЕ
			             |	ОстаткиАвтомобилей.Регистратор = &Регистратор
			             |	И ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРасход
			             |
			             |ОБЪЕДИНИТЬ ВСЕ
			             |
			             |ВЫБРАТЬ
			             |	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
			             |	КомплектацияАвтомобилей.СуммаУпр КАК Сумма
			             |ИЗ
			             |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			             |ГДЕ
			             |	КомплектацияАвтомобилей.Регистратор = &Регистратор
			             |	И КомплектацияАвтомобилей.ВидДвижения = &ВидДвиженияРасход
			             |
			             |) КАК ОбъединенныйЗапрос
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ОбъединенныйЗапрос.Автомобиль";
		ИначеЕсли ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
			Запрос.Текст="ВЫБРАТЬ
			             |	ОбъединенныйЗапрос.Автомобиль,
			             |	СУММА(ОбъединенныйЗапрос.Сумма) КАК Сумма
			             |ИЗ (
			             |ВЫБРАТЬ
			             |	АвтомобилиОтданные.Автомобиль,
			             |	АвтомобилиОтданные.СуммаУпр КАК Сумма
			             |ИЗ
			             |	РегистрНакопления.АвтомобилиОтданные КАК АвтомобилиОтданные
			             |ГДЕ
			             |	АвтомобилиОтданные.Регистратор = &Регистратор
			             |	И АвтомобилиОтданные.ВидДвижения = &ВидДвиженияРасход
			             |
			             |ОБЪЕДИНИТЬ ВСЕ
			             |
			             |ВЫБРАТЬ
			             |	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
			             |	КомплектацияАвтомобилей.СуммаУпр КАК Сумма
			             |ИЗ
			             |	РегистрНакопления.ПартииТоваровОтданные КАК КомплектацияАвтомобилей
			             |ГДЕ
			             |	КомплектацияАвтомобилей.Регистратор = &Регистратор
			             |	И КомплектацияАвтомобилей.ВидДвижения = &ВидДвиженияРасход
			             |
			             |) КАК ОбъединенныйЗапрос
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ОбъединенныйЗапрос.Автомобиль";
			Запрос.УстановитьПараметр("ДокументПередачи",Автомобили.ВыгрузитьКолонку("ДокументПередачи"));
		КонецЕсли; 
		Запрос.УстановитьПараметр("Регистратор",Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
		тКэшСуммСписания=Запрос.Выполнить().Выгрузить();
		Если ВалютаДокумента<>ВалютаУпр Тогда
			Для каждого СтрокаСписания Из тКэшСуммСписания Цикл
				СтрокаСписания.Сумма=обПересчет(СтрокаСписания.Сумма,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
			КонецЦикла; 
		КонецЕсли; 
		СуммаВсего=тКэшСуммСписания.Итог("Сумма");
	КонецЕсли;
	Возврат СуммаВсего;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			// проверим вид договора
			Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
				Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомиссионером И
					 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
					 #Если Клиент Тогда
					 Предупреждение("Необходимо выбрать договор комиссии !",10);
					 #КонецЕсли
					 ДоговорВзаиморасчетов=Неопределено;
					 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
					 Возврат Ложь;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании = Неопределено;
			КонецЕсли; 
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Количество" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.ДокументПередачи" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="ПолучитьРасчетноеКоличество" Тогда
		////подставляет расчетное количество в текущую строку
		//Если ХозОперация<>Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
		//	СтруктураОтбора=Новый Структура("СкладКомпании,Автомобиль");
		//	СтруктураОтбора.СкладКомпании=СкладКомпании;
		//	СтруктураОтбора.Автомобиль=ТекСтрока.Автомобиль;
		//	МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
		//	ТаблицаОстатков=РегистрыНакопления.ОстаткиАвтомобилей.Остатки(МоментВремени,СтруктураОтбора,,"Количество");
		//	Если ТаблицаОстатков.Количество()>0 Тогда
		//		ТекСтрока.Количество=ТаблицаОстатков.Итог("Количество");
		//	Иначе
		//		ТекСтрока.Количество=0;
		//	КонецЕсли;
		//Иначе
		//	СтруктураОтбора=Новый Структура("Контрагент,ДоговорВзаиморасчетов,Автомобиль");
		//	СтруктураОтбора.Контрагент=Контрагент;
		//	СтруктураОтбора.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		//	СтруктураОтбора.Автомобиль=ТекСтрока.Автомобиль;
		//	МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
		//	ТаблицаОстатков=РегистрыНакопления.АвтомобилиОтданные.Остатки(МоментВремени,СтруктураОтбора,,"Количество");
		//	Если ТаблицаОстатков.Количество()>0 Тогда
		//		ТекСтрока.Количество=ТаблицаОстатков.Итог("Количество");
		//	Иначе
		//		ТекСтрока.Количество=0;
		//	КонецЕсли;
		//КонецЕсли;
		ТекСтрока.Количество=1;
		Возврат ОбработкаРеквизита("Автомобили.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ИнвентаризацияАвтомобилей - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаТовараПоИнвентаризации) КАК КодОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ОстаткиАвтомобилей.Автомобиль КАК Номенклатура,
	|	ОстаткиАвтомобилей.ГТД КАК РНПТ,
	|	ОстаткиАвтомобилей.Количество КАК КоличествоПрослеживаемости,
	|	ОстаткиАвтомобилей.Сумма - ОстаткиАвтомобилей.СуммаНДС КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &Ссылка
	|	И ОстаткиАвтомобилей.ГТД.РНПТ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим есть РНПТ у документа
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = РезультатЗапроса.Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
	
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = Дата;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "СписаниеАвтомобилей"
// Возвращает сформированный табличный документ:
Функция ПечатьСписаниеАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("СписаниеАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
			
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект); 
	
	//bz++
	ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(Организация);
	//ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.СкладКомпанииПредставление	= спПолучитьНаименование(СкладКомпании);
	//bz--	  
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы,,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил    = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ16"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ16(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Макет = ПолучитьОбщийМакет("ТОРГ16");

	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Выводим общие реквизиты шапки
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО		= ЭтотОбъект.Организация.КодПоОКПО;
	
	// Заполним информацию о руководителе организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);

	НомерСтраницы   = 1;
	
	Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации.СписаниеАвтомобилей Тогда
		ВыборкаСтрокТовары = РегистрыНакопления.ОстаткиАвтомобилей.СоздатьНаборЗаписей();
		ПростоеСписание = Истина;
	ИначеЕсли ЭтотОбъект.ХозОперация = Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
		ВыборкаСтрокТовары = РегистрыНакопления.АвтомобилиОтданные.СоздатьНаборЗаписей();
		ПростоеСписание = Ложь;
	Иначе
		Сообщить("Неправильный вид хозяйственной операции.");
		Возврат Неопределено;
	КонецЕсли;
	ВыборкаСтрокТовары.Отбор.Регистратор.Значение = ЭтотОбъект.Ссылка;
	ВыборкаСтрокТовары.Прочитать();

	//определимся откуда будем брать многострочную часть документа
	//если движения есть, то из регистра иначе из табличной части документа
	ЕстьДвиженияПоРегистру = Ложь;
	Если ВыборкаСтрокТовары.Количество() > 0 Тогда
		ЕстьДвиженияПоРегистру = Истина;
	Иначе
		Сообщить("Документ не проведен по партионному регистру. Отображение партий невозможно.");
		ВыборкаСтрокТовары = ЭтотОбъект.Автомобили;
	КонецЕсли; 
	КоличествоСтрок  = ВыборкаСтрокТовары.Количество();

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыПервогоЛиста");
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаПервогоЛиста");

	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		// Проверим, помещаются ли нужные области на странице
		МассивОбластей = Новый Массив();			
		МассивОбластей.Добавить(ОбластьМакета);
		
		Попытка
			Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
				НомерСтраницы            = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ОбластьМакета.Параметры.ДатаСписанияТовара = ЭтотОбъект.Дата;

		Если ЕстьДвиженияПоРегистру Тогда
			//тут вопрос о разнице ДатаПоступленияТовара и ТоварнаяНакладнаяДата KOZT
			Если ПростоеСписание Тогда
				Партия = СтрокаТовар.Партия;
			Иначе
				Партия = СтрокаТовар.ДокументПередачи;
			КонецЕсли;
			ОбластьМакета.Параметры.ДатаПоступленияТовара = Партия.Дата;
			ОбластьМакета.Параметры.ТоварнаяНакладнаяНомер =Партия.Номер;
			ОбластьМакета.Параметры.ТоварнаяНакладнаяДата = Партия.Дата;
		КонецЕсли; 
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла; 
	
	НомерСтраницы   = НомерСтраницы + 1;
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыВторогоЛиста");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ТабДокумент.Вывести(ЗаголовокТаблицы);
	
	СуммаПечатногоДокумента=0;
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаВторогоЛиста");
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьМакетаИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	СуммаПоСтранице = 0;
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		// Проверим, помещаются ли нужные области на странице
		Если СтрокаТовар.НомерСтроки = КоличествоСтрок Тогда
			// только на последнем шаге, когда нужно вывести все
			МассивОбластей = Новый Массив();
			МассивОбластей.Добавить(ОбластьМакета);
			МассивОбластей.Добавить(ОбластьМакетаИтогоПоСтранице);
			МассивОбластей.Добавить(ОбластьМакетаИтого);
			МассивОбластей.Добавить(ОбластьМакетаПодвал);			
		Иначе
			МассивОбластей = Новый Массив();			
			МассивОбластей.Добавить(ОбластьМакета);
			МассивОбластей.Добавить(ОбластьМакетаИтогоПоСтранице);
		КонецЕсли; 			
		
		Попытка
			Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
				НомерСтраницы            = НомерСтраницы + 1;
				ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоПоСтранице = Формат(СуммаПоСтранице,ФорматВыводаСуммы);
				СуммаПоСтранице = 0;
				ТабДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ОбластьМакета.Параметры.Номенклатура = СтрокаТовар.Автомобиль;
		ОбластьМакета.Параметры.ТоварНаименование = спПолучитьНаименование(СтрокаТовар.Автомобиль);
		ОбластьМакета.Параметры.ТоварКод = СтрокаТовар.Автомобиль.VIN;
		ОбластьМакета.Параметры.КоличествоМест = СтрокаТовар.Количество;
		ОбластьМакета.Параметры.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;	
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = 796;
	
		Если ЕстьДвиженияПоРегистру Тогда
			ОбластьМакета.Параметры.Сумма = Формат(СтрокаТовар.Сумма,ФорматВыводаСуммы);
			СуммаПоСтранице = СтрокаТовар.Сумма + СуммаПоСтранице;						
			СуммаПечатногоДокумента = СуммаПечатногоДокумента + СтрокаТовар.Сумма;
			ОбластьМакета.Параметры.Цена = Формат(СтрокаТовар.Сумма / СтрокаТовар.Количество,ФорматВыводаСуммы);
		Иначе
		КонецЕсли; 
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	Если НомерСтраницы > 2 Тогда
		// Выводим итоги по cтранице
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоПоСтранице = Формат(СуммаПоСтранице,ФорматВыводаСуммы);
		СуммаПоСтранице = 0;
		ТабДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);
	КонецЕсли;

	Сумма = СуммаПечатногоДокумента;
	// Выводим итоги по документу в общем
	ОбластьМакета = ОбластьМакетаИтого;
	ОбластьМакета.Параметры.Сумма = Формат(Сумма, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим подвал документа
	ОбластьМакета = ОбластьМакетаПодвал;
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.СуммаСписанияПрописью = обЧислоПрописью(Сумма,ЭтотОбъект.ВалютаДокумента);
	
	// Комиссия
	ПредседательКомиссии= дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");	
	ЧленКомиссии1		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ЧленКомиссии2		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	МОЛ					= дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию ИЛИ
		 ВедетсяБалансПоПодразделению Тогда
		 ПодразделениеЗатрат=Справочники.ПодразделенияКомпании.ПустаяСсылка();
	Иначе
		 ПодразделениеЗатрат=ПодразделениеКомпании;
	КонецЕсли; 
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если (ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию) И 
		 (НЕ обЗначениеНеЗаполнено(СкладКомпании)) Тогда
		 СкладКомпании=Неопределено;
	КонецЕсли; 
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 	
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	РезультатЗапросаПоАвтомобилям=ПолучитьРезультатЗапросаПоАвтомобилям();
	
	Если ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилей Тогда
		
		Отказ = ПроверитьЗаказыНаАвтомобиль() ИЛИ Отказ;
		
		// Спишем автомобили со склада
		НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
		НаборЗаписейОстаткиАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилям;
		НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
		//НаборЗаписейОстаткиАвтомобилей.Приходовать=Истина;
		//НаборЗаписейОстаткиАвтомобилей.Резервировать=Ложь;
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Новый Массив;
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии.Добавить(Перечисления.СтатусыПартий.ТоварКупленный);
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии.Добавить(Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		Отказ= НЕ НаборЗаписейОстаткиАвтомобилей.Расход() ИЛИ Отказ;
		
	ИначеЕсли ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию Тогда
		// списываем автомобили отданные на комиссию
		НаборЗаписейАвтомобилиОтданные=Движения.АвтомобилиОтданные;
		НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
		НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		НаборЗаписейАвтомобилиОтданные.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилям;
		Отказ=НЕ НаборЗаписейАвтомобилиОтданные.Расход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейАвтомобилиОтданные.Записать();
		КонецЕсли; 
		
	КонецЕсли;
	
	// проведем партии товаров
	Если НЕ Отказ Тогда
		Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	КонецЕсли; 
	
	Если ХозОперация = Справочники.ХозОперации.СписаниеАвтомобилей Тогда
		// Зачитываем принятые на реализацию автомобили, которые были списаны
		НаборЗаписейРеализованныеАвтомобили = Движения.РеализованныеАвтомобили;
		НаборЗаписейРеализованныеАвтомобили.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейРеализованныеАвтомобили.Списание       = Истина;
		НаборЗаписейРеализованныеАвтомобили.РезультатЗапросаПоАвтомобилям=РезультатЗапросаПоАвтомобилям;
		Отказ=НЕ НаборЗаписейРеализованныеАвтомобили.Приход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейОстаткиАвтомобилей.Записать();
			
			ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
			
			//доходы и расходы
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	ОбъединенныйЗапрос.Подразделение КАК Подразделение,
			|	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаАвтомобилейУпр), 0) КАК СуммаАвтомобилейУпр,
			|	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаОборудованияУпр), 0) КАК СуммаОборудованияУпр
			|ИЗ(
			|ВЫБРАТЬ
			|	РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
			|	ЕСТЬNULL(СУММА(РеализованныеАвтомобили.СуммаУпр), 0) КАК СуммаАвтомобилейУпр,
			|	0 КАК СуммаОборудованияУпр
			|ИЗ
			|	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
			|ГДЕ
			|	РеализованныеАвтомобили.Регистратор = &Регистратор
			|СГРУППИРОВАТЬ ПО
			|	РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение,
			|	0,
			|	ЕСТЬNULL(СУММА(РеализованныеТовары.СуммаУпр),0)
			|ИЗ
			|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
			|ГДЕ
			|	РеализованныеТовары.Регистратор = &Регистратор
			|СГРУППИРОВАТЬ ПО
			|	РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение
			|) КАК ОбъединенныйЗапрос
			|СГРУППИРОВАТЬ ПО
			|	ОбъединенныйЗапрос.Подразделение";
			Запрос.УстановитьПараметр("Регистратор", Ссылка);
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				Выборка = Запрос.Выполнить().Выбрать();
				НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
				Пока Выборка.Следующий() Цикл
					СуммаАвтомобилейУпр  = Выборка.СуммаАвтомобилейУпр;
					СуммаОборудованияУпр = Выборка.СуммаОборудованияУпр;
					Если СуммаАвтомобилейУпр <> 0 Тогда
						НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
						НаборЗаписейДоходыИРасходы.Подразделение          = Выборка.Подразделение;
						НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
						НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
						НаборЗаписейДоходыИРасходы.Расход                 = СуммаАвтомобилейУпр;
						НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
						Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
					КонецЕсли;
					Если СуммаОборудованияУпр <> 0 Тогда
						НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
						НаборЗаписейДоходыИРасходы.Подразделение          = Выборка.Подразделение;
						НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
						НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
						НаборЗаписейДоходыИРасходы.Расход                 = СуммаОборудованияУпр;
						НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
						Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ТаблицаСебестоимости = Запрос.Выполнить().Выгрузить();
				СуммаАвтомобилейУпр  = ТаблицаСебестоимости.Итог("СуммаАвтомобилейУпр");
				СуммаОборудованияУпр = ТаблицаСебестоимости.Итог("СуммаОборудованияУпр");
				Если СуммаАвтомобилейУпр <> 0 Тогда
					НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
					НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ПодразделениеЗатрат;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыИРасходы.Расход                 = СуммаАвтомобилейУпр;
					НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
				КонецЕсли;
				Если СуммаОборудованияУпр <> 0 Тогда
					НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
					НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ПодразделениеЗатрат;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыИРасходы.Расход                 = СуммаОборудованияУпр;
					НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
	КонецЕсли;
	
	// Проверим налиие прослеживаемых товаров, которые были реализованы
	Если НЕ Отказ Тогда
		Движения.ОстаткиАвтомобилей.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И ХозОперация=Справочники.ХозОперации.СписаниеАвтомобилей);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	ДвигаемГраницу = (ДвигаемГраницу И Движения.КомплектацияАвтомобилей.Количество()>0);
	// двигаем границу последовательности комплектаций автомобилей
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  	 = Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);

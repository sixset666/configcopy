// Модуль документа УведомлениеОВвозеПрослеживаемыхТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ВыполнитьДвиженияПоГТД(Отказ)
	
	СкладКомпании = ДокументОснование.СкладКомпании;
	
	// Получим партии и ГТД текущих номенклатур
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
		               |	УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
		               |ПОМЕСТИТЬ ПрослеживаемыйТовар
		               |ИЗ
		               |	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
		               |ГДЕ
		               |	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		               |	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	СУММА(ПартииТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
		               |ПОМЕСТИТЬ ТоварыПоПартии
		               |ИЗ
		               |	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		               |			&МоментВремени,
		               |			Номенклатура В
		               |					(ВЫБРАТЬ
		               |						ПрослеживаемыйТовар.Номенклатура КАК Номенклатура
		               |					ИЗ
		               |						ПрослеживаемыйТовар КАК ПрослеживаемыйТовар)
		               |				И Партия = &Партия
		               |				И СкладКомпании = &СкладКомпании) КАК ПартииТоваровКомпанииОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПартииТоваровКомпанииОстатки.Номенклатура,
		               |	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ГТДПартийТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		               |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	СУММА(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
		               |ПОМЕСТИТЬ ТоварыПоГТД
		               |ИЗ
		               |	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
		               |			&МоментВремени,
		               |			Номенклатура В
		               |					(ВЫБРАТЬ
		               |						ПрослеживаемыйТовар.Номенклатура КАК Номенклатура
		               |					ИЗ
		               |						ПрослеживаемыйТовар КАК ПрослеживаемыйТовар)
		               |				И Партия = &Партия
		               |				И СкладКомпании = &СкладКомпании) КАК ГТДПартийТоваровКомпанииОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
		               |	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПрослеживаемыйТовар.Номенклатура КАК Номенклатура,
		               |	ЕСТЬNULL(ТоварыПоПартии.ХарактеристикаНоменклатуры, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
		               |	ПрослеживаемыйТовар.КоличествоПрослеживаемости КАК Количество,
		               |	ЕСТЬNULL(ТоварыПоПартии.КоличествоОстаток, 0) КАК КоличествоПартии
		               |ПОМЕСТИТЬ ТоварыПартииДокумента
		               |ИЗ
		               |	ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыПоПартии КАК ТоварыПоПартии
		               |		ПО ПрослеживаемыйТовар.Номенклатура = ТоварыПоПартии.Номенклатура
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ ПрослеживаемыйТовар
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ ТоварыПоПартии
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТоварыПартииДокумента.Номенклатура КАК Номенклатура,
		               |	ТоварыПартииДокумента.Характеристика КАК Характеристика,
		               |	ТоварыПартииДокумента.Количество КАК Количество,
		               |	ТоварыПартииДокумента.КоличествоПартии КАК КоличествоПартии,
		               |	ЕСТЬNULL(ТоварыПоГТД.КоличествоОстаток, 0) КАК КоличествоГТД
		               |ИЗ
		               |	ТоварыПартииДокумента КАК ТоварыПартииДокумента
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыПоГТД КАК ТоварыПоГТД
		               |		ПО ТоварыПартииДокумента.Номенклатура = ТоварыПоГТД.Номенклатура
		               |			И ТоварыПартииДокумента.Характеристика = ТоварыПоГТД.ХарактеристикаНоменклатуры";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("Партия", ДокументОснование);
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			// Проверим, что количество товара по документу не выходит за количество по партии
			Если Выборка.Количество > (Выборка.КоличествоПартии - Выборка.КоличествоГТД) Тогда
				
				дкСообщитьРезультат("РНПТ прослеживаемого товара " + Выборка.Номенклатура + " была не полностью распределена. В документе указано количество товара больше, чем необходимо.",, ЭтотОбъект);
				Отказ = Истина;
				
			КонецЕсли;
			
			НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.Период = Дата;
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
			НоваяЗапись.СкладКомпании = СкладКомпании;
			НоваяЗапись.Партия = ДокументОснование;
			НоваяЗапись.ГТД = РНПТ;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
		               |	УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
		               |ПОМЕСТИТЬ ПрослеживаемыйТовар
		               |ИЗ
		               |	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
		               |ГДЕ
		               |	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
		               |	ОстаткиАвтомобилейОстатки.КоличествоОстаток КАК КоличествоОстаток,
		               |	ОстаткиАвтомобилейОстатки.СтатусПартии КАК СтатусПартии
		               |ПОМЕСТИТЬ ОстаткиАвтомобилей
		               |ИЗ
		               |	РегистрНакопления.ОстаткиАвтомобилей.Остатки(
		               |			&МоментВремени,
		               |			Автомобиль В
		               |					(ВЫБРАТЬ
		               |						ПрослеживаемыйТовар.Номенклатура КАК Номенклатура
		               |					ИЗ
		               |						ПрослеживаемыйТовар КАК ПрослеживаемыйТовар)
		               |				И ГТД = ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)
		               |				И Партия = &Партия
		               |				И СкладКомпании = &СкладКомпании) КАК ОстаткиАвтомобилейОстатки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПрослеживаемыйТовар.Номенклатура КАК Автомобиль,
		               |	ПрослеживаемыйТовар.КоличествоПрослеживаемости КАК Количество,
		               |	ЕСТЬNULL(ОстаткиАвтомобилей.КоличествоОстаток, 0) КАК КоличествоОстаток,
		               |	ОстаткиАвтомобилей.СтатусПартии КАК СтатусПартии
		               |ИЗ
		               |	ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		               |		ПО ПрослеживаемыйТовар.Номенклатура = ОстаткиАвтомобилей.Автомобиль";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("Партия", ДокументОснование);
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КоличествоОстаток = 0 Тогда
				дкСообщитьРезультат("Автомобиль " + Выборка.Автомобиль + " не найден на остатках! РНПТ не присвоен",, ЭтотОбъект);
				Продолжить;
			КонецЕсли;
			
			// Спишем автомобиль с текущей ГТД
			НоваяЗапись = Движения.ОстаткиАвтомобилей.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.Период = Дата;
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
			НоваяЗапись.СкладКомпании = СкладКомпании;
			НоваяЗапись.Партия = ДокументОснование;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			
			// Добавим заново автомобль с РНПТ
			НоваяЗапись = Движения.ОстаткиАвтомобилей.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.Период = Дата;
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
			НоваяЗапись.СкладКомпании = СкладКомпании;
			НоваяЗапись.Партия = ДокументОснование;
			НоваяЗапись.ГТД = РНПТ;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыПоОснованию() Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		
		#Если Клиент Тогда
			Сообщить("Не указан документ основание. Заполнение отменено.");
		#КонецЕсли
		
		Возврат;
		
	КонецЕсли;
	
	ВалютаОснования = ДокументОснование.ВалютаДокумента;
	КурсОснования = ДокументОснование.КурсДокумента;
	
	Запрос = Новый Запрос;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеАвтомобилей") Тогда
		
		ХозОперация = Справочники.ХозОперации.УведомлениеОВвозеПрослеживаемыхАвтомобилей;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеАвтомобилейАвтомобили.Автомобиль КАК Номенклатура,
		|	ПоступлениеАвтомобилейАвтомобили.Количество КАК Количество,
		|	1 КАК Коэффициент,
		|	ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка) КАК ЕдиницаИзмеренияПрослеживаемости,
		|	ПоступлениеАвтомобилейАвтомобили.СуммаВсего КАК СуммаВсего,
		|	ПоступлениеАвтомобилейАвтомобили.СуммаНДС КАК СуммаНДС,
		|	ПоступлениеАвтомобилейАвтомобили.Автомобиль.КодТНВЭД КАК КодТНВЭД,
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка) КАК СтранаПроисхождения,
		|	ПоступлениеАвтомобилейАвтомобили.Автомобиль.КодОКПД2 КАК КодОКПД2
		|ИЗ
		|	Документ.ПоступлениеАвтомобилей.Автомобили КАК ПоступлениеАвтомобилейАвтомобили
		|ГДЕ
		|	ПоступлениеАвтомобилейАвтомобили.Ссылка = &Ссылка
		|	И ПоступлениеАвтомобилейАвтомобили.Автомобиль.Прослеживаемый
		|ИТОГИ ПО
		|	КодТНВЭД";
		
	Иначе
		
		ХозОперация = Справочники.ХозОперации.УведомлениеОВвозеПрослеживаемыхТоваров;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество,
		|	ПоступлениеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	МАКСИМУМ(ПоступлениеТоваровТовары.Коэффициент) КАК Коэффициент,
		|	ПоступлениеТоваровТовары.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмеренияПрослеживаемости,
		|	ПоступлениеТоваровТовары.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ПоступлениеТоваровТовары.Номенклатура.КодТНВЭД КАК КодТНВЭД,
		|	СУММА(ПоступлениеТоваровТовары.СуммаВсего) КАК СуммаВсего,
		|	СУММА(ПоступлениеТоваровТовары.СуммаНДС) КАК СуммаНДС,
		|	ПоступлениеТоваровТовары.Номенклатура.КодОКПД2 КАК КодОКПД2
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|	И ПоступлениеТоваровТовары.Номенклатура.Прослеживаемый
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура,
		|	ПоступлениеТоваровТовары.ЕдиницаИзмерения,
		|	ПоступлениеТоваровТовары.Номенклатура.БазоваяЕдиницаИзмерения,
		|	ПоступлениеТоваровТовары.Номенклатура.СтранаПроисхождения,
		|	ПоступлениеТоваровТовары.Номенклатура.КодТНВЭД,
		|	ПоступлениеТоваровТовары.Номенклатура.КодОКПД2
		|ИТОГИ ПО
		|	КодТНВЭД";
		
	КонецЕсли;
	
	Товары.Очистить();
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		#Если Клиент Тогда
			Сообщить("В документе основании нет прослеживаемых товаров");
		#КонецЕсли
		
	Иначе
		
		// Заполним табличную часть
		ВыборкаПоТНВЭД = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ЗначениеЗаполнено(КодТНВЭД) Тогда
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("КодТНВЭД", КодТНВЭД);
			
			Если НЕ ВыборкаПоТНВЭД.НайтиСледующий(СтруктураПоиска) Тогда
				#Если Клиент Тогда
					Сообщить("В документе основании нет прослеживаемых товаров по указанному коду ТНВЭД");
				#КонецЕсли
				Возврат;
			КонецЕсли;
			
		Иначе
			ВыборкаПоТНВЭД.Следующий();
			КодТНВЭД = ВыборкаПоТНВЭД.КодТНВЭД;
		КонецЕсли;
		
		ВыборкаТоваров = ВыборкаПоТНВЭД.Выбрать();
		
		ПерваяСтрока = Истина;
		
		Пока ВыборкаТоваров.Следующий() Цикл
			
			Если ПерваяСтрока Тогда
				Если НЕ ЗначениеЗаполнено(СтранаПроисхождения) Тогда
					СтранаПроисхождения = ВыборкаТоваров.СтранаПроисхождения;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
					ЕдиницаИзмерения = ВыборкаТоваров.ЕдиницаИзмерения;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияПрослеживаемости) Тогда
					ЕдиницаИзмеренияПрослеживаемости = ВыборкаТоваров.ЕдиницаИзмеренияПрослеживаемости;
				КонецЕсли;
				ПерваяСтрока = Ложь;
			КонецЕсли;
			
			Если СтранаПроисхождения <> ВыборкаТоваров.СтранаПроисхождения
				ИЛИ ЕдиницаИзмерения <> ВыборкаТоваров.ЕдиницаИзмерения
				ИЛИ ЕдиницаИзмеренияПрослеживаемости <> ВыборкаТоваров.ЕдиницаИзмеренияПрослеживаемости Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТоваров);
			НоваяСтрока.КоличествоПрослеживаемости = ВыборкаТоваров.Количество * ВыборкаТоваров.Коэффициент;
			
			СуммаБезНДС = ВыборкаТоваров.СуммаВсего - ВыборкаТоваров.СуммаНДС;
			
			// Пересчитаем сумму
			Если ВалютаДокумента <> ВалютаОснования Тогда
				СуммаБезНДС = обПересчет(СуммаБезНДС, ВалютаОснования, КурсОснования, ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			
			НоваяСтрока.Сумма = СуммаБезНДС;
			
		КонецЦикла;
		
		СуммаДокумента = Товары.Итог("Сумма");
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("КоличествоПрослеживаемости",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Состояние",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("КодТНВЭД",1);
	ОбязательныеРеквизиты.Вставить("СтранаПроисхождения",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Если Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.ПолученОтвет Тогда
		ОбязательныеРеквизиты.Вставить("РНПТ",1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УведомлениеОВвозеПрослеживаемыхТоваров",		"Уведомление о ввозе прослеживаемых товаров",Истина);
	СписокХозОпераций.Добавить("УведомлениеОВвозеПрослеживаемыхАвтомобилей",		"Уведомление о ввозе прослеживаемых автомобилей", Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.КодОКПД2) Тогда
			ТекСтрока.КодОКПД2 = ТекСтрока.Номенклатура.КодОКПД2;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры	= Новый Структура();
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.КоличествоПрослеживаемости = ТекСтрока.Количество * ?(НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) ИЛИ ЕдиницаИзмерения.Коэффициент = 0, 1, ЕдиницаИзмерения.Коэффициент);
		Возврат Рез;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
	// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
	//
	// Параметры:
	//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
	//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
	//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
	//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
	//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
	//
	// Возвращаемое значение:
	//   ТабличныйДокумент	- Сформированная печатная форма
	//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Формирует печатную форму "Уведомление о ввозе"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьУведомлениеОВвозе(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("УведомлениеОВвозеПрослеживаемогоТовара");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Титул");
	
	ОбластьМакета.Параметры.НомерУведомления = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.ДатаУведомления = Формат(Дата, "ДЛФ=D");
	ОбластьМакета.Параметры.НомерКорректировки = "0";
	ОбластьМакета.Параметры.НаимОрг = СокрЛП(Организация.НаименованиеПолное);
	ОбластьМакета.Параметры.ИНН = Организация.ИНН;
	ОбластьМакета.Параметры.КПП = Организация.КПП;
	ОбластьМакета.Параметры.НаимЛицаПередПраво = спПолучитьПредставление(Контрагент, Новый Структура("Наименование"));
	ОбластьМакета.Параметры.КодСтраныКонтрагента = Контрагент.СтранаРегистрации.Код;
	ОбластьМакета.Параметры.НалоговыйОрганКонтаргента = Контрагент.НалоговыйНомер;
	ОбластьМакета.Параметры.АдресКонтрагента = спПолучитьПредставление(Контрагент, Новый Структура("АдресЮридический"));
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСопрДокумент");
	ОбластьМакета.Параметры.ВидСопоставляемогоДокумента = "2";
	ОбластьМакета.Параметры.ДатаВходящегоДокумента = Формат(ДокументОснование.ВхДокДата, "ДЛФ=D");
	ОбластьМакета.Параметры.НомерВходящегоДокумента = ДокументОснование.ВхДокНомер;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Стр9");
	ОбластьМакета.Параметры.КодТНВЭД = КодТНВЭД.Код;
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Стр10");
	ОбластьМакета.Параметры.КоличествоТовара = Формат(Товары.Итог("Количество"), "ЧЦ=19; ЧДЦ=6; ЧРД=.; ЧН=-");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Стр11");
	ОбластьМакета.Параметры.КодЕдиницыПервичногоДокумента = ?(ЗначениеЗаполнено(ЕдиницаИзмерения), ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код, "796");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Стр12");
	ОбластьМакета.Параметры.РНПТ = РНПТ;
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Стр13");
	ОбластьМакета.Параметры.КодЕдиницыПроследиваемости = ?(ЗначениеЗаполнено(ЕдиницаИзмеренияПрослеживаемости), ЕдиницаИзмеренияПрослеживаемости.Код, "796");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Стр14");
	ОбластьМакета.Параметры.КоличествоТовараПрослеживаемости = Формат(Товары.Итог("КоличествоПрослеживаемости"), "ЧЦ=19; ЧДЦ=6; ЧРД=.; ЧН=-");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Стр15");
	ОбластьМакета.Параметры.Стоимость = Формат(Товары.Итог("Сумма"), "ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=-");
	
	СтруктураОтбора=Новый Структура("Организация,Объект", Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	
	Если ЗначениеЗаполнено(Руководитель) Тогда
		ОбластьМакета.Параметры.ПрПодп = "1";
		МассивФИО = СтрРазделить(Руководитель.Наименование, " ", Ложь);
		ОбластьМакета.Параметры.ОргПодписантФамилия = СокрЛП(МассивФИО[0]);
		ОбластьМакета.Параметры.ОргПодписантИмя = ?(МассивФИО.Количество() > 1, МассивФИО[1], "");
		Если МассивФИО.Количество() > 2 Тогда
			Отчество = "";
			Для Инд = 2 По МассивФИО.Количество() - 1 Цикл
				Отчество = Отчество + МассивФИО[Инд];
			КонецЦикла;
			ОбластьМакета.Параметры.ОргПодписантОтчество = Отчество;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДатаПодписи = Формат(Дата, "ДЛФ=D");
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЧек()

#КонецЕсли

// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Рез=дкОбработкаЗаполнения(ЭтотОбъект, Основание);
	
	Если ЭтоНовый() Тогда
		Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
	КонецЕсли;
	
	// Приведем валюту документа в регл. валюте
	ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаДокумента, Дата);
	КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Если Основание <> Неопределено Тогда
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеАвтомобилей") Тогда
			
			ХозОперация = Справочники.ХозОперации.УведомлениеОВвозеПрослеживаемыхАвтомобилей;
			
		Иначе
			
			ХозОперация = Справочники.ХозОперации.УведомлениеОВвозеПрослеживаемыхТоваров;
			
		КонецЕсли;
		
		ЗаполнитьТоварыПоОснованию();
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	РНПТ = "";
	Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Товары.Итог("Сумма");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		//Проверим корректность видов введенной номенклатуры
		БлокироватьВидыНоменклатуры = ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
		Если БлокироватьВидыНоменклатуры<>Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры)=Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
			ЭтоПриходАвтомобилей = (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеАвтомобилей"));
			Ошибки="";
			Для каждого СтрокаТоваров Из Товары Цикл
				Если ЭтоПриходАвтомобилей И ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					ИЛИ НЕ ЭтоПриходАвтомобилей И ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Указанный тип товара не соответствует первичному документу.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если НЕ СтрокаТоваров.Номенклатура.Прослеживаемый Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Указанная номенклатура не является прослеживаемым товаром.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если СтрокаТоваров.Номенклатура.КодТНВЭД <> КодТНВЭД Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Код ТН ВЭД номенклатуры не соответствует коду ТН ВЭД документа.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
					Продолжить;
				КонецЕсли;
				ЕстьОшибка=(БлокироватьВидыНоменклатуры[СтрокаТоваров.Номенклатура.ВидНоменклатуры]<>Неопределено);
				Если ЕстьОшибка Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. В таблицу нельзя вводить номенклатуру вида """+СокрЛП(СтрокаТоваров.Номенклатура.ВидНоменклатуры)+""".";
					Отказ=Истина;
				КонецЕсли;
			КонецЦикла; 
			Если Отказ Тогда
				#Если Клиент Тогда
				// Выведем все накопившиеся ошибки 
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
				Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				#КонецЕсли
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РНПТ) И НЕ РНПТ.РНПТ Тогда
		Отказ = Истина;
		#Если Клиент Тогда
			Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки: РНПТ указан без соответствующего признака",СтатусСообщения.Внимание);
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.ПолученОтвет Тогда
		ВыполнитьДвиженияПоГТД(Отказ);
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

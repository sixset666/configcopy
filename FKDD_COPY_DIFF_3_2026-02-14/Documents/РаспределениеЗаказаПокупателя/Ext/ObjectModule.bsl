// Модуль документа РаспределениеЗаказаПокупателя

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ЗаказПоставщику",3);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	//Если ТипЗнч(Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
	//	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	//КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РаспределениеЗаказаПокупателя","Распределение заказа покупателя",Истина);

	Возврат СписокХозОпераций;
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат 0;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ДокументОснование" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Контрагент=ДокументОснование.Контрагент;
			ДоговорВзаиморасчетов=ДокументОснование.ДоговорВзаиморасчетов;
		ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Контрагент=ДокументОснование.ПодразделениеПолучатель;
			ДоговорВзаиморасчетов=Неопределено;
		Иначе
			Контрагент=Неопределено;
			ДоговорВзаиморасчетов=Неопределено;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Контрагент",,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Контрагент" Тогда
		Рез=Истина;
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Если Контрагент<>Неопределено И ТипЗнч(Контрагент)<>Тип("СправочникСсылка.Контрагенты") Тогда
				Контрагент=Неопределено;
			КонецЕсли; 
			Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказВнутренний") Тогда
			Если Контрагент<>Неопределено И ТипЗнч(Контрагент)<>Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
				Контрагент=Неопределено;
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ДоговорВзаиморасчетов=Неопределено;
			КонецЕсли; 
			Рез=дкОбработкаРеквизита(ЭтотОбъект,"ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			Контрагент=Неопределено;
			ДоговорВзаиморасчетов=Неопределено;
			Рез=дкОбработкаРеквизита(ЭтотОбъект,"ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Если ТипЗнч(ДокументОснование)<>Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ДоговорВзаиморасчетов=Неопределено;
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("РасчетКоличества",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитатьЦену",Ложь);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("РасчетКоличества",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("РасчетКоличества",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЗаказПоставщику" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("РасчетКоличества",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="РасчетКоличества" Тогда
		Рез=Истина;
		Если ДопПараметры<>Неопределено Тогда
			РасчетКоличества=Истина;
			Если НЕ ДопПараметры.Свойство("РасчетКоличества",РасчетКоличества) Тогда
				РасчетКоличества=Истина;
			КонецЕсли; 
			Если НЕ РасчетКоличества Тогда
				Возврат Рез;
			КонецЕсли; 
		КонецЕсли;
		Если ТекСтрока = Неопределено Тогда
			Возврат Рез;
		КонецЕсли;
		Количество=0; ЗаказПоставщику=Неопределено;
		//Получим нераспределенные остатки по заказу покупателя
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	СУММА(ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0)) КАК Заказано,
					 |	СУММА(ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0)) КАК Резерв,
					 |	СУММА(ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Распределено
					 |ИЗ
					 |	РегистрНакопления.ЗаказыПокупателей.Остатки(
					 |		&НаМомент,
					 |		Заказ = &Заказ
					 |			И Номенклатура = &Номенклатура
					 |			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыПокупателейОстатки
					 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(
					 |		&НаМомент,
					 |		ЗаказПокупателя = &Заказ
					 |			И Номенклатура = &Номенклатура
					 |			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыРаспределениеОстатки
					 |		ПО ЗаказыПокупателейОстатки.Заказ = ЗаказыРаспределениеОстатки.ЗаказПокупателя
					 |			И ЗаказыПокупателейОстатки.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
					 |			И ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры";
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
		Запрос.УстановитьПараметр("Заказ",ДокументОснование);
		Запрос.УстановитьПараметр("Номенклатура",ТекСтрока.Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
		ВыборкаЗаказПокупателя=Запрос.Выполнить().Выбрать();
		Если ВыборкаЗаказПокупателя.Следующий() И ВыборкаЗаказПокупателя.Заказано<>NULL И ВыборкаЗаказПокупателя.Резерв<>NULL И ВыборкаЗаказПокупателя.Распределено<>NULL Тогда
			Заказано=ВыборкаЗаказПокупателя.Заказано-ВыборкаЗаказПокупателя.Резерв-ВыборкаЗаказПокупателя.Распределено;
			СтрокиТоваров=Товары.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры));
			Для каждого СтрокаТоваров Из СтрокиТоваров Цикл
				Если СтрокаТоваров.НомерСтроки=ТекСтрока.НомерСтроки Тогда
					Продолжить;
				КонецЕсли;
				Заказано=Заказано-(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент);
			КонецЦикла; 
			Если Заказано>0 Тогда
				//Получим нераспределенные остатки по заказам поставщикам
				Запрос=Новый Запрос;
				Запрос.Текст="ВЫБРАТЬ
				             |	ОбъединенныйЗапрос.ЗаказПоставщику КАК ЗаказПоставщику,
							 |	СУММА(ЕСТЬNULL(ОбъединенныйЗапрос.Заказано, 0)) КАК Заказано,
							 |	СУММА(ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Распределено
							 |ИЗ(ВЫБРАТЬ
							 |	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
							 |	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
							 |	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
							 |	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК Заказано
							 |ИЗ
							 |	РегистрНакопления.ЗаказыПоставщикам.Остатки(
							 |		&НаМомент,
							 |		"+?(обЗначениеНеЗаполнено(ТекСтрока.ЗаказПоставщику),"","ЗаказПоставщику = &ЗаказПоставщика И")+"
							 |			  Номенклатура = &Номенклатура
							 |			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыПоставщикамОстатки
							 |
							 |ОБЪЕДИНИТЬ ВСЕ
							 |
							 |ВЫБРАТЬ
							 |	ЗаказыПокупателейОстатки.Заказ,
							 |	ЗаказыПокупателейОстатки.Номенклатура,
							 |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
							 |	ЗаказыПокупателейОстатки.ЗаказаноОстаток - ЗаказыПокупателейОстатки.РезервОстаток
							 |ИЗ
							 |	РегистрНакопления.ЗаказыПокупателей.Остатки(
							 |		&НаМомент,
							 |		Заказ ССЫЛКА Документ.ЗаказВнутренний
							 |		И Заказ<>&Заказ
							 |		И 
							 |		"+?(обЗначениеНеЗаполнено(ТекСтрока.ЗаказПоставщику),"","Заказ = &ЗаказПоставщика И")+"
							 |			  Номенклатура = &Номенклатура
							 |			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыПокупателейОстатки) КАК ОбъединенныйЗапрос
							 |	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(
							 |		&НаМомент,
							 |		"+?(обЗначениеНеЗаполнено(ТекСтрока.ЗаказПоставщику),"","ЗаказПоставщика = &ЗаказПоставщика И")+"
							 |			  Номенклатура = &Номенклатура
							 |			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыРаспределениеОстатки
							 |		ПО ОбъединенныйЗапрос.ЗаказПоставщику = ЗаказыРаспределениеОстатки.ЗаказПоставщика
							 |		И ОбъединенныйЗапрос.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
							 |		И ОбъединенныйЗапрос.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
							 |                                
							 |СГРУППИРОВАТЬ ПО
							 |	ОбъединенныйЗапрос.ЗаказПоставщику
							 |
							 |УПОРЯДОЧИТЬ ПО
							 |	ЗаказПоставщику.МоментВремени";
				Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
				Запрос.УстановитьПараметр("Номенклатура",ТекСтрока.Номенклатура);
				Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
				Запрос.УстановитьПараметр("ЗаказПоставщика",ТекСтрока.ЗаказПоставщику);
				Запрос.УстановитьПараметр("Заказ", ДокументОснование);
				ВыборкаЗаказыПоставщикам=Запрос.Выполнить().Выбрать();
				КоэффициентЕдиницыИзмерения = ?(обЗначениеНеЗаполнено(ТекСтрока.Коэффициент), ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, ТекСтрока.Коэффициент);
				Пока ВыборкаЗаказыПоставщикам.Следующий() Цикл
					ЗаказаноУПоставщика=ВыборкаЗаказыПоставщикам.Заказано-ВыборкаЗаказыПоставщикам.Распределено;
					Если ЗаказаноУПоставщика=0 Тогда
						Продолжить;
					КонецЕсли; 
					СтрокиТоваров=Товары.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ЗаказПоставщику",ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры,ВыборкаЗаказыПоставщикам.ЗаказПоставщику));
					Для каждого СтрокаТоваров Из СтрокиТоваров Цикл
						Если СтрокаТоваров.НомерСтроки=ТекСтрока.НомерСтроки Тогда
							Продолжить;
						КонецЕсли;
						ЗаказаноУПоставщика=ЗаказаноУПоставщика-(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент);
					КонецЦикла; 
					Если ЗаказаноУПоставщика>0 Тогда
						Количество=Мин(Заказано,ЗаказаноУПоставщика);
						Количество=Окр(Количество/КоэффициентЕдиницыИзмерения, 3);
						//ТекСтрока.Коэффициент
						ЗаказПоставщику=ВыборкаЗаказыПоставщикам.ЗаказПоставщику;
						Прервать;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли; 
		КонецЕсли;
		Если ЗаказПоставщику<>Неопределено И ТекСтрока.ЗаказПоставщику<>ЗаказПоставщику Тогда
			ТекСтрока.ЗаказПоставщику=ЗаказПоставщику;
		КонецЕсли; 
		Если Количество<>ТекСтрока.Количество Тогда
			ТекСтрока.Количество=Количество;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции

// Формирует печатную форму "РаспределениеЗаказаПокупателя"
// Возвращает сформированный табличный документ:
Функция ПечатьРаспределениеЗаказаПокупателя(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РаспределениеЗаказаПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
			
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка			= ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредставлениеЗаказа		= дкПолучитьПредставление(ДокументОснование);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	мсвДопОбластиПодвала = Неопределено;
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	//перебор строк
	Инд = 0;
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	КоличествоСтрок = ВыборкаТабличнойЧасти.Количество();
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		Инд = Инд+1;
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("ЗаказПоставщикуПредставление", дкПолучитьПредставление(СтрокаТабличнойЧасти.ЗаказПоставщику));
		СтруктураСтроки.Вставить("ЗаказПоставщику", СтрокаТабличнойЧасти.ЗаказПоставщику);
		СтруктураСтроки.Вставить("Поставщик", ?(ТипЗнч(СтрокаТабличнойЧасти.ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказВнутренний"), СтрокаТабличнойЧасти.ЗаказПоставщику.ПодразделениеКомпании, СтрокаТабличнойЧасти.ЗаказПоставщику.Контрагент));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект, ?(Инд = КоличествоСтрок, мсвДопОбластиПодвала, Неопределено));
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ОбработкаРеквизита("ДокументОснование");
	
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		Товары.Очистить();
		//Получим нераспределенные остатки по заказу покупателя
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	СУММА(ЗаказыПокупателейОстатки.ЗаказаноОстаток) КАК Заказано,
		             |	СУММА(ЗаказыПокупателейОстатки.РезервОстаток) КАК Резерв,
		             |	СУММА(ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Распределено
		             |ИЗ
		             |	РегистрНакопления.ЗаказыПокупателей.Остатки(&НаМомент, Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(&НаМомент, ЗаказПокупателя = &Заказ) КАК ЗаказыРаспределениеОстатки
		             |		ПО ЗаказыПокупателейОстатки.Заказ = ЗаказыРаспределениеОстатки.ЗаказПокупателя
		             |			И ЗаказыПокупателейОстатки.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
		             |			И ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
		             |
		             |СГРУППИРОВАТЬ ПО
		             |	ЗаказыПокупателейОстатки.Номенклатура,
		             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		             |
		             |УПОРЯДОЧИТЬ ПО
		             |	Номенклатура,
		             |	ХарактеристикаНоменклатуры";
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
		Запрос.УстановитьПараметр("Заказ",Основание);
		ВыборкаЗаказПокупателя=Запрос.Выполнить().Выгрузить();
		//Получим нераспределенные остатки по заказам поставщикам
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	ОбъединенныйЗапрос.ЗаказПоставщику КАК ЗаказПоставщику,
		             |	ОбъединенныйЗапрос.Номенклатура КАК Номенклатура,
		             |	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	СУММА(ОбъединенныйЗапрос.Заказано) КАК Заказано,
		             |	СУММА(ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Распределено
		             |	ИЗ(ВЫБРАТЬ
		             |	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
		             |	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		             |	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК Заказано
		             |	
		             |ИЗ
		             |	РегистрНакопления.ЗаказыПоставщикам.Остатки(
		             |		&НаМомент,
		             |		Номенклатура В (&Номенклатура)
		             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПоставщикамОстатки
					 |
					 |ОБЪЕДИНИТЬ ВСЕ
					 |
					 | ВЫБРАТЬ
					 |	ЗаказыПокупателейОстатки.Заказ,
		             |	ЗаказыПокупателейОстатки.Номенклатура,
		             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
		             |	ЗаказыПокупателейОстатки.ЗаказаноОстаток - ЗаказыПокупателейОстатки.РезервОстаток
		             |ИЗ
		             |	РегистрНакопления.ЗаказыПокупателей.Остатки(
		             |		&НаМомент,
					 |		Заказ ССЫЛКА Документ.ЗаказВнутренний
					 |		И Заказ <> &Заказ
					 |		И Номенклатура В (&Номенклатура)
		             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПокупателейОстатки) КАК ОбъединенныйЗапрос
					 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(
		             |		&НаМомент,
		             |		Номенклатура В (&Номенклатура)
		             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыРаспределениеОстатки
		             |		ПО ОбъединенныйЗапрос.ЗаказПоставщику = ЗаказыРаспределениеОстатки.ЗаказПоставщика
		             |			И ОбъединенныйЗапрос.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
		             |			И ОбъединенныйЗапрос.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
		             |
		             |СГРУППИРОВАТЬ ПО
		             |	ОбъединенныйЗапрос.ЗаказПоставщику,
		             |	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры,
		             |	ОбъединенныйЗапрос.Номенклатура
		             |
		             |УПОРЯДОЧИТЬ ПО
		             |	Номенклатура,
		             |	ХарактеристикаНоменклатуры,
		             |	ЗаказПоставщику.МоментВремени";
					 
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
		Запрос.УстановитьПараметр("Номенклатура",ВыборкаЗаказПокупателя.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ВыборкаЗаказПокупателя.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
		Запрос.УстановитьПараметр("Заказ", ДокументОснование);
		
		ВыборкаЗаказыПоставщикам=Запрос.Выполнить().Выбрать();
		ДопПараметры=Новый Структура("РасчетКоличества",Ложь);
		Для каждого НоменклатураЗаказаПокупателя Из ВыборкаЗаказПокупателя Цикл
			Заказано=НоменклатураЗаказаПокупателя.Заказано-НоменклатураЗаказаПокупателя.Резерв-НоменклатураЗаказаПокупателя.Распределено;
			Если Заказано<=0 Тогда
				Продолжить;
			КонецЕсли; 
			ВыборкаЗаказыПоставщикам.Сбросить();
			СтруктураОтбора=Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",НоменклатураЗаказаПокупателя.Номенклатура,НоменклатураЗаказаПокупателя.ХарактеристикаНоменклатуры);
			Пока Заказано>0 И ВыборкаЗаказыПоставщикам.НайтиСледующий(СтруктураОтбора) Цикл
				ЗаказаноУПоставщика=ВыборкаЗаказыПоставщикам.Заказано-ВыборкаЗаказыПоставщикам.Распределено;
				Если ЗаказаноУПоставщика<=0 Тогда
					Продолжить;
				КонецЕсли;
				Распределить=Мин(Заказано,ЗаказаноУПоставщика);
				НоваяСтрока=Товары.Добавить();
				НоваяСтрока.Номенклатура=НоменклатураЗаказаПокупателя.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры=НоменклатураЗаказаПокупателя.ХарактеристикаНоменклатуры;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока,,ДопПараметры);
				НоваяСтрока.Количество=Распределить/НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				НоваяСтрока.ЗаказПоставщику=ВыборкаЗаказыПоставщикам.ЗаказПоставщику;
				Заказано=Заказано-Распределить;
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли; 
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, ДокументОснование);
		Если ТаблицаДолгов.Количество()>0 Тогда
			СтрокаПоиска = ТаблицаДолгов[0];
			Сообщить("Заказ: """+СокрЛП(ДокументОснование)+""" нельзя распределить. Долг по предоплате составляет: "+Формат(СтрокаПоиска.Долг,"ЧЦ=15; ЧДЦ=2")+" "+СокрЛП(СтрокаПоиска.Валюта)+".", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПоставщика
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	//Получим остатки заказанных покупателем товаров
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
	             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	СУММА(ЗаказыПокупателейОстатки.ЗаказаноОстаток) КАК Заказано,
	             |	СУММА(ЗаказыПокупателейОстатки.РезервОстаток) КАК Резерв,
	             |	СУММА(ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Распределено
	             |ИЗ
	             |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	             |		&НаМомент,
	             |		Заказ = &Заказ
	             |		    И Номенклатура В (&Номенклатура)
	             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПокупателейОстатки
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(
	             |		&НаМомент,
	             |		ЗаказПокупателя = &Заказ
	             |		    И Номенклатура В (&Номенклатура)
	             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыРаспределениеОстатки
	             |		ПО ЗаказыПокупателейОстатки.Заказ = ЗаказыРаспределениеОстатки.ЗаказПокупателя
	             |			И ЗаказыПокупателейОстатки.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
	             |			И ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ЗаказыПокупателейОстатки.Номенклатура,
	             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
	             |
	             |ДЛЯ ИЗМЕНЕНИЯ
	             |	РегистрНакопления.ЗаказыПокупателей.Остатки,
	             |	РегистрНакопления.ЗаказыРаспределение.Остатки
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	Номенклатура,
	             |	ХарактеристикаНоменклатуры";
	Запрос.УстановитьПараметр("НаМомент",МоментВремени());
	Запрос.УстановитьПараметр("Заказ",ДокументОснование);
	Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	// Наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПокупателей");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(,Дата));
	ЗначенияБлокировки.Вставить("Заказ", ДокументОснование);
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", Товары);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	СтруктураПараметровБлокировки.ИмяТаблицы = "ЗаказыРаспределение";
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(,Дата));
	ЗначенияБлокировки.Вставить("ЗаказПокупателя", ДокументОснование);
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	ВыборкаЗаказПокупателя=Запрос.Выполнить().Выгрузить();
	
	//Получим остатки нераспределенных заказов поставщикам
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОбъединенныйЗапрос.ЗаказПоставщику КАК ЗаказПоставщику,
	             |	ОбъединенныйЗапрос.Номенклатура,
	             |	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры,
	             |	СУММА(ОбъединенныйЗапрос.Заказано) КАК Заказано,
	             |	СУММА(ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Распределено
				 |
				 |ИЗ(ВЫБРАТЬ
	             |	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
	             |	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
	             |	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК Заказано
	             |ИЗ
	             |	РегистрНакопления.ЗаказыПоставщикам.Остатки(
	             |		&НаМомент,
	             |		ЗаказПоставщику В (&Заказ)
	             |		    И Номенклатура В (&Номенклатура)
	             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПоставщикамОстатки
				 |
				 |ОБЪЕДИНИТЬ ВСЕ
				 |
				 |ВЫБРАТЬ
				 |	ЗаказыПокупателейОстатки.Заказ,
	             |	ЗаказыПокупателейОстатки.Номенклатура,
	             |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	             |	ЗаказыПокупателейОстатки.ЗаказаноОстаток - ЗаказыПокупателейОстатки.РезервОстаток
	             |ИЗ
	             |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	             |		&НаМомент,
				 |		Заказ ССЫЛКА Документ.ЗаказВнутренний
				 |		И Заказ <> &ВыбЗаказ
	             |		И Заказ В (&Заказ)
	             |		И Номенклатура В (&Номенклатура)
	             |		И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыПокупателейОстатки) КАК ОбъединенныйЗапрос
				 |	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(
	             |		&НаМомент,
	             |		ЗаказПоставщика В (&Заказ)
	             |		    И Номенклатура В (&Номенклатура)
	             |		    И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ЗаказыРаспределениеОстатки
	             |		ПО ОбъединенныйЗапрос.ЗаказПоставщику = ЗаказыРаспределениеОстатки.ЗаказПоставщика
	             |			И ОбъединенныйЗапрос.Номенклатура = ЗаказыРаспределениеОстатки.Номенклатура
	             |			И ОбъединенныйЗапрос.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ОбъединенныйЗапрос.ЗаказПоставщику,
	             |	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры,
	             |	ОбъединенныйЗапрос.Номенклатура
	             |
	             |ДЛЯ ИЗМЕНЕНИЯ
	             |	РегистрНакопления.ЗаказыРаспределение.Остатки
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ЗаказПоставщику.МоментВремени";
	Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,МоментВремени()));
	Запрос.УстановитьПараметр("Заказ",Товары.ВыгрузитьКолонку("ЗаказПоставщику"));
	Запрос.УстановитьПараметр("ВыбЗаказ", ДокументОснование);
	Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	// Наложим блокировку на считываемые данные
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(,Дата));
	СтруктураПараметровБлокировки = Новый Соответствие;
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных", Товары);
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыРаспределение");
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("ЗаказПоставщика", "ЗаказПоставщику");
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	ВыборкаЗаказыПоставщикам=Запрос.Выполнить().Выгрузить();
	
	ДвиженияЗаказыРаспределение=Движения.ЗаказыРаспределение;
	
	Для каждого СтрокаТоваров Из Товары Цикл
		ТоварыКоличество=СтрокаТоваров.Количество*(?(СтрокаТоваров.Коэффициент=0,1,СтрокаТоваров.Коэффициент));
		//Проверим остатки товара по заказу покупателя
		СтрокиЗаказаПокупателя=ВыборкаЗаказПокупателя.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрокаТоваров.Номенклатура,СтрокаТоваров.ХарактеристикаНоменклатуры));
		Если СтрокиЗаказаПокупателя.Количество()=0 Тогда
			ЗаказПокупателяЗаказано=0;
			ЗаказПокупателяРезерв=0;
			ЗаказПокупателяРаспределено=0;
			ЗаказПокупателяОсталосьРаспределить=0;
		Иначе
			ЗаказПокупателяЗаказано=СтрокиЗаказаПокупателя[0].Заказано;
			ЗаказПокупателяРезерв=СтрокиЗаказаПокупателя[0].Резерв;
			ЗаказПокупателяРаспределено=СтрокиЗаказаПокупателя[0].Распределено;
			ЗаказПокупателяОсталосьРаспределить=ЗаказПокупателяЗаказано-ЗаказПокупателяРезерв-ЗаказПокупателяРаспределено;
		КонецЕсли; 
		Если ЗаказПокупателяОсталосьРаспределить<ТоварыКоличество Тогда
			Если обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат("["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""". Осталось распределить по заказу покупателя "+Формат(ЗаказПокупателяОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Распределяется "+Формат(ТоварыКоличество,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Превышение "+Формат(ТоварыКоличество-ЗаказПокупателяОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения),"Важное&Заказы распределение:",ЭтотОбъект,СтрокаТоваров.Номенклатура);
			Иначе
				дкСообщитьРезультат("["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТоваров.ХарактеристикаНоменклатуры)+""". Осталось распределить по заказу покупателя "+Формат(ЗаказПокупателяОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Распределяется "+Формат(ТоварыКоличество,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Превышение "+Формат(ТоварыКоличество-ЗаказПокупателяОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения),"Важное&Заказы распределение:",ЭтотОбъект,СтрокаТоваров.Номенклатура);
			КонецЕсли; 
			Отказ=Истина;
		КонецЕсли; 
		//Проверим остатки нераспределенного товара по заказу поставщику
		СтрокиЗаказаПоставщику=ВыборкаЗаказыПоставщикам.НайтиСтроки(Новый Структура("ЗаказПоставщику,Номенклатура,ХарактеристикаНоменклатуры",СтрокаТоваров.ЗаказПоставщику,СтрокаТоваров.Номенклатура,СтрокаТоваров.ХарактеристикаНоменклатуры));
		Если СтрокиЗаказаПоставщику.Количество()=0 Тогда
			ЗаказПоставщикуЗаказано=0;
			ЗаказПоставщикуРаспределено=0;
			ЗаказПоставщикуОсталосьРаспределить=0;
		Иначе
			ЗаказПоставщикуЗаказано=СтрокиЗаказаПоставщику[0].Заказано;
			ЗаказПоставщикуРаспределено=СтрокиЗаказаПоставщику[0].Распределено;
			ЗаказПоставщикуОсталосьРаспределить=ЗаказПоставщикуЗаказано-ЗаказПоставщикуРаспределено;
		КонецЕсли; 
		Если ЗаказПоставщикуОсталосьРаспределить<ТоварыКоличество Тогда
			Если обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат("["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""". Осталось нераспределенным в заказе поставщику "+Формат(ЗаказПоставщикуОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Распределяется "+Формат(ТоварыКоличество,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Превышение "+Формат(ТоварыКоличество-ЗаказПоставщикуОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения),"Важное&Заказы распределение:",ЭтотОбъект,СтрокаТоваров.Номенклатура);
			Иначе
				дкСообщитьРезультат("["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТоваров.ХарактеристикаНоменклатуры)+""". Осталось нераспределенным в заказе поставщику "+Формат(ЗаказПоставщикуОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Распределяется "+Формат(ТоварыКоличество,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения)+". Превышение "+Формат(ТоварыКоличество-ЗаказПоставщикуОсталосьРаспределить,"ЧДЦ=3; ЧН=0,00")+" "+СокрЛП(СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения),"Важное&Заказы распределение:",ЭтотОбъект,СтрокаТоваров.Номенклатура);
			КонецЕсли; 
			Отказ=Истина;
		КонецЕсли; 
		//Если все нормально, добавим запись в регистр распределения
		Если НЕ Отказ Тогда
			НоваяЗапись=ДвиженияЗаказыРаспределение.Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=Дата;
			НоваяЗапись.Регистратор=Ссылка;
			НоваяЗапись.ЗаказПокупателя=ДокументОснование;
			НоваяЗапись.ЗаказПоставщика=СтрокаТоваров.ЗаказПоставщику;
			НоваяЗапись.Номенклатура=СтрокаТоваров.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
			НоваяЗапись.Количество=ТоварыКоличество;
			НоваяЗапись.ХозОперация=ХозОперация;
		КонецЕсли; 
	КонецЦикла; 
	
	// двигаем границу последовательности заказов
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаЗаказов = Движения.ЗаказыРаспределение.Выгрузить();
			ТаблицаЗаказов.Свернуть("ЗаказПоставщика","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПоставщика");
			НаборЗаписейГраницыЗаказы.Заказ.Добавить(ДокументОснование);
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

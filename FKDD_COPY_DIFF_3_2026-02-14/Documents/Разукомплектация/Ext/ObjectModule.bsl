// Модуль документа Разукомплектация

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	//++СнимченкоАА_бизтех++
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	//--СнимченкоАА_бизтех--
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Комплект",1);
	
	РучноеСписаниеХарактеристик = Комплект.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ Комплект.ТипНоменклатуры.АвтоСписаниеХарактеристик.Пустая();
	УчетВедется = Комплект.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ Комплект.ТипНоменклатуры.ИспользованиеХарактеристик = 2;
	Если УчетВедется И (РучноеСписаниеХарактеристик) И обЗначениеНеЗаполнено(ХарактеристикаКомплекта) Тогда
		ОбязательныеРеквизиты.Вставить("ХарактеристикаКомплекта",1);
	КонецЕсли;
		
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	//++СнимченкоАА_бизтех++
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	//--СнимченкоАА_бизтех-- 
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;  
	
	//++СнимченкоАА_бизтех++
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	//--СнимченкоАА_бизтех--
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Разукомплектация","Разукомплектация",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  Режим - число, 0-состав комплекта (по остаткам), 1-состав комплекта (по партиям), 2-сам комплект
//  ШапкаДокумента - выборка.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента,Режим=0)
	Запрос=Новый Запрос();
	Если Режим=2 Тогда
		// КОМПЛЕКТ
	    ТекстЗапроса="
		|ВЫБРАТЬ
		|	Разукомплектация.Комплект КАК Номенклатура,
		|	Разукомплектация.ХарактеристикаКомплекта КАК ХарактеристикаНоменклатуры,
		|	Разукомплектация.ЦенаКомплектаРозничная КАК ЦенаРозничная,
		|	Разукомплектация.КоличествоКомплектов КАК Количество,
		|	Разукомплектация.КоличествоКомплектов*Разукомплектация.ЦенаКомплекта КАК СуммаВсего,";
		Если ШапкаДокумента.СкладКомпании.Розничный Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	Разукомплектация.КоличествоКомплектов*Разукомплектация.ЦенаКомплектаРозничная КАК СуммаРозн,";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|	0 КАК СуммаРозн,";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|	0 КАК СуммаНДС
		|ИЗ
		|	Документ.Разукомплектация КАК Разукомплектация
	    |ГДЕ
		|	Разукомплектация.Ссылка=&Ссылка
		|";
		Запрос.Текст=ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		РезультатЗапроса=Запрос.Выполнить();
		
	Иначе
		// СОСТАВ КОМПЛЕКТА
		Если обЗначениеНеЗаполнено(ШапкаДокумента.ДокументОснование) Тогда
		    ТекстЗапроса="
			|ВЫБРАТЬ
			|	РазукомплектацияТовары.Номенклатура КАК Номенклатура,
			|	РазукомплектацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	РазукомплектацияТовары.ЦенаРозничная КАК ЦенаРозничная,
			|	РазукомплектацияТовары.Количество*РазукомплектацияТовары.Коэффициент*(&КоличествоКомплектов) КАК Количество,";
			Если ШапкаДокумента.СкладКомпании.Розничный Тогда
				ТекстЗапроса=ТекстЗапроса+"
				|	РазукомплектацияТовары.СуммаРозничная КАК СуммаРозн,";
			Иначе
				ТекстЗапроса=ТекстЗапроса+"
				|	0 КАК СуммаРозн,";
			КонецЕсли;
			ТекстЗапроса=ТекстЗапроса+"
			|	РазукомплектацияТовары.СуммаНДС КАК СуммаНДС,
			|	РазукомплектацияТовары.СтавкаНДС КАК СтавкаНДС,
			|	РазукомплектацияТовары.Сумма КАК СуммаВсего
			|
			|ИЗ
			|	Документ.Разукомплектация.Товары КАК РазукомплектацияТовары
		    |ГДЕ
			|	РазукомплектацияТовары.Ссылка=&Ссылка
			|";
			Запрос.Текст=ТекстЗапроса;
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			Запрос.УстановитьПараметр("КоличествоКомплектов",ШапкаДокумента.КоличествоКомплектов);
			РезультатЗапроса=Запрос.Выполнить();
		Иначе
			Если Режим=0 Тогда
				Запрос=Новый Запрос("
				|ВЫБРАТЬ
				|	Номенклатура КАК Номенклатура,
				|	ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	СкладКомпании КАК СкладКомпании,
				|	СУММА(0) КАК ЦенаРозничная,
				|   СУММА(0) КАК СуммаРозничная,
				|   СУММА(0) КАК СуммаСкидки,
				|	СУММА(Количество) КАК Количество,
				|   СУММА(0) КАК СуммаНДС,
				|	СУММА(0) КАК Резерв
				|ИЗ
				|	РегистрНакопления.ОстаткиТоваровКомпании
				|
				|ГДЕ
				|	Регистратор=&Ссылка И ВидДвижения=&ВидДвиженияРасход
				|	
				|СГРУППИРОВАТЬ ПО
				|	Номенклатура,
				|	ХарактеристикаНоменклатуры,
				|	СкладКомпании");
			Иначе
				Запрос=Новый Запрос("
				|ВЫБРАТЬ
				|	Номенклатура КАК Номенклатура,
				|	ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|	Партия КАК Партия,
				|	МАКСИМУМ(СтавкаНДС) КАК СтавкаНДС,
				|	СУММА(СуммаНДС) КАК СуммаНДС,
				|	СУММА(Сумма) КАК СуммаВсего,
				|	СУММА(Количество) КАК Количество
				|ИЗ
				|	РегистрНакопления.ПартииТоваровКомпании
				|
				|ГДЕ
				|	Регистратор=&Ссылка И ВидДвижения=&ВидДвиженияРасход
				|	
				|СГРУППИРОВАТЬ ПО
				|	Номенклатура,
				|	ХарактеристикаНоменклатуры,
				|	Партия
				|");
			КонецЕсли;
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.ДокументОснование);
			Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
			ТаблицаДвижений=Запрос.Выполнить().Выгрузить();
			РезультатЗапроса=ТаблицаДвижений.Скопировать(); РезультатЗапроса.Очистить();
			
			ЗапросТовары=Новый Запрос("
			|ВЫБРАТЬ
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры,
			|	СУММА(Количество*Коэффициент) КАК Количество,
			|	МАКСИМУМ(СтавкаНДС) КАК СтавкаНДС
			|ИЗ
			|	Документ.Разукомплектация.Товары
			|ГДЕ
			|	Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО 
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры
			|");
			ЗапросТовары.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			ВыборкаТовары=ЗапросТовары.Выполнить().Выбрать();
			
			// немножко посчитаем таблицу по количеству комплектов
			Пока ВыборкаТовары.Следующий() Цикл
				НадоОприходовать=ВыборкаТовары.Количество*ШапкаДокумента.КоличествоКомплектов;
				СтруктураОтбора=Новый Структура("Номенклатура",ВыборкаТовары.Номенклатура);
				Если НЕ обЗначениеНеЗаполнено(ВыборкаТовары.ХарактеристикаНоменклатуры) Тогда
					СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",ВыборкаТовары.ХарактеристикаНоменклатуры);
				КонецЕсли;
				НайденныеСтроки=ТаблицаДвижений.НайтиСтроки(СтруктураОтбора);
				
				Для Сч=0 По НайденныеСтроки.ВГраница() Цикл
					ТекСтрока=НайденныеСтроки[Сч];
					Если ТекСтрока.Количество=0 Тогда Продолжить; КонецЕсли;
					
					НоваяСтрока=РезультатЗапроса.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока);
					
					НоваяСтрока.Количество=Мин(НадоОприходовать,НоваяСтрока.Количество);
					
					Если Режим=1 Тогда
						// если партии, то изменим стоимость и НДС	
						НоваяСтрока.СуммаВсего=НоваяСтрока.СуммаВсего*(НоваяСтрока.Количество/ТекСтрока.Количество);
						НоваяСтрока.СтавкаНДС = ?(обЗначениеНеЗаполнено(НоваяСтрока.СтавкаНДС),ВыборкаТовары.СтавкаНДС,НоваяСтрока.СтавкаНДС);
						НоваяСтрока.СуммаНДС=НоваяСтрока.СуммаНДС*(НоваяСтрока.Количество/ТекСтрока.Количество);
					
					ИначеЕсли ШапкаДокумента.СкладКомпании.Розничный Тогда
						// для остатков получим текущую розничную цену
						НоваяСтрока.ЦенаРозничная=обПолучитьЦену(ШапкаДокумента.СкладКомпании.ТипЦенРозничнойТорговли,ВыборкаТовары.Номенклатура,ШапкаДокумента.Ссылка,,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента);
						НоваяСтрока.СуммаРозничная=НоваяСтрока.ЦенаРозничная*НоваяСтрока.Количество;
					КонецЕсли;
					
					ТекСтрока.Количество=ТекСтрока.Количество-НоваяСтрока.Количество;
					НадоОприходовать=НадоОприходовать-НоваяСтрока.Количество;
					Если НадоОприходовать<=0 Тогда Прервать; КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Возврат РезультатЗапроса;
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.КоличествоКомплектов КАК КоличествоКомплектов,
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦенРозничнойТорговли,
	|	Док.СкладКомпании.Розничный КАК СкладКомпанииРозничный,
	|   Док.СкладКомпании.Подразделение КАК ПодразделениеСклада,  
	|	Док.ДокументОснование КАК ДокументОснование,
	|	ДокТовары.Цена КАК Цена
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(Цена) КАК Цена ИЗ Документ."+Метаданные().Имя+".Товары ГДЕ Ссылка=&Ссылка) КАК ДокТовары
	|	ПО Истина
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// определим способ ведения баланса
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим результат запроса по комплекту
	РезультатЗапросаПоТоварамКомплект=ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента,2);
	
	// 1. Списываем комплект
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварамКомплект;
	Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	
	//Получим результат запроса по составу комплекта
	РезультатЗапросаПоТоварамСостав=ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента,1);
	
	// 2. Приходуем состав
	НаборЗаписейПартии.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейПартии.ШапкаДокумента = ШапкаДокумента;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамСостав;
	НаборЗаписейПартии.СтатусПартии   = Перечисления.СтатусыПартий.ТоварКупленный;
	НаборЗаписейПартии.ЕстьСтавкаНДС  = Истина;
	Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
	
	// Доходы и расходы
	ТаблицаДиР=НаборЗаписейПартии.Выгрузить();
	ТаблицаДиР.Свернуть("ВидДвижения","СуммаУпр");
	
	ПриходСумма=0;
	СтрокаТЗ = ТаблицаДиР.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
	Если СтрокаТЗ <> Неопределено Тогда
		ПриходСумма = СтрокаТЗ.СуммаУпр;
	КонецЕсли;
	
	РасходСумма = 0;
	СтрокаТЗ = ТаблицаДиР.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
	Если СтрокаТЗ <> Неопределено Тогда
		РасходСумма = СтрокаТЗ.СуммаУпр;
	КонецЕсли;
	
	Если ПриходСумма <> РасходСумма Тогда
		НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
		НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение       = ШапкаДокумента.ПодразделениеСклада;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.ОтклонениеСебестоимостиПриКомплектации;
		НаборЗаписейДиР.ВУпрВалюте				= Истина;  
		
		Разница = ПриходСумма-РасходСумма;
		Если Разница > 0 Тогда
			НаборЗаписейДиР.Доход = Разница;
		Иначе
			НаборЗаписейДиР.Расход = -Разница;
		КонецЕсли; 

		Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли; 
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Товары.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="СкладКомпании" Тогда
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			// заполнение ячеек
			Если НЕ обЗначениеНеЗаполнено(Комплект) Тогда
				Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(Комплект,СкладКомпании);
			КонецЕсли;
			
			Для Каждого стрТовары Из Товары Цикл
				Если НЕ обЗначениеНеЗаполнено(стрТовары.Номенклатура) Тогда
					стрТовары.Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.Номенклатура,СкладКомпании);
				Иначе
					стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
			
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Рез=ОбработкаРеквизита("УстановитьРозничнуюЦенуКомплекта",,ЭтаФорма);
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма) И Рез;
				КонецЦикла;
				Возврат Рез;
			КонецЕсли;
		Иначе
			Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Для Каждого стрТовары Из Товары Цикл
				стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЦикла;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Комплект" Тогда
		Если НЕ обЗначениеНеЗаполнено(Комплект) Тогда
			// заполнение ячейки
			Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(Комплект,СкладКомпании);
			Если Комплект.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Комплект Тогда
				#Если Клиент Тогда
				Предупреждение("Необходимо выбирать только комплекты !",10); Комплект=Неопределено;
				#КонецЕсли
				Возврат Ложь;
			КонецЕсли;
			ЦенаКомплекта=обПолучитьЦену(ТипЦен,Комплект,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ХарактеристикаКомплекта,, ПодразделениеКомпании);
			ЦенаКомплектаРозничная=0;
			ОбработкаРеквизита("УстановитьРозничнуюЦенуКомплекта",,ЭтаФорма);
			КоличествоКомплектов=1;
			Если (НЕ обЗначениеНеЗаполнено(ХарактеристикаКомплекта)) И (ХарактеристикаКомплекта.Владелец<>Комплект) И (ХарактеристикаКомплекта.Владелец<>Комплект.ТипНоменклатуры) Тогда
				ХарактеристикаКомплекта=Неопределено;
			КонецЕсли;
		Иначе
			ЦенаКомплекта=0;
			ЦенаКомплектаРозничная=0;
			КоличествоКомплектов=0;
			ХарактеристикаКомплекта=Неопределено;
			Товары.Очистить();
			Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="КоличествоКомплектов" Тогда
		Рез = Истина;
		Для каждого СтрокаТоваров Из Товары Цикл
			Рез = ОбработкаРеквизита("Товары.Количество",СтрокаТоваров,ЭтаФорма) И Рез;
		КонецЦикла;
		#Если Клиент Тогда
		дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ХарактеристикаКомплекта" Тогда
		ИзменятьКоличество = (НЕ обЗначениеНеЗаполнено(ХарактеристикаКомплекта)) И КоличествоКомплектов <> 1
							И Комплект.ТипНоменклатуры.УникальностьСерийногоНомера;
		Если ИзменятьКоличество Тогда
			КоличествоКомплектов = 1;
			#Если Клиент Тогда
			Сообщить("Для характеристики комплекта """+СокрЛП(Комплект)+""" установлена уникальность серийного номера. Количество не может быть не равным 1.");
			#КонецЕсли
			Возврат ОбработкаРеквизита("КоличествоКомплектов",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Истина;
		
	// ТОВАРЫ
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если ТекСтрока.Коэффициент<>1 Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
				ТекСтрока.ЕдиницаИзмерения = ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
			КонецЕсли;
			Рез = ОбработкаРеквизита("Товары.ЕдиницаИзмерения",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез = ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		ТекСтрока.Сумма = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов*ТекСтрока.Цена;
		Рез = ОбработкаРеквизита("Товары.СтавкаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		ТекКоличество = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов;
		Если ТекКоличество <> 0 Тогда
			ТекСтрока.Цена = ТекСтрока.Сумма/ТекКоличество;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
		КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл));
		ТекСтрока.ЦенаРозничная=ТекЦенаЗакупки+(ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		ТекСтрока.СуммаРозничная = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов*ТекСтрока.ЦенаРозничная;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		ТекКоличество = ТекСтрока.Количество*ТекСтрока.Коэффициент*КоличествоКомплектов;
		Если ТекКоличество <> 0 Тогда
			ТекСтрока.ЦенаРозничная = ТекСтрока.СуммаРозничная/ТекКоличество;
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры); 		
		        	
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена=0 Тогда
			ТекСтрока.ПроцентНаценки=0;
		Иначе
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(ВалютаРегл = ВалютаДокумента,ТекСтрока.Цена,обПересчет(ТекСтрока.Цена,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл));
			ТекСтрока.ПроцентНаценки=((ТекСтрока.ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
		КонецЕсли;		
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) Тогда
			ВалютаРегл  = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл ,КурсРегл, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюЦенуКомплекта" Тогда
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(Комплект)) Тогда
			ВалютаРегл     = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
			КурсРегл       = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ЦенаКомплектаРозничная = обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,Комплект,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл,КурсРегл, ХарактеристикаКомплекта,, СкладКомпании.Подразделение);
		КонецЕсли;
		Возврат Истина;
	ИначеЕсли Имя="Товары.СтавкаНДС" Тогда
		СуммаСоСкидкой = дкПолучитьСуммуСтрокиСоСкидкой(ЭтотОбъект,ТекСтрока);
		Попытка
			СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
			ТекСтрока.СуммаНДС = Окр((СуммаСоСкидкой * СтавкаНДС)/(100 + СтавкаНДС),2);
		Исключение
		КонецПопытки;
		ТекСтрока.Сумма = СуммаСоСкидкой;
		Возврат Истина;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Разукомплектация"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРазукомплектация(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Разукомплектация");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);

	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеКомплекта = спПолучитьНаименование(ЭтотОбъект.Комплект)+ ", " + Строка(ЭтотОбъект.КоличествоКомплектов) + " " + ЭтотОбъект.Комплект.БазоваяЕдиницаИзмерения;
	ОбластьМакета.Параметры.ПредставлениеСклада = спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма, СуммаВсего",ВалютаДокумента,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("КоличествоВсего", Формат(СтруктураСтроки.Количество*КоличествоКомплектов, ФорматВыводаКоличества));
		СтруктураСтроки.Вставить("Сумма", Формат(СтруктураСтроки.Количество*СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("СуммаВсего", Формат(СтрокаТабличнойЧасти.Сумма, ФорматВыводаСуммы));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма, СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтроки, СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	Сумма = 0;
	Для каждого ТекущаяСтрока Из ВыборкаТабличнойЧасти Цикл    
		Сумма = Сумма + ТекущаяСтрока.Цена*ТекущаяСтрока.Количество;	                          
	КонецЦикла; 
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьПодвал.Параметры.Сумма 		= Формат(Сумма,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаВсего  = Формат(СуммаВсего,ФорматВыводаСуммы);
	
	ОбластьПодвал.Параметры.ЧислоПозиций = "Всего наименований: "+ВыборкаТабличнойЧасти.Количество();
	ОбластьПодвал.Параметры.СуммаПрописью = "Себестоимость комплекта: " + обЧислоПрописью(Сумма,ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.СуммаПрописьюВсего = "Себестоимость всех комплектующих: " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРазукомплектация()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если Основание <> Неопределено Тогда
		Если Основание.ХозОперация = Справочники.ХозОперации.Комплектация Тогда
			ВалютаДокумента = Основание.ВалютаДокумента;
			СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Для Каждого СтрокаТовар Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура",СтрокаТовар);
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;   
	
	//++СнимченкоАА_бизтех++
	КодыМаркировки.Очистить();
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --

	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	Отказ = КоличествоКомплектов = 0;
	Если Отказ Тогда
		РезультатОбработки = "Не задано количество комплектов";
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	КонецЕсли;
	
	// получим результаты запроса
	РезультатЗапросаПоТоварамКомплект=ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект,2);
	РезультатЗапросаПоТоварамСостав=ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект,0);
	
	// 1. Списываем комплект
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварамКомплект;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;

	Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // дальше смысла не имеет
	КонецЕсли;
	
	// 2. Приходуем состав
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварамСостав;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;

	Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// если приходуем товар на розничный склад, то установим розничные цены на этот товар
	Если НЕ Отказ И СкладКомпании.Розничный И ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ПодразделениеКомпании = СкладКомпании.Подразделение;
		НаборЗаписейЦены.ТипЦен=СкладКомпании.ТипЦенРозничнойТорговли;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				//Расход
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Количество = тс.Количество;  
				
				Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

  //++СнимченкоАА_бизтех++
Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
  //--СнимченкоАА_бизтех--

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль документа ПереразмещениеТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Если ХозОперация=Справочники.ХозОперации.ПереразмещениеШин Тогда
		Для каждого ВидНоменклатуры Из Метаданные.Перечисления.ВидыНоменклатуры.ЗначенияПеречисления Цикл
			ВидНоменклатурыСсылка=Перечисления.ВидыНоменклатуры[ВидНоменклатуры.Имя];
			Если ВидНоменклатурыСсылка<>Перечисления.ВидыНоменклатуры.Шины Тогда
				Результат.Вставить(ВидНоменклатурыСсылка,0);
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	КонецЕсли; 
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		ТекСтрока.ЕдиницаИзмеренияНовая = ТекСтрока.ЕдиницаИзмерения;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.КоличествоБазовоеНовое = ТекСтрока.КоличествоБазовое;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.КоличествоБазовоеНовое = ТекСтрока.КоличествоБазовое;
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.ЕдиницаИзмеренияНовая" Тогда
		Возврат Истина;
        
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
       
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПереразмещениеТоваров", "Переразмещение товаров", Истина);
	СписокХозОпераций.Добавить("ПереразмещениеШин", "Переразмещение шин", Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмеренияНовая",3);
	ОбязательныеТовары.Вставить("Ячейка",3);
	ОбязательныеТовары.Вставить("ЯчейкаНовая",3);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если ХозОперация=Справочники.ХозОперации.ПереразмещениеШин Тогда
		ОбязательныеТовары.Вставить("ЗаявкаНаХранениеШин",3);
	КонецЕсли; 

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("СкладПолучатель",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("СкладПолучатель", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
		
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(ДокументОснование);
		Если ХозОперация = Справочники.ХозОперации.ПереразмещениеШин Тогда
			Если ТипЗнч(ЗаявкаНаХранениеШин)<>Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" должен быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли;
		Иначе
			Если ТипЗнч(ЗаявкаНаХранениеШин)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" не может быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ПереразмещениеТоваров"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПереразмещениеТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПереразмещениеТоваров");
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
			
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	Если СкладКомпании=СкладПолучатель Тогда
		ОбластьМакета.Параметры.НадписьСклад="Склад:";
		ОбластьМакета.Параметры.СкладПолучатель=Неопределено;
		ОбластьМакета.Параметры.ПредставлениеСкладаПолучателя=Неопределено;
	Иначе
		ОбластьМакета.Параметры.НадписьСклад="Склад отправитель:";
		ОбластьМакета.Параметры.НадписьСкладПолучатель="Склад получатель:";
		ОбластьМакета.Параметры.ПредставлениеСкладаПолучателя=спПолучитьНаименование(СкладПолучатель);
	КонецЕсли;	
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("ЕдиницаИзмеренияНовая", СтрокаТабличнойЧасти.ЕдиницаИзмеренияНовая);
		СтруктураСтроки.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);
		СтруктураСтроки.Вставить("ЯчейкаНовая", СтрокаТабличнойЧасти.ЯчейкаНовая);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы,,ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПереразмещениеТоваров()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	СкладПолучатель=СкладКомпании;
	
	Если (ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин")) ИЛИ
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.ПриходныйСкладскойОрдер") И Основание.ХозОперация=Справочники.ХозОперации.ПриходныйСкладскойОрдерШин) Тогда
		ХозОперация=Справочники.ХозОперации.ПереразмещениеШин;
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(Основание);
		Товары.Очистить();
		ТекстЗапроса = "ВЫБРАТЬ
					   |	ОстаткиТоваровОрдерныйСкладОстатки.СкладКомпании КАК СкладКомпании,
					   |	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка КАК Ячейка,
					   |	ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура КАК Номенклатура,
					   |	ОстаткиТоваровОрдерныйСкладОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					   |	ОстаткиТоваровОрдерныйСкладОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
					   |	ОстаткиТоваровОрдерныйСкладОстатки.ЗаявкаНаХранениеШин КАК ЗаявкаНаХранениеШин,
					   |	ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток КАК Количество
					   |ИЗ
					   |	РегистрНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(
					   |			&Момент,
					   |			СкладКомпании = &СкладКомпании
					   |				И ЗаявкаНаХранениеШин = &ЗаявкаНаХранениеШин) КАК ОстаткиТоваровОрдерныйСкладОстатки
					   |
					   |УПОРЯДОЧИТЬ ПО
					   |	Ячейка,
					   |	Номенклатура,
					   |	ХарактеристикаНоменклатуры";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Момент",?(ЭтоНовый(),Новый Граница(Новый МоментВремени(КонецДня(Дата)),ВидГраницы.Исключая), Новый Граница(Ссылка.МоментВремени(),ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("ЗаявкаНаХранениеШин", ЗаявкаНаХранениеШин);
		ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаОстатков Из ТаблицаОстатков Цикл
			СтрокаТЧ=Товары.Добавить();
			СтрокаТЧ.Номенклатура=СтрокаОстатков.Номенклатура;
			СтрокаТЧ.ЗаявкаНаХранениеШин=ЗаявкаНаХранениеШин;
			Если обЗначениеНеЗаполнено(СтрокаОстатков.ЕдиницаИзмерения) Тогда
				СтрокаТЧ.ЕдиницаИзмерения=СтрокаТЧ.Номенклатура.ОсновнаяЕдиницаИзмерения;
				СтрокаТЧ.Количество=СтрокаОстатков.Количество*СтрокаТЧ.ЕдиницаИзмерения.Коэффициент;
			Иначе
				СтрокаТЧ.ЕдиницаИзмерения=СтрокаТЧ.ЕдиницаИзмерения;
				СтрокаТЧ.Количество=СтрокаОстатков.Количество;
			КонецЕсли; 
			СтрокаТЧ.Коэффициент=СтрокаТЧ.ЕдиницаИзмерения.Коэффициент;
			СтрокаТЧ.КоличествоБазовое=СтрокаТЧ.Количество;
			СтрокаТЧ.Ячейка=СтрокаОстатков.Ячейка;
			СтрокаТЧ.ХарактеристикаНоменклатуры=СтрокаОстатков.ХарактеристикаНоменклатуры;
		КонецЦикла; 
	КонецЕсли;
	
	Если Основание <> Неопределено Тогда
		Для Каждого СтрТовар Из Товары Цикл
			СтрТовар.ЕдиницаИзмеренияНовая = СтрТовар.ЕдиницаИзмерения;
			СтрТовар.КоличествоБазовоеНовое = СтрТовар.КоличествоБазовое;
			Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПереразмещениеТоваров") Тогда
				СтрокаОснования = Основание.Товары[Товары.Индекс(СтрТовар)];
				СтрТовар.Ячейка = СтрокаОснования.ЯчейкаНовая;
			КонецЕсли;
			Если ОбЗначениеНеЗаполнено(СтрТовар.ЯчейкаНовая) Тогда	СтрТовар.ЯчейкаНовая = СтрТовар.Ячейка КонецЕсли;		
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// ордерный склад
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиТоваровОрдерныйСклад ГДЕ Регистратор=&Ссылка";
		Выборка = Запрос.Выполнить();
		Если НЕ Выборка.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Выборка.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	// на всякий случай проверим тип склада, его могли поменять в самом справочнике
	Если СкладКомпании.ВидСклада <> Перечисления.ВидыСкладов.Ячеистый Тогда
		Отказ = Истина;
		РезультатОбработки = "Склад документа имеет вид """ + СкладКомпании.ВидСклада + """. Проведение возможно только для ""Ячеистого"" склада!";
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли; 
	Если СкладПолучатель.ВидСклада <> Перечисления.ВидыСкладов.Ячеистый Тогда
		Отказ = Истина;
		РезультатОбработки = "Склад получатель документа имеет вид """ + СкладПолучатель.ВидСклада + """. Проведение возможно только для ""Ячеистого"" склада!";
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли; 
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//расход
	СписокТоваров = ЭтотОбъект.Товары.ВыгрузитьКолонку("Номенклатура");
	НаборЗаписейОстатки = Движения.ОстаткиТоваровОрдерныйСклад;
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.ДокументОбъект  = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании   = СкладКомпании;
	Если ХозОперация=Справочники.ХозОперации.ПереразмещениеШин Тогда
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин = "ЗаявкаНаХранениеШин";
	Иначе
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин = Неопределено;
	КонецЕсли; 
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = Неопределено;
	НаборЗаписейОстатки.Приходовать     = Истина;
	НаборЗаписейОстатки.ПоБазовомуКоличеству = Истина;
	Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	
	// приход
	Запрос=Новый Запрос();
	ТекстЗапроса="ВЫБРАТЬ
	             |	ДокументТовары.Номенклатура КАК Номенклатура,
	             |	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	ДокументТовары.ЕдиницаИзмеренияНовая КАК ЕдиницаИзмерения,
	             |	ДокументТовары.ЯчейкаНовая КАК Ячейка,
	             |	ДокументТовары.ЗаявкаНаХранениеШин КАК ЗаявкаНаХранениеШин,
	             |	СУММА(ДокументТовары.КоличествоБазовоеНовое) КАК Количество
	             |ИЗ
	             |	Документ.ПереразмещениеТоваров.Товары КАК ДокументТовары
	             |ГДЕ
	             |	ДокументТовары.Ссылка = &Ссылка
	             |	И ДокументТовары.Номенклатура.ВидНоменклатуры <> &Услуга
	             |	И ДокументТовары.ЯчейкаНовая <> ДокументТовары.Ячейка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ДокументТовары.Номенклатура,
	             |	ДокументТовары.ХарактеристикаНоменклатуры,
	             |	ДокументТовары.ЕдиницаИзмеренияНовая,
	             |	ДокументТовары.ЯчейкаНовая,
	             |	ДокументТовары.ЗаявкаНаХранениеШин";
	
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
	
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.ДокументОбъект  = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании   = СкладПолучатель;
	НаборЗаписейОстатки.ЗаявкаНаХранениеШин = "ЗаявкаНаХранениеШин";
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = Запрос.Выполнить();
	НаборЗаписейОстатки.Приходовать     = Истина;
	НаборЗаписейОстатки.ПоБазовомуКоличеству = Истина;
    Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	ХранениеШинКомплектами=обПраво("ХранениеШинКомплектами",Права,,ЭтотОбъект);
	Если (НЕ Отказ) И (ХозОперация=Справочники.ХозОперации.ПереразмещениеШин) И (ХранениеШинКомплектами)Тогда
		//Контроль остатка шин на хранении
		НаборЗаписейОстатки.Записать();
		ТаблицаОстатковТоваров=РегистрыНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(
			Новый Граница(МоментВремени(),ВидГраницы.Включая),
			Новый Структура("ЗаявкаНаХранениеШин",Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(ДокументОснование)),
			"ЗаявкаНаХранениеШин,СкладКомпании,Номенклатура,Ячейка",
			"Количество"
		);
		ЗаголовокСообщения=Истина;
		СписокЗаявокНаХранениеШин=Новый СписокЗначений;
		ТаблицаОстатковПоЯчейкам=ТаблицаОстатковТоваров.Скопировать();
		ТаблицаОстатковПоЯчейкам.Свернуть("ЗаявкаНаХранениеШин,Ячейка","Количество");
		Для каждого СтрокаОстатков Из ТаблицаОстатковТоваров Цикл
			Если СписокЗаявокНаХранениеШин.НайтиПоЗначению(СтрокаОстатков.ЗаявкаНаХранениеШин)<>Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СписокЗаявокНаХранениеШин.Добавить(СтрокаОстатков.ЗаявкаНаХранениеШин);
			
			ЯчейкиЗаявкиНаХранениеШин=ТаблицаОстатковПоЯчейкам.НайтиСтроки(Новый Структура("ЗаявкаНаХранениеШин",СтрокаОстатков.ЗаявкаНаХранениеШин));
			Если ЯчейкиЗаявкиНаХранениеШин.Количество()>1 Тогда
				ЯчейкиЗаявкиНаХранениеШин=ТаблицаОстатковТоваров.НайтиСтроки(Новый Структура("ЗаявкаНаХранениеШин",СтрокаОстатков.ЗаявкаНаХранениеШин));
				Если ЗаголовокСообщения Тогда
					дкСообщитьРезультат("Хранение шин допускается только покомплектно.","Важное&Остатки товаров ордерного склада:",ЭтотОбъект,Неопределено);
					ЗаголовокСообщения=Ложь;
				КонецЕсли; 
				Для каждого СтрокаОстатковЗаявкиНаХранениеШин Из ЯчейкиЗаявкиНаХранениеШин Цикл
					СтрРезультат = ""+СтрокаОстатковЗаявкиНаХранениеШин.ЗаявкаНаХранениеШин+": склад """+СтрокаОстатковЗаявкиНаХранениеШин.СкладКомпании+""". Остаток";
					Если СтрокаОстатковЗаявкиНаХранениеШин.СкладКомпании.ВидСклада=Перечисления.ВидыСкладов.Ячеистый Тогда
						СтрРезультат=СтрРезультат+" в ячейке """+СокрЛП(СтрокаОстатковЗаявкиНаХранениеШин.Ячейка)+"""";
					КонецЕсли; 
					СтрРезультат=СтрРезультат+" в количестве "+Формат(СтрокаОстатковЗаявкиНаХранениеШин.Количество,"ЧДЦ=0; ЧН=0")+".";
					дкСообщитьРезультат(СтрРезультат,"Важное&Остатки товаров ордерного склада:",ЭтотОбъект,Неопределено);
				КонецЦикла; 
				Отказ=Истина;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	// двигаем границу последовательности ордерного склада
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		НаборЗаписейГраницыОрдерныйСклад = РегистрыСведений.ГраницыОрдерныйСклад.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаСкладов = Движения.ОстаткиТоваровОрдерныйСклад.Выгрузить();
			ТаблицаСкладов.Свернуть("СкладКомпании");

			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= ТаблицаСкладов.ВыгрузитьКолонку("СкладКомпании");;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= Дата;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыОрдерныйСклад.ДокументСсылка	= Ссылка;
		НаборЗаписейГраницыОрдерныйСклад.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

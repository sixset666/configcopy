// Модуль документа Чек

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем СообщениеОбОшибке Экспорт;	// Переменная для накопления результатов записи. Если = неопределено, то вывод будет на экран, иначе тут сообщения об ошибках.
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОтключитьПроверкиПередЗаписью Экспорт; // Флаг отключения проверок (используется при программном формировании документа)
Перем КонтролироватьЗаполнениеОплат;// Вспомогательная переменная определяющая необходимость выполнения контроля оплат документа
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.

Перем ТекущийРежимПроведения;
									 
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("МестоРазмещения",3);
	ОбязательныеТовары.Вставить("Набор",2);
	//Если обПраво("ЗапретитьПодарки",Права) Тогда
	//	ОбязательныеТовары.Вставить("СуммаВсего",1);
	//КонецЕсли;
	
	// Обязательные поля таблицы оплат
	ОбязательныеОплаты=Новый Структура();
	ОбязательныеОплаты.Вставить("ТипОплаты",3);
	ОбязательныеОплаты.Вставить("ТипПлатежнойКарты",2);
	ОбязательныеОплаты.Вставить("Карточка",2);
	ОбязательныеОплаты.Вставить("Контрагент",2);
	ОбязательныеОплаты.Вставить("ДоговорВзаиморасчетов",2);
	//ОбязательныеОплаты.Вставить("Сумма",1);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("КассаККМ",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Оплаты",ОбязательныеОплаты);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// ИЗМЕНЕНО Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//  ПроверитьОстатки - булево, флаг проверки остатков по складу.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина, ПроверитьОстатки = Ложь) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();  			
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаККМ");
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	// Если задача контроля заполнения не стоит, выходим
	Если НЕ Заполнение Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Проконтролируем корректность заполнения фискальных реквизитов документа
	Если НЕ(обЗначениеНеЗаполнено(ФР.КлассОборудования) ИЛИ ФР.КлассОборудования=Перечисления.КлассыОборудования.ФР) Тогда
		дкДобавитьОшибку(Ошибки, "Реквизит <ФР> класс оборудования должен быть фискальным регистратором !");
		Результат = ЛОЖЬ;
	КонецЕсли;
	
	// Проводим инициализацию основных 
	ОплатыВсего = Оплаты.Итог("Сумма");
	ОплатыТалон = 0;
	ОплатыБезнал= 0;
	СтрокаТалон = Неопределено;
	
	// Выполним проверку корректности заполнения реквизит таблицы оплат
	Для Каждого ТекОплата Из Оплаты Цикл
		Если НЕ ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Наличные Тогда
			ОплатыБезнал = ОплатыБезнал + ТекОплата.Сумма;
		КонецЕсли;
		
		Если ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Безналичные Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.ТипПлатежнойКарты) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Тип платежной карты> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Кредит Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если НЕ ТекОплата.ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.Кредит Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> вид договора оплаты не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Талон Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если НЕ ТекОплата.Карточка.ВидКарточки=Перечисления.ВидыКарточек.Талон Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> вид карточки не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			ОплатыТалон = ОплатыТалон + ТекОплата.Сумма;
			СтрокаТалон = ТекОплата;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.КлубнаяКарта Тогда
			Если НЕ ТекОплата.Карточка.ВидКарточки=Перечисления.ВидыКарточек.КлубнаяКарта Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> вид карточки не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Проводим контроль сумм оплат
	Если (ХозОперация=Справочники.ХозОперации.ЧекОтложенный) ИЛИ (НЕ КонтролироватьЗаполнениеОплат) Тогда
		// Проверку корректности сумм оплат производить сейчас не нужно
		
	ИначеЕсли СуммаДокумента>ОплатыВсего Тогда
		дкДобавитьОшибку(Ошибки, "Общая сумма оплат не может быть менее суммы документа !");
		Результат = ЛОЖЬ;
		
	ИначеЕсли СуммаДокумента<ОплатыВсего Тогда
		// Определим причину превышения оплаты
		Если ОплатыВсего=ОплатыТалон Тогда
			Если (ОплатыВсего-СуммаДокумента)>=СтрокаТалон.Карточка.Номинал Тогда
				дкДобавитьОшибку(Ошибки, "Сумма оплаты талонами, превышает необходимую !");
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли (НЕ ОплатыБезнал=ОплатыТалон) И СуммаДокумента<ОплатыБезнал Тогда
			дкДобавитьОшибку(Ошибки, "Сумма безналичной оплаты не может превышать сумму документа !");
			Результат = ЛОЖЬ;
			
		ИначеЕсли (ПолученоБезнал=СуммаДокумента) И (ПолученоНал>0) Тогда
			дкДобавитьОшибку(Ошибки, "Сумма безналичной оплаты полностью закрывает сумму чека. Оплата наличными не допустима !");
			Результат = ЛОЖЬ;
			
		Иначе
			дкДобавитьОшибку(Ошибки, "Общая сумма оплат не может быть больше суммы документа !");
			Результат = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
	Если ПроверитьОстатки И ХозОперация=Справочники.ХозОперации.Чек И обПраво("РазрешитьОтрицательныеСкладскиеОстатки", Права) = Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены Тогда
		
		ТаблицаБезХарактеристик = Товары.ВыгрузитьКолонки("МестоРазмещения, Номенклатура");
		ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		МассивРежимовСписания = Новый Массив;
		МассивРежимовСписания.Добавить(Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание);
		МассивРежимовСписания.Добавить(Перечисления.РежимыАвтоСписанияХарактеристик.ПустаяСсылка());
		
		МассивСкладов       = Новый Массив;
		МассивТоваров       = Новый Массив;
		МассивХарактеристик = Новый Массив;
		МассивСкладовАвтоСписание = Новый Массив;
		МассивТоваровАвтоСписание = Новый Массив;
		Для Каждого ТекЭлемент Из Товары Цикл
			Если ТекЭлемент.ХарактеристикаНоменклатуры = ПустаяХарактеристика И 
				МассивРежимовСписания.Найти(ТекЭлемент.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик) = Неопределено Тогда
				Если МассивТоваровАвтоСписание.Найти(ТекЭлемент.Номенклатура) = Неопределено Тогда
					МассивТоваровАвтоСписание.Добавить(ТекЭлемент.Номенклатура);
				КонецЕсли;
				Если МассивСкладовАвтоСписание.Найти(ТекЭлемент.МестоРазмещения) = Неопределено Тогда
					МассивСкладовАвтоСписание.Добавить(ТекЭлемент.МестоРазмещения);
				КонецЕсли;
			Иначе
				Если МассивТоваров.Найти(ТекЭлемент.Номенклатура) = Неопределено Тогда
					МассивТоваров.Добавить(ТекЭлемент.Номенклатура);
				КонецЕсли;
				Если МассивСкладов.Найти(ТекЭлемент.МестоРазмещения) = Неопределено Тогда
					МассивСкладов.Добавить(ТекЭлемент.МестоРазмещения);
				КонецЕсли;
				Если МассивХарактеристик.Найти(ТекЭлемент.ХарактеристикаНоменклатуры) = Неопределено Тогда
					МассивХарактеристик.Добавить(ТекЭлемент.ХарактеристикаНоменклатуры);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ЕстьАвтоСписаниеХарактеристик = МассивТоваровАвтоСписание.Количество()>0;
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток
		|	КОНЕЦ КАК Остаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Момент, СкладКомпании В (&СкладКомпании) И Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки" + ?(ЕстьАвтоСписаниеХарактеристик, "
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.СкладКомпании,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
		|	Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Момент, СкладКомпании В (&СкладКомпанииАвтоСписание) И Номенклатура В (&НоменклатураАвтоСписание)) КАК ОстаткиТоваровКомпанииОстатки", "");
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Момент", МоментВремени());
		Запрос.УстановитьПараметр("ТаблицаТовары", Товары);
		Запрос.УстановитьПараметр("СкладКомпании",              МассивСкладов);
		Запрос.УстановитьПараметр("Номенклатура",               МассивТоваров);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", МассивХарактеристик);
		Запрос.УстановитьПараметр("СкладКомпанииАвтоСписание",  МассивСкладовАвтоСписание);
		Запрос.УстановитьПараметр("НоменклатураАвтоСписание",   МассивТоваровАвтоСписание);
		
		Выборка = Запрос.Выполнить().Выбрать();
		СтруктураОтбора = Новый Структура;
		
		ВидНоменклатурыУслуги = Перечисления.ВидыНоменклатуры.Услуга;
		Для Каждого СтрокаТоваров Из Товары Цикл
			Если СтрокаТоваров.Номенклатура.ВидНоменклатуры = ВидНоменклатурыУслуги Тогда
				Продолжить;
			КонецЕсли;
			Выборка.Сбросить();
			СтруктураОтбора.Вставить("Номенклатура",               СтрокаТоваров.Номенклатура);
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТоваров.ХарактеристикаНоменклатуры);
			СтруктураОтбора.Вставить("СкладКомпании",              СтрокаТоваров.МестоРазмещения);
			Если Выборка.НайтиСледующий(СтруктураОтбора) Тогда
				Остаток = Окр(Выборка.Остаток/СтрокаТоваров.Коэффициент, 3);
			Иначе
				Остаток = 0;
			КонецЕсли;
			Количество = СтрокаТоваров.Количество;
			Если Количество > Остаток Тогда
				//Рсход по складу превышает остаток
				Если обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры) Тогда
					ТекстОшибки = "["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""". Остаток "+СокрЛП(Остаток)+" "+СокрЛП(СтрокаТоваров.ЕдиницаИзмерения)+". Списывается "+СокрЛП(Количество)+" "+СокрЛП(СтрокаТоваров.ЕдиницаИзмерения)+". Превышение "+СокрЛП(Количество-Остаток)+" "+СокрЛП(СтрокаТоваров.ЕдиницаИзмерения);
				Иначе
					ТекстОшибки = "["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТоваров.ХарактеристикаНоменклатуры)+""". Остаток "+СокрЛП(Остаток)+" "+СокрЛП(СтрокаТоваров.ЕдиницаИзмерения)+". Списывается "+СокрЛП(Количество)+" "+СокрЛП(СтрокаТоваров.ЕдиницаИзмерения)+". Превышение "+СокрЛП(Количество-Остаток)+" "+СокрЛП(СтрокаТоваров.ЕдиницаИзмерения);
				КонецЕсли;
				дкДобавитьОшибку(Ошибки, ТекстОшибки);
				Результат = ЛОЖЬ;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
		дкДобавитьОшибку(Ошибки, ТекстОшибки);
		Результат = Ложь;
	КонецЕсли;
 
	Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
		дкДобавитьОшибку(Ошибки, ТекстОшибки);
		Результат = Ложь;
	КонецЕсли;
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки, (ХозОперация=Справочники.ХозОперации.ЧекНаВозврат ИЛИ ХозОперация = Справочники.ХозОперации.ЧекОтложенный));
	
	Если НЕ ХозОперация = Справочники.ХозОперации.ЧекНаВозврат
		И НЕ ХозОперация = Справочники.ХозОперации.ЧекОтложенный Тогда
		
		СписокСостоянийВводаВОборот = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
		
		ТаблицаРазбораМаркировки = Новый ТаблицаЗначений;
		ТаблицаРазбораМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
		ТаблицаРазбораМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
		ТаблицаРазбораМаркировки.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)));
		
		ОбъектШК = Обработки.ШтрихКоды.Создать();
		
		// Разберем маркировку на состовляющие для поиска
		Для Каждого ТекущаяСтрока Из КодыМаркировки Цикл
			
			СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
			
			Если НЕ СтруктураМаркировки.Разобран Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРазбораМаркировки.Добавить();
			НоваяСтрока.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
			НоваяСтрока.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
			НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировки;
			
		КонецЦикла;
		
		// Получим текущие статусы маркировки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	СписокКодовМаркировок.GTIN КАК GTIN,
		               |	СписокКодовМаркировок.СерийныйНомер КАК СерийныйНомер,
		               |	СписокКодовМаркировок.КодМаркировки КАК КодМаркировки
		               |ПОМЕСТИТЬ ТаблицаМаркировок
		               |ИЗ
		               |	&СписокКодовМаркировок КАК СписокКодовМаркировок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	СостоянияКодовМаркировкиСрезПоследних.Период КАК Период,
		               |	СостоянияКодовМаркировкиСрезПоследних.Организация.ИНН КАК ИННОрганизации,
		               |	СостоянияКодовМаркировкиСрезПоследних.Номенклатура КАК Номенклатура,
		               |	СостоянияКодовМаркировкиСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ТаблицаМаркировок.КодМаркировки КАК КодМаркировки,
		               |	СостоянияКодовМаркировкиСрезПоследних.Состояние КАК Состояние
		               |ПОМЕСТИТЬ ТаблицаСостоянийКодовМаркировок
		               |ИЗ
		               |	ТаблицаМаркировок КАК ТаблицаМаркировок
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияКодовМаркировки.СрезПоследних(
		               |				&МоментВремени,
		               |				(GTIN, СерийныйНомер) В
		               |					(ВЫБРАТЬ
		               |						ТаблицаМаркировок.GTIN КАК GTIN,
		               |						ТаблицаМаркировок.СерийныйНомер КАК СерийныйНомер
		               |					ИЗ
		               |						ТаблицаМаркировок КАК ТаблицаМаркировок)) КАК СостоянияКодовМаркировкиСрезПоследних
		               |		ПО ТаблицаМаркировок.GTIN = СостоянияКодовМаркировкиСрезПоследних.GTIN
		               |			И ТаблицаМаркировок.СерийныйНомер = СостоянияКодовМаркировкиСрезПоследних.СерийныйНомер
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки КАК КодМаркировки,
		               |	МАКСИМУМ(ТаблицаСостоянийКодовМаркировок.Период) КАК Период
		               |ПОМЕСТИТЬ ТаблицаПоследнегоПериода
		               |ИЗ
		               |	ТаблицаСостоянийКодовМаркировок КАК ТаблицаСостоянийКодовМаркировок
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТаблицаСостоянийКодовМаркировок.ИННОрганизации КАК ИННОрганизации,
		               |	ТаблицаСостоянийКодовМаркировок.Номенклатура КАК Номенклатура,
		               |	ТаблицаСостоянийКодовМаркировок.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки КАК КодМаркировки,
		               |	ТаблицаСостоянийКодовМаркировок.Состояние КАК Состояние
		               |ИЗ
		               |	ТаблицаСостоянийКодовМаркировок КАК ТаблицаСостоянийКодовМаркировок
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоследнегоПериода КАК ТаблицаПоследнегоПериода
		               |		ПО ТаблицаСостоянийКодовМаркировок.Период = ТаблицаПоследнегоПериода.Период
		               |			И ТаблицаСостоянийКодовМаркировок.КодМаркировки = ТаблицаПоследнегоПериода.КодМаркировки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	МаркировкаТоваровВПроизводствеОстатки.ЗаказНаряд КАК ЗаказНаряд,
		               |	МаркировкаТоваровВПроизводствеОстатки.Номенклатура КАК Номенклатура,
		               |	МаркировкаТоваровВПроизводствеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	МаркировкаТоваровВПроизводствеОстатки.КоличествоОстаток КАК КоличествоОстаток,
		               |	ТаблицаМаркировок.КодМаркировки КАК КодМаркировки
		               |ИЗ
		               |	РегистрНакопления.МаркировкаТоваровВПроизводстве.Остатки(
		               |			&МоментВремени,
		               |			(GTIN, СерийныйНомер) В
		               |				(ВЫБРАТЬ
		               |					ТаблицаМаркировок.GTIN КАК GTIN,
		               |					ТаблицаМаркировок.СерийныйНомер КАК СерийныйНомер
		               |				ИЗ
		               |					ТаблицаМаркировок КАК ТаблицаМаркировок)) КАК МаркировкаТоваровВПроизводствеОстатки
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаМаркировок КАК ТаблицаМаркировок
		               |		ПО МаркировкаТоваровВПроизводствеОстатки.GTIN = ТаблицаМаркировок.GTIN
		               |			И МаркировкаТоваровВПроизводствеОстатки.СерийныйНомер = ТаблицаМаркировок.СерийныйНомер";
		Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("Документ", Ссылка);
		Запрос.УстановитьПараметр("КодыМаркировки", КодыМаркировки.ВыгрузитьКолонку("КодМаркировки"));
		Запрос.УстановитьПараметр("СписокКодовМаркировок", ТаблицаРазбораМаркировки);
		
		ПакетЗапросов = Запрос.ВыполнитьПакет();
		
		Выборка = ПакетЗапросов[3].Выбрать();
		
		ИННОрганизации = Организация.ИНН;
		Пока Выборка.Следующий() Цикл
			Если ИННОрганизации <> Выборка.ИННОрганизации Тогда
				//Если осуществляется снятие резерва, снятие резерва не должно превышать остаток резерва
				Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
					дкДобавитьОшибку(Ошибки, 
						"Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> принадлежит другой организации.");
				Иначе
					дкДобавитьОшибку(Ошибки, 
						"Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> принадлежит другой организации.");
				КонецЕсли;
				Результат = ЛОЖЬ;
			ИначеЕсли СписокСостоянийВводаВОборот.Найти(Выборка.Состояние) = Неопределено Тогда
				//Если осуществляется снятие резерва, снятие резерва не должно превышать остаток резерва
				Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
					дкДобавитьОшибку(Ошибки, 
						"Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> не был введен в оборот или был выведен из оборота ранее.");
				Иначе
					дкДобавитьОшибку(Ошибки,
						"Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> не был введен в оборот или был выведен из оборота ранее.");
				КонецЕсли;
				Результат = ЛОЖЬ;
			КонецЕсли;
		КонецЦикла;
		
		Выборка = ПакетЗапросов[4].Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КоличествоОстаток > 0 Тогда
				Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
					дкДобавитьОшибку(Ошибки,
						"Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+
							"> ранее перемещен в производство для заказ-наряда <"+СокрЛП(Выборка.ЗаказНаряд)+">");
				Иначе
					дкДобавитьОшибку(Ошибки,
						"Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+
							"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> ранее перемещен в производство для заказ-наряда <"+СокрЛП(Выборка.ЗаказНаряд)+">");
				КонецЕсли; 
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// Возвращаем результат проверки
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// УДАЛИТЬ!!! Контроль корректности фискальных реквизитов
//
// Параметры:
// Отказ - булево - Признак проведения документа.
//
Процедура КонтрольФискальныхРеквизитов(Отказ) Экспорт
	//Контроль фискальных реквизитов нужен только при проведении
	Если обЗначениеНеЗаполнено(ФР) Тогда
		СообщениеОбОшибке=СообщениеОбОшибке+"Реквизит <ФР> не заполнен !";
		дкСообщитьРезультат("Реквизит <ФР> не заполнен !",,ЭтотОбъект);
		Отказ=Истина;
	КонецЕсли;
	Если ФР.КлассОборудования<>Перечисления.КлассыОборудования.ФР Тогда		
		СообщениеОбОшибке=СообщениеОбОшибке+"Реквизит <ФР> должен быть фискальным регистратором !";
		дкСообщитьРезультат("Реквизит <ФР> должен быть фискальным регистратором",,ЭтотОбъект);
		Отказ=Истина;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ДатаФР) Тогда
		СообщениеОбОшибке=СообщениеОбОшибке+"Реквизит <Дата чека ФР> не заполнен !";
		дкСообщитьРезультат("Реквизит <Дата чека ФР> не заполнен !",,ЭтотОбъект);
		Отказ=Истина;
	КонецЕсли;
КонецПроцедуры // КонтрольФискальныхРеквизитов()

// УДАЛИТЬ!!! Контроль корректности реквизитов оплаты
//
// Параметры:
// Отказ - булево - Признак проведения документа.
//
Процедура КонтрольОплаты(Отказ) Экспорт
	Если ХозОперация=Справочники.ХозОперации.Чек Тогда	
		//Контроль сумм оплаты при продаже
		Если Оплаты.Итог("Сумма") > СуммаДокумента Тогда
			// Проверим , может разница на талонах
			СуммаТалон = 0;
			Для Каждого СтрокаОплаты Из Оплаты Цикл
				Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Талон Тогда
					СуммаТалон =  СуммаТалон+СтрокаОплаты.Сумма;
				КонецЕсли;
			КонецЦикла;
			Если (Оплаты.Итог("Сумма") - СуммаДокумента) <> (СуммаТалон - СуммаДокумента) Тогда
				СообщениеОбОшибке=СообщениеОбОшибке+"Сумма оплаты превышает сумму чека!";
				дкСообщитьРезультат("Сумма оплаты превышает сумму чека!",,ЭтотОбъект);
				Отказ=Истина;
			ИначеЕсли ((СуммаТалон - СуммаДокумента) / СтрокаОплаты.Карточка.Номинал) > 1 Тогда
				СообщениеОбОшибке=СообщениеОбОшибке+"Сумма оплаты талонами превышает необходимую!";
				дкСообщитьРезультат("Сумма оплаты талонами, превышает необходимую!",,ЭтотОбъект);
				Отказ=Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Оплаты.Итог("Сумма")<СуммаДокумента Тогда
			СообщениеОбОшибке=СообщениеОбОшибке+"Общая сумма оплаты не может быть менее суммы документа !";
			дкСообщитьРезультат("Общая сумма оплаты не может быть менее суммы документа !",,ЭтотОбъект);
			Отказ=Истина;
		КонецЕсли;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаВозврат Тогда
		ПолученоБезнал = 0;
		Для Каждого СтрокаОплат Из Оплаты Цикл
			Если СтрокаОплат.ТипОплаты.Объект <> Перечисления.ТипыОплатыВРознице.Наличные Тогда
				ПолученоБезнал = ПолученоБезнал + СтрокаОплат.Сумма;
			КонецЕсли;
		КонецЦикла;
				
		//Контроль сумм оплаты при возврате
		Если ПолученоБезнал>СуммаДокумента Тогда
			СообщениеОбОшибке=СообщениеОбОшибке+"Сумма безналичной оплаты не может превышать сумму документа !";
			дкСообщитьРезультат("Сумма безналичной оплаты не может превышать сумму документа !",,ЭтотОбъект);
			Отказ=Истина;
		ИначеЕсли Оплаты.Итог("Сумма")<>СуммаДокумента Тогда
			СообщениеОбОшибке=СообщениеОбОшибке+"Сумма возврата должна быть равна сумме документа !";
			дкСообщитьРезультат("Сумма возврата должна быть равна сумме документа !",,ЭтотОбъект);
			Отказ=Истина;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры // КонтрольОплаты()

// УДАЛИТЬ!!! Контроль строк с пустой суммой
//
// Параметры:
// Отказ - булево - Признак проведения документа.
//
Процедура КонтрольПустыхСтрок(Отказ) Экспорт
	Если обПраво("ЗапретитьПодарки",Права) Тогда
		Для Каждого СтрокаТоваров Из Товары Цикл
			Если СтрокаТоваров.СуммаВсего=0 Тогда
				СообщениеОбОшибке=СообщениеОбОшибке+"Строки с нулевой суммой запрещены ! Строка №"+СокрЛП(СтрокаТоваров.НомерСтроки);
				дкСообщитьРезультат("Строки с нулевой суммой запрещены ! Строка №"+СокрЛП(СтрокаТоваров.НомерСтроки),,ЭтотОбъект);
				Отказ=Истина;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры // КонтрольПустыхСтрок()

// УДАЛИТЬ!!! Контроль остатков товара
//
// Параметры:
// Отказ - булево - Признак проведения документа.
//
Процедура КонтрольОстатков(Отказ) Экспорт
	Если обПраво("РазрешитьОтрицательныеСкладскиеОстатки",Права)=Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены Тогда
		//Контроль остатков товара
		Если ХозОперация=Справочники.ХозОперации.Чек Тогда
			Запрос=Новый Запрос();
			ТекстЗапроса="
			|ВЫБРАТЬ
			|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
			|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
			|	ВЫБОР 
			|		КОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток ЕСТЬ NULL
			|		ТОГДА 0
			|		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
			|		КОНЕЦ
			|	-
			|	ВЫБОР 
			|		КОГДА ОстаткиТоваровКомпанииОстатки.РезервОстаток ЕСТЬ NULL
			|		ТОГДА 0
			|		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.РезервОстаток
			|		КОНЕЦ
			|	КАК Остаток
			|ИЗ 
			|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Момент,(Номенклатура В (&Номенклатура)) И (ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) И (СкладКомпании В (&СкладКомпании))) КАК ОстаткиТоваровКомпанииОстатки
			|";
			Запрос.Текст=ТекстЗапроса;
			Запрос.УстановитьПараметр("Момент",МоментВремени());
			Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
			Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
			Запрос.УстановитьПараметр("СкладКомпании",Товары.ВыгрузитьКолонку("МестоРазмещения"));
			Выборка=Запрос.Выполнить().Выбрать();
			СтруктураОтбора=Новый Структура;
			Для Каждого СтрокаТоваров Из Товары Цикл
				Если СтрокаТоваров.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
					Продолжить;
				КонецЕсли; 
				Выборка.Сбросить();
				СтруктураОтбора.Вставить("Номенклатура",СтрокаТоваров.Номенклатура);
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТоваров.ХарактеристикаНоменклатуры);
				СтруктураОтбора.Вставить("СкладКомпании",СтрокаТоваров.МестоРазмещения);
				Если Выборка.НайтиСледующий(СтруктураОтбора) Тогда
					Остаток=Выборка.Остаток;
				Иначе
					Остаток=0;
				КонецЕсли;
				Количество=СтрокаТоваров.Количество;
				Если Количество>Остаток Тогда
					//Расход по складу превышает остаток
					Если обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры) Тогда
						СтрРезультат="["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""". Остаток "+СокрЛП(Остаток)+" "+СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения)+". Списывается "+СокрЛП(Количество)+" "+СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения)+". Превышение "+СокрЛП(Количество-Остаток)+" "+СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения);
					Иначе
						СтрРезультат="["+СокрЛП(СтрокаТоваров.Номенклатура.Код)+"] Товар """+СокрЛП(СтрокаТоваров.Номенклатура)+""" с характеристикой """+СокрЛП(СтрокаТоваров.ХарактеристикаНоменклатуры)+""". Остаток "+СокрЛП(Остаток)+" "+СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения)+". Списывается "+СокрЛП(Количество)+" "+СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения)+". Превышение "+СокрЛП(Количество-Остаток)+" "+СокрЛП(СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения);
					КонецЕсли;
					дкСообщитьРезультат(СтрРезультат,,ЭтотОбъект);					
					Отказ=Истина;
					СообщениеОбОшибке=СообщениеОбОшибке+СтрРезультат;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры // КонтрольОстатков()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Чек","Чек",Истина);
	СписокХозОпераций.Добавить("ЧекНаВозврат","Чек на возврат");
	СписокХозОпераций.Добавить("ЧекОтложенный","Отложенный чек");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// обработаем бонусы при изменении ТЧ
	Если Найти(Имя, "Товары.") > 0 И Имя <> "Товары.СуммаНДС" И Имя <> "Товары.МестоРазмещения" Тогда
		Для Каждого Строка Из Товары Цикл
			Если НЕ (Имя = "Товары.СуммаВсего" И ТекСтрока = Строка) Тогда
				Строка.СуммаВсего = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
			КонецЕсли;
			Строка.СуммаСкидкиБонусами = 0;
		КонецЦикла;
		
		Если Имя = "Товары.Номенклатура" Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
				ТекСтрока.ПризнакПредметаРасчета = ТекСтрока.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
			Иначе
				ТекСтрока.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Карточка" Тогда
		Если НЕ Карточка.Пустая() Тогда
			Если (Карточка.ВидКарточки<>Перечисления.ВидыКарточек.ДисконтнаяКарта) Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранная карточка не является дисконтной!");
				#КонецЕсли
				Карточка=Неопределено;
				ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(Карточка.Объект) Тогда
				Если Контрагент<>Карточка.Объект Тогда
					//В документе выбран другой контрагент - заменяем на владельца карточки
					Контрагент=Карточка.Объект;
					ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
		
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		ОбработкаРеквизита("КоличествоКСписанию", ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Возврат Результат;
		
	ИначеЕсли Имя="Организация" Тогда

		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (НЕ Контрагент.Пустая()) И (НЕ Карточка.Пустая()) И ((Карточка.Объект<>Неопределено) И (НЕ Карточка.Объект.Пустая())) И (Контрагент<>Карточка.Объект)  Тогда
			Карточка=Неопределено;
			Возврат ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании"
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли НЕ СкладКомпании.Розничный Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранный склад: " + СкладКомпании + " не является розничным!
			|Выберите розничный склад",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="КассаККМ" Тогда
		#Если Клиент Тогда
		Рез=Истина;
		Если ЭтаФорма<>Неопределено Тогда
			Если КассаККМ.Организация<>Организация Тогда
				Если Вопрос("Организация кассы отлична от текущей организации документа. Установить организацию документа в соответствие с организацией кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					Организация=КассаККМ.Организация;
					Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.Подразделение<>ПодразделениеКомпании Тогда
				Если Вопрос("Подразделение кассы отлично от текущего подразделения документа. Установить подразделение документа в соответствии с подразделением кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ПодразделениеКомпании=КассаККМ.Подразделение;
					Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента Тогда
				Если Вопрос("Валюта кассы отлична от текущей валюты документа. Установить валюту документа в соответствие с валютой кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					// Выполним пересчет суммовых показателей в табличных частях
					Если Вопрос("Были изменены данные влияющие на суммовые показатели документа !"+Символы.ПС+"Выполнить пересчет ?", РежимДиалогаВопрос.ДаНет, обПраво("ТаймаутДиалогов",глПрава), КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
						// Определим курс новой валюты документа
						стКурс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(МоментВремени(), Новый Структура("Валюта",КассаККМ.ВалютаДенежныхСредств));
						КурсВалютаДенежныхСредств = стКурс.Курс / ?(стКурс.Кратность=0, 1, стКурс.Кратность);
						
						// Определим коэффициент пересчета суммовых показателей
						КоэффициентПересчета = обПересчет(1, ВалютаДокумента, КурсДокумента, КассаККМ.ВалютаДенежныхСредств, КурсВалютаДенежныхСредств);
						
						// Обработаем каждую строку табличных частей "Товары" и "Оплаты"
						Для Каждого ТекСтрока Из Товары Цикл
							ТекСтрока.Цена = ТекСтрока.Цена * КоэффициентПересчета;
							ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма, ДопПараметры);
						КонецЦикла;
						Для Каждого ТекСтрока Из Оплаты Цикл
							ТекСтрока.Сумма = ТекСтрока.Сумма * КоэффициентПересчета;
						КонецЦикла;
					КонецЕсли;
					
					// Установим новое значение валюты и курса документа
					ВалютаДокумента = КассаККМ.ВалютаДенежныхСредств;
					КурсДокумента   = КурсВалютаДенежныхСредств;
					Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли;	
					Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма,ДопПараметры) И Рез;
					
					// Обновляем информацию о валюте и сумме документа
					дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				КонецЕсли; 
			КонецЕсли; 
		Иначе
			Организация=КассаККМ.Организация;
			Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
			ПодразделениеКомпании=КассаККМ.Подразделение;
			Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
			ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
			Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		КонецЕсли; 
		#Иначе
		Организация=КассаККМ.Организация;
		Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
		ПодразделениеКомпании=КассаККМ.Подразделение;
		Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
		ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
		Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ФР" Тогда
		Если НЕ обЗначениеНеЗаполнено(ФР) Тогда
			Если ФР.КлассОборудования<>Перечисления.КлассыОборудования.ФР Тогда
				#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Предупреждение("Выбранное оборудование: " + ФР + " не является фискальным регистратором!
					|Выберите фискальный регистратор",10);
				КонецЕсли; 
				#КонецЕсли
				ФР=Неопределено;
				Возврат ОбработкаРеквизита("ФР",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="ВидОплаты" Тогда
		Если ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет И ПолученоБезнал<>0 Тогда
			ПолученоБезнал=0;
			Рез=ОбработкаРеквизита("ПолученоБезнал",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет И ПолученоНал<>0 Тогда
			ПолученоНал=0;
			Возврат ОбработкаРеквизита("ПолученоНал",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="ПолученоНал" ИЛИ Имя="ПолученоБезнал" Тогда
		ЕстьНал=(ПолученоНал>0);
		ЕстьБезнал=(ПолученоБезнал>0);
		Если (ЕстьНал) И (НЕ ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.НаличныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (НЕ ЕстьНал) И (ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.БезналичныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (ЕстьНал) И (ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.ПроизвольнаяОплата) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (НЕ ЕстьНал) И (НЕ ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.НаличныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Истина;
	ИначеЕсли Имя = "КоличествоКСписанию" Тогда
		
		// сравним бонусные баллы с суммой всего
		Если НЕ БонусныеПрограммыСервер.БонуснаяПрограммаАктивна(Карточка.БонуснаяПрограмма, Дата) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если ТипЗнч(ДопПараметры) = Тип("Структура") И ДопПараметры.Свойство("ПроверятьБлокировкуКарты")
			И БонусныеПрограммыСервер.ЗаблокированаКарточка(Карточка) Тогда
			КоличествоКСписанию = 0;
		КонецЕсли;
		
		КоличествоКСписанию = Мин(КоличествоКСписанию,
								  БонусныеПрограммыСервер.КоличествоБалловВСуммеДокумента(ЭтотОбъект, Карточка.БонуснаяПрограмма),
								  БонусныеПрограммыСервер.МаксимальноеКоличетсвоБоллов(ЭтотОбъект, Карточка.БонуснаяПрограмма));
		
		// расписываем сумму по строкам документа документам
		СводнаяТаблица = СформироватьСводнуюТаблицу();
		
		БонусныеПрограммыСервер.РаспределитьСуммуПоТаблице(СводнаяТаблица, БонусныеПрограммыСервер.БаллыВВалюту(КоличествоКСписанию, Карточка.БонуснаяПрограмма, ВалютаДокумента, Дата), "Сумма");
		
		Для Каждого Строка Из СводнаяТаблица Цикл
			Строка.Строка.СуммаВсего          = Строка.Сумма;
			Строка.Строка.СуммаНДС            = Строка.Строка.СуммаВсего*Строка.Строка.СтавкаНДС.Ставка/(100+Строка.Строка.СтавкаНДС.Ставка);
			Строка.Строка.СуммаСкидкиБонусами = Строка.РаспределенноеЗначение;
		КонецЦикла;
		
		БонусныеПрограммыСервер.РасчитатьБонусныеБаллыКНачислению(ЭтотОбъект, Карточка.БонуснаяПрограмма);
		
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.МестоРазмещения" Тогда
		Если ТекСтрока.МестоРазмещения.Пустая() Тогда
		ИначеЕсли НЕ ТекСтрока.МестоРазмещения.Розничный Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранный отдел: " + ТекСтрока.МестоРазмещения + " не является розничным складом!
			|Выберите розничный склад",10);
			#КонецЕсли
			ТекСтрока.МестоРазмещения = Неопределено;
			ОбработкаРеквизита("Товары.МестоРазмещения",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Лев(Имя,7)="Товары." Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку		
		Если обЗначениеНеЗаполнено(ТекСтрока.МестоРазмещения) Тогда
			ТекСтрока.МестоРазмещения=СкладКомпании;
			Рез=ОбработкаРеквизита("Товары.МестоРазмещения",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	Иначе
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если Имя = "Дата" ИЛИ Имя = "ВалютаДокумента" Тогда
			ОбработкаРеквизита("КоличествоКСписанию", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

Функция СформироватьСводнуюТаблицу()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Строка");
	Таблица.Колонки.Добавить("Таблица");
	Таблица.Колонки.Добавить("Сумма");
	Таблица.Колонки.Добавить("РаспределенноеЗначение");
	
	Для Каждого Строка Из Товары Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Строка                 = Строка;
		НоваяСтрока.Таблица                = "Товары";
		НоваяСтрока.Сумма                  = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
		НоваяСтрока.РаспределенноеЗначение = 0;
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ТОРГ-12"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Дляпечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.ПодразделениеКомпании, СтруктураПредставления2, ДополнительныеПараметры);
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикОрганизация",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	// в новой ПФ не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	
	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		
	
	ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 

	ВыборкаСтрок = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();

	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для Каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,Права);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,, ЭтотОбъект);
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		
		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС    = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", Формат(СуммаНДС, ФорматВыводаСуммы));
		ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьВсего);
			//мсвДопОбластиПодвала.Добавить(ОбластьВсегоСтавкаНДС);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		Если КоличествоСтрокНаТекущейСтранице=0 Тогда
			ТабДокумент.Вывести(ОбластьСтрока);
		Иначе
			//выводим строку, делая проверку попадания на лист		
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
				НомерСтраницы, СтруктураИтоговПоСтранице, Права, мсвДопОбластиПодвала);
		КонецЕсли; 
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.Количество;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			КоличествоСтрокНаТекущейСтранице=0;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Иначе
			КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,Права);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = Формат(ВыборкаСтрок.Итог("Количество"), ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "+дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьВсегоСтавкаНДС, , , НомерСтраницы,,Права);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли;
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,Права);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьТОРГ12()

// Формирует печатную форму "Чек"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//  ФронтКассира - Обработка ФронтКассира или Документ Чек. (объект, который следует напечатать)
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЧек(ТабДокумент,ФронтКассира=Неопределено) Экспорт
	// В переменную ФронтКассира может быть передан или сам объект-фронт, если это непробитый чек, или документ "Чек",
	// если чек уже пробит. Определим что нам передали и получим необходимые данные для дальнейшей подстановки в макет.
	Если ФронтКассира = Неопределено Тогда
		// Ничего не передали. Печатаем этот объект.
		ДокументДляПечати = ЭтотОбъект;
		НомерДляПечати = дкПолучитьНомерДляПечати(ДокументДляПечати);
	Иначе
		ДокументДляПечати = ФронтКассира;
		Если ТипЗнч(ФронтКассира) = Тип("ДокументСсылка.Чек") Тогда
			// Это уже пробитый чек.
			НомерДляПечати = дкПолучитьНомерДляПечати(ДокументДляПечати);
		Иначе
			// непробитый чек. передали фронт.
			// номера нет.
			НомерДляПечати = "";
		КонецЕсли;
	КонецЕсли;
	
	Макет = ПолучитьМакет("Чек");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы	= дкПривестиМакетПечатнойФормы(ДокументДляПечати,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ДокументДляПечати);
	ТекстЗаголовка = ХозОперация.Наименование+" № "+НомерДляПечати+" от "+Формат(ДокументДляПечати.Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(ДокументДляПечати.Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.Организация				= ДокументДляПечати.Организация;
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(ДокументДляПечати.Контрагент);
	ОбластьМакета.Параметры.Контрагент				= ДокументДляПечати.Контрагент;
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(ДокументДляПечати.СкладКомпании);
	ОбластьМакета.Параметры.СкладКомпании			= ДокументДляПечати.СкладКомпании;
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ДокументДляПечати);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ДокументДляПечати.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ДокументДляПечати.Товары.Итог("СуммаСкидки") > 0 ИЛИ ДокументДляПечати.Товары.Итог("СуммаСкидкиБонусами") > 0 Тогда
		СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки") + ДокументДляПечати.Товары.Итог("СуммаСкидкиБонусами");
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ДокументДляПечати.ВалютаДокумента);
	
	// сформируем информацию о бонусных баллах
	ТекстБаллы = "";
	Если ДокументДляПечати.КоличествоКСписанию > 0 Тогда
		ТекстБаллы = ТекстБаллы + "Для оплаты использованы бонусные баллы в количестве " + ДокументДляПечати.КоличествоКСписанию +
		" на сумму "+(ДокументДляПечати.Товары.Итог("СуммаСкидкиБонусами")) + " " + ДокументДляПечати.ВалютаДокумента +".";
	КонецЕсли;
	
	Если ДокументДляПечати.КоличествоКНачислению > 0 Тогда
		ТекстБаллы = ТекстБаллы +?(ПустаяСтрока(ТекстБаллы), "", " ")+ "Было начислено "+ ДокументДляПечати.КоличествоКНачислению +" бонусных баллов на сумму в " +
		ДокументДляПечати.КоличествоКНачислению*ДокументДляПечати.Карточка.БонуснаяПрограмма.КратностьБонусов + " "+ ДокументДляПечати.Карточка.БонуснаяПрограмма.ВалютаБонуса+".";
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстБаллы) Тогда
		ТекОбласть = ОбластьПодвал.Область(5, , 5, );
		ТекОбласть.АвтовысотаСтроки = Ложь;
		ТекОбласть.ВысотаСтроки = 1;
		ОбластьПодвал.Область(5, 2, 5, 2).Текст = "";
	Иначе
		ОбластьПодвал.Параметры.БонусныеБаллы = ТекстБаллы;
	КонецЕсли;
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЧек()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если (НЕ СкладКомпании.Пустая()) И (НЕ СкладКомпании.Розничный) Тогда
		СкладКомпании = Неопределено;
		Модифицированность=Ложь;
	КонецЕсли;
	
	// Производим установку реквизита "Касса ККМ" в значение по умолчанию
	Если обЗначениеНеЗаполнено(КассаККМ) Тогда
		КассаККМ = ПараметрыСеанса.Компьютер.КассаККМ;
	КонецЕсли;
	
	// Определяем хоз.операцию нового документа
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.Чек") И ДокументОснование.ХозОперация=Справочники.ХозОперации.Чек Тогда
		ХозОперация = Справочники.ХозОперации.ЧекНаВозврат;
		КоличествоКСписанию = 0; КоличествоКНачислению = 0;
		
		Товары.Очистить();
		
		Для Каждого Товар Из Основание.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Товар,, "СуммаСкидкиБонусами");
			
			ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
			НоваяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЦикла;
		
		КодыМаркировки.Очистить();
		
	КонецЕсли;
	
	// Для нового документа нельзя заполнять данные ФР
	Если ЭтоНовый() Тогда
		Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	дкПриКопировании(ЭтотОбъект, ОбъектКопирования);
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	//Заблокируем проведение чека, если ХО = отложенный чек
	Если ХозОперация=Справочники.ХозОперации.ЧекОтложенный Тогда
		Если РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
			РежимЗаписи=РежимЗаписиДокумента.Запись;
		КонецЕсли;
	КонецЕсли;
	
	// Сохранение текущего режима проведения. Пригодится в контроле остатков.
	ТекущийРежимПроведения = РежимПроведения;
	
	// Установим признак необходимости проведения контроля оплат
	КонтролироватьЗаполнениеОплат = (РежимЗаписи=РежимЗаписиДокумента.Проведение);
	
	// Стандартный обработчик	
	Если ОтключитьПроверкиПередЗаписью Тогда
		// подкорректируем право
		Права[обПолучитьСсылкуПВХПравИНастроек("РедактированиеДокументовПриНаличииПодчиненных")] = Истина;
		дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , Ложь, Ложь, "00010");
		// Уберем не нужные проверки при проведении если запись из Фронта.
		Права[обПолучитьСсылкуПВХПравИНастроек("РазрешитьОтрицательныеСкладскиеОстатки")] = Перечисления.ВидыРазрешенныхОтрицательныхОстатков.ПоОстаткам;
	Иначе
		дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	
	Если БонусныеПрограммыСервер.ЗаблокированаКарточка(Карточка)
		И (ДополнительныеСвойства.Свойство("ПроверятьБлокировкуКарты") ИЛИ (КоличествоКНачислению = 0 И ЗначениеЗаполнено(Ссылка))) Тогда
		КоличествоКНачислению = 0;
	Иначе
		БонусныеПрограммыСервер.РасчитатьБонусныеБаллыКНачислению(ЭтотОбъект, Карточка.БонуснаяПрограмма);
	КонецЕсли;
	
	КонтролироватьЗаполнениеОплат = ИСТИНА;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	дкПриЗаписи(ЭтотОбъект, Отказ);
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	дкПередУдалением(ЭтотОбъект, Отказ);
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
	//Удалим накопление сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.ОтменаПроведения();
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
	// Для начала необходимо получить записи регистра для данного документа.
	ПроведениеПослеЗагрузки = Неопределено;
	Если НЕ ДополнительныеСвойства.Свойство("ПроведениеПослеЗагрузки", ПроведениеПослеЗагрузки) Тогда
		ПроведениеПослеЗагрузки = Ложь;
	КонецЕсли;
	
	Если НЕ ПроведениеПослеЗагрузки Тогда
		
		// Чек на возврат при распроведении - уменьшает количество товара на складе, простой чек - увеличивает.
		МножительКоличества = ?(ХозОперация = Справочники.ХозОперации.ЧекНаВозврат, -1 , 1);
		
	КонецЕсли;

КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим);
	
	// определим способ ведения баланса
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проведем остатки товаров
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейОстатки.СкладКомпании=Неопределено;
	НаборЗаписейОстатки.Приходовать=Истина;
	НаборЗаписейОстатки.ПоБазовомуКоличеству=Ложь;
	НаборЗаписейОстатки.Резервировать=Ложь;
	НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная =  "Цена";
	НаборЗаписейОстатки.ИмяРеквизитаСуммаРозничная =  "СуммаВсего";
	НаборЗаписейОстатки.ДвиженияПоРознице=Истина;
	НаборЗаписейОстатки.РазрешитьПереоценку = НЕ (ХозОперация = Справочники.ХозОперации.ЧекНаВозврат);
	НаборЗаписейОстатки.СписыватьРозницуПоОстаткам=Истина;
	Если (ХозОперация=Справочники.ХозОперации.Чек) Тогда
		Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Иначе
		Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// ANDV Проведем деньги по кассе ККМ
   	НаборЗаписейКассыККМ=Движения.КассыККМ;
	СуммаОплатыПоКассе =0;
	Для Каждого СтрокаОплаты Из Оплаты Цикл
		НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
		НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
		НаборЗаписейКассыККМ.КассаККМ 			= КассаККМ;
		НаборЗаписейКассыККМ.СпособОплаты 		= СтрокаОплаты.ТипОплаты;
		Если (ХозОперация=Справочники.ХозОперации.Чек) Тогда
			НаборЗаписейКассыККМ.Сумма 			= СтрокаОплаты.Сумма;
		Иначе
			НаборЗаписейКассыККМ.Сумма			= -СтрокаОплаты.Сумма;
		КонецЕсли;
		Отказ=НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
		
		Если СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Наличные ИЛИ
			СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Электронно Тогда
			
			Если (ХозОперация=Справочники.ХозОперации.Чек) Тогда
				СуммаОплатыПоКассе = СуммаОплатыПоКассе+СтрокаОплаты.Сумма;
			Иначе
				СуммаОплатыПоКассе = СуммаОплатыПоКассе-СтрокаОплаты.Сумма;
			КонецЕсли;
			
		КонецЕсли;	 
	КонецЦикла;
	
	// подготовим таблицу движений в разрезе подразделений по взаиморасчетам
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений();
	ТаблицаВзаиморасчетов.Колонки.Добавить("Сумма");
	ОписаниеТипов=Новый ОписаниеТипов;
	ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение",ОписаниеТипов);
	
	// проведем взаиморасчеты
	Для Каждого СтрокаОплаты Из Оплаты Цикл
		Если (СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата ИЛИ
			 СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата) И
			 НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения Тогда
	        		
			НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
			НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
			НаборЗаписейВзаиморасчеты.Контрагент			= СтрокаОплаты.Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = СтрокаОплаты.ДоговорВзаиморасчетов;
			Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
				НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
			Иначе
				НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
			КонецЕсли; 
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
			Если (ХозОперация=Справочники.ХозОперации.Чек) Тогда
				НаборЗаписейВзаиморасчеты.Сумма = СтрокаОплаты.Сумма;
			Иначе
				НаборЗаписейВзаиморасчеты.Сумма = -СтрокаОплаты.Сумма;
				НаборЗаписейВзаиморасчеты.ПриходРасход=Ложь;
			КонецЕсли;
			//НаборЗаписейВзаиморасчеты.СделкиКонтрагента=Ложь;
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
			Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			
			// доходы и расходы по суммовым разницам
			СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
				Если ВедетсяБалансПоПодразделению Тогда
					НаборЗаписейДиР.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
					НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
				Иначе
					НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
			
			СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
			СтрокаВзаиморасчетов.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
			Если (ХозОперация=Справочники.ХозОперации.Чек) Тогда
				СтрокаВзаиморасчетов.Сумма = СтрокаОплаты.Сумма;
			Иначе
				СтрокаВзаиморасчетов.Сумма = -СтрокаОплаты.Сумма;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	//Проведем сумму выручки по доходам и расходам
	
	ТаблицаВзаиморасчетов.Свернуть("Подразделение","Сумма");
	// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
	Если ВедетсяБалансПоПодразделению Тогда
		Для Каждого СтрокаСписания Из ТаблицаВзаиморасчетов Цикл
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=СтрокаСписания.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.Доход=СтрокаСписания.Сумма;	
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЦикла;
	Иначе
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
		НаборЗаписейДоходыИРасходы.Доход=ТаблицаВзаиморасчетов.Итог("Сумма");	
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Если СуммаОплатыПоКассе<>0 Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение=КассаККМ.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Выручка;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
		НаборЗаписейДоходыИРасходы.Доход=СуммаОплатыПоКассе;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Если (НЕ обЗначениеНеЗаполнено(Контрагент)) ИЛИ (НЕ обЗначениеНеЗаполнено(Карточка)) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	СУММА(ЧекТовары.Количество * ЧекТовары.Коэффициент) КАК Количество
		                      |ИЗ
		                      |	Документ.Чек.Товары КАК ЧекТовары
		                      |ГДЕ
		                      |	ЧекТовары.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		КоличествоНоменклатуры = 0;
		Если Выборка.Следующий() Тогда
			Если ТипЗнч(Выборка.Количество) = Тип("Число") Тогда
				КоличествоНоменклатуры = Выборка.Количество;
			КонецЕсли;
		КонецЕсли;
		//Сформируем сумму накопления
		НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
		НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейНакоплениеСумм.Контрагент=Контрагент;
		НаборЗаписейНакоплениеСумм.КоличествоНоменклатуры = КоличествоНоменклатуры;
		НаборЗаписейНакоплениеСумм.Карточка=Карточка;
		НаборЗаписейНакоплениеСумм.Сторно=(ХозОперация=Справочники.ХозОперации.ЧекНаВозврат);
		Отказ=НЕ НаборЗаписейНакоплениеСумм.НакоплениеСуммы() ИЛИ Отказ;
	КонецЕсли; 
	
	Если НЕ Отказ Тогда
		
		ЭтоВозврат = (ХозОперация = Справочники.ХозОперации.ЧекНаВозврат);
		СостояниеКодаМаркировки = ?(ЭтоВозврат,
			Перечисления.СостоянияКодовМаркировки.ВведенВОборотПриВозврате,
			Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа);
		
		// Изменим состояние маркировки
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.ПроверятьВыводИзОборота = НЕ ЭтоВозврат;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = СостояниеКодаМаркировки;
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
		
	КонецЕсли;
	Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
		СообщениеОбОшибке=дкДобавитьОшибку(?(СообщениеОбОшибке=Неопределено,"",СообщениеОбОшибке), ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
 
	Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
		СообщениеОбОшибке=дкДобавитьОшибку(?(СообщениеОбОшибке=Неопределено,"",СообщениеОбОшибке), ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;

	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// бонусные баллы
	Если ХозОперация = Справочники.ХозОперации.Чек Тогда
		НаборБонусныеПрограммы = Движения.БонусныеБаллы;
		Если КоличествоКНачислению > 0 Тогда
			НаборБонусныеПрограммы.Карта             = Карточка;
			НаборБонусныеПрограммы.БонуснаяПрограмма = Карточка.БонуснаяПрограмма;
			НаборБонусныеПрограммы.ХозОперация       = ХозОперация;
			НаборБонусныеПрограммы.КоличетсвоБаллов  = КоличествоКНачислению;
			НаборБонусныеПрограммы.Регистратор       = Ссылка;
			НаборБонусныеПрограммы.ДокДата           = Дата;
			
			Отказ = НЕ НаборБонусныеПрограммы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		Если КоличествоКСписанию > 0 Тогда
			МаксимальноеКоличествоБаллов = БонусныеПрограммыСервер.МаксимальноеКоличетсвоБоллов(ЭтотОбъект, Карточка.БонуснаяПрограмма);
			Если КоличествоКСписанию > МаксимальноеКоличествоБаллов Тогда
				Отказ = Истина;
				ТекстСообщения = НСтр("ru = 'Максимальное количество бонусных баллов доступных к оплате %1 меньше указанных %2.'");
				ТекстСообщения = СтрЗаменить(СтрЗаменить(ТекстСообщения, "%1", МаксимальноеКоличествоБаллов), "%2", КоличествоКСписанию);
				Сообщить(ТекстСообщения, СтатусСообщения.Важное);
			КонецЕсли;
			
			НаборБонусныеПрограммы.Карта = Карточка;
			НаборБонусныеПрограммы.БонуснаяПрограмма = Карточка.БонуснаяПрограмма;
			НаборБонусныеПрограммы.ХозОперация       = ХозОперация;
			НаборБонусныеПрограммы.КоличетсвоБаллов  = КоличествоКСписанию;
			НаборБонусныеПрограммы.Регистратор       = Ссылка;
			НаборБонусныеПрограммы.ДокДата           = Дата;
			
			Отказ = НЕ НаборБонусныеПрограммы.Расход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	Если СообщениеОбОшибке=Неопределено Тогда
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	Иначе
		Если ТипЗнч(РезультатОбработки)=Тип("Строка") Тогда
			СообщениеОбОшибке=РезультатОбработки;
		Иначе
			Для Каждого СтрокаОшибки Из РезультатОбработки.Сообщения Цикл
				СообщениеОбОшибке=СообщениеОбОшибке+СтрокаОшибки.Сообщение+"
				|";
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;

ОтключитьПроверкиПередЗаписью = Ложь; // При необходимости переустанавливается в Истина из кода
КонтролироватьЗаполнениеОплат = ИСТИНА;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли

СообщениеОбОшибке=Неопределено;

ТекущийРежимПроведения = РежимПроведенияДокумента.Оперативный;
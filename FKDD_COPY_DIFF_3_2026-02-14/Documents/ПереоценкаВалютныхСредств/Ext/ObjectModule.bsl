// Модуль документа ПереоценкаВалютныхСредств

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем
	// только в случае если все нормально с остальными реквизитами
	// Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;	
	
	Возврат Результат;

КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПереоценкаВалютныхСредств","Переоценка валютных средств",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="" Тогда
		//Для приватной обработки изменения реквизитов
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		ДокументОснование=Основание;
		//Вычислим сумму
		СуммаДокумента=обПересчет(Основание.СуммаДокумента,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,Дата);
		//Статья ДДС - Оплата от покупателя
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаПоставщику;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Определим, ведется ли баланс по организациям.
	СпособВеденияБаланса          = Константы.СпособВеденияБаланса.Получить();
	БалансВедетсяПоОрганизациям   = (СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоОрганизации);
	БалансВедетсяПоПодразделениям = (СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// разбираемся с суммовыми разницами
	// Для списания курсовых и суммовых разниц задействуем таблицу значений.
	ТаблицаСуммовыхИКурсовыхРазниц = Новый ТаблицаЗначений;
	ТаблицаСуммовыхИКурсовыхРазниц.Колонки.Добавить("Подразделение",      Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхИКурсовыхРазниц.Колонки.Добавить("СуммоваяРазницаУпр", Новый ОписаниеТипов("Число"));
	ТаблицаСуммовыхИКурсовыхРазниц.Колонки.Добавить("КурсоваяРазницаУпр", Новый ОписаниеТипов("Число"));
	
	Регламентно=(обПраво("РежимСписанияСуммовыхРазниц",Права,,ЭтотОбъект)=Перечисления.РежимыРегламентныхОпераций.Регламентно);
	Если СуммовыеРазницы И Регламентно Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект  = ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения = Режим;
		НаборЗаписейВзаиморасчеты.Контрагент      = Неопределено;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = Неопределено;
		НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
		НаборЗаписейВзаиморасчеты.Сумма  = 0;
		НаборЗаписейВзаиморасчеты.Валюта = Неопределено;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
		
		// В набор записей передаем созданную таблицу через преодопределенное свойство "ДополнительныеСвойства".
		// После отработки функции модуля набора записей, эта таблица будет заполнена соответствующими движениями.
		НаборЗаписейВзаиморасчеты.ДополнительныеСвойства.Вставить("ТаблицаДвижений", ТаблицаСуммовыхИКурсовыхРазниц);
		
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейВзаиморасчеты.ДополнительныеСвойства.Вставить("ФильтрПоПодразделению", ПодразделениеКомпании);
		ИначеЕсли БалансВедетсяПоОрганизациям Тогда
			НаборЗаписейВзаиморасчеты.ДополнительныеСвойства.Вставить("ФильтрПоОрганизации", Организация);		
		КонецЕсли;
		
		Отказ = НЕ НаборЗаписейВзаиморасчеты.СписаниеСуммовыхРазниц() ИЛИ Отказ; 		
	КонецЕсли;
	
	// разбираемся с курсовыми разницами
	СуммаДоходаРасходаКурсовыхРазниц=0;
	Если КурсовыеРазницыВзаиморасчетыКомпании Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект  = ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения = Режим;
		НаборЗаписейВзаиморасчеты.Контрагент      = Неопределено;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = Неопределено;
		НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
		НаборЗаписейВзаиморасчеты.Сумма  = 0;
		НаборЗаписейВзаиморасчеты.Валюта = Неопределено;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
		
		// В набор записей передаем созданную таблицу через преодопределенное свойство "ДополнительныеСвойства".
		// После отработки функции модуля набора записей, эта таблица будет заполнена соответствующими движениями.
		НаборЗаписейВзаиморасчеты.ДополнительныеСвойства.Вставить("ТаблицаДвижений", ТаблицаСуммовыхИКурсовыхРазниц);
		
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейВзаиморасчеты.ДополнительныеСвойства.Вставить("ФильтрПоПодразделению", ПодразделениеКомпании);
		ИначеЕсли БалансВедетсяПоОрганизациям Тогда
			НаборЗаписейВзаиморасчеты.ДополнительныеСвойства.Вставить("ФильтрПоОрганизации", Организация);		
		КонецЕсли;
		
		Отказ=НЕ НаборЗаписейВзаиморасчеты.СписаниеКурсовыхРазниц() ИЛИ Отказ;   		
	КонецЕсли;
	
	СуммаДоходаРасходаКурсовыхРазниц=0;
	Если КурсовыеРазницыДенежныеСредстваКомпании Тогда
		НаборЗаписейДС = Движения.ДенежныеСредстваКомпании;
		НаборЗаписейДС.ДокументОбъект     = ЭтотОбъект;
		НаборЗаписейДС.РежимПроведения    = Режим;
		НаборЗаписейДС.СтруктурнаяЕдиница = Неопределено;
		НаборЗаписейДС.Валюта             = Неопределено;
		НаборЗаписейДС.СтатьяДДС          = Справочники.СтатьиДДС.КурсовыеРазницы;
		НаборЗаписейДС.Сумма              = 0;
		
		// В набор записей передаем созданную таблицу через преодопределенное свойство "ДополнительныеСвойства".
		// После отработки функции модуля набора записей, эта таблица будет заполнена соответствующими движениями.
		НаборЗаписейДС.ДополнительныеСвойства.Вставить("ТаблицаДвижений", ТаблицаСуммовыхИКурсовыхРазниц);
		
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДС.ДополнительныеСвойства.Вставить("ФильтрПоПодразделению", ПодразделениеКомпании);
		ИначеЕсли БалансВедетсяПоОрганизациям Тогда
			НаборЗаписейДС.ДополнительныеСвойства.Вставить("ФильтрПоОрганизации", Организация);		
		КонецЕсли; 
		
		Отказ = НЕ НаборЗаписейДС.СписаниеКурсовыхРазниц() ИЛИ Отказ;				
	КонецЕсли;
	
	Если КурсовыеРазницыКассыККМ Тогда
		НаборЗаписейКассыККМ = Движения.КассыККМ;
		НаборЗаписейКассыККМ.ДокументОбъект  = ЭтотОбъект;
		НаборЗаписейКассыККМ.РежимПроведения = Режим;
		
		// В набор записей передаем созданную таблицу через преодопределенное свойство "ДополнительныеСвойства".
		// После отработки функции модуля набора записей, эта таблица будет заполнена соответствующими движениями.
		НаборЗаписейКассыККМ.ДополнительныеСвойства.Вставить("ТаблицаДвижений", ТаблицаСуммовыхИКурсовыхРазниц); 
		
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейКассыККМ.ДополнительныеСвойства.Вставить("ФильтрПоПодразделению", ПодразделениеКомпании);
		ИначеЕсли БалансВедетсяПоОрганизациям Тогда
			НаборЗаписейКассыККМ.ДополнительныеСвойства.Вставить("ФильтрПоОрганизации", Организация);		
		КонецЕсли;

		Отказ=НЕ НаборЗаписейКассыККМ.СписаниеКурсовыхРазниц() ИЛИ Отказ;  				
	КонецЕсли;
	
	// Выполняем движения по регистру "Доходы и расходы"
	Если БалансВедетсяПоОрганизациям Тогда
		// Если баланс ведется по организации, то движения по регистру "Доходы и расходы"
		// выполняются на итоговые суммы по подразделению документа.
		СуммоваяРазницаИтого = ТаблицаСуммовыхИКурсовыхРазниц.Итог("СуммоваяРазницаУпр");
		КурсоваяРазницаИтого = ТаблицаСуммовыхИКурсовыхРазниц.Итог("КурсоваяРазницаУпр");
		
		// доходы и расходы списания суммовых разниц
		Если СуммоваяРазницаИтого<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение          = ПодразделениеКомпании;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			Если СуммоваяРазницаИтого<0 Тогда
				НаборЗаписейДиР.Расход = -СуммоваяРазницаИтого;
			Иначе
				НаборЗаписейДиР.Доход  = СуммоваяРазницаИтого;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 	
		
		// доходы и расходы списания курсовых разниц
		Если КурсоваяРазницаИтого<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение          = ПодразделениеКомпании; 
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КурсовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			Если КурсоваяРазницаИтого<0 Тогда
				НаборЗаписейДиР.Расход = - КурсоваяРазницаИтого;
			Иначе
				НаборЗаписейДиР.Доход  =  КурсоваяРазницаИтого;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 		
	Иначе
		// Если баланс ведется по подразделениям или по компании, то движения по регистру "Доходы и расходы"
		// необходимо выполнять в разрезе подразделений.
		ТаблицаСуммовыхИКурсовыхРазниц.Свернуть("Подразделение", "СуммоваяРазницаУпр, КурсоваяРазницаУпр"); 		
		Для Каждого ТекСтрока Из ТаблицаСуммовыхИКурсовыхРазниц Цикл  	
			// доходы и расходы списания суммовых разниц
			Если ТекСтрока.СуммоваяРазницаУпр<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение          = ТекСтрока.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте             = Истина;
				Если ТекСтрока.СуммоваяРазницаУпр<0 Тогда
					НаборЗаписейДиР.Расход = -ТекСтрока.СуммоваяРазницаУпр;
				Иначе
					НаборЗаписейДиР.Доход  = ТекСтрока.СуммоваяРазницаУпр;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли; 	
			
			// доходы и расходы списания курсовых разниц
			Если ТекСтрока.КурсоваяРазницаУпр<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение          = ТекСтрока.Подразделение; 
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КурсовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте             = Истина;
				Если ТекСтрока.КурсоваяРазницаУпр<0 Тогда
					НаборЗаписейДиР.Расход = - ТекСтрока.КурсоваяРазницаУпр;
				Иначе
					НаборЗаписейДиР.Доход  =  ТекСтрока.КурсоваяРазницаУпр;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
	Если БалансВедетсяПоПодразделениям Тогда
		дкДобавитьОшибку(РезультатОбработки, "Установлен способ ведения баланса по подразделениям.
		|Произведена переоценка валютных сумм выбранных активов по подразделению документа: " + ПодразделениеКомпании);
	ИначеЕсли БалансВедетсяПоОрганизациям Тогда
		дкДобавитьОшибку(РезультатОбработки, "Установлен способ ведения баланса по организациям.
		|Произведена переоценка валютных сумм выбранных активов по подразделениям организации документа: " + Организация);	
	Иначе  	
		дкДобавитьОшибку(РезультатОбработки, "Установлен способ ведения баланса по компании.
		|Произведена переоценка валютных сумм выбранных активов по всем подразделениям компании.");		
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
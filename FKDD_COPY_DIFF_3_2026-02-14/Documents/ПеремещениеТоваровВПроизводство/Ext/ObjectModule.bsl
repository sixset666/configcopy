// Модуль документа ПеремещениеТоваровВПроизводство

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания

Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
		ОбязательныеТовары.Вставить("ГТД",2);
	КонецЕсли;
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Цех",1);
	
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
		
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("Цех",КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации И обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
				КонтролируемыеРеквизитыТовары = Новый Структура();
				КонтролируемыеРеквизитыТовары.Вставить("Партия",Ложь);
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;	
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;	
	КонецЕсли;	
	
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПеремещениеТоваровВПроизводство","Перемещение товаров в производство",Истина);

	Возврат СписокХозОпераций;
КонецФункции

//Заполнение табличной части документа остатками из заказ-наряда
Процедура ЗаполнитьОстаткамиПоЗаказНаряду() Экспорт
	Товары.Очистить();
	//Выход, если заказ-наряд не задан
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда Возврат; КонецЕсли; 

	//Получим документ заказ-наряд
	ДокументОснованиеОбъект=ДокументОснование.ПолучитьОбъект();
	Если обЗначениеНеЗаполнено(Цех) Тогда
		Цех=ДокументОснованиеОбъект.Цех;
	КонецЕсли; 
	
	ТаблицаТоваров = ДокументОснованиеОбъект.Товары.Выгрузить();
	Для Каждого ТекСтрока Из ДокументОснованиеОбъект.Материалы Цикл
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	
	ДокументОснованиеОбъект.ПолучитьКэшДанныхПоПеремещению(СкладКомпании, ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду,, ТаблицаТоваров);
	КэшДанныхПоПеремещению = ДокументОснованиеОбъект.КэшДанныхПоПеремещению;
	
	Для Каждого СтрокаТоваров Из КэшДанныхПоПеремещению Цикл
		Если СтрокаТоваров.ВозможноПереместить=0 Тогда Продолжить; КонецЕсли;
		НоваяСтрокаТоваров=Товары.Добавить();
		НоваяСтрокаТоваров.Номенклатура=СтрокаТоваров.Номенклатура;
		ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрокаТоваров);
		НоваяСтрокаТоваров.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
		НоваяСтрокаТоваров.ЕдиницаИзмерения=СтрокаТоваров.ЕдиницаИзмерения;
		НоваяСтрокаТоваров.Количество=СтрокаТоваров.ВозможноПереместить/СтрокаТоваров.Коэффициент;
		НоваяСтрокаТоваров.Коэффициент=СтрокаТоваров.Коэффициент;
	КонецЦикла;
	
	Товары.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент, Ячейка", "Количество");
	
	//БИЗТЕХ КОВАЛЬ 14.10.2025 ++
	//Заполним доп. поля для товарной строки
	Для Каждого СтрокаТовар Из Товары Цикл
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ КОВАЛЬ 14.10.2025 --
	
КонецПроцедуры

//Получение строкового представления о ремонтируемом автомобиле
Функция ОтобразитьИнформациюОбАвтомобиле() Экспорт
	ИнформацияОбАвтомобиле="";
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") И (НЕ обЗначениеНеЗаполнено(ДокументОснование)) Тогда
		Попытка
			ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(ДокументОснование.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,ДокументОснование.Дата);
		Исключение
			ГосНомер = "";
		КонецПопытки;
		Если обЗначениеНеЗаполнено(ГосНомер) Тогда
			ГосНомер="";
		Иначе
			ГосНомер="; гос № "+ГосНомер;
		КонецЕсли; 
		Попытка
			ИнформацияОбАвтомобиле=СокрЛП(ДокументОснование.Автомобиль.Модель)+"; VIN "+ДокументОснование.Автомобиль.VIN+ГосНомер;
		Исключение
			#Если Клиент Тогда
				Сообщить("При получении информации о автомобиле произошли ошибки:
				|	" + ОписаниеОшибки(),СтатусСообщения.Внимание);
			#КонецЕсли
			ИнформацияОбАвтомобиле="";
		КонецПопытки;
	КонецЕсли;
	Возврат ИнформацияОбАвтомобиле;
КонецФункции 

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Цех КАК Цех,
	|	Док.СкладКомпании.Подразделение КАК ПодразделениеОтправителя,
	|	Док.Цех.Подразделение КАК ПодразделениеПолучателя,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.ДокументОснование КАК ДокументПродажи
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Режим - режим проведения (оперативный/неоперативный)
// ДокументСсылка - ссылка на документ который надо допровести по партиям
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Очистим возможные движения по производственному регистру, 
	//если было отложенное проведение по партиям
	НаборЗаписейТоварыВПроизводстве=РегистрыНакопления.ТоварыВПроизводстве.СоздатьНаборЗаписей();
	НаборЗаписейТоварыВПроизводстве.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейТоварыВПроизводстве.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейТоварыВПроизводстве.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		//Поступление товаров в производство - создадим фиктивное перемещение в производство
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ПеремещениеТоваровВПроизводствоТовары.Номенклатура КАК Номенклатура,
		             |	ПеремещениеТоваровВПроизводствоТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	ПеремещениеТоваровВПроизводствоТовары.Количество * ПеремещениеТоваровВПроизводствоТовары.Коэффициент КАК Количество,
		             |	NULL КАК СтатусПартии,
		             |	NULL КАК Партия,
		             |	NULL КАК ГТД,
					 |	0 КАК Сумма,
					 |	0 КАК СуммаНДС
		             |ИЗ
		             |	Документ.ПеремещениеТоваровВПроизводство.Товары КАК ПеремещениеТоваровВПроизводствоТовары
		             |ГДЕ
		             |	ПеремещениеТоваровВПроизводствоТовары.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		НаборЗаписейТоварыВПроизводстве=Движения.ТоварыВПроизводстве;
		НаборЗаписейТоварыВПроизводстве.РежимПроведения=Режим;
		НаборЗаписейТоварыВПроизводстве.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейТоварыВПроизводстве.РезультатЗапросаПоТоварам=Запрос.Выполнить().Выгрузить();
		НаборЗаписейТоварыВПроизводстве.ДокументЗаказНаряд=ШапкаДокумента.ДокументОснование;
		НаборЗаписейТоварыВПроизводстве.Цех=ШапкаДокумента.Цех;
		НаборЗаписейТоварыВПроизводстве.ЕстьСтавкаНДС=Ложь;
		НаборЗаписейТоварыВПроизводстве.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейТоварыВПроизводстве.Приход() ИЛИ Отказ;
		
		// двигаем границу последовательности Производства
		Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказНарядБыл) Тогда
			// если идет допроведение, то надо получить те значения которые были раньше
			Если Ссылка<>ДокументСсылка Тогда
				Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаряд ИЗ РегистрНакопления.ТоварыВПроизводстве ГДЕ Регистратор=&Ссылка");
				Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
				Результат = Запрос.Выполнить(); ЗаказНарядБыл=Неопределено;
				Если НЕ Результат.Пустой() Тогда
					ЗаказНарядБыл = Результат.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд");
				КонецЕсли;
				ДополнительныеСвойства.Вставить("ЗаказНарядБыл",ЗаказНарядБыл);
				ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
			КонецЕсли;
			
			НаборЗаписейГраницыПроизводство = РегистрыСведений.ГраницыПроизводство.СоздатьНаборЗаписей();
			Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
				НаборЗаписейГраницыПроизводство.ЗаказНаряд     		= ШапкаДокумента.ДокументОснование;
				НаборЗаписейГраницыПроизводство.МоментВремени  		= ШапкаДокумента.Дата;
				НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= ДополнительныеСвойства.ЗаказНарядБыл;
				НаборЗаписейГраницыПроизводство.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
			Иначе
				НаборЗаписейГраницыПроизводство.ЗаказНаряд			= ДополнительныеСвойства.ЗаказНарядБыл;
				НаборЗаписейГраницыПроизводство.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
				НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= Неопределено;
				НаборЗаписейГраницыПроизводство.МоментВремениБыл	= Неопределено;
			КонецЕсли;
			НаборЗаписейГраницыПроизводство.ДокументСсылка	= ШапкаДокумента.Ссылка;
			НаборЗаписейГраницыПроизводство.ИзменитьГраницу();
		КонецЕсли;
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// БАЛАНС: Если происходит перемещение товаров между складами подразделений, принадлежащих
	// различным балансовым "веткам", то возможен разрыв баланса.
	СпособВеденияБаланса		  = Константы.СпособВеденияБаланса.Получить(); 
	ДобавлятьКорректирующиеЗаписи = (СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	ПодразделениеОтправителя      = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеОтправителя);
	ПодразделениеПолучатель       = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеПолучателя);
    ДобавлятьКорректирующиеЗаписи = ДобавлятьКорректирующиеЗаписи И (ПодразделениеОтправителя<>ПодразделениеПолучатель);
	
	// проверяем, присутствуют ли партии в табличной части
		ЕстьПартии = Ложь;
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
	// проведем партии товаров
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.СкладКомпанииКуда=Неопределено; //Формируем только расход по регистру партий
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейПартии.ИмяРеквизитаДокумент=?(ЕстьПартии,"Партия","");
	НаборЗаписейПартии.ПоБазовомуКоличеству=Ложь;
	Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	Если НЕ Отказ Тогда НаборЗаписейПартии.Записать(); КонецЕсли; 	
	НаборЗаписейПартии.ГраницаРасчетаОстатков=Неопределено; // сбросим границу
	
	//Поступление товаров в производство
	НаборЗаписейТоварыВПроизводстве=Движения.ТоварыВПроизводстве;
	НаборЗаписейТоварыВПроизводстве.РежимПроведения=Режим;
	НаборЗаписейТоварыВПроизводстве.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейТоварыВПроизводстве.РезультатЗапросаПоТоварам=НаборЗаписейПартии.Выгрузить();
	НаборЗаписейТоварыВПроизводстве.ДокументЗаказНаряд=ШапкаДокумента.ДокументОснование;
	НаборЗаписейТоварыВПроизводстве.Цех=ШапкаДокумента.Цех;
	НаборЗаписейТоварыВПроизводстве.ЕстьСтавкаНДС=Ложь;
	НаборЗаписейТоварыВПроизводстве.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейТоварыВПроизводстве.Приход() ИЛИ Отказ;
	
	// Переместим в производство маркируемый товар
	НаборЗаписейМаркировкаТоваровВПроизводстве=Движения.МаркировкаТоваровВПроизводстве;
	НаборЗаписейМаркировкаТоваровВПроизводстве.РежимПроведения=Режим;
	НаборЗаписейМаркировкаТоваровВПроизводстве.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейМаркировкаТоваровВПроизводстве.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейМаркировкаТоваровВПроизводстве.ДокументЗаказНаряд=ШапкаДокумента.ДокументОснование;
	НаборЗаписейМаркировкаТоваровВПроизводстве.Цех=ШапкаДокумента.Цех;
	НаборЗаписейМаркировкаТоваровВПроизводстве.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейМаркировкаТоваровВПроизводстве.Приход() ИЛИ Отказ;
	
	// Доходы и расходы
	Если ДобавлятьКорректирующиеЗаписи Тогда
		// У подразделения склада-отправителя возникает расход.
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		ТаблицаПартий = НаборЗаписейПартии.Выгрузить();
		ТаблицаПартий.Свернуть("СтатусПартии","СуммаУпр");
		Для каждого ТекСтрокаДвижения Из ТаблицаПартий Цикл  	
			Если ТекСтрокаДвижения.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия Тогда
				Продолжить;
			КонецЕсли;
			СуммаДиР = ТекСтрокаДвижения.СуммаУпр;
			Если СуммаДиР<>0 Тогда
				НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыРасходы.ДокументОбъект = ЭтотОбъект;
				// Если способ ведения баланса не по подразделению то подразделение должно быть как у документа
				Если СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоПодразделению Тогда
					НаборЗаписейДоходыРасходы.Подразделение = ШапкаДокумента.ПодразделениеОтправителя;
				Иначе
					НаборЗаписейДоходыРасходы.Подразделение = Неопределено;
				КонецЕсли;
				НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
				НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
				НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
				НаборЗаписейДоходыРасходы.Расход         = СуммаДиР;
				Отказ = НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
			КонецЕсли;			
		КонецЦикла;			
		// У подразделения цеха-получателя - доход.
		НаборЗаписейТоварыВПроизводстве=Движения.ТоварыВПроизводстве;
		ТаблицаПартий = НаборЗаписейТоварыВПроизводстве.Выгрузить();
		ТаблицаПартий.Свернуть("СтатусПартии","СуммаУпр");
		Для каждого ТекСтрокаДвижения Из ТаблицаПартий Цикл  	
			Если ТекСтрокаДвижения.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия Тогда
				Продолжить;
			КонецЕсли;
			СуммаДиР = ТекСтрокаДвижения.СуммаУпр;
			Если СуммаДиР<>0 Тогда
				НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыРасходы.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДоходыРасходы.Подразделение  = ШапкаДокумента.ПодразделениеПолучателя;
				НаборЗаписейДоходыРасходы.ВУпрВалюте     = Истина;
				НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДоходыРасходы.ШапкаДокумента = ШапкаДокумента;
				НаборЗаписейДоходыРасходы.Доход          = СуммаДиР;
				Отказ = НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
			КонецЕсли;			
		КонецЦикла;			
	КонецЕсли; 	
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности Производства
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказНарядБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаряд ИЗ РегистрНакопления.ТоварыВПроизводстве ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); ЗаказНарядБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				ЗаказНарядБыл = Результат.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("ЗаказНарядБыл",ЗаказНарядБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПроизводство = РегистрыСведений.ГраницыПроизводство.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПроизводство.ЗаказНаряд     		= ШапкаДокумента.ДокументОснование;
			НаборЗаписейГраницыПроизводство.МоментВремени  		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= ДополнительныеСвойства.ЗаказНарядБыл;
			НаборЗаписейГраницыПроизводство.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПроизводство.ЗаказНаряд			= ДополнительныеСвойства.ЗаказНарядБыл;
			НаборЗаписейГраницыПроизводство.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПроизводство.ЗаказНарядБыл		= Неопределено;
			НаборЗаписейГраницыПроизводство.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПроизводство.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПроизводство.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	Возврат дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", тКэшСуммСписания);
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
			Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
			Иначе
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		// глобально
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма) И Рез;
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;

	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если (ТекСтрока.Количество*ТекСтрока.Коэффициент=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=ТекСтрока.СуммаРозничная/(ТекСтрока.Количество*ТекСтрока.Коэффициент);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Ячейка" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Возврат Истина;
		КонецЕсли;
		ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладКомпании);
		ТекСтрока.Ячейка=ЯчейкаСсылка;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.ЕдиницаИзмерения,СкладКомпании.Подразделение);
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную сумму (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	СтруктураПараметров=Неопределено;
	Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
		СтруктураПараметров=Новый Структура("ИмяРеквизитаЦена,ТипЦен","ЦенаРозничная",СкладКомпании.ТипЦенРозничнойТорговли);
	КонецЕсли;
	дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
КонецФункции

Функция ПечатьПеремещениеТоваровВПроизводствоСОстатком(ТабДокумент, Остаток = Ложь) Экспорт
	
	Возврат ПечатьПеремещениеТоваровВПроизводство(ТабДокумент, Истина);
	
КонецФункции

// Формирует печатную форму "ПеремещениеТоваровВПроизводство"
// Возвращает сформированный табличный документ:
Функция ПечатьПеремещениеТоваровВПроизводство(ТабДокумент, Остаток = Ложь) Экспорт      
	Если Остаток Тогда
		Макет = ПолучитьМакет("ПеремещениеТоваровВПроизводствоОстаток");
	Иначе
		Макет = ПолучитьМакет("ПеремещениеТоваровВПроизводство");
	КонецЕсли;
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	//Настроим макет отчёта
	//Удаляем колонку "ЯчейкаХранения", если ни в одной из номенклатур этот реквизит не заполнен
	//Увеличиваем за её счёт колонку "Товар"
	ЕстьЯчейкиХранения=Ложь;
	
	Для Каждого СтрокаТЧ Из ВыборкаТабличнойЧасти Цикл
		Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.Ячейка) Тогда
			ЕстьЯчейкиХранения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
		
	Если НЕ ЕстьЯчейкиХранения Тогда
		ОбластьТовар = Макет.Область("Товар");
		ОбластьЯчейкаХранения = Макет.Область("ЯчейкаХранения");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЯчейкаХранения.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейкаХранения,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;	
	
	//Настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьНаименование(ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ЗаказНарядПредставление=дкПолучитьПредставление(ЭтотОбъект.ДокументОснование);
	ОбластьМакета.Параметры.АвтомобильПредставление=ОтобразитьИнформациюОбАвтомобиле();

	ОбластьМакета.Параметры.ПредставлениеПолучателя = спПолучитьНаименование(ЭтотОбъект.Цех);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	
	ТабДокумент.Вывести(ОбластьМакета);

	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура();
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);  
		
		Если Остаток Тогда
			
			СтруктураСтроки.Вставить("Остаток", зфПолучитьОстаткиНоменклатуры(ЭтотОбъект,
				СтрокаТабличнойЧасти.Номенклатура, 
				СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, 
				ЭтотОбъект.СкладКомпании));	
	
		КонецЕсли;
		//В возвращаемой структуре нет ключа "Ячейка", создадим его
		СтруктураСтроки.Вставить("Ячейка");
		//Если ЯчейкаХранения не определена для данной номенклатуры, то печатаем пробел
		ЯчейкаДляПечати = СокрЛП(СтрокаТабличнойЧасти.Ячейка.Код);
		СтруктураСтроки.Ячейка 	= ?(обЗначениеНеЗаполнено(ЯчейкаДляПечати)," ",ЯчейкаДляПечати);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура();
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//Итогов по странице на этой печатной форме нет, но выводим для прорисовки завершающей чёрной линии снизу страницы
	дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	
	
	//Подвал
	//Выводим общее количество деталей
	ВсегоНаименований 	= Формат(ВыборкаТабличнойЧасти.Количество(), ФорматВыводаКоличества);
	ОбщееКоличество 	= Формат(ВыборкаТабличнойЧасти.Итог("Количество"), ФорматВыводаКоличества);
	ОбластьПодвал.Параметры.Итого = "Всего наименований " + ВсегоНаименований + " в количестве " + ОбщееКоличество;
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ " + Отпустил.ОтпустилПредставление + " /");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ " + Получил.ПолучилПредставление + " /");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции //ПечатьПеремещениеТоваровВПроизводство()

// Формирует печатную форму "ТОРГ13"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ13(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ13");

	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.Номер = НомерДляПечати;
	ОбластьМакета.Параметры.Дата = Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ОтправительПодразделение	= спПолучитьНаименование(ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.ПолучательПодразделение		= спПолучитьНаименование(ЭтотОбъект.Цех);
	ТабДокумент.Вывести(ОбластьМакета);

	СтрокНаСтранице = 25;
	СтрокШапки      = 12;
	СтрокПодвала    = 5;
	НомерСтраницы   = 1;
	
	ВалютаРегл			= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаТек			= ВалютаРегл;
	КурсТек				= ЭтотОбъект.КурсДокумента;
	СтруктураКурса		= обКурсДляВалюты(ВалютаРегл, ЭтотОбъект.Дата);
	КурсРегл			= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр			= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ЭтотОбъект.Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ЭтотОбъект.КурсВалютыУпр; 
	КонецЕсли; 

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ВалютаТек;
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	СтрокаКолонокСвертки = "Количество, КоличествоБазовое, СуммаНДС, Сумма"+?(ВалютаТек=ВалютаУпр,"Упр","");
	ИспользуемыеРесурсы = Новый Структура("СуммаНДС, Сумма"+?(ВалютаТек=ВалютаУпр,"Упр",""));
	ЕстьПартии = Истина;
	ИмяРегистра = "ТоварыВПроизводстве";
	// выборка запросом по движениям документа по регистру "ТоварыВПроизводстве"
	ТаблицаСуммСписания = дкПолучитьТаблицуСуммСписания(Ссылка, ЕстьПартии, ИмяРегистра, ИспользуемыеРесурсы, ВидДвиженияНакопления.Приход);
	ТаблицаСуммСписания.Свернуть("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры", СтрокаКолонокСвертки);
	
	КоличествоСтрок = ТаблицаСуммСписания.Количество();
    Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку	= 0;
	Иначе
		ЦелыхСтраницСПодвалом		= Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала		= Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку	= ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Определим, что показывать в колонке кода - код или артикул
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	// инициализация итогов по странице
	ИтогКоличествоМестПоСтранице = 0;
	ИтогМассаБруттоПоСтранице    = 0;
	ИтогМассыНеттоПоСтранице     = 0;
	ИтогСуммыПоСтранице          = 0;

	// инициализация итогов по документу
	ИтогоКоличество  = 0;
	ИтогоМассаБрутто = 0;
	ИтогоМассаНетто  = 0;
	ИтогоСумма       = 0;
    Ном = 0;

	// Выводим многострочную часть документа
	ОбластьИтоговПоСтранице	= Макет.ПолучитьОбласть("ИтогиПоСтранице");
	ОбластьМакета			= Макет.ПолучитьОбласть("Строка");
	
	Для Каждого ТекСтрока Из ТаблицаСуммСписания Цикл
		Ном = Ном + 1;
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

			// инициализация итогов по странице
			ИтогКоличествоМестПоСтранице = 0;
			ИтогМассаБруттоПоСтранице    = 0;
			ИтогМассаНеттоПоСтранице     = 0;
			ИтогСуммыПоСтранице          = 0;

			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;

		Сумма = ТекСтрока.Сумма-ТекСтрока.СуммаНДС;
		
		// Измерения
		ОбластьМакета.Параметры.Заполнить(ТекСтрока);
		Характеристика										= Строка(ТекСтрока.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(ТекСтрока.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					= дкПолучитьЗначениеКолонкиКода(ТекСтрока.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		ОбластьМакета.Параметры.ЕдиницаИзмерения			= ТекСтрока.ЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ	= ТекСтрока.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьМакета.Параметры.КоличествоВОдномМесте		= Формат(ТекСтрока.ЕдиницаИзмерения.Коэффициент,ФорматВыводаКоличества);
		// Суммовые показатели
		ОбластьМакета.Параметры.КоличествоМест				= Формат(ТекСтрока.Количество,ФорматВыводаКоличества);
		МассаБрутто 										= ТекСтрока.Количество * Справочники.Номенклатура.ПолучитьВесНоменклатуры(ТекСтрока.Номенклатура, ТекСтрока.ЕдиницаИзмерения);
		ОбластьМакета.Параметры.МассаБрутто					= МассаБрутто;
		ОбластьМакета.Параметры.Цена						= Формат(Сумма/ТекСтрока.Количество,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Сумма						= Формат(Сумма,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Обновим итоги по странице
		ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + ТекСтрока.Количество;
		ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице    + МассаБрутто;
		ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице     + 0;
		ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице          + Сумма;

		// Обновим итоги по документу
		ИтогоКоличество  = ИтогоКоличество  + ТекСтрока.Количество;
		ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБрутто;
		ИтогоМассаНетто  = ИтогоМассаНетто  + 0;
		ИтогоСумма       = ИтогоСумма       + Сумма;
	КонецЦикла;

	ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
    ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим итоги по документу в целом
	ОбластьМакета = Макет.ПолучитьОбласть("Всего");
	ОбластьМакета.Параметры.ИтогоКоличествоМест = ИтогоКоличество;
	ОбластьМакета.Параметры.ИтогоМассаБрутто    = ИтогоМассаБрутто;
	ОбластьМакета.Параметры.ИтогоМассаНетто     = ИтогоМассаНетто;
	ОбластьМакета.Параметры.ИтогоСумма          = ИтогоСумма;
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ИтогоСуммаПрописью = обЧислоПрописью(ИтогоСумма,ВалютаТек);

	Отпустил = обПолучитьЗначениеСвойства(ЭтотОбъект,"Отпустил",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.Отпустил=Отпустил;
	ОбластьМакета.Параметры.ОтпустилДолжность=Отпустил.Должность;
	ОбластьМакета.Параметры.ОтпустилПредставление=Отпустил.Наименование;
	
	Получил = обПолучитьЗначениеСвойства(ЭтотОбъект,"Получил",Цех.Мастер);
	ОбластьМакета.Параметры.Получил=Получил;
	ОбластьМакета.Параметры.ПолучилДолжность=Получил.Должность;
	ОбластьМакета.Параметры.ПолучилПредставление=Получил.Наименование;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму Т-1 "Товарно-транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТ1(ТабДокумент) Экспорт
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТТН");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	//дальше заполнить Шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	//ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления2=Новый Структура(
	"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с ");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.НомерДокумента                = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДокумента                 = Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.Грузоотправитель			  = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.Грузополучатель				  = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Цех);
	
	Если ТипЗнч(ОбластьМакета.Параметры.Грузоотправитель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
		ОбластьМакета.Параметры.ГрузоотправительПредставление = спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель.Организация,СтруктураПредставления2);
		ОбластьМакета.Параметры.ГрузополучательПоОКПО		  = ОбластьМакета.Параметры.Грузополучатель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузоотправительПредставление = ОбластьМакета.Параметры.Грузоотправитель.Наименование;
	КонецЕсли;
	
	Если ТипЗнч(ОбластьМакета.Параметры.Грузополучатель) = Тип("СправочникСсылка.Цеха") Тогда
		ОбластьМакета.Параметры.ГрузополучательПредставление  = спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель.Организация,СтруктураПредставления2);
		ОбластьМакета.Параметры.ГрузоотправительПоОКПО		  = ОбластьМакета.Параметры.Грузоотправитель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузополучательПредставление  = ОбластьМакета.Параметры.Грузополучатель.Наименование;
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим заголовок таблицы
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьЗаголовокТаблицы.Параметры.Валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	ОбластьСтрока	= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги	= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего	= Макет.ПолучитьОбласть("Всего");
	ОбластьПодвал	= Макет.ПолучитьОбласть("Подвал");
	
	// инициализация итогов по странице
	СтруктураИтоговПоСтранице = Новый Структура("Количество, СуммаВсего",0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы  = "Страница: " + НомерСтраницы;
	ОбластьЗаголовокТаблицы.Параметры.ТекстЗаголовка = "Товарно-транспортная накладная товарный раздел";
	
	СтрокаКолонокСвертки = "Количество, КоличествоБазовое, СуммаНДС, Сумма";
	ИспользуемыеРесурсы = Новый Структура("СуммаНДС, Сумма");
	ЕстьПартии = Истина;
	ИмяРегистра = "ТоварыВПроизводстве";
	// выборка запросом по движениям документа по регистру "ТоварыВПроизводстве"
	ТаблицаСуммСписания = дкПолучитьТаблицуСуммСписания(Ссылка, ЕстьПартии, ИмяРегистра, ИспользуемыеРесурсы, ВидДвиженияНакопления.Приход);
	ТаблицаСуммСписания.Свернуть("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры", СтрокаКолонокСвертки);
	
	КоличествоСтрок = ТаблицаСуммСписания.Количество();
	
	// инициализация итогов по документу
	Ном             = 0;
	ИтогоМест       = 0;
	ИтогоКоличество = 0;
	ИтогоВес        = 0;
	ИтогоСумма      = 0;
	
	//доп. области
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьВсего);
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	Для Каждого ТекСтрока Из ТаблицаСуммСписания Цикл
		//заполняем данные строки
		Ном = Ном + 1;
		ТоварНаименование = спПолучитьНаименование(ТекСтрока.Номенклатура);
		Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		//характеристику выводим в строке с наименованием
		ТоварНаименование = ТоварНаименование + ?(НЕ обЗначениеНеЗаполнено(Характеристика),", " + спПолучитьНаименование(Характеристика),"");
		
		ОбластьСтрока.Параметры.КодПродукции				= ТекСтрока.Номенклатура.Код;
		ОбластьСтрока.Параметры.Артикул 					= ТекСтрока.Номенклатура.Артикул;
		ОбластьСтрока.Параметры.Количество					= Формат(ТекСтрока.КоличествоБазовое, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена						= Формат(ТекСтрока.Сумма/ТекСтрока.Количество,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.ТоварНаименование			= ТоварНаименование;
		ОбластьСтрока.Параметры.Номенклатура				= ТекСтрока.Номенклатура;
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= спПолучитьНаименование(ТекСтрока.ЕдиницаИзмерения);
		ОбластьСтрока.Параметры.ВидУпаковки					= ТекСтрока.ЕдиницаИзмерения.ЕдиницаПоКлассификатору;
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(ТекСтрока.Количество, ФорматВыводаКоличества);
		МассаБрутто 										= ТекСтрока.Количество * Справочники.Номенклатура.ПолучитьВесНоменклатуры(ТекСтрока.Номенклатура, ТекСтрока.ЕдиницаИзмерения);
		ОбластьСтрока.Параметры.Масса						= Формат(МассаБрутто / 1000, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.СуммаВсего					= Формат(ТекСтрока.Сумма, ФорматВыводаСуммы);
		
		ИтогоМест		= ИтогоМест + ТекСтрока.Количество;
		ИтогоВес		= ИтогоВес + МассаБрутто;
		ИтогоКоличество	= ИтогоКоличество + ТекСтрока.КоличествоБазовое;
		ИтогоСумма		= ИтогоСумма + ТекСтрока.Сумма;
		
		//выводим строку, делая проверку попадания на лист		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, ?(Ном=КоличествоСтрок,мсвДопОбластиПодвала,Неопределено));
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("Количество, СуммаВсего",0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//обновим итоги по странице
		СтруктураИтоговПоСтранице.Количество = СтруктураИтоговПоСтранице.Количество + ТекСтрока.КоличествоБазовое;
		СтруктураИтоговПоСтранице.СуммаВсего = СтруктураИтоговПоСтранице.СуммаВсего + ТекСтрока.Сумма;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.Количество = Формат(ИтогоКоличество, ФорматВыводаКоличества);
	ОбластьВсего.Параметры.СуммаВсего = Формат(ИтогоСумма, ФорматВыводаСуммы);
	
	ТабДокумент.Вывести(ОбластьВсего);
	
	// Выводим подвал документа
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	ОбластьПодвал.Параметры.КоличествоСтрок         = КоличествоСтрок;
	ОбластьПодвал.Параметры.МассаГрузаБрутто		= ЧислоПрописью(ИтогоВес,,",,,,,,,,0")+"кг";
	ОбластьПодвал.Параметры.МассаВсего				= Формат(ИтогоВес / 1000, ФорматВыводаКоличества);
	ОбластьПодвал.Параметры.ОтпущеноНаСуммуПрописью = обЧислоПрописью(ИтогоСумма,ВалютаРегл);
	ОбластьПодвал.Параметры.ВсегоКОплате			 = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ВсегоМестПрописью       = ЧислоПрописью(ИтогоМест,,",,,,,,,,0");
	ОбластьПодвал.Параметры.ВсегоНаименований       = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	
	ТабДокумент.Вывести(ОбластьПодвал);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета   = Макет.ПолучитьОбласть("ТранспортныйРаздел");
	
	ЛицензионнаяКарточка 		= обПолучитьЗначениеСвойства(ЭтотОбъект, "ЛицензионнаяКарточка");
	СрокДоставки 				= обПолучитьЗначениеСвойства(ЭтотОбъект, "СрокДоставки");
	Перевозчик 					= обПолучитьЗначениеСвойства(ЭтотОбъект, "ОрганизацияПеревозчик");
	МаркаАвтомобиля     	    = обПолучитьЗначениеСвойства(ЭтотОбъект, "МаркаАвтомобиля");
	ГосНомерАвтомобиля	        = обПолучитьЗначениеСвойства(ЭтотОбъект, "ГосНомерАвтомобиля");
	Заказчик	 				= обПолучитьЗначениеСвойства(ЭтотОбъект, "ОрганизацияЗаказчик");
	Водитель					= обПолучитьЗначениеСвойства(ЭтотОбъект, "ФИОВодителя");
	ВодительскоеУдостоверение	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ВодительскоеУдостоверение");
	ВидПеревозки	            = обПолучитьЗначениеСвойства(ЭтотОбъект, "ВидПеревозки");
	ПунктПогрузки             	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПунктПогрузки");
	ПунктРазгрузки            	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПунктРазгрузки");
	МаркаПрицепа 			  	= обПолучитьЗначениеСвойства(ЭтотОбъект, "Прицеп");
	ГосНомерПрицепа	           	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ГосНомерПрицепа");
	
	Если ТипЗнч(ЛицензионнаяКарточка) = Тип("Булево") Тогда
		ШрифтСтандарт   = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , , НЕ ЛицензионнаяКарточка);
		ШрифтОграничено = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , , ЛицензионнаяКарточка);
		ОбластьМакета.Области.Стандарт.Шрифт   = ШрифтСтандарт;
		ОбластьМакета.Области.Ограничено.Шрифт = ШрифтОграничено;
	КонецЕсли;
	
	ОбластьМакета.Параметры.СрокДоставки              = СрокДоставки;
	ОбластьМакета.Параметры.Номер                     = НомерДляПечати;
	ОбластьМакета.Параметры.ОрганизацияПеревозчик     = Перевозчик;
	ОбластьМакета.Параметры.МаркаАвтомобиля           = МаркаАвтомобиля;
	ОбластьМакета.Параметры.ГосНомерАвтомобиля        = ГосНомерАвтомобиля;
	ОбластьМакета.Параметры.ОрганизацияЗаказчик       = Заказчик;
	ОбластьМакета.Параметры.ФИОВодителя               = Водитель;
	ОбластьМакета.Параметры.ВодительскоеУдостоверение = ВодительскоеУдостоверение;
	ОбластьМакета.Параметры.ВидПеревозки              = ВидПеревозки;
	ОбластьМакета.Параметры.ПунктПогрузки             = ПунктПогрузки;
	ОбластьМакета.Параметры.ПунктРазгрузки            = ПунктРазгрузки;
	ОбластьМакета.Параметры.Прицеп                    = МаркаПрицепа;
	ОбластьМакета.Параметры.ГосНомерПрицепа           = ГосНомерПрицепа;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СведенияОГрузе");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалСведенийОГрузе");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПогрузочныеОперации");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПрочиеСведения");
	ОбластьМакета.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции //ПечатьТ1()

// Формирует печатную форму Приложение №4 "Транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриложение4(ТабДокумент) Экспорт
	ТабДокумент = дкПечатьНовойТТН(ЭтотОбъект,ТабДокумент);
	Возврат ТабДокумент;
КонецФункции // ПечатьПриложение4()

// Формирует печатную форму "М-11"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ11(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("М11");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
	"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.Заголовок = "ТРЕБОВАНИЕ-НАКЛАДНАЯ № "+дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.КодОКПО		= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ДатаСоставления			= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.СкладОтправитель	= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.СкладПолучатель			= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Цех.СкладКомпании);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//переменные управляющие постраничным выводом
	СтрокНаСтранице = 25;
	СтрокШапки      = 10;
	СтрокПодвала    = 6;
	НомерСтраницы   = 1;		
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
	
	// выводим заголовок таблицы
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	Если ЭтотОбъект.Проведен Тогда
		СтрокаКолонокСвертки = "Количество, КоличествоБазовое, СуммаНДС, Сумма";
		ИспользуемыеРесурсы = Новый Структура("СуммаНДС, Сумма");
		ЕстьПартии = Истина;
		ИмяРегистра = "ТоварыВПроизводстве";
		ВыборкаСтрок = дкПолучитьТаблицуСуммСписания(Ссылка, ЕстьПартии, ИмяРегистра, ИспользуемыеРесурсы, ВидДвиженияНакопления.Приход);
		ВыборкаСтрок.Свернуть("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры", СтрокаКолонокСвертки);
	Иначе
		ВыборкаСтрок = ЭтотОбъект.Товары;
	КонецЕсли;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// Определим имя показываемой колонки кода
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	Ном = 0;

	// проверим, входит ли НДС в стоимость	
	ЦенаВключаетНДС = ТипЦен.ЦенаВключаетНДС;
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТабличнойЧасти Из ВыборкаСтрок Цикл
		Ном = Ном + 1;
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		СтавкаНДС=СтрокаТабличнойЧасти.Номенклатура;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		Характеристика											= Строка(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
		ОбластьСтрока.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьСтрока.Параметры.НоменклатурныйНомер				= дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод				= СтрокаТабличнойЧасти.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование	= СтрокаТабличнойЧасти.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.Количество						= Формат(СтрокаТабличнойЧасти.Количество,ФорматВыводаКоличества);
		Если ЦенаВключаетНДС Тогда
			ОбластьСтрока.Параметры.Цена				= Формат(?(ЭтотОбъект.Проведен,СтрокаТабличнойЧасти.Сумма/СтрокаТабличнойЧасти.Количество,СтрокаТабличнойЧасти.ЦенаРозничная),ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаБезНДС			= Формат(?(ЭтотОбъект.Проведен,СтрокаТабличнойЧасти.Сумма,СтрокаТабличнойЧасти.СуммаРозничная),ФорматВыводаСуммы);
		Иначе
			ОбластьСтрока.Параметры.Цена		 		= Формат(?(ЭтотОбъект.Проведен,СтрокаТабличнойЧасти.Сумма/СтрокаТабличнойЧасти.Количество,СтрокаТабличнойЧасти.ЦенаРозничная),ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаБезНДС			= Формат(?(ЭтотОбъект.Проведен,СтрокаТабличнойЧасти.Сумма,СтрокаТабличнойЧасти.СуммаРозничная),ФорматВыводаСуммы);
		КонецЕсли;		
		ТабДокумент.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	// выводим подвал
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб=Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьМ11()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	ОбработкаЗаполненияОтказ = НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание);
	
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
			ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
		Иначе
			ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ОбработкаЗаполненияОтказ Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		//Если документ вводится на основании заказ-наряда
		ЗаполнитьОстаткамиПоЗаказНаряду();
	Иначе
		Цех=обПраво("ОсновнойЦех",Права,,ЭтотОбъект);
	КонецЕсли; 
	Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный)  Тогда
		ТипЦен = СкладКомпании.ТипЦенРозничнойТорговли;
		ВалютаДокумента = обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
		СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
		КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Для Каждого СтрТовар Из Товары Цикл
			СтрТовар.ЦенаРозничная = обПолучитьЦену(ТипЦен,СтрТовар.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента,СтрТовар.ХарактеристикаНоменклатуры,СтрТовар.ЕдиницаИзмерения,СкладКомпании.Подразделение);
			ОбработкаРеквизита("Товары.ЦенаРозничная",СтрТовар);
		КонецЦикла;
	КонецЕсли;
	
	// Заполним доп. поля для товарной строки
	Для Каждого СтрокаТовар Из Товары Цикл
		
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказНарядБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		//БИЗТЕХ КОВАЛЬ 11.09.2024
		//Игнорирование для пользователя БИЗТЕХ
		Если СтрНайти(ПараметрыСеанса.Пользователь.Код,"БизТех") = 1 Тогда
			Сообщить("Отключена проверка состояния заказ-наряда для пользователя БИЗТЕХ !!");
		Иначе
			Если ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
				 ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
				Если НЕ ЭтотОбъект.Проведен Тогда
					Сообщить("Заказ-наряд выполнен или закрыт. Перемещения в производство запрещено.");
					Отказ=Истина; 
					дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
					Возврат;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;       
		//БизТех Аляль 19.12.24г. пропишем партию для Гарантийных <<
		//Если ДокументОснование.ВидРемонта = Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000010") //Гарантия
		//	или  ДокументОснование.ВидРемонта = Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000049")  Тогда   //Сервисные мероприятия
		//	//Найдем были ли заказы покупателей по этому ЗН
		//	Заказы = ПолучитьЗаказыПоЗН(ДокументОснование);
		//	Если Заказы <> Неопределено Тогда
		//		Для каждого Строка из Товары Цикл  
		//			Если Не ЗначениеЗаполнено(Строка.Партия) Тогда
		//				ДокПартия = НайтиПартиюПоЗаказу(Строка.Номенклатура, Строка.Количество, Заказы);
		//				Если Не ДокПартия = Неопределено Тогда
		//					Строка.Партия = ДокПартия; 
		//					Сообщить("Партия товара " +Строка.Номенклатура +" установлена автоматически в соответствии с Заказом покупателя.");
		//				Иначе
		//					Сообщить("Не найдена партия по товару " +Строка.Номенклатура +"!"); 
		//				КонецЕсли; 
		//			КонецЕсли;
		//		КонецЦикла;  
		//	КонецЕсли;
		//КонецЕсли;
		//БизТех Аляль 19.12.24г.>>
	КонецЕсли; 
	
	// отработаем право "Запись раньше документа-основания"
	Если (Дата < ДокументОснование.ДатаСоздания) Тогда
		ЗН="Заказ-наряд № "+СокрЛП(ДокументОснование.Номер)+" от "+Формат(ДокументОснование.ДатаСоздания,"ДФ='dd.MM.yyyy ЧЧ:мм:сс'");
		Если обПраво("ЗаписьРаньшеДокументаОснования",Права,,ЭтотОбъект) Тогда
			Если обПраво("ВыводитьСообщенияПроверкиПравДоступа",Права,,ЭтотОбъект) Тогда
				Сообщить("Внимание! Документ <"+Строка(ЭтотОбъект)+"> записывается раньше создания своего документа-основания: <"+ЗН+">",СтатусСообщения.Внимание);
			КонецЕсли;
		Иначе
			Сообщить("Нет прав на запись раньше создания чем документ-основание: <"+ЗН+">");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	ИначеЕсли (ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт) И (Дата > ДокументОснование.ДатаОкончания) Тогда
		ЗН="Заказ-наряд № "+СокрЛП(ДокументОснование.Номер)+" от "+Формат(ДокументОснование.ДатаОкончания,"ДФ='dd.MM.yyyy ЧЧ:мм:сс'");
		Если обПраво("ЗаписьРаньшеДокументаОснования",Права,,ЭтотОбъект) Тогда
			Если обПраво("ВыводитьСообщенияПроверкиПравДоступа",Права,,ЭтотОбъект) Тогда
				Сообщить("Внимание! Документ <"+Строка(ЭтотОбъект)+"> записывается позже окончания своего документа-основания: <"+ЗН+">",СтатусСообщения.Внимание);
			КонецЕсли;
		Иначе
			Сообщить("Нет прав на запись позже окончания чем документ-основание: <"+ЗН+">");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ обПраво("ПеремещениеДеталейВПроизводство",Права,,ЭтотОбъект) Тогда
		Если РежимЗаписи=РежимЗаписиДокумента.Проведение ИЛИ РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения Тогда
			Сообщить("Нет прав на перемещение деталей в производство!
			|Обратитесь к администратор системы.",СтатусСообщения.Важное);
			Отказ=Истина; Возврат;
		КонецЕсли;
	КонецЕсли;
		
	Если (НЕ Отказ) И (РежимЗаписи<>РежимЗаписиДокумента.Проведение) Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаряд ИЗ РегистрНакопления.ТоварыВПроизводстве ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказНарядБыл", Результат.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПоставщика
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --

	//++СнимченкоАА_бизтех++
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	//БИЗТЕХ КОВАЛЬ 11.09.2024
	//Игнорирование для пользователя БИЗТЕХ
	Если СтрНайти(ПараметрыСеанса.Пользователь.Код,"БизТех") = 1 Тогда
		Сообщить("Отключена проверка состояния заказ-наряда для пользователя БИЗТЕХ !!");
	Иначе
		Если ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
			 ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			Сообщить("Заказ-наряд выполнен или закрыт. Отмена перемещения в производство запрещена.");
			Отказ=Истина; 
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//Закрытие заказов покупателя
	//Закрываем заказы покупателя по FIFO
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	МАКСИМУМ(ДокументТовары.ЦенаРозничная) КАК ЦенаРозничная,
	|	СУММА(ДокументТовары.Количество*ДокументТовары.Коэффициент) КАК Количество,
	|	СУММА(0) КАК Резерв,
	|	СУММА(ДокументТовары.СуммаРозничная) КАК Сумма
	|ИЗ
	|	Документ.ПеремещениеТоваровВПроизводство.Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка=&Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.Номенклатура,ДокументТовары.ХарактеристикаНоменклатуры
	|";
	Запрос=Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	//Закрываем заказы
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = Запрос.Выполнить();
	НаборЗаписейЗаказыПокупателей.Контрагент = ДокументОснование.Контрагент;
	Если (НЕ ДокументОснование=Неопределено) И ТипЗнч(ДокументОснование.ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") И
		ЗначениеЗаполнено(ДокументОснование.ДокументОснование) Тогда
		НаборЗаписейЗаказыПокупателей.Заказ = ДокументОснование.ДокументОснование;
	Иначе
		НаборЗаписейЗаказыПокупателей.Заказ = Неопределено;
	КонецЕсли;
	НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
	НаборЗаписейЗаказыПокупателей.Заказывать    = Истина;
	НаборЗаписейЗаказыПокупателей.Резервировать = Истина;
	НаборЗаписейЗаказыПокупателей.ЗакрытиеЗаказаПоДокументуОснованию = ЗакрыватьЗаказыТолькоПоДанномуЗаказНаряду;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.ЗакрытиеЗаказовПокупателя() ИЛИ Отказ;
	
	РезультатЗапросаПоТоварам = НаборЗаписейЗаказыПокупателей.Выгрузить(, "Заказ, Номенклатура, ХарактеристикаНоменклатуры, Заказано, Резерв");
	
	//Снимаем распределение заказов покупателя
	НаборЗаписейРаспределениеЗаказов = Движения.ЗаказыРаспределение;
	НаборЗаписейРаспределениеЗаказов.РежимПроведения = Режим;
	НаборЗаписейРаспределениеЗаказов.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейРаспределениеЗаказов.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
	НаборЗаписейРаспределениеЗаказов.ЗаказПокупателя = Неопределено;
	НаборЗаписейРаспределениеЗаказов.ЗаказПоставщика = Неопределено;
	НаборЗаписейРаспределениеЗаказов.ПоБазовомуКоличеству=Ложь;
	НаборЗаписейРаспределениеЗаказов.Контрагент = ДокументОснование.Контрагент;
	Отказ=НЕ НаборЗаписейРаспределениеЗаказов.ЗакрытиеРаспределенияПоЗаказам() ИЛИ Отказ;
	
	//Если было списание резервов то таблица товаров содержит списанные резервы
	РезультатЗапросаПоТоварам = НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам;
	
	//Списание товаров со склада
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.Приходовать=Истина;
	НаборЗаписейОстатки.Резервировать=РезультатЗапросаПоТоварам<>Неопределено;
	НаборЗаписейОстатки.ПоБазовомуКоличеству=Ложь;
	//НаборЗаписейОстатки.ДвиженияПоРознице=ПодразделениеКомпании.ПереоценкаРозницаПоРасходу И СкладКомпании.Розничный;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
	Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // дальше смысла не имеет
	КонецЕсли;
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыПокупателей.Количество()>0 ИЛИ Движения.ЗаказыРаспределение.Количество()>0));
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			// БИЗТЕХ КОВАЛЬ 09.06.2025 ++
			// Добавил тип ЗаявкаНаРемонт так как она является заказом и по ней есть резервирование
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаявкаНаРемонт,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			// БИЗТЕХ КОВАЛЬ 09.06.2025 --
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		//движения появились после записи, нужно обновить сумму
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ЭтотОбъект.ОбменДанными.Загрузка = Истина;
		Записать(РежимЗаписиДокумента.Запись);
		ЭтотОбъект.ОбменДанными.Загрузка = Ложь;
	КонецЕсли;
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				//КодыМаркировки
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Количество = тс.Количество; 
				
				Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
				
				//КодыМаркировкиВПроизводстве
				Движения.КодыМаркировкиВПроизводстве.Записывать = Истина;
				Движение = Движения.КодыМаркировкиВПроизводстве.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Цех = Цех; 
				Движение.ЗаказНаряд = ДокументОснование; 
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Количество = тс.Количество;  
				
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

//БизТех Аляль 19.12.24г. находим заказы на основании ЗН
Функция ПолучитьЗаказыПоЗН(ЗН)   
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
					|	ЗаказПокупателя.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
					|ГДЕ
					|	ЗаказПокупателя.ДокументОснование = &ЗН"; 
	ДокЗаявкаНаРемонт = Неопределено;
	Если ЗначениеЗаполнено(ЗН.ДокументОснование) Тогда
		Если ТипЗНЧ(ЗН.ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
			ДокЗаявкаНаРемонт = ЗН.ДокументОснование;
		ИначеЕсли ЗначениеЗаполнено(ЗН.ДокументОснование.ДокументОснование) Тогда
			Если ТипЗНЧ(ЗН.ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда  
				ДокЗаявкаНаРемонт = ЗН.ДокументОснование.ДокументОснование;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	Если НЕ ДокЗаявкаНаРемонт = Неопределено Тогда 
		Запрос.Текст = Запрос.Текст + " ИЛИ ЗаказПокупателя.ДокументОснование = &ЗаявкаНаРемонт";
		Запрос.УстановитьПараметр("ЗаявкаНаРемонт", ДокЗаявкаНаРемонт);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЗН", ЗН.Ссылка); 
	//Запрос.УстановитьПараметр("ЗаявкаНаРемонт", ?(ЗначениеЗаполнено(ЗН.ДокументОснование), ЗН.ДокументОснование, Неопределено));
	Рез = Запрос.Выполнить().Выгрузить();
	Если Рез.Количество()>0 Тогда	
		Возврат Рез;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

//БизТех Аляль 19.12.24г. ищем документ поступление товаров по ЗаказуПокупателя
Функция НайтиПартиюПоЗаказу(Номенклатура, Количество, Заказы)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыРаспределение.Количество КАК КоличествоОстаток,
	               |	ЗаказыРаспределение.Регистратор КАК Регистратор
	               |ИЗ
	               |	РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
	               |ГДЕ
	               |	ЗаказыРаспределение.ЗаказПокупателя В(&ЗаказПокупателя)
	               |	И ЗаказыРаспределение.ХозОперация = &ХозОперация
	               |	И ЗаказыРаспределение.Номенклатура = &Номенклатура";
	Запрос.УстановитьПараметр("ЗаказПокупателя", Заказы);
	Запрос.УстановитьПараметр("ХозОперация", Справочники.ХозОперации.ПоступлениеТоваров);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	//Рез = Запрос.Выполнить().Выгрузить();  
	//Если Рез.Количество()>0 Тогда	
	//	Возврат Рез[0].Регистратор;  
	//Иначе
	//	Возврат Неопределено;
	//КонецЕсли;
	ДокПартия = Неопределено;
	РезультатЗапроса = Запрос.Выполнить();   
	Если не РезультатЗапроса.Пустой() тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		НужноСписать = 0;
		ОсталосьСписать = Количество; 
		Пока ВыборкаДетальныеЗаписи.Следующий() и ОсталосьСписать>0 Цикл  
			Если ВыборкаДетальныеЗаписи.КоличествоОстаток >= Количество и ОсталосьСписать=Количество Тогда
				ОсталосьСписать = 0;  
				ДокПартия = ВыборкаДетальныеЗаписи.Регистратор;
			Иначе  
				Сообщить("Не хватает количества товара " +Номенклатура+ " по заказу для указания партии!");
				//НужноСписать = Мин(ОсталосьСписать,ВыборкаДетальныеЗаписи.КоличествоОстаток);
				//Если НужноСписать<ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				//	ДокПартия = ВыборкаДетальныеЗаписи.Регистратор;
				//Иначе
				//	 
				//КонецЕсли;
				//ОсталосьСписать = ОсталосьСписать - НужноСписать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	Возврат ДокПартия;
		
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Модуль документа Опрос

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Назначает тип ответа в зависимости от вопроса.
//
// Параметры:
//	Вопрос - Собственно, сам вопрос
//
// Возвращаемое значение:
//	нет
//
Процедура НазначитьТипОтвета(Вопрос) Экспорт
	
	Вопрос.ТиповойОтвет = Вопрос.Вопрос.ТипЗначения.ПривестиЗначение();
	
КонецПроцедуры // НазначитьТипОтвета()

// Загружает вопросы по образцу указанной анкеты.
//
// Параметры: 
//	ОбразецЗаполнения	- анкета-образец
//
// Возвращаемое значение:
//	нет
//
Процедура ЗаполнитьВопросыАнкеты(ОбразецЗаполнения) Экспорт
	
	Вопросы.Загрузить(ОбразецЗаполнения.ВопросыАнкеты.Выгрузить());
	
	Для Каждого Вопрос Из Вопросы Цикл
		
		Если НЕ ЗначениеЗаполнено(Вопрос.ТиповойОтвет) тогда
			НазначитьТипОтвета(Вопрос);
		Конецесли;
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеВопросы=Новый Структура();
	//ОбязательныеВопросы.Вставить("Вопрос",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ОпрашиваемоеЛицо",1);
	ОбязательныеРеквизиты.Вставить("ТиповаяАнкета",1);
	ОбязательныеРеквизиты.Вставить("Вопросы",ОбязательныеВопросы);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("Опрос","Опрос",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

// Заполнение свойств из документа основания
//
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Опрос"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОпрос(ТабДокумент) Экспорт

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ТиповаяАнкета",	ТиповаяАнкета);
	Запрос.УстановитьПараметр("Опрос",			Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТиповыеАнкетыВопросыАнкеты.Ссылка,
	|	ОпросВопросы.Ответ,
	|	ОпросВопросы.ТиповойОтвет,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос КАК Вопрос,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ТипЗначения,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ТипОтветаНаВопрос,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.КоличествоСтрокТаблицы,
	|	ТиповыеАнкетыВопросыАнкеты.НомерСтроки КАК НомерСтрокиВАнкете,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Код КАК КодВопроса,
	|	ОпросСоставнойОтвет.НомерСтрокиВТаблице,
	|	ОпросСоставнойОтвет.Ответ КАК ОтветВТаблицу,
	|	ОпросСоставнойОтвет.ТиповойОтвет КАК ТиповойОтветВТаблицу,
	|	ВопросыДляАнкетированияКолонкиТаблицы.НомерСтроки КАК НомерКолонки,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы,
	|	ОпросВопросы.ТиповойОтвет.Код КАК КодВариантаОтвета,
	|	ОпросСоставнойОтвет.ТиповойОтвет.Код КАК КодВариантаОтветаНесколько,
	|	ВариантыОтветовОпросов.Код
	|ИЗ
	|	Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Опрос.Вопросы КАК ОпросВопросы
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ОпросВопросы.Вопрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Опрос.СоставнойОтвет КАК ОпросСоставнойОтвет
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросов
	|			ПО ОпросСоставнойОтвет.ТиповойОтвет = ВариантыОтветовОпросов.Ссылка
	|				И (ОпросСоставнойОтвет.Ссылка = &Опрос)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВопросыДляАнкетирования.КолонкиТаблицы КАК ВопросыДляАнкетированияКолонкиТаблицы
	|			ПО ОпросСоставнойОтвет.Вопрос = ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы
	|				И ОпросСоставнойОтвет.ВопросВладелец = ВопросыДляАнкетированияКолонкиТаблицы.Ссылка
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ОпросСоставнойОтвет.ВопросВладелец
	|			И (ОпросСоставнойОтвет.Ссылка = &Опрос)
	|ГДЕ
	|	ОпросВопросы.Ссылка = &Опрос
	|	И ТиповыеАнкетыВопросыАнкеты.Ссылка = &ТиповаяАнкета
	|ИТОГИ ПО
	|	НомерСтрокиВАнкете,
	|	Вопрос";
	
	МакетАнкеты = ТиповаяАнкета.МакетАнкеты.Получить();
	Если МакетАнкеты = Неопределено Тогда
		МакетАнкеты = Новый ТабличныйДокумент();
		
		Если НЕ ЗначениеЗаполнено(ТиповаяАнкета) Тогда
			Сообщить("Документ не может быть распечатан. Выберите типовую анкету.");
			Возврат Неопределено;
		КонецЕсли;
		
		МакетАнкеты = ТиповаяАнкета.ПолучитьОбъект().СформироватьМакет(МакетАнкеты);
		
	ИначеЕсли ТипЗнч(МакетАнкеты) = Тип("ТабличныйДокумент") тогда
		Если МакетАнкеты.ВысотаТаблицы = 0 Тогда
			МакетАнкеты = Новый ТабличныйДокумент();
			МакетАнкеты = ТиповаяАнкета.ПолучитьОбъект().СформироватьМакет(МакетАнкеты);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбластьНомераДокумента			= МакетАнкеты.Области["ОбластьНомераДокумента"];
	ОбластьНомераДокумента.Текст	= "Опрос №"+Номер;
	ОбластьКИ = МакетАнкеты.ПолучитьОбласть("КонтактнаяИнформация");
	Если ТипЗнч(ОпрашиваемоеЛицо) <> Тип("Строка") Тогда
		Если ОпрашиваемоеЛицо.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
			МакетАнкеты.Область("R5C3").Текст = ОпрашиваемоеЛицо.Фамилия + " " + ОпрашиваемоеЛицо.Имя + " " + ОпрашиваемоеЛицо.Отчество;
		Иначе
			МакетАнкеты.Область("R5C3").Текст  = ОпрашиваемоеЛицо;
		КонецЕсли;
		
		ЗапросТелефон = Новый Запрос;
		ЗапросТелефон.Текст = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Представление КАК Телефон
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &Контрагент
		|	И КонтактнаяИнформация.Тип = &Тип
		|
		|УПОРЯДОЧИТЬ ПО
		|	КонтактнаяИнформация.ЗначениеПоУмолчанию УБЫВ";
		
		ЗапросТелефон.УстановитьПараметр("Контрагент", ОпрашиваемоеЛицо);
		ЗапросТелефон.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
		
		Выборка= ЗапросТелефон.Выполнить().Выбрать();
		ПредставлениеТелефона = "";
		Пока Выборка.Следующий() Цикл
			ПредставлениеТелефона = ПредставлениеТелефона + Выборка.Телефон + ", ";
		КонецЦикла;
		
		МакетАнкеты.Область("R7C3").Текст = Лев(ПредставлениеТелефона, СтрДлина(ПредставлениеТелефона) -2);
		МакетАнкеты.Область("R6C3").Текст = ?(ЗначениеЗаполнено(ОпрашиваемоеЛицо.ДатаРождения),Формат(ОпрашиваемоеЛицо.ДатаРождения,"ДФ=dd.MM.yyyy"),"");
		ЗапросПочта = Новый Запрос;
		ЗапросПочта.Текст = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Представление КАК Email
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &Контрагент
		|	И КонтактнаяИнформация.Тип = &Тип
		|
		|УПОРЯДОЧИТЬ ПО
		|	КонтактнаяИнформация.ЗначениеПоУмолчанию УБЫВ";
		
		ЗапросПочта.УстановитьПараметр("Контрагент", ОпрашиваемоеЛицо);
		ЗапросПочта.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		
		Выборка= ЗапросПочта.Выполнить().Выбрать();
		ПредставлениеПочты = "";
		Пока Выборка.Следующий() Цикл
			ПредставлениеПочты = ПредставлениеПочты + Выборка.Email + ", ";
		КонецЦикла;
		МакетАнкеты.Область("R8C3").Текст = Лев(ПредставлениеПочты, СтрДлина(ПредставлениеПочты) -2);
	Иначе
		МакетАнкеты.Область("R5C3").Текст  =  ОпрашиваемоеЛицо;
	КонецЕсли;

	ВыборкаЗапросаПоНомерамСтрок 	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "НомерСтрокиВАнкете");
	Пока ВыборкаЗапросаПоНомерамСтрок.Следующий() Цикл
		ВыборкаЗапросаПоВопросам = ВыборкаЗапросаПоНомерамСтрок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Вопрос");
		Пока ВыборкаЗапросаПоВопросам.Следующий() Цикл
			Если ВыборкаЗапросаПоВопросам.ВопросТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.ОдинИзВариантовОтвета или 
				 ВыборкаЗапросаПоВопросам.ВопросТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета тогда
				ВыборкаЗапросаПоВариантамОтвета = ВыборкаЗапросаПоВопросам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "");
				Пока ВыборкаЗапросаПоВариантамОтвета.Следующий() Цикл
					Если ВыборкаЗапросаПоВопросам.ВопросТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета тогда
						ИмяОбласти = "Вопрос" + ВыборкаЗапросаПоВариантамОтвета.КодВопроса + "ВариантОтвета" + ВыборкаЗапросаПоВариантамОтвета.КодВариантаОтветаНесколько;
					Иначе
						ИмяОбласти = "Вопрос" + ВыборкаЗапросаПоВариантамОтвета.КодВопроса + "ВариантОтвета" + ВыборкаЗапросаПоВариантамОтвета.КодВариантаОтвета;
					КонецЕсли;
					Попытка
					ОбластьОтвета=МакетАнкеты.Области[ИмяОбласти];
					ОбластьЧекБокса = МакетАнкеты.Область("R"+ОбластьОтвета.Верх+"C"+(ОбластьОтвета.Лево));
					ОбластьЧекБокса.Текст = "R";
					ОбластьРазвернутогоОтвета = МакетАнкеты.Область("R"+ОбластьОтвета.Верх+"C"+(ОбластьОтвета.Право+1));
					ОбластьРазвернутогоОтвета.Текст = ОбластьРазвернутогоОтвета.Текст +"   "+ ВыборкаЗапросаПоВариантамОтвета.Ответ + ВыборкаЗапросаПоВариантамОтвета.ОтветВТаблицу;
					Исключение
					КонецПопытки;
				КонецЦикла;
				
			ИначеЕсли ВыборкаЗапросаПоВопросам.ВопросТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный тогда
				ВыборкаЗапросаПоОтветам = ВыборкаЗапросаПоВопросам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "");
				Пока ВыборкаЗапросаПоОтветам.Следующий() Цикл
					Если ВыборкаЗапросаПоОтветам.НомерКолонки = NULL тогда
						Продолжить;
					КонецЕсли;
					ИмяОбласти = "Вопрос" + ВыборкаЗапросаПоОтветам.КодВопроса+"ОтветТаблицы"+ВыборкаЗапросаПоОтветам.НомерКолонки+"Строка"+ВыборкаЗапросаПоОтветам.НомерСтрокиВТаблице;
					Попытка
						//МакетАнкеты.Области[ИмяОбласти].Текст = ВыборкаЗапросаПоОтветам.ТиповойОтветВТаблицу;
						МакетАнкеты.Параметры[ИмяОбласти] = ВыборкаЗапросаПоОтветам.ТиповойОтветВТаблицу;;
					Исключение
						Сообщить("Набор вопросов в анкете и в документе ""Опрос"" различны! Не найден вопрос с кодом " + ВыборкаЗапросаПоОтветам.КодВопроса);
					КонецПопытки;
				КонецЦикла;
				
			Иначе
				ВыборкаЗапросаПоОтветам = ВыборкаЗапросаПоВопросам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "");
				Пока ВыборкаЗапросаПоОтветам.Следующий() Цикл
					ИмяОбласти = "ТиповойОтвет" + ВыборкаЗапросаПоОтветам.КодВопроса;
					Попытка
						//МакетАнкеты.Области[ИмяОбласти].Текст = СтрЗаменить(""+ВыборкаЗапросаПоОтветам.ТиповойОтвет, "¤", ",");
						ТиповойОтвет = ?(ВыборкаЗапросаПоОтветам.ТиповойОтвет = Истина, "Да", ВыборкаЗапросаПоОтветам.ТиповойОтвет);
						ТиповойОтвет = ?(ВыборкаЗапросаПоОтветам.ТиповойОтвет = Ложь, "Нет", ВыборкаЗапросаПоОтветам.ТиповойОтвет);
						МакетАнкеты.Параметры[ИмяОбласти] = СтрЗаменить(""+ТиповойОтвет, "¤", ",");
					Исключение
						Сообщить("Набор вопросов в анкете и в документе ""Опрос"" различны! Не найден вопрос с кодом " + ВыборкаЗапросаПоОтветам.КодВопроса);
					КонецПопытки;
				КонецЦикла;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТабДокумент = Новый ТабличныйДокумент();
	Если МакетАнкеты = Неопределено Тогда
		МакетАнкеты = Новый ТабличныйДокумент();
		Если НЕ ЗначениеЗаполнено(ТиповаяАнкета) Тогда
			Сообщить("Документ не может быть распечатан. Выберите типовую анкету.");
			Возврат Неопределено;
		КонецЕсли;
		МакетАнкеты = ТиповаяАнкета.ПолучитьОбъект().СформироватьМакет(МакетАнкеты);
	КонецЕсли;
	ТабДокумент.Вывести(МакетАнкеты);
	
	Возврат ТабДокумент;

КонецФункции // ПечатьОпрос()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
                         
// Обработчик события при установке нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ТиповыеАнкеты") Тогда
		ТиповаяАнкета = Основание;
		ЗаполнитьВопросыАнкеты(Основание);
	//ИначеЕсли ТипЗнч(Основание) = Тип("СправочникСсылка.Контрагенты") Тогда	
	//	ОпрашиваемоеЛицо = Основание;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РабочийЛист") Или 
		ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияАвтомобилей") Или
		ТипЗнч(Основание) = Тип("ДокументСсылка.Событие") Или
		ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваров") Или
		ТипЗнч(Основание) = Тип("ДокументСсылка.ТестДрайв") Или 
		ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ОпрашиваемоеЛицо = Основание.Контрагент;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Или
		ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ОпрашиваемоеЛицо = Основание.Заказчик;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ТестДрайв") Тогда
		ОпрашиваемоеЛицо = Основание.Контрагент;
		
		Если ЗначениеЗаполнено(Основание.Автомобиль) Тогда
			Автомобиль = Основание.Автомобиль;
			
			//получим доступные анкеты
			ПредАнкета = Неопределено;
			ИтогАнкета = Неопределено;
			Анкеты = Документы.ТестДрайв.ПолучитьАнкетыТестДрайва(Дата,Автомобиль.Модель);	
			Если ТипЗнч(Анкеты)=Тип("Структура") Тогда
				ПредАнкета = Анкеты.ПредАнкета;
				ИтогАнкета = Анкеты.ИтогАнкета;
			КонецЕсли;
			
			//получим введенные ранее документы
			ПредОпрос = Неопределено;
			ИтогОпрос = Неопределено;
			Опросы = Документы.ТестДрайв.ПолучитьОпросыТестДрайва(Основание.Ссылка,ПредАнкета,ИтогАнкета);
			Если ТипЗнч(Опросы)=Тип("Структура") Тогда
				ПредОпрос = Опросы.ПредОпрос;
				ИтогОпрос = Опросы.ИтогОпрос;
			КонецЕсли;
			
			//подставим еще не заполненную анкету
			Если ЗначениеЗаполнено(ПредОпрос) Тогда
				Если НЕ ЗначениеЗаполнено(ИтогОпрос) И ЗначениеЗаполнено(ИтогАнкета) Тогда
					ТиповаяАнкета = ИтогАнкета;	
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(ПредАнкета) Тогда
					ТиповаяАнкета = ПредАнкета;						
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	#Если Клиент Тогда
		Оповестить("ОбновитьДеревоСобытий", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

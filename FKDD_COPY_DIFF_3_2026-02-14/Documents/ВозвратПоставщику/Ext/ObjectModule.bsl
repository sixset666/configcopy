// Модуль документа ВозвратПоставщику

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// получает цену на товар из документа поставки
//
// Параметры:
//  ДокПоставки                - Характеристика.ТипыПартий,
//  Номенклатура               - СправочникСсылка.Номенклатура,
//  СтавкаНДС                  - СправочникСсылка.СтавкиНДС,
//  ХарактеристикаНоменклатуры - СправочникСсылка.ХарактеристикиНоменклатуры,
//
// Возвращаемое значение:
//  Цена - число.
//
Функция ПолучитьЦенуИзДокументаПоставки(ДокПоставки, ГТД, Номенклатура, ХарактеристикаНоменклатуры = Неопределено)
	// Проверки
	СтруктураЦены = Новый Структура("СуммаВсего,Количество", 0, 0);
	Если НЕ ЗначениеЗаполнено(СкладКомпании) Тогда
		Возврат СтруктураЦены;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДокПоставки) Тогда
		Возврат СтруктураЦены;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат СтруктураЦены;
	КонецЕсли;
	
	// Получаем цены
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
	                    |	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК Сумма,
	                    |	ПартииТоваровКомпанииОстатки.СуммаУпрОстаток КАК СуммаУпр,
	                    |	ЕСТЬNULL(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК КоличествоГТД
	                    |ИЗ
	                    |	РегистрНакопления.ПартииТоваровКомпании.Остатки(
	                    |			&НаМомент,
	                    |			СкладКомпании = &СкладКомпании
	                    |				И Номенклатура = &Номенклатура
	                    |				И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
	                    |				И Партия = &Партия) КАК ПартииТоваровКомпанииОстатки
	                    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	                    |				&НаМомент,
	                    |				СкладКомпании = &СкладКомпании
	                    |					И Номенклатура = &Номенклатура
	                    |					И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
	                    |					И Партия = &Партия
	                    |					И ГТД = &ГТД) КАК ГТДПартийТоваровКомпанииОстатки
	                    |		ПО ПартииТоваровКомпанииОстатки.СкладКомпании = ГТДПартийТоваровКомпанииОстатки.СкладКомпании
	                    |			И ПартииТоваровКомпанииОстатки.Номенклатура = ГТДПартийТоваровКомпанииОстатки.Номенклатура
	                    |			И ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры = ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	                    |			И ПартииТоваровКомпанииОстатки.Партия = ГТДПартийТоваровКомпанииОстатки.Партия");
    НаМомент = ?(Ссылка.Пустая(), Дата, Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("НаМомент", НаМомент);
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("Партия", ДокПоставки);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("ГТД", ГТД);

	РезультатСумма = Запрос.Выполнить().Выбрать();
	Если РезультатСумма.Следующий() Тогда
		ВалютаПересчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Если ВалютаДокумента = ВалютаПересчета Тогда
			СуммаНоменклатуры = РезультатСумма.Сумма;
		Иначе
			ВалютаПересчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			СуммаНоменклатуры = РезультатСумма.СуммаУпр;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ГТД) Тогда
			Если РезультатСумма.Количество > РезультатСумма.КоличествоГТД Тогда
				СуммаНоменклатуры = Окр(СуммаНоменклатуры * РезультатСумма.КоличествоГТД / РезультатСумма.Количество, 2);
				КоличествоНоменклатуры = РезультатСумма.КоличествоГТД;
			Иначе
				КоличествоНоменклатуры = РезультатСумма.Количество;
			КонецЕсли;
		Иначе
			КоличествоНоменклатуры = РезультатСумма.Количество;
		КонецЕсли;
	    КурсНаМомент = ?(Ссылка.Пустая(), Дата, Новый Граница(МоментВремени(), ВидГраницы.Включая));
		КурсВалютыПересчета = обКурсДляВалюты(ВалютаПересчета, КурсНаМомент);
		СтруктураЦены.СуммаВсего=обПересчет(СуммаНоменклатуры,ВалютаПересчета,КурсВалютыПересчета,ВалютаДокумента,КурсДокумента);
		СтруктураЦены.Количество=КоличествоНоменклатуры;
	КонецЕсли; 

	Возврат СтруктураЦены;
КонецФункции // ПолучитьЦенуИзДокументаПоставки()

// Возвращает ставку НДС партии
//
// Параметры:
//  ТекСтрока               - Строка табличной части Товары
//
// Возвращаемое значение:
//  Возвращает ставку НДС по регистру накопления "ПартииТоваровКомпании"
//
Функция ПолучитьСтавкуНДСПартии(ТекСтрока)

	Запрос  = Новый Запрос;
	Запрос.Текст  =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПартииТоваровКомпании.Количество,
	|	ПартииТоваровКомпании.Сумма,
	|	ПартииТоваровКомпании.СтавкаНДС
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Регистратор
	|	И ПартииТоваровКомпании.Номенклатура = &Номенклатура
	|	И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
	|	И ПартииТоваровКомпании.СкладКомпании = &СкладКомпании
	|	И ПартииТоваровКомпании.Партия = &Партия
	|";
	
	Запрос.УстановитьПараметр("Регистратор",ТекСтрока.Партия);
	Запрос.УстановитьПараметр("Номенклатура",ТекСтрока.Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
	Запрос.УстановитьПараметр("Партия",ТекСтрока.Партия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
	    Возврат Выборка.СтавкаНДС;
	Иначе	
	    Возврат ТекСтрока.СтавкаНДС;
	КонецЕсли; 
	
КонецФункции // ПолучитьСтавкуНДСПартии()

// Возвращает количество партии
//
// Параметры:
//  Номенклатура               - СправочникСсылка.Номенклатура,
//  ХарактеристикаНоменклатуры - СправочникСсылка.ХарактеристикиНоменклатуры,
//  Партия                     - Характеристика.ТипыПартий.
//
// Возвращаемое значение:
//  Возвращает остатки по регистру накопления "ПартииТоваровКомпании"
//
Функция ПолучитьКоличествоПартии(Номенклатура,ХарактеристикаНоменклатуры,Партия,ГТД)
	СтруктураОтбора=Новый Структура("СкладКомпании,Номенклатура,ХарактеристикаНоменклатуры",СкладКомпании,Номенклатура,ХарактеристикаНоменклатуры);
	Если НЕ обЗначениеНеЗаполнено(Партия) Тогда
		СтруктураОтбора.Вставить("Партия",Партия);
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(ГТД) Тогда
		СтруктураОтбора.Вставить("ГТД",ГТД);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ГТД) Тогда
		Остатки=РегистрыНакопления.ПартииТоваровКомпании.Остатки(?(Ссылка.Пустая(),Дата,Новый Граница(МоментВремени(),ВидГраницы.Исключая)),СтруктураОтбора,,"Количество");
	Иначе
		Остатки=РегистрыНакопления.ГТДПартийТоваровКомпании.Остатки(?(Ссылка.Пустая(),Дата,Новый Граница(МоментВремени(),ВидГраницы.Исключая)),СтруктураОтбора,,"Количество");
	КонецЕсли;
	Остатки.Свернуть("","Количество");
	Если Остатки.Количество()=0 Тогда
		Возврат 0;
	Иначе
		Возврат Остатки[0].Количество;
	КонецЕсли; 
КонецФункции // ПолучитьКоличествоПартии()

Функция ТаблицаКодовМаркировкиПоОснованию(Основание)
	
	ТаблицаКодовМаркировки = Новый ТаблицаЗначений();
	ТаблицаКодовМаркировки.Колонки.Добавить("Номенклатура");
	ТаблицаКодовМаркировки.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаКодовМаркировки.Колонки.Добавить("КодМаркировки");
	ТаблицаКодовМаркировки.Колонки.Добавить("КодМаркировкиBase64");
	
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЗнч(Основание));
	Если НЕ дкДокументЕстьТабЧасть(МетаданныеДокумента.Имя, "КодыМаркировки") Тогда
		Возврат ТаблицаКодовМаркировки;
	КонецЕсли;
	
	ИмяДокумента = Основание.Метаданные().Имя;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	Товары.ИдентификаторТовара КАК ИдентификаторТовара
	               |ПОМЕСТИТЬ ТаблицаТоваров
	               |ИЗ
	               |	Документ.#ИмяДокумента.Товары КАК Товары
	               |ГДЕ
	               |	Товары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КодыМаркировки.КодМаркировки КАК КодМаркировки,
	               |	КодыМаркировки.КодМаркировкиBase64 КАК КодМаркировкиBase64,
	               |	КодыМаркировки.ИдентификаторТовара КАК ИдентификаторТовара
	               |ПОМЕСТИТЬ ТаблицаКМ
	               |ИЗ
	               |	Документ.#ИмяДокумента.КодыМаркировки КАК КодыМаркировки
	               |ГДЕ
	               |	КодыМаркировки.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаКМ.КодМаркировки КАК КодМаркировки,
	               |	ТаблицаКМ.КодМаркировкиBase64 КАК КодМаркировкиBase64
	               |ИЗ
	               |	ТаблицаТоваров КАК ТаблицаТоваров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКМ КАК ТаблицаКМ
	               |		ПО ТаблицаТоваров.ИдентификаторТовара = ТаблицаКМ.ИдентификаторТовара";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяДокумента", ИмяДокумента);
	Запрос.УстановитьПараметр("Ссылка", Основание);
	
	ТаблицаМаркировки = Запрос.Выполнить().Выгрузить();
	
	// Получим актуальные статусы
	ТаблицаМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	ТаблицаМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	// Разберем маркировку на состовляющие для поиска
	Для Каждого ТекущийКодМаркировки Из ТаблицаМаркировки Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураМаркировки =
			ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировки);
		
		// Это не маркировка товара
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийКодМаркировки.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		ТекущийКодМаркировки.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
	КонецЦикла;
	
	ТекущиеСтатусыМаркировки = РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(
		ТаблицаМаркировки,
		ТекущаяДатаСеанса()
	);
	
	// Проверим коды на выбытие
	СостоянияВОбороте = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
	
	Для Каждого ТекущаяСтрока Из ТекущиеСтатусыМаркировки Цикл
		
		// Исключим из рассмотрения коды, которые не в обороте.
		Если СостоянияВОбороте.Найти(ТекущаяСтрока.Состояние) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаКодовМаркировки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		
	КонецЦикла;
	
	Возврат ТаблицаКодовМаркировки;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("Партия",?(обПраво("РежимСписанияПриВозвратеПоставщику",Права,,ЭтотОбъект)=Перечисления.РежимыСписанияПриВозврате.ТолькоПоУказаннойПартии,3,2));
	ОбязательныеТовары.Вставить("ГТД",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);	
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если Не обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Если ДокументОснование.Контрагент<>Контрагент Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Контрагент документа основания отличается от контрагента текущего документа ");
		КонецЕсли;
		
		Если ДокументОснование.ДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Договор взаиморасчетов документа основания отличается от договора взаиморасчетов текущего документа ");
		КонецЕсли;
	КонецЕсли; 	
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);   		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;  	
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки, Истина);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВозвратТоваровПоставщику","Возврат товаров поставщику",Истина);
	СписокХозОпераций.Добавить("ВозвратТоваровПоставщикуКомиссия","Возврат товаров поставщику (комиссия)");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	ЕСТЬNULL(ДокТовары.СуммаВсего,0) КАК СуммаВсего
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(СуммаВсего) КАК СуммаВсего ИЗ Документ."+Метаданные().Имя+".Товары ГДЕ Ссылка=&Ссылка) КАК ДокТовары
	|	ПО Истина
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Определим необходимость корректирующих проводок для поддержания баланса по подразделениям.
	ПодразделениеСклад                 = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны     = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);

	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		// Доходы и расходы на себестоимость не оприходованных партий
		Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.Доход                  = ШапкаДокумента.СуммаВсего;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		Возврат НЕ Отказ;
	КонецЕсли;
	
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании             = ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам = Неопределено;
	НаборЗаписейПартии.ИмяРеквизитаДокумент      = "Партия";
	НаборЗаписейПартии.Сторно                    = Истина;
	НаборЗаписейПартии.ШапкаДокумента            = ШапкаДокумента;
	НаборЗаписейПартии.СтатусПартии              = ?(ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия,Перечисления.СтатусыПартий.ТоварПринятыйКомиссия,Перечисления.СтатусыПартий.ТоварКупленный);
	Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
	
	// Движения по Доходам и Расходам
	Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия Тогда
		// определим суммы для товаров и услуг в документе
		СуммаУслуг=0;
		СуммаТоваров=0;
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры =  &Услуга ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ) КАК СуммаУслуг,
		|	СУММА(ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры <> &Услуга ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ) КАК СуммаТоваров
		|ИЗ
		|	Документ.ВозвратПоставщику.Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &ДокументСсылка
		|
		|");
		Запрос.УстановитьПараметр("ДокументСсылка",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СуммаУслуг	 = Выборка.СуммаУслуг;
			СуммаТоваров = Выборка.СуммаТоваров;
		КонецЕсли;
		
		ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
			КурсУпр 	   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр        = ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;	
		
		СуммаУслуг   = обПересчет(СуммаУслуг,  ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр);
		СуммаТоваров = обПересчет(СуммаТоваров,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр);
		ДоходТовары  = -НаборЗаписейПартии.Итог("СуммаУпр");
		
		Если (НЕ БалансВедетсяПоПодразделениям ИЛИ Не БалансовыеПодразделенияНеРавны) И обМОД(СуммаТоваров-ДоходТовары)>=0.01 Тогда
			// запишем суммы в регистр
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.ОтклонениеСтоимостиВозврата;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= СуммаТоваров-ДоходТовары;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		// если возвращали услуги
		Если СуммаУслуг<>0 Тогда
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.ВозвратУслугПоставщика;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Доход					= СуммаУслуг;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		Если БалансВедетсяПоПодразделениям и БалансовыеПодразделенияНеРавны Тогда	
			// запишем суммы в регистр
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.Подразделение			= ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Расход					= СуммаУслуг;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;

	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
				
		Возврат Рез;
	ИначеЕсли Имя="СкладКомпании" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ Товары.Количество() = 0 Тогда
			Для Каждого стрТовары Из Товары Цикл
				ОбработкаРеквизита("Товары.Ячейка",стрТовары,ЭтаФорма);
			КонецЦикла;
		КонецЕсли;
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли;
			ДопПараметры.Вставить("Контрагент",Контрагент);
        КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Количество=ПолучитьКоличествоПартии(ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.Партия,ТекСтрока.ГТД);
		ТекСтрока.Количество=Количество/?(ТекСтрока.Коэффициент=0,1,ТекСтрока.Коэффициент);
		Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		СтруктураЦены=ПолучитьЦенуИзДокументаПоставки(ТекСтрока.Партия,ТекСтрока.ГТД,ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры);
		ТекСтрока.СтавкаНДС = ПолучитьСтавкуНДСПартии(ТекСтрока);
		Если СтруктураЦены.СуммаВсего>0 И СтруктураЦены.Количество Тогда
			ТекСтрока.СуммаВсего=СтруктураЦены.СуммаВсего*ТекСтрока.Количество/СтруктураЦены.Количество;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		// ставку НДС 
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
				
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Количество=ПолучитьКоличествоПартии(ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.Партия,ТекСтрока.ГТД);
		ТекСтрока.Количество=Количество/?(ТекСтрока.Коэффициент=0,1,ТекСтрока.Коэффициент);
		Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		СтруктураЦены=ПолучитьЦенуИзДокументаПоставки(ТекСтрока.Партия,ТекСтрока.ГТД,ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры);
		Если СтруктураЦены.СуммаВсего>0 И СтруктураЦены.Количество Тогда
			ТекСтрока.СуммаВсего=СтруктураЦены.СуммаВсего*ТекСтрока.Количество/СтруктураЦены.Количество;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Партия" ИЛИ Имя="Товары.ГТД" Тогда
		Количество=ПолучитьКоличествоПартии(ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.Партия,ТекСтрока.ГТД);
		ТекСтрока.Количество =  Мин(ТекСтрока.Количество,Количество/?(ТекСтрока.Коэффициент=0,1,ТекСтрока.Коэффициент));
		Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		СтруктураЦены=ПолучитьЦенуИзДокументаПоставки(ТекСтрока.Партия,ТекСтрока.ГТД,ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры);
		ТекСтрока.СтавкаНДС = ПолучитьСтавкуНДСПартии(ТекСтрока);
		Если СтруктураЦены.СуммаВсего>0 И СтруктураЦены.Количество Тогда
			ТекСтрока.СуммаВсего=СтруктураЦены.СуммаВсего*ТекСтрока.Количество/СтруктураЦены.Количество;
		КонецЕсли;
		Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Ячейка" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) ИЛИ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Возврат Истина;
		КонецЕсли;
		ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладКомпании);
		ТекСтрока.Ячейка=ЯчейкаСсылка;
		Возврат Истина
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ВозвратПоставщику - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Для контрагентов из ЕАЭС другая форма отчетности
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(Контрагент.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ОсвобождентОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Товары.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ВозвратПоставщикуТовары.Количество * ВозвратПоставщикуТовары.Коэффициент) КАК КоличествоОсталось,
	|	СУММА(ВозвратПоставщикуТовары.СуммаВсего - ВозвратПоставщикуТовары.СуммаНДС) КАК СуммаОсталось,
	|	ВозвратПоставщикуТовары.Партия КАК Партия,
	|	ВозвратПоставщикуТовары.ГТД КАК ГТД
	|ИЗ
	|	Документ.ВозвратПоставщику.Товары КАК ВозвратПоставщикуТовары
	|ГДЕ
	|	ВозвратПоставщикуТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратПоставщикуТовары.Номенклатура,
	|	ВозвратПоставщикуТовары.Партия,
	|	ВозвратПоставщикуТовары.ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочнаяСчетФактураУменьшение) КАК КодОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД КАК РНПТ,
	|	СУММА(-ГТДПартийТоваровКомпании.Количество) КАК КоличествоПрослеживаемости,
	|	ГТДПартийТоваровКомпании.Партия КАК Партия
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
	|ГДЕ
	|	ГТДПартийТоваровКомпании.Регистратор = &Ссылка
	|	И ГТДПартийТоваровКомпании.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпании.Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД,
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаТовараПоИнвентаризации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратУтерянногоТовара)
	|	КОНЕЦ,
	|	ГТДПартийТоваровКомпании.Партия";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	// Проверим есть РНПТ у документа
	Если ПакетЗапросов[1].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаТоваров = ПакетЗапросов[0].Выгрузить();
	ТаблицаРНПТ = ПакетЗапросов[1].Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = Дата;
		НоваяСтрока.Контрагент = Контрагент;
		
		// Получим сумму товара
		КоличествоРНПТ = ТекущаяСтрока.КоличествоПрослеживаемости;
		СтруктураПоиска = Новый Структура("Номенклатура,ГТД,Партия");
		СтруктураПоиска.ГТД = ТекущаяСтрока.РНПТ;
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтруктураПоиска.ГТД = Справочники.ГТД.ПустаяСсылка();
			НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		СтрокаНоменклатуры = НайденныеСтроки[0];
		
		Если КоличествоРНПТ >= СтрокаНоменклатуры.КоличествоОсталось Тогда
			СуммаБезНДС = СтрокаНоменклатуры.СуммаОсталось;
		Иначе
			СуммаБезНДС = Окр(СтрокаНоменклатуры.СуммаОсталось /
				?(СтрокаНоменклатуры.КоличествоОсталось = 0, 1, СтрокаНоменклатуры.КоличествоОсталось)
				* КоличествоРНПТ, 2);
		КонецЕсли;
		НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(обПересчет(НоваяСтрока.СуммаБезНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		КонецЕсли;
		
		СтрокаНоменклатуры.КоличествоОсталось = СтрокаНоменклатуры.КоличествоОсталось - КоличествоРНПТ;
		СтрокаНоменклатуры.СуммаОсталось = СтрокаНоменклатуры.СуммаОсталось - СуммаБезНДС;
		Если СтрокаНоменклатуры.КоличествоОсталось <= 0 Тогда
			ТаблицаТоваров.Удалить(СтрокаНоменклатуры);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаОперацииПрослеживаемости.Свернуть(
		"Организация,ПериодОтчета,ОтчетностьОперации,КодОперации,Документ,Контрагент,Номенклатура,РНПТ,ВидДокумента,НомерДокумента,ДатаДокумента",
		"КоличествоПрослеживаемости,СуммаБезНДС");
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВозвратПоставщику(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВозвратПоставщику");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//для начала настроим макет
	
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОтправителя = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	ОбластьМакета.Параметры.ПредставлениеПолучателя	= спПолучитьПредставление(Контрагент);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	// Выводим документ основания, если он не равен ВозвратОтПокупателя или Справочники.Контрагенты. 
	Если ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ВводОстатковТоваров") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ВводОстатковТоваровВПроизводстве") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда 
			ОбластьМакета = Макет.ПолучитьОбласть("Основание"); 
			ОбластьМакета.Параметры.ДокументОснованиеПредставление = спПолучитьНаименование(ДокументОснование); 
			ОбластьМакета.Параметры.ДокументОснование = ДокументОснование;
			ТабДокумент.Вывести(ОбластьМакета); 
	КонецЕсли;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);	
				
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
    	
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
	Получил.ПолучилКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Получил.ПолучилКонтрагент),"","/ "+ Получил.ПолучилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВозвратПоставщику()

// Формирует печатную форму "ТОРГ-12"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получис р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.ПодразделениеКомпании, СтруктураПредставления2, ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикОрганизация",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = ""; 
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	//!!! TimR: В новой пф данное поле не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	
	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		
	
	ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 

	ВыборкаСтрок = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();

	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,, ЭтотОбъект);
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.ТоварНаименование			= СтруктураСтроки.ТоварНаименование + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "");

		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС    = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", Формат(СуммаНДС, ФорматВыводаСуммы));
		ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
		
		// Рассчитаем цену.
		ОбластьСтрока.Параметры.ЦенаБезНДС = Формат(СуммаБезНДС / (СтрокаТоваров.Количество * ?(СтрокаТоваров.Коэффициент = 0, 1, СтрокаТоваров.Коэффициент)), ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьВсего);
			//!!! TimR: В новой пф данное поле не выводится
			//мсвДопОбластиПодвала.Добавить(ОбластьВсегоСтавкаНДС);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		Если КоличествоСтрокНаТекущейСтранице=0 Тогда
			ТабДокумент.Вывести(ОбластьСтрока);
		Иначе
			//выводим строку, делая проверку попадания на лист		
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		КонецЕсли; 
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.Количество;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			КоличествоСтрокНаТекущейСтранице=0;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Иначе
			КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 1 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = Формат(ВыборкаСтрок.Итог("Количество"), ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	//!!! TimR: В новой пф данное поле не выводится
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "
	//	+дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьВсегоСтавкаНДС, , , НомерСтраницы,,ЭтотОбъект);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
	Получил.Вставить("ПолучилПредставление", Получил.ПолучилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьТОРГ12()

//Функция печати Акт о возврате ТМЦ
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьМХ3(ТабДокумент) Экспорт
	
	Макет = ПолучитьОбщийМакет("МХ3");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.НомерДокумента = Номер;
	ОбластьЗаголовок.Параметры.ДатаДокумента  = Дата;
	ОбластьЗаголовок.Параметры.Заполнить(ЭтотОбъект);
	ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО				= Организация.КодПоОКПО;
	ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП	= Организация.КодПоОКДП;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		ОбластьЗаголовок.Параметры.ПредставлениеВладельца = спПолучитьПредставление(Контрагент);
		Если Тип("СправочникСсылка.СкладыКомпании") = ТипЗнч(Контрагент) Тогда
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = Контрагент.Организация.КодПоОКПО
		Иначе
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = Контрагент.КодПоОКПО;
		КонецЕсли;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьЗаголовок);
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьШапка);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ВыборкаСтрок = Товары.Выгрузить();
	НомерСтроки = 1;
	ВыводитьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,ЭтотОбъект);
	
	ЕстьЦена = НЕ (ВыборкаСтрок.Колонки.Найти("Цена") = Неопределено);
	ЕстьСуммаВсего = НЕ (ВыборкаСтрок.Колонки.Найти("СуммаВсего") = Неопределено);
	
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
		Если ВыводитьКод Тогда
			ОбластьСтрока.Параметры.Код = Строка(СтрокаТоваров.Номенклатура.Артикул);
		КонецЕсли;
		ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Номенклатура);
		ОбластьСтрока.Параметры.Характеристика = Строка(СтрокаТоваров.ХарактеристикаНоменклатуры);
		ОбластьСтрока.Параметры.ЕдиницаИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.КодОКЕИ = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена       =  ?(ЕстьЦена, Формат(СтрокаТоваров.Цена,ФорматВыводаСуммы), "");
		ОбластьСтрока.Параметры.СуммаВсего =  ?(ЕстьСуммаВсего, Формат(СтрокаТоваров.СуммаВсего,ФорматВыводаСуммы), "");
		ТабДокумент.Вывести(ОбластьСтрока);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"),ФорматВыводаКоличества);
	ОбластьИтого.Параметры.Цена       = ?(ЕстьЦена, Формат(ВыборкаСтрок.Итог("Цена"),ФорматВыводаСуммы), "");
	ОбластьИтого.Параметры.СуммаВсего = ?(ЕстьСуммаВсего, Формат(ВыборкаСтрок.Итог("СуммаВсего"),ФорматВыводаСуммы), "");
	ТабДокумент.Вывести(ОбластьИтого);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьПодвал);
	Возврат ТабДокумент;
КонецФункции // ПечатьМХ3(ТабДокумент)

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Формирует печатную форму "УПД"
// Возвращает сформированный табличный документ:
Функция ПечатьУПД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
	
	Возврат ТабДокумент;
	
КонецФункции //ПечатьУПД()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

//Получение табличной части товаров по документу
//
// Возвращаемое значение: 
//  Результат запроса - результат запроса по табличной части товаров
Функция ПолучитьТаблицуТоваров()
	
	ТипЦенСклада = СкладКомпании.ТипЦенРозничнойТорговли;
	АлгоритмПолученияЦены        = ТипЦенСклада.АлгоритмПолученияЦены;
	ВедетсяУчетПоЕдиницам        = (АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения);
	ВедетсяУчетПоХарактеристикам = (АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоХарактеристике);
	
	//Запрос.УстановитьПараметр("ПодразделениеОрг", Справочники.ПодразделенияКомпании.ОсновноеПодразделение);
	
	ОсновноеПодразделение = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
	ДобавитьОсновноеПодразделение = (НЕ ОсновноеПодразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка());
	
	ТаблицаИерархииПодразделений = Новый ТаблицаЗначений;
	ТаблицаИерархииПодразделений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаИерархииПодразделений.Колонки.Добавить("Уровень",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	
	ТекущееПодразделение = ПодразделениеКомпании;
	Уровень = 0;
	Пока ЗначениеЗаполнено(ТекущееПодразделение) Цикл
		СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
		СтрокаПодразделения.Подразделение = ТекущееПодразделение;
		СтрокаПодразделения.Уровень       = Уровень;
		
		Если ТекущееПодразделение = ОсновноеПодразделение Тогда
			ДобавитьОсновноеПодразделение = Ложь;
		КонецЕсли;
		
		// Получаем родителя подразделения
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		Уровень = Уровень + 1;
	КонецЦикла;
	
	Если ДобавитьОсновноеПодразделение Тогда
		СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
		СтрокаПодразделения.Подразделение = ОсновноеПодразделение;
		СтрокаПодразделения.Уровень       = Уровень;
		Уровень = Уровень + 1;
	КонецЕсли;
	
	СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
	СтрокаПодразделения.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
	СтрокаПодразделения.Уровень = Уровень;
	
	ТекстЦена     = "ТаблицаЦен.Цена";
	РабочийТипЦен = ТипЦенСклада;
	
	Запрос = Новый Запрос;
	
	ТекстСоединенийПроцентыСкидкиНаценки = "";
	// Если расчетная цена получим базовую цену
	Счетчик = 1;
	Пока РабочийТипЦен.Рассчитывается Цикл
		// ведется ли расчет по типу номенклатуры
		Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
			ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
			ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
			|ПО
			|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
			|	ТаблицаЦен.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
			
			ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
			
			Запрос.УстановитьПараметр("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
			
			Счетчик = Счетчик + 1;
		Иначе
			
			Если РабочийТипЦен.ПроцентСкидкиНаценки <> 0 Тогда
				ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
			КонецЕсли;
			
		КонецЕсли;
		
		РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
	КонецЦикла;
	
	ТекстВыборкиЦен = "";
	ТекстСоединениеЦен = "";
	Если ВедетсяУчетПоЕдиницам Тогда
		
		ТекстВыборкиЦен = ",
		|	СУММА(ЕСТЬNULL(ТаблицаЦен.Цена, ЕСТЬNULL(ТаблицаЦенБаза.Цена, ЕСТЬNULL(ТаблицаЦенОбщ.Цена, 0)))*ТаблДок.Количество) КАК СуммаРозничная";
		
		ТекстСоединениеЦен = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ИтоговаяТаблицаЦен КАК ТаблицаЦен
		|ПО
		|	ТаблДок.Номенклатура = ТаблицаЦен.Номенклатура И
		|	ТаблДок.ЕдиницаИзмерения = ТаблицаЦен.ЕдиницаИзмерения
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ИтоговаяТаблицаЦен КАК ТаблицаЦенБаза
		|ПО
		|	ТаблДок.Номенклатура = ТаблицаЦенБаза.Номенклатура И
		|	ТаблицаЦенБаза.ЭтоБазовая = ИСТИНА
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ИтоговаяТаблицаЦен КАК ТаблицаЦенОбщ
		|ПО
		|	ТаблДок.Номенклатура = ТаблицаЦенОбщ.Номенклатура И
		|	ТаблицаЦенОбщ.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|";
	ИначеЕсли ВедетсяУчетПоХарактеристикам Тогда
		
		ТекстВыборкиЦен = ",
		|	СУММА(ЕСТЬNULL(ТаблицаЦен.Цена, ЕСТЬNULL(ТаблицаЦенОбщ.Цена, 0))*ТаблДок.Количество) КАК СуммаРозничная";
		
		ТекстСоединениеЦен = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ИтоговаяТаблицаЦен КАК ТаблицаЦен
		|ПО
		|	ТаблДок.Номенклатура = ТаблицаЦен.Номенклатура И
		|	ТаблДок.ХарактеристикаНоменклатуры = ТаблицаЦен.ХарактеристикаНоменклатуры
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ИтоговаяТаблицаЦен КАК ТаблицаЦенОбщ
		|ПО
		|	ТаблДок.Номенклатура = ТаблицаЦенОбщ.Номенклатура И 
		|	ТаблицаЦенОбщ.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|";
	Иначе
		
		ТекстВыборкиЦен = ",
		|	СУММА(ЕСТЬNULL(ТаблицаЦен.Цена, 0)*ТаблДок.Количество) КАК СуммаРозничная";
		
		ТекстСоединениеЦен = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ИтоговаяТаблицаЦен КАК ТаблицаЦен
		|ПО
		|	ТаблДок.Номенклатура = ТаблицаЦен.Номенклатура
		|";
	КонецЕсли;
	
	ТекстЦена = ТекстЦена + "*ТабВалют.Курс";
	
	ВалютаИзНоменклатуры = РабочийТипЦен.ВВалютеУчета;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаПодразделений.Подразделение КАК Подразделение,
	|	ТаблицаПодразделений.Уровень КАК Уровень
	|ПОМЕСТИТЬ
	|	ТаблицаПодразделений
	|ИЗ
	|	&ТаблицаПодразделений КАК ТаблицаПодразделений
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|ВЫБРАТЬ
	|	ВозвратПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ВозвратПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	"+?(ВедетсяУчетПоЕдиницам,"ВозвратПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,", "")+"
	|	СУММА(ВозвратПоставщикуТовары.Количество * ВозвратПоставщикуТовары.Коэффициент) КАК Количество,
	|	0 КАК СуммаСкидки,
	|	0 КАК Резерв
	|ПОМЕСТИТЬ
	|	ТаблДокумента
	|ИЗ
	|	Документ.ВозвратПоставщику.Товары КАК ВозвратПоставщикуТовары
	|ГДЕ
	|	ВозвратПоставщикуТовары.Ссылка = &Ссылка И
	|	ВозвратПоставщикуТовары.Номенклатура.ВидНоменклатуры <> &Услуга
	|СГРУППИРОВАТЬ ПО
	|	ВозвратПоставщикуТовары.Номенклатура,
	|	ВозвратПоставщикуТовары.ХарактеристикаНоменклатуры"+?(ВедетсяУчетПоЕдиницам, ",
	|	ВозвратПоставщикуТовары.ЕдиницаИзмерения", "")+"
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблДокумента.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ
	|	ОтборПоНоменклатуре
	|ИЗ
	|	ТаблДокумента КАК ТаблДокумента
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	Валюты.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА Валюты.Кратность = 0 Тогда
	|			Валюты.Курс
	|		ИНАЧЕ
	|			Валюты.Курс/Валюты.Кратность
	|	КОНЕЦ КАК Курс
	|	
	|ПОМЕСТИТЬ
	|	ТабВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, "+?(ВалютаИзНоменклатуры, "", "Валюта = &ВалютаЦен")+") КАК Валюты
	|ГДЕ
	|	Валюты.Курс > 0
	|;
	|
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура" + ?(ВедетсяУчетПоЕдиницам, ",
	|	ЦеныСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения", "") + ?(ВедетсяУчетПоХарактеристикам, ",
	|	ЦеныСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры", "") + ",
	|	ТаблицаПодразделений.Уровень КАК Уровень,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦен
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			&МоментВремени,
	|			ТипЦен = &ТипЦен И 
	|			Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)" + ?(НЕ ВедетсяУчетПоЕдиницам, " И 
	|			ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)", "") + ?(НЕ ВедетсяУчетПоХарактеристикам, " И 
	|			ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)", "") + " И 
	|			Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ОтборПоНоменклатуре) И 
	|			ПодразделениеКомпании В (ВЫБРАТЬ Подразделение ИЗ ТаблицаПодразделений)) КАК ЦеныСрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаПодразделений КАК ТаблицаПодразделений
	|ПО
	|	ЦеныСрезПоследних.ПодразделениеКомпании = ТаблицаПодразделений.Подразделение
	|ГДЕ
	|	ЦеныСрезПоследних.Цена > 0
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаПодразделений
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура" + ?(ВедетсяУчетПоЕдиницам, ",
	|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения", "") + ?(ВедетсяУчетПоХарактеристикам, ",
	|	ТаблицаЦен.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры", "") + ",
	|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦен КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Номенклатура" + ?(ВедетсяУчетПоЕдиницам, ",
	|	ТаблицаЦен.ЕдиницаИзмерения", "") + ?(ВедетсяУчетПоХарактеристикам, ",
	|	ТаблицаЦен.ХарактеристикаНоменклатуры", "") + "
	|;
	|
	|ВЫБРАТЬ
	|	СрезПоследних.Номенклатура КАК Номенклатура" + ?(ВедетсяУчетПоЕдиницам, ",
	|	СрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА СрезПоследних.Номенклатура.БазоваяЕдиницаИзмерения = СрезПоследних.ЕдиницаИзмерения.ЕдиницаПоКлассификатору И 
	|			СрезПоследних.ЕдиницаИзмерения.Коэффициент = 1 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоБазовая", "") + ?(ВедетсяУчетПоХарактеристикам, ",
	|	СрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры", "") + ",
	|	" + ТекстЦена + " КАК Цена
	|ПОМЕСТИТЬ
	|	ИтоговаяТаблицаЦен
	|ИЗ
	|	СрезПоследних КАК СрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦен КАК ТаблицаЦен
	|ПО
	|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура" + ?(ВедетсяУчетПоЕдиницам, " И 
	|	СрезПоследних.ЕдиницаИзмерения = ТаблицаЦен.ЕдиницаИзмерения", "") + ?(ВедетсяУчетПоХарактеристикам, " И 
	|	СрезПоследних.ХарактеристикаНоменклатуры = ТаблицаЦен.ХарактеристикаНоменклатуры", "") + " И 
	|	СрезПоследних.Уровень = ТаблицаЦен.Уровень
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ТабВалют КАК ТабВалют
	|ПО
	|	" + ?(ВалютаИзНоменклатуры, "СрезПоследних.Номенклатура.ВалютаУчета = ТабВалют.Валюта", "ИСТИНА") + "
	|" + ТекстСоединенийПроцентыСкидкиНаценки + "
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаЦен
	|;
	|
	|ВЫБРАТЬ
	|	&СкладКомпании                             КАК СкладКомпании,
	|	ИтоговаяТаблица.Номенклатура               КАК Номенклатура,
	|	ИтоговаяТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ИтоговаяТаблица.Количество                 КАК Количество,
	|	ИтоговаяТаблица.СуммаСкидки                КАК СуммаСкидки,
	|	ИтоговаяТаблица.Резерв                     КАК Резерв,
	|	ВЫБОР
	|		КОГДА ИтоговаяТаблица.СуммаРозничная = 0 ТОГДА
	|			ИтоговаяТаблица.СуммаРозничнаяПоОст
	|		ИНАЧЕ
	|			ИтоговаяТаблица.СуммаРозничная
	|	КОНЕЦ КАК СуммаРозничная,
	|	ВЫБОР
	|		КОГДА ИтоговаяТаблица.СуммаРозничная = 0 ТОГДА
	|			ИтоговаяТаблица.ЦенаРозничная
	|		КОГДА ИтоговаяТаблица.Количество = 0 ТОГДА
	|			0
	|		ИНАЧЕ
	|			ИтоговаяТаблица.СуммаРозничная/ИтоговаяТаблица.Количество
	|	КОНЕЦ КАК ЦенаРозничная
	|ИЗ
	|(	ВЫБРАТЬ
	|		ТаблДок.Номенклатура               КАК Номенклатура,
	|		ТаблДок.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		СУММА(ТаблДок.Количество)          КАК Количество,
	|		МАКСИМУМ(ТаблДок.СуммаСкидки)      КАК СуммаСкидки,
	|		МАКСИМУМ(ТаблДок.Резерв)           КАК Резерв"+ТекстВыборкиЦен+",
	|		СУММА(ВЫБОР
	|			КОГДА Остатки.КоличествоОстаток ЕСТЬ NULL ИЛИ Остатки.КоличествоОстаток = 0 Тогда
	|				0
	|			ИНАЧЕ
	|				(Остатки.СуммаРознОстаток/Остатки.КоличествоОстаток)*ТаблДок.Количество
	|		КОНЕЦ) КАК СуммаРозничнаяПоОст,
	|		МАКСИМУМ(ВЫБОР
	|			КОГДА Остатки.КоличествоОстаток ЕСТЬ NULL ИЛИ Остатки.КоличествоОстаток = 0 Тогда
	|				0
	|			ИНАЧЕ
	|				(Остатки.СуммаРознОстаток/Остатки.КоличествоОстаток)
	|		КОНЕЦ) КАК ЦенаРозничная
	|	ИЗ
	|		ТаблДокумента КАК ТаблДок
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&МоментВремени, СкладКомпании = &СкладКомпании) КАК Остатки
	|	ПО
	|		ТаблДок.Номенклатура = Остатки.Номенклатура И
	|		ТаблДок.ХарактеристикаНоменклатуры = Остатки.ХарактеристикаНоменклатуры" + ТекстСоединениеЦен + "
	|	СГРУППИРОВАТЬ ПО
	|		ТаблДок.Номенклатура,
	|		ТаблДок.ХарактеристикаНоменклатуры) КАК ИтоговаяТаблица
	|";
	
	Запрос.УстановитьПараметр("МоментВремени",        МоментВремени());
	Запрос.УстановитьПараметр("Ссылка",               Ссылка);
	Запрос.УстановитьПараметр("СкладКомпании",        СкладКомпании);
	Запрос.УстановитьПараметр("Услуга",               Перечисления.ВидыНоменклатуры.Услуга);
	Запрос.УстановитьПараметр("ТипЦен",               РабочийТипЦен);
	Запрос.УстановитьПараметр("ТаблицаПодразделений", ТаблицаИерархииПодразделений);
	Запрос.УстановитьПараметр("ВалютаЦен",            РабочийТипЦен.ВалютаЦены);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции // ПолучитьТаблицуТоваров()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если обЗначениеНеЗаполнено(ХозОперация) Тогда
		ХозОперация=Справочники.ХозОперации.ВозвратТоваровПоставщику;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВводОстатковТоваров") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
		// подставим тип цен закупки
		ТипЦен=обПраво("ОсновнойТипЦенЗакупки",Права,,ЭтотОбъект);
		Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
			ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
		КонецЕсли; 
	КонецЕсли; 
	
	Если Основание=Неопределено Тогда
		МассивУслуг=Новый Массив;
		Для каждого СтрокаТоваров Из Товары Цикл
			Если СтрокаТоваров.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
				МассивУслуг.Добавить(СтрокаТоваров);
			КонецЕсли; 
		КонецЦикла;
		Для каждого СтрокаТоваров Из МассивУслуг Цикл
			Товары.Удалить(СтрокаТоваров);
		КонецЦикла; 
	Иначе
		Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
			СтратегияСписанияПартийТоваровПоДатам=Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
		Иначе
			СтратегияСписанияПартийТоваровПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
		КонецЕсли;
		Если СтратегияСписанияПартийТоваровПоДатам=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
			Товары.Очистить();
			ТоварыОснование = ДокументОснование.Товары;
			Партия = Константы.ПартияТоваровОтрицательныхОстатков.Получить();
			Для Каждого ТекСтрока Из ТоварыОснование Цикл
				НоваяСтрока=Товары.Добавить();
				НоваяСтрока.Номенклатура=ТекСтрока.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры=ТекСтрока.ХарактеристикаНоменклатуры;
				НоваяСтрока.Партия=Партия;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				НоваяСтрока.Количество = ТекСтрока.Количество;
				ОбработкаРеквизита("Товары.Количество",НоваяСтрока);
				Попытка
					НоваяСтрока.ИдентификаторТовара = ТекСтрока.ИдентификаторТовара;
				Исключение
				КонецПопытки;
			КонецЦикла;
		Иначе
			
			КодыМаркировки.Очистить();
			ТаблицаКодовМаркировки = ТаблицаКодовМаркировкиПоОснованию(ДокументОснование);
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
			
			Партия = ДокументОснование;	
			Товары.Очистить();
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	ПартииТоваровКомпанииОстатки.Номенклатура,
			|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
			|ИЗ
			|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
			|		,
			|		Партия = &Партия
			|			И СкладКомпании = &СкладКомпании) КАК ПартииТоваровКомпанииОстатки
			|
			|СГРУППИРОВАТЬ ПО
			|	ПартииТоваровКомпанииОстатки.Номенклатура,
			|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры";
			Запрос.УстановитьПараметр("Партия",Партия);
			Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
			Выборка=Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока=Товары.Добавить();
				НоваяСтрока.Номенклатура=Выборка.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры=Выборка.ХарактеристикаНоменклатуры;
				НоваяСтрока.Партия=Партия;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				
				// Найдем КМ основания
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
				НайденныеСтроки = ТаблицаКодовМаркировки.НайтиСтроки(СтруктураПоиска);
				СтрокиУдалить = Новый Массив;
				
				// Заполним коды маркировки
				КоличествоЗаполнено = 0;
				Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
					НоваяСтрокаКМ = КодыМаркировки.Добавить();
					НоваяСтрокаКМ.ИдентификаторТовара = НоваяСтрока.ИдентификаторТовара;
					НоваяСтрокаКМ.КодМаркировки = ТекущаяСтрока.КодМаркировки;
					НоваяСтрокаКМ.КодМаркировкиBase64 = ТекущаяСтрока.КодМаркировкиBase64;
					СтрокиУдалить.Добавить(ТекущаяСтрока);
					КоличествоЗаполнено = КоличествоЗаполнено + 1;
					Если КоличествоЗаполнено = НоваяСтрока.Количество Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				// Удалим ранее заполненные коды из таблицы
				Для Каждого ТекущаяСтрока Из СтрокиУдалить Цикл
					ТаблицаКодовМаркировки.Удалить(ТекущаяСтрока);
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// изменим итоговую сумму документа
	СуммаДокумента=Товары.Итог("СуммаВсего");
	
	// проверим склад
	ОбработкаРеквизита("ПроверкаСкладаКомпании",,,"НЕ СООБЩАТЬ");
	
	ИдентификаторГосударственногоКонтракта = ?(ЗначениеЗаполнено(ИдентификаторГосударственногоКонтракта),
		ИдентификаторГосударственногоКонтракта,
		ДоговорВзаиморасчетов.ИдентификаторГосударственногоКонтракта);
	
	// Заполним доп. поля
	Для Каждого ТекущаяСтрока Из Товары Цикл
		Если ПустаяСтрока(ТекущаяСтрока.ИдентификаторТовара) Тогда
			ТекущаяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;  
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --
	
	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Определим необходимость корректирующих проводок, для поддержания баланса по подразделениям.
	ПодразделениеСклад                 = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны     = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	Если СкладКомпании.Розничный И ПодразделениеКомпании.ПереоценкаРозницаПоРасходу Тогда
		ТаблицаПоТоварам=ПолучитьТаблицуТоваров();
		СписыватьРозницуПоОстаткам=Ложь;
	Иначе
		ТаблицаПоТоварам=Неопределено;
		СписыватьРозницуПоОстаткам=Истина;
	КонецЕсли;
	// проведем остатки товаров
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=ТаблицаПоТоварам;
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.Приходовать=Истина;
	НаборЗаписейОстатки.СписыватьРозницуПоОстаткам=СписыватьРозницуПоОстаткам;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
	НаборЗаписейОстатки.Контрагент=Контрагент;
	НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная="Цена";
	НаборЗаписейОстатки.ИмяРеквизитаСуммаРозничная="Сумма";
	Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // дальше смысла не имеет
	КонецЕсли;
	
	// проведем взаиморасчеты
	Если ХозОперация<>Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		Иначе
			НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
		КонецЕсли;
		НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
		
		// Доходы и расходы по суммовым разницам.
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// Проведем партии товаров.
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// способ ведения не по подразделениям или балансовые подразделения равны то ничего не будем корректировать
	// Комиссионный товар не на балансе
	Если ХозОперация<>Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия И БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны Тогда
		
		СуммаВзаиморасчетов = Движения.ВзаиморасчетыКомпании.Итог("СуммаУпр");
		СуммаПартий = -Движения.ПартииТоваровКомпании.Итог("СуммаУпр");
		Если СуммаПартий<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = СкладКомпании.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СуммаПартий;
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		КонецЕсли;
		
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте = Ложь;
		НаборЗаписейДиР.Доход = СуммаДокумента;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		СостояниеКодаМаркировки = ?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо,
			Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаВозвращенФизическомуЛицу,
			Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику);
		
		// Изменим состояние маркировки
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.ПроверятьВыводИзОборота = Истина;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = СостояниеКодаМаркировки;
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверим наличие прослеживаемых товаров, которые были реализованы
	Если НЕ Отказ Тогда
		Движения.ГТДПартийТоваровКомпании.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;     	
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);   
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Количество = тс.Количество;  
				
				Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ОбработкаПроведения()

Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
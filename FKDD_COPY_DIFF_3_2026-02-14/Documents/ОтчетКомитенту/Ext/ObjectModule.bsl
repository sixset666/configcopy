// Модуль документа ОтчетКомитенту

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();	
	Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда		
		Для Каждого ВидНоменклатуры Из Перечисления.ВидыНоменклатуры Цикл
			Если ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
				Результат.Вставить(ВидНоменклатуры,0);
			КонецЕсли;
		КонецЦикла; 
	Иначе
		Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	КонецЕсли;	
	
	Возврат Результат;	
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда
		ОбязательныеТовары.Вставить("ДокументРеализации",3);
	Иначе	
		ОбязательныеТовары.Вставить("ДокументПоступления",3);
		ОбязательныеТовары.Вставить("ГТД",2);
	КонецЕсли;

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверим договор документов поступления, он должен совпадать с договором шапки
	Если Результат Тогда
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда
				ДокументДляПроверки			= СтрокаТовар.ДокументРеализации;
				ДоговорДокументаПоступления = ДокументДляПроверки.ДоговорКомитента;
				ТекстТипаДокумента	= "реализации";
			Иначе
				ДокументДляПроверки			= СтрокаТовар.ДокументПоступления;
				ДоговорДокументаПоступления = ДокументДляПроверки.ДоговорВзаиморасчетов;
				ТекстТипаДокумента	= "поступления";
			КонецЕсли;
			
			Если НЕ обЗначениеНеЗаполнено(ДокументДляПроверки) Тогда			
				Если ДоговорДокументаПоступления <> ДоговорВзаиморасчетов Тогда
					дкДобавитьОшибку(Ошибки,"Строка №"+СтрокаТовар.НомерСтроки+". Договор документов "+ТекстТипаДокумента+" """+СокрЛП(ДоговорДокументаПоступления.Наименование)+""" должен быть равен договору в шапке документа");
					Результат = Ложь;
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;	
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению); 			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыТовары = Новый Структура();
				Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда
					КонтролируемыеРеквизитыТовары.Вставить("ДокументРеализации");
				Иначе
					КонтролируемыеРеквизитыТовары.Вставить("ДокументПоступления");
				КонецЕсли;	
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций	= Новый СписокЗначений();
	СписокХозОпераций.Добавить("ОтчетКомитенту",	"Отчет комитенту",	Истина);
	СписокХозОпераций.Добавить("ОтчетАгента",		"Отчет агента",	Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ДокТовары.СуммаВсего КАК СуммаВсего,
	|	ДокТовары.Вознаграждение КАК Вознаграждение
	|	
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(СуммаВсего) КАК СуммаВсего, СУММА(Вознаграждение) КАК Вознаграждение ИЗ Документ."+Метаданные().Имя+".Товары ГДЕ Ссылка=&Ссылка) КАК ДокТовары
	|	ПО Истина
    |
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента = ПолучитьШапкуДокумента(ДокументСсылка);
	
	Попытка
		ЭтоОтчетАгента	= (ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ОтчетАгента);
	Исключение
		ЭтоОтчетАгента	= Ложь;
	КонецПопытки;
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
		НаборЗаписейДоходыИРасходы.Расход = ШапкаДокумента.СуммаВсего-ШапкаДокумента.Вознаграждение;
		НаборЗаписейДоходыИРасходы.Приход();
		Возврат НЕ Отказ;
	КонецЕсли;
	
	НаборЗаписейРеализованныеТовары					= Движения.РеализованныеТовары;
	НаборЗаписейРеализованныеТовары.ДокументОбъект	= ЭтотОбъект;
	НаборЗаписейРеализованныеТовары.Контрагент		= ШапкаДокумента.Контрагент;
	НаборЗаписейРеализованныеТовары.ДоговорВзаиморасчетов	= ШапкаДокумента.ДоговорВзаиморасчетов;
	НаборЗаписейРеализованныеТовары.ШапкаДокумента	= ШапкаДокумента;
	Отказ = НЕ НаборЗаписейРеализованныеТовары.Расход() ИЛИ Отказ;
	
	Если НЕ Отказ Тогда
		//доходы и расходы
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
			КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр = ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;
		
		СуммаУпр = НаборЗаписейРеализованныеТовары.Итог("СуммаУпр");
		СуммаДиР = Окр(обПересчет(ШапкаДокумента.СуммаВсего, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр)-СуммаУпр, 2);
		Если СуммаДиР<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = СуммаДиР;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход();
		КонецЕсли;
	КонецЕсли;
	
	//Итог Вознаграждение
	Если ШапкаДокумента.Вознаграждение<>0 Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Вознаграждение;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.Доход                  = ШапкаДокумента.Вознаграждение;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции // ПровестиПоПартиям()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ОтчетКомитенту ИЛИ
			ХозОперация = Справочники.ХозОперации.ОтчетАгента Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры	= Новый Структура();
			КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);			
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
				
		Рез	=  дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если (ЭтаФорма<>Неопределено) И
			 (НЕ ДоговорВзаиморасчетов.Пустая()) И
			 (ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом) Тогда
			Если Товары.Количество()>0 Тогда
				Если Вопрос("Перерассчитать сумму комиссионного вознаграждения?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
					ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;		
					//Пройдемся по ТЧ и рассчитаем сумму комиссионного вознаграждения
					Для Каждого СтрокаТЧ Из Товары Цикл
						Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
							ОбработкаРеквизита("Товары.Сумма",СтрокаТЧ);	
						Иначе
							ОбработкаРеквизита("Товары.СуммаПродажи",СтрокаТЧ);	
						КонецЕсли;
					КонецЦикла;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		#КонецЕсли
	    Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли;
			ДопПараметры.Вставить("Контрагент",Контрагент);
        КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку		
		Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда
			Если обЗначениеНеЗаполнено(ТекСтрока.ДокументРеализации) Тогда
				ТекСтрока.ДокументРеализации=ДокументОснование;
			КонецЕсли;
			Возврат ОбработкаРеквизита("Товары.ДокументРеализации",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Иначе
			Если обЗначениеНеЗаполнено(ТекСтрока.ДокументПоступления) Тогда
				ТекСтрока.ДокументПоступления=ДокументОснование;
			КонецЕсли;
			Возврат ОбработкаРеквизита("Товары.ДокументПоступления",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда
			Возврат ОбработкаРеквизита("Товары.ДокументРеализации",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Иначе
			Возврат ОбработкаРеквизита("Товары.ДокументПоступления",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;
		
		СуммаПоступления	= ТекСтрока.Сумма;
		Рез	= дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Т.к. работаем от суммы продажи, а не от суммы поступления
		// то сумму поступления следует оставить прежней, а рассчитанную сумму перенести в сумму продажи		
		Если НЕ ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			ТекСтрока.СуммаПродажи	= ТекСтрока.Сумма;
			ТекСтрока.Сумма			= СуммаПоступления;
		КонецЕсли;
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;		
        СуммаПоступления	= ТекСтрока.Сумма;
		Рез	= дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Т.к. работаем от суммы продажи, а не от суммы поступления
		// то сумму поступления следует оставить прежней, а рассчитанную сумму перенести в сумму продажи
		Если НЕ ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			ТекСтрока.СуммаПродажи	= ТекСтрока.Сумма;
			ТекСтрока.Сумма			= СуммаПоступления;
		КонецЕсли;		
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда		
		Рез	= дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаПродажи" Тогда 		
		Рез	= Истина;
		ЭтоОтчетАгента	= (ХозОперация=Справочники.ХозОперации.ОтчетАгента);
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;		
		
		Если ЭтоОтчетАгента И НЕ ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			СуммаПоступления		= ТекСтрока.Сумма;
			ТекСтрока.Сумма			= ТекСтрока.СуммаПродажи;
			Рез	= дкОбработкаРеквизита(ЭтотОбъект,"Товары.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаПродажи	= ТекСтрока.Сумма;
			ТекСтрока.Сумма			= СуммаПоступления;			
		КонецЕсли;				
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя = "Товары.СтавкаНДС" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;
				
		СуммаПоступления= ТекСтрока.Сумма;
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Т.к. работаем от суммы продажи, а не от суммы поступления
		// то сумму поступления следует оставить прежней, а рассчитанную сумму перенести в сумму продажи		
		Если НЕ ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			ТекСтрока.СуммаПродажи	= ТекСтрока.Сумма;
			ТекСтрока.Сумма			= СуммаПоступления;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.СуммаНДС" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ДокументПоступления" ИЛИ Имя="Товары.ГТД" ИЛИ Имя="Товары.ДокументРеализации" Тогда
		Рез = Истина;
		ЭтоОтчетАгента	= (ХозОперация=Справочники.ХозОперации.ОтчетАгента);
		Если ЭтоОтчетАгента Тогда
			ДокументПоступленияРеализации	= ТекСтрока.ДокументРеализации;
		Иначе
			ДокументПоступленияРеализации	= ТекСтрока.ДокументПоступления;
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(ДокументПоступленияРеализации) Тогда			
			ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
					
			Запрос = Новый Запрос;
			Если ЭтоОтчетАгента Тогда
				Запрос.Текст= "ВЫБРАТЬ
				              |	РеализованныеТоварыОстатки.Номенклатура,
				              |	СУММА(РеализованныеТоварыОстатки.Количество) КАК Количество,
				              |	СУММА(РеализованныеТоварыОстатки.СуммаПродажи) КАК СуммаПродажи,
				              |	СУММА(РеализованныеТоварыОстатки.СуммаПродажиРегл) КАК СуммаПродажиРегл,
				              |	СУММА(РеализованныеТоварыОстатки.СуммаУпр) КАК СуммаУпр,
				              |	СУММА(РеализованныеТоварыОстатки.СуммаРегл) КАК СуммаРегл
				              |ИЗ
				              |	РегистрНакопления.РеализованныеТовары КАК РеализованныеТоварыОстатки
				              |ГДЕ
				              |	РеализованныеТоварыОстатки.Регистратор = &ДокументПередачи
				              |	И РеализованныеТоварыОстатки.Номенклатура = &Номенклатура
				              |	И РеализованныеТоварыОстатки.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
				              |
				              |СГРУППИРОВАТЬ ПО
				              |	РеализованныеТоварыОстатки.Номенклатура";
			Иначе
				Запрос.Текст =
				"ВЫБРАТЬ
				|	РеализованныеТоварыОстатки.Номенклатура,
				|	РеализованныеТоварыОстатки.КоличествоОстаток КАК Количество,
				|	РеализованныеТоварыОстатки.СуммаПродажиОстаток КАК СуммаПродажи,
				|	РеализованныеТоварыОстатки.СуммаПродажиРеглОстаток КАК СуммаПродажиРегл,
				|	РеализованныеТоварыОстатки.СуммаУпрОстаток КАК СуммаУпр,
				|	РеализованныеТоварыОстатки.СуммаРеглОстаток КАК СуммаРегл
				|ИЗ
				|	РегистрНакопления.РеализованныеТовары.Остатки(,
				|			"+?(НЕ обЗначениеНеЗаполнено(ТекСтрока.ГТД),"ГТД = &ГТД И","")+" ДокументПередачи = &ДокументПередачи
				|			И Номенклатура     = &Номенклатура
				|			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК РеализованныеТоварыОстатки";
			КонецЕсли;
			Запрос.УстановитьПараметр("ДокументПередачи",           ДокументПоступленияРеализации);			
			Запрос.УстановитьПараметр("Номенклатура",               ТекСтрока.Номенклатура);
			Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ТекСтрока.ХарактеристикаНоменклатуры);
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ГТД) Тогда
				Запрос.УстановитьПараметр("ГТД", ТекСтрока.ГТД);
			КонецЕсли;
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
			Иначе
				ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
			КонецЕсли;			
			
			Если Выборка.Следующий() Тогда				
				Если Выборка.Количество <> 0 Тогда					
					// получим сумму для расчета					
					Если ВалютаДокумента = ВалютаУпр Тогда
						ТекСтрока.Сумма			= Выборка.СуммаУпр;
						ТекСтрока.СуммаПродажи	= Выборка.СуммаПродажи;
					ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
						ТекСтрока.Сумма			= Выборка.СуммаРегл;
						ТекСтрока.СуммаПродажи	= Выборка.СуммаПродажиРегл;
					Иначе						
						ТекСтрока.Сумма			= обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
						ТекСтрока.СуммаПродажи	= обПересчет(Выборка.СуммаПродажи,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
					КонецЕсли;					
					
					Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
						СуммаРасчет	= ТекСтрока.Сумма;
					Иначе
						СуммаРасчет	= ТекСтрока.СуммаПродажи;
					КонецЕсли;
	
					СтавкаНДС	= ТекСтрока.СтавкаНДС.Ставка;
					Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
						СуммаРасчет = Окр((СуммаРасчет*100)/(100+СтавкаНДС),2);
					КонецЕсли;
					ТекСтрока.Цена = Окр(СуммаРасчет/Выборка.Количество,2);
					
					Если ТекущаяКомиссияОтчетНаОснованииПродаж И ТекСтрока.Сумма>0 Тогда
						ТекСтрока.СуммаВсего	= ТекСтрока.Сумма;
						Рез	= ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока);
					ИначеЕсли НЕ ТекущаяКомиссияОтчетНаОснованииПродаж И ТекСтрока.СуммаПродажи>0 Тогда
						ТекСтрока.СуммаВсего	= ТекСтрока.СуммаПродажи;
						Рез	= ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока);
					Иначе
						Рез=ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
					КонецЕсли;					
				Иначе
					ТекСтрока.Цена = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез И ОбработкаРеквизита("РассчитатьВознаграждение", ТекСтрока, ЭтаФорма, ДопПараметры);
			
	ИначеЕсли Имя="РассчитатьВознаграждение" Тогда
		//расчет вознаграждения по реализованному товару
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			Если ДоговорВзаиморасчетов.ТипСуммыКомиссии=0 Тогда
				ТекСтрока.Вознаграждение= (ТекСтрока.СуммаВсего*ДоговорВзаиморасчетов.ПроцентКомиссионногоВознаграждения)/100;
			Иначе	
				ТекСтрока.Вознаграждение=ДоговорВзаиморасчетов.СуммаКомиссионногоВознаграждения;
			КонецЕсли;
		Иначе
			ТекСтрока.Вознаграждение=0;
		КонецЕсли;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Процедура предназначена для пересчета строк при изменении валюты
//
//	ТекСтрока			- Число				- строка табличной части
//	ВалютаНач			- СправочникСсылка	- Начальная валюта
//	ПоКурсуВалютыНач	- Число, Дата		- Курс валюты или дата курса
//	ВалютаКон			- СправочникСсылка	- Конечная валюта
//	ПоКурсуВалютыКон	- Число, Дата		- Курс валюты или дата курса
//
// Возвращаемое значение:
//	Число - Сумма после пересчета
//
Процедура ПересчитатьСуммыСтроки(ТекСтрока,СтараяВалюта,СтарыйКурс,НоваяВалюта,НовыйКурс) Экспорт
	Если ТекСтрока<>Неопределено И
		НЕ обЗначениеНеЗаполнено(СтараяВалюта) И
		НЕ обЗначениеНеЗаполнено(НоваяВалюта) Тогда
		ТекСтрока.Сумма			= обПересчет(ТекСтрока.Сумма, СтараяВалюта, СтарыйКурс, НоваяВалюта, НовыйКурс);
		ТекСтрока.СуммаПродажи	= обПересчет(ТекСтрока.СуммаПродажи, СтараяВалюта, СтарыйКурс, НоваяВалюта, НовыйКурс);
		ТекСтрока.Вознаграждение= обПересчет(ТекСтрока.Вознаграждение, СтараяВалюта, СтарыйКурс, НоваяВалюта, НовыйКурс);
	КонецЕсли;
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ОтчетКомитенту"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОтчетКомитенту(ТабДокумент) Экспорт
	Если Проведен Тогда
		ЭтоОтчетАгента	= (ХозОперация=Справочники.ХозОперации.ОтчетАгента);
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура               КАК Номенклатура,
		|	ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	"+?(ЭтоОтчетАгента,"ДокументРеализации","ДокументПоступления")+"        КАК ДокументПередачи
		|	ПОМЕСТИТЬ ТабДокумента
		|	ИЗ
		|		&ТабДокумента КАК Таб
		|;
		|ВЫБРАТЬ
		|	РеализованныеТовары.Номенклатура               КАК Номенклатура,
		|	РеализованныеТовары.ДоговорВзаиморасчетов      КАК ДоговорВзаиморасчетов,
		|	РеализованныеТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	РеализованныеТовары.ДокументПередачи           КАК ДокументПередачи,
		|	РеализованныеТовары.СуммаУпр                   КАК СуммаУпр,
		|	РеализованныеТовары.СуммаПродажи               КАК СуммаПродажи,
		|	РеализованныеТовары.СуммаРегл                  КАК СуммаРегл,
		|	РеализованныеТовары.СуммаПродажиРегл           КАК СуммаПродажиРегл		
		|ИЗ
		|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
		|ГДЕ
		|	РеализованныеТовары.Регистратор = &Регистратор
		|	И РеализованныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И РеализованныеТовары.ДоговорВзаиморасчетов = &Договор
		|	И (Номенклатура,ХарактеристикаНоменклатуры,ДокументПередачи) В 
		|(ВЫБРАТЬ
		|	ТабДокумента.Номенклатура,
		|	ТабДокумента.ХарактеристикаНоменклатуры,
		|	ТабДокумента.ДокументПередачи
		|	ИЗ
		|		ТабДокумента КАК ТабДокумента)";
		Запрос.УстановитьПараметр("Регистратор" , Ссылка);
		Запрос.УстановитьПараметр("Договор"     , ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("ТабДокумента", Товары.Выгрузить(,"Номенклатура,ХарактеристикаНоменклатуры"+?(ЭтоОтчетАгента,",ДокументРеализации",",ДокументПоступления")));	
		ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	Макет = ПолучитьМакет("ОтчетКомитенту");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеКомитента		= спПолучитьПредставление(Контрагент);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеКомиссионера = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего,Вознаграждение",ВалютаДокумента,0,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	ВалютаУпр	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРегл	= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	Отбор = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ДокументПередачи");
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		Если Проведен Тогда
			Отбор.Номенклатура               = СтрокаТабличнойЧасти.Номенклатура;
			Отбор.ХарактеристикаНоменклатуры = СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры;
			Если ЭтоОтчетАгента Тогда
				Отбор.ДокументПередачи           = СтрокаТабличнойЧасти.ДокументРеализации;
			Иначе
				Отбор.ДокументПередачи           = СтрокаТабличнойЧасти.ДокументПоступления;
			КонецЕсли;
			МассивСтрок         = ТаблицаРезультатаЗапроса.НайтиСтроки(Отбор);
			Если МассивСтрок.Количество() > 0 И СтрокаТабличнойЧасти.Коэффициент<>0 Тогда
				СтрокаПоступления   = МассивСтрок[0];
				Если ВалютаДокумента=ВалютаУпр Тогда
					СуммаПоступления    = СтрокаПоступления.СуммаУпр;
					ЦенаПоступления     = СтрокаПоступления.СуммаУпр/(СтрокаТабличнойЧасти.Количество*СтрокаТабличнойЧасти.Коэффициент);	
					СуммаПродажи		= СтрокаПоступления.СуммаПродажи;
					ЦенаПродажи			= СтрокаПоступления.СуммаПродажи/(СтрокаТабличнойЧасти.Количество*СтрокаТабличнойЧасти.Коэффициент);					
				ИначеЕсли ВалютаДокумента=ВалютаРегл Тогда
					СуммаПоступления    = СтрокаПоступления.СуммаРегл;
					ЦенаПоступления     = СтрокаПоступления.СуммаРегл/(СтрокаТабличнойЧасти.Количество*СтрокаТабличнойЧасти.Коэффициент);	
					СуммаПродажи		= СтрокаПоступления.СуммаПродажиРегл;
					ЦенаПродажи			= СтрокаПоступления.СуммаПродажиРегл/(СтрокаТабличнойЧасти.Количество*СтрокаТабличнойЧасти.Коэффициент);					
				Иначе
					СуммаПоступления    = обПересчет(СтрокаПоступления.СуммаУпр, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
					ЦенаПоступления     = обПересчет(СтрокаПоступления.СуммаУпр/(СтрокаТабличнойЧасти.Количество*СтрокаТабличнойЧасти.Коэффициент), ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);	
					СуммаПродажи		= обПересчет(СтрокаПоступления.СуммаПродажи, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
					ЦенаПродажи			= обПересчет(СтрокаПоступления.СуммаПродажи/(СтрокаТабличнойЧасти.Количество*СтрокаТабличнойЧасти.Коэффициент), ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);					
				КонецЕсли;
			Иначе
				ЦенаПоступления     = 0;
				СуммаПоступления    = 0;
				ЦенаПродажи			= 0;
				СуммаПродажи		= 0;
			КонецЕсли;
		Иначе 
			ЦенаПоступления     = 0;
			СуммаПоступления    = 0;
			ЦенаПродажи			= 0;
			СуммаПродажи		= 0;
		КонецЕсли;
		
		СтруктураСтроки.Вставить("ЦенаПоступления" ,	Формат(ЦенаПоступления,ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("СуммаПоступления",	Формат(СуммаПоступления,ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("Цена" ,				Формат(ЦенаПродажи,ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("СуммаВсего",			Формат(СуммаПродажи,ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("Вознаграждение"  , 	Формат(СтрокаТабличнойЧасти.Вознаграждение,ФорматВыводаСуммы));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	Если Проведен Тогда
		Если ВалютаДокумента=ВалютаУпр Тогда
			СуммаВсего     = ТаблицаРезультатаЗапроса.Итог("СуммаПродажи");	
		ИначеЕсли ВалютаДокумента=ВалютаРегл Тогда
			СуммаВсего     = ТаблицаРезультатаЗапроса.Итог("СуммаПродажиРегл")	
		Иначе
			РасчетнаяСуммаВсего	= ТаблицаРезультатаЗапроса.Итог("СуммаПродажи");
			СуммаВсего	= обПересчет(РасчетнаяСуммаВсего, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
	Иначе
		СуммаВсего = 0;	
	КонецЕсли;
	Вознаграждение = ВыборкаТабличнойЧасти.Итог("Вознаграждение"); 
	ОбластьПодвал.Параметры.СуммаВсего     = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.Вознаграждение = Формат(Вознаграждение,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьОтчетКомитенту()

// Формирует печатную форму "АктОбУслугах"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьАктОбУслугах(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("АктОбУслугах");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.Организация = Организация;
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.Контрагент = Контрагент;
	ОбластьМакета.Параметры.ПредставлениеКонтрагента = спПолучитьПредставление(ЭтотОбъект.Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ОбластьМакета.Параметры.СуммаВознаграждения	= Формат(СуммаВознаграждения, ФорматВыводаСуммы);
	ОбластьМакета.Параметры.ТекстЗаголовка		= "Акт об оказании услуг "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+ЭтотОбъект.Дата;
	ОбластьМакета.Параметры.ТекстОСуммеПрописью	= "Сумма комиссионного вознаграждения составила " + обЧислоПрописью(ЭтотОбъект.СуммаВознаграждения,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"Исполнитель");
	Исполнитель.ИсполнительПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.Исполнитель),"","/ "+ Исполнитель.ИсполнительПредставление);
	ОбластьМакета.Параметры.Заполнить(Исполнитель);
	Заказчик  = дкОтветственноеЛицо(ЭтотОбъект,"Заказчик");
	Заказчик.ЗаказчикПредставление = ?(обЗначениеНеЗаполнено(Заказчик.Заказчик),"","/ "+ Заказчик.ЗаказчикПредставление);
	ОбластьМакета.Параметры.Заполнить(Заказчик);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьАктОбУслугах()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	ЭтоРеализацияАгентскихУсулуг	= (НЕ обЗначениеНеЗаполнено(Основание) И НЕ ТипЗнч(Основание) = Тип("СправочникСсылка.Контрагенты") И Основание.ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг);
	// Необходимо указать соответствующую хозоперацию для корректного
	// заполнения договора из основания.
	Если (ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияТоваров")) И
		(НЕ обЗначениеНеЗаполнено(Основание)) И (ЭтоРеализацияАгентскихУсулуг) Тогда
		ХозОперация				= Справочники.ХозОперации.ОтчетАгента;
		Контрагент				= Основание.Комитент;
		ДоговорВзаиморасчетов	= Основание.ДоговорКомитента;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И 
		 (Основание.ХозОперация=Справочники.ХозОперации.ПоступлениеТоваровКомиссия ИЛИ
		  Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию ИЛИ
		  Основание.ХозОперация=Справочники.ХозОперации.РеализацияТоваров ИЛИ
		  Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхИПроданных) Тогда
		Для Каждого СтрокаТЧ Из Товары Цикл
			СтрокаТЧ.ДокументПоступления = Основание;
		КонецЦикла;
	ИначеЕсли (НЕ обЗначениеНеЗаполнено(Основание)) И (ЭтоРеализацияАгентскихУсулуг) Тогда
		Для Каждого СтрокаТЧ Из Товары Цикл
			СтрокаТЧ.ДокументРеализации = Основание;
		КонецЦикла;		 
	КонецЕсли;
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияТоваров") И 
		 (Основание.ХозОперация=Справочники.ХозОперации.РеализацияТоваров ИЛИ
		 Основание.ХозОперация=Справочники.ХозОперации.РеализацияАгентскихУслуг) Тогда
			Товары.Очистить();
			Запрос=Новый Запрос;
			Запрос.Текст=
			"ВЫБРАТЬ
			|	РеализованныеТовары.Контрагент,
			|	РеализованныеТовары.Номенклатура,
			|	РеализованныеТовары.ДоговорВзаиморасчетов,
			|	РеализованныеТовары.ХарактеристикаНоменклатуры,
			|	РеализованныеТовары.ДокументПередачи,
			|	РеализованныеТовары.ГТД,
			|	СУММА(РеализованныеТовары.Количество) КАК Количество,
			|	СУММА(РеализованныеТовары.СуммаПродажи) КАК СуммаПродажи,
			|	СУММА(РеализованныеТовары.СуммаПродажиРегл) КАК СуммаПродажиРегл,
			|	СУММА(РеализованныеТовары.СуммаУпр) КАК СуммаУпр,
			|	СУММА(РеализованныеТовары.СуммаРегл) КАК СуммаРегл			
			|ИЗ
			|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
			|ГДЕ
			|	РеализованныеТовары.Регистратор = &Регистратор
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализованныеТовары.ДоговорВзаиморасчетов,
			|	РеализованныеТовары.ДокументПередачи,
			|	РеализованныеТовары.ГТД,
			|	РеализованныеТовары.ХарактеристикаНоменклатуры,
			|	РеализованныеТовары.Номенклатура,
			|	РеализованныеТовары.Контрагент";
			Запрос.УстановитьПараметр("Регистратор", Основание);
			РезультатЗапроса=Запрос.Выполнить();
			Выборка=РезультатЗапроса.Выбрать();
			Если Выборка.Следующий() Тогда
				Контрагент				= Выборка.Контрагент;
				ДоговорВзаиморасчетов	= Выборка.ДоговорВзаиморасчетов;
			КонецЕсли;
			ВалютаУпр	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			ВалютаРегл	= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл
				НоваяСтрокаТоваров						= Товары.Добавить();
				НоваяСтрокаТоваров.Номенклатура			= Выборка.Номенклатура;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрокаТоваров);
				НоваяСтрокаТоваров.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				Если ХозОперация=Справочники.ХозОперации.ОтчетАгента Тогда
					НоваяСтрокаТоваров.ДокументРеализации	= Выборка.ДокументПередачи;
				ИНаче
					НоваяСтрокаТоваров.ДокументПоступления	= Выборка.ДокументПередачи;
					НоваяСтрокаТоваров.ГТД					= Выборка.ГТД;
				КонецЕсли;					
				НоваяСтрокаТоваров.Количество			= Выборка.Количество;
				
				Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
					ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
				Иначе
					ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
				КонецЕсли;				
				
				Если ВалютаДокумента = ВалютаУпр Тогда
					НоваяСтрокаТоваров.Сумма		= Выборка.СуммаУпр;
					НоваяСтрокаТоваров.СуммаПродажи	= Выборка.СуммаПродажи;
				ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
					НоваяСтрокаТоваров.Сумма		= Выборка.СуммаРегл;
					НоваяСтрокаТоваров.СуммаПродажи	= Выборка.СуммаПродажиРегл;
				Иначе
					НоваяСтрокаТоваров.Сумма		= обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);	
					НоваяСтрокаТоваров.СуммаПродажи	= обПересчет(Выборка.СуммаПродажи,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
				КонецЕсли;				
		
				Если ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрокаТоваров.Сумма>0 Тогда
					НоваяСтрокаТоваров.СуммаВсего	= НоваяСтрокаТоваров.Сумма;
					ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаТоваров);
				ИначеЕсли НЕ ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрокаТоваров.СуммаПродажи>0 Тогда
					НоваяСтрокаТоваров.СуммаВсего	= НоваяСтрокаТоваров.СуммаПродажи;
					ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаТоваров);
				КонецЕсли;		
			КонецЦикла;			
		КонецЕсли;
		
		Если (ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") И 
			(Основание.ХозОперация=Справочники.ХозОперации.ПоступлениеТоваровКомиссия)
			ИЛИ (ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковТоваров") И 
			Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию)) Тогда
			Контрагент=Основание.Контрагент;
			ДоговорВзаиморасчетов=Основание.ДоговорВзаиморасчетов;
			Товары.Очистить();
			Запрос=Новый Запрос;
			Запрос.Текст=
			"ВЫБРАТЬ
			|	РеализованныеТоварыОстатки.Номенклатура,
			|	РеализованныеТоварыОстатки.ХарактеристикаНоменклатуры,
			|	РеализованныеТоварыОстатки.ДокументПередачи,
			|	РеализованныеТоварыОстатки.ГТД,
			|	РеализованныеТоварыОстатки.КоличествоОстаток КАК Количество,
			|	РеализованныеТоварыОстатки.СуммаПродажиОстаток КАК СуммаПродажи,
			|	РеализованныеТоварыОстатки.СуммаПродажиРеглОстаток КАК СуммаПродажиРегл,
			|	РеализованныеТоварыОстатки.СуммаУпрОстаток КАК СуммаУпр,
			|	РеализованныеТоварыОстатки.СуммаРеглОстаток КАК СуммаРегл
			|ИЗ
			|	РегистрНакопления.РеализованныеТовары.Остатки(
			|			&Момент,
			|			Контрагент = &Контрагент
			|				И ДоговорВзаиморасчетов = &Договор) КАК РеализованныеТоварыОстатки";
			
			Запрос.УстановитьПараметр("Момент"     , ТекущаяДата());
			Запрос.УстановитьПараметр("Контрагент" , Контрагент);
			Запрос.УстановитьПараметр("Договор"    , ДоговорВзаиморасчетов);
			РезультатЗапроса=Запрос.Выполнить();
			Выборка=РезультатЗапроса.Выбрать();
			
			ВалютаУпр	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			ВалютаРегл	= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();			
			
			Пока Выборка.Следующий() Цикл
				НоваяСтрокаТоваров=Товары.Добавить();
				НоваяСтрокаТоваров.Номенклатура=Выборка.Номенклатура;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрокаТоваров);
				НоваяСтрокаТоваров.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				НоваяСтрокаТоваров.ДокументПоступления=Выборка.ДокументПередачи;
				НоваяСтрокаТоваров.ГТД=Выборка.ГТД;
				НоваяСтрокаТоваров.Количество=Выборка.Количество;
				
				Если ВалютаДокумента = ВалютаУпр Тогда
					НоваяСтрокаТоваров.Сумма		= Выборка.СуммаУпр;
				ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
					НоваяСтрокаТоваров.Сумма		= Выборка.СуммаРегл;
				Иначе
					НоваяСтрокаТоваров.Сумма		= обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);	
				КонецЕсли;				
				ОбработкаРеквизита("Товары.Сумма",НоваяСтрокаТоваров);
				
				Если ВалютаДокумента = ВалютаУпр Тогда
					НоваяСтрокаТоваров.СуммаПродажи = Выборка.СуммаПродажи;
				ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
					НоваяСтрокаТоваров.СуммаПродажи = Выборка.СуммаПродажиРегл;
				Иначе
					НоваяСтрокаТоваров.СуммаПродажи = обПересчет(Выборка.СуммаПродажи,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
				КонецЕсли;
				ОбработкаРеквизита("Товары.СуммаПродажи",НоваяСтрокаТоваров);
				
				Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
					ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
				Иначе
					ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
				КонецЕсли;				
				Если ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрокаТоваров.Сумма>0 Тогда
					НоваяСтрокаТоваров.СуммаВсего	= НоваяСтрокаТоваров.Сумма;
					ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаТоваров);
				ИначеЕсли НЕ ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрокаТоваров.СуммаПродажи>0 Тогда	
					НоваяСтрокаТоваров.СуммаВсего	= НоваяСтрокаТоваров.СуммаПродажи;
					ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаТоваров);
				КонецЕсли;				
			КонецЦикла;
		КонецЕсли;
	// посчитаем вознаграждение
	Для Каждого СтрокаТЧ Из Товары Цикл
		ОбработкаРеквизита("РассчитатьВознаграждение",СтрокаТЧ);
	КонецЦикла;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	СуммаВознаграждения=Товары.Итог("Вознаграждение");
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проведем взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	
	// подготовим таблицу для накопления сумм суммовых разниц по подразделениям
	ТаблицаСуммовыхРазниц = Новый ТаблицаЗначений();
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	
	ЭтоОтчетАгента	= (ХозОперация=Справочники.ХозОперации.ОтчетАгента);
	
	ТаблицаТоваров=Товары.Выгрузить();
	Если ЭтоОтчетАгента Тогда
		ТаблицаТоваров.Свернуть("ДокументРеализации","СуммаВсего,Вознаграждение");
	Иначе	
		ТаблицаТоваров.Свернуть("ДокументПоступления","СуммаВсего,Вознаграждение");
	КонецЕсли;
	Для каждого СтрокаВзаиморасчетов Из ТаблицаТоваров Цикл
		Если ЭтоОтчетАгента Тогда
			ДокументПоступленияРеализации	= СтрокаВзаиморасчетов.ДокументРеализации;
		Иначе
			ДокументПоступленияРеализации	= СтрокаВзаиморасчетов.ДокументПоступления;
		КонецЕсли;
		
		НаборЗаписейВзаиморасчеты.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения           = Режим;
		Если ЭтоОтчетАгента Тогда
			НаборЗаписейВзаиморасчеты.Контрагент                = ДокументПоступленияРеализации.Комитент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов     = ДокументПоступленияРеализации.ДоговорКомитента;
		Иначе
			НаборЗаписейВзаиморасчеты.Контрагент                = ДокументПоступленияРеализации.Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов     = ДокументПоступленияРеализации.ДоговорВзаиморасчетов;
		КонецЕсли;	
		НаборЗаписейВзаиморасчеты.Сделка                    = ДокументПоступленияРеализации;
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Ложь;
		НаборЗаписейВзаиморасчеты.Сумма                     = СтрокаВзаиморасчетов.СуммаВсего-СтрокаВзаиморасчетов.Вознаграждение;
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
		
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		СтрокаСуммовыхРазниц = ТаблицаСуммовыхРазниц.Добавить(); 		
		Если БалансВедетсяПоПодразделениям Тогда
			Если ЭтоОтчетАгента Тогда
				СтрокаСуммовыхРазниц.Подразделение = ДокументПоступленияРеализации.ДоговорКомитента.Подразделение;
			Иначе
				СтрокаСуммовыхРазниц.Подразделение = ДокументПоступленияРеализации.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
		Иначе
			СтрокаСуммовыхРазниц.Подразделение = ПодразделениеКомпании;
		КонецЕсли;
		СтрокаСуммовыхРазниц.Сумма = СуммаДоходаРасходаСуммовыхРазниц;
	КонецЦикла; 
	
	// доходы и расходы по суммовым разницам
	ТаблицаСуммовыхРазниц.Свернуть("Подразделение","Сумма");
	Для Каждого СтрокаСуммовыхРазниц Из ТаблицаСуммовыхРазниц Цикл
		Если СтрокаСуммовыхРазниц.Сумма<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение          = СтрокаСуммовыхРазниц.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			Если СтрокаСуммовыхРазниц.Сумма<0 Тогда
				НаборЗаписейДиР.Расход = -СтрокаСуммовыхРазниц.Сумма;
			Иначе
				НаборЗаписейДиР.Доход = СтрокаСуммовыхРазниц.Сумма;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	// партии
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль документа БюджетПродаж

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Имя реквизита хранящего код номенклатуры
Перем ВалютаУпр;
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОсновнаяСтавкаНДС Экспорт;    // Хранит основную ставку НДС

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	
	ОбязательныеУслуги=Новый Структура();
	ОбязательныеУслуги.Вставить("Номенклатура",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СценарийПланирования",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Услуги",ОбязательныеУслуги);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Результат = Истина;	
	
	// Запретим запись с невыбранным сценарием планирования
	Если обЗначениеНеЗаполнено(СценарийПланирования) ИЛИ обЗначениеНеЗаполнено(СценарийПланирования.Периодичность) Тогда
		дкДобавитьОшибку(Ошибки, "Не выбран сценарий планирования, либо у выбранного сценария не выбрана периодичность планирования.");		
		Результат = Ложь;		
	КонецЕсли;
	
	// проверим правильность введенного подразделения хоз.операции.
	Результат = плПроверитьКорректностьВыбораПодразделенияИХозОперации(ХозОперация, ПодразделениеКомпании) И Результат;
	Если НЕ Результат Тогда
		дкДобавитьОшибку(Ошибки, "Для данного подразделения нельзя вводить данный вид бюджета.");		
	КонецЕсли; 
	
	Результат = (дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И Результат);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа.
//  Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СценарийПланирования" Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		плПолучитьДатыПланируемогоПериода(Параметры); 	
		ДатаПланирования = Параметры.ДатаНачала;  		
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда 
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			Результат=дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
			Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		Если ТекСтрока.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
			#КонецЕсли
			ТекСтрока.Номенклатура=Неопределено;
			Возврат Ложь;
		КонецЕсли;
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Дата, Справочники.Контрагенты.ПустаяСсылка(), ВалютаУпр,,,, ПодразделениеКомпании);
			ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		
		ТекСтрока.СуммаВсегоУпр = ТекСтрока.Количество * ТекСтрока.Цена;
		
		// Рассчитаем НДС по ставке из справочника по актуальному курсу
		// Получим ставку
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС.Ставка;
		Иначе
			СтавкаНДС = ОсновнаяСтавкаНДС.Ставка;
		КонецЕсли;
		
		// Получим курсы упр. и рег. валюты на дату документа
		КоэфПересчета      = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС/100;	
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		ОбработкаРеквизита("Товары.Цена"           , ТекСтрока, ЭтаФорма);
		ОбработкаРеквизита("Товары.НормативнаяЦена", ТекСтрока, ЭтаФорма);		
		
	ИначеЕсли Имя="Товары.СуммаВсегоУпр" Тогда
		ХОПоНоменклатуре    = (ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре);
		
		СтавкаНДС           = ?(ХОПоНоменклатуре, ТекСтрока.Номенклатура.СтавкаНДС.Ставка, ОсновнаяСтавкаНДС.Ставка);
		КоэфПересчета       = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС  = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС/100;			
		
		Если ХОПоНоменклатуре Тогда
			ТекСтрока.Цена = ?(ТекСтрока.Количество=0,ТекСтрока.СуммаВсегоУпр,ТекСтрока.СуммаВсегоУпр/ТекСтрока.Количество);			
		КонецЕсли;		
		
	ИначеЕсли Имя="Товары.СебестоимостьУпр" Тогда
		Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре Тогда 			
			ТекСтрока.НормативнаяЦена = ?(ТекСтрока.Количество=0,ТекСтрока.СебестоимостьУпр,ТекСтрока.СебестоимостьУпр/ТекСтрока.Количество);
		КонецЕсли;	
		
	ИначеЕсли Имя="Товары.НормативнаяЦена" Тогда
		ТекСтрока.СебестоимостьУпр = ТекСтрока.НормативнаяЦена * ТекСтрока.Количество;
		
	ИначеЕсли Имя="Услуги.Номенклатура" Тогда 
		
		Если ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
			#КонецЕсли
			ТекСтрока.Номенклатура=Неопределено;
			Возврат Ложь; 
		КонецЕсли;
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Дата, Справочники.Контрагенты.ПустаяСсылка(), ВалютаУпр,,,, ПодразделениеКомпании);
			ОбработкаРеквизита("Услуги.Цена", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли Имя="Услуги.Цена" Тогда
		ТекСтрока.СуммаВсегоУпр = ТекСтрока.Количество * ТекСтрока.Цена;
		// Рассчитаем НДС по ставке из справочника по актуальному курсу
		// Получим ставку
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС.Ставка;
		Иначе
			СтавкаНДС = ОсновнаяСтавкаНДС.Ставка;
		КонецЕсли;
		
		// Получим курсы упр. и рег. валюты на дату документа
		КоэфПересчета      = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС/100;	
		
	ИначеЕсли Имя="Услуги.Количество" Тогда
		ОбработкаРеквизита("Услуги.Цена"           , ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя="Услуги.СуммаВсегоУпр" Тогда
		ХОПоНоменклатуре    = (ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре);
		
		СтавкаНДС           = ?(ХОПоНоменклатуре, ТекСтрока.Номенклатура.СтавкаНДС.Ставка, ОсновнаяСтавкаНДС.Ставка);
		КоэфПересчета       = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС  = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * СтавкаНДС/100;			
		
		Если ХОПоНоменклатуре Тогда
			ТекСтрока.Цена = ?(ТекСтрока.Количество=0,ТекСтрока.СуммаВсегоУпр,ТекСтрока.СуммаВсегоУпр/ТекСтрока.Количество);			
		КонецЕсли;		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("БюджетПродажПоКатегориям"   ,"По категориям",Истина);
	СписокХозОпераций.Добавить("БюджетПродажПоНоменклатуре","По номенклатуре",Ложь);	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Формирует строку по которой последний раз заполняли
//
Процедура СформироватьСтрокуПоследнегоЗаполнения(ЭтаФорма, СпособЗаполнения=-1) Экспорт
	
	Если СпособЗаполнения=-1 Тогда
		СпособЗаполнения = СпособПоследнегоЗаполнения;
	Иначе
		СпособПоследнегоЗаполнения = СпособЗаполнения;
	КонецЕсли;	
	
	Если СпособЗаполнения=-2 Тогда
		ЭтаФорма.ЭлементыФормы.НадписьПараметры.Заголовок = "";	
		Возврат;
	КонецЕсли;
	
	Текст = "";
	Если СпособЗаполнения=0 Тогда // Наивное прогнозирование
		
		Текст = "Тип анализа:  " + СокрЛП(ТипАнализа);
		Текст = Текст + " (Коэф.роста=" + СокрЛП(КоэффициентРостаПродаж) + ";  Коэф.сез.=" + СокрЛП(КоэффициентСезонности) + ")" + Символы.ПС;
		Текст = Текст + "Показатель планирования:  " + ?(ПоказательПланирования,"Количество","Сумма (упр.)") + Символы.ПС;
		Текст = Текст + "Периоды планирования: " + плВывестиПредставлениеПериода(ЭтаФорма, СценарийПланирования.Периодичность, ДатаПланирования);
		
	ИначеЕсли СпособЗаполнения=1 Тогда // Прогнозирование мат.методами
		
		Текст = Текст + "Модель: " + СокрЛП(МодельПрогнозирования) + Символы.ПС;
		
		Если МодельПрогнозирования=Перечисления.МоделиПрогнозирования.Сглаживанием Тогда
			
			Текст = Текст + "Постоянная уровня: " + СокрЛП(Параметр1) + "; Постоянная тренда: "+ СокрЛП(Параметр2);
			Если ( (СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Месяц) И
				(КоличествоПериодов >= 24) ) 
				ИЛИ				
				( (СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Квартал) И
				(КоличествоПериодов >= 8)   ) Тогда  
				Текст = Текст + "; Постоянная сезонности: " + СокрЛП(Параметр3) + Символы.ПС;  				
			КонецЕсли;
			
		ИначеЕсли МодельПрогнозирования=Перечисления.МоделиПрогнозирования.Полиномиальный Тогда
			Текст = Текст + "Степень полинома: " + СокрЛП(Параметр1) + Символы.ПС;
		КонецЕсли;
		
		Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре Тогда
			Текст = Текст + "Показатель планирования:  " + ?(ПоказательПланирования,"Количество","Сумма (упр.)") + Символы.ПС;
		КонецЕсли;
		Текст = Текст + "Периоды планирования: " + плВывестиПредставлениеПериода(ЭтаФорма, СценарийПланирования.Периодичность, ДатаПланирования);
		
	ИначеЕсли СпособЗаполнения=2 Тогда // Заполнение по документу основанию
		Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
			Текст = "";
		Иначе
			Текст = Текст + "Док.основание:  " + СокрЛП(ДокументОснование);
			Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре И ДокументОснование.ХозОперация=Справочники.ХозОперации.БюджетПродажПоКатегориям Тогда
				Текст = Текст + Символы.ПС + "Метод распределения суммы продаж категорий:  " + СокрЛП(МетодыРаспределенияКатегорий);
				Текст = Текст + Символы.ПС + "Изменяемый показатель:  " + ?(ПараметрУправленияРаспределением=0, "Количество", "Цена");
			КонецЕсли;
		КонецЕсли;			
	КонецЕсли;
	
	ЭтаФорма.ЭлементыФормы.НадписьПараметры.Заголовок = Текст;
		
КонецПроцедуры


#Если Клиент Тогда  	
	////////////////////////////////////////////////////////////////////////////////
	// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
	
	// Формирует табличный документ 
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
	Функция ПечатьБюджетПродаж(ТабДокумент) Экспорт
		
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		Макет = ПолучитьМакет("БюджетПродаж");
		//для начала настроим макет
		ОбластьТовар = Макет.Область(1,Макет.Область("Товар").Право);
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ИмяКодаТовара = Неопределено;
		Если ПланированиеСебестоимости Тогда
			Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоКатегориям Тогда
				ОбластьНормативнаяЦена = Макет.Область("НормативнаяЦена");
				ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьНормативнаяЦена.ШиринаКолонки;
				Макет.УдалитьОбласть(ОбластьНормативнаяЦена,ТипСмещенияТабличногоДокумента.ПоВертикали);
			КонецЕсли;  
		Иначе
			ОбластьНормативнаяЦена	= Макет.Область("НормативнаяЦена");
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьНормативнаяЦена.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьНормативнаяЦена,ТипСмещенияТабличногоДокумента.ПоВертикали);
			ОбластьСебестоимость = Макет.Область("Себестоимость");
			ШиринаКолонокСебестоимости = Макет.Область(1, ОбластьСебестоимость.Лево).ШиринаКолонки+Макет.Область(1, ОбластьСебестоимость.Право).ШиринаКолонки;
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ШиринаКолонокСебестоимости;
			Макет.УдалитьОбласть(ОбластьСебестоимость,ТипСмещенияТабличногоДокумента.ПоВертикали);
		КонецЕсли;
		
		Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоКатегориям Тогда
			ОбластьКоличество	= Макет.Область("Количество");
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКоличество.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьКоличество,ТипСмещенияТабличногоДокумента.ПоВертикали);
			ОбластьЕдиницаИзмерения	= Макет.Область("ЕдиницаИзмерения");
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЕдиницаИзмерения.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьЕдиницаИзмерения,ТипСмещенияТабличногоДокумента.ПоВертикали);
			ОбластьЦена	= Макет.Область("Цена");
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьЦена.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьЦена,ТипСмещенияТабличногоДокумента.ПоВертикали);
			ОбластьКод	= Макет.Область("Код");
			ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
			Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);
		Иначе
			ИмяКодаТовара = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
		КонецЕсли;	
		
		//ЗАГОЛОВОК
		ОбластьЗаголовка = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовка.Параметры.Заполнить(ЭтотОбъект); 		
		ТекстЗаголовка = ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy");  		
		ОбластьЗаголовка.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		Параметры = Новый Структура;                                 
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		ПредставлениеПериодаПланирования = плПолучитьДатыПланируемогоПериода(Параметры);	
		
		ОбластьЗаголовка.Параметры.СценарийПланирования = СценарийПланирования;
		ОбластьЗаголовка.Параметры.ПредставлениеПериодаПланирования = ПредставлениеПериодаПланирования;
		ТабДокумент.Вывести(ОбластьЗаголовка);
		
		//форматы вывода
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		
		// ИТОГИ НА СТРАНИЦЕ
		УчетКоличества    = (ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре);
		УчетСебестоимости = ПланированиеСебестоимости;
		СтруктураИтоговПоСтранице 	= Новый Структура;
		СтруктураИтоговПоТаблице 	= Новый Структура;
		СтруктураИтоговПоДокументу	= Новый Структура;
		
		СтруктураИтоговПоСтранице.Вставить("СуммаУпр",0);
		
		Если УчетСебестоимости Тогда
			Если УчетКоличества Тогда
				СтруктураИтоговПоСтранице.Вставить("НормативнаяЦена",0);
			КонецЕсли;
			СтруктураИтоговПоСтранице.Вставить("СебестоимостьУпр",0);
			СтруктураИтоговПоСтранице.Вставить("МаржинальнаяПрибыль",0);
		КонецЕсли;
		Если УчетКоличества Тогда
			СтруктураИтоговПоСтранице.Вставить("Количество",0);
		КонецЕсли;
		СтруктураИтоговПоСтранице.Вставить("СуммаНДС",0);
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		ОбластьПодвалТоваров = Макет.ПолучитьОбласть("ПодвалТоваров");
		ОбластьПодвалУслуг = Макет.ПолучитьОбласть("ПодвалУслуг");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		ИмяТЧТовара = """Товары""";
		ИмяТЧУслуги = """Услуги""";
		Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоКатегориям Тогда
			ИмяТЧТовара = """Типы номенклатуры""";
			ИмяТЧУслуги = """Типы услуг""";
		КонецЕсли;
		
		Для Каждого ЭлементСтруктуры Из СтруктураИтоговПоСтранице Цикл
			СтруктураИтоговПоТаблице.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
			СтруктураИтоговПоДокументу.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		КонецЦикла;
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьШапкаТаблицы.Параметры.Товар = ИмяТЧТовара;
		Если ИмяКодаТовара<>Неопределено Тогда
			ОбластьШапкаТаблицы.Параметры.ИмяКодаТовара = ИмяКодаТовара;
		КонецЕсли;
		
		//сразу два, т.к. выводим на второй странице только
		НомерСтраницы = 2;
		НомерСтраницыПред = НомерСтраницы;
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	БюджетПродажТовары.Номенклатура КАК Номенклатура,
		|	БюджетПродажТовары.Номенклатура.Наименование КАК ТоварНаименование,
		|	БюджетПродажТовары.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	БюджетПродажТовары.Цена КАК Цена,
		|	БюджетПродажТовары.Количество КАК Количество,
		|	БюджетПродажТовары.СуммаВсегоУпр КАК СуммаУпр,
		|	БюджетПродажТовары.СуммаВсегоУпр - БюджетПродажТовары.СебестоимостьУпр КАК МаржинальнаяПрибыль,
		|	БюджетПродажТовары.НормативнаяЦена КАК НормативнаяЦена,
		|	БюджетПродажТовары.СебестоимостьУпр КАК СебестоимостьУпр,
		|	БюджетПродажТовары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	Документ.БюджетПродаж.Товары КАК БюджетПродажТовары
		|ГДЕ
		|	БюджетПродажТовары.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	БюджетПродажТовары.НомерСтроки";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать();
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТовара");
		Ном=1;
		КоличествоНоменклатуры = ВыборкаНоменклатуры.Количество();
		Если КоличествоНоменклатуры > 0 Тогда
			ТабДокумент.Вывести(ОбластьШапкаТаблицы);
			//заполним параметры шапки таблицы для следующего листа
			ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
			Пока ВыборкаНоменклатуры.Следующий() Цикл
				ОбластьМакета.Параметры.НомерСтроки = Ном;
				ОбластьМакета.Параметры.ТоварНаименование = ВыборкаНоменклатуры.ТоварНаименование;
				ОбластьМакета.Параметры.Номенклатура	  = ВыборкаНоменклатуры.Номенклатура;
				ЗаполнитьМакетНоменклатурнойСтроки(ОбластьМакета, ВыборкаНоменклатуры, УчетКоличества, УчетСебестоимости, Истина, ИмяКодаТовара, ФорматВыводаСуммы, ФорматВыводаКоличества);
				мсвДопОбластиПодвала = Неопределено;
				Если Ном=КоличествоНоменклатуры Тогда
					мсвДопОбластиПодвала = Новый Массив;
					мсвДопОбластиПодвала.Добавить(ОбластьПодвалТоваров);
				КонецЕсли;
				
				//выводим строку, делая проверку попадания на лист
				НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
				
				////инициализация итогов по странице
				Если НомерСтраницы <> НомерСтраницыПред Тогда
					// обнулим структуру
					Для Каждого ЭлементСтруктуры Из СтруктураИтоговПоСтранице Цикл
						СтруктураИтоговПоТаблице.Вставить(ЭлементСтруктуры.Ключ, СтруктураИтоговПоТаблице[ЭлементСтруктуры.Ключ]+ЭлементСтруктуры.Значение);
						СтруктураИтоговПоСтранице.Вставить(ЭлементСтруктуры.Ключ, 0);
					КонецЦикла;
					НомерСтраницыПред = НомерСтраницы;
					ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
					ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
				КонецЕсли;
				дкДобавитьИтогиПоСтранице(ВыборкаНоменклатуры,СтруктураИтоговПоСтранице);
				Ном=Ном+1;	
			КонецЦикла;
			
			Для Каждого ЭлементСтруктуры Из СтруктураИтоговПоСтранице Цикл
				СтруктураИтоговПоТаблице.Вставить(ЭлементСтруктуры.Ключ, СтруктураИтоговПоТаблице[ЭлементСтруктуры.Ключ]+ЭлементСтруктуры.Значение);
			КонецЦикла;
			ЗаполнитьМакетНоменклатурнойСтроки(ОбластьПодвалТоваров, СтруктураИтоговПоТаблице, УчетКоличества, УчетСебестоимости, Ложь,, ФорматВыводаСуммы, ФорматВыводаКоличества);
			ОбластьПодвалТоваров.Параметры.ТекстИтога = "Итого по таблице "+ОбластьШапкаТаблицы.Параметры.Товар+":";
			ТабДокумент.Вывести(ОбластьПодвалТоваров);
		КонецЕсли;
		
		Для Каждого ЭлементСтруктуры Из СтруктураИтоговПоТаблице Цикл
			СтруктураИтоговПоДокументу.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
			СтруктураИтоговПоТаблице.Вставить(ЭлементСтруктуры.Ключ, 0);
		КонецЦикла;
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	БюджетПродажУслуги.Номенклатура КАК Номенклатура,
		|	БюджетПродажУслуги.Номенклатура.Наименование КАК ТоварНаименование,
		|	БюджетПродажУслуги.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	БюджетПродажУслуги.Цена КАК Цена,
		|	БюджетПродажУслуги.Количество КАК Количество,
		|	БюджетПродажУслуги.СуммаВсегоУпр КАК СуммаУпр,
		|	БюджетПродажУслуги.СуммаВсегоУпр КАК МаржинальнаяПрибыль,
		|	0 КАК НормативнаяЦена,
		|	0 КАК СебестоимостьУпр,
		|	БюджетПродажУслуги.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	Документ.БюджетПродаж.Услуги КАК БюджетПродажУслуги
		|ГДЕ
		|	БюджетПродажУслуги.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	БюджетПродажУслуги.НомерСтроки";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ВыборкаУслуг = Запрос.Выполнить().Выбрать();
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаУслуги");
		Ном=1;
		КоличествоНоменклатуры = ВыборкаУслуг.Количество();
		Если КоличествоНоменклатуры>0 Тогда
			ОбластьШапкаТаблицы.Параметры.Товар = ИмяТЧУслуги;
			ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = "";
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "";
			ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы", Права,,ЭтотОбъект);
			
			Если ИспользоватьРазбиениеНаСтраницы Тогда
				//запишем их в один массив
				МассивОбластейДляПроверки = Новый Массив;
				МассивОбластейДляПроверки.Добавить(ОбластьШапкаТаблицы);
				МассивОбластейДляПроверки.Добавить(ОбластьМакета);
				МассивОбластейДляПроверки.Добавить(ОбластьМакетаИтогоПоСтранице);
				
				//проверим, помещаются ли на странице
				Попытка
					Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластейДляПроверки) Тогда
						дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект); //выводим итог по странице
						ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц(); //переходим на следующую страницу
						ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
						ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
						НомерСтраницы = НомерСтраницы + 1;
					КонецЕсли;
				Исключение
				КонецПопытки;
				
				КонецЕсли;
				ТабДокумент.Вывести(ОбластьШапкаТаблицы);
				ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
				
				Пока ВыборкаУслуг.Следующий() Цикл
					ОбластьМакета.Параметры.НомерСтроки = Ном;
					ОбластьМакета.Параметры.ТоварНаименование = ВыборкаУслуг.ТоварНаименование;
					ОбластьМакета.Параметры.Номенклатура	  = ВыборкаУслуг.Номенклатура;
					
					ЗаполнитьМакетНоменклатурнойСтроки(ОбластьМакета, ВыборкаУслуг, УчетКоличества, УчетСебестоимости, Истина, ИмяКодаТовара, ФорматВыводаСуммы, ФорматВыводаКоличества, Истина);
					мсвДопОбластиПодвала = Неопределено;
					Если Ном=КоличествоНоменклатуры Тогда
						мсвДопОбластиПодвала = Новый Массив;
						мсвДопОбластиПодвала.Добавить(ОбластьПодвалУслуг);
					КонецЕсли;
					
					//выводим строку, делая проверку попадания на лист
					НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
					
					//инициализация итогов по странице
					Если НомерСтраницы <> НомерСтраницыПред Тогда
						// обнулим структуру
						Для Каждого стрСтруктуры Из СтруктураИтоговПоСтранице Цикл
							СтруктураИтоговПоСтранице.Вставить(стрСтруктуры.Ключ, 0);
						КонецЦикла;
						НомерСтраницыПред = НомерСтраницы;
					КонецЕсли;
					дкДобавитьИтогиПоСтранице(ВыборкаУслуг,СтруктураИтоговПоСтранице);
					Ном=Ном+1;	
				КонецЦикла;
				дкДобавитьИтогиПоСтранице(ВыборкаУслуг,СтруктураИтоговПоТаблице);
				ЗаполнитьМакетНоменклатурнойСтроки(ОбластьПодвалУслуг, СтруктураИтоговПоТаблице, УчетКоличества, УчетСебестоимости, Ложь,, ФорматВыводаСуммы, ФорматВыводаКоличества, Истина);
				ОбластьПодвалУслуг.Параметры.ТекстИтога = "Итого по таблице "+ОбластьШапкаТаблицы.Параметры.Товар+":";
				ТабДокумент.Вывести(ОбластьПодвалУслуг);
			
			Для Каждого ЭлементСтруктуры Из СтруктураИтоговПоТаблице Цикл
				СтруктураИтоговПоДокументу.Вставить(ЭлементСтруктуры.Ключ, СтруктураИтоговПоДокументу[ЭлементСтруктуры.Ключ]+ЭлементСтруктуры.Значение);
			КонецЦикла;	
		КонецЕсли;
		ОбластьПодвал.Параметры.Автор = Автор;
		
		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		ЗаполнитьМакетНоменклатурнойСтроки(ОбластьПодвал, СтруктураИтоговПоДокументу, УчетКоличества, УчетСебестоимости, Ложь,, ФорматВыводаСуммы, ФорматВыводаКоличества);
		КоличествоПозиций = Товары.Количество() + Услуги.Количество();
		Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоНоменклатуре Тогда
			ОбластьПодвал.Параметры.СуммаПрописью     = "Всего запланировано позиций номенклатуры "+КоличествоПозиций+" на сумму " + обЧислоПрописью(СтруктураИтоговПоДокументу.СуммаУпр, ВалютаУпр);
		Иначе
			ОбластьПодвал.Параметры.СуммаПрописью     = "Всего запланировано типов номенклатуры "+КоличествоПозиций+" на сумму " + обЧислоПрописью(СтруктураИтоговПоДокументу.СуммаУпр, ВалютаУпр);
		КонецЕсли;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
		
		Возврат ТабДокумент;
		
	КонецФункции // ПечатьБюджетПродаж()
	
	// Процедура заполенения макета данными из строки номенклатуры
	Процедура ЗаполнитьМакетНоменклатурнойСтроки(ОбластьМакета, СтруктураДанных, УчетКоличества, УчетСебестоимости, ЭтоСтрока, ИмяКодаТовара="", ФорматВыводаСуммы, ФорматВыводаКоличества, ЭтоУслуга=Ложь)
		
		ВыводитьПроизводителя = обПраво("ВыводитьПроизводителяВПечатныхФормах",Права,,ЭтотОбъект);
		
		Если УчетКоличества Тогда
			ОбластьМакета.Параметры.Количество = Формат(СтруктураДанных.Количество, ФорматВыводаКоличества);
			Если ЭтоСтрока Тогда
				ОбластьМакета.Параметры.Код  = дкПолучитьЗначениеКолонкиКода(СтруктураДанных.Номенклатура, ИмяКодаТовара, , ВыводитьПроизводителя);
				ОбластьМакета.Параметры.Цена = Формат(СтруктураДанных.Цена, ФорматВыводаСуммы);
				ОбластьМакета.Параметры.ЕдиницаИзмерения  = СтруктураДанных.ЕдиницаИзмерения;
			КонецЕсли;
		КонецЕсли;
		ОбластьМакета.Параметры.СуммаУпр = Формат(СтруктураДанных.СуммаУпр, ФорматВыводаСуммы);
		Если УчетСебестоимости Тогда
			Если НЕ ЭтоУслуга Тогда
				Если УчетКоличества И ЭтоСтрока Тогда
					ОбластьМакета.Параметры.НормативнаяЦена = Формат(СтруктураДанных.НормативнаяЦена, ФорматВыводаСуммы);
				КонецЕсли; 
				ОбластьМакета.Параметры.СебестоимостьУпр    = Формат(СтруктураДанных.СебестоимостьУпр, ФорматВыводаСуммы); 
			КонецЕсли;
			// Выводим марженальную прибыль
			ОбластьЯчеек = ОбластьМакета.Область(1, 2, 1, 2);
			Если СтруктураДанных.МаржинальнаяПрибыль<0 Тогда
				ОбластьЯчеек.ЦветТекста = Новый Цвет(255, 0, 0);
			Иначе
				ОбластьЯчеек.ЦветТекста = Новый Цвет(0, 0, 0);
			КонецЕсли;
			ОбластьМакета.Параметры.МаржинальнаяПрибыль = Формат(СтруктураДанных.МаржинальнаяПрибыль, ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьМакета.Параметры.СуммаНДС = Формат(СтруктураДанных.СуммаНДС, ФорматВыводаСуммы);
		
	КонецПроцедуры
	
// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
	Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
		Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
	КонецФункции // Печать()

#КонецЕсли  

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , , ,"10111") Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли; 
	
	// Собственно само проведение
	НаборЗаписей = Движения.БюджетПродаж;
	НаборЗаписей.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписей.ДокументОбъект        = Ссылка;
	НаборЗаписей.РежимПроведения       = Режим;
	
	Отказ = НЕ НаборЗаписей.Приход();
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);  	
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
ОсновнаяСтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

// Модуль документа ЧекНаОплату

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;                              // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;                 // Переменная для накопления результатов проведения по модулям наборов записей
Перем СообщениеОбОшибке Экспорт;                  // Переменная для накопления результатов записи. Если = неопределено, то вывод будет на экран, иначе тут сообщения об ошибках.
Перем ИмяРеквизитаКода Экспорт;                   // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОтключитьПроверкиПередЗаписью Экспорт;      // Флаг отключения проверок (используется при программном формировании документа)
Перем КонтролироватьЗаполнениеОплат;              // Вспомогательная переменная определяющая необходимость выполнения контроля оплат документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт;   // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт;              // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
Перем ОтменитьПроверкуДоступностиСкладов Экспорт; // Булево - отменяет проверку доступности склада в дкДокументы.дкПроверитьДоступностьСкладовКомпании()
                                                  // свойства и методы для расчета скидок документа.


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД", 2);
	ОбязательныеТовары.Вставить("ДоговорВзаиморасчетов", 2);
	//Если обПраво("ЗапретитьПодарки",Права,,ЭтотОбъект) Тогда
	//	ОбязательныеТовары.Вставить("СуммаВсего",1);
	//КонецЕсли;
	
	// Обязательные поля таблицы оплат
	ОбязательныеОплаты=Новый Структура();
	ОбязательныеОплаты.Вставить("ТипОплаты",3);
	//ОбязательныеОплаты.Вставить("Сумма",1);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("КассаККМ",1);
	//Если ДляПробитияНаФР Тогда
	//	ОбязательныеРеквизиты.Вставить("ТипСпособаРасчетаККТ",1);
	//КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Оплаты",ОбязательныеОплаты);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаККМ");
			КонецЕсли; 			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	// Если задача контроля заполнения не стоит, выходим
	Если НЕ Заполнение Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Проконтролируем корректность заполнения фискальных реквизитов документа
	Если НЕ(обЗначениеНеЗаполнено(ФР.КлассОборудования) ИЛИ ФР.КлассОборудования=Перечисления.КлассыОборудования.ФР) Тогда
		дкДобавитьОшибку(Ошибки, "Реквизит <ФР> класс оборудования должен быть фискальным регистратором !");
		Результат = ЛОЖЬ;
	КонецЕсли;
	
	// Проводим инициализацию основных 
	ОплатыВсего = Оплаты.Итог("Сумма");
	ОплатыТалон = 0;
	ОплатыБезнал= 0;
	СтрокаТалон = Неопределено;
	
	// Выполним проверку корректности заполнения реквизит таблицы оплат
	Для Каждого ТекОплата Из Оплаты Цикл
		Если НЕ ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Наличные Тогда
			ОплатыБезнал = ОплатыБезнал + ТекОплата.Сумма;
		КонецЕсли;
		
		Если ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Безналичные Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.ТипПлатежнойКарты) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Тип платежной карты> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Кредит Тогда
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			ИначеЕсли обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			ИначеЕсли НЕ ТекОплата.ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.Кредит Тогда
				дкДобавитьОшибку(Ошибки,"Таблица <Оплаты> вид договора оплаты не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Талон Тогда
			Если НЕ ТекОплата.Карточка.ВидКарточки=Перечисления.ВидыКарточек.Талон Тогда
				дкДобавитьОшибку(Ошибки,"Таблица <Оплаты> вид карточки не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			ОплатыТалон = ОплатыТалон + ТекОплата.Сумма;
			СтрокаТалон = ТекОплата;
			
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.КлубнаяКарта Тогда
			Если НЕ ТекОплата.Карточка.ВидКарточки=Перечисления.ВидыКарточек.КлубнаяКарта Тогда
				дкДобавитьОшибку(Ошибки,"Таблица <Оплаты> вид карточки не соответствует типу оплаты. Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ТекОплата.Контрагент) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Контрагент> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			ИначеЕсли обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
				дкДобавитьОшибку(Ошибки, "Таблица <Оплаты> значение колонки <Договор> не заполнен ! Строка номер "+СокрЛП(ТекОплата.НомерСтроки));
				Результат = ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Проводим контроль сумм оплат
	Если НЕ КонтролироватьЗаполнениеОплат Тогда
		// Проверку корректности сумм оплат производить сейчас не нужно
		
	ИначеЕсли СуммаДокумента>ОплатыВсего Тогда
		дкДобавитьОшибку(Ошибки, "Общая сумма оплат не может быть менее суммы документа !");
		Результат = ЛОЖЬ;
		
	ИначеЕсли СуммаДокумента<ОплатыВсего Тогда
		// Определим причину превышения оплаты
		Если ОплатыВсего=ОплатыТалон Тогда
			Если (ОплатыВсего-СуммаДокумента)>=СтрокаТалон.Карточка.Номинал Тогда
				дкДобавитьОшибку(Ошибки, "Сумма оплаты талонами, превышает необходимую !");
				Результат = ЛОЖЬ;
			КонецЕсли;
			
		ИначеЕсли (НЕ ОплатыБезнал=ОплатыТалон) И СуммаДокумента<ОплатыБезнал Тогда
			дкДобавитьОшибку(Ошибки, "Сумма безналичной оплаты не может превышать сумму документа !");
			Результат = ЛОЖЬ;
			
		ИначеЕсли (ПолученоБезнал=СуммаДокумента) И (ПолученоНал>0) Тогда
			дкДобавитьОшибку(Ошибки, "Сумма безналичной оплаты полностью закрывает сумму чека. Оплата наличными не допустима !");
			Результат = ЛОЖЬ;
			
		Иначе
			дкДобавитьОшибку(Ошибки, "Общая сумма оплат не может быть больше суммы документа !");
			Результат = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
		дкДобавитьОшибку(Ошибки, ТекстОшибки);
		Результат = Ложь;
	КонецЕсли;
 
	Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
		дкДобавитьОшибку(Ошибки, ТекстОшибки);
		Результат = Ложь;
	КонецЕсли;
	
	// Проверим количество маркировок и количество товара в строке
	Если ХозОперация = Справочники.ХозОперации.ЧекНаОплату
		ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаВозврат Тогда
		// Отключим некоторые проверки
		РеализацияТовара = ХозОперация = Справочники.ХозОперации.ЧекНаОплату
			И ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача
			И (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")
			ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров"));
		Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки, НЕ РеализацияТовара);
	КонецЕсли;
	
	// Возвращаем результат проверки
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЧекНаОплату","Чек на оплату",Истина);
	СписокХозОпераций.Добавить("ЧекНаОплатуВозврат","Возврат по чеку на оплату",Ложь);
	СписокХозОпераций.Добавить("ЧекНаОплатуПокупки", "Чек на оплату покупки", Ложь);
	СписокХозОпераций.Добавить("ЧекНаОплатуПокупкиВозврат", "Возврат по чеку на оплату покупки", Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет общей суммы амортизации активов
// 
// Возвращаемое значение:
//  Возвращает итог по колонке "СуммаВсего".
//
Функция РассчитатьСуммуВсего() Экспорт
	НеПересчитыватьСуммуДокумента=Ложь;
	Если НЕ ДополнительныеСвойства.Свойство("НеПересчитыватьСуммуДокумента",НеПересчитыватьСуммуДокумента) Тогда
		НеПересчитыватьСуммуДокумента=Ложь;
	КонецЕсли; 
	
	Возврат ?(НеПересчитыватьСуммуДокумента, СуммаДокумента, Товары.Итог("СуммаВсего"));
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Карточка" Тогда
		Если НЕ Карточка.Пустая() Тогда
			Если Карточка.ВидКарточки<>Перечисления.ВидыКарточек.ДисконтнаяКарта Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранная карточка не является дисконтной!");
				#КонецЕсли
				Карточка=Неопределено;
				ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(Карточка.Объект) Тогда
				Если Контрагент<>Карточка.Объект Тогда
					//В документе выбран другой контрагент - заменяем на владельца карточки
					Контрагент=Карточка.Объект;
					Возврат ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (НЕ Контрагент.Пустая()) И (НЕ Карточка.Пустая()) И ((Карточка.Объект<>Неопределено) И (НЕ Карточка.Объект.Пустая())) И (Контрагент<>Карточка.Объект)  Тогда
			Карточка=Неопределено;
			Возврат ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="КассаККМ" Тогда
		Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
			Возврат Истина;
		КонецЕсли;
		#Если Клиент Тогда
		Рез=Истина;
		Если ЭтаФорма<>Неопределено Тогда
			Если КассаККМ.Организация<>Организация Тогда
				Если Вопрос("Организация кассы отлична от текущей организации документа. Установить организацию документа в соответствие с организацией кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					Организация=КассаККМ.Организация;
					Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.Подразделение<>ПодразделениеКомпании Тогда
				Если Вопрос("Подразделение кассы отлично от текущего подразделения документа. Установить подразделение документа в соответствии с подразделением кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ПодразделениеКомпании=КассаККМ.Подразделение;
					Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента Тогда
				Если Вопрос("Валюта кассы отлична от текущей валюты документа. Установить валюту документа в соответствие с валютой кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
					Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли;
					Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЕсли; 
		Иначе
			Организация=КассаККМ.Организация;
			Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
			ПодразделениеКомпании=КассаККМ.Подразделение;
			Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
			ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
			Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		КонецЕсли; 
		#Иначе
		Организация=КассаККМ.Организация;
		Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
		ПодразделениеКомпании=КассаККМ.Подразделение;
		Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
		ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
		Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ФР" Тогда
		Если НЕ обЗначениеНеЗаполнено(ФР) Тогда
			Если ФР.КлассОборудования<>Перечисления.КлассыОборудования.ФР Тогда
				#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Предупреждение("Выбранное оборудование: " + ФР + " не является фискальным регистратором!
					|Выберите фискальный регистратор",10);
				КонецЕсли; 
				#КонецЕсли
				ФР=Неопределено;
				Возврат ОбработкаРеквизита("ФР",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="ВидОплаты" Тогда
		Если ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет И ПолученоБезнал<>0 Тогда
			ПолученоБезнал=0;
			Рез=ОбработкаРеквизита("ПолученоБезнал",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет И ПолученоНал<>0 Тогда
			ПолученоНал=0;
			Возврат ОбработкаРеквизита("ПолученоНал",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="ПолученоНал" ИЛИ Имя="ПолученоБезнал" Тогда
		ЕстьНал=(ПолученоНал>0);
		ЕстьБезнал=(ПолученоБезнал>0);
		Если (ЕстьНал) И (НЕ ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.НаличныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (НЕ ЕстьНал) И (ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.БезналичныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (ЕстьНал) И (ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.ПроизвольнаяОплата) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (НЕ ЕстьНал) И (НЕ ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.НаличныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет;
			Возврат ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Сделка" Тогда
		Если ТипЗнч(Сделка) = Тип("ДокументСсылка.СчетНаОплату") Тогда
			ДоговорВзаиморасчетов = Сделка.ДоговорВзаиморасчетов;
			СуммаДокумента = обПересчет(Сделка.СуммаДокумента,Сделка.ВалютаДокумента,Дата,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		Если ЗначениеЗаполнено(Сделка) Тогда
			дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, Сделка, СуммаДокумента);
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя = "СкидкаНаценка"
		  ИЛИ Имя = "ТипЦен" 
		  ИЛИ Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		СуммаДокумента = РассчитатьСуммуВсего();
		
		Если Имя = "Товары.Номенклатура" Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
				ТекСтрока.ПризнакПредметаРасчета = ТекСтрока.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
			Иначе
				ТекСтрока.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		Возврат Рез И ОбработкаРеквизита("СуммаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СуммаДокумента" Тогда
		Если НЕ СтавкаНДС.Пустая() Тогда
			Если СуммаДокумента = Товары.Итог("СуммаВсего") Тогда
				СуммаНДС = Товары.Итог("СуммаНДС");	
			Иначе
				СуммаНДС = Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
			КонецЕсли;
		Иначе
			СуммаНДСВсего = Товары.Итог("СуммаНДС");
			СуммаВсего = Товары.Итог("СуммаВсего");
			Если СуммаДокумента = СуммаВсего Тогда
				СуммаНДС = СуммаНДСВсего;
			Иначе
				СуммаНДС = ?(СуммаВсего = 0, 0, Окр((СуммаДокумента / СуммаВсего * СуммаНДСВсего), 2));
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="СтавкаНДС" Тогда
		Если НЕ СтавкаНДС.Пустая() Тогда
			СуммаНДС = Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Найти(Имя, "Товары.") > 0 Тогда 
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СуммаСкидкиНаценки = Товары.Итог("СуммаСкидки");
		СуммаСкидкиНаценки = СуммаСкидкиНаценки + Товары.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиНаценки = СуммаСкидкиНаценки + Товары.Итог("СуммаСкидкиБонусами");
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Проверим наличие марировок в документ-основание
Функция ВедетсяУчетКодовМаркировок() Экспорт
	
	ЭтоВозврат = (ХозОперация = Справочники.ХозОперации.ЧекНаОплатуВозврат
		ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат);
	
	// Проверим наличие ТЧ Маркировки
	Возврат (НЕ ЭтоВозврат И НЕ ЗначениеЗаполнено(ДокументОснование))
		ИЛИ (ЭтоВозврат И (НЕ ЗначениеЗаполнено(ДокументОснование)
		ИЛИ (ЗначениеЗаполнено(ДокументОснование)
		И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.Чек")
		И НЕ ЗначениеЗаполнено(ДокументОснование.ДокументОснование))));
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "Чек"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЧек(ТабДокумент,ФронтКассира=Неопределено) Экспорт
	Макет = ПолучитьМакет("ЧекНаОплату");
	
	//для начала настроим макет
	ДокументДляПечати	= ?(ФронтКассира = Неопределено, ЭтотОбъект, ФронтКассира);
	ОбластьШапкаТаблицы	= дкПривестиМакетПечатнойФормы(ДокументДляПечати,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ДокументДляПечати);
	ТекстЗаголовка = ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(ДокументДляПечати.Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.Организация				= ДокументДляПечати.Организация;
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(ДокументДляПечати.Контрагент);
	ОбластьМакета.Параметры.Контрагент				= ДокументДляПечати.Контрагент;
	Если НЕ обЗначениеНеЗаполнено(ДокументДляПечати.ДокументОснование) Тогда
		ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДокументДляПечати.ДокументОснование.ХозОперация.Наименование+" № "+СокрЛП(ДокументДляПечати.ДокументОснование.Номер)+" от "+Формат(ДокументДляПечати.ДокументОснование.Дата,"ДФ=dd.MM.yyyy");
		ОбластьМакета.Параметры.ДокументОснование		= ДокументДляПечати.ДокументОснование;
	КонецЕсли; 
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ДокументДляПечати);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьШапкаТаблицыОплата = Макет.ПолучитьОбласть("ШапкаТаблицыОплаты");
	ТабДокумент.Вывести(ОбластьШапкаТаблицыОплата);
	ОбластьСтрокаОплаты = Макет.ПолучитьОбласть("СтрокаОплаты");
	
	ВыборкаТЧОплаты = ДокументДляПечати.Оплаты;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТЧОплаты Цикл
		ОбластьСтрокаОплаты.Параметры.НомерСтроки       = СтрокаТабличнойЧасти.НомерСтроки;
		ОбластьСтрокаОплаты.Параметры.ТипОплаты         = СтрокаТабличнойЧасти.ТипОплаты;
		ОбластьСтрокаОплаты.Параметры.ТипПлатежнойКарты = СтрокаТабличнойЧасти.ТипПлатежнойКарты;
		ОбластьСтрокаОплаты.Параметры.Карточка          = СтрокаТабличнойЧасти.Карточка;
		ОбластьСтрокаОплаты.Параметры.Контрагент        = СтрокаТабличнойЧасти.Контрагент;
		ОбластьСтрокаОплаты.Параметры.Договор           = СтрокаТабличнойЧасти.ДоговорВзаиморасчетов;
		ОбластьСтрокаОплаты.Параметры.СуммаВсего        = Формат(СтрокаТабличнойЧасти.Сумма, "ЧДЦ=2");
		ТабДокумент.Вывести(ОбластьСтрокаОплаты);
	КонецЦикла;
	
	ОбластьПодвалОплаты = Макет.ПолучитьОбласть("ПодвалОплаты");
	ОбластьПодвалОплаты.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОбластьПодвалОплаты.Параметры.СуммаВсего = ДокументДляПечати.Оплаты.итог("Сумма");
	ТабДокумент.Вывести(ОбластьПодвалОплаты);

	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ДокументДляПечати.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки") + ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки") + ВыборкаТабличнойЧасти.Итог("СуммаСкидкиБонусами");
	Если СкидкаВсего > 0 Тогда
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;

	//ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ДокументДляПечати.ВалютаДокумента);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего сумма оплаты "+обЧислоПрописью(СуммаДокумента,ДокументДляПечати.ВалютаДокумента);
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЧек()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	// Установим значение вспомогательного реквизита (используется для отбора документов в журнале документов на оплату)
	ДляПробитияНаФР = ИСТИНА;
	
	//Bizteh++ 30.09.2022 создаем документ Реализация товаров
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда 
		СуммаДокКСО = Основание.ВариантыСтрахования.Итог("СуммаПремии"); 
		ДокРеализация = СоздатьДокРеализацияУслуг(Основание);
		Если ДокРеализация <> Неопределено Тогда
			Основание = ДокРеализация; 
		Иначе
			Сообщить("Не удалось создать документ Реализация услуг!");
		КонецЕсли;  
	КонецЕсли; 
	//Bizteh--
	
	ТипОснования = ТипЗнч(Основание);
	Если ТипОснования = Тип("ДокументСсылка.ЗаказПоставщику") ИЛИ 
		ТипОснования = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") ИЛИ
		ТипОснования = Тип("ДокументСсылка.ПоступлениеАвтомобилей") ИЛИ
		ТипОснования = Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
		ТипОснования = Тип("ДокументСсылка.ПоступлениеДопРасходов") ИЛИ
		ТипОснования = Тип("ДокументСсылка.СчетОтПоставщика") ИЛИ
		ТипОснования = Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили")Тогда
		ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки;
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратПоставщику") ИЛИ
		ТипОснования = Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей") Тогда
		ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат;
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратОтПокупателя") ИЛИ
		ТипОснования = Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей") Тогда
		ХозОперация = Справочники.ХозОперации.ЧекНаОплатуВозврат;
	ИначеЕсли ТипОснования=Тип("ДокументСсылка.ЧекНаОплату") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ЧекНаОплату Тогда
			ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат;
		ИначеЕсли Основание.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки Тогда
			ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипОснования = Тип("ДокументСсылка.ПоступлениеДопРасходов")
		И НЕ (Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходов
		ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили) Тогда
		
		Сообщить("Ввод чека на оплату возможен только для хоз. операций ""Поступление дополнительных расходов"" и ""Поступление дополнительных расходов на автомобили""!",СтатусСообщения.Внимание);
		дкОтменаВводаДокумента(ЭтотОбъект);
		Возврат;
		
	КонецЕсли;
	
	// Вызываем стандартный обработчик
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Сделка = ДокументОснование;
	
	Если обЗначениеНеЗаполнено(КассаККМ) Тогда
		КассаККМ=ПараметрыСеанса.Компьютер.КассаККМ;
	КонецЕсли; 
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЧекНаОплату") И
		(ДокументОснование.ХозОперация=Справочники.ХозОперации.ЧекНаОплату ИЛИ
		ДокументОснование.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки) Тогда
		
		КодыМаркировки.Очистить();
		
		// Если был чек на оплату сформирован по сделке
		Если ЗначениеЗаполнено(ДокументОснование.Сделка) Тогда
			Сделка = ДокументОснование.Сделка;
		КонецЕсли;
		
		// Необходимо восстановить именно ту сумму, которая была оплачена
		СуммаДокумента = ДокументОснование.СуммаДокумента;
		
		ТипСпособаРасчетаККТ = ДокументОснование.ТипСпособаРасчетаККТ;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПокупателя") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		
		ВремКомментарий = "";
		
		ДокументОбъектСтруктура=Новый Структура();
		ДокументОбъектСтруктура.Вставить("Дата",          ТекущаяДата());
		ДокументОбъектСтруктура.Вставить("МоментВремени", ЭтотОбъект.МоментВремени());
		ДокументОбъектСтруктура.Вставить("Проведен",      ЭтотОбъект.Проведен);
		
		//ТаблицаРасчетов = зфЗащищенныеФункцииСервер.ПолучитьТаблицуРасчетовПоЗаказам(ДокументОбъектСтруктура, ДокументОснование);
		//KamikadZe begin add 09.03.2017 16:37:14
		ТаблицаРасчетов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, ДокументОснование);
		//KamikadZe end add
		
		ДолгПоПредоплате = 0;
		Долг = 0;
		Для Каждого ТекСтрока Из ТаблицаРасчетов Цикл
			//ДолгПоПредоплате = ДолгПоПредоплате + ТекСтрока.ДолгПоПредоплате;
			//KamikadZe begin add 09.03.2017 16:37:14
			ДолгПоПредоплате = ДолгПоПредоплате + ТекСтрока.Долг;
			//KamikadZe end add
			Долг = Долг + ТекСтрока.Долг;
		КонецЦикла;
		
		Если ДолгПоПредоплате>0 Тогда
			СуммаДокумента = ДолгПоПредоплате;
			ВремКомментарий = "Предоплата по документу <"+дкПолучитьПредставление(ДокументОснование)+">";
		Иначе
			СуммаДокумента = Долг;
		КонецЕсли;
		
		Если НЕ ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
			СуммаДокумента = обПересчет(СуммаДокумента, ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ВремКомментарий) Тогда
			Комментарий = ВремКомментарий;
		КонецЕсли;
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетНаОплату") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили") Тогда
		
		//Проверим сумму предоплаты
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаРасход КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрРасход КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		|		Контрагент = &Контрагент И
		|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		|		(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)
		|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка",                ДокументОснование);
		
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		ДокументОснованиеСуммаДокумента = ДокументОснование.СуммаДокумента;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			Оплачено = обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		Иначе
			Оплачено = обПересчет(тзОплаты.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсДокумента), Дата, ЭтотОбъект.КурсДокумента),РежимОкругления.Окр15как20);
		КонецЕсли;
		
		СуммаДокумента = ДокументОснованиеСуммаДокумента-Оплачено;
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РеализацияТоваров") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
		ОперативнаяОтметкаВремени=ТекущаяДата();
		
		СтруктураОтбора=Новый Структура(); 
		СтруктураОтбора.Вставить("Контрагент",Контрагент);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		СтруктураОтбора.Вставить("Сделка",ДокументОснование);
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ОперативнаяОтметкаВремени,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		Если ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			ОстатокПоСделке=обПересчет(тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		ИначеЕсли Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()=ВалютаДокумента Тогда
			ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
		Иначе
			ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		СуммаДокумента=ОстатокПоСделке;
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
		ОперативнаяОтметкаВремени=ТекущаяДата();
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		//РЕАЛИЗОВАНО В "орОбработкаЗаполнения_ЗаказНаряд_ЧекНаОплату"
		//
		//СуммаСделки=обПересчет(ДокументОснование.СуммаДокумента,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		//СтруктураОтбора=Новый Структура(); 
		//СтруктураОтбора.Вставить("Контрагент",Контрагент);
		//СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		//СтруктураОтбора.Вставить("Сделка",ДокументОснование);
		//тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ОперативнаяОтметкаВремени,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		//Если ДокументОснование.Состояние=Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
		//	ОстатокПоСделке=0;
		//Иначе
		//	ОстатокПоСделке=обПересчет(ДокументОснование.СуммаДокумента,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		//КонецЕсли; 
		//Если ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
		//	ОстатокПоСделке=ОстатокПоСделке+обПересчет(тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		//ИначеЕсли Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()=ВалютаДокумента Тогда
		//	ОстатокПоСделке=ОстатокПоСделке+тзДолги.Итог("СуммаБаз");
		//Иначе
		//	ОстатокПоСделке=ОстатокПоСделке+обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
		//КонецЕсли;
		////ОстатокПоСделке=СуммаСделки-ОстатокПоСделке;
		//
		//СуммаДокумента=ОстатокПоСделке;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПоступлениеАвтомобилей") ИЛИ 
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
		ОперативнаяОтметкаВремени=ТекущаяДата();
		
		ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки;
		
		СтруктураОтбора=Новый Структура(); 
		СтруктураОтбора.Вставить("Контрагент",Контрагент);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		СтруктураОтбора.Вставить("Сделка",ДокументОснование);
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ОперативнаяОтметкаВремени,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		Если ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			ОстатокПоСделке=обПересчет(тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		ИначеЕсли Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()=ВалютаДокумента Тогда
			ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
		Иначе
			ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		
		СуммаДокумента=-ОстатокПоСделке;
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		
		//Проверим сумму предоплаты
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаРасход КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрРасход КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		|		Контрагент = &Контрагент И
		|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		|		(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)
		|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка",                ДокументОснование);
		
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		ДокументОснованиеСуммаДокумента = ДокументОснование.СуммаДокумента;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			Оплачено = обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		Иначе
			Оплачено = обПересчет(тзОплаты.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсДокумента), Дата, ЭтотОбъект.КурсДокумента),РежимОкругления.Окр15как20);
		КонецЕсли;
		
		СуммаДокумента = ДокументОснованиеСуммаДокумента+Оплачено;
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		//Проверим сумму предоплаты
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаРасход КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрРасход КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		|		Контрагент = &Контрагент И
		|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		|		(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)
		|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка",                ДокументОснование);
		
		тзОплаты = Запрос.Выполнить().Выгрузить();
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			Оплачено = обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		Иначе
			Оплачено = обПересчет(тзОплаты.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсДокумента), Дата, ЭтотОбъект.КурсДокумента),РежимОкругления.Окр15как20);
		КонецЕсли;
		
		ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		
		СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
		КурсДокумента = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикам.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СУММА(ЗаказыПоставщикам.Заказано) КАК Заказано,
		|	СУММА(ЗаказыПоставщикам.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ЗаказыПоставщикамОстатки
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|ГДЕ
		|	ЗаказыПоставщикам.Период <= &Момент
		|	И НЕ ЗаказыПоставщикам.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
		|	И ЗаказыПоставщикам.ЗаказПоставщику = &ВыбЗаказПоставщику
		|	И ЗаказыПоставщикам.Контрагент = &ВыбКонтрагент
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПоставщикам.Номенклатура,
		|	ЗаказыПоставщикам.ХарактеристикаНоменклатуры
		|
		|ИМЕЮЩИЕ
		|	(НЕ СУММА(ЗаказыПоставщикам.Заказано) = 0 ИЛИ (НЕ СУММА(ЗаказыПоставщикам.Сумма) = 0))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЗаказыПоставщикамОстатки.Заказано КАК Количество,
		|	ЗаказыПоставщикамОстатки.Сумма КАК СуммаЗаказа,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.Количество * ЗаказПоставщикуТовары.Коэффициент, 1) КАК КоличествоБазовое,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.Коэффициент, 1) КАК Коэффициент,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.СтавкаНДС, ЗаказыПоставщикамОстатки.Номенклатура.СтавкаНДС) КАК СтавкаНДС
		|ИЗ
		|	ЗаказыПоставщикамОстатки КАК ЗаказыПоставщикамОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|		ПО (ЗаказПоставщикуТовары.Ссылка = &ВыбЗаказПоставщику)
		|			И ЗаказыПоставщикамОстатки.Номенклатура = ЗаказПоставщикуТовары.Номенклатура
		|			И ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоБазовое),
		|	МАКСИМУМ(СуммаЗаказа)
		|ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры");
		
		Запрос.УстановитьПараметр("Момент",             КонецДня(Дата));
		Запрос.УстановитьПараметр("ВыбКонтрагент",      Основание.Контрагент);
		Запрос.УстановитьПараметр("ВыбЗаказПоставщику", Основание);
		
		ВалютаЗаказа = ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		
		СтруктураКурса = обКурсДляВалюты(ВалютаЗаказа,Дата);
		КурсЗаказа = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		Товары.Очистить(); // нужные только скорректированные позиции
		ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатуры.Следующий() Цикл
			ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристик.Следующий() Цикл
				ВсегоОсталось = ВыборкаХарактеристик.Количество;
				КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
				СуммаОсталось = обПересчет(ВыборкаХарактеристик.СуммаЗаказа, ВалютаЗаказа, КурсЗаказа,ВалютаДокумента,КурсДокумента,РежимОкругления.Окр15как20);
				ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
				НоваяСтрока   = Неопределено;
				Пока ВыборкаДетали.Следующий() Цикл
					Если ВсегоОсталось=0 Тогда
						Прервать;
					КонецЕсли;
					
					Если ВыборкаДетали.Количество = 0 Тогда
						Продолжить;
					КонецЕсли;
					Если КоличествоБазовоеПоЗаказу = 1 Тогда
						КоличествоСтроки = ВыборкаДетали.Количество;
					Иначе
						КоличествоСтроки = ВыборкаДетали.Количество*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
					КонецЕсли;
					ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
					
					НоваяСтрока=Товары.Добавить();
					НоваяСтрока.Номенклатура				= ВыборкаДетали.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры	= ВыборкаДетали.ХарактеристикаНоменклатуры;
					НоваяСтрока.ЕдиницаИзмерения			= ВыборкаДетали.ЕдиницаИзмерения;
					НоваяСтрока.Коэффициент					= ВыборкаДетали.Коэффициент;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
					НоваяСтрока.Количество					= ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
					//ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
					НоваяСтрока.СтавкаНДС					= ВыборкаДетали.СтавкаНДС;
					ТекСумма = (СуммаОсталось / ВсегоОсталось) * ТекущееКоличество;
					СуммаОсталось = СуммаОсталось - ТекСумма;
				
					НоваяСтрока.СуммаВсего = ТекСумма;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					
					ВсегоОсталось = ВсегоОсталось-ТекущееКоличество;
				КонецЦикла;
				
				Если ВсегоОсталось=0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ НоваяСтрока = Неопределено Тогда
					НоваяСтрока.Количество = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
					ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
				Иначе
					НоваяСтрока=Товары.Добавить();
					НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
					НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
					НоваяСтрока.Количество = ВсегоОсталось/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
					ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
					НоваяСтрока.СуммаВсего = СуммаОсталось;
				КонецЕсли;
				ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
			КонецЦикла;
		КонецЦикла;
		СуммаТоваров = Товары.Итог("СуммаВсего");
		СуммаДокумента	= СуммаТоваров - Оплачено;
		СуммаДокумента = ?(СуммаДокумента < 0, 0, СуммаДокумента);
		ОбработкаРеквизита("СуммаВсего");
		
		// Рассчитаем сумму НДС
		ОдинаковаяСтавкаНДС = Истина;
		СтавкаНДСДокумента  = Неопределено;
		СуммаНДС            = 0;
		
		Для Каждого ТекущаяСтрока Из Товары Цикл
			Если СтавкаНДСДокумента = Неопределено Тогда
				СтавкаНДСДокумента = ТекущаяСтрока.СтавкаНДС;
			ИначеЕсли СтавкаНДСДокумента <> ТекущаяСтрока.СтавкаНДС Тогда
				ОдинаковаяСтавкаНДС = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		// Получим сумму НДС
		СуммаНДС = СуммаНДС + Товары.Итог("СуммаНДС");
		
		// пропорционально установим сумму НДС
		Если СуммаТоваров > СуммаДокумента Тогда
			СуммаНДС = (СуммаДокумента * СуммаНДС) / СуммаТоваров;
		КонецЕсли;
		
		Если ОдинаковаяСтавкаНДС Тогда
			СтавкаНДС = СтавкаНДСДокумента;
		Иначе
			СтавкаНДС = Неопределено;
		КонецЕсли;
		СуммаНДС = СуммаНДС;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратОтПокупателя") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетОтПоставщика") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили") Тогда
		
		Если ЗначениеЗаполнено(ДокументОснование.ДокументОснование)
			И НЕ ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ЗакрытиеСмены") Тогда
			Сделка = ДокументОснование.ДокументОснование;
		КонецЕсли;
		
		// Проверим сумму долга
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	- ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот КАК Сумма,
		|	- ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрОборот КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(
		|			,
		|			,
		|			,
		|			Контрагент = &Контрагент
		|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		|				И (Сделка = &Сделка
		|					ИЛИ Сделка.ДокументОснование = &Сделка)) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|ГДЕ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот < 0";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка", Сделка);
		
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		Если ЭтотОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ЭтотОбъект.ВалютаДокумента Тогда
			Долг = обПересчет(тзОплаты.Итог("Сумма"),ЭтотОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ЭтотОбъект.Дата,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.Дата,РежимОкругления.Окр15как20);
		Иначе
			Долг	= тзОплаты.Итог("СуммаУпр");
			Долг	= обПересчет(Долг,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ?(НЕ ЗначениеЗаполнено(ЭтотОбъект.КурсДокумента), ЭтотОбъект.Дата, ЭтотОбъект.КурсДокумента),РежимОкругления.Окр15как20);
		КонецЕсли;
		
		Если Долг < СуммаДокумента ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетОтПоставщика")
			ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили") Тогда
			СуммаДокумента = Долг;
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		Иначе 
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратПоставщику") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей") Тогда
		
		Если ЗначениеЗаполнено(ДокументОснование.ДокументОснование) Тогда
			Сделка = ДокументОснование.ДокументОснование;
		КонецЕсли;
		
		ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат;
		
		// Проверим сумму долга
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрОборот КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(
		|			,
		|			,
		|			,
		|			Контрагент = &Контрагент
		|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		|				И (Сделка = &Сделка
		|					ИЛИ Сделка.ДокументОснование = &Сделка)) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|ГДЕ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот > 0";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка", Сделка);
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		Если ЭтотОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ЭтотОбъект.ВалютаДокумента Тогда
			Долг = обПересчет(тзОплаты.Итог("Сумма"),ЭтотОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ЭтотОбъект.Дата,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.Дата,РежимОкругления.Окр15как20);
		Иначе
			Долг	= тзОплаты.Итог("СуммаУпр");
			Долг	= обПересчет(Долг,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ?(НЕ ЗначениеЗаполнено(ЭтотОбъект.КурсДокумента), ЭтотОбъект.Дата, ЭтотОбъект.КурсДокумента),РежимОкругления.Окр15как20);
		КонецЕсли;
		
		Если Долг < СуммаДокумента  Тогда
			СуммаДокумента = Долг;
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		Иначе 
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
	
		Контрагент=Основание.Страхователь;
		//вычислим сумму к оплате
		СуммаДок=Основание.ВариантыСтрахования.Итог("СуммаПремии");
		СуммаДокумента=обПересчет(СуммаДок,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,Дата);
		СуммаНДС=0;
		СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		ДоговорВзаиморасчетов = Основание.ДоговорКСО;
	    ДляПробитияНаФР=Истина;
		
		НоваяСтрока=Товары.Добавить();
		НоваяСтрока.Номенклатура				= Справочники.Номенклатура.НайтиПоКоду("ЦБ046062");
		НоваяСтрока.ЕдиницаИзмерения			= НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
		НоваяСтрока.Коэффициент					= 1;
		ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
		НоваяСтрока.Количество					= 1;
		НоваяСтрока.СтавкаНДС					= Справочники.СтавкиНДС.БезНДС;
		
		НоваяСтрока.СуммаВсего = СуммаДок;
		ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);		
		
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПокупателя") И ЗначениеЗаполнено(Основание) Тогда
		
		ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК Количество,
		|	ЗаказыПокупателейОстатки.СуммаОстаток КАК СуммаЗаказа,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.Количество * ЗаказПокупателяТовары.Коэффициент,1) КАК КоличествоБазовое,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.ЕдиницаИзмерения,Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.Коэффициент,1) КАК Коэффициент,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.СтавкаНДС, ЗаказыПокупателейОстатки.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.НомерСтроки, 1000000) КАК НомерСтроки,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.СкидкаНаТовар, ЗНАЧЕНИЕ(Справочник.ТипыСкидок.ПустаяСсылка)) КАК СкидкаНаТовар,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.ПроцентСкидкиСтроки,0) КАК ПроцентСкидкиСтроки,
		|	ЕСТЬNULL(ЗаказПокупателяТовары.ПроцентСкидки,0) КАК ПроцентСкидки
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+",
		|	Контрагент = &Контрагент И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
		|ПО 
		|	ЗаказыПокупателейОстатки.Заказ                      = ЗаказПокупателяТовары.Ссылка И 
		|	ЗаказыПокупателейОстатки.Номенклатура               = ЗаказПокупателяТовары.Номенклатура И 
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказПокупателяТовары.ХарактеристикаНоменклатуры
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ
		|	СУММА(КоличествоБазовое),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(СуммаЗаказа)
		|ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры");
		
		ВалютаЗаказа	= Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		СтруктураКурса	= обКурсДляВалюты(ВалютаЗаказа,Дата);
		КурсЗаказа		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		Запрос.УстановитьПараметр("НаМомент",   МоментВремени());
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("Заказ",      Основание);
		
		СкидкаНаценка = Основание.СкидкаНаценка;
		ОбработкаРеквизита("СкидкаНаценка");
		Товары.Очистить(); // нужные только скорректированные позиции
		ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатуры.Следующий() Цикл
			ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристик.Следующий() Цикл
				ВсегоОсталось = ВыборкаХарактеристик.Количество;
				КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
				СуммаОсталось = обПересчет(ВыборкаХарактеристик.СуммаЗаказа, ВалютаЗаказа, КурсЗаказа,ВалютаДокумента,КурсДокумента);
				ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
				НоваяСтрока   = Неопределено;
				Пока ВыборкаДетали.Следующий() Цикл
					Если ВсегоОсталось=0 Тогда
						Прервать;
					КонецЕсли;
					Если ВыборкаДетали.Количество = 0 Тогда
						Продолжить;
					КонецЕсли;
					Если КоличествоБазовоеПоЗаказу = 1 Тогда
						КоличествоСтроки = ВыборкаДетали.Количество;
					Иначе
						КоличествоСтроки = ВыборкаДетали.Количество*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
					КонецЕсли;
					ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
					НоваяСтрока=Товары.Добавить();
					НоваяСтрока.Номенклатура				= ВыборкаДетали.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры	= ВыборкаДетали.ХарактеристикаНоменклатуры;
					НоваяСтрока.ЕдиницаИзмерения			= ВыборкаДетали.ЕдиницаИзмерения;
					НоваяСтрока.Коэффициент					= ВыборкаДетали.Коэффициент;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
					НоваяСтрока.Количество					= ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);;
					НоваяСтрока.СтавкаНДС					= ВыборкаДетали.СтавкаНДС;
					ТекСумма = (СуммаОсталось/ВсегоОсталось)*ТекущееКоличество;
					СуммаОсталось = СуммаОсталось - ТекСумма;
					
					НоваяСтрока.СкидкаНаТовар = ВыборкаДетали.СкидкаНаТовар;
					НоваяСтрока.ПроцентСкидки = ВыборкаДетали.ПроцентСкидки;
					НоваяСтрока.ПроцентСкидкиСтроки = ВыборкаДетали.ПроцентСкидкиСтроки;
					
					НоваяСтрока.СуммаВсего = ТекСумма;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					
					ВсегоОсталось = ВсегоОсталось-ТекущееКоличество;
				КонецЦикла;
				
				Если ВсегоОсталось>0 ИЛИ СуммаОсталось>0 Тогда
					Если НЕ НоваяСтрока = Неопределено Тогда
						НоваяСтрока.Количество = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						НоваяСтрока.СуммаВсего = НоваяСтрока.СуммаВсего + СуммаОсталось;
					Иначе
						НоваяСтрока=Товары.Добавить();
						НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
						ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
						НоваяСтрока.Количество = ВсегоОсталось/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						НоваяСтрока.СуммаВсего = СуммаОсталось;
					КонецЕсли;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		ДополнительныеСвойства.Удалить("НеПересчитыватьСуммуДокумента");
	КонецЕсли;
	
	ВалютаДенежныхСредствККМ=КассаККМ.ВалютаДенежныхСредств;
	Если (НЕ обЗначениеНеЗаполнено(ВалютаДенежныхСредствККМ)) И ВалютаДенежныхСредствККМ<>ВалютаДокумента Тогда
		СтруктураКурса=обКурсДляВалюты(ВалютаДенежныхСредствККМ, Дата);
		КурсВалютыДенежныхСредствККМ=СтруктураКурса.Курс/?(СтруктураКурса.Кратность=0,1,СтруктураКурса.Кратность);
		Для каждого СтрокаТЧ Из Товары Цикл
			СтрокаТЧ.Цена=обПересчет(СтрокаТЧ.Цена,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
			СтрокаТЧ.Сумма=обПересчет(СтрокаТЧ.Сумма,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
			СтрокаТЧ.СуммаСкидки=обПересчет(СтрокаТЧ.СуммаСкидки,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
			СтрокаТЧ.СуммаСкидкиСтроки=обПересчет(СтрокаТЧ.СуммаСкидкиСтроки,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
			СтрокаТЧ.СуммаНДС=обПересчет(СтрокаТЧ.СуммаНДС,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
			СтрокаТЧ.СуммаВсего=обПересчет(СтрокаТЧ.СуммаВсего,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
			СтрокаТЧ.СуммаСкидкиБонусами=обПересчет(СтрокаТЧ.СуммаСкидкиБонусами,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
		КонецЦикла;
		СуммаДокумента=обПересчет(СуммаДокумента,ВалютаДокумента,КурсДокумента,ВалютаДенежныхСредствККМ,КурсВалютыДенежныхСредствККМ);
		ВалютаДокумента=ВалютаДенежныхСредствККМ;
		КурсДокумента=КурсВалютыДенежныхСредствККМ;
	КонецЕсли; 
	
	// Для нового документа нельзя заполнять данные ФР
	Если ЭтоНовый() Тогда
		Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
		Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ТипСпособаРасчетаККТ) Тогда
			ЭтотОбъект.ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) И (ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваров") ИЛИ 
		ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратПоставщику") ИЛИ
		ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") ИЛИ
		ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратОтПокупателя") ИЛИ
		(ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплату") И НЕ обЗначениеНеЗаполнено(Основание.ДокументОснование) И
		(ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров") ИЛИ
		ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд"))) ИЛИ 
		(ТипЗнч(Основание) = Тип("ДокументСсылка.СчетОтПоставщика") И НЕ обЗначениеНеЗаполнено(Основание.ДокументОснование) И
		ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров"))) Тогда
		БлокироватьПерерасчетСкидок = Истина;
		дкЗаполнитьГТД(ЭтотОбъект, Товары);
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров")
		И ДокументОснование.ХоЗоперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
		ДоговорКомиссионера = ДокументОснование.ДоговорВзаиморасчетов;
		Для Каждого ТекущаяСтрока Из Товары Цикл
			ТекущаяСтрока.ДоговорВзаиморасчетов = ДоговорКомиссионера;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетОтПоставщика")
		И НЕ обЗначениеНеЗаполнено(ДокументОснование.ДокументОснование)
		И ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров")
		И ДокументОснование.ДокументОснование.ХоЗоперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
		ДоговорКомиссионера = ДокументОснование.ДокументОснование.ДоговорВзаиморасчетов;
		Для Каждого ТекущаяСтрока Из Товары Цикл
			ТекущаяСтрока.ДоговорВзаиморасчетов = ДоговорКомиссионера;
		КонецЦикла;
	КонецЕсли;   
	
	//<<БАА
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.БТ_ПредоплаченноеТО") Тогда
		СуммаДокумента = Основание.СуммаДокумента - Документы.БТ_ПредоплаченноеТО.СуммаОплатПоПредТО(Основание);
		Товары.Очистить();
		Если СуммаДокумента > 0 Тогда
			НоваяСтрока = Товары.Добавить();
			//НоваяСтрока.Номенклатура = Справочники.Номенклатура.ПредоплаченноеТО;     
			//ЧалапАю БизТех 12.08.2024                                                
			Если не Справочники.Номенклатура.НайтиПоНаименованию("Предоплаченное ТО") = Неопределено тогда 
				НоваяСтрока.Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию("Предоплаченное ТО")
			иначе
				Сообщить("В спрваочнике Ногменклатура не найдена номенклатура с наименованием Предоплаченное ТО");
				Возврат
			КонецЕсли;
			
			НоваяСтрока.Количество = 1;
			НоваяСтрока.Коэффициент = 1;
			
			НоваяСтрока.Цена = СуммаДокумента;
			НоваяСтрока.Сумма = СуммаДокумента;
			НоваяСтрока.СуммаВсего = СуммаДокумента; 
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС; 
			НоваяСтрока.СуммаНДС=Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
			НоваяСтрока.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Услуга;
			НоваяСтрока.ЕдиницаИзмерения = НоваяСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
		КонецЕсли;	
	КонецЕсли;	
	//>>БАА

	
	// Заполним доп. поля для товарной строки
	Для Каждого СтрокаТовар Из Товары Цикл
		
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
	КонецЦикла;
	
	// Перезаполним коды маркировок только при вводе на основании конкретных документов
	дкЗаполнитьКодыМаркировкиТоваров(ЭтотОбъект, Основание);
	
	Если НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщику") 
		и НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СтраховойПолис") и НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
		дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, ЭтотОбъект.ДокументОснование, СуммаДокумента);
	КонецЕсли;  
	
	//Если НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СтраховойПолис") и НЕ ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда
	//	дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, ЭтотОбъект.ДокументОснование, СуммаДокумента);
	//КонецЕсли;	
	
	// Всегда задается принудительно кассиром при приеме оплаты/возврате от покупателя
	ПолученоНал=0; ПолученоБезнал=0;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	// Установим значение вспомогательного реквизита (используется для отбора документов в журнале документов на оплату)
	ДляПробитияНаФР = ИСТИНА;
	
	// Вызываем стандартный обработчик
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//БИЗТЕХ КОВАЛЬ 16.05.2025 ++
	//Если Оплата была по QR О2 то запрет изменений
	#Если Клиент Тогда
	Отказ = О2_QR_ОбщийМодуль.БлокировкаЧекаНаОплату(О2_QR_ОплатаПоQR);
	#КонецЕсли
	//БИЗТЕХ КОВАЛЬ 16.05.2025 --
	
	//БИЗТЕХ МАМОНОВ 22.01.2025 ++
	//Если количество товара НЕ равно количеству маркировок, то удаляем маркировки по этому товару из ТЧ "Коды маркировки"
	Для Каждого Товар Из Товары Цикл
		СтруктураПоиска = Новый Структура("ИдентификаторТовара", Товар.ИдентификаторТовара);
		МассивКМДляТовара = КодыМаркировки.НайтиСтроки(СтруктураПоиска);
		Если МассивКМДляТовара.Количество() И Товар.Количество <> МассивКМДляТовара.Количество() Тогда
			Для Каждого СтрокаКМ Из МассивКМДляТовара Цикл
				КодыМаркировки.Удалить(СтрокаКМ);				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 22.01.2025 --
	
	Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
		Если СуммаДокумента <> Товары.Итог("СуммаВсего") Тогда
			Отказ = Истина;
			Сообщить("Неверно заполнена колонка ""Всего"" в табличной части товары");
		ИначеЕсли СуммаДокумента <> Оплаты.Итог("Сумма") Тогда
			Отказ = Истина;
			Сообщить("Неверно заполнена колонка ""Сумма"" в табличной части оплаты");
		КонецЕсли;
		Если НЕ Отказ Тогда
			Для Каждого СтрокаТоваров ИЗ Товары Цикл
				СтрокаТоваров.СуммаОплаты = СтрокаТоваров.СуммаВсего;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Если СуммаДокумента > 0 И Товары.Итог("СуммаОплаты") = 0 Тогда
			Отказ = Истина;
			Сообщить("Необходимо произвести пропорциональное распределение суммы вносимой оплаты между предметами платежа");
		ИначеЕсли СуммаДокумента <> Товары.Итог("СуммаОплаты") Тогда
			Отказ = Истина;
			Сообщить("Неверно заполнена колонка ""Сумма оплаты"" в табличной части товары, необходимо распределить сумму оплаты");
		ИначеЕсли СуммаДокумента <> Оплаты.Итог("Сумма") Тогда
			Отказ = Истина;
			Сообщить("Неверно заполнена колонка ""Сумма"" в табличной части оплаты");
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Установим признак необходимости проведения контроля оплат
	КонтролироватьЗаполнениеОплат = (РежимЗаписи=РежимЗаписиДокумента.Проведение);
	
	// Отключаем функцию автоматического подитога табличной части "Товары"
	//СуммаДокументаКОплате=СуммаДокумента;
	ДополнительныеСвойства.Вставить("НеПересчитыватьСуммуДокумента", Истина);
	
	// Стандартный обработчик	
	Если ОтключитьПроверкиПередЗаписью Тогда
		флОбработки=дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , Ложь, Ложь, "00000");
	Иначе
		флОбработки=дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	КонтролироватьЗаполнениеОплат = Истина;
	
	// Восстанавливаем функцию подитога
	ДополнительныеСвойства.Удалить("НеПересчитыватьСуммуДокумента");
	
	// Проверяем результат работы стандартного обработчика
	Если НЕ флОбработки Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	//БИЗТЕХ КОВАЛЬ 16.05.2025 ++
	//Если Оплата была по QR О2 то запрет изменений
	Отказ = О2_QR_ОбщийМодуль.БлокировкаЧекаНаОплату(О2_QR_ОплатаПоQR);
	//БИЗТЕХ КОВАЛЬ 16.05.2025 --
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	ОтменитьПроверкуДоступностиСкладов = Истина;
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// определим способ ведения баланса
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// ANDV Проведем деньги по кассе ККМ
   	НаборЗаписейКассыККМ=Движения.КассыККМ;
	СуммаОплатыПоКассе =0;
	Для Каждого СтрокаОплаты из Оплаты Цикл
		НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
		НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
		НаборЗаписейКассыККМ.КассаККМ 			= КассаККМ;
		НаборЗаписейКассыККМ.СпособОплаты 		= СтрокаОплаты.ТипОплаты;
		
		Если (ХозОперация=Справочники.ХозОперации.ЧекНаОплату) ИЛИ
			(ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки) Тогда
			НаборЗаписейКассыККМ.Сумма = СтрокаОплаты.Сумма;	
		Иначе
			НаборЗаписейКассыККМ.Сумма = -СтрокаОплаты.Сумма;
		КонецЕсли;
		
		Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
			ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
			Отказ=НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
		Иначе
			Отказ=НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		Если СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Наличные ИЛИ
			СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Электронно Тогда
			Если (ХозОперация=Справочники.ХозОперации.ЧекНаОплату) ИЛИ
				(ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат) Тогда
				СуммаОплатыПоКассе = СуммаОплатыПоКассе+СтрокаОплаты.Сумма;
			Иначе
				СуммаОплатыПоКассе = СуммаОплатыПоКассе-СтрокаОплаты.Сумма;
			КонецЕсли;  			
		КонецЕсли;	 
	КонецЦикла;
	
	
	// подготовим таблицу движений в разрезе подразделений по взаиморасчетам
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений();
	ТаблицаВзаиморасчетов.Колонки.Добавить("Сумма");
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	
	// проведем взаиморасчеты
	Для Каждого СтрокаОплаты из Оплаты Цикл
		
		//Если (СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата ИЛИ
		//	 СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата) И
		//	 НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения Тогда
		
		//KamikadZe begin add 26.12.2018 17:28:41
		Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Кредит ИЛИ
			//anton
			 СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Безналичные ИЛИ	
			//anton
			//KamikadZe end add
			(СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата ИЛИ
			СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата) И
			НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения Тогда			 
			
			НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
			НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
			НаборЗаписейВзаиморасчеты.Контрагент			= СтрокаОплаты.Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = СтрокаОплаты.ДоговорВзаиморасчетов;
			Если ЗначениеЗаполнено(Сделка) Тогда
				НаборЗаписейВзаиморасчеты.Сделка = Сделка;
			Иначе
				НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
			КонецЕсли; 
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
			Если (ХозОперация=Справочники.ХозОперации.ЧекНаОплату) ИЛИ
				(ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки) Тогда
				НаборЗаписейВзаиморасчеты.Сумма = СтрокаОплаты.Сумма;
			Иначе
				НаборЗаписейВзаиморасчеты.Сумма = -СтрокаОплаты.Сумма;
				НаборЗаписейВзаиморасчеты.ПриходРасход=Ложь;
			КонецЕсли;
			//НаборЗаписейВзаиморасчеты.СделкиКонтрагента=Ложь;
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
			
			Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
				ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
			Иначе
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			КонецЕсли;
		
			// доходы и расходы по суммовым разницам
			СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
				Если ВедетсяБалансПоПодразделению Тогда
					НаборЗаписейДиР.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
					НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
				Иначе
					НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				КонецЕсли;
				Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
					ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
					Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
				Иначе
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли;
			КонецЕсли;
			
			//KamikadZe begin add 26.12.2018 17:28:41
			Если НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Кредит 
				//anton
				И НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Безналичные 	
				//anton			
				Тогда
				//KamikadZe end add
				
				СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
				
				СтрокаВзаиморасчетов.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
				Если (ХозОперация=Справочники.ХозОперации.ЧекНаОплату) ИЛИ
					(ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат) Тогда
					СтрокаВзаиморасчетов.Сумма = СтрокаОплаты.Сумма;
				Иначе
					СтрокаВзаиморасчетов.Сумма = -СтрокаОплаты.Сумма;
				КонецЕсли;  
				
				//KamikadZe begin add 26.12.2018 17:52:47
			КонецЕсли;
			//KamikadZe end add			
		КонецЕсли;
	КонецЦикла;	

	СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
	СтрокаВзаиморасчетов.Подразделение = КассаККМ.Подразделение;
	СтрокаВзаиморасчетов.Сумма = СуммаОплатыПоКассе;
	ТаблицаВзаиморасчетов.Свернуть("Подразделение","Сумма");
	
	
	// проводим взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	Если ЗначениеЗаполнено(Сделка)Тогда
		НаборЗаписейВзаиморасчеты.Сделка = Сделка;
		ТипСделка=ТипЗнч(Сделка);
		Если орПолучитьТипыСделок(Ложь).СодержитТип(ТипСделка) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		ИначеЕсли орПолучитьТипыСделок(Истина).СодержитТип(ТипСделка) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		Иначе
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Неопределено;
		КонецЕсли; 
	Иначе
		НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
	КонецЕсли; 
	НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок               = АвтоЗакрытиеСделок;
	НаборЗаписейВзаиморасчеты.Сумма                            = ТаблицаВзаиморасчетов.Итог("Сумма");
	Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
		ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
		НаборЗаписейВзаиморасчеты.Сумма = - НаборЗаписейВзаиморасчеты.Сумма;
	КонецЕсли;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
	//НаборЗаписейВзаиморасчеты.СделкиКонтрагента=Ложь;
	Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат ИЛИ
		ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки Тогда
		НаборЗаписейВзаиморасчеты.ПриходРасход=Истина;
	КонецЕсли;
	Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
		ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
	Иначе
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	КонецЕсли;
	
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Если ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
			ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		Иначе
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
		
	Для Каждого СтрокаСписания Из ТаблицаВзаиморасчетов Цикл
		ПодразделениеВыручки = обПолучитьБалансовоеПодразделение(СтрокаСписания.Подразделение);
		ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеВыручки);
		Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейДиР.Подразделение			= СтрокаСписания.Подразделение;
			НаборЗаписейДиР.ВУпрВалюте				= Ложь;
			НаборЗаписейДиР.Доход					= СтрокаСписания.Сумма;
			Если СтрокаСписания.Сумма>0 Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			Иначе	
				НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
			КонецЕсли;	
			
			НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
			НаборЗаписейДиР.Подразделение			= ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте				= Ложь;
			НаборЗаписейДиР.Расход					= СтрокаСписания.Сумма;
			Если СтрокаСписания.Сумма>0 Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			Иначе	
				НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;
	
	Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
		СообщениеОбОшибке=дкДобавитьОшибку(?(СообщениеОбОшибке=Неопределено,"",СообщениеОбОшибке), ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
 
	Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
		СообщениеОбОшибке=дкДобавитьОшибку(?(СообщениеОбОшибке=Неопределено,"",СообщениеОбОшибке), ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// выведем сообщения об ошибках и предупреждениях
	Если СообщениеОбОшибке=Неопределено Тогда
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	Иначе
		Если ТипЗнч(РезультатОбработки)=Тип("Строка") Тогда
			СообщениеОбОшибке=РезультатОбработки;
		Иначе
			Для Каждого СтрокаОшибки Из РезультатОбработки.Сообщения Цикл
				СообщениеОбОшибке=СообщениеОбОшибке+СтрокаОшибки.Сообщение+"
				|";
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроведения()

Функция СоздатьДокРеализацияУслуг(ДокументОснование)
	//поиск документа
    Запрос = Новый запрос;  
	Запрос.Текст =  "ВЫБРАТЬ
	                |	РеализацияТоваров.Ссылка КАК Ссылка
	                |ИЗ
	                |	Документ.РеализацияТоваров КАК РеализацияТоваров
	                |ГДЕ
	                |	РеализацияТоваров.ДокументОснование = &ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество()>0 Тогда
		ДокРеал = Результат[0].Ссылка; 
		Док = ДокРеал.ПолучитьОбъект();
	Иначе 
		Док = Документы.РеализацияТоваров.СоздатьДокумент(); 
	КонецЕсли;
	//Заполняем документ
	Док.ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг;
	Если НЕ дкОбработкаЗаполнения(Док, ДокументОснование,, Ложь) Тогда
		Возврат Неопределено; 
	КонецЕсли;
	Док.Комитент = ДокументОснование.Комитент;
	Док.ДоговорКомитента = ДокументОснование.ДоговорКомитента;  
	Док.ДокументОснование = ДокументОснование;
	Док.Контрагент = ДокументОснование.Страхователь;   
	//<<ЧалапАЮ БизТех 06.10.2023   
	Если этоНовый() тогда
		Если ЗначениеЗаполнено(Док.Контрагент.БТ_ПризнакЛояльности)  тогда            
			//Сообщить("Контрагент " + Док.Контрагент.Наименование +" имеет признак " + Док.Контрагент.БТ_ПризнакЛояльности +", дальнейшие работы с ним запрещены, необходимо обратиться к руководителю отдела.");	
			//Док.Контрагент = Справочники.Контрагенты.ПустаяСсылка();  
			#Если Клиент Тогда
			Предупреждение("Контрагент " + Док.Контрагент.Наименование +" имеет признак " + Док.Контрагент.БТ_ПризнакЛояльности); //ЧалапАю БизТех 10.01.2025
			#Иначе
			Сообщить("Контрагент " + Док.Контрагент.Наименование +" имеет признак " + Док.Контрагент.БТ_ПризнакЛояльности); // БИЗТЕХ Коваль 16.05 добавил условие #Если Клиент из за HTTP сервиса О2 QR
			#КонецЕсли
		КонецЕсли;
	КонецЕсли;
	//ЧалапАЮ БизТех 06.10.2023>>
	Док.ДоговорВзаиморасчетов = ДокументОснование.ДоговорКСО;
	СтрокаУсл = Док.Товары.Добавить(); 
	Ном = Справочники.Номенклатура.НайтиПоНаименованию("Финансовый продукт страхование");
	СтрокаУсл.Номенклатура = Ном;
	СтрокаУсл.ЕдиницаИзмерения = Ном.ОсновнаяЕдиницаИзмерения;
	СтрокаУсл.Цена = ДокументОснование.ВариантыСтрахования.Итог("СуммаПремии"); 
	СтрокаУсл.Количество = 1;
	СтрокаУсл.Коэффициент = 1;
	СтрокаУсл.Сумма = СтрокаУсл.Цена*СтрокаУсл.Количество;
	СтрокаУсл.СуммаВсего = СтрокаУсл.Сумма;
	СтрокаУсл.СтавкаНДС = Справочники.СтавкиНДС.БезНДС; 
	СтрокаУсл.СуммаНДС = Окр((СтрокаУсл.Сумма * СтрокаУсл.СтавкаНДС.Ставка)/(100 + СтрокаУсл.СтавкаНДС.Ставка),2);
	СтрокаУсл.СтатьяДоходов = Справочники.СтатьиДоходовИРасходов.НайтиПоНаименованию("Финансовй продукт страхования");   
	Док.Комментарий = "Создан автоматически "+ТекущаяДата();
	Док.Записать();  
	Возврат Док.Ссылка;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
ОтключитьПроверкиПередЗаписью = Ложь;
КонтролироватьЗаполнениеОплат = ИСТИНА;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
#КонецЕсли 

СообщениеОбОшибке=Неопределено;


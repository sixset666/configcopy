// Модуль документа ВозвратОтПокупателяАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаРасчетСкидокАвтомобили Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

Перем КешСебестоимостиАвтомобилейКупленныхУФизЛица; // Переменная для хранения значения валюты регламентированного учета


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция возвращает положительную себестоимость автомобиля в валюте документа если 
// этот автомобиль был приобретен у физического лица
//
// Параметры:
//  СтрокаТЧ       - строка табличной части Товары
//
// Возвращаемое значение:
//  Себестоимость - Число себестоимость автомобиля в валюте документа
// 
Функция ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(СтрокаТЧ, ДокументРеализации)
	
	// Перезаполняем ставки НДС только для сФ введенных на основания Реализации автомобилей
	Если НЕ ДокументРеализации.ХозОперация = Справочники.ХозОперации.РеализацияАвтомобилей Тогда
		Возврат 0;
	КонецЕсли;
	
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("ХозОперация",Справочники.ХозОперации.РеализацияАвтомобилей);
	ДокументОбъектСтруктура.Вставить("ВалютаДокумента",ВалютаДокумента);
	ДокументОбъектСтруктура.Вставить("КурсДокумента",КурсДокумента);
	Если КешСебестоимостиАвтомобилейКупленныхУФизЛица=Неопределено Тогда
		ДокументОбъектСтруктура.Вставить("КешСебестоимостиАвтомобилейКупленныхУФизЛица",Неопределено);
	Иначе
		ДокументОбъектСтруктура.Вставить("КешСебестоимостиАвтомобилейКупленныхУФизЛица",КешСебестоимостиАвтомобилейКупленныхУФизЛица.Скопировать());
	КонецЕсли; 
	ДокументОбъектСтрока=Новый Структура();
	Для каждого РеквизитТабличнойЧасти Из Метаданные.Документы.РеализацияАвтомобилей.ТабличныеЧасти.Автомобили.Реквизиты Цикл
		ДокументОбъектСтрока.Вставить(РеквизитТабличнойЧасти.Имя,СтрокаТЧ[РеквизитТабличнойЧасти.Имя]);
	КонецЦикла; 
	Себестоимость=зфЗащищенныеФункцииСервер.РеализацияАвтомобилейПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(ДокументОбъектСтруктура,ДокументОбъектСтрока);
	Если ДокументОбъектСтруктура.КешСебестоимостиАвтомобилейКупленныхУФизЛица<>Неопределено Тогда
		КешСебестоимостиАвтомобилейКупленныхУФизЛица=ДокументОбъектСтруктура.КешСебестоимостиАвтомобилейКупленныхУФизЛица.Скопировать();
	КонецЕсли; 
	Возврат Себестоимость;
КонецФункции // ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица()


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("ИдентификаторАвтомобиля",1);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	ОбязательныеАвтомобили.Вставить("ДокументПродажи",3);
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("ИдентификаторАвтомобиля",3);
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат=дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Если ДокументОснование.Контрагент<>Контрагент Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Контрагент документа основания отличается от контрагента текущего документа ");
		КонецЕсли;
		
		Если ДокументОснование.ДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Договор взаиморасчетов документа основания отличается от договора взаиморасчетов текущего документа ");
		КонецЕсли;
	КонецЕсли;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);   		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВозвратАвтомобилейОтПокупателя","Возврат автомобилей от покупателя",Истина);
	СписокХозОпераций.Добавить("ВозвратАвтомобилейОтПокупателяКомиссия","Возврат автомобилей от покупателя (комиссия)");
	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(СуммыТаблиц.СуммаАвтомобилей) КАК СуммаАвтомобилей,
	|	СУММА(СуммыТаблиц.СуммаДопОборудования) КАК СуммаДопОборудования
	|ПОМЕСТИТЬ
	|	СуммыТаблиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаАвтомобили.СуммаВсего КАК СуммаАвтомобилей,
	|		0 КАК СуммаДопОборудования
	|	ИЗ
	|		Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ТаблицаАвтомобили
	|	ГДЕ
	|		ТаблицаАвтомобили.Ссылка=&Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		0,
	|		ТаблицаТовары.СуммаВсего
	|	ИЗ
	|		Документ.ВозвратОтПокупателяАвтомобилей.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка=&Ссылка) КАК СуммыТаблиц
	|;
	|
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	|	ЕСТЬNULL(СуммыТаблиц.СуммаАвтомобилей, 0) КАК СуммаАвтомобилей,
	|	ЕСТЬNULL(СуммыТаблиц.СуммаДопОборудования, 0) КАК СуммаДопОборудования
	|
	|ИЗ
	|	Документ.ВозвратОтПокупателяАвтомобилей КАК Док
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	СуммыТаблиц КАК СуммыТаблиц
	|ПО
	|	ИСТИНА
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;	
	
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	//Вернем обратно опции и оборудование
	Если НЕ Отказ Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
		             |	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
		             |	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	КомплектацияАвтомобилей.СтатусПартии КАК СтатусПартии,
		             |	КомплектацияАвтомобилей.Партия КАК Партия,
		             |	КомплектацияАвтомобилей.ГТД КАК ГТД,
		             |	РеализацияАвтомобилейТовары.СтавкаНДС,
		             |	КомплектацияАвтомобилей.Количество КАК Количество,
		             |	КомплектацияАвтомобилей.Сумма КАК Сумма,
		             |	КомплектацияАвтомобилей.СуммаНДС КАК СуммаНДС,
		             |	КомплектацияАвтомобилей.СуммаУпр КАК СуммаУпр,
		             |	КомплектацияАвтомобилей.СуммаПродажи КАК СуммаПродажи,
		             |	КомплектацияАвтомобилей.СуммаПродажиУпр КАК СуммаПродажиУпр
		             |ИЗ
		             |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		             |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		             |			РеализацияАвтомобилейАвтомобили.Ссылка КАК Ссылка,
		             |			РеализацияАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		             |			РеализацияАвтомобилейТовары.Номенклатура КАК Номенклатура,
		             |			РеализацияАвтомобилейТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |			РеализацияАвтомобилейТовары.СтавкаНДС КАК СтавкаНДС
		             |		ИЗ
		             |			Документ.РеализацияАвтомобилей.Автомобили КАК РеализацияАвтомобилейАвтомобили
		             |				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияАвтомобилей.Товары КАК РеализацияАвтомобилейТовары
		             |				ПО РеализацияАвтомобилейАвтомобили.Ссылка = РеализацияАвтомобилейТовары.Ссылка
		             |					И РеализацияАвтомобилейАвтомобили.ИдентификаторАвтомобиля = РеализацияАвтомобилейТовары.ИдентификаторАвтомобиля
		             |		ГДЕ
		             |			РеализацияАвтомобилейАвтомобили.Ссылка В(
					 |				ВЫБРАТЬ РАЗЛИЧНЫЕ
					 |					ВозвратОтПокупателяАвтомобилейАвтомобили.ДокументПродажи
					 |				ИЗ
					 |					Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ВозвратОтПокупателяАвтомобилейАвтомобили
					 |				ГДЕ
					 |					ВозвратОтПокупателяАвтомобилейАвтомобили.Ссылка = &Ссылка
					 |			)) КАК РеализацияАвтомобилейТовары
		             |		ПО КомплектацияАвтомобилей.Автомобиль = РеализацияАвтомобилейТовары.Автомобиль
		             |			И КомплектацияАвтомобилей.Номенклатура = РеализацияАвтомобилейТовары.Номенклатура
		             |			И КомплектацияАвтомобилей.ХарактеристикаНоменклатуры = РеализацияАвтомобилейТовары.ХарактеристикаНоменклатуры
		             |			И КомплектацияАвтомобилей.Регистратор = РеализацияАвтомобилейТовары.Ссылка
		             |ГДЕ
		             |	КомплектацияАвтомобилей.Регистратор В(
					 |		ВЫБРАТЬ РАЗЛИЧНЫЕ
					 |			ВозвратОтПокупателяАвтомобилейАвтомобили.ДокументПродажи
					 |		ИЗ
					 |			Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ВозвратОтПокупателяАвтомобилейАвтомобили
					 |		ГДЕ
					 |			ВозвратОтПокупателяАвтомобилейАвтомобили.Ссылка = &Ссылка
					 |	)
		             |	И КомплектацияАвтомобилей.Автомобиль В(
					 |		ВЫБРАТЬ РАЗЛИЧНЫЕ
					 |			ВозвратОтПокупателяАвтомобилейАвтомобили.Автомобиль
					 |		ИЗ
					 |			Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ВозвратОтПокупателяАвтомобилейАвтомобили
					 |		ГДЕ
					 |			ВозвратОтПокупателяАвтомобилейАвтомобили.Ссылка = &Ссылка
					 |	)
		             |	И КомплектацияАвтомобилей.ВидДвижения = &ВидДвиженияРасход
					 
					 |ОБЪЕДИНИТЬ ВСЕ
					 |ВЫБРАТЬ
		             |	ВводОстатковАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		             |	ВводОстатковАвтомобилейОпции.Опция КАК Номенклатура,
		             |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
		             |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
		             |	ВводОстатковАвтомобилейОпции.Ссылка КАК Партия,
		             |	ВводОстатковАвтомобилейАвтомобили.ГТД КАК ГТД,
		             |	ВводОстатковАвтомобилейОпции.СтавкаНДС КАК СтавкаНДС,
		             |	ВводОстатковАвтомобилейОпции.Количество КАК Количество,
		             |	ВводОстатковАвтомобилейОпции.СуммаВсего/ВводОстатковАвтомобилейОпции.Ссылка.КурсДокумента КАК Сумма,
		             |	ВводОстатковАвтомобилейОпции.СуммаНДС/ВводОстатковАвтомобилейОпции.Ссылка.КурсДокумента КАК СуммаНДС,
		             |	ВводОстатковАвтомобилейОпции.СуммаВсего/ВводОстатковАвтомобилейОпции.Ссылка.КурсВалютыУпр КАК СуммаУпр,
		             |	ВводОстатковАвтомобилейОпции.СуммаВсего/ВводОстатковАвтомобилейОпции.Ссылка.КурсДокумента КАК СуммаПродажи,
		             |	ВводОстатковАвтомобилейОпции.СуммаВсего/ВводОстатковАвтомобилейОпции.Ссылка.КурсВалютыУпр КАК СуммаПродажиУпр
		             |ИЗ
		             |	Документ.ВводОстатковАвтомобилей.Опции КАК ВводОстатковАвтомобилейОпции
					 |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковАвтомобилей.Автомобили КАК ВводОстатковАвтомобилейАвтомобили
					 |		ПО ВводОстатковАвтомобилейОпции.Ссылка = ВводОстатковАвтомобилейАвтомобили.Ссылка
					 |			И ВводОстатковАвтомобилейОпции.ИдентификаторАвтомобиля = ВводОстатковАвтомобилейАвтомобили.ИдентификаторАвтомобиля
		             |ГДЕ
		             |	ВводОстатковАвтомобилейОпции.Ссылка В (&ДокументыПродажи) И
					 |	ВводОстатковАвтомобилейОпции.Ссылка.ХозОперация=ЗНАЧЕНИЕ(Справочник.ХозОперации.ВводОстатковАвтомобилейПереданныхНаКомиссию)
			
					 |";
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ДокументыПродажи",Автомобили.ВыгрузитьКолонку("ДокументПродажи"));
		ВыборкаОборудования=Запрос.Выполнить().Выбрать();
		НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
		ДвиженияОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей.Выгрузить();
		Пока ВыборкаОборудования.Следующий() Цикл
			НоваяЗапись=НаборЗаписейКомплектацияАвтомобилей.Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗапись.Автомобиль=ВыборкаОборудования.Автомобиль;
			НоваяЗапись.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НоваяЗапись.Номенклатура=ВыборкаОборудования.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры=ВыборкаОборудования.ХарактеристикаНоменклатуры;
			// Вернем по партии с которой списано было оборудование.
			СтрокаОстаткиАвтомобилей=ДвиженияОстаткиАвтомобилей.Найти(ВыборкаОборудования.Автомобиль,"Автомобиль");
			Если СтрокаОстаткиАвтомобилей.Партия = ЭтотОбъект.Ссылка Тогда
				НоваяЗапись.СтатусПартии=СтрокаОстаткиАвтомобилей.СтатусПартии;
				НоваяЗапись.Партия=СтрокаОстаткиАвтомобилей.Партия;
			Иначе
				НоваяЗапись.СтатусПартии=ВыборкаОборудования.СтатусПартии;
				НоваяЗапись.Партия=ВыборкаОборудования.Партия;
			КонецЕсли;
			НоваяЗапись.ГТД=ВыборкаОборудования.ГТД;
			НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
			НоваяЗапись.СтавкаНДС=ВыборкаОборудования.СтавкаНДС;
			НоваяЗапись.Количество=-ВыборкаОборудования.Количество;
			НоваяЗапись.Сумма=-ВыборкаОборудования.Сумма;
			НоваяЗапись.СуммаНДС=-ВыборкаОборудования.СуммаНДС;
			НоваяЗапись.СуммаУпр=-ВыборкаОборудования.СуммаУпр;
			НоваяЗапись.СуммаПродажи=-ВыборкаОборудования.СуммаПродажи;
			НоваяЗапись.СуммаПродажиУпр=-ВыборкаОборудования.СуммаПродажиУпр;
		КонецЦикла; 
		Если НЕ Отказ Тогда
			НаборЗаписейКомплектацияАвтомобилей.Записать();
		КонецЕсли; 
	КонецЕсли;
	
	// двигаем границу последовательности комплектаций автомобилей
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); АвтомобильБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				АвтомобильБыл = Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("АвтомобильБыл",АвтомобильБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  = ШапкаДокумента.Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.Расход=ШапкаДокумента.СуммаДокумента;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр),0) КАК СуммаУпр
			|ИЗ
			|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			|ГДЕ
			|	ОстаткиАвтомобилей.Регистратор = &Регистратор";
			Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.СуммаУпр<>0 Тогда
					НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
					НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
					НаборЗаписейДоходыИРасходы.Расход=Выборка.СуммаУпр;
					НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаУпр<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				НаборЗаписейДоходыИРасходы.Расход=Выборка.СуммаУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
		Возврат НЕ Отказ;
	КонецЕсли;
	
	Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
		// если среди возвращенного товара есть комиссионный, то отсторнируем реализованные товары
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	КомплектацияАвтомобилей.Автомобиль,
		|	КомплектацияАвтомобилей.СкладКомпании,
		|	КомплектацияАвтомобилей.Номенклатура,
		|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
		|	КомплектацияАвтомобилей.СтатусПартии,
		|	КомплектацияАвтомобилей.Партия,
		|	КомплектацияАвтомобилей.ГТД,
		|	КомплектацияАвтомобилей.Количество,
		|	КомплектацияАвтомобилей.Сумма,
		|	КомплектацияАвтомобилей.СуммаНДС,
		|	КомплектацияАвтомобилей.СуммаУпр,
		|	КомплектацияАвтомобилей.СуммаПродажи,
		|	КомплектацияАвтомобилей.СуммаПродажиУпр,
		|	КомплектацияАвтомобилей.ХозОперация,
		|	КомплектацияАвтомобилей.СтавкаНДС
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор
		|	И КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Сторно=Истина;
		НаборЗаписейРеализованныеТовары.ЕстьАвтомобиль = Истина;
		НаборЗаписейРеализованныеТовары.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоПартиям=Запрос.Выполнить().Выгрузить();
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоГТД=НаборЗаписейРеализованныеТовары.РезультатЗапросаПоПартиям.Скопировать();
		НаборЗаписейРеализованныеТовары.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
		// продажи
		Если НЕ Отказ Тогда
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			             |	КомплектацияАвтомобилей.ВидДвижения,
			             |	КомплектацияАвтомобилей.Автомобиль,
			             |	КомплектацияАвтомобилей.Номенклатура,
			             |	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
			             |	КомплектацияАвтомобилей.СтатусПартии,
			             |	КомплектацияАвтомобилей.Партия,
			             |	КомплектацияАвтомобилей.ГТД,
			             |	КомплектацияАвтомобилей.СтавкаНДС,
			             |	КомплектацияАвтомобилей.Количество,
			             |	КомплектацияАвтомобилей.Сумма,
			             |	КомплектацияАвтомобилей.СуммаУпр,
			             |	КомплектацияАвтомобилей.СуммаНДС
			             |ИЗ
			             |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			             |ГДЕ
			             |	КомплектацияАвтомобилей.Регистратор = &Регистратор
			             |	И КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура";
			Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
			НаборЗаписейПродажи = Движения.Продажи;
			НаборЗаписейПродажи.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейПродажи.РезультатЗапросаПоПартиям = Запрос.Выполнить().Выгрузить();
			НаборЗаписейПродажи.СкладКомпании             = ШапкаДокумента.СкладКомпании;
			НаборЗаписейПродажи.Сторно                    = Истина;
			НаборЗаписейПродажи.ПоБазовомуКоличеству      = Ложь;
			НаборЗаписейПродажи.Покупатель                = ШапкаДокумента.Контрагент;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов     = ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании     = ШапкаДокумента.ПодразделениеКомпании;
			НаборЗаписейПродажи.Комиссия                  = (ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия);
			НаборЗаписейПродажи.ШапкаДокумента            = ШапкаДокумента;
			НаборЗаписейПродажи.ЕстьАвтомобиль            = Истина;
			Запрос=Новый Запрос("
			|ВЫБРАТЬ
			|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
			|	КомплектацияАвтомобилей.Номенклатура КАК Номенклатура,
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	КомплектацияАвтомобилей.Партия КАК Партия,
			|	КомплектацияАвтомобилей.ГТД КАК ГТД,
			|	СУММА(КомплектацияАвтомобилей.Сумма) КАК Сумма,
			|	СУММА(КомплектацияАвтомобилей.СуммаУпр) КАК СуммаУпр,
			|	СУММА(КомплектацияАвтомобилей.СуммаНДС) КАК СуммаНДС
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Регистратор В (
			|		ВЫБРАТЬ РАЗЛИЧНЫЕ
			|			ВозвратОтПокупателяАвтомобилейАвтомобили.ДокументПродажи
			|		ИЗ
			|			Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ВозвратОтПокупателяАвтомобилейАвтомобили
			|		ГДЕ
			|			ВозвратОтПокупателяАвтомобилейАвтомобили.Ссылка = &Ссылка
			|	) И 
			|	КомплектацияАвтомобилей.Автомобиль                 В (&Автомобили) И 
			|	КомплектацияАвтомобилей.Партия                     В (&Партии) И 
			|	КомплектацияАвтомобилей.СтатусПартии               = &СтатусПартии И 
			|	КомплектацияАвтомобилей.Номенклатура               В (&Номенклатура) И 
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры В (&ХарактеристикиНоменклатуры) И 
			|	КомплектацияАвтомобилей.ВидДвижения = &ВидДвижения
			|СГРУППИРОВАТЬ ПО
			|	КомплектацияАвтомобилей.Автомобиль,
			|	КомплектацияАвтомобилей.Номенклатура,
			|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
			|	КомплектацияАвтомобилей.Партия,
			|	КомплектацияАвтомобилей.ГТД,
			|	КомплектацияАвтомобилей.Регистратор");
			Запрос.УстановитьПараметр("ВидДвижения",                ВидДвиженияНакопления.Расход);
			Запрос.УстановитьПараметр("СтатусПартии",               Перечисления.СтатусыПартий.ТоварКупленный);
			Запрос.УстановитьПараметр("Ссылка",                     ШапкаДокумента.Ссылка);
			Запрос.УстановитьПараметр("Автомобили",                 НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("Автомобиль"));
			Запрос.УстановитьПараметр("Партии",                     НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("Партия"));
			Запрос.УстановитьПараметр("Номенклатура",               НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("Номенклатура"));
			Запрос.УстановитьПараметр("ХарактеристикиНоменклатуры", НаборЗаписейПродажи.РезультатЗапросаПоПартиям.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
			НаборЗаписейПродажи.КэшСебестоимостиПриКомиссии = Запрос.Выполнить().Выгрузить();
			Отказ = НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли;
	Иначе
		// товар возвращает комиссионер. отсторнируем передачу на комиссию оборудования
		НаборЗаписейПартииОтданные = Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейПартииОтданные.Сторно                = Истина;
		НаборЗаписейПартииОтданные.Контрагент            = ШапкаДокумента.Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов = ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.ШапкаДокумента        = ШапкаДокумента;
		НаборЗаписейПартииОтданные.ПоБазовомуКоличеству  = Ложь;
		НаборЗаписейПартииОтданные.ЕстьАвтомобиль        = Истина;
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ВозвратОтПокупателяАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		             |	ВозвратОтПокупателяАвтомобилейТовары.Номенклатура КАК Номенклатура,
		             |	ВозвратОтПокупателяАвтомобилейТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		             |	ВозвратОтПокупателяАвтомобилейАвтомобили.ДокументПродажи КАК ДокументПередачи,
		             |	ВозвратОтПокупателяАвтомобилейТовары.СуммаНДС КАК СуммаНДС,
		             |	ВозвратОтПокупателяАвтомобилейТовары.Сумма КАК Сумма,
		             |	ВозвратОтПокупателяАвтомобилейТовары.СуммаВсего КАК СуммаВсего,
		             |	ВозвратОтПокупателяАвтомобилейТовары.Количество КАК Количество
		             |ИЗ
		             |	Документ.ВозвратОтПокупателяАвтомобилей.Товары КАК ВозвратОтПокупателяАвтомобилейТовары
		             |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		             |	Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ВозвратОтПокупателяАвтомобилейАвтомобили
		             |ПО
		             |	ВозвратОтПокупателяАвтомобилейТовары.Ссылка = &Ссылка И 
		             |	ВозвратОтПокупателяАвтомобилейТовары.ИдентификаторАвтомобиля = ВозвратОтПокупателяАвтомобилейАвтомобили.ИдентификаторАвтомобиля И 
		             |	ВозвратОтПокупателяАвтомобилейТовары.Ссылка = ВозвратОтПокупателяАвтомобилейАвтомобили.Ссылка
		             |ГДЕ
		             |	ВозвратОтПокупателяАвтомобилейТовары.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
		НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам=Запрос.Выполнить();
		Отказ=НЕ НаборЗаписейПартииОтданные.Приход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейПартииОтданные.Записать();
		КонецЕсли; 
		// товар возвращает комиссионер. отсторнируем передачу на комиссию опций, установленных производителем
		Запрос=Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВозвратОтПокупателяАвтомобилейАвтомобили.ДокументПродажи,
		|	ВозвратОтПокупателяАвтомобилейАвтомобили.Автомобиль
		|ПОМЕСТИТЬ
		|	ТаблицаАвтомобилей
		|ИЗ
		|	Документ.ВозвратОтПокупателяАвтомобилей.Автомобили КАК ВозвратОтПокупателяАвтомобилейАвтомобили
		|ГДЕ
		|	ВозвратОтПокупателяАвтомобилейАвтомобили.Ссылка = &Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	ПартииТоваровОтданные.Автомобиль КАК Автомобиль,
		|	ПартииТоваровОтданные.Контрагент КАК Контрагент,
		|	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданные.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ПартииТоваровОтданные.ДокументПередачи КАК ДокументПередачи,
		|	ПартииТоваровОтданные.Партия КАК Партия,
		|	ПартииТоваровОтданные.ГТД КАК ГТД,
		|	СУММА(ВЫБОР
		|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ПартииТоваровОтданные.Количество
		|		ИНАЧЕ
		|			-ПартииТоваровОтданные.Количество
		|	КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР
		|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ПартииТоваровОтданные.Сумма
		|		ИНАЧЕ
		|			-ПартииТоваровОтданные.Сумма
		|	КОНЕЦ) КАК Сумма,
		|	СУММА(ВЫБОР
		|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ПартииТоваровОтданные.СуммаУпр
		|		ИНАЧЕ
		|			-ПартииТоваровОтданные.СуммаУпр
		|	КОНЕЦ) КАК СуммаУпр,
		|	СУММА(ВЫБОР
		|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ПартииТоваровОтданные.СуммаНДС
		|		ИНАЧЕ
		|			-ПартииТоваровОтданные.СуммаНДС
		|	КОНЕЦ) КАК СуммаНДС,
		|	СУММА(ВЫБОР
		|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ПартииТоваровОтданные.СуммаСебестоимостиУпр
		|		ИНАЧЕ
		|			-ПартииТоваровОтданные.СуммаСебестоимостиУпр
		|	КОНЕЦ) КАК СуммаСебестоимостиУпр,
		|	СУММА(ВЫБОР
		|		КОГДА ПартииТоваровОтданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ПартииТоваровОтданные.СуммаСебестоимостиРегл
		|		ИНАЧЕ
		|			-ПартииТоваровОтданные.СуммаСебестоимостиРегл
		|	КОНЕЦ) КАК СуммаСебестоимостиРегл
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
		|ГДЕ
		|	ПартииТоваровОтданные.Период <= &НаДату И 
		|	ПартииТоваровОтданные.Номенклатура = &ОпцияПроизводителя И 
		|	(ПартииТоваровОтданные.Автомобиль, ПартииТоваровОтданные.Регистратор) В 
		|	(ВЫБРАТЬ
		|		ТаблицаАвтомобилей.Автомобиль,
		|		ТаблицаАвтомобилей.ДокументПродажи
		|	ИЗ
		|		ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровОтданные.Автомобиль,
		|	ПартииТоваровОтданные.Контрагент,
		|	ПартииТоваровОтданные.Номенклатура,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданные.ДоговорВзаиморасчетов,
		|	ПартииТоваровОтданные.ДокументПередачи,
		|	ПартииТоваровОтданные.Партия,
		|	ПартииТоваровОтданные.ГТД
		|";
		Запрос.УстановитьПараметр("НаДату", ШапкаДокумента.МоментВремени.Дата);
		Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("ОпцияПроизводителя",Справочники.Номенклатура.ОпцияПроизводителя);
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
			НоваяЗапись=НаборЗаписейПартииОтданные.Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗапись.Автомобиль=Выборка.Автомобиль;
			НоваяЗапись.Контрагент=Выборка.Контрагент;
			НоваяЗапись.Номенклатура=Выборка.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры=Выборка.ХарактеристикаНоменклатуры;
			НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.ДокументПередачи=Выборка.ДокументПередачи;
			НоваяЗапись.Партия=Выборка.Партия;
			НоваяЗапись.ГТД=Выборка.ГТД;
			НоваяЗапись.Количество=-Выборка.Количество;
			НоваяЗапись.Сумма=-Выборка.Сумма;
			НоваяЗапись.СуммаУпр=-Выборка.СуммаУпр;
			НоваяЗапись.СуммаНДС=-Выборка.СуммаНДС;
			НоваяЗапись.СуммаСебестоимостиУпр=-Выборка.СуммаСебестоимостиУпр;
			НоваяЗапись.СуммаСебестоимостиРегл=-Выборка.СуммаСебестоимостиРегл;
			НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
		КонецЦикла;
	КонецЕсли;
	
	//доходы и расходы
	//получим себестоимость
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателя Тогда
		СебестоимостьАвтомобилейУпр  = 0;
		СебестоимостьОборудованияУпр = 0;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		|ГДЕ
		|	ОстаткиАвтомобилей.Регистратор = &Регистратор И 
		|	(НЕ ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварПринятыйКомиссия))";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СебестоимостьАвтомобилейУпр = -Выборка.СуммаУпр;
		КонецЕсли; 
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор И 
		|	(НЕ КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварПринятыйКомиссия))";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СебестоимостьОборудованияУпр = - Выборка.СуммаУпр;
		КонецЕсли;
		//Итоговая Себестоимость автомобилей
		Если СебестоимостьАвтомобилейУпр <> 0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
			НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьАвтомобилейУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		
		Если СебестоимостьОборудованияУпр <> 0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.Расход                 = СебестоимостьОборудованияУпр;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		//Итог СуммаВсего
		Если ШапкаДокумента.СуммаАвтомобилей<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоАвтомобилям;
			НаборЗаписейДоходыИРасходы.Доход                  = ШапкаДокумента.СуммаАвтомобилей;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
		Если ШапкаДокумента.СуммаДопОборудования<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
			НаборЗаписейДоходыИРасходы.Доход                  = ШапкаДокумента.СуммаДопОборудования;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
		//возможно, что возврат производится на склад, на котором подразделение не совпадает с подразделением договора
		ПодразделениеСклад = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
		ПодразделениеДоговор = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговор<>ПодразделениеСклад);
		Если БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны Тогда
			//получим себестоимость
			ОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
			КомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
			СебестоимостьАвтомобилей = -(ОстаткиАвтомобилей.Итог("СуммаУпр")+КомплектацияАвтомобилей.Итог("СуммаУпр"));
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьАвтомобилей;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			АвтомобилиОтданные=Движения.АвтомобилиОтданные;
			ПартииТоваровОтданные=Движения.ПартииТоваровОтданные;
			СебестоимостьАвтомобилейОтданные = -(АвтомобилиОтданные.Итог("СуммаСебестоимостиУпр")+ПартииТоваровОтданные.Итог("СуммаСебестоимостиУпр"));
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьАвтомобилейОтданные;
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
			
		КонецЕсли;	
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

//Добавление оборудование автомобиля в возврат автомобиля от покупателя
Процедура ДобавитьОборудованиеАвтомобиля(Знач Автомобиль,ДокументПродажи,ИзменятьЦену)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("ВалютаДокумента",ВалютаДокумента);
	ДокументОбъектСтруктура.Вставить("КурсДокумента",КурсДокумента);
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.Выгрузить());
	ДокументОбъектСтруктура.Вставить("Товары",Товары.Выгрузить());
	зфЗащищенныеФункцииСервер.ВозвратОтПокупателяАвтомобилейДобавитьОборудованиеАвтомобиля(ДокументОбъектСтруктура,Автомобиль,ДокументПродажи,ИзменятьЦену);
	Товары.Очистить();
	Товары.Загрузить(ДокументОбъектСтруктура.Товары);
	Для Каждого стрТоваров Из Товары Цикл
		ОбработкаРеквизита("Товары.Цена",стрТоваров);
	КонецЦикла;
КонецПроцедуры

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Автомобили.Итог("СуммаВсего")+Товары.Итог("СуммаВсего");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
		КонецЕсли; 
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				СтрокаТЧ.СебестоимостьАвтомобиля = обПересчет(СтрокаТЧ.СебестоимостьАвтомобиля,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Результат=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Результат = ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиАвтомобили", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
		
	ИначеЕсли Имя="Организация" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		
		// Организация может является плательщиком НДС, а может и нет.. надо обработать ТЧ
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли; 
			Результат=ОбработкаРеквизита("Автомобили.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла;
	
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Подразделение может является плательщиком НДС, а может и нет.. надо обработать ТЧ
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли; 
			Результат=ОбработкаРеквизита("Автомобили.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла;
		Результат=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиАвтомобили", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомиссионером И
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбрать договор комиссии !",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Результат=Ложь;
			КонецЕсли; 
		ИначеЕсли ХозОперация<>Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Продажа И
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбрать договор с покупателем !",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Результат=Ложь;
			КонецЕсли; 
		КонецЕсли;
		// Если у документа есть реквизит СкидкаНаценка, заполним его из Договора 
		Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов.ТипСкидкиНаценки)) И 
			 (СкидкаНаценкаАвтомобили<>ДоговорВзаиморасчетов.ТипСкидкиНаценки) Тогда
			 СкидкаНаценкаАвтомобили=ДоговорВзаиморасчетов.ТипСкидкиНаценки;
			 Результат=ОбработкаРеквизита("СкидкаНаценкаАвтомобили",,ЭтаФорма,ДопПараметры);
		КонецЕсли;		
		
	ИначеЕсли Имя="Карточка" Тогда
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	ИначеЕсли Имя="СкидкаНаценкаАвтомобили" Тогда
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """+СкладКомпании+""" является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании=Неопределено;
			КонецЕсли; 
		КонецЕсли; 
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		//Проставим количество
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
		// Теперь получаем подставим значение скидки
		Попытка // документ может не поддерживать скидки
			Если (НЕ ЭтотОбъект.СкидкаНаценкаАвтомобили.Пустая()) И (ЭтотОбъект.СкидкаНаценкаАвтомобили.СпособВычисления = Перечисления.СкидкиСпособВычисления.Относительная) Тогда
				ТекСтрока.ПроцентСкидки=ЭтотОбъект.ЗначениеСкидкиНаценкиАвтомобили;
			Иначе
				ТекСтрока.ПроцентСкидки=0;
			КонецЕсли;
		Исключение КонецПопытки;
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
			Если ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли; 
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ТекСтрока.ДокументПродажи) Тогда
			ТекСтрока.ДокументПродажи=ДокументОснование;
		КонецЕсли; 
		Результат=ОбработкаРеквизита("Автомобили.ДокументПродажи",ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли; 
		#КонецЕсли
		
	ИначеЕсли Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		Результат=ОбработкаРеквизита("РассчитатьСкидкиАвтомобили",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.ПроцентСкидки" Тогда
		// Скидка не может быть выше 100%
		Если ТекСтрока.ПроцентСкидки>100 Тогда
			ТекСтрока.ПроцентСкидки = 100;	
		КонецЕсли;
		ТекСтрока.СуммаСкидки = Окр(ТекСтрока.Сумма * ТекСтрока.ПроцентСкидки / 100, 2);
		ТекСтрока.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценкаАвтомобили);
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитыватьПроцент", Ложь);
		РезультатОбработки = ЭтотОбъект.ОбработкаРеквизита("Автомобили.СуммаСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);  			
		
	ИначеЕсли Имя="Автомобили.СуммаСкидки" Тогда
		// Выполним расчет итоговой суммы скидки на документ.
		Попытка	// Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцент = ДопПараметры.ПересчитыватьПроцент;
		Исключение 
			ПересчитыватьПроцент = Истина;	
		КонецПопытки;
		Если ПересчитыватьПроцент Тогда
			ТекСтрока.ПроцентСкидки = ?(ТекСтрока.Сумма=0, 0, Окр(ТекСтрока.СуммаСкидки * 100 / ТекСтрока.Сумма, 2));
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиАвтомобили = Автомобили.Итог("СуммаСкидки");
		РезультатОбработки = ОбработкаРеквизита("Автомобили.СтавкаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		СуммаВсегоБезСкидки  = ТекСтрока.Сумма - ТекСтрока.СуммаСкидки - ТекСтрока.СебестоимостьАвтомобиля;	// База для расчета НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Расчет НДС
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаНДС = Окр((СуммаВсегоБезСкидки * ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС =  Окр(СуммаВсегоБезСкидки * ТекСтрока.СтавкаНДС.Ставка / 100,2);
		КонецЕсли; 			
		// В любом случаее идем в секцию "СуммаНДС", т.к. в ней будем произведен расчет суммы всего.
		РезультатОбработки = ЭтотОбъект.ОбработкаРеквизита("Автомобили.СуммаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаНДС" Тогда
		// Вычисляем сумму с учетом скидок.
		СуммаВсегоБезСкидки = ТекСтрока.Сумма - ТекСтрока.СуммаСкидки;
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда			
			ТекСтрока.СуммаВсего = СуммаВсегоБезСкидки;			
		Иначе
			ТекСтрока.СуммаВсего = СуммаВсегоБезСкидки + ТекСтрока.СуммаНДС;			
		КонецЕсли;
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма = Окр(ТекСтрока.СуммаВсего / (1-ТекСтрока.ПроцентСкидки/100),2);
		Иначе
			Сумма = ТекСтрока.СуммаВсего * 100 / (100 + ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.Сумма = Окр(Сумма / (1 - ТекСтрока.ПроцентСкидки/100),2);
		КонецЕсли;
		// Пересчитываем цену.
		ТекСтрока.Цена = ?(ТекСтрока.Количество=0,ТекСтрока.Сумма,Окр(ТекСтрока.Сумма/ТекСтрока.Количество,2));
		// Вычисляем новую сумму "шапочной" скидки приходящейся на текущую строку.
		ТекСтрока.СуммаСкидки = Окр(ТекСтрока.Сумма * ТекСтрока.ПроцентСкидки / 100, 2);
		ТекСтрока.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценкаАвтомобили);
		// Рассчитываем новую сумму НДС. 		
		СуммаВсегоБезСкидки = ТекСтрока.Сумма - ТекСтрока.СуммаСкидки - ТекСтрока.СебестоимостьАвтомобиля;
		Если ЦенаВключаетНДС Тогда			
			ТекСтрока.СуммаНДС = Окр((СуммаВсегоБезСкидки * ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС = Окр(СуммаВсегоБезСкидки * ТекСтрока.СтавкаНДС.Ставка / 100,2);
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиАвтомобили = Автомобили.Итог("СуммаСкидки");
		
	ИначеЕсли Имя="Автомобили.ДокументПродажи" Тогда
		ДобавитьОборудованиеАвтомобиля(ТекСтрока.ИдентификаторАвтомобиля,ТекСтрока.ДокументПродажи,Истина);
		ТекСтрока.СебестоимостьАвтомобиля = 0;
		// Поищем автомобиль в документе продажи по строке
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДокументПродажи) Тогда
			СтрокаДокументаОснования=ТекСтрока.ДокументПродажи.Автомобили.Найти(ТекСтрока.Автомобиль,"Автомобиль");
			Если СтрокаДокументаОснования<>Неопределено Тогда
				ТекСтрока.Цена=обПересчет(СтрокаДокументаОснования.Цена,ТекСтрока.ДокументПродажи.ВалютаДокумента,ТекСтрока.ДокументПродажи.КурсДокумента,ВалютаДокумента,КурсДокумента);
				Если ТипЗнч(ТекСтрока.ДокументПродажи) = Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
					Если СтрокаДокументаОснования.СебестоимостьАвтомобиля = 0 Тогда
						ТекСтрока.СебестоимостьАвтомобиля=ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(СтрокаДокументаОснования, ТекСтрока.ДокументПродажи);
					Иначе
						ТекСтрока.СебестоимостьАвтомобиля=обПересчет(СтрокаДокументаОснования.СебестоимостьАвтомобиля,ТекСтрока.ДокументПродажи.ВалютаДокумента,ТекСтрока.ДокументПродажи.КурсДокумента,ВалютаДокумента,КурсДокумента);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		//Поищем автомобиль в документе продажи из шапки
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			СтрокаДокументаОснования=ДокументОснование.Автомобили.Найти(ТекСтрока.Автомобиль,"Автомобиль");
			Если СтрокаДокументаОснования<>Неопределено Тогда
				ТекСтрока.ДокументПродажи=ДокументОснование;
				ТекСтрока.Цена=обПересчет(СтрокаДокументаОснования.Цена,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
				Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
					Если СтрокаДокументаОснования.СебестоимостьАвтомобиля = 0 Тогда
						ПолучитьСебестоимостьАвтомобиляКупленногоУФизЛица(СтрокаДокументаОснования, ДокументОснование);
					Иначе
						ТекСтрока.СебестоимостьАвтомобиля=обПересчет(СтрокаДокументаОснования.СебестоимостьАвтомобиля,ТекСтрока.ДокументПродажи.ВалютаДокумента,ТекСтрока.ДокументПродажи.КурсДокумента,ВалютаДокумента,КурсДокумента);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		Результат=ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="РассчитатьСкидкиАвтомобили" Тогда	
		Попытка	
			НеРассчитыватьСкидки = ДопПараметры.НеРассчитыватьСкидки;
		Исключение 
			НеРассчитыватьСкидки = Ложь;	
		КонецПопытки;
		
		Попытка
			БлокироватьПерерасчетСкидок = ЭтотОбъект.БлокироватьПерерасчетСкидок;
		Исключение
			БлокироватьПерерасчетСкидок = Ложь;
		КонецПопытки;
		
		ОбработкаРасчетСкидокАвтомобили.СкидкаНаценка=СкидкаНаценкаАвтомобили;
		// Подбираем подходящую скидку. Если скидка не изменилась, то функция вернет Ложь.
		
		Если БлокироватьПерерасчетСкидок Тогда
			Попытка
				Если обЗначениеНеЗаполнено(ЭтотОбъект.СкидкаНаценкаАвтомобили) Тогда
					Для Каждого стрДок Из ЭтотОбъект.Автомобили Цикл
						стрДок.ПроцентСкидки = 0;
						ЭтотОбъект.ОбработкаРеквизита("Автомобили.ПроцентСкидки", стрДок, ЭтаФорма);
					КонецЦикла;
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			Если ТекСтрока = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ЭтотОбъект.ОбработкаРеквизита("Автомобили.ПроцентСкидки", ТекСтрока, ЭтаФорма);
			Возврат Истина;
		ИначеЕсли (НЕ НеРассчитыватьСкидки) И ОбработкаРасчетСкидокАвтомобили.ПодобратьСкидку(ЭтотОбъект) Тогда
			ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСкидкаНаценка] = ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка;
			ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаЗначениеСкидкиНаценки] = ОбработкаРасчетСкидокАвтомобили.ТекущееЗначениеСкидки;
			ЭтотОбъект[ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСуммаСкидкиНаценки] = ОбработкаРасчетСкидокАвтомобили.ТекущаяСуммаСкидки;
			// Подобралась другая "шапочная" скидка, значит, возможно, придется заблокировать работу с некоторыми колонками 
			// в зависимости от типа скидки.
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					дкУправлениеДоступностьюКолонокСкидок(ЭтаФорма, ОбработкаРасчетСкидокАвтомобили.ТекущаяСкидкаНаценка, ОбработкаРасчетСкидокАвтомобили.ИмяТабличнойЧасти);	
				КонецЕсли;
			#КонецЕсли
			// Применяем скидку к табличной части. Пересчитываем строчные скидки.
			ОбработкаРасчетСкидокАвтомобили.ПрименитьСкидку(ЭтотОбъект);
			#Если Клиент Тогда					
				Если ЭтаФорма<>Неопределено Тогда
					дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);		
				КонецЕсли;  
			#КонецЕсли 
		Иначе // Просто пересчитываем значение суммы скидки для текущей строки.
			Если ТекСтрока<>Неопределено Тогда									
				// Рассчитываем "шапочную" скидку для текущей строки. За одно рассчитаем общую сумму скидки на документ.
				Если СкидкаНаценкаАвтомобили.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ОбработкаРеквизита("Автомобили.ПроцентСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);				
				Иначе
					ОбработкаРеквизита("Автомобили.СуммаСкидки", ТекСтрока, ЭтаФорма, ДопПараметры);				
				КонецЕсли;
			Иначе
				// Если произошла смена типа скидки, но значение скидки не изменилась, то пересчитывать ТЧ нет необходимости, но
				// сохранить новый тип скидки нужно.
				СкидкаНаценкаАвтомобили = ОбработкаРасчетСкидок.ТекущаяСкидкаНаценка;
				СуммаСкидкиНаценкиАвтомобили = Автомобили.Итог("СуммаСкидки");					
			КонецЕсли;			
		КонецЕсли;
		
	Иначе 
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ВозвратОтПокупателяАвтомобилей - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажиАвтомобилей.Автомобиль КАК Номенклатура,
	|	ПродажиАвтомобилей.ГТД КАК РНПТ,
	|	СУММА(-ПродажиАвтомобилей.Количество) КАК КоличествоПрослеживаемости,
	|	СУММА(-(ПродажиАвтомобилей.Сумма - ПродажиАвтомобилей.СуммаНДС)) КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.ПродажиАвтомобилей КАК ПродажиАвтомобилей
	|ГДЕ
	|	ПродажиАвтомобилей.Регистратор = &Ссылка
	|	И ПродажиАвтомобилей.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиАвтомобилей.Автомобиль,
	|	ПродажиАвтомобилей.ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратОтПокупателяАвтомобилей.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ВозвратОтПокупателяАвтомобилей.Дата, КВАРТАЛ) КАК ПериодОтчета,
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУменьшение) КАК КодОперации,
	|	ВозвратОтПокупателяАвтомобилей.Ссылка КАК Документ,
	|	ВозвратОтПокупателяАвтомобилей.Дата КАК ДатаДокумента,
	|	ВозвратОтПокупателяАвтомобилей.Номер КАК НомерДокумента,
	|	ВозвратОтПокупателяАвтомобилей.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ВозвратОтПокупателяАвтомобилей.Контрагент.СтранаРегистрации КАК СтранаРегистрации
	|ИЗ
	|	Документ.ВозвратОтПокупателяАвтомобилей КАК ВозвратОтПокупателяАвтомобилей
	|ГДЕ
	|	ВозвратОтПокупателяАвтомобилей.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	// Проверим есть РНПТ у документа
	Если ПакетЗапросов[0].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ШапкаДокумента = ПакетЗапросов[1].Выбрать();
	ШапкаДокумента.Следующий();
	
	// Указываем для отчета только на основании Закрытия смены
	// и для контрагентов не из ЕАЭС
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(ШапкаДокумента.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	РНПТДокумента = ПакетЗапросов[0].Выгрузить();
	НомерДокумента = дкПолучитьНомерДляПечати(ШапкаДокумента.Документ);
	
	Для Каждого ТекущаяСтрока Из РНПТДокумента Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ШапкаДокумента);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.НомерДокумента = НомерДокумента;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Возврат от покупателя автомобилей"
// Возвращает сформированный табличный документ:
Функция ПечатьВозвратОтПокупателяАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВозвратОтПокупателяАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеОтправителя	= спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПолучателя = спПолучитьПредставление(
		ЭтотОбъект.Организация, , ДополнительныеПараметры);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();	
	//подготовим дополнительную таблицу
	ТаблицаТоваровПоАвтомобилям = ЭтотОбъект.Товары.Выгрузить();
	ТаблицаТоваровПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаСкидки,СуммаВсего");
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		СтрокаТабличнойЧастиТовары = ТаблицаТоваровПоАвтомобилям.Найти(СтрокаТабличнойЧасти.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиТовары<>Неопределено Тогда
			СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧастиТовары.Сумма;
			СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаНДС + СтрокаТабличнойЧастиТовары.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаСкидки = СтрокаТабличнойЧасти.СуммаСкидки + СтрокаТабличнойЧастиТовары.СуммаСкидки;
			СтрокаТабличнойЧасти.СуммаВсего = СтрокаТабличнойЧасти.СуммаВсего + СтрокаТабличнойЧастиТовары.СуммаВсего;
		КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
	Если СкидкаВсего > 0 Тогда
		ОбластьМакета.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если обЗначениеНеЗаполнено(ХозОперация) Тогда
		ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателя;
	КонецЕсли;

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияАвтомобилей") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.РеализацияАвтомобилейКомиссия Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВводОстатковАвтомобилей") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковАвтомобилейПереданныхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ИнвентаризацияАвтомобилей") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия;
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Дата) Тогда
		#Если Клиент Тогда
		НаДату=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
		#Иначе
	    НаДату=ТекущаяДата();
		#КонецЕсли
		Дата = НаДату;
	КонецЕсли; 
	
	// Если вид договора не соответствует хоз. операции, то его следует
	// подставлять автоматически
	Если (НЕ обЗначениеНеЗаполнено(Основание)) 
		И Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Основание)) 
		И Основание.Метаданные().Реквизиты.Найти("Контрагент")<>Неопределено Тогда
			
		// Попробуем запонить контрагента и его договор
		Контрагент	= Основание.Контрагент;		
		ДоговорВзаиморасчетов	= Основание.ДоговорВзаиморасчетов;
		ВалютаДокумента			= Основание.ВалютаДокумента;
		ТипЦен					= Основание.ТипЦен;
		
		// Проверим соответствие договора хозю операции
		Если НЕ обЗначениеНеЗаполнено(Контрагент) И
			ТипЗнч(ДоговорВзаиморасчетов)=Тип("СправочникСсылка.ДоговорыВзаиморасчетов") И
			(НЕ ДоговорВзаиморасчетов.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация)) Тогда
			
			// Произвдем поиск договора с правильным видом
			Если ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
				ТекущийВидДоговора = Перечисления.ВидыДоговоров.СКомиссионером;
			Иначе
				ТекущийВидДоговора = Перечисления.ВидыДоговоров.Продажа;
			КонецЕсли;
			
			//Параметры для подстановки или создания договора контрагента
			ДопПараметры = Новый Структура();
			ДопПараметры.Вставить("ВалютаВзаиморасчетов", ВалютаДокумента);
			
			ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, ТекущийВидДоговора, ЭтотОбъект, ДопПараметры, Истина, Истина);
			
			// Попробуем найти договор с видом "Прочее"
			Если обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, Перечисления.ВидыДоговоров.Прочее, ЭтотОбъект, ДопПараметры, Истина, Истина);
			КонецЕсли;
			
			// Данные реквизиты необходимо заполнять стандартно из общего модуля документов
			ВалютаДокумента = Неопределено;
			ТипЦен          = Неопределено;
			Дата            = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Товары.Очистить();
	// определим ХО.
	Если Основание=Неопределено Тогда
		ТипЦен=обПраво("ОсновнойТипЦенПродажиАвтомобилей",Права,,ЭтотОбъект);
	Иначе
		ТипЦен=Основание.ТипЦен;
		// проставим партию
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			Если СтрокаТЧ.Количество=0 Тогда
				СтрокаТЧ.Количество=1;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(СтрокаТЧ.ИдентификаторАвтомобиля) Тогда
				СтрокаТЧ.ИдентификаторАвтомобиля=Новый УникальныйИдентификатор;
			КонецЕсли;
			СтрокаТЧ.ДокументПродажи=Основание;
			ОбработкаРеквизита("Автомобили.ДокументПродажи",СтрокаТЧ);
			Если обЗначениеНеЗаполнено(СтрокаТЧ.СтавкаНДС) Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				ОбработкаРеквизита("Автомобили.СтавкаНДС",СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	Если обЗначениеНеЗаполнено(СкладКомпании) Тогда
		СкладКомпании=обПраво("ОсновнойСкладКомпании",Права);
	КонецЕсли;
	
	// изменим итоговую сумму документа
	СуммаДокумента=РассчитатьСуммуВсего();
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	////Обновим оборудование автомобилей
	//Для каждого СтрокаАвтомобилей Из Автомобили Цикл
	//	ДобавитьОборудованиеАвтомобиля(СтрокаАвтомобилей.Автомобиль,СтрокаАвтомобилей.ДокументПродажи,Ложь);
	//КонецЦикла; 
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
		Для Каждого СтрокаАвтомобилей Из Автомобили Цикл
			ОборудованиеАвтомобиля=Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",СтрокаАвтомобилей.ИдентификаторАвтомобиля));
			Для Каждого СтрокаТоваров Из ОборудованиеАвтомобиля Цикл
				СтрокаТоваров.ДокументПродажи=СтрокаАвтомобилей.ДокументПродажи;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли; 	
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	////Удалим накопление сумм
	//НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	//НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	//НаборЗаписейНакоплениеСумм.ОтменаПроведения();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	//проверим, что автомобиль отсутствует на остатках по различным регистрам
	Если НЕ РегистрыНакопления.ОстаткиАвтомобилей.ПроверитьОстаткиАвтомобилей(ЭтотОбъект,Отказ) Тогда 
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);		
		Возврат; 
	КонецЕсли;
	
	// проведем взаиморасчеты
	Если ХозОперация<>Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			НаборЗаписейВзаиморасчеты.Сделка = ДокументОснование;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		Иначе
			НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
		КонецЕсли;
		НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// вернем автомобили
	НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
	НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
	НаборЗаписейОстаткиАвтомобилей.Партия="ДокументПродажи";
	//НаборЗаписейОстаткиАвтомобилей.Сторно=Истина;
	Отказ=НЕ НаборЗаписейОстаткиАвтомобилей.ВозвратАвтомобилей(ВидДвиженияНакопления.Расход) ИЛИ Отказ;
	
	Если (Не Отказ) И ХозОперация = Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия Тогда
		
		// автомобили возвращает комиссионер. отсторнируем передачу на комиссию
		НаборЗаписейАвтомобилиОтданные=Движения.АвтомобилиОтданные;
		НаборЗаписейАвтомобилиОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейАвтомобилиОтданные.Сторно=Истина;
		НаборЗаписейАвтомобилиОтданные.Контрагент=Контрагент;
		НаборЗаписейАвтомобилиОтданные.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Отказ=НЕ НаборЗаписейАвтомобилиОтданные.Приход() ИЛИ отказ;
		
	КонецЕсли;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	Если (Не Отказ) И (НЕ ХозОперация = Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия) Тогда
		
		ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
		// определим необходимость формирования корректирующих проводок
		БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
		
		// если среди возвращаемых автомобилей есть комиссионные, то отсторнируем реализованные автомобили
		НаборЗаписейРеализованныеАвтомобили=Движения.РеализованныеАвтомобили;
		НаборЗаписейРеализованныеАвтомобили.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеАвтомобили.Сторно=Истина;
		Отказ=НЕ НаборЗаписейРеализованныеАвтомобили.Приход() ИЛИ Отказ;
		
		// продажи
		Если НЕ Отказ Тогда
			НаборЗаписейПродажи=Движения.ПродажиАвтомобилей;
			НаборЗаписейПродажи.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейПродажи.СкладКомпании=СкладКомпании;
			НаборЗаписейПродажи.Сторно=Истина;
			НаборЗаписейПродажи.Покупатель=Контрагент;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании=ПодразделениеКомпании;
			НаборЗаписейПродажи.Комиссия=(ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия);
			Отказ=НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		//доходы и расходы
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.Записать();
		НаборЗаписейРеализованныеАвтомобили.Записать();
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ОбъединенныйЗапрос.Подразделение КАК Подразделение,
		|	-СУММА(ОбъединенныйЗапрос.СуммаАвтомобилейУпр) КАК СуммаАвтомобилейУпр,
		|	-СУММА(ОбъединенныйЗапрос.СуммаОборудованияУпр) КАК СуммаОборудованияУпр
		|ИЗ(
		|ВЫБРАТЬ
		|	РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(РеализованныеАвтомобили.СуммаУпр, 0) КАК СуммаАвтомобилейУпр,
		|	0 КАК СуммаОборудованияУпр
		|ИЗ
		|	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
		|ГДЕ
		|	РеализованныеАвтомобили.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение,
		|	0,
		|	ЕСТЬNULL(РеализованныеТовары.СуммаУпр, 0)
		|ИЗ
		|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
		|ГДЕ
		|	РеализованныеТовары.Регистратор = &Регистратор) КАК ОбъединенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ОбъединенныйЗапрос.Подразделение";
		Запрос.УстановитьПараметр("Регистратор", ШапкаДокумента.Ссылка);
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если БалансВедетсяПоПодразделениям Тогда
			Выборка=Запрос.Выполнить().Выбрать();
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			Пока Выборка.Следующий() Цикл
				СуммаАвтомобилейУпр  = Выборка.СуммаАвтомобилейУпр;
				СуммаОборудованияУпр = Выборка.СуммаОборудованияУпр;
				Если СуммаАвтомобилейУпр <> 0 Тогда
					НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.Подразделение          = Выборка.Подразделение;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыИРасходы.Расход                 = СуммаАвтомобилейУпр;
					НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
				КонецЕсли;
				Если СуммаОборудованияУпр <> 0 Тогда
					НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
					НаборЗаписейДоходыИРасходы.Подразделение          = Выборка.Подразделение;
					НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
					НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыИРасходы.Расход                 = СуммаОборудованияУпр;
					НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
				КонецЕсли;
			КонецЦикла; 
		Иначе
			ТаблицаСебестоимости = Запрос.Выполнить().Выгрузить();
			СуммаАвтомобилейУпр = ТаблицаСебестоимости.Итог("СуммаАвтомобилейУпр");
			СуммаОборудованияУпр = ТаблицаСебестоимости.Итог("СуммаОборудованияУпр");
			Если СуммаАвтомобилейУпр <> 0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
				НаборЗаписейДоходыИРасходы.Расход                 = СуммаАвтомобилейУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			КонецЕсли;
			Если СуммаОборудованияУпр <> 0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
				НаборЗаписейДоходыИРасходы.Расход                 = СуммаОборудованияУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверим наличие прослеживаемых товаров, которые были возвращены из розницы
	Если НЕ Отказ Тогда
		Движения.ПродажиАвтомобилей.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	////Уменьшим сумму накопления
	//НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	//НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	//НаборЗаписейНакоплениеСумм.Контрагент=Контрагент;
	//НаборЗаписейНакоплениеСумм.КоличествоНоменклатуры=Автомобили.Итог("Количество");
	//НаборЗаписейНакоплениеСумм.Карточка=Неопределено;
	//НаборЗаписейНакоплениеСумм.Сторно=Истина;
	//Отказ=НЕ НаборЗаписейНакоплениеСумм.НакоплениеСуммы() ИЛИ Отказ;
	
	// двигаем границу последовательности автомобилей
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОбработкаРасчетСкидок=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки=Истина;

КешСебестоимостиАвтомобилейКупленныхУФизЛица = Неопределено;

ОбработкаРасчетСкидокАвтомобили=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСкидкаНаценка="СкидкаНаценкаАвтомобили";
ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаЗначениеСкидкиНаценки="ЗначениеСкидкиНаценкиАвтомобили";
ОбработкаРасчетСкидокАвтомобили.ИмяРеквизитаСуммаСкидкиНаценки="СуммаСкидкиНаценкиАвтомобили";
ОбработкаРасчетСкидокАвтомобили.НеРассчитыватьСтрочныеСкидки=Истина;
ОбработкаРасчетСкидокАвтомобили.ИмяТабличнойЧасти="Автомобили";
ОбработкаРасчетСкидокАвтомобили.НеРассчитыватьАвтоматическиеСкидки=Истина;

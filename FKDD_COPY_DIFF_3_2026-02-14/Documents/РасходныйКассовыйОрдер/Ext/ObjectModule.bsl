// Модуль документа РасходныйКассовыйОрдер

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем СообщениеОбОшибке Экспорт;	// Переменная для накопления результатов записи. Если = неопределено, то вывод будет на экран, иначе тут сообщения об ошибках.
Перем ОбработкаРасчетСкидок Экспорт;              // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
Перем ЗначениеСкидкиНаценки Экспорт;
Перем СуммаСкидкиНаценки Экспорт;


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("КассаКомпании",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СуммаДокумента",1);
	ОбязательныеРеквизиты.Вставить("СтатьяДДС",1);

	Если ДляПробитияНаФР Тогда
		ОбязательныеРеквизиты.Вставить("ТипСпособаРасчетаККТ",1);
		ОбязательныеРеквизиты.Вставить("ТипРасчета", 1);
	КонецЕсли;

	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД", 2);
	ОбязательныеТовары.Вставить("ДоговорВзаиморасчетов", 2);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаКомпании", Ложь);
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Если Товары.Количество() > 0 И ДляПробитияНаФР Тогда
		
		Если СуммаДокумента > 0 И Товары.Итог("СуммаОплаты") = 0 Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, "Необходимо произвести пропорциональное распределение суммы вносимой оплаты между предметами платежа");
		ИначеЕсли СуммаДокумента <> Товары.Итог("СуммаОплаты") Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, "Неверно заполнена колонка ""Сумма оплаты"" в табличной части товары, необходимо распределить сумму оплаты");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	Результат = Истина;
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РасходныйКассовыйОрдер","Расходный кассовый ордер",Истина);
	СписокХозОпераций.Добавить("ВыдачаДенежныхСредствПодотчетнику","Выдача денежных средств подотчетнику",Ложь);
	СписокХозОпераций.Добавить("ВыплатаЗарплатыРаботнику","Выплата заработной платы работнику",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ОБЪЕКТА
	Если Имя="ВалютаДокумента" ИЛИ Имя="КурсДокумента" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОтображениеВалютыДокумента();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		//Параметры для подстановки или создания договора контрагента
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		Если ХозОперация=Справочники.ХозОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
			ВидДоговора=Перечисления.ВидыДоговоров.Подотчет;	
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ВыплатаЗарплатыРаботнику Тогда	
			ВидДоговора=Перечисления.ВидыДоговоров.Зарплата;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер Тогда	
			ВидДоговора=Перечисления.ВидыДоговоров.Покупка;
		Иначе
			ВидДоговора	= Перечисления.ВидыДоговоров.Прочее;
		КонецЕсли;
		ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
		ДопПараметры.Вставить("ВалютаВзаиморасчетов",ВалютаДокумента);
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			Выдать=СокрЛП(Контрагент.НаименованиеПолное);
			ПодтверждающийДокумент=спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Паспорт);
			Если НЕ ПодтверждающийДокумент.Пустая() Тогда
				ПоДокументу=ПодтверждающийДокумент.Наименование+?(ПустаяСтрока(ПодтверждающийДокумент.КемВыдан),""," выданный "+ПодтверждающийДокумент.КемВыдан);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаДокумента" ИЛИ Имя="СтавкаНДС" Тогда
		СуммаНДС = Окр((СуммаДокумента * СтавкаНДС.Ставка)/(100 + СтавкаНДС.Ставка),2);
		Возврат Истина;
		
	ИначеЕсли Имя="КассаККМ" Тогда
		Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
			Возврат Истина;
		КонецЕсли;
		#Если Клиент Тогда
		Рез=Истина;
		Если ЭтаФорма<>Неопределено Тогда
			Если КассаККМ.Организация<>Организация Тогда
				Если Вопрос("Организация кассы отлична от текущей организации документа. Установить организацию документа в соответствие с организацией кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					Организация=КассаККМ.Организация;
					Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.Подразделение<>ПодразделениеКомпании Тогда
				Если Вопрос("Подразделение кассы отлично от текущего подразделения документа. Установить подразделение документа в соответствии с подразделением кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ПодразделениеКомпании=КассаККМ.Подразделение;
					Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
				КонецЕсли; 
			КонецЕсли; 
			Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента Тогда
				Если Вопрос("Валюта кассы отлична от текущей валюты документа. Установить валюту документа в соответствие с валютой кассы?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
					ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
					Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли;
					Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЕсли; 
		Иначе
			Организация=КассаККМ.Организация;
			Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
			ПодразделениеКомпании=КассаККМ.Подразделение;
			Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
			ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
			Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		КонецЕсли; 
		#Иначе
		Организация=КассаККМ.Организация;
		Рез=ОбработкаРеквизита("Организация",,ЭтаФорма) И Рез;
		ПодразделениеКомпании=КассаККМ.Подразделение;
		Рез=ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма) И Рез;
		ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
		Рез=ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма) И Рез;
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ФР" Тогда
		Если НЕ обЗначениеНеЗаполнено(ФР) Тогда
			Если ФР.КлассОборудования<>Перечисления.КлассыОборудования.ФР Тогда
				#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Предупреждение("Выбранное оборудование: " + ФР + " не является фискальным регистратором!
					|Выберите фискальный регистратор",10);
				КонецЕсли; 
				#КонецЕсли
				ФР=Неопределено;
				Возврат ОбработкаРеквизита("ФР",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат Истина;
	ИначеЕсли Имя = "ДляПробитияНаФР" Тогда
		Рез = Истина;
		
		Если ДляПробитияНаФР И НЕ ЗначениеЗаполнено(ТипРасчета) Тогда
			ТипРасчета = дкТипРасчетаДокумента(ЭтотОбъект);
		КонецЕсли;
		
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "РасходныйКассовыйОрдер"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРасходныйКассовыйОрдер(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РасходныйКассовыйОрдер");
    ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//Получаем область шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
    ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.СуммаДокумента = Формат(ЭтотОбъект.СуммаДокумента,ФорматВыводаСуммы);
	
	ОбластьМакета.Параметры.ОрганизацияПоОКПО	= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.КодПодразделения	= ЭтотОбъект.ПодразделениеКомпании.Код;
	ОбластьМакета.Параметры.РеквизитыДокументаУдостоверяющегоЛичность = ЭтотОбъект.ПоДокументу;
	
	Если обЗначениеНеЗаполнено(Основание) Тогда
		ОбластьМакета.Параметры.Основание = ЭтотОбъект.ХозОперация;
	КонецЕсли;
	
	ОбластьМакета.Параметры.КредитСубСчет		= ?(ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),"50.1","50.2");
	ОбластьМакета.Параметры.ДебетСубСчет		= ЭтотОбъект.СтатьяДДС.КоррСчет.Код;
	
	ОбластьМакета.Параметры.СуммаПрописью		= обЧислоПрописью(ЭтотОбъект.СуммаДокумента,ЭтотОбъект.ВалютаДокумента);
	Если (ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер) Тогда
		ОбластьМакета.Параметры.ВТомЧисле = "НДС (" + дкПолучитьПредставлениеСтавкиНДС(СтавкаНДС) + ") " 
											  + Формат(СуммаНДС, "ЧЦ=15;ЧДЦ=2;ЧРД=-;ЧН=0-00")+" "+ЭтотОбъект.ВалютаДокумента;
	КонецЕсли;	
    	
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	Кассир = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Кассир);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(Кассир);  
	//БизТех 24.10.2023г. по каждому подразделению свой кассир, поэтому кассир - это автор документа.  <<
	Кассир = ЭтотОбъект.Автор.Наименование;
	ОбластьМакета.Параметры.КассирПредставление = Кассир; 
	//>>

	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.Подразделение = ПодразделениеКомпании;
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции // ПечатьРасходныйКассовыйОрдер()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,, Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	//ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер;
	Если Основание=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОснование    = Основание;
	ЭтотОбъект.Основание = Основание;
	Сделка               = Основание;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СУММА(ВложеннаяТаблица.СуммаЗаказа) КАК СуммаЗаказа,
		|	СУММА(ВложеннаяТаблица.СуммаЗаказаУпр) КАК СуммаЗаказаУпр
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикамОстатки.СуммаОстаток КАК СуммаЗаказа,
		|		ЗаказыПоставщикамОстатки.СуммаУпрОстаток КАК СуммаЗаказаУпр
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам.Остатки(&МоментВремени, ЗаказПоставщику = &Заказ) КАК ЗаказыПоставщикамОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		-ВзаиморасчетыКомпанииОстатки.СуммаОстаток,
		|		-ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток
		|	ИЗ
		|		РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&МоментВремени, Сделка ССЫЛКА Документ.ЗаказПоставщику И Сделка = &Заказ) КАК ВзаиморасчетыКомпанииОстатки) КАК ВложеннаяТаблица
		|");
		Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());
		Запрос.УстановитьПараметр("Заказ",         Сделка);
		
		СуммаСделки    = 0;
		СуммаСделкиУпр = 0;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			СуммаСделки    = Выборка.СуммаЗаказа;
			СуммаСделкиУпр = Выборка.СуммаЗаказаУпр;
			
		КонецЕсли;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
			СуммаДокумента = обПересчет(СуммаСделки, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ДокументОснование.КурсДокумента, ВалютаДокумента, КурсДокумента);
		Иначе
			СуммаДокумента = обПересчет(СуммаСделкиУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		
		ОбработкаРеквизита("СуммаДокумента");
		//Статья ДДС - Оплата поставщику
		СтатьяДДС = Справочники.СтатьиДДС.ОплатаПоставщику;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СУММА(ВложеннаяТаблица.СуммаЗаказа) КАК СуммаЗаказа,
		|	СУММА(ВложеннаяТаблица.СуммаЗаказаУпр) КАК СуммаЗаказаУпр
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикамОстатки.СуммаОстаток КАК СуммаЗаказа,
		|		ЗаказыПоставщикамОстатки.СуммаУпрОстаток КАК СуммаЗаказаУпр
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки(&МоментВремени, ЗаказПоставщику = &Заказ) КАК ЗаказыПоставщикамОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		-ВзаиморасчетыКомпанииОстатки.СуммаОстаток,
		|		-ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток
		|	ИЗ
		|		РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&МоментВремени, Сделка ССЫЛКА Документ.ЗаказПоставщикуНаАвтомобиль И Сделка = &Заказ) КАК ВзаиморасчетыКомпанииОстатки) КАК ВложеннаяТаблица
		|");
		Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());
		Запрос.УстановитьПараметр("Заказ",         Сделка);
		
		СуммаСделки    = 0;
		СуммаСделкиУпр = 0;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			СуммаСделки    = Выборка.СуммаЗаказа;
			СуммаСделкиУпр = Выборка.СуммаЗаказаУпр;
			
		КонецЕсли;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
			СуммаДокумента = обПересчет(СуммаСделки, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, ДокументОснование.КурсДокумента, ВалютаДокумента, КурсДокумента);
		Иначе
			СуммаДокумента = обПересчет(СуммаСделкиУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		
		ОбработкаРеквизита("СуммаДокумента");
		//Статья ДДС - Оплата поставщику
		СтатьяДДС = Справочники.СтатьиДДС.ОплатаПоставщику;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетОтПоставщика") ИЛИ
		ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СчетОтПоставщикаЗаАвтомобили") Тогда
		
		//Проверим сумму предоплаты
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаПриход КАК Сумма,
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрПриход КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(,,,
		|		Контрагент = &Контрагент И
		|		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов И
		|		(Сделка = &Сделка ИЛИ Сделка.ДокументОснование = &Сделка)
		|	) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка",                ДокументОснование);
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		ДокументОснованиеСуммаДокумента = ДокументОснование.СуммаДокумента;
		
		Если ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			СуммаДокумента = ДокументОснованиеСуммаДокумента-обПересчет(тзОплаты.Итог("Сумма"), ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, Дата, РежимОкругления.Окр15как20);
		Иначе
			СуммаДокумента = ДокументОснованиеСуммаДокумента-обПересчет(тзОплаты.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата, ВалютаДокумента, ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсДокумента), Дата, ЭтотОбъект.КурсДокумента), РежимОкругления.Окр15как20);
		КонецЕсли;
		
		ОбработкаРеквизита("СуммаДокумента");
		//Статья ДДС - Оплата поставщику
		СтатьяДДС = Справочники.СтатьиДДС.ОплатаПоставщику;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.Инкассация") Тогда
		
		ИзменилсяКонтрагент = (Контрагент <> Основание.Инкассатор);
		ИзменилсяДоговор    = (ДоговорВзаиморасчетов <> Основание.ДоговорВзаиморасчетовИнкассатор);
		
		Контрагент=Основание.Инкассатор;
		ДоговорВзаиморасчетов=Основание.ДоговорВзаиморасчетовИнкассатор;
		
		Если ИзменилсяКонтрагент Тогда
			ОбработкаРеквизита("Контрагент");
		КонецЕсли;
		
		Если ИзменилсяДоговор Тогда
			ОбработкаРеквизита("ДоговорВзаиморасчетов");
		КонецЕсли;
		СуммаДокумента=Основание.СуммаНал;
		ОбработкаРеквизита("СуммаДокумента");
		КурсВалютыВзаиморасчетов = дкПолучитьКурсВалютыВзаиморасчетов(ЭтотОбъект, ДоговорВзаиморасчетов, Основание);
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ОбслуживаниеАктива") Тогда
		//Вычислим сумму
		СуммаДокумента=обПересчет(Основание.СтоимостьСтороннихУслуг,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,Дата);
		ОбработкаРеквизита("СуммаДокумента");
		//Статья ДДС - Оплата от покупателя
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаПоставщику;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
		//Вычислим сумму
		СуммаДокумента=обПересчет(Основание.СуммаДокумента,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,Дата);
		ОбработкаРеквизита("СуммаДокумента");
		//Статья ДДС - Возврат покупателю
		СтатьяДДС=Справочники.СтатьиДДС.ВозвратПокупателю;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		
		СтруктураОтбора=Новый Структура();
		СтруктураОтбора.Вставить("Контрагент",ДокументОснование.Контрагент);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДокументОснование.ДоговорВзаиморасчетов);
		Если ЗначениеЗаполнено(ДокументОснование.Сделка) Тогда
			СтруктураОтбора.Вставить("Сделка", ДокументОснование.Сделка);
			Сделка = ДокументОснование.Сделка;
		КонецЕсли;
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		Если ВалютаДокумента=ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов Тогда
			Долг=обПересчет(-тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.Дата,ВалютаДокумента,ДокументОснование.Дата,РежимОкругления.Окр15как20);
		ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			Долг=-тзДолги.Итог("СуммаБаз");
		Иначе
			Долг=обПересчет(-тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.Дата,ВалютаДокумента,ДокументОснование.Дата,РежимОкругления.Окр15как20);
		КонецЕсли;
		
		СуммаДокумента=Макс(Долг,0);
		ОбработкаРеквизита("СуммаДокумента");
		СтатьяДДС=Справочники.СтатьиДДС.ВозвратПокупателю;
		ТипРасчета = Неопределено;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРасходДС") Тогда
		СуммаДокумента = ДокументОснование.СуммаДокумента;
		ОбработкаРеквизита("СуммаДокумента");
		// В зависимости от хоз операции документа основания будем подставлять Кассу или нет
		Если ДокументОснование.ХозОперация = Справочники.ХозОперации.ЗаявкаНаРасходИзКассы Тогда
			КассаКомпании = ДокументОснование.СтруктурнаяЕдиница;
		КонецЕсли;
		
		Сделка               = Основание.ДокументОснование;
		ДокументПланирования = Основание;
		
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей") Тогда
		
		Если ЗначениеЗаполнено(Основание.ДокументОснование) Тогда
			Сделка = Основание.ДокументОснование;
		КонецЕсли;
		
		// Проверим сумму долга
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	- ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот КАК Сумма,
		|	- ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаУпрОборот КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Обороты(
		|			,
		|			,
		|			,
		|			Контрагент = &Контрагент
		|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		|				И (Сделка = &Сделка
		|					ИЛИ Сделка.ДокументОснование = &Сделка)) КАК ВзаиморасчетыКомпанииОстаткиИОбороты
		|ГДЕ
		|	ВзаиморасчетыКомпанииОстаткиИОбороты.СуммаОборот < 0";
		Запрос.УстановитьПараметр("Контрагент",            ДокументОснование.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДокументОснование.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("Сделка", Сделка);
		
		тзОплаты = Запрос.Выполнить().Выгрузить();
		
		Если ЭтотОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов = ЭтотОбъект.ВалютаДокумента Тогда
			Долг = обПересчет(тзОплаты.Итог("Сумма"),ЭтотОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ЭтотОбъект.Дата,ЭтотОбъект.ВалютаДокумента,ЭтотОбъект.Дата,РежимОкругления.Окр15как20);
		Иначе
			Долг	= тзОплаты.Итог("СуммаУпр");
			Долг	= обПересчет(Долг,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ?(НЕ ЗначениеЗаполнено(ЭтотОбъект.КурсДокумента), ЭтотОбъект.Дата, ЭтотОбъект.КурсДокумента),РежимОкругления.Окр15как20);
		КонецЕсли;
		
		Если Долг < Основание.СуммаДокумента Тогда
			СуммаДокумента=Макс(Долг,0);
		Иначе
			СуммаДокумента=Основание.СуммаДокумента;
		КонецЕсли;
		ОбработкаРеквизита("СуммаДокумента");
		
	Иначе
		//Вычислим сумму
		СтруктураОтбора=Новый Структура(); 
		СтруктураОтбора.Вставить("Контрагент",ДокументОснование.ДоговорВзаиморасчетов.Владелец);
		СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ДокументОснование.ДоговорВзаиморасчетов);
		СтруктураОтбора.Вставить("Сделка",ДокументОснование);
		тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(ТекущаяДата(),СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
		Если ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаДокумента Тогда
			ОстатокПоСделке=обПересчет(тзДолги.Итог("Сумма"),ДокументОснование.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
		ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			ОстатокПоСделке=тзДолги.Итог("СуммаБаз");
		Иначе
			ОстатокПоСделке=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ДокументОснование.КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли;
		СуммаДокумента=-ОстатокПоСделке;
		ОбработкаРеквизита("СуммаДокумента");
		//Статья ДДС - Оплата от покупателя
		СтатьяДДС=Справочники.СтатьиДДС.ОплатаПоставщику;
		
	КонецЕсли;
	
	// Для нового документа нельзя заполнять данные ФР
	Если ЭтоНовый() Тогда
		Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ВыплатаЗарплаты") Тогда	
		ХозОперация=Справочники.ХозОперации.ВыплатаЗарплатыРаботнику;
	ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		ХозОперация=Справочники.ХозОперации.ВыдачаДенежныхСредствПодотчетнику;
	КонецЕсли;
	
	КурсВалютыВзаиморасчетов = дкПолучитьКурсВалютыВзаиморасчетов(ЭтотОбъект, ?(обЗначениеНеЗаполнено(ЭтотОбъект.ДоговорВзаиморасчетов), Неопределено, ЭтотОбъект.ДоговорВзаиморасчетов), ?(обЗначениеНеЗаполнено(ЭтотОбъект.ДокументОснование), Неопределено, ЭтотОбъект.ДокументОснование));
	ОбработкаРеквизита("КассаКомпании",,,Новый Структура("ЗадаватьВопрос",Ложь));
	ОбработкаРеквизита("Контрагент");
	
	дкСформироватьСуммуНДСДокументаОтгрузки(ЭтотОбъект, ЭтотОбъект.ДокументОснование, СуммаДокумента);
	
	Если НЕ ЗначениеЗаполнено(ТипРасчета) Тогда
		ТипРасчета = дкТипРасчетаДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	
	Товары.Очистить();
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// приходуем деньги в кассу
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДС.РежимПроведения=Режим;
	НаборЗаписейДС.СтруктурнаяЕдиница=КассаКомпании;
	НаборЗаписейДС.Валюта=Неопределено;
	НаборЗаписейДС.СтатьяДДС=СтатьяДДС;
	НаборЗаписейДС.Сумма=СуммаДокумента;
	Отказ=НЕ НаборЗаписейДС.Расход() ИЛИ Отказ;
	
	// проводим взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок=АвтоЗакрытиеСделок;
	Если ЗначениеЗаполнено(Сделка)Тогда
		НаборЗаписейВзаиморасчеты.Сделка = Сделка;
		ТипСделка=ТипЗнч(Сделка);
		Если орПолучитьТипыСделок(Истина).СодержитТип(ТипСделка) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		ИначеЕсли орПолучитьТипыСделок(Ложь).СодержитТип(ТипСделка) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		Иначе
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Неопределено;
		КонецЕсли; 
	Иначе
		НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
	КонецЕсли; 
	НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
	НаборЗаписейВзаиморасчеты.Валюта=Неопределено;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	// доходы и расходы по суммовым разницам
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	ПодразделениеКасса = обПолучитьБалансовоеПодразделение(КассаКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКасса);
	Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		
		НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейДиР.Подразделение			= КассаКомпании.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте				= Ложь;
		НаборЗаписейДиР.Доход					= СуммаДокумента;
		Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		
		НаборЗаписейДиР.ДокументОбъект			= ЭтотОбъект;
		НаборЗаписейДиР.Подразделение			= ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов	= Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте				= Ложь;
		НаборЗаписейДиР.Доход					= СуммаДокумента;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;	
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	Если ДляПробитияНаФР=Истина И ЗначениеЗаполнено(КассаККМ) Тогда
		СуммаОплаты=обПересчет(СуммаДокумента,ВалютаДокумента,КурсДокумента,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),Дата);
		НаборЗаписейКассыККМ=Движения.КассыККМ;
		НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
		НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
		НаборЗаписейКассыККМ.КассаККМ 			= КассаККМ;
		НаборЗаписейКассыККМ.СпособОплаты 		= Справочники.ТипыОплат.Наличные;
		НаборЗаписейКассыККМ.Сумма				= СуммаОплаты;	
		Отказ=НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейКассыККМ.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейКассыККМ.РежимПроведения= Режим;
			НаборЗаписейКассыККМ.КассаККМ 		= КассаККМ;
			НаборЗаписейКассыККМ.СпособОплаты 	= Справочники.ТипыОплат.Наличные;
			НаборЗаписейКассыККМ.Сумма			= СуммаОплаты;	
			Отказ=НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ Отказ Тогда
		//проведение в Платежный календарь
		НаборЗаписей = Движения.ПлатежныйКалендарь;
		НаборЗаписей.ДокументОбъект = Ссылка;
		НаборЗаписей.Поступление    = Ложь;
		
		Отказ = Не НаборЗаписей.Факт();
	КонецЕсли;
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
#КонецЕсли
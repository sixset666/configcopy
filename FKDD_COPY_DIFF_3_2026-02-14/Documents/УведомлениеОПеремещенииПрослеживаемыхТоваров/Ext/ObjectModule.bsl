// Модуль документа УведомлениеОПеремещенииПрослеживаемыхТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ЗаполнитьПоОснованию(Основание) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполним табличную часть "Контрагенты"
	СтрокаКонтрагента = Неопределено;
	КонтрагентОснования = Основание.Контрагент;
	Если Контрагенты.Количество() > 0 Тогда
		НайденныеСтроки = Контрагенты.НайтиСтроки(Новый Структура("Контрагент", КонтрагентОснования));
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаКонтрагента = НайденныеСтроки[0];
		КонецЕсли;
	КонецЕсли;
	Если СтрокаКонтрагента = Неопределено Тогда
		СтрокаКонтрагента = Контрагенты.Добавить();
		СтрокаКонтрагента.Контрагент = КонтрагентОснования;
		СтрокаКонтрагента.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	КонецЕсли;
	ИдентификаторКонтрагента = СтрокаКонтрагента.ИдентификаторСтроки;
	
	// Найдем прослеживаемый товар по СФ
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетФактураВыданный.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	               |ГДЕ
	               |	СчетФактураВыданный.ДокументОснование = &Основание
	               |	И НЕ СчетФактураВыданный.ПометкаУдаления";
	Запрос.УстановитьПараметр("Основание", Основание);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТоварыСчетФактуры = Выборка.Ссылка.Товары;
	Иначе
		СчетФактура = Документы.СчетФактураВыданный.СоздатьДокумент();
		СчетФактура.ДополнительныеСвойства.Вставить("НеИскатьСчетФактуру", Истина);
		СчетФактура.Заполнить(Основание);
		ТоварыСчетФактуры = СчетФактура.Товары;
	КонецЕсли;
	
	// Оставим только прослеживаемые товары
	Для Каждого ТекущаяСтрока Из ТоварыСчетФактуры Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ГТД)
			ИЛИ НЕ ТекущаяСтрока.ГТД.РНПТ
			ИЛИ (НЕ ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				И НЕ ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Автомобили"))
			ИЛИ НЕ ТекущаяСтрока.Номенклатура.Прослеживаемый Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.СопроводительныйДокумент = Основание;
		НоваяСтрока.ПорядковыйНомер = ТекущаяСтрока.НомерСтроки;
		НоваяСтрока.РНПТ = ТекущаяСтрока.ГТД;
		НоваяСтрока.Сумма = ТекущаяСтрока.СуммаВсего - ТекущаяСтрока.СуммаНДС;
		НоваяСтрока.ЕдиницаИзмеренияПрослеживаемости = 
			?(ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Номенклатура"),
			ТекущаяСтрока.Номенклатура.БазоваяЕдиницаИзмерения,
			Справочники.КлассификаторЕдиницИзмерения.шт);
		НоваяСтрока.КодТНВЭД = ТекущаяСтрока.Номенклатура.КодТНВЭД;
		НоваяСтрока.КоличествоПрослеживаемости = ТекущаяСтрока.Количество * ТекущаяСтрока.Коэффициент;
		НоваяСтрока.ИдентификаторСтроки = ИдентификаторКонтрагента;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("КоличествоПрослеживаемости",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмеренияПрослеживаемости",1);
	ОбязательныеТовары.Вставить("Сумма",1);
	ОбязательныеТовары.Вставить("РНПТ",3);
	ОбязательныеТовары.Вставить("ПорядковыйНомер",3);
	ОбязательныеТовары.Вставить("СопроводительныйДокумент",3);
	ОбязательныеТовары.Вставить("ИдентификаторСтроки",1);
	
	ОбязательныеКонтрагенты = Новый Структура();
	ОбязательныеКонтрагенты.Вставить("Контрагент",3);
	ОбязательныеКонтрагенты.Вставить("ИдентификаторСтроки",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Состояние",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Контрагенты",ОбязательныеКонтрагенты);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УведомлениеОПеремещенииПрослеживаемыхТоваров",		"Уведомление о перемещении прослеживаемых товаров в ЕАЭС",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
			// Поставим единицу если она не заполнена
			Если ТипЗнч(ТекСтрока.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
				ТекСтрока.ЕдиницаИзмерения = ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				ТекСтрока.ЕдиницаИзмеренияПрослеживаемости = ТекСтрока.Номенклатура.БазоваяЕдиницаИзмерения;
			Иначе
				ТекСтрока.ЕдиницаИзмерения = Справочники.Номенклатура.Автомобиль.ОсновнаяЕдиницаИзмерения;
				ТекСтрока.ЕдиницаИзмеренияПрослеживаемости = Справочники.Номенклатура.Автомобиль.БазоваяЕдиницаИзмерения;
			КонецЕсли;
			ТекСтрока.КодТНВЭД = ТекСтрока.Номенклатура.КодТНВЭД;
		Иначе
			ТекСтрока.ЕдиницаИзмерения = Неопределено;
			ТекСтрока.ЕдиницаИзмеренияПрослеживаемости = Неопределено;
			ТекСтрока.КодТНВЭД = Неопределено;
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры	= Новый Структура();
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.КоличествоПрослеживаемости = ТекСтрока.Количество * ?(НЕ ЗначениеЗаполнено(ТекСтрока.ЕдиницаИзмерения) ИЛИ ТекСтрока.ЕдиницаИзмерения.Коэффициент = 0, 1, ТекСтрока.ЕдиницаИзмерения.Коэффициент);
		Возврат Рез;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
	// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
	//
	// Параметры:
	//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
	//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
	//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
	//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
	//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
	//
	// Возвращаемое значение:
	//   ТабличныйДокумент	- Сформированная печатная форма
	//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Формирует печатную форму "Уведомление о перемещении"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьУведомлениеОПеремещении(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("УведомлениеОПеремещенииПрослеживаемыхТоваров");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакета.Параметры.НомерУведомления = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.ДатаУведомления = Формат(Дата, "ДЛФ=D");
	ОбластьМакета.Параметры.НомерКорректировки = "0";
	ОбластьМакета.Параметры.НаимОрг = СокрЛП(Организация.НаименованиеПолное);
	ОбластьМакета.Параметры.ИНН = Организация.ИНН;
	ОбластьМакета.Параметры.КПП = Организация.КПП;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	ОбластьСтрокаСопрДокумент = Макет.ПолучитьОбласть("СтрокаСопрДокумент");
	ОбластьСведОПунктахНазначения = Макет.ПолучитьОбласть("СведОПунктахНазначения");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ЭтоПервыйКонтрагент = Истина;
	
	// Пройдемся по каждому контрагенту
	Для Каждого ТекущийКонтрагент Из Контрагенты Цикл
		Если НЕ ЭтоПервыйКонтрагент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ЭтоПервыйКонтрагент = Ложь;
		ОбластьКонтрагент.Параметры.НаимЛицаПередПраво = спПолучитьПредставление(ТекущийКонтрагент.Контрагент, Новый Структура("Наименование"));
		ОбластьКонтрагент.Параметры.КодСтраныКонтрагента = ТекущийКонтрагент.Контрагент.СтранаРегистрации.Код;
		ОбластьКонтрагент.Параметры.НалоговыйОрганКонтрагента = ТекущийКонтрагент.Контрагент.НалоговыйНомер;
		ОбластьКонтрагент.Параметры.АдресКонтрагента = спПолучитьПредставление(ТекущийКонтрагент.Контрагент, Новый Структура("АдресЮридический"));
		ТабДокумент.Вывести(ОбластьКонтрагент);
		
		// Найдем документы контрагента и товары
		НайденныеТовары = Товары.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ТекущийКонтрагент.ИдентификаторСтроки));
		
		// Сформируем список документов
		СписокДокументов = Новый Массив;
		Для Каждого СтрокаТоваров Из НайденныеТовары Цикл
			Если СписокДокументов.Найти(СтрокаТоваров.СопроводительныйДокумент) = Неопределено Тогда
				СписокДокументов.Добавить(СтрокаТоваров.СопроводительныйДокумент);
			КонецЕсли;
		КонецЦикла;
		Для Каждого ТекущийДокумент Из СписокДокументов Цикл
			ОбластьСтрокаСопрДокумент.Параметры.ВидСопоставляемогоДокумента = "2";
			ОбластьСтрокаСопрДокумент.Параметры.ДатаВходящегоДокумента = Формат(ТекущийДокумент.Дата, "ДЛФ=D");
			ОбластьСтрокаСопрДокумент.Параметры.НомерВходящегоДокумента = дкПолучитьНомерДляПечати(ТекущийДокумент);
			ТабДокумент.Вывести(ОбластьСтрокаСопрДокумент);
		КонецЦикла;
		
		// Адрес доставки пока не заполняем
		ТабДокумент.Вывести(ОбластьСведОПунктахНазначения);
		
		// Теперь выведем таблицу с товарами
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		Для Каждого СтрокаТоваров Из НайденныеТовары Цикл
			ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, СтрокаТоваров);
			ОбластьСтрока.Параметры.Номенклатура = спПолучитьНаименование(СтрокаТоваров.Номенклатура);
			КодЕдиницыИзмерения = "";
			Если ТипЗнч(СтрокаТоваров.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
				КодЕдиницыИзмерения = СтрокаТоваров.ЕдиницаИзмерения.Код;
			ИначеЕсли ТипЗнч(СтрокаТоваров.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
				КодЕдиницыИзмерения = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			Иначе
				КодЕдиницыИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт.Код;
			КонецЕсли;
			ОбластьСтрока.Параметры.КодЕдиницыИзмерения = КодЕдиницыИзмерения;
			ОбластьСтрока.Параметры.КодЕдиницыИзмеренияПрослеживаемости = СтрокаТоваров.ЕдиницаИзмеренияПрослеживаемости.Код;
			дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы,,,, ЭтотОбъект);
		КонецЦикла;
	КонецЦикла;
	
	// Выведем подвал
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	СтруктураОтбора=Новый Структура("Организация,Объект", Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	Если ЗначениеЗаполнено(Руководитель) Тогда
		ОбластьМакета.Параметры.ПрПодп = "1";
		МассивФИО = СтрРазделить(Руководитель.Наименование, " ", Ложь);
		ОбластьМакета.Параметры.ОргПодписантФамилия = СокрЛП(МассивФИО[0]);
		ОбластьМакета.Параметры.ОргПодписантИмя = ?(МассивФИО.Количество() > 1, МассивФИО[1], "");
		Если МассивФИО.Количество() > 2 Тогда
			Отчество = "";
			Для Инд = 2 По МассивФИО.Количество() - 1 Цикл
				Отчество = Отчество + МассивФИО[Инд];
			КонецЦикла;
			ОбластьМакета.Параметры.ОргПодписантОтчество = Отчество;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДатаПодписи = Формат(Дата, "ДЛФ=D");
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЧек()

#КонецЕсли

// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Рез=дкОбработкаЗаполнения(ЭтотОбъект, Основание);
	
	Если ЭтоНовый() Тогда
		Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
	КонецЕсли;
	
	// Приведем валюту документа в регл. валюте
	ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаДокумента, Дата);
	КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Если Основание <> Неопределено Тогда
		
		Контрагенты.Очистить();
		Товары.Очистить();
		ЗаполнитьПоОснованию(Основание);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Товары.Итог("Сумма");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		//Проверим корректность видов введенной номенклатуры
		БлокироватьВидыНоменклатуры = ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
		Если БлокироватьВидыНоменклатуры<>Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры)=Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
			Ошибки="";
			Для каждого СтрокаТоваров Из Товары Цикл
				ЭтоРеализацияАвтомобилей = (ТипЗнч(СтрокаТоваров.СопроводительныйДокумент) = Тип("ДокументСсылка.РеализацияАвтомобилей"));
				Если ЭтоРеализацияАвтомобилей И ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					ИЛИ НЕ ЭтоРеализацияАвтомобилей И ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Указанный тип товара не соответствует первичному документу.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если НЕ СтрокаТоваров.Номенклатура.Прослеживаемый Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Указанная номенклатура не является прослеживаемым товаром.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
					Продолжить;
				КонецЕсли;
				ЕстьОшибка=(БлокироватьВидыНоменклатуры[СтрокаТоваров.Номенклатура.ВидНоменклатуры]<>Неопределено);
				Если ЕстьОшибка Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. В таблицу нельзя вводить номенклатуру вида """+СокрЛП(СтрокаТоваров.Номенклатура.ВидНоменклатуры)+""".";
					Отказ=Истина;
				КонецЕсли;
			КонецЦикла; 
			Если Отказ Тогда
				#Если Клиент Тогда
				// Выведем все накопившиеся ошибки 
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
				Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				#КонецЕсли
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

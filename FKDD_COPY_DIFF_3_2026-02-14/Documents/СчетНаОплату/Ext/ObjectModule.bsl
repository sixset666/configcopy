// Модуль документа СчетНаОплату

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("РасчетныйСчетОрганизации",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	Если ЗначениеЗаполнено(ДокументОснование) И 
			(НЕ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетНаОплату")) И 
			(НЕ ДокументОснование.Метаданные().Реквизиты.Найти("ТипЦенРабот") = Неопределено) Тогда
		ОбязательныеРеквизиты.Вставить("ТипЦенРабот",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только в случае если
	// все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;     	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СчетНаОплату","Счет на оплату",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя = "Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ Организация.Пустая() Тогда
			//БизТех 15.05.2023г. для некоторых подразделений требуется брать банковский счет из самого спр. Подразделния <<
			Если ЗначениеЗаполнено(ПодразделениеКомпании.ОсновнойБанковскийСчет) Тогда
				РасчетныйСчетОрганизации = ПодразделениеКомпании.ОсновнойБанковскийСчет; 
			// БизТех >>
			ИначеЕсли (РасчетныйСчетОрганизации.Пустая()) ИЛИ (НЕ РасчетныйСчетОрганизации.Владелец = Организация) Тогда
				РасчетныйСчетОрганизации = Организация.ОсновнойБанковскийСчет;	
			КонецЕсли;
		Иначе
			РасчетныйСчетОрганизации = Справочники.БанковскиеСчета.ПустаяСсылка();
		КонецЕсли; 
		Возврат Рез;

	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
			//Если имеем дело с автоработой - обработаем локально
			//Установим количество и коэффициент
			Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
			Если ТекСтрока.Коэффициент=0 Тогда ТекСтрока.Коэффициент=1; КонецЕсли; 
			//Заполним ставку НДС    
			
			//<<Павличенко 23.03.2020
			//ТекСтрока.СтавкаНДС=ТекСтрока.Номенклатура.Номенклатура.СтавкаНДС;
			ТекСтрока.СтавкаНДС=?(Этаформа.Организация.Код = "ЦБ000004",Справочники.СтавкиНДС.БезНДС ,ТекСтрока.Номенклатура.Номенклатура.СтавкаНДС);
			//>>Павличенко 23.03.2020 
			
			//Перерассчитаем цену текущей работы
			ОбработкаРеквизита("РасчетЦеныРаботы",ТекСтрока,ЭтаФорма,ДопПараметры);
			//Расчет сумм по строке
			ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
			//Сбросим характеристику, так как для автоработы она не применима
			ТекСтрока.ХарактеристикаНоменклатуры=Неопределено;
			Возврат Истина;
		Иначе
			Если ТекСтрока.Номенклатура=Неопределено Тогда
				ТекСтрока.Номенклатура=Справочники.Номенклатура.ПустаяСсылка();
			КонецЕсли; 
			//В противном случае стандартная обработка
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
			//Если имеем дело с автоработой - обработаем локально
			Возврат ОбработкаРеквизита("Товары.Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			//В противном случае стандартная обработка
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
			//Если имеем дело с автоработой - обработаем локально
			ЦенаНормочаса=ТекСтрока.ЕдиницаИзмерения.Цена;
			ТекСтрока.Цена=обПересчет(ЦенаНормочаса,ТекСтрока.ЕдиницаИзмерения.Валюта,Дата,ВалютаДокумента,КурсДокумента);
			Возврат ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			//В противном случае стандартная обработка
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Коэффициент" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
			//Если имеем дело с автоработой - обработаем локально
			Возврат ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			//В противном случае стандартная обработка
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя = "РассчитатьСкидки" Тогда
		Рез=Истина;
		Если БлокироватьПерерасчетСкидок Тогда
			Попытка
				Для Каждого стрДок Из ЭтотОбъект.Товары Цикл
					Если НЕ ЗначениеЗаполнено(ЭтотОбъект[?(ТипЗнч(стрДок.Номенклатура) = Тип("СправочникСсылка.Номенклатура"), "СкидкаНаценка", "СкидкаНаценкаРаботы")]) Тогда
						стрДок.ПроцентСкидки = 0;
						Рез=ЭтотОбъект.ОбработкаРеквизита(ОбработкаРасчетСкидок.ИмяТабличнойЧасти + ".ПроцентСкидки", стрДок, ЭтаФорма) И Рез;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
			Если ТекСтрока = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Рез=ЭтотОбъект.ОбработкаРеквизита(ОбработкаРасчетСкидок.ИмяТабличнойЧасти + ".ПроцентСкидки", ТекСтрока, ЭтаФорма) И Рез;
			Попытка
				Рез=ЭтотОбъект.ОбработкаРеквизита(ОбработкаРасчетСкидок.ИмяТабличнойЧасти + ".ПроцентСкидкиСтроки", ТекСтрока, ЭтаФорма) И Рез;
			Исключение
			КонецПопытки;
			
			Возврат Истина;
		Иначе
			Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="РасчетЦеныРаботы" Тогда
		//Расчет цены работы
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда Возврат Ложь; КонецЕсли;
		Модель = ПолучитьМодель();
		Попытка
			Цех=ДокументОснование.Цех;
		Исключение
			Цех=Справочники.Цеха.ПустаяСсылка();
		КонецПопытки; 
		Попытка
			ВидРемонта=ДокументОснование.ВидРемонта;
		Исключение
			ВидРемонта=Справочники.ВидыРемонта.ПустаяСсылка();
		КонецПопытки; 
		ЦенаРаботы=орПолучитьЦенуАвтоработы(ТипЦенРабот, ТекСтрока.Номенклатура,Модель,ДоговорВзаиморасчетов,Цех,ВидРемонта,,ВалютаДокумента,КурсДокумента);
		ТекСтрока.ЕдиницаИзмерения=ЦенаРаботы.Нормочас;
		ТекСтрока.Коэффициент=?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени),ТекСтрока.Коэффициент,ЦенаРаботы.НормаВремени);
		ТекСтрока.Цена=ЦенаРаботы.Цена;
		Возврат Истина;

	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Получение модели автомобиля для расчета цен
//
Функция ПолучитьМодель()
	
	Модель = Справочники.Модели.ПустаяСсылка();
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Если ДокументОснование.Метаданные().Реквизиты.Найти("Автомобиль")<>Неопределено Тогда
			Автомобиль = ДокументОснование.Автомобиль;
			Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") И ЗначениеЗаполнено(Автомобиль) Тогда
				Если ЗначениеЗаполнено(Автомобиль.ВариантКомплектации) Тогда
					Модель = Автомобиль.ВариантКомплектации;
				Иначе
					Модель = Автомобиль.Модель;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ДокументОснование.Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
				Модель = ДокументОснование.Автомобиль;
				Если ДокументОснование.Метаданные().Реквизиты.Найти("ВариантКомплектации")<>Неопределено Тогда
					Если ЗначениеЗаполнено(ДокументОснование.ВариантКомплектации) Тогда
						Модель = ДокументОснование.ВариантКомплектации;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Модель;
	
КонецФункции

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "СчетНаОплату"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьСчетНаОплату(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("СчетНаОплату");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод образца заполнения платёжного поручения
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета"); 
	ОбластьМакета.Параметры.БанкПолучателя						= Символы.НПП+РасчетныйСчетОрганизации.Банк;
	ОбластьМакета.Параметры.БанкПолучателяПредставление			= спПолучитьНаименование(РасчетныйСчетОрганизации.Банк);
	ОбластьМакета.Параметры.БИКБанкаПолучателя					= Символы.НПП+СокрЛП(РасчетныйСчетОрганизации.Банк.Код);
	ОбластьМакета.Параметры.СчетБанкаПолучателя					= Символы.НПП+СокрЛП(РасчетныйСчетОрганизации.Банк.КоррСчет);
	ОбластьМакета.Параметры.ИНН									= Символы.НПП+спПолучитьПредставление(Организация,Новый Структура("ИНН"));
	
	//bz++
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.КПП = Символы.НПП + спПолучитьПредставление(Организация, Новый Структура("КПП"), ДополнительныеПараметры);
	
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления 									= Символы.НПП+спПолучитьПредставление(Организация,Новый Структура("КПП"));
		ОбластьМакета.Параметры.КПП								= СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьМакета.Параметры.КПП									= Символы.НПП+спПолучитьПредставление(Организация,Новый Структура("КПП"));
	КонецЕсли;
	//bz--	
	
	ОбластьМакета.Параметры.Получатель							= Организация;
	ОбластьМакета.Параметры.ПолучательПредставление				= спПолучитьПредставление(Организация,Новый Структура("Наименование"));
	ОбластьМакета.Параметры.СчетПолучателя						= Символы.НПП+СокрЛП(РасчетныйСчетОрганизации.НомерСчета);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтруктураПредставления=Новый Структура(
		"ИНН,КПП,Наименование,АдресЮридический,ТелефонРабочий",
		"ИНН ","КПП ","","","тел.: "
	);
	
	//bz++
	//ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация,
	//	СтруктураПредставления, ДополнительныеПараметры);
	
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления 									= спПолучитьПредставление(Организация,СтруктураПредставления);
		ОбластьМакета.Параметры.ПредставлениеПоставщика			= СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
    	ОбластьМакета.Параметры.ПредставлениеПоставщика 		= спПолучитьПредставление(Организация,СтруктураПредставления);
	КонецЕсли;
	//bz--
	
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Контрагент,СтруктураПредставления);
	ОбластьМакета.Параметры.ПредставлениеДоговорВзаиморасчетов = Строка(ДоговорВзаиморасчетов);
	ОбластьМакета.Параметры.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;

	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод документа основания
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ДокументОснование");		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			ЗаказНарядНомерДляПечати = дкПолучитьНомерДляПечати(ДокументОснование);
			РеквизитыЗаказНаряда = обЗначенияРеквизитовОбъекта(ДокументОснование, "Дата,ДатаЗакрытия");			
			ЗаказНарядДатаПечатная	= ?(обЗначениеНеЗаполнено(РеквизитыЗаказНаряда.ДатаЗакрытия), РеквизитыЗаказНаряда.Дата, РеквизитыЗаказНаряда.ДатаЗакрытия);
			ДокументОснованиеПредставление = ДокументОснование.Метаданные().Синоним + " № " + ЗаказНарядНомерДляПечати + " от " + Формат(ЗаказНарядДатаПечатная,"ДФ = dd.MM.yyyy");	
		Иначе
			ДокументОснованиеПредставление = дкПолучитьПредставление(ДокументОснование);		
		КонецЕсли;
		ОбластьМакета.Параметры.ДокументОснованиеПредставление=ДокументОснованиеПредставление;
		ОбластьМакета.Параметры.ДокументОснование=ДокументОснование;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 
	
	//вывод срока действия счета
	Если ЗначениеЗаполнено(ДействителенДо) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ДействителенДо");
		ОбластьМакета.Параметры.ДействителенДо = Формат(ДействителенДо,"ДФ = dd.MM.yyyy");
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ЕдиницаИзмеренияАвтоработВПечатныхФормах = ЭтотОбъект.ДоговорВзаиморасчетов.ЕдиницаИзмеренияАвтоработВПечатныхФормах;
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтрокаКомментарий=СокрЛП(СтрокаТабличнойЧасти.Комментарий);
		Если НЕ ПустаяСтрока(СтрокаКомментарий) Тогда
			СтруктураСтроки.ТоварНаименование = СокрЛП(СтрокаКомментарий);
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)<>Тип("СправочникСсылка.Номенклатура") Тогда
			Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)<>Тип("СправочникСсылка.Автоработы") Тогда
				ОбластьМакета.Параметры.ЕдиницаИзмерения = "-";
			Иначе
				КоличествоТовара = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Коэффициент;
				ОбластьМакета.Параметры.Количество = КоличествоТовара;
				//ОбластьМакета.Параметры.ЕдиницаИзмерения = ЕдиницаИзмеренияАвтоработВПечатныхФормах; 
				ОбластьМакета.Параметры.ЕдиницаИзмерения = "ч";
				ОбластьМакета.Параметры.Цена = Формат(СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы);
			КонецЕсли;
		ИначеЕсли НЕ СтруктураСтроки.Свойство("ЕдиницаИзмерения") Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмерения = "-";
		КонецЕсли;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки") + ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки");
	Если СкидкаВсего > 0 Тогда
		//СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	Руководитель.РуководительПредставление = ?(обЗначениеНеЗаполнено(Руководитель.Руководитель),"","/"+Руководитель.РуководительПредставление+"/");
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/"+ГлавныйБухгалтер.ГлавныйБухгалтерПредставление+"/");
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьСчетНаОплату()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	//БИЗТЕХ КОВАЛЬ 30.10.2024 ++
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") И
		Основание.ВидРемонта = Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000051") И 
		Основание.Состояние <> Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
		Сообщить("Счет на оплату на основании заказ-наряда с видом ремонта <Дополнительное оборудование КСО> можно вводить только в состоянии <Закрыт>!");
		дкОтменаВводаДокумента(ЭтотОбъект);
	КонецЕсли;
	//--
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если НЕ Организация.Пустая() И (РасчетныйСчетОрганизации.Пустая()
		ИЛИ РасчетныйСчетОрганизации.Владелец <> Организация) Тогда
		РасчетныйСчетОрганизации = Организация.ОсновнойБанковскийСчет;
	КонецЕсли;
	
	//БизТех 15.05.2023г. для некоторых подразделений требуется брать банковский счет из самого спр. Подразделния <<
	Если ЗначениеЗаполнено(ПодразделениеКомпании.ОсновнойБанковскийСчет) Тогда
		РасчетныйСчетОрганизации = ПодразделениеКомпании.ОсновнойБанковскийСчет;
	КонецЕсли;
	// БизТех >>
	
	Если Основание <> Неопределено Тогда
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров")
			ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.АвансовыйОтчет")
			ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
			Товары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,ХарактеристикаНоменклатуры,Комментарий,СтавкаНДС,ПроцентСкидки,СкидкаНаТовар,ПроцентСкидкиСтроки,Поставщик,СрокПоставкиВСтроке,КлючСтрокиПоставщика", "Количество,Сумма,СуммаНДС,СуммаВсего,СуммаСкидки,СуммаСкидкиСтроки");
			Для Каждого ТекСтрока Из Товары Цикл
				СуммаНДС = ТекСтрока.СуммаНДС;
				ОбработкаРеквизита("Товары.СуммаВсего", ТекСтрока);
				ТекСтрока.СуммаНДС = СуммаНДС;
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
			ТипЗнч(Основание)=Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			Контрагент = Неопределено;
			ДоговорВзаиморасчетов = Неопределено;
			ТипЦенНовый=обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
			Если ТипЦен<>ТипЦенНовый Тогда
				ТипЦен=ТипЦенНовый;
				Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
					ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
				КонецЕсли; 
			КонецЕсли; 
			ТипЦенРабот = обПраво("ОсновнойТипЦенРабот",Права,,ЭтотОбъект);
			СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			Для Каждого ТекСтрока Из Товары Цикл
				ТекСтрока.Цена = обПолучитьЦену(ТипЦен,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
				ОбработкаРеквизита("Товары.Цена",ТекСтрока);
			КонецЦикла;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			Контрагент = Неопределено;
			ДоговорВзаиморасчетов = Неопределено;
			ТипЦенНовый=обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
			Если ТипЦен<>ТипЦенНовый Тогда
				ТипЦен=ТипЦенНовый;
				Если обПраво("ИзменятьВалютуПоКатегорииЦен",Права,,ЭтотОбъект) Тогда
					ВалютаДокумента=обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
				КонецЕсли; 
			КонецЕсли; 
			ТипЦенРабот = обПраво("ОсновнойТипЦенРабот",Права,,ЭтотОбъект);
			СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента = СтруктураКурса.Курс/?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			Запрос = Новый Запрос("ВЫБРАТЬ
			|	ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
			|	ЗаказыПоставщикам.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	СУММА(ЗаказыПоставщикам.Заказано) КАК Заказано
			|ПОМЕСТИТЬ ЗаказыПоставщикамОстатки
			|ИЗ
			|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
			|ГДЕ
			|	ЗаказыПоставщикам.Период <= &Момент
			|	И НЕ ЗаказыПоставщикам.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
			|	И ЗаказыПоставщикам.ЗаказПоставщику = &ВыбЗаказПоставщику
			|	И ЗаказыПоставщикам.Контрагент = &ВыбКонтрагент
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗаказыПоставщикам.Номенклатура,
			|	ЗаказыПоставщикам.ХарактеристикаНоменклатуры
			|
			|ИМЕЮЩИЕ
			|	НЕ СУММА(ЗаказыПоставщикам.Заказано) = 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
			|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ЗаказыПоставщикамОстатки.Заказано КАК Количество,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.Количество * ЗаказПоставщикуТовары.Коэффициент, 1) КАК КоличествоБазовое,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.Коэффициент, 1) КАК Коэффициент
			|ИЗ
			|	ЗаказыПоставщикамОстатки КАК ЗаказыПоставщикамОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
			|		ПО (ЗаказПоставщикуТовары.Ссылка = &ВыбЗаказПоставщику)
			|			И ЗаказыПоставщикамОстатки.Номенклатура = ЗаказПоставщикуТовары.Номенклатура
			|			И ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки
			|ИТОГИ
			|	МАКСИМУМ(Количество),
			|	СУММА(КоличествоБазовое)
			|ПО
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры");
			
			Запрос.УстановитьПараметр("Момент", КонецДня(Дата));
			Запрос.УстановитьПараметр("ВыбКонтрагент", Основание.Контрагент);
			Запрос.УстановитьПараметр("ВыбЗаказПоставщику",      Основание);
			
			Товары.Очистить(); // нужные только скорректированные позиции
			ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатуры.Следующий() Цикл
				ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаХарактеристик.Следующий() Цикл
					ВсегоОсталось = ВыборкаХарактеристик.Количество;
					КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
					ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
					НоваяСтрока   = Неопределено;
					Пока ВыборкаДетали.Следующий() Цикл
						Если ВсегоОсталось=0 Тогда
							Прервать;
						КонецЕсли;
						
						Если ВыборкаДетали.Количество = 0 Тогда
							Продолжить;
						КонецЕсли;
						Если КоличествоБазовоеПоЗаказу = 1 Тогда
							КоличествоСтроки = ВыборкаДетали.Количество;
						Иначе
							КоличествоСтроки = ВыборкаДетали.Количество*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
						КонецЕсли;
						ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
						
						НоваяСтрока=Товары.Добавить();
						НоваяСтрока.Номенклатура				= ВыборкаДетали.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры	= ВыборкаДетали.ХарактеристикаНоменклатуры;
						НоваяСтрока.ЕдиницаИзмерения			= ВыборкаДетали.ЕдиницаИзмерения;
						НоваяСтрока.Коэффициент					= ВыборкаДетали.Коэффициент;
						ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.Количество					= ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);;
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						
						ВсегоОсталось = ВсегоОсталось-ТекущееКоличество;
					КонецЦикла;
					
					Если ВсегоОсталось=0 Тогда
						Продолжить;
					КонецЕсли;
					
					Если НЕ НоваяСтрока = Неопределено Тогда
						НоваяСтрока.Количество = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
					Иначе
						НоваяСтрока=Товары.Добавить();
						НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
						ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
						НоваяСтрока.Количество = ВсегоОсталось/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
					КонецЕсли;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
				КонецЦикла;
			КонецЦикла;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ
			|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
			|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК Количество,
			|	ЗаказыПокупателейОстатки.СуммаОстаток КАК СуммаЗаказа,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.Количество * ЗаказПокупателяТовары.Коэффициент,1) КАК КоличествоБазовое,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.ЕдиницаИзмерения,Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.Коэффициент,1) КАК Коэффициент,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.СтавкаНДС, ЗаказыПокупателейОстатки.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.НомерСтроки, 1000000) КАК НомерСтроки,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.СкидкаНаТовар, ЗНАЧЕНИЕ(Справочник.ТипыСкидок.ПустаяСсылка)) КАК СкидкаНаТовар,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.ПроцентСкидкиСтроки,0) КАК ПроцентСкидкиСтроки,
			|	ЕСТЬNULL(ЗаказПокупателяТовары.ПроцентСкидки,0) КАК ПроцентСкидки
			|ИЗ
			|	РегистрНакопления.ЗаказыПокупателей.Остатки("+?(Ссылка.Пустая(),"","&НаМомент")+",
			|	Контрагент = &Контрагент И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
			|ПО 
			|	ЗаказыПокупателейОстатки.Заказ                      = ЗаказПокупателяТовары.Ссылка И 
			|	ЗаказыПокупателейОстатки.Номенклатура               = ЗаказПокупателяТовары.Номенклатура И 
			|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказПокупателяТовары.ХарактеристикаНоменклатуры
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки
			|ИТОГИ
			|	СУММА(КоличествоБазовое),
			|	МАКСИМУМ(Количество),
			|	МАКСИМУМ(СуммаЗаказа)
			|ПО
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры");
			
			ВалютаЗаказа	= Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			СтруктураКурса	= обКурсДляВалюты(ВалютаЗаказа,Дата);
			КурсЗаказа		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			Если ДополнительныеСвойства.Свойство("ПолучитьДанныеНаДатуОснования") И ДополнительныеСвойства.ПолучитьДанныеНаДатуОснования Тогда
				Запрос.УстановитьПараметр("НаМомент",   Новый Граница(Основание.МоментВремени(), ВидГраницы.Включая));
			Иначе
				Запрос.УстановитьПараметр("НаМомент",   МоментВремени());
			КонецЕсли;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Запрос.УстановитьПараметр("Заказ",      Основание);
			
			СкидкаНаценка = Основание.СкидкаНаценка;
			ОбработкаРеквизита("СкидкаНаценка");
			Товары.Очистить(); // нужные только скорректированные позиции
			ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатуры.Следующий() Цикл
				ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаХарактеристик.Следующий() Цикл
					ВсегоОсталось = ВыборкаХарактеристик.Количество;
					КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
					СуммаОсталось = обПересчет(ВыборкаХарактеристик.СуммаЗаказа, ВалютаЗаказа, КурсЗаказа,ВалютаДокумента,КурсДокумента);
					ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
					НоваяСтрока   = Неопределено;
					Пока ВыборкаДетали.Следующий() Цикл
						Если ВсегоОсталось=0 Тогда
							Прервать;
						КонецЕсли;
						
						Если ВыборкаДетали.Количество = 0 Тогда
							Продолжить;
						КонецЕсли;
						Если КоличествоБазовоеПоЗаказу = 1 Тогда
							КоличествоСтроки = ВыборкаДетали.Количество;
						Иначе
							КоличествоСтроки = ВыборкаДетали.Количество*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
						КонецЕсли;
						ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
						
						НоваяСтрока=Товары.Добавить();
						НоваяСтрока.Номенклатура				= ВыборкаДетали.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры	= ВыборкаДетали.ХарактеристикаНоменклатуры;
						НоваяСтрока.ЕдиницаИзмерения			= ВыборкаДетали.ЕдиницаИзмерения;
						НоваяСтрока.Коэффициент					= ВыборкаДетали.Коэффициент;
						ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.Количество					= ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);;
						НоваяСтрока.СтавкаНДС					= ВыборкаДетали.СтавкаНДС;
						ТекСумма = Окр((СуммаОсталось/ВсегоОсталось)*ТекущееКоличество,2);
						СуммаОсталось = СуммаОсталось - ТекСумма;
						
						НоваяСтрока.СкидкаНаТовар = ВыборкаДетали.СкидкаНаТовар;
						НоваяСтрока.ПроцентСкидки = ВыборкаДетали.ПроцентСкидки;
						НоваяСтрока.ПроцентСкидкиСтроки = ВыборкаДетали.ПроцентСкидкиСтроки;
						
						НоваяСтрока.СуммаВсего = ТекСумма;
						ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
						
						ВсегоОсталось = ВсегоОсталось-ТекущееКоличество;
					КонецЦикла;
					
					Если ВсегоОсталось>0 ИЛИ СуммаОсталось>0 Тогда
						Если НЕ НоваяСтрока = Неопределено Тогда
							НоваяСтрока.Количество = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
							ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
							НоваяСтрока.СуммаВсего = НоваяСтрока.СуммаВсего + СуммаОсталось;
						Иначе
							НоваяСтрока=Товары.Добавить();
							НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
							НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
							ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
							НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
							НоваяСтрока.Количество = ВсегоОсталось/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
							ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
							НоваяСтрока.СуммаВсего = СуммаОсталось;
						КонецЕсли;
						ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.Событие") Тогда
			ОбработкаРеквизита("Контрагент");  
			//Misha
		ИначеЕсли (ТипЗнч(Основание)=Тип("ДокументСсылка.Соллерс_АктДистрибьютораПоГарантии")) Тогда
			НовСтрока = Товары.Добавить();
			НовСтрока.Номенклатура = Основание.Услуга;
			НовСтрока.Коэффициент = 1;
			НовСтрока.Количество = 1;
			НовСтрока.ЕдиницаИзмерения = Основание.Услуга.ОсновнаяЕдиницаИзмерения;
			НовСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			НовСтрока.Цена = Основание.ТаблицаЗаказНарядов.Итог("СуммаПодтвержденная");
			НовСтрока.Сумма = Основание.ТаблицаЗаказНарядов.Итог("СуммаПодтвержденная");
			НовСтрока.СуммаВсего = Основание.ТаблицаЗаказНарядов.Итог("СуммаПодтвержденная");
			//Misha
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли


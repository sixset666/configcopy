// Модуль документа ПоступлениеТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Если ХозОперация=Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
		Результат=Новый Соответствие();
		Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	Если ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
		ОбязательныеТовары.Вставить("ЗаказНаряд",2);
	КонецЕсли;
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	Если (НЕ (ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций)) И
		 (НЕ (ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду)) Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций ИЛИ 
		 ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
		Запрос.УстановитьПараметр("НаДиР", Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы);
		Запрос.УстановитьПараметр("СписокНоменклатуры", Товары.ВыгрузитьКолонку("Номенклатура"));
		Запрос.Текст = "ВЫБРАТЬ
		|	Номенклатура.Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	(Номенклатура.ВидНоменклатуры <> &Услуга
		|	ИЛИ Номенклатура.СпособРаспределенияДопРасходов <> &НаДиР)
		|	И Номенклатура.Ссылка В (&СписокНоменклатуры)";
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				дкДобавитьОшибку(Ошибки,"Номенклатура " + СокрЛП(Выборка.Ссылка) + "не является услугой, относящейся на доходы и расходы");
			КонецЦикла;	
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
				дкДобавитьОшибку(Ошибки,"Документ-основание не заполнено");
				Результат = Ложь;
			КонецЕсли; 
		Иначе 
			Для каждого СтрокаТоваров Из Товары Цикл
				Если обЗначениеНеЗаполнено(СтрокаТоваров.ЗаказНаряд) Тогда
					дкДобавитьОшибку(Ошибки,"У работы по субподряду <"+СокрЛП(СтрокаТоваров.Номенклатура)+"> не указан заказ-наряд, в рамках которого данная работы была выполнена");
					Результат = Ложь;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПоступлениеТоваров",		"Поступление товаров",Истина);
	Если Константы.СтратегияСписанияПартийТоваровПоДатам.Получить() <> Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
		СписокХозОпераций.Добавить("ПоступлениеТоваровКомиссия","Поступление товаров (комиссия)");
	КонецЕсли;
	СписокХозОпераций.Добавить("ПоступлениеБезвозмездное",	"Безвозмездное поступление");
	СписокХозОпераций.Добавить("УслугиСтороннихОрганизаций","Услуги сторонних организаций");
	СписокХозОпераций.Добавить("УслугиПоСубподряду",		"Услуги по субподряду");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// посчитаем суммы товаров и услуг
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=&Услуга И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&НаДоходыРасходы ТОГДА &Услуга ИНАЧЕ &Товар КОНЕЦ КАК ВидНоменклатуры,
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=&Услуга ТОГДА ДокументТовары.Номенклатура.СтатьяДопРасходов ИНАЧЕ NULL КОНЕЦ КАК СтатьяДопРасходов,
	|	СУММА(ДокументТовары.СуммаВсего) КАК СуммаВсего,
	//<<Павличенко 19.01.18
	|	СУММА(ДокументТовары.СуммаНДС) КАК СуммаНДС
	//>>Павличенко 19.01.18
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары 	КАК ДокументТовары
	|
	|ГДЕ  
	|	ДокументТовары.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО 
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=&Услуга И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&НаДоходыРасходы ТОГДА &Услуга ИНАЧЕ &Товар КОНЕЦ,
	|	ВЫБОР КОГДА ДокументТовары.Номенклатура.ВидНоменклатуры=&Услуга ТОГДА ДокументТовары.Номенклатура.СтатьяДопРасходов ИНАЧЕ NULL КОНЕЦ
	|
	|ИТОГИ ПО ВидНоменклатуры
	|");
	Запрос.УстановитьПараметр("ДокументСсылка",ШапкаДокумента.Ссылка);
	Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
	Запрос.УстановитьПараметр("Товар",Перечисления.ВидыНоменклатуры.Товар);
	Запрос.УстановитьПараметр("НаДоходыРасходы",Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы);
	ДеревоДоходовИРасходов=Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НайденнаяСтрокаТоваров=ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Товар,"ВидНоменклатуры");
		НайденнаяСтрокаУслуг=ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Услуга,"ВидНоменклатуры");
		Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ПоступлениеТоваровКомиссия И ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное И
			(НайденнаяСтрокаТоваров<>Неопределено ИЛИ НайденнаяСтрокаУслуг<>Неопределено) Тогда
			// Доходы и расходы на себестоимость не оприходованных партий
			СуммаВсего=?(НайденнаяСтрокаТоваров<>Неопределено,НайденнаяСтрокаТоваров.СуммаВсего,0)+?(НайденнаяСтрокаУслуг<>Неопределено,НайденнаяСтрокаУслуг.СуммаВсего,0);
			
			//<<Павличенко 12.01.18
			СуммаНДС=?(НайденнаяСтрокаТоваров<>Неопределено,НайденнаяСтрокаТоваров.СуммаНДС,0)+?(НайденнаяСтрокаУслуг<>Неопределено,НайденнаяСтрокаУслуг.СуммаНДС,0);
			//>>Павличенко 12.01.18
			
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
			НаборЗаписейДоходыИРасходы.Расход = СуммаВсего; 
			
			//<<Павличенко 12.01.18
			НаборЗаписейДоходыИРасходы.РасходБезНДС = СуммаВсего-СуммаНДС;
			//>>Павличенко 12.01.18
			
			НаборЗаписейДоходыИРасходы.Приход();
		КонецЕсли;
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.ЕстьСтавкаНДС=Истина;
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
		НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
	Иначе
		НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	КонецЕсли;
	НаборЗаписейПартии.ПоБазовомуКоличеству=Истина;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;	
	НаборЗаписейПартии.РежимДопРасходы	= 1;	
	Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
	
	// запишем в ДоходыИРасходы услуги стоимость которых не распределяется на себестоимость поступления
	НайденнаяСтрокаУслуг=ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Услуга,"ВидНоменклатуры");
	Если НайденнаяСтрокаУслуг<>Неопределено Тогда
		СуммаУслуг = 0;    
		
		//<<Павличенко 12.01.18
		СуммаУслугБезНДС = 0;
		//>>Павличенко 12.01.18

		Для Каждого СтрокаУслуги Из НайденнаяСтрокаУслуг.Строки Цикл
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ШапкаДокумента = ШапкаДокумента;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = СтрокаУслуги.СтатьяДопРасходов;
			НаборЗаписейДиР.ВУпрВалюте = (ШапкаДокумента.ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДиР.Расход = СтрокаУслуги.СуммаВсего;
			//<<Павличенко 12.01.18
			НаборЗаписейДиР.РасходБезНДС = СтрокаУслуги.СуммаВсего-СтрокаУслуги.СуммаНДС;
			//>>Павличенко 12.01.18
			СуммаУслуг = СуммаУслуг+СтрокаУслуги.СуммаВсего;   
			//<<Павличенко 12.01.18
			СуммаУслугБезНДС = СуммаУслугБезНДС+СтрокаУслуги.СуммаВсего-СтрокаУслуги.СуммаНДС;
			//>>Павличенко 12.01.18
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЦикла;
		
		// способ ведения не по подразделениям то ничего не будем корректировать
		Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны  И СуммаУслуг<>0 Тогда	
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Ложь;
			НаборЗаписейДиР.Доход = СуммаУслуг;  
			//<<Павличенко 12.01.18
			НаборЗаписейДиР.ДоходБезНДС = СуммаУслугБезНДС;
			//>>Павличенко 12.01.18
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
		КонецЕсли;	
	КонецЕсли;
	
	// при безвозмездном поступлении сумму на доходы и расходы
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда
		НайденнаяСтрокаТоваров=ДеревоДоходовИРасходов.Строки.Найти(Перечисления.ВидыНоменклатуры.Товар,"ВидНоменклатуры");
		СуммаСебестоимости = 0;
		Если НайденнаяСтрокаТоваров<>Неопределено Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение          = ШапкаДокумента.СкладКомпании.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.ВУпрВалюте             = Ложь;
			НаборЗаписейДиР.Доход                  = НайденнаяСтрокаТоваров.СуммаВсего;
			СуммаСебестоимости                     = СуммаСебестоимости+НайденнаяСтрокаТоваров.СуммаВсего;  
			
			//<<Павличенко 12.01.18
			НаборЗаписейДиР.ДоходБезНДС            = НайденнаяСтрокаТоваров.СуммаВсего-НайденнаяСтрокаТоваров.СуммаНДС;
			//>>Павличенко 12.01.18

			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;  		
	КонецЕсли;
	
	// двигаем границу последовательности партий
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры	= Новый Структура();
			КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());			
		КонецЕсли; 
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Для Каждого стрТовары Из Товары Цикл
			Если НЕ обЗначениеНеЗаполнено(стрТовары.Номенклатура) Тогда
				стрТовары.Ячейка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.Номенклатура,СкладКомпании);
			Иначе
				стрТовары.Ячейка = Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла;
		Рез=ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					Рез=ОбработкаРеквизита("УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма) И Рез;
					Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",СтрокаТоваров,ЭтаФорма) И Рез;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ЗначениеЗаполнено(ПодразделениеКомпании) Тогда
			Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании,"ЗакрытиеЗаказовПоПодразделению",ЭтотОбъект) Тогда
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Да;
			Иначе
				ЗакрытиеЗаказовПоПодразделению=Перечисления.ВариантыОтветов.Нет;
			КонецЕсли; 
			
			СпособКонтроляКорректностиДоговораИСклада = обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании, "КонтрольКорректностиДоговораИСкладаДокумента", ЭтотОбъект);
			ЗакрытиеЗаказовПоОрганизации = (НЕ СпособКонтроляКорректностиДоговораИСклада = Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании"
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли (ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия) И (СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
				Предупреждение("Выбранный склад: " + СкладКомпании + " не соответствует хоз. операции!
				|Выберите оптовый склад",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ДокументОснование" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду;
			Если ЭтаФорма<>Неопределено Тогда
				#Если Клиент Тогда
				Попытка
					ЭтаФорма.УправлениеДиалогом();
					дкДействияФормыОперация(ЭтаФорма,Неопределено);
				Исключение
				КонецПопытки; 
				#КонецЕсли
			КонецЕсли; 
		Иначе
			Если ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду Тогда
			//	ХозОперация=Справочники.ХозОперации.ПоступлениеТоваров;
				Если ЭтаФорма<>Неопределено Тогда
					#Если Клиент Тогда
					Попытка
						ЭтаФорма.УправлениеДиалогом();
			//			дкДействияФормыОперация(ЭтаФорма,ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Операция.Кнопки.ПоступлениеТоваров);
					Исключение
					КонецПопытки; 
					#КонецЕсли
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		Возврат Рез;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Если ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций ИЛИ
			 ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
			
			Если ТекСтрока.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
				#Если Клиент Тогда
					Предупреждение("В таблицу нельзя вводить номенклатуру, которая не является услугой",5);
				#КонецЕсли
				ТекСтрока.Номенклатура=Неопределено;
			
			ИначеЕсли ТекСтрока.Номенклатура.СпособРаспределенияДопРасходов <> Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы Тогда
				#Если Клиент Тогда
					Предупреждение("В таблицу нельзя вводить услуги, которые распределяются не на доходы и расходы ",5);
				#КонецЕсли
				ТекСтрока.Номенклатура=Неопределено;
			КонецЕсли;
			
			Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
				ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
			КонецЕсли;
		
        КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("Контрагент",Контрагент);
		КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			Рез = ОбработкаРеквизита("УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма,ДопПараметры);
			Рез = ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				// изменим сумму документа
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры	= Новый Структура();
		КонецЕсли; 
		ДопПараметры.Вставить("ПерерасчетПроцентаНаценки",Ложь);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		
		Рез = Истина;
		Если ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ВалютаРозницы  = обВалютаТипаЦены(ТекСтрока.Номенклатура, СкладКомпании.ТипЦенРозничнойТорговли, Истина);
			СтруктураКурса = обКурсДляВалюты(ВалютаРозницы, Дата);
			КурсРозницы    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			ПерерасчетПроцентаНаценки = Истина;
			Если ДопПараметры <> Неопределено Тогда
				Если НЕ ДопПараметры.Свойство("ПерерасчетПроцентаНаценки", ПерерасчетПроцентаНаценки) Тогда
					ПерерасчетПроцентаНаценки = Истина;
				КонецЕсли; 
			КонецЕсли; 

			Если ПерерасчетПроцентаНаценки Тогда			
				ТекЦенаЗакупки = ?(КурсРозницы = КурсДокумента, ТекСтрока.Цена, обПересчет(ТекСтрока.Цена, ВалютаДокумента, КурсДокумента, ВалютаРозницы, КурсРозницы)); 
				ТекСтрока.ЦенаРозничная = ТекЦенаЗакупки + (ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
				Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
					ТекСтрока.ЦенаРозничная = ТекСтрока.ЦенаРозничная*(1 + (ТекСтрока.СтавкаНДС.Ставка/100));
				КонецЕсли;
			КонецЕсли;
			
			Рез = ОбработкаРеквизита("УстановитьРозничнуюСумму", ТекСтрока, ЭтаФорма, ДопПараметры);
			
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=?(ТекСтрока.КоличествоБазовое=0,0,ТекСтрока.СуммаРозничная/ТекСтрока.КоличествоБазовое);
		КонецЕсли;
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Ячейка" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Возврат Истина;
		КонецЕсли;
		ЯчейкаСсылка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладКомпании);
		ТекСтрока.Ячейка=ЯчейкаСсылка;
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена = 0 Тогда
			ТекСтрока.ПроцентНаценки = ТекСтрока.Номенклатура.ПроцентНаценки;
		ИначеЕсли ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ВалютаРозницы  = обВалютаТипаЦены(ТекСтрока.Номенклатура, СкладКомпании.ТипЦенРозничнойТорговли, Истина);
			СтруктураКурса = обКурсДляВалюты(ВалютаРозницы, Дата);
			КурсРозницы    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ТекЦенаЗакупки = ?(КурсРозницы = КурсДокумента, ТекСтрока.Цена, обПересчет(ТекСтрока.Цена, ВалютаДокумента, КурсДокумента, ВалютаРозницы, КурсРозницы)); 
			ЦенаРозничная  = ТекСтрока.ЦенаРозничная;
 			Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
				ЦенаРозничная = ЦенаРозничная * 100 / (100 + ТекСтрока.СтавкаНДС.Ставка);
			КонецЕсли;
			ТекСтрока.ПроцентНаценки=((ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная = обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
				Если обЗначениеНеЗаполнено(ТекСтрока.ЦенаРозничная) И НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура.ПроцентНаценки) Тогда
					ТекСтрока.ЦенаРозничная = ТекСтрока.Цена + ТекСтрока.Цена*ТекСтрока.Номенклатура.ПроцентНаценки/100;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную цену (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.КоличествоБазовое;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Проверка - а нет ли в документе тех позиций, которые не включены в ассортимент данного подразделения
// в прайс данного склада? Если есть - сообщаем ошибки
//
// Параметры:
//  РежимЗаписи - режим записи документа.
//
// Возвращаемое значение:
//  Булево - результат успешной проверки
//
Функция ПроверитьАссортиментПодразделения(РежимЗаписи) Экспорт
	ФлагСоответствияАссортимента = Истина;
	Если ((РежимЗаписи=РежимЗаписиДокумента.Запись) ИЛИ (РежимЗаписи=РежимЗаписиДокумента.Проведение)) И (обПраво("КонтролироватьАссортиментПодразделения",Права,,ЭтотОбъект)<> Перечисления.ВидыКонтроля.НеКонтролировать) Тогда
		Если (РежимЗаписи = РежимЗаписиДокумента.Проведение) ИЛИ (НЕ обПраво("КонтролироватьЗаполнениеТолькоПриПроведении",Права,,ЭтотОбъект)) тогда
			ТаблицаОтсутствующихВАссортименте=Товары.Выгрузить();
			ТаблицаОтсутствующихВАссортименте.Свернуть("Номенклатура","");
			
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	ОграничениеАссортиментаПодразделенияСрезПоследних.Номенклатура КАК Номенклатура
			|ИЗ
			|	РегистрСведений.ОграничениеАссортиментаПодразделения.СрезПоследних(&Дата,ПодразделениеКомпании=&ПодразделениеКомпании И Номенклатура В (&Номенклатура)) КАК ОграничениеАссортиментаПодразделенияСрезПоследних
			|ГДЕ
			|	ОграничениеАссортиментаПодразделенияСрезПоследних.Состояние=&Введен
			|");
			Запрос.УстановитьПараметр("Дата", Дата);
			Запрос.УстановитьПараметр("Введен", Перечисления.СостоянияТовараВАссортименте.Введен);
			ТекПодразделение=ПодразделениеКомпании;
			Пока НЕ обЗначениеНеЗаполнено(ТекПодразделение) Цикл
				Запрос.УстановитьПараметр("ПодразделениеКомпании",ТекПодразделение);
				Запрос.УстановитьПараметр("Номенклатура",ТаблицаОтсутствующихВАссортименте.ВыгрузитьКолонку("Номенклатура"));
				Результат = Запрос.Выполнить();
				Если НЕ Результат.Пустой() Тогда
					Выборка=Результат.Выбрать();
					Пока Выборка.Следующий() Цикл
						НайденнаяСтрока=ТаблицаОтсутствующихВАссортименте.Найти(Выборка.Номенклатура,"Номенклатура");
						Если НайденнаяСтрока<>Неопределено Тогда
							ТаблицаОтсутствующихВАссортименте.Удалить(НайденнаяСтрока);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				Если ТаблицаОтсутствующихВАссортименте.Количество()=0 Тогда Прервать; КонецЕсли;
				ТекПодразделение=ТекПодразделение.Родитель;
			КонецЦикла;
			
			Если (ТаблицаОтсутствующихВАссортименте.Количество()>0) тогда
				ФлагСоответствияАссортимента = НЕ (обПраво("КонтролироватьАссортиментПодразделения",Права,,ЭтотОбъект)=Перечисления.ВидыКонтроля.Запрещать);
			КонецЕсли;
			ИмяКолонкиКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
			Для Каждого СтрокаТовар Из ТаблицаОтсутствующихВАссортименте Цикл
				Сообщить("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ЭтотОбъект,Истина)+"]" +СокрЛП(СтрокаТовар.Номенклатура)+" Не входит в ассортимент подразделения.",?(ФлагСоответствияАссортимента,СтатусСообщения.Информация,СтатусСообщения.Важное));
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Возврат ФлагСоответствияАссортимента;
КонецФункции	

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ПоступлениеТоваров - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Для контрагентов из ЕАЭС другая форма отчетности
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(Контрагент.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ОсвобождентОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Товары.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПоступлениеТоваровТовары.Количество * ПоступлениеТоваровТовары.Коэффициент) КАК КоличествоПрослеживаемости,
	|	ПоступлениеТоваровТовары.ГТД КАК РНПТ,
	|	ПоступлениеТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ПоступлениеТоваровТовары.СуммаВсего - ПоступлениеТоваровТовары.СуммаНДС) КАК СуммаБезНДС
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	|ГДЕ
	|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
	|	И ПоступлениеТоваровТовары.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровТовары.Номенклатура,
	|	ПоступлениеТоваровТовары.ГТД,
	|	ПоступлениеТоваровТовары.СтавкаНДС";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим, что в документе есть РНПТ
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = Запрос.Выполнить().Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	Если ХозОперация = Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда
		КодОперации = Справочники.КодыОперацийПрослеживаемости.БезвозмездноеПолучение;
	Иначе
		КодОперации = Справочники.КодыОперацийПрослеживаемости.ПоступлениеТоваров;
	КонецЕсли;
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	// Сформируем таблицу
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.КодОперации = КодОперации;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = ВхДокНомер;
		НоваяСтрока.ДатаДокумента = ВхДокДата;
		НоваяСтрока.Контрагент = Контрагент;
		
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(обПересчет(НоваяСтрока.СуммаБезНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,Дата), 2);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

Функция Незаказанные() Экспорт
	
	тзНезаказанных = Новый ТаблицаЗначений; 
	тзНезаказанных.Колонки.Добавить("Артикул", Новый ОписаниеТипов("Строка"));
	тзНезаказанных.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	тзНезаказанных.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	
	ТоварыКопи = Новый ТаблицаЗначений;
	ТоварыКопи.Колонки.Добавить("Номенклатура");   
	ТоварыКопи.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));  
	
	Для Каждого Строка Из Товары Цикл
		НоваяСтрока = ТоварыКопи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
	КонецЦикла;

	Для Каждого Строка Из РаспределениеТовара Цикл
		НоваяСтрока = ТоварыКопи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.Количество = -НоваяСтрока.Количество;
	КонецЦикла;
	
	ТоварыКопи.Свернуть("Номенклатура", "Количество");
	
	//НезаказанныеТовары.Очистить();
	Для Каждого Строка Из ТоварыКопи Цикл
		НоваяСтрока = тзНезаказанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;	
	
	Строки = тзНезаказанных.НайтиСтроки(Новый Структура("Количество", 0)); 
	Для Каждого Строка Из Строки Цикл 
		тзНезаказанных.Удалить(Строка);
	КонецЦикла;	
	
	Возврат тзНезаказанных;
Конецфункции

#Если Клиент Тогда
	
	////////////////////////////////////////////////////////////////////////////////
	// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
	
//Функция печати этикеток и ценников
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
		СтруктураПараметров=Неопределено;
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			СтруктураПараметров=Новый Структура("ИмяРеквизитаЦена,ТипЦен","ЦенаРозничная",СкладКомпании.ТипЦенРозничнойТорговли);
		КонецЕсли;
		дкПечатьЭтикетокИЦенников(ЭтотОбъект,СтруктураПараметров);
	КонецФункции // ПечатьЭтикетокИЦенников()
	
// Формирует табличный документ 
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
Функция ПечатьПоступлениеТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПоступлениеТоваров");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = "Поступление товаров №" + дкПолучитьНомерДляПечати(ЭтотОбъект)
		+ " от " + Формат(Дата, "ДФ = dd.MM.yyyy");
	
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Контрагент);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
	Отпустил.Вставить("ОтпустилПредставление", ?(обЗначениеНеЗаполнено(Отпустил.ОтпустилКонтрагент),"","/ "+ Отпустил.ОтпустилКонтрагентПредставление));
	//Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПоступлениеТоваров()
	
// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРаскладочныйЛист(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РаскладочныйЛист");
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	
	//Настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	ТекстЗаголовка = "Раскладочный лист №" + дкПолучитьНомерДляПечати(ЭтотОбъект)
		+ " от " + Формат(Дата, "ДФ = dd.MM.yyyy");

	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Контрагент);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура();
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		//В возвращаемой структуре нет ключа "Ячейка", создадим его
		СтруктураСтроки.Вставить("Ячейка");
		//Если ЯчейкаХранения не определена для данной номенклатуры, то печатаем пробел
		ЯчейкаДляПечати = СокрЛП(СтрокаТабличнойЧасти.Ячейка.Код);
		СтруктураСтроки.Ячейка 	= ?(обЗначениеНеЗаполнено(ЯчейкаДляПечати)," ",ЯчейкаДляПечати);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура();
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	//Итогов по странице на этой печатной форме нет, но выводим для прорисовки завершающей чёрной линии снизу страницы
	дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
	Отпустил.Вставить("ОтпустилПредставление", ?(обЗначениеНеЗаполнено(Отпустил.ОтпустилКонтрагент),"","/ "+ Отпустил.ОтпустилКонтрагентПредставление));
	//Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРаскладочныйЛист()

// Формирует печатную форму "ТОРГ-12"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
	"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
	"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ЭтотОбъект.Контрагент,СтруктураПредставления1);
	ОбластьМакета.Параметры.ПодразделениеКомпании	= Неопределено;
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикКонтрагент",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.ПодразделениеКомпании);
	
	//bz++
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2,Истина,РасчетныйСчетДокумента);
	
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.ДляПечати = Истина;
	//ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	//ОбластьМакета.Параметры.ГрузополучательПредставление = спПолучитьПредставление(
	//ОбластьМакета.Параметры.Грузополучатель, СтруктураПредставления2, ДополнительныеПараметры);
	//bz--
	
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления2);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Контрагент.КодПоОКПО;
	Если ТипЗнч(ОбластьМакета.Параметры.Грузополучатель)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	КонецЕсли;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	Если ТипЗнч(ОбластьМакета.Параметры.Плательщик)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = "";
	ОбластьМакета.Параметры.Номер			= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект), ЭтотОбъект.ВхДокНомер);
	ОбластьМакета.Параметры.Дата			= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),ЭтотОбъект.Дата, ЭтотОбъект.ВхДокДата);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	//!!! TimR: В новой пф данное поле не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	
	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		
	
	ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ВыборкаСтрок = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	
	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,, ЭтотОбъект);
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.КоличествоБазовое,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.ТоварНаименование 			= СтруктураСтроки.ТоварНаименование + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "");
		
		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС    = Формат(СуммаНДС, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
		
		// Рассчитаем цену.
		ОбластьСтрока.Параметры.ЦенаБезНДС = Формат(СуммаБезНДС / (СтрокаТоваров.Количество * ?(СтрокаТоваров.Коэффициент = 0, 1, СтрокаТоваров.Коэффициент)), ФорматВыводаСуммы);
		
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьВсего);
			//!!! TimR: В новой пф данное поле не выводится
			//мсвДопОбластиПодвала.Добавить(ОбластьВсегоСтавкаНДС);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		Если КоличествоСтрокНаТекущейСтранице=0 Тогда
			ТабДокумент.Вывести(ОбластьСтрока);
		Иначе
			//выводим строку, делая проверку попадания на лист		
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		КонецЕсли; 
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.КоличествоБазовое;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаВсего-СуммаНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			КоличествоСтрокНаТекущейСтранице=0;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Иначе
			КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 1 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = Формат(ВыборкаСтрок.Итог("КоличествоБазовое"), ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	//!!! TimR: В новой пф данное поле не выводится
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "+
	//	дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = Формат(ЭлементСоответствия.Значение,ФорматВыводаСуммы);
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьВсегоСтавкаНДС, , , НомерСтраницы,,ЭтотОбъект);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
	Отпустил.Вставить("ОтпустилПредставление", Отпустил.ОтпустилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьТОРГ12()

// Формирует печатную форму "ТОРГ-1"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ1(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ1");
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);	
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
	"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий",
	"","ИНН ","КПП ","","тел.: "
	);
	СтруктураПредставления2=Новый Структура(
	"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий",
	"","ИНН ","КПП ","","тел.: "
	);
	
	//bz++
	//// Правим, что автоматом не заполнилось, или заполнилось неправильно
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(
	//ОбластьМакета.Параметры.Организация, СтруктураПредставления1, ДополнительныеПараметры);
	//ОбластьМакета.Параметры.КодПоОКПО					= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	//ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	//
	//ОбластьМакета.Параметры.Номер			= дкПолучитьНомерДляПечати(ЭтотОбъект);
	//ОбластьМакета.Параметры.Дата			= ЭтотОбъект.Дата;
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления1);
		ОбластьМакета.Параметры.ОрганизацияПредставление = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления1);
	КонецЕсли;
	ОбластьМакета.Параметры.КодПоОКПО					= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	
	ОбластьМакета.Параметры.Номер			= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект), ЭтотОбъект.ВхДокНомер);
	ОбластьМакета.Параметры.Дата			= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),ЭтотОбъект.Дата, ЭтотОбъект.ВхДокДата);
	//bz--
	
	ОбластьМакета.Параметры.МестоПриемки	= ЭтотОбъект.СкладКомпании;
	
	// Заполним информацию о руководителе организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Введение");
	
	ОбластьМакета.Параметры.Грузоотправитель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузоотправительПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Поставщик				= ЭтотОбъект.Контрагент;
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления1);
	
	Производитель = дкОтветственноеЛицо(ЭтотОбъект,"Производитель");
	ОбластьМакета.Параметры.Заполнить(Производитель);
	
	СтраховаяКомпания = дкОтветственноеЛицо(ЭтотОбъект,"СтраховаяКомпания");
	ОбластьМакета.Параметры.Заполнить(СтраховаяКомпания);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СопроводительныеДокументы"	, обПолучитьЗначениеСвойства(ЭтотОбъект,"СопроводительныеДокументы"));
	Параметры.Вставить("СпособДоставки"				, обПолучитьЗначениеСвойства(ЭтотОбъект,"СпособДоставки"));
	Параметры.Вставить("НомерТранспортногоСредства"	, обПолучитьЗначениеСвойства(ЭтотОбъект,"НомерТранспортногоСредства"));
	Параметры.Вставить("ДатаОтправления"			, обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтправления"));
	ОбластьМакета.Параметры.Заполнить(Параметры);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	//переменные управляющие постраничным выводом
	СтрокНаСтранице = 41;
	СтрокШапки      = 5;
	СтрокПодвала    = 3;
	НомерСтраницы   = 1;	
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	ОбластьСтрокаСертификата= Макет.ПолучитьОбласть("СтрокаСертификата");
	
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	ВыборкаСтрок = ЭтотОбъект.Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаСтрок, ЭтотОбъект, ВалютаРегл);
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;
	
	// Определим имя показываемой колонки кода
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	// инициализация итогов по странице
	ИтогоКоличествоНаСтранице	= 0;
	ИтогоСуммаНаСтранице		= 0;
	ИтогоНДСНаСтранице			= 0;
	ИтогоСНДСНаСтранице			= 0;
	ИтогоМассаБруттоНаСтранице = 0;
	
	// инициализация итогов по документу
	ИтогоКоличество  = 0;
	ИтогоСумма       = 0;
	ИтогоНДС         = 0;
	ИтогоСНДС        = 0;
	Ном              = 0;
	ИтогоМассаБрутто = 0;
	
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		Ном = Ном + 1;
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			ИЛИ ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоги.Параметры.ПоставкаКоличествоПоСтранице  = Формат(ИтогоКоличествоНаСтранице,ФорматВыводаКоличества);
			ОбластьИтоги.Параметры.ПоставкаМассаБруттоПоСтранице = ИтогоМассаБруттоНаСтранице;
			ОбластьИтоги.Параметры.ПоставкаСтоимостьПоСтранице   = Формат(ИтогоСуммаНаСтранице,ФорматВыводаСуммы);
			ОбластьИтоги.Параметры.СуммаНДСПоСтранице            = Формат(ИтогоНДСНаСтранице,ФорматВыводаСуммы);
			ОбластьИтоги.Параметры.СуммаСНДСПоСтранице           = Формат(ИтогоСНДСНаСтранице,ФорматВыводаСуммы);
			ТабДокумент.Вывести(ОбластьИтоги);
			
			// очистим итоги по странице
			ИтогоКоличествоНаСтранице	= 0;
			ИтогоМассаБруттоНаСтранице	= 0;
			ИтогоСуммаНаСтранице		= 0;
			ИтогоНДСНаСтранице			= 0;
			ИтогоСуммаСНДСНаСтранице	= 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.ТоварНаименование             = спПолучитьНаименование(СтрокаТоваров.Номенклатура);
		ОбластьСтрока.Параметры.ТоварКод                      = дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование  = СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.ЕдиницаИзмеренияКодПоОКЕИ     = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код; 
		
		//ОбластьСтрокаТовара.Параметры.ПоставкаКоличествоВОдномМесте = 1;
		ОбластьСтрока.Параметры.ПоставкаКоличествоВОдномМесте	= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.ПоставкаКоличествоМест			= Формат(СтрокаТоваров.КоличествоБазовое,ФорматВыводаКоличества);
		МассаБруттоСтр											= Справочники.Номенклатура.ПолучитьВесНоменклатуры(СтрокаТоваров.Номенклатура, СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения) * СтрокаТоваров.Количество;
		ОбластьСтрока.Параметры.ПоставкаМассаБрутто				= МассаБруттоСтр;
		ОбластьСтрока.Параметры.СуммаСНДС						= Формат(СтрокаТоваров.СуммаВсего,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.ПоставкаСтоимость				= Формат(СтрокаТоваров.Сумма,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.Цена							= Формат(СтрокаТоваров.Цена,ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС						= Формат(СтрокаТоваров.СуммаНДС,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьСтрока);
		
		// увеличим итоги по странице
		ИтогоКоличествоНаСтранице  = ИтогоКоличествоНаСтранице  + СтрокаТоваров.КоличествоБазовое;
		ИтогоМассаБруттоНаСтранице = ИтогоМассаБруттоНаСтранице + МассаБруттоСтр;
		ИтогоСуммаНаСтранице       = ИтогоСуммаНаСтранице       + СтрокаТоваров.Сумма;
		ИтогоНДСНаСтранице         = ИтогоНДСНаСтранице         + СтрокаТоваров.СуммаНДС;
		ИтогоСНДСНаСтранице        = ИтогоСНДСНаСтранице        + СтрокаТоваров.СуммаВсего;
		
		// увеличим итоги по странице
		ИтогоКоличество  = ИтогоКоличество  + СтрокаТоваров.КоличествоБазовое;
		ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБруттоСтр;
		ИтогоСумма       = ИтогоСумма       + СтрокаТоваров.Сумма;
		ИтогоНДС         = ИтогоНДС         + СтрокаТоваров.СуммаНДС;
		ИтогоСНДС        = ИтогоСНДС        + СтрокаТоваров.СуммаВсего;
	КонецЦикла;
	
	// Выводим итоги по последней странице
	ОбластьИтоги.Параметры.ПоставкаКоличествоПоСтранице  = Формат(ИтогоКоличествоНаСтранице,ФорматВыводаКоличества);;
	ОбластьИтоги.Параметры.ПоставкаМассаБруттоПоСтранице = ИтогоМассаБруттоНаСтранице;
	ОбластьИтоги.Параметры.ПоставкаСтоимостьПоСтранице   = Формат(ИтогоСуммаНаСтранице,ФорматВыводаСуммы);
	ОбластьИтоги.Параметры.СуммаНДСПоСтранице            = Формат(ИтогоНДСНаСтранице,ФорматВыводаСуммы);
	ОбластьИтоги.Параметры.СуммаСНДСПоСтранице           = Формат(ИтогоСНДСНаСтранице,ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьИтоги);
	
	// Выводим итоги по всему документу
	ОбластьВсего.Параметры.ПоставкаКоличество  = Формат(ИтогоКоличество,ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ПоставкаМассаБрутто = ИтогоМассаБрутто;
	ОбластьВсего.Параметры.ПоставкаСтоимость   = Формат(ИтогоСумма,ФорматВыводаСуммы);
	ОбластьВсего.Параметры.СуммаНДС            = Формат(ИтогоНДС,ФорматВыводаСуммы);
	ОбластьВсего.Параметры.СуммаСНДС           = Формат(ИтогоСНДС,ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	// Комиссия
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	
	ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	
	ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	
	// Бухгалтер
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	// Кладовщик
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	ОбластьМакета.Параметры.Заполнить(МОЛ);
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьТОРГ1()

// Формирует печатную форму "ТОРГ-2"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
Функция ПечатьТОРГ2(ТабДокумент) Экспорт
		Перем тзВрем; // Временная таблица значений
		Перем ФорматВыводаСуммы; // Строка формата вывода суммы
		Перем ФорматВыводаКоличества; // Строка формата вывода количества
		
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		
		ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы",     Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
		
		// Получим табличную часть за исключением услуг
		тзВрем = Товары.Выгрузить();
		зфПерерасчетТаблицыТоваров(тзВрем, ЭтотОбъект, ВалютаРегл);
		тзВрем.Колонки.Добавить("Брутто", Новый ОписаниеТипов("Число",
		Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
		мсвУдалить = Новый Массив;
		Для каждого ТекСтрока Из тзВрем Цикл
			Если ТекСтрока.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
				мсвУдалить.Добавить(ТекСтрока);
				Продолжить;
			КонецЕсли;
			ТекСтрока.Брутто = ТекСтрока.Количество * Справочники.Номенклатура.ПолучитьВесНоменклатуры(ТекСтрока.Номенклатура, ТекСтрока.ЕдиницаИзмерения);
		КонецЦикла;
		Для каждого ТекСтрока Из мсвУдалить Цикл
			тзВрем.Удалить(ТекСтрока);	   
		КонецЦикла;
		
		Макет = ПолучитьОбщийМакет("ТОРГ2");
		
		ОбластьСтраница1 = Макет.ПолучитьОбласть("ШапкаСтраница1");
		ОбластьСтраница1.Параметры.Заполнить(ЭтотОбъект);
		
		Параметры = Новый Структура;
		
		СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий",
		"","ИНН ","КПП ","","тел.: "
		);
		СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий",
		"","ИНН ","КПП ","","тел.: "
		);
		
		//bz++
		// Правим, что автоматом не заполнилось, или заполнилось неправильно
		//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		//Параметры.Вставить("ОрганизацияПредставление",
		//спПолучитьПредставление(Организация, СтруктураПредставления1, ДополнительныеПараметры));
		//Параметры.Вставить("КодПоОКПО",				 Организация.КодПоОКПО);
		//Параметры.Вставить("ВидДеятельностиПоОКДП",	 Организация.КодПоОКДП);
		//
		//Параметры.Вставить("ПредставлениеДокумента", дкПолучитьПредставление(Ссылка));
		//Параметры.Вставить("Номер", дкПолучитьНомерДляПечати(ЭтотОбъект));
		//Параметры.Вставить("Дата",	 Дата);
		
		// Правим, что автоматом не заполнилось, или заполнилось неправильно
		
		Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
			СтрокаПредставления = спПолучитьПредставление(Организация, СтруктураПредставления1);
			Параметры.Вставить("ОрганизацияПредставление", СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП));
		Иначе	
			Параметры.Вставить("ОрганизацияПредставление", спПолучитьПредставление(Организация, СтруктураПредставления1));
		КонецЕсли;
		Параметры.Вставить("КодПоОКПО",				 Организация.КодПоОКПО);
		Параметры.Вставить("ВидДеятельностиПоОКДП",	 Организация.КодПоОКДП);
		
		Параметры.Вставить("ПредставлениеДокумента", дкПолучитьПредставление(Ссылка));
		Параметры.Вставить("Номер",	 ?(обЗначениеНеЗаполнено(ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект), ВхДокНомер));
		Параметры.Вставить("Дата",	 ?(обЗначениеНеЗаполнено(ВхДокНомер),Дата, ВхДокДата));
		//bz--		 
		
		Параметры.Вставить("МестоПриемки",	                 СкладКомпании);
		Параметры.Вставить("ГрузоотправительПредставление",	 спПолучитьПредставление(обПолучитьЗначениеСвойства(,"Грузоотправитель",Контрагент),СтруктураПредставления2));
		Параметры.Вставить("ПоставщикПредставление",	     спПолучитьПредставление(Контрагент,СтруктураПредставления1));
		Параметры.Вставить("ПроизводительПредставление",	 дкОтветственноеЛицо(ЭтотОбъект, "Производитель").ПроизводительПредставление);
		Параметры.Вставить("СтраховаяКомпанияПредставление", дкОтветственноеЛицо(ЭтотОбъект, "СтраховаяКомпания").СтраховаяКомпанияПредставление);
		Параметры.Вставить("СпособДоставкиПредставление",    обПолучитьЗначениеСвойства(ЭтотОбъект, ПланыВидовХарактеристик.СвойстваОбъектов.СпособДоставки, ""));
		
		ДатаОтправления = обПолучитьЗначениеСвойства(ЭтотОбъект, ПланыВидовХарактеристик.СвойстваОбъектов.ДатаОтправления, "");
		Если ДатаОтправления<>"" Тогда
			ДатаОтправления = "Дата отправления товара: " + Формат(ДатаОтправления, "ДЛФ=DD");
		Иначе
			ДатаОтправления = "Дата отправления товара " + " ""_______""  _____________________ _______ г.";
		КонецЕсли;
		
		Параметры.Вставить("ДатаОтправления", ДатаОтправления);
		
		Параметры.Вставить("НомерДоговора",	СокрЛП(ДоговорВзаиморасчетов.НомерДоговора));
		Параметры.Вставить("ДатаДоговора",  Формат(ДоговорВзаиморасчетов.ДатаНачала, "ДЛФ=DD"));
		
		// Счет-фактура, если есть.
		МассивВидов=Новый Массив(1); МассивВидов[0]="СчетФактураПолученный"; 
		МассивПодчиненных = дкПолучитьМассивПодчиненных(Ссылка, МассивВидов);
		Если МассивПодчиненных.Количество()>0 Тогда
			// нашли
			СФ=МассивПодчиненных[0].Ссылка;
			Попытка
				НомерСФ = дкПолучитьНомерДляПечати(СФ);
				ДатаСФ  = Формат(СФ.Дата, "ДЛФ=DD");
			Исключение
				НомерСФ = "";
				ДатаСФ  = """" + "_______"  + """" + "  __________________________  " + "__________ г.";
			КонецПопытки; 
		Иначе
			НомерСФ = "";
			ДатаСФ  = """" + "_______"  + """" + "  __________________________  " + "__________ г.";
		КонецЕсли; 
		
		Параметры.Вставить("НомерСФ", НомерСФ);
		Параметры.Вставить("ДатаСФ",  ДатаСФ);
		
		ОбластьСтраница1.Параметры.Заполнить(Параметры);
		
		ТабДокумент.Вывести(ОбластьСтраница1);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// ВТОРАЯ СТРАНИЦА
		НомерСтраницы     = 3;
		НомерСтраницыПред = НомерСтраницы;
		
		ОбластьНомерСтроки = Макет.ПолучитьОбласть("НомерСтраницы");
		ОбластьНомерСтроки.Параметры.НомерСтраницы = "Страница: " + 2; 
		ТабДокумент.Вывести(ОбластьНомерСтроки);
		
		ОбластьСтраница2   = Макет.ПолучитьОбласть("ШапкаСтраница2"); 
		ОбластьСтраница2.Параметры.КоличествоМестПоДокументу = Формат(тзВрем.Итог("КоличествоБазовое"), ФорматВыводаКоличества);
		ОбластьСтраница2.Параметры.Брутто                    = Формат(тзВрем.Итог("Брутто"),           ФорматВыводаКоличества);    
		ТабДокумент.Вывести(ОбластьСтраница2);
		
		// ВЫВОДИМ ТАБЛИЧНУЮ ЧАСТЬ
		ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТабличнойЧасти"); 
		ОбластьШапкаТЧ.Параметры.Валюта = ВалютаРегл;
		ТабДокумент.Вывести(ОбластьШапкаТЧ);
		// Для последующего вывода
		ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		
		// получаем макет строки
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка1");
		
		//перебор строк
		ВыборкаТабличнойЧасти = Товары;
		Для каждого СтрокаТабличнойЧасти Из тзВрем Цикл
			
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			
			// Рассчитаем цену.
			ОбластьСтрока.Параметры.Цена = Формат(СтрокаТабличнойЧасти.СуммаВсего / СтрокаТабличнойЧасти.Количество, ФорматВыводаСуммы);
			
			ОбластьСтрока.Параметры.КодЕдиницыПоОКЕИ = "" + СокрЛП(СтруктураСтроки.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код);
			ОбластьСтрока.Параметры.Артикул          = "" + СтруктураСтроки.Номенклатура.Артикул;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТЧ, , НомерСтраницы, ,ЭтотОбъект);  		
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли; 		
		КонецЦикла;
		
		
		// ВЫВОДИМ 3-Ю СТРАНИЦУ
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();	
		ОбластьНомерСтроки = Макет.ПолучитьОбласть("НомерСтраницы");
		ОбластьНомерСтроки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ТабДокумент.Вывести(ОбластьНомерСтроки);
		
		ОбластьСтраница3   = Макет.ПолучитьОбласть("ШапкаСтраница3"); 	
		ТабДокумент.Вывести(ОбластьСтраница3);
		
		НомерСтраницы = НомерСтраницы + 1;
		
		// ВЫВОДИМ ПРОДОЛЖЕНИЕ ТАБЛИЧНОЙ ЧАСТИ
		ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТабличнойЧасти2"); 
		ОбластьШапкаТЧ.Параметры.Валюта = ВалютаРегл;
		ТабДокумент.Вывести(ОбластьШапкаТЧ);
		// Для последующего вывода
		ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		
		// получаем макет строки
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка2");
		
		//перебор строк
		Для каждого СтрокаТабличнойЧасти Из тзВрем Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);		
			ОбластьСтрока.Параметры.Артикул = "" + СтруктураСтроки.Номенклатура.Артикул;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТЧ, , НомерСтраницы, ,ЭтотОбъект);  		
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТЧ.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли; 		
		КонецЦикла;
		
		
		// ВЫВОДИМ 4-Ю СТРАНИЦУ
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ОбластьНомерСтроки = Макет.ПолучитьОбласть("НомерСтраницы");
		ОбластьНомерСтроки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ТабДокумент.Вывести(ОбластьНомерСтроки);
		
		ОбластьСтраница4 = Макет.ПолучитьОбласть("ШапкаСтраница4");
		
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,"ГлавныйБухгалтер");
		ОбластьСтраница4.Параметры.Заполнить(ГлавныйБухгалтер);
		
		// Комиссия
		ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
		ОбластьСтраница4.Параметры.Заполнить(ПредседательКомиссии);
		
		ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
		ОбластьСтраница4.Параметры.Заполнить(ЧленКомиссии1);
		
		ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
		ОбластьСтраница4.Параметры.Заполнить(ЧленКомиссии2);
		
		ТабДокумент.Вывести(ОбластьСтраница4); 
		
		
		// Зададим параметры макета
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 15;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		Возврат ТабДокумент;	
	КонецФункции // ПечатьТОРГ2()
	
// Формирует печатную форму "ТОРГ-4"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
Функция ПечатьТОРГ4(ТабДокумент) Экспорт
		Макет = ПолучитьОбщийМакет("ТОРГ4");
		
		// Получим валюту регламентированного учета для перерасчета цены и суммы
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		
		ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы",     Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		
		СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		
		ОбластьМакета.Параметры.ОрганизацияПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления1);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО 				= ЭтотОбъект.Организация.КодПоОКПО;
		ОбластьМакета.Параметры.ПодразделениеКомпании			= ЭтотОбъект.ПодразделениеКомпании;
		ОбластьМакета.Параметры.ПоставщикПредставление			= спПолучитьПредставление(ЭтотОбъект.Контрагент,СтруктураПредставления1);
		ОбластьМакета.Параметры.Поставщик						= ЭтотОбъект.Контрагент;
		
		ОбластьМакета.Параметры.Грузоотправитель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Контрагент);
		ОбластьМакета.Параметры.ГрузоотправительПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,СтруктураПредставления2);
		
		//bz++
		//ОбластьМакета.Параметры.Номер			= дкПолучитьНомерДляПечати(ЭтотОбъект);
		//ОбластьМакета.Параметры.Дата			= ЭтотОбъект.Дата;
		ОбластьМакета.Параметры.Номер			= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект), ЭтотОбъект.ВхДокНомер);
		ОбластьМакета.Параметры.Дата			= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),ЭтотОбъект.Дата, ЭтотОбъект.ВхДокДата);
		//bz--
		
		ОбластьМакета.Параметры.МестоПриемки	= ЭтотОбъект.СкладКомпании;
		
		// Заполним информацию о руководителе организации
		Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
		ОбластьМакета.Параметры.Заполнить(Руководитель);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		//переменные управляющие постраничным выводом
		СтрокНаСтранице = 23;
		СтрокШапки      = 20;
		СтрокПодвала    = 7;
		НомерСтраницы   = 1;
		
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
		ОбластьИтоги            = Макет.ПолучитьОбласть("ИтогиПоСтранице");
		ОбластьВсего            = Макет.ПолучитьОбласть("Всего");
		
		ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
		ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		ВыборкаСтрок = ЭтотОбъект.Товары.Выгрузить();
		зфПерерасчетТаблицыТоваров(ВыборкаСтрок, ЭтотОбъект, ВалютаРегл);
		
		КоличествоСтрок = ВыборкаСтрок.Количество();
		Если КоличествоСтрок = 1 Тогда
			ПереноситьПоследнююСтроку = 0;
		Иначе
			ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
			ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
			ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
		КонецЕсли;
		
		// Определим имя показываемой колонки кода
		ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
		
		// инициализация итогов по странице
		ИтогоМассаБруттоПоСтранице = 0;
		ИтогоКоличествоПоСтранице  = 0;
		ИтогоСуммаПоСтранице       = 0;
		
		// инициализация итогов по документу
		ИтогоМассаБрутто = 0;
		ИтогоКоличество  = 0;
		ИтогоСумма       = 0;
		Ном              = 0;
		
		// Выводим многострочную часть документа
		Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
			Ном = Ном + 1;
			ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
			
			Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
				ИЛИ ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда
				ОбластьИтоги.Параметры.ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице;
				ОбластьИтоги.Параметры.ИтогоШтукПоСтранице        = Формат(ИтогоКоличествоПоСтранице,ФорматВыводаКоличества);
				ОбластьИтоги.Параметры.ИтогоСтоимостьПоСтранице   = Формат(ИтогоСуммаПоСтранице,ФорматВыводаСуммы);
				ТабДокумент.Вывести(ОбластьИтоги);
				
				// очистим итоги по странице
				ИтогоБруттоПоСтранице     = 0;
				ИтогоКоличествоПоСтранице = 0;
				ИтогоСуммаПоСтранице      = 0;
				
				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
			КонецЕсли;
			
			ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
			Характеристика											= Строка(СтрокаТоваров.ХарактеристикаНоменклатуры);
			ОбластьСтрока.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТоваров.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика) + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "") ;
			ОбластьСтрока.Параметры.ТоварКод						= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
			ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
			ОбластьСтрока.Параметры.ЕдиницаИзмеренияКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			
			МассаБруттоСтр											= Справочники.Номенклатура.ПолучитьВесНоменклатуры(СтрокаТоваров.Номенклатура, СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения) * СтрокаТоваров.Количество;
			ОбластьСтрока.Параметры.МассаБрутто						= МассаБруттоСтр;
			ОбластьСтрока.Параметры.КоличествоМест					= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.КоличествоВОдномМесте			= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.КоличествоШтук					= Формат(СтрокаТоваров.КоличествоБазовое,ФорматВыводаКоличества);
			ОбластьСтрока.Параметры.Цена							= Формат(СтрокаТоваров.Цена,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.Стоимость						= Формат(СтрокаТоваров.Сумма,ФорматВыводаСуммы);
			ТабДокумент.Вывести(ОбластьСтрока);
			
			// увеличим итоги по странице
			ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице + МассаБруттоСтр;
			ИтогоКоличествоПоСтранице  = ИтогоКоличествоПоСтранице  + СтрокаТоваров.КоличествоБазовое;
			ИтогоСуммаПоСтранице       = ИтогоСуммаПоСтранице       + СтрокаТоваров.Сумма;
			
			// увеличим итоги по документу
			ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБруттоСтр;
			ИтогоКоличество  = ИтогоКоличество  + СтрокаТоваров.КоличествоБазовое;
			ИтогоСумма       = ИтогоСумма       + СтрокаТоваров.Сумма;
		КонецЦикла;
		
		// Выводим итоги по последней странице
		ОбластьИтоги.Параметры.ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице;
		ОбластьИтоги.Параметры.ИтогоШтукПоСтранице        = Формат(ИтогоКоличествоПоСтранице,ФорматВыводаКоличества);
		ОбластьИтоги.Параметры.ИтогоСтоимостьПоСтранице   = Формат(ИтогоСуммаПоСтранице,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьИтоги);
		
		// Выводим итоги по документу в целом
		ОбластьВсего.Параметры.ИтогоМассаБрутто = ИтогоМассаБрутто;
		ОбластьВсего.Параметры.ИтогоШтук        = Формат(ИтогоКоличество,ФорматВыводаКоличества);
		ОбластьВсего.Параметры.ИтогоСтоимость   = Формат(ИтогоСумма,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьВсего);
		
		// Выводим подвал документа
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		
		// Комиссия
		ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
		ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
		
		ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
		
		ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
		ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
		
		// Кладовщик
		МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
		ОбластьМакета.Параметры.Заполнить(МОЛ);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Зададим параметры макета
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 15;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьТОРГ4()
	
	// Формирует печатную форму "ТОРГ13"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
	Функция ПечатьТОРГ13(ТабДокумент) Экспорт
		Макет = ПолучитьОбщийМакет("ТОРГ13");
		
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
		
		СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ЭтотОбъект.Организация.КодПоОКПО;
		ОбластьМакета.Параметры.ОтправительПодразделение	= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Контрагент);
		ОбластьМакета.Параметры.ПолучательПодразделение		= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладКомпании);
		
		//bz++
		//ОбластьМакета.Параметры.Номер						= дкПолучитьНомерДляПечати(ЭтотОбъект);
		//ОбластьМакета.Параметры.Дата						= ЭтотОбъект.Дата;
		ОбластьМакета.Параметры.Номер						= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект), ЭтотОбъект.ВхДокНомер);
		ОбластьМакета.Параметры.Дата						= ?(обЗначениеНеЗаполнено(ЭтотОбъект.ВхДокНомер),ЭтотОбъект.Дата, ЭтотОбъект.ВхДокДата);
		//bz++
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		СтрокНаСтранице = 25;
		СтрокШапки      = 12;
		СтрокПодвала    = 5;
		НомерСтраницы   = 1;
		
		// Выводим заголовок таблицы
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
		ЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		ВыборкаСтрокТовары = ЭтотОбъект.Товары.Выгрузить();
		зфПерерасчетТаблицыТоваров(ВыборкаСтрокТовары, ЭтотОбъект, ВалютаРегл);
		
		КоличествоСтрок = ВыборкаСтрокТовары.Количество();
		Если КоличествоСтрок = 1 Тогда
			ПереноситьПоследнююСтроку	= 0;
		Иначе
			ЦелыхСтраницСПодвалом		= Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
			ЦелыхСтраницБезПодвала		= Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
			ПереноситьПоследнююСтроку	= ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
		КонецЕсли;
		
		// Определим имя показываемой колонки кода
		ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
		
		// инициализация итогов по странице
		ИтогКоличествоМестПоСтранице = 0;
		ИтогМассаБруттоПоСтранице    = 0;
		ИтогМассыНеттоПоСтранице     = 0;
		ИтогСуммыПоСтранице          = 0;
		
		// инициализация итогов по документу
		ИтогоКоличество  = 0;
		ИтогоМассаБрутто = 0;
		ИтогоМассаНетто  = 0;
		ИтогоСумма       = 0;
		Ном = 0;
		
		// Выводим многострочную часть документа
		ОбластьИтоговПоСтранице	= Макет.ПолучитьОбласть("ИтогиПоСтранице");
		ОбластьМакета			= Макет.ПолучитьОбласть("Строка");
		
		Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
			Ном = Ном + 1;
			ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;
			
			Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
				ИЛИ ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда
				ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = Формат(ИтогКоличествоМестПоСтранице,ФорматВыводаКоличества);
				ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = Формат(ИтогСуммыПоСтранице,ФорматВыводаСуммы);
				ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
				
				// инициализация итогов по странице
				ИтогКоличествоМестПоСтранице = 0;
				ИтогМассаБруттоПоСтранице    = 0;
				ИтогМассаНеттоПоСтранице     = 0;
				ИтогСуммыПоСтранице          = 0;
				
				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
			Характеристика										= Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
			ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(СтрокаТовар.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
			ОбластьМакета.Параметры.ТоварКод					= дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
			ОбластьМакета.Параметры.ЕдиницаИзмерения			= СтрокаТовар.ЕдиницаИзмерения;
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ	= СтрокаТовар.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			
			ОбластьМакета.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТовар.Коэффициент,ФорматВыводаКоличества);
			ОбластьМакета.Параметры.КоличествоМест				= Формат(СтрокаТовар.Количество,ФорматВыводаКоличества);
			МассаБруттоСтр										= Справочники.Номенклатура.ПолучитьВесНоменклатуры(СтрокаТовар.Номенклатура, СтрокаТовар.Номенклатура.ОсновнаяЕдиницаИзмерения) * СтрокаТовар.Количество;
			ОбластьМакета.Параметры.МассаБрутто					= МассаБруттоСтр;
			Сумма												= СтрокаТовар.Сумма - ?(ЭтотОбъект.ТипЦен.ЦенаВключаетНДС,СтрокаТовар.СуммаНДС,0);
			ОбластьМакета.Параметры.Цена						= Формат(Сумма/(СтрокаТовар.Количество),ФорматВыводаСуммы);
			ОбластьМакета.Параметры.Сумма						= Формат(Сумма,ФорматВыводаСуммы);
			
			ТабДокумент.Вывести(ОбластьМакета);
			
			// Обновим итоги по странице
			ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + СтрокаТовар.Количество;
			ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице    + МассаБруттоСтр;
			ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице     + 0;
			ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице          + Сумма;
			
			// Обновим итоги по документу
			ИтогоКоличество  = ИтогоКоличество  + СтрокаТовар.Количество;
			ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБруттоСтр;
			ИтогоМассаНетто  = ИтогоМассаНетто  + 0;
			ИтогоСумма       = ИтогоСумма       + Сумма;
		КонецЦикла;
		
		ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = Формат(ИтогКоличествоМестПоСтранице,ФорматВыводаКоличества);
		ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
		ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
		ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = Формат(ИтогСуммыПоСтранице,ФорматВыводаКоличества);
		ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
		
		// Выводим итоги по документу в целом
		ОбластьМакета = Макет.ПолучитьОбласть("Всего");
		ОбластьМакета.Параметры.ИтогоКоличествоМест = Формат(ИтогоКоличество,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.ИтогоМассаБрутто    = ИтогоМассаБрутто;
		ОбластьМакета.Параметры.ИтогоМассаНетто     = ИтогоМассаНетто;
		ОбластьМакета.Параметры.ИтогоСумма          = Формат(ИтогоСумма,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Выводим подвал документа
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		// текст без копеек (не Сто 00   , а   просто Сто)
		ПараметрыПрописиНаРусскомОбщ = ВалютаРегл.ПараметрыПрописиНаРусском;
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусскомОбщ, "1", "0");
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "2", "0");
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "3", "0");
		ОбластьМакета.Параметры.ИтогоСуммаПрописью = ЧислоПрописью(Цел(ИтогоСумма), "L=ru_RU; НП=Ложь; НД=Ложь", ПараметрыПрописиНаРусском);
		//ОбластьМакета.Параметры.ИтогоСуммаКоп      = Формат(Цел((ИтогоСумма-Цел(ИтогоСумма))*100), "ЧЦ=2; ЧН=00");
		ОбластьМакета.Параметры.Валюта = ВалютаРегл;
		ОбщаяСумма = ЧислоПрописью(ИтогоСумма, "L=ru_RU; НП=Ложь; НД=Истина", ПараметрыПрописиНаРусскомОбщ);
		ОбластьМакета.Параметры.ИтогоСуммаКоп      = СокрЛП(СтрЗаменить(ОбщаяСумма,ОбластьМакета.Параметры.ИтогоСуммаПрописью,""));
		
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
		Отпустил.Вставить("ОтпустилПредставление", Отпустил.ОтпустилКонтрагентПредставление);
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		
		Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
		ОбластьМакета.Параметры.Заполнить(Получил);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Зададим параметры макета
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 15;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьТОРГ13()
	
// Формирует печатную форму "М-4"
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
Функция ПечатьМ4(ТабДокумент) Экспорт
		Макет = ПолучитьОбщийМакет("М4");
		
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
		
		// выводим шапку
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		
		СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
		);
		
		// Правим, что автоматом не заполнилось, или заполнилось неправильно
		ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО		= Организация.КодПоОКПО;
		ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// выводим заголовок документа
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		ОбластьМакета.Параметры.ДатаСоставления			= Формат(Дата, "ДФ = дд.ММ.гггг");
		ОбластьМакета.Параметры.МестоПриемки			= СкладКомпании;
		ОбластьМакета.Параметры.ПоставщикНаименование	= спПолучитьНаименование(Контрагент);
		ОбластьМакета.Параметры.Поставщик				= Контрагент;
		ОбластьМакета.Параметры.ПоставщикКод			= Контрагент.Код;
		ОбластьМакета.Параметры.НомерСопроводительногоДокумента=ВхДокНомер;
		СтраховаяКомпания = дкОтветственноеЛицо(ЭтотОбъект,"СтраховаяКомпания");
		ОбластьМакета.Параметры.Заполнить(СтраховаяКомпания);
		ТабДокумент.Вывести(ОбластьМакета);
		
		//переменные управляющие постраничным выводом
		СтрокНаСтранице = 23;
		СтрокШапки      = 10;
		СтрокПодвала    = 6;
		НомерСтраницы   = 1;		
		
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
		ОбластьИтогиПоСтранице  = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		ОбластьПодвалСтраницы=Макет.ПолучитьОбласть("ПодвалСтраницы");
		
		// выводим заголовок таблицы
		ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
		ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		ВыборкаСтрок = ЭтотОбъект.Товары;
		
		КоличествоСтрок = ВыборкаСтрок.Количество();
		Если КоличествоСтрок = 1 Тогда
			ПереноситьПоследнююСтроку = 0;
		Иначе
			ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
			ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
			ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
		КонецЕсли;
		
		// Определим имя показываемой колонки кода
		ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
		
		// инициализация итогов по странице
		ИтогоКоличествоПринятоПоСтранице = 0;
		ИтогоСуммаБезНДСПоСтранице       = 0;
		ИтогоСуммаНДСПоСтранице 		 = 0;
		ИтогоВсегоСНДСПоСтранице		 = 0;
		Ном 							 = 0;
		
		// инициализация итогов по документу
		ИтогоКоличествоПринято 	= 0;
		ИтогоСуммаБезНДС       	= 0;
		ИтогоСуммаНДС		   	= 0;
		ИтогоВсегоСНДС			= 0;
		
		// проверим, входит ли НДС в стоимость	
		ЦенаВключаетНДС = ТипЦен.ЦенаВключаетНДС;
		
		// Выводим многострочную часть документа
		Для каждого СтрокаТабличнойЧасти Из ВыборкаСтрок Цикл
			Ном = Ном + 1;
			ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
			
			Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
				ИЛИ ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда
				ОбластьИтогиПоСтранице.Параметры.ИтогоКоличествоПринято = Формат(ИтогоКоличествоПринятоПоСтранице,ФорматВыводаКоличества);
				ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаБезНДС = Формат(ИтогоСуммаБезНДСПоСтранице,ФорматВыводаСуммы);
				ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаНДС = Формат(ИтогоСуммаНДСПоСтранице,ФорматВыводаСуммы);
				ОбластьИтогиПоСтранице.Параметры.ИтогоВсегоСНДС = Формат(ИтогоВсегоСНДСПоСтранице,ФорматВыводаСуммы);
				ТабДокумент.Вывести(ОбластьИтогиПоСтранице);
				ТабДокумент.Вывести(ОбластьПодвалСтраницы);
				
				// очистим итоги по странице
				ИтогоКоличествоПринятоПоСтранице = 0;
				ИтогоСуммаБезНДСПоСтранице       = 0;
				ИтогоСуммаНДСПоСтранице 		 = 0;
				ИтогоВсегоСНДСПоСтранице		 = 0;
				
				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
			КонецЕсли;
			
			ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
			Характеристика											= Строка(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
			ОбластьСтрока.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
			ОбластьСтрока.Параметры.ТоварКод						= дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
			ОбластьСтрока.Параметры.ЕдиницаИзмеренияКод				= СтрокаТабличнойЧасти.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			ОбластьСтрока.Параметры.ЕдиницаИзмеренияНаименование	= СтрокаТабличнойЧасти.ЕдиницаИзмерения;
			ОбластьСтрока.Параметры.КоличествоПринято				= Формат(СтрокаТабличнойЧасти.Количество,ФорматВыводаКоличества);
			Если ЦенаВключаетНДС Тогда
				ОбластьСтрока.Параметры.Цена		= Формат(СтрокаТабличнойЧасти.Цена - СтрокаТабличнойЧасти.СуммаНДС/СтрокаТабличнойЧасти.Количество,ФорматВыводаСуммы);
				ОбластьСтрока.Параметры.СуммаБезНДС	= Формат(СтрокаТабличнойЧасти.Сумма - ОбластьСтрока.Параметры.СуммаНДС,ФорматВыводаСуммы);
				ИтогоСуммаБезНДСПоСтранице			= ИтогоСуммаБезНДСПоСтранице + СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаНДС;
				ИтогоСуммаБезНДС					= ИтогоСуммаБезНДС + СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаНДС;
			Иначе
				ОбластьСтрока.Параметры.СуммаБезНДС	= Формат(СтрокаТабличнойЧасти.Сумма,ФорматВыводаСуммы);
				ОбластьСтрока.Параметры.Цена 		= Формат(СтрокаТабличнойЧасти.Цена,ФорматВыводаСуммы);
				ИтогоСуммаБезНДСПоСтранице			= ИтогоСуммаБезНДСПоСтранице + СтрокаТабличнойЧасти.Сумма;
				ИтогоСуммаБезНДС					= ИтогоСуммаБезНДС + СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;		
			ОбластьСтрока.Параметры.ВсегоСНДС		= Формат(СтрокаТабличнойЧасти.СуммаВсего,ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СуммаНДС		= Формат(СтрокаТабличнойЧасти.СуммаНДС,ФорматВыводаСуммы);
			ТабДокумент.Вывести(ОбластьСтрока);
			
			// увеличим итоги по странице
			ИтогоКоличествоПринятоПоСтранице = ИтогоКоличествоПринятоПоСтранице + СтрокаТабличнойЧасти.Количество;
			ИтогоСуммаНДСПоСтранице			 = ИтогоСуммаНДСПоСтранице + СтрокаТабличнойЧасти.СуммаНДС;
			ИтогоВсегоСНДСПоСтранице		 = ИтогоВсегоСНДСПоСтранице + СтрокаТабличнойЧасти.СуммаВсего;
			
			// увеличим итоги по документу
			ИтогоКоличествоПринято	= ИтогоКоличествоПринято + СтрокаТабличнойЧасти.Количество;
			ИтогоСуммаНДС			= ИтогоСуммаНДС + СтрокаТабличнойЧасти.СуммаНДС;
			ИтогоВсегоСНДС			= ИтогоВсегоСНДС + СтрокаТабличнойЧасти.СуммаВсего;
			
		КонецЦикла;
		
		// выводим итоги по последней странице
		ОбластьИтогиПоСтранице.Параметры.ИтогоКоличествоПринято = Формат(ИтогоКоличествоПринятоПоСтранице,ФорматВыводаКоличества);
		ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаБезНДС = Формат(ИтогоСуммаБезНДСПоСтранице,ФорматВыводаСуммы);
		ОбластьИтогиПоСтранице.Параметры.ИтогоСуммаНДС = Формат(ИтогоСуммаНДСПоСтранице,ФорматВыводаСуммы);
		ОбластьИтогиПоСтранице.Параметры.ИтогоВсегоСНДС = Формат(ИтогоВсегоСНДСПоСтранице,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьИтогиПоСтранице);
		
		// Выводим итоги по документу
		ОбластьМакетаИтого = Макет.ПолучитьОбласть("Итого");
		ОбластьМакетаИтого.Параметры.ИтогоКоличествоПринято = Формат(ИтогоКоличествоПринято,ФорматВыводаКоличества);
		ОбластьМакетаИтого.Параметры.ИтогоСуммаБезНДС = Формат(ИтогоСуммаБезНДС,ФорматВыводаСуммы);
		ОбластьМакетаИтого.Параметры.ИтогоСуммаНДС = Формат(ИтогоСуммаНДС,ФорматВыводаСуммы);
		ОбластьМакетаИтого.Параметры.ИтогоВсегоСНДС = Формат(ИтогоВсегоСНДС,ФорматВыводаСуммы);
		ТабДокумент.Вывести(ОбластьМакетаИтого);
		
		// выводим подвал
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
		Отпустил.Вставить("ОтпустилПредставление", Отпустил.ОтпустилКонтрагентПредставление);
		ОбластьМакета.Параметры.Заполнить(Отпустил);
		
		Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
		ОбластьМакета.Параметры.Заполнить(Получил);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Зададим параметры макета
		
		ТабДокумент.ПолеСверху = 0;
		ТабДокумент.ПолеСлева  = 0;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.АвтоМасштаб=Истина;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьМ4()
	
// Формирует печатную форму "УПД"
// Возвращает сформированный табличный документ:
Функция ПечатьУПД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУПД(ЭтотОбъект,ТабДокумент);
	
	Возврат ТабДокумент;
	
КонецФункции //ПечатьУПД()	
	
// Функция осуществляет печать произвольного документа.
	// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
	//
	// Параметры:
	//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
	//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
	//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
	//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
	//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
	//
	// Возвращаемое значение:
	//   ТабличныйДокумент	- Сформированная печатная форма
	//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
		Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
	КонецФункции // Печать()

//Формирование отчета по распределению заказов покупателей по заказам поставщикам.
Процедура СформироватьОтчетПоРаспределению() Экспорт
	
	ДокСсылка = ЭтотОбъект.Ссылка;
	
	Если обЗначениеНеЗаполнено(ДокСсылка) Тогда
		Сообщить("Для формирования отчета документ должен быть записан!",СтатусСообщения.Информация);
		Возврат;
	КонецЕсли;
	
	Отчет = Отчеты.РаспределениеПоЗаказамПокупателей.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	//ВариантОтчета     = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьВариантОтчета(МетаданныеОтчета.ПолноеИмя(), "Основной");
	//НастройкиВарианта = ВариантОтчета.Настройки.Получить();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.Основной;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",    НачалоДня(ДокСсылка.Дата));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ДокСсылка.Дата));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("Документ",      ДокСсылка);
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ФормаНастроек = ПолучитьФорму(ИмяФормыОтчета, СтруктураПараметров);
	ФормаНастроек.Открыть();
	
КонецПроцедуры

#КонецЕсли

// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		 ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду;
		 Контрагент = обПраво("ОсновнойПоставщик",Права,,ЭтотОбъект);
		 //Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		 //	ЭтотОбъект.ОбработкаРеквизита("Контрагент",);
		 //КонецЕсли;
	КонецЕсли;
	
	Рез=дкОбработкаЗаполнения(ЭтотОбъект, Основание,,ТипЗнч(Основание)<>Тип("ДокументСсылка.КорректировкаЗаказаПоставщику"));
	
	Если ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций ИЛИ
		 ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
		СкладКомпании	= Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	ЗакрыватьНераспределенныеЗаказыПокупателей = обПраво("ЗакрыватьНераспределенныеЗаказыПокупателей",Права,,ЭтотОбъект);
	Если НЕ Рез Тогда Возврат; КонецЕсли;
	Если НЕ Основание = Неопределено Тогда
		Если Основание.ХозОперация = Справочники.ХозОперации.ПриходныйСкладскойОрдер Тогда
			Если НЕ обЗначениеНеЗаполнено(Основание.ВладелецТовара) Тогда
				Если ТипЗнч(Основание.ВладелецТовара) = Тип("СправочникСсылка.Контрагенты") Тогда
					Контрагент = Основание.ВладелецТовара;
					ОбработкаРеквизита("Контрагент");
				КонецЕсли;
			КонецЕсли;
			Для Каждого СтрТовар Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура", СтрТовар);
			КонецЦикла;
			// заполним ячейки
			Попытка
				Для Каждого стрТовар Из Товары Цикл
					Если стрТовар.Номенклатура = Основание.Товары[стрТовар.НомерСтроки-1].Номенклатура Тогда
						стрТовар.Ячейка = Основание.Товары[стрТовар.НомерСтроки-1].Ячейка;
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			ВхДокНомер = Основание.Номер;
			
		// необходимо учесть корректировку заказа, если таковая имеется
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			
			ТекстЗапроса="ВЫБРАТЬ
			|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
			|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК ЗаказаноОстаток,
			|	ЗаказыПоставщикамОстатки.СуммаОстаток КАК Сумма,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.Количество * ЗаказПоставщикуТовары.Коэффициент, 1) КАК КоличествоБазовое,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.ЕдиницаИзмерения, Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.Коэффициент, 1) КАК Коэффициент,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.СтавкаНДС, ЗаказыПоставщикамОстатки.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
			|	ЕСТЬNULL(ЗаказПоставщикуТовары.НомерСтроки, 1000000) КАК НомерСтроки
			|ИЗ
			|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
			|				&Момент,
			|				ЗаказПоставщику = &ВыбЗаказПоставщику
			|					И Контрагент = &ВыбКонтрагент) КАК ЗаказыПоставщикамОстатки
			|ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
			|		ПО ЗаказыПоставщикамОстатки.ЗаказПоставщику = ЗаказПоставщикуТовары.Ссылка
			|		 И ЗаказыПоставщикамОстатки.Номенклатура = ЗаказПоставщикуТовары.Номенклатура
			|		 И ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки
			|ИТОГИ
			|	СУММА(КоличествоБазовое),
			|	МАКСИМУМ(ЗаказаноОстаток),
			|	МАКСИМУМ(Сумма)
			|ПО
			|	Номенклатура,
			|	ХарактеристикаНоменклатуры";
			
			Запрос=Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Момент", Новый МоментВремени(КонецДня(Дата)));
			Запрос.УстановитьПараметр("ВыбКонтрагент", Контрагент);
			Запрос.УстановитьПараметр("ВыбЗаказПоставщику", ДокументОснование);
			Товары.Очистить(); // нужные только скорректированные позиции
			
			ВалютаЗаказа	= Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			СтруктураКурса	= обКурсДляВалюты(ВалютаЗаказа,Дата);
			КурсЗаказа		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатуры.Следующий() Цикл
				ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаХарактеристик.Следующий() Цикл
					ВсегоОсталось = ВыборкаХарактеристик.ЗаказаноОстаток;
					КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
					СуммаОсталось = обПересчет(ВыборкаХарактеристик.Сумма, ВалютаЗаказа, КурсЗаказа, ВалютаДокумента, КурсДокумента);
					ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
					НоваяСтрока	  = Неопределено;
					Пока ВыборкаДетали.Следующий() Цикл
						Если ВсегоОсталось=0 Тогда
							Прервать;
						КонецЕсли;
						Если ВыборкаДетали.ЗаказаноОстаток = 0 Тогда
							Продолжить;
						КонецЕсли;
						Если КоличествоБазовоеПоЗаказу = 1 Тогда
							КоличествоСтроки = ВыборкаДетали.ЗаказаноОстаток;
						Иначе
							КоличествоСтроки = ВыборкаДетали.ЗаказаноОстаток*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
						КонецЕсли;
						ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
						НоваяСтрока = Товары.Добавить();
						НоваяСтрока.Номенклатура               = ВыборкаДетали.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаДетали.ХарактеристикаНоменклатуры;
						НоваяСтрока.ЕдиницаИзмерения           = ВыборкаДетали.ЕдиницаИзмерения;
						НоваяСтрока.Коэффициент                = ВыборкаДетали.Коэффициент;
						ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
						НоваяСтрока.СтавкаНДС                  = ВыборкаДетали.СтавкаНДС;
						НоваяСтрока.Количество                 = ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);;
						НоваяСтрока.КоличествоБазовое          = ТекущееКоличество;
						
						ТекСумма = СуммаОсталось/ВсегоОсталось*ТекущееКоличество;
						
						СуммаОсталось = СуммаОсталось - ТекСумма;
						НоваяСтрока.СуммаВсего = ТекСумма;
						ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
						
						ВсегоОсталось = ВсегоОсталось - ТекущееКоличество;
					КонецЦикла;
					//Если НЕ НоваяСтрока = Неопределено Тогда
					//	НоваяСтрока.Сумма = НоваяСтрока.Сумма + СуммаОсталось;
					//КонецЕсли;
					Если ВсегоОсталось>0 ИЛИ СуммаОсталось>0 Тогда
						Если НЕ НоваяСтрока = Неопределено Тогда
							НоваяСтрока.Количество		  = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
							НоваяСтрока.КоличествоБазовое = НоваяСтрока.КоличествоБазовое + ВсегоОсталось;
							ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
							НоваяСтрока.СуммаВсего        = НоваяСтрока.СуммаВсего + СуммаОсталось;
						ИначеЕсли ВсегоОсталось>0 Тогда
							НоваяСтрока=Товары.Добавить();
							НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
							НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
							ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
							НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
							НоваяСтрока.Количество		  = ВсегоОсталось/НоваяСтрока.Коэффициент;
							НоваяСтрока.КоличествоБазовое = ВсегоОсталось;
							ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
							НоваяСтрока.СуммаВсего        = СуммаОсталось;
						КонецЕсли;
						ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		// если это ввод на основании реализации и контроль баланса на уровне организации 
		// то презаполним организацию из параметров сеанса	
		ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.РеализацияТоваров Тогда	
			//Чтение значения для определения способа ведения баланса
			СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
			// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
			Если  СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда	
				Организация = ПараметрыСеанса.Организация;
				ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
				СкладКомпании = обПраво("ОсновнойСкладКомпании",Права,,ЭтотОбъект);
				Контрагент    = обПраво("ОсновнойПоставщик",Права,,ЭтотОбъект);
				ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, Перечисления.ВидыДоговоров.Покупка, ЭтотОбъект,, Истина, Ложь);
				ОбработкаРеквизита("ДоговорВзаиморасчетов",);
			КонецЕсли;
			
		ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.КорректировкаЗаказаПоставщику Тогда		
			ДоговорВзаиморасчетов = Основание.ДокументОснование.ДоговорВзаиморасчетов;
			ТекстЗапроса = "ВЫБРАТЬ
			|	ЗаказыПоставщикамОстатки.Номенклатура,
			|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры,
			|	СУММА(ЗаказыПоставщикамОстатки.ЗаказаноОстаток) КАК ЗаказаноОстаток
			|ИЗ
			|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
			|		&Момент,
			|		ЗаказПоставщику = &ВыбЗаказПоставщику
			|			И Контрагент = &ВыбКонтрагент) КАК ЗаказыПоставщикамОстатки
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗаказыПоставщикамОстатки.Номенклатура,
			|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры";
			Запрос=Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Момент", Новый МоментВремени(КонецДня(Дата)));
			Запрос.УстановитьПараметр("ВыбКонтрагент", Контрагент);
			Запрос.УстановитьПараметр("ВыбЗаказПоставщику", Основание.ДокументОснование);
			
			Выборка = Запрос.Выполнить().Выбрать();
			ВсегоСтрок = Выборка.Количество();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока=Товары.Добавить();
				НоваяСтрока.Номенклатура=Выборка.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				НоваяСтрока.Количество=Выборка.ЗаказаноОстаток;
				ОбработкаРеквизита("Товары.Количество",НоваяСтрока);
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду;
			//Если Товары.Количество()>0 Тогда
			//	Товары.Очистить();
			//КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			//	Контрагент=Справочники.Контрагенты.ПустаяСсылка();
				ОбработкаРеквизита("Контрагент");
			КонецЕсли;
			
#Область БИЗТЕХ
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.тт_РабочийЛистТрейдИн") Тогда
			
			Для каждого тс Из Основание.ТЧКолеса Цикл
				
				Если тс.Тип = Перечисления.БТ_ТипКолеса.Шины Тогда
					Наименование = "Б/у"
					+"_Шина"
					+"_"+Сокрлп(тс.ПрозводительШин)
					+"_"+Сокрлп(тс.МодельШины)
					+"_"+Сокрлп(тс.Ширина)+"/"+Сокрлп(тс.Высота)+"/"+Сокрлп(тс.Диаметр)
					+"_"+Сокрлп(тс.Сезонность)
					+?(тс.ОстатокПротектора = 0,"","_" + Сокрлп(тс.ОстатокПротектора))
					+?(тс.ДатаПроизводства = "","","_" + Формат(тс.ДатаПроизводства));
				ИначеЕсли тс.Тип = Перечисления.БТ_ТипКолеса.КолесаВСборе Тогда
					Наименование = "Б/у"
					+"_Колесо в сборе"
					+"_"+Сокрлп(тс.ПрозводительШин)
					+"_"+Сокрлп(тс.МодельШины)
					+"_"+Сокрлп(тс.Ширина)+"/"+Сокрлп(тс.Высота)+"/"+Сокрлп(тс.Диаметр)
					+"_"+Сокрлп(тс.Сезонность)
					+?(тс.ОстатокПротектора = 0,"","_" + Сокрлп(тс.ОстатокПротектора))
					+?(тс.ДатаПроизводства = "","","_"+ Формат(тс.ДатаПроизводства))
					+"_"+Сокрлп(тс.ТипДиска);   
				КонецЕсли;
				
				Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(Наименование,Истина);
				
				Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
					
					Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
					Номенклатура.Наименование = Наименование; 
					Номенклатура.НаименованиеПолное = Наименование; 
					Номенклатура.НаименованиеИностранное = Наименование; 
					Номенклатура.ТипНоменклатуры = Справочники.ТипыНоменклатуры.Шины;
					Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Шины; 
					Номенклатура.СтавкаНДС = Справочники.СтавкиНДС.БезНДС; 
					Номенклатура.ВалютаУчета = Справочники.Валюты.НайтиПоНаименованию("Руб");  
					Номенклатура.БазоваяЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("шт");
					Номенклатура.Родитель = Справочники.Номенклатура.НайтиПоНаименованию("Шины/колеса БУ");
					//БизТех 15.07.2025г. ТЗ от Тихонова <<
					Если НЕ ЗначениеЗаполнено(Номенклатура.Родитель) Тогда
						Номенклатура.Родитель = Справочники.Номенклатура.НайтиПоКоду("ЦБ032163");; 
					КонецЕсли;	
					//БизТех 15.07.2025г. >>

					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	Номенклатура.Артикул КАК Артикул
					|ИЗ
					|	Справочник.Номенклатура КАК Номенклатура
					|ГДЕ
					|	Номенклатура.Артикул ПОДОБНО ""ШИ0%""
					|
					|УПОРЯДОЧИТЬ ПО
					|	Артикул УБЫВ";
					РезультатЗапроса = Запрос.Выполнить();
					ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
					Если РезультатЗапроса.Пустой() Тогда
						Номенклатура.Артикул = "ШИ"+Прав("00000001",6);
					Иначе
						Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
							ПоследнийНомер = сфпСтроковыеФункцииКлиентСервер.СтрокаВЧисло(стрЗаменить(ВыборкаДетальныеЗаписи.Артикул,"ШИ0",""));
							ПоследнийНомер = ПоследнийНомер + 1; 
							Номенклатура.Артикул = "ШИ"+Прав("0000000"+Формат(ПоследнийНомер,"ЧГ="),6);
						КонецЦикла;
					КонецЕсли;
								
					Номенклатура.Записать();
					
					новЕдИзм = Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
					новЕдИзм.Наименование = "шт";
					новЕдИзм.Владелец = Номенклатура.Ссылка;
					новЕдИзм.Коэффициент = 1;
					новЕдИзм.ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("шт");
					новЕдИзм.Записать();
					Номенклатура.ОсновнаяЕдиницаИзмерения = новЕдИзм.Ссылка;
					Номенклатура.Записать();
					
				КонецЕсли;
				
				нс =  Товары.Добавить();
				нс.Номенклатура = Номенклатура.Ссылка; 
				нс.ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения; 	
				нс.Количество = 1; 
				нс.Коэффициент = 1; 
				нс.КоличествоБазовое = 1; 
				нс.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	БТ_ЦенаЗаКолесоСрезПоследних.Цена КАК Цена
				|ИЗ
				|	РегистрСведений.БТ_ЦенаЗаКолесо.СрезПоследних КАК БТ_ЦенаЗаКолесоСрезПоследних";
				РезультатЗапроса = Запрос.Выполнить();
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					нс.Цена = ВыборкаДетальныеЗаписи.Цена
				КонецЦикла;
				нс.Сумма = нс.Цена * нс.Количество;
				нс.СуммаВсего = нс.Сумма;
			КонецЦикла; 
			
			тз = Товары.Выгрузить();
			тз.Свернуть("Номенклатура,ЕдиницаИзмерения,СтавкаНДС,Цена","Количество,Коэффициент,КоличествоБазовое,Сумма,СуммаВсего"); 
			
			Товары.Загрузить(тз);
			
			СкладКомпании = Справочники.СкладыКомпании.НайтиПоНаименованию("Шины Б/У Транзит");
			
			Если ЗначениеЗаполнено(Основание.ДокументПоступленияНаСклад.Контрагент) Тогда
				Контрагент = Основание.ДокументПоступленияНаСклад.Контрагент; 
				ДоговорВзаиморасчетов =Основание.ДокументПоступленияНаСклад.ДоговорВзаиморасчетов; 
			КонецЕсли;
			
			#КонецОбласти
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПодразделениеКомпании) Тогда
		Если обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании, "ЗакрытиеЗаказовПоПодразделению", ЭтотОбъект) Тогда
			ЗакрытиеЗаказовПоПодразделению = Перечисления.ВариантыОтветов.Да;
		Иначе
			ЗакрытиеЗаказовПоПодразделению = Перечисления.ВариантыОтветов.Нет;
		КонецЕсли;
		СпособКонтроляКорректностиДоговораИСклада = обПолучитьПраваИНастройкиПользователя(ПодразделениеКомпании, "КонтрольКорректностиДоговораИСкладаДокумента", ЭтотОбъект);
		ЗакрытиеЗаказовПоОрганизации = (НЕ СпособКонтроляКорректностиДоговораИСклада = Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
	КонецЕсли; 
	
	Если ЭтоНовый() Тогда
		Если Товары.Количество()>0 И Основание = Неопределено Тогда
			Для Каждого СтрокаТовара Из Товары Цикл
				ОбработкаРеквизита("Товары.Номенклатура",СтрокаТовара,ЭтотОбъект);
			КонецЦикла;
		Иначе
			Для Каждого СтрокаТовара Из Товары Цикл
				Если ПустаяСтрока(СтрокаТовара.ИдентификаторТовара) Тогда
					СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;  
		//<<чАЛАПаю БизТех 18.09.2024
		БТ_НомерСЧФ = "";
		БТ_ДатаСЧФ = Дата(1,1,1); 
		БТ_СтатусСЧФ = "";
		БТ_СуммаНДС = 0;           
		//чАЛАПаю БизТех 18.09.2024>>
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
		Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
			СтратегияСписанияПартийПоДатам = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
		Иначе
			СтратегияСписанияПартийПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
		КонецЕсли;
		
		Если СтратегияСписанияПартийПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
			Сообщить("При стратегии списания партий по среднему нельзя приходывать комиссионный товар.",СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	//БизТех 25.02.2025г. закомментировано <<
	// Почистим распределения товаров по заказам
	//СтрокиУдаления = Новый Массив;
	//СтруктураОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	//Для Каждого СтрокаРаспределения Из РаспределениеТовара Цикл
	//	СтруктураОтбора.Номенклатура               = СтрокаРаспределения.Номенклатура;
	//	СтруктураОтбора.ХарактеристикаНоменклатуры = СтрокаРаспределения.ХарактеристикаНоменклатуры;
	//	НайденныеСтроки = Товары.НайтиСтроки(СтруктураОтбора);
	//	УдалитьРаспределение = Истина;
	//	Для Каждого ТекСтрока Из НайденныеСтроки Цикл
	//		Если ТекСтрока.СпособРаспределения = 2 И (НЕ ЗначениеЗаполнено(СтрокаРаспределения.ЗаказПокупателя)) Тогда
	//			Продолжить;
	//		ИначеЕсли ТекСтрока.СпособРаспределения>0 Тогда
	//			УдалитьРаспределение = Ложь;
	//			Прервать;
	//		КонецЕсли;
	//	КонецЦикла;
	//	Если УдалитьРаспределение Тогда 
	//		СтрокиУдаления.Добавить(СтрокаРаспределения);
	//	КонецЕсли;
	//КонецЦикла;
	//
	//Для Каждого ТекСтрока Из СтрокиУдаления Цикл
	//	РаспределениеТовара.Удалить(ТекСтрока);
	//КонецЦикла;
	//БизТех 25.02.2025г. закомментировано>>

	
	//Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
	//	ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду;
	//Иначе
	//КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций ИЛИ
		 ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
		СкладКомпании	= Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если ХозОперация<>Справочники.ХозОперации.ПоступлениеТоваровСреднееОтрицательное Тогда
		Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
		Если обЗначениеНеЗаполнено(СкладКомпании) ИЛИ НЕ СкладКомпании.Розничный Тогда
			Для каждого ТекСтрокаТЧ Из Товары Цикл  	
				ТекСтрокаТЧ.ПроцентНаценки = 0;
				ТекСтрокаТЧ.ЦенаРозничная  = 0;
				ТекСтрокаТЧ.СуммаРозничная = 0;
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Если ХозОперация <> Справочники.ХозОперации.УслугиПоСубподряду ИЛИ
			 (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")) Тогда
			 Для каждого СтрокаТоваров Из Товары Цикл
				Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.ЗаказНаряд) Тогда
					СтрокаТоваров.ЗаказНаряд=Документы.ЗаказНаряд.ПустаяСсылка();
				КонецЕсли; 
			 КонецЦикла; 
		КонецЕсли;
    КонецЕсли;
	
	//обработаем пометку удаления, если это отрицательная партия (константа)
	Если ПометкаУдаления И (НЕ Ссылка.Пустая()) И Ссылка = Константы.ПартияТоваровОтрицательныхОстатков.Получить() Тогда
		#Если Клиент Тогда
			Сообщить("Невозможно пометить на удаление документ партий отрицательных остатков!");
		#КонецЕсли
		Отказ = Истина;
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
	КонецЕсли;
	
	Если ЭтоНовый() тогда
		Отказ = Отказ ИЛИ (НЕ ПроверитьАссортиментПодразделения(РежимЗаписи));
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателей.Заказ КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
		|	ГДЕ
		|		ЗаказыПокупателей.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыПоставщикам.ЗаказПоставщику
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|	ГДЕ
		|		ЗаказыПоставщикам.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПоставщика
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНовый()и не отказ тогда 
		Если Товары.найти(Справочники.СтавкиНДС.ОсновнаяСтавкаНДС,"СтавкаНДС")= Неопределено тогда    //без ндс
			БТ_СтатусСЧФ = Строка("Не требуется"); 
			БТ_НомерСЧФ = "";     
			БТ_ДатаСЧФ = Дата(1,1,1);
		иначе
			БТ_СтатусСЧФ= Строка("Нет счет-фактуры"); 
			БТ_НомерСЧФ = "";     
			БТ_ДатаСЧФ = Дата(1,1,1);
		КонецЕсли;
		БТ_СуммаНДС = Строка(Товары.Итог("СуммаНДС"));  
	КонецЕсли;
	
	//БИЗТЕХ КОВАЛЬ 08.10.2024 ++
	//Заполнение табличной части если есть документ основание
	//решение проблемы с выгрузкой субподрядов в БухБазу если они созданы на основании
	Если ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду И Значениезаполнено(документоснование) И ТипЗнч(документоснование) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		Для Каждого Товар из Товары Цикл
			Если НЕ ЗначениеЗаполнено(Товар.ЗаказНаряд) Тогда
				Товар.ЗаказНаряд = документоснование.Ссылка;
			КонецЕсли;
		КонецЦикла;
		//ДокументОснование = Неопределено;
	КонецЕсли;
	//БИЗТЕХ КОВАЛЬ 08.10.2024 -- 
	
	// БИЗТЕХ КОВАЛЬ 24.11.2025 ++
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	// БИЗТЕХ КОВАЛЬ 24.11.2025 --
	
	//++СнимченкоАА_бизтех++
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли; 
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Отказ = Ложь Тогда
		Если Константы.ПартияТоваровОтрицательныхОстатков.Получить() = Ссылка Тогда
			#Если Клиент Тогда
				Сообщить("Документ является документом партии отрицательных остатков и не может быть удалён");
			#КонецЕсли
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
	Отказ = Отказ ИЛИ дкЕстьВведенныеДокументыПрослеживаемости(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;   
	//БизТех 29.11.2024г. если не было распределения, то распределить товары << 
	ДатаПроверки = РегистрыСведений.БТ_КонстантныеЗначения.ПолучитьЗначение("ДатаНачалаОбязательногоРаспределенияВПоступлении");
	//Если ЭтотОбъект.РаспределениеТовара.Количество()= 0 и ЗначениеЗаполнено(ДатаПроверки) И ТипЗнч(ДатаПроверки) = Тип("Дата") И ДатаПроверки < Дата Тогда
	//	ЗаполнитьРаспределениеТоваров();
	//КонецЕсли;	
	
	//БИЗТЕХ МАМОНОВ 21.11.2025 ++
	//Проверяем, что подразделение ЗН в ТЧ соответствует подразделению документа
	//Если НЕ РольДоступна("ПолныеПрава") Тогда
		Если Товары.Количество() И ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
			
			ПодразделениеЗН = Товары[0].ЗаказНаряд.ПодразделениеКомпании;
			
			Если ПодразделениеЗН <> ПодразделениеКомпании Тогда
				Сообщить("Подразделение заказ-наряда: " + Товары[0].ЗаказНаряд + " отличается от подразделения документа");	
				Отказ = Истина;
			КонецЕсли;
		
			Если Отказ = Ложь И Товары.Количество() > 1 Тогда
				Для Каждого Товар Из Товары Цикл
					Если ПодразделениеЗН <> Товар.ЗаказНаряд.ПодразделениеКомпании Тогда
						Сообщить("Подразделения в списке заказ-нарядов отличаются");	
						Прервать;
						Отказ = Истина;
					КонецЕсли;	
				КонецЦикла; 
			КонецЕсли;
			
		КонецЕсли;
	//КонецЕсли;
    //БИЗТЕХ МАМОНОВ 21.11.2025 --
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
	Если ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций ИЛИ
		 ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду Тогда
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеПодразделениеКомпании);
	Иначе	
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	КонецЕсли;
	
	ФлагУпрВалюты = (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	СписокТоваров = ЭтотОбъект.Товары.ВыгрузитьКолонку("Номенклатура");
	// проведем взаиморасчеты если поступление не комиссионное
	Если ХозОперация<>Справочники.ХозОперации.ПоступлениеТоваровКомиссия И ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда // И Контрагент.ВидКонтрагента<>Перечисления.ВидыКонтрагентов.ПодотчетноеЛицо
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
		Иначе
			НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
		КонецЕсли; 
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	Если (НЕ (ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций)) И
		 (НЕ (ХозОперация = Справочники.ХозОперации.УслугиПоСубподряду)) Тогда
		
		// Закрытие распределений заказов поставщику
		НаборЗаписейРаспределения=Движения.ЗаказыРаспределение;
		НаборЗаписейРаспределения.РежимПроведения=Режим;
		НаборЗаписейРаспределения.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРаспределения.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейРаспределения.ЗаказПокупателя=Неопределено;
		НаборЗаписейРаспределения.ЗаказПоставщика=Неопределено;
		НаборЗаписейРаспределения.Контрагент=Контрагент;
		НаборЗаписейРаспределения.ДвиженияПоРознице=Истина;
		НаборЗаписейРаспределения.ПоБазовомуКоличеству=Истина;
		Отказ=НЕ НаборЗаписейРаспределения.ЗакрытиеРаспределенийЗаказовПоставщику() ИЛИ Отказ;
		
		// Резервирование товаров по заказам покупателей
		НаборЗаписейЗаказыПокупателей=Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=НаборЗаписейРаспределения.ТаблицаЗакрытияРаспределений;
		НаборЗаписейЗаказыПокупателей.Контрагент=Неопределено;
		НаборЗаписейЗаказыПокупателей.Заказ=Неопределено;
		НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладКомпании;
		НаборЗаписейЗаказыПокупателей.Заказывать=Ложь;
		НаборЗаписейЗаказыПокупателей.Резервировать=Истина;
		НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству=Истина;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
		
		//Закрытие нераспределенных заказов покупателей
		//Если обПраво("ЗакрыватьНераспределенныеЗаказыПокупателей",Права,,ЭтотОбъект) Тогда
		//Возьмем данное право из ранее сохраненного значения в реквизите документа
		Если ЗакрыватьНераспределенныеЗаказыПокупателей Тогда
			НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
			НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=НаборЗаписейРаспределения.РезультатЗапросаПоТоварам;
			НаборЗаписейЗаказыПокупателей.Контрагент=Неопределено;
			НаборЗаписейЗаказыПокупателей.Заказ=Неопределено;
			НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладКомпании;
			НаборЗаписейЗаказыПокупателей.Заказывать=Ложь;
			НаборЗаписейЗаказыПокупателей.Резервировать=Истина;
			НаборЗаписейЗаказыПокупателей.ПоБазовомуКоличеству=Истина;
			Отказ=НЕ НаборЗаписейЗаказыПокупателей.ЗакрытиеНераспределенныхЗаказов() ИЛИ Отказ;
		КонецЕсли; 
		
		// Закрытие заказов поставщику
		НаборЗаписейЗаказыПоставщикам=Движения.ЗаказыПоставщикам;
		НаборЗаписейЗаказыПоставщикам.РежимПроведения=Режим;
		НаборЗаписейЗаказыПоставщикам.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЗаказыПоставщикам.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейЗаказыПоставщикам.Контрагент=Контрагент;
		НаборЗаписейЗаказыПоставщикам.ЗаказПоставщику=Неопределено;
		НаборЗаписейЗаказыПоставщикам.ЗаказОснование=Неопределено;
		Если (ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщику")) И
			(НЕ обЗначениеНеЗаполнено(ДокументОснование)) Тогда
			НаборЗаписейЗаказыПоставщикам.ЗаказОснование=ДокументОснование;
		КонецЕсли; 
		НаборЗаписейЗаказыПоставщикам.ПоБазовомуКоличеству=Истина;
		
		Запрос = Новый Запрос;
		
		ТекстОтбора = "";  
		
		//anton
		ЗакрытиеЗаказовПоОрганизации=Истина;
		//anton
		
		Если ЗакрытиеЗаказовПоОрганизации Тогда
			Запрос.УстановитьПараметр("Организация", СкладКомпании.Организация);
			ТекстОтбора = ТекстОтбора + " И 
			|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.Организация = &Организация";
		КонецЕсли;
		
		Если ЗакрытиеЗаказовПоПодразделению = Перечисления.ВариантыОтветов.Да Тогда
			Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
			ТекстОтбора = " И 
			|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)";
		КонецЕсли;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТабЗакрытыхРаспределений.ЗаказПоставщика КАК ЗаказПоставщика,
		|	ТабЗакрытыхРаспределений.Номенклатура КАК Номенклатура,
		|	ТабЗакрытыхРаспределений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТабЗакрытыхРаспределений.Количество КАК Количество
		|ПОМЕСТИТЬ
		|	ВремТаблицаЗакрытыхРаспределений
		|ИЗ
		|	&ТабЗакрытыхРаспределений КАК ТабЗакрытыхРаспределений
		|;
		|
		|ВЫБРАТЬ
		|	ТабЗакрытыхРаспределений.ЗаказПоставщика КАК ЗаказПоставщика,
		|	ТабЗакрытыхРаспределений.Номенклатура КАК Номенклатура,
		|	ТабЗакрытыхРаспределений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СУММА(ТабЗакрытыхРаспределений.Количество) КАК Количество
		|ПОМЕСТИТЬ
		|	ТаблицаЗакрытыхРаспределений
		|ИЗ
		|	ВремТаблицаЗакрытыхРаспределений КАК ТабЗакрытыхРаспределений
		|СГРУППИРОВАТЬ ПО
		|	ТабЗакрытыхРаспределений.ЗаказПоставщика,
		|	ТабЗакрытыхРаспределений.Номенклатура,
		|	ТабЗакрытыхРаспределений.ХарактеристикаНоменклатуры
		|;
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТаблицаЗакрытыхРаспределений.ЗаказПоставщика ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ КАК ЗаказРаспределение,
		|	ЗаказыПоставщикамОстатки.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ЗаказыПоставщикамОстатки.ЗаказПоставщику=&ЗаказОснование ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ КАК ЗаказОснование,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаДоговора,
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ЗаказыПоставщикамОстатки.ЗаказаноОстаток = 0 ТОГДА
		|			ЗаказыПоставщикамОстатки.СуммаОстаток
		|		КОГДА (ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаЗакрытыхРаспределений.Количество, 0)) = 0 Тогда
		|			ЗаказыПоставщикамОстатки.СуммаОстаток
		|		ИНАЧЕ
		|			(ЗаказыПоставщикамОстатки.СуммаОстаток/ЗаказыПоставщикамОстатки.ЗаказаноОстаток)*(ЗаказыПоставщикамОстатки.ЗаказаноОстаток - (ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаЗакрытыхРаспределений.Количество, 0)))
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА ЗаказыПоставщикамОстатки.ЗаказаноОстаток = 0 ТОГДА
		|			ЗаказыПоставщикамОстатки.СуммаУпрОстаток
		|		КОГДА (ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаЗакрытыхРаспределений.Количество, 0)) = 0 Тогда
		|			ЗаказыПоставщикамОстатки.СуммаУпрОстаток
		|		ИНАЧЕ
		|			(ЗаказыПоставщикамОстатки.СуммаУпрОстаток/ЗаказыПоставщикамОстатки.ЗаказаноОстаток)*(ЗаказыПоставщикамОстатки.ЗаказаноОстаток - (ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаЗакрытыхРаспределений.Количество, 0)))
		|	КОНЕЦ КАК СуммаУпр,
		|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток - (ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаЗакрытыхРаспределений.Количество, 0)) КАК Заказано
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&Момент,
		|		Номенклатура В (&Номенклатура) И 
		|		(ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) ИЛИ ХарактеристикаНоменклатуры=&ПустаяХарактеристика) И 
		|		Контрагент=&Контрагент
		|	) КАК ЗаказыПоставщикамОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаЗакрытыхРаспределений КАК ТаблицаЗакрытыхРаспределений
		|ПО
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику            = ТаблицаЗакрытыхРаспределений.ЗаказПоставщика И 
		|	ЗаказыПоставщикамОстатки.Номенклатура               = ТаблицаЗакрытыхРаспределений.Номенклатура И 
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ТаблицаЗакрытыхРаспределений.ХарактеристикаНоменклатуры
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ЗаказыРаспределение.Остатки(&Момент, 
		|	Номенклатура В (&Номенклатура) И 
		|	(ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры) ИЛИ ХарактеристикаНоменклатуры=&ПустаяХарактеристика)
		|	) КАК ЗаказыРаспределениеОстатки
		|ПО
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику            = ЗаказыРаспределениеОстатки.ЗаказПоставщика И 
		|	ЗаказыПоставщикамОстатки.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
		|ГДЕ
		|	(ЗаказыПоставщикамОстатки.ЗаказаноОстаток - (ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаЗакрытыхРаспределений.Количество, 0))) <> 0 "+ТекстОтбора+"
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки
		|УПОРЯДОЧИТЬ ПО
		|	ВЫБОР
		|		КОГДА ТаблицаЗакрытыхРаспределений.ЗаказПоставщика ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ Убыв,
		|	ВЫБОР
		|		КОГДА ЗаказыПоставщикамОстатки.ЗаказПоставщику=&ЗаказОснование ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ Возр,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.Дата,
		|	ЗаказыПоставщикамОстатки.Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры.Сортировка Возр
		|";
		Запрос.УстановитьПараметр("ТабЗакрытыхРаспределений", НаборЗаписейРаспределения.Выгрузить());
			
		// Наложим блокировку на считываемые данные
		ЗапросИсточник = Новый Запрос("
		|ВЫБРАТЬ
		|	Номенклатура КАК Номенклатура,
		|	ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ПОМЕСТИТЬ
		|	ТаблицаИсточника
		|ИЗ
		|	&ТаблицаИсточника КАК ТаблицаИсточника
		|;//////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Номенклатура КАК Номенклатура,
		|	ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ИЗ
		|	ТаблицаИсточника
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	Номенклатура КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры
		|ИЗ
		|	ТаблицаИсточника
		|");
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ЗапросИсточник.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ЗапросИсточник.УстановитьПараметр("ТаблицаИсточника",Товары);
		ТаблицаИсточника = ЗапросИсточник.Выполнить().Выгрузить();
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПоставщикам");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период", Новый Диапазон(,Дата));
		Если Контрагент <> Неопределено Тогда
			ЗначенияБлокировки.Вставить("Контрагент", Контрагент);
		КонецЕсли;
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаИсточника);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
		ОписаниеИсточника.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		Запрос.УстановитьПараметр("Момент",?(Режим=РежимПроведенияДокумента.Оперативный,Неопределено,ЭтотОбъект.МоментВремени()));
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("ЗаказОснование",НаборЗаписейЗаказыПоставщикам.ЗаказОснование);
		Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
		Запрос.УстановитьПараметр("ПустаяХарактеристика",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		НаборЗаписейЗаказыПоставщикам.РезультатЗапросаПоЗаказам=Запрос.Выполнить().Выгрузить();
		Отказ=НЕ НаборЗаписейЗаказыПоставщикам.ЗакрытиеЗаказовПоставщику() ИЛИ Отказ;
		
		// проведем остатки товаров
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		// Оприходуем товары и резервируем те, что сняты с распределений покупателей
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=НаборЗаписейРаспределения.РезультатЗапросаПоТоварам;
		НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.Резервировать=Истина;
		НаборЗаписейОстатки.Контрагент=Контрагент;
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
		НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
		Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
		
		// проведем партии товаров
		Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
		
		// способ ведения не по подразделениям или балансовые подразделения равны то ничего не будем корректировать
		Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда	
			Если ХозОперация<>Справочники.ХозОперации.ПоступлениеТоваровКомиссия И
				ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда
				СуммаПартий = Движения.ПартииТоваровКомпании.Итог("СуммаУпр");
				Если СуммаПартий<>0 Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение = СкладКомпании.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					НаборЗаписейДиР.Доход = СуммаПартий;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
				КонецЕсли;			
				
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Ложь;
				НаборЗаписейДиР.Расход = СуммаДокумента;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
			КонецЕсли;
		КонецЕсли;	
	
	Иначе
		// проведем услуги по доходам и расходам
		Запрос=Новый Запрос("ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура.СтатьяДопРасходов КАК СтатьяРасходов,
		|	СУММА(ПоступлениеТоваровТовары.СуммаВсего) КАК СуммаУслуг,
		//<<Павличенко 12.01.18
		|	СУММА(ПоступлениеТоваровТовары.СуммаНДС) КАК СуммаНДС
		//>>Павличенко 12.01.18
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &ТекДок
		|	И ПоступлениеТоваровТовары.Номенклатура.ВидНоменклатуры = &Услуга
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура.СтатьяДопРасходов");
		Запрос.УстановитьПараметр("ТекДок",Ссылка);
		Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		Выборка=Запрос.Выполнить().Выбрать();
		СуммаУслуг = 0;     
		//<<Павличенко 12.01.18
		СуммаУслугБезНДС = 0;
  		//>>Павличенко 12.01.18
		Пока Выборка.Следующий() Цикл
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Выборка.СтатьяРасходов;
			НаборЗаписейДоходыИРасходы.Расход=Выборка.СуммаУслуг;//
			//<<Павличенко 12.01.18
			НаборЗаписейДоходыИРасходы.РасходБезНДС=Выборка.СуммаУслуг-Выборка.СуммаНДС;//
			//>>Павличенко 12.01.18
			СуммаУслуг=СуммаУслуг+Выборка.СуммаУслуг; 
			//<<Павличенко 12.01.18
			СуммаУслугБезНДС=СуммаУслугБезНДС+Выборка.СуммаУслуг-Выборка.СуммаНДС; 
			//>>Павличенко 12.01.18
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЦикла;
		
		//Если балансовые подразделение договора и подразделения склад не равны, то сформируем корректирующие проводки
		Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Расход=СуммаУслуг;            //
			//<<Павличенко 12.01.18
			НаборЗаписейДоходыИРасходы.РасходБезНДС=СуммаУслугБезНДС;          
			//>>Павличенко 12.01.18
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеКомпании;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.Доход=СуммаУслуг;         //
			НаборЗаписейДоходыИРасходы.Доход=СуммаУслуг;         
			//<<Павличенко 12.01.18
			НаборЗаписейДоходыИРасходы.ДоходБезНДС=СуммаУслугБезНДС; 
			//>>Павличенко 12.01.18
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		Если ХозОперация=Справочники.ХозОперации.УслугиПоСубподряду Тогда
			НаборЗаписейСубподряд=Движения.Субподряд;
			НаборЗаписейСубподряд.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейСубподряд.ДатаВыполнения=Неопределено;
			Запрос=Новый Запрос("ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ПоступлениеТоваровТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ЗаказНаряд
			|				И НЕ ПоступлениеТоваровТовары.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказНаряд.ПустаяСсылка)
			|			ТОГДА ПоступлениеТоваровТовары.Ссылка.ДокументОснование
			|		ИНАЧЕ ПоступлениеТоваровТовары.ЗаказНаряд
			|	КОНЕЦ КАК ЗаказНаряд,
			|	ПоступлениеТоваровТовары.Номенклатура КАК Работа,
			|	ПоступлениеТоваровТовары.Ссылка.Контрагент КАК Контрагент,
			|	ПоступлениеТоваровТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
			|	СУММА(ПоступлениеТоваровТовары.СуммаВсего) КАК СуммаВсего,
			|	СУММА(ПоступлениеТоваровТовары.СуммаНДС) КАК СуммаНДС
			|ИЗ
			|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
			|ГДЕ
			|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫБОР
			|		КОГДА ПоступлениеТоваровТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ЗаказНаряд
			|				И НЕ ПоступлениеТоваровТовары.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказНаряд.ПустаяСсылка)
			|			ТОГДА ПоступлениеТоваровТовары.Ссылка.ДокументОснование
			|		ИНАЧЕ ПоступлениеТоваровТовары.ЗаказНаряд
			|	КОНЕЦ,
			|	ПоступлениеТоваровТовары.Номенклатура,
			|	ПоступлениеТоваровТовары.Ссылка.Контрагент,
			|	ПоступлениеТоваровТовары.Ссылка.ДоговорВзаиморасчетов");
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			НаборЗаписейСубподряд.РезультатЗапросаПоРаботам=Запрос.Выполнить().Выгрузить();
			Отказ=НЕ НаборЗаписейСубподряд.Движение(Ложь) ИЛИ Отказ;
		КонецЕсли; 
		
	КонецЕсли;
	
	// если приходуем товар на розничный склад, то установим розничные цены на этот товар
	Если НЕ Отказ И (ХозОперация=Справочники.ХозОперации.ПоступлениеТоваров ИЛИ ХозОперация=Справочники.ХозОперации.ПоступлениеБезвозмездное) И
		 СкладКомпании.Розничный И
		 ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И
		 НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ТипЦен=СкладКомпании.ТипЦенРозничнойТорговли;
		НаборЗаписейЦены.ПодразделениеКомпании = СкладКомпании.Подразделение;
		НаборЗаписейЦены.УстанавливатьЦеныУслуг=Ложь;
		НаборЗаписейЦены.ИмяРеквизитаЦена="ЦенаРозничная";
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// установим цены контрагентов
	Если НЕ Отказ И ХозОперация=Справочники.ХозОперации.ПоступлениеТоваров И
		 ТипЦен.РегистрироватьЦеныПоПриходу И
		 (НЕ обЗначениеНеЗаполнено(Контрагент)) И
		 НЕ ТипЦен.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Контрагент;
		НаборЗаписейЦены.ТипЦен=ТипЦен;
		НаборЗаписейЦены.УстанавливатьЦеныУслуг=Истина;
		НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// установим закупочные цены компании
	Если НЕ Отказ И ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное И
		 ПодразделениеКомпании.ФормироватьЗакупочнуюЦену И
		 НЕ Справочники.ТипыЦен.ОсновнойТипЦенЗакупки.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
		НаборЗаписейЦены.ТипЦен=Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	//установим нормативные цены компании
	Если НЕ Отказ И ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное И
		 ПодразделениеКомпании.ФормироватьНормативнуюЦену И
		 НЕ Справочники.ТипыЦен.НормативнаяЦена.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
		НаборЗаписейЦены.ТипЦен=Справочники.ТипыЦен.НормативнаяЦена;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// Изменим состояние маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
	НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборот;
	НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
	НаборЗаписейСостоянияКодовМаркировки.ПроверятьВводВОборот = Истина;
	Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И (Движения.ЗаказыПокупателей.Количество()>0 ИЛИ Движения.ЗаказыПоставщикам.Количество()>0 ИЛИ Движения.ЗаказыРаспределение.Количество()>0));
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Колонки.Удалить("Заказ");
			ТаблицаЗаказов.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя,ДокументСсылка.ЗаказВнутренний,ДокументСсылка.ЗаказПоставщику"));
			ТаблицаЗаказов.ЗагрузитьКолонку(Движения.ЗаказыПокупателей.ВыгрузитьКолонку("Заказ"),"Заказ");
			Для каждого СтрокаЗаказа Из Движения.ЗаказыПоставщикам Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщику;
			КонецЦикла; 
			Для каждого СтрокаЗаказа Из Движения.ЗаказыРаспределение Цикл
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПокупателя;
				НоваяСтрока=ТаблицаЗаказов.Добавить();
				НоваяСтрока.Заказ=СтрокаЗаказа.ЗаказПоставщика;
			КонецЦикла; 
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	// Зафиксируем новые штрихкоды товара из кода маркировки
	дкПроверитьИДобавитьГТИННоменклатуры(ЭтотОбъект);
	
	// Проверим налиие прослеживаемых товаров, которые были возвращены из розницы
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;  
	
	//<<БАА
	//ДатаПроверки = РегистрыСведений.БТ_КонстантныеЗначения.ПолучитьЗначение("ДатаНачалаОбязательногоРаспределенияВПоступлении");
	Если ЗначениеЗаполнено(ДатаПроверки) И ТипЗнч(ДатаПроверки) = Тип("Дата") И ДатаПроверки < Дата 
		и ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваров Тогда
		
		Если не ЗначениеЗаполнено(ДокументОснование) или ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.ЗаказПоставщику") тогда
			Если ОбПраво("ЗапретПроведенияПНБезЗаказаПоставщику") Тогда
				Сообщить("Поступление можно ввести только на основании Заказа поставщику", СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;	
		КонецЕсли;	
		
		ТЗНезаказанных = Незаказанные();
		
		Если ТЗНезаказанных.Количество() Тогда
			Если ОбПраво("ЗапретПроведенияПНСНераспределеннымиТоварами") Тогда
				Сообщить("В Поступлении не должно быть нераспределенных товаров, создайте заказы поставщику на позиции:", СтатусСообщения.Важное);
				Для Каждого СтрокаТЗ Из ТЗНезаказанных Цикл
					Сообщить(""+СтрокаТЗ.Номенклатура+" в кол-ве " + СтрокаТЗ.Количество);
				КонецЦикла;	
				Отказ = Истина;
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЕсли;	
	//>>БАА  
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Количество = тс.Количество;  
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ЗаполнитьРаспределениеТоваров()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыРаспределениеОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказыРаспределениеОстатки.ЗаказПоставщика КАК ЗаказПоставщика,
		|	ЗаказыРаспределениеОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыРаспределениеОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЗаказыРаспределение.Остатки(
		|			&Дата,
		|			ЗаказПоставщика.Контрагент = &Контрагент
		|				И Номенклатура В (&СписокТоваров)) КАК ЗаказыРаспределениеОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказыРаспределениеОстатки.ЗаказПокупателя.Дата
		|ИТОГИ ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("СписокТоваров", Товары.ВыгрузитьКолонку("Номенклатура"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Если НЕ РезультатЗапроса.выгрузить().Количество() = 0 Тогда   

		Пока ВыборкаНоменклатура.Следующий() Цикл
			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
			
			СтрП = Товары.Найти(ВыборкаНоменклатура.Номенклатура);
			Если СтрП = Неопределено Тогда
				Продолжить;
			КонецЕсли;	
			
			Стрп.СпособРаспределения = 2;
			
			ОсталосьРаспределить = СтрП.Количество;
			
			Пока ВыборкаДетальныеЗаписи.Следующий() И ОсталосьРаспределить > 0 Цикл
				
				Колво = Мин(ОсталосьРаспределить, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				НоваяСтрока = РаспределениеТовара.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
				НоваяСтрока.Количество = Колво;
				
				ОсталосьРаспределить = ОсталосьРаспределить - Колво;
				
			КонецЦикла;
			
		КонецЦикла;  
	Иначе

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщика,
		|	ЗаказыПоставщикамОстатки.Контрагент КАК Контрагент,
		|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
		|			&Дата,
		|			Контрагент = &Контрагент
		|				И Номенклатура В (&СписокТоваров)) КАК ЗаказыПоставщикамОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.Дата
		|ИТОГИ ПО
		|	Номенклатура";
		
		Запрос.УстановитьПараметр("Дата", Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("СписокТоваров", Товары.ВыгрузитьКолонку("Номенклатура"));
		
		РезультатЗапроса = Запрос.Выполнить();  
        ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
			
			СтрП = Товары.Найти(ВыборкаНоменклатура.Номенклатура);
			Если СтрП = Неопределено Тогда
				Продолжить;
			КонецЕсли;	
			
			Стрп.СпособРаспределения = 2;
			
			ОсталосьРаспределить = СтрП.Количество;
			
			Пока ВыборкаДетальныеЗаписи.Следующий() И ОсталосьРаспределить > 0 Цикл
				
				Колво = Мин(ОсталосьРаспределить, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				НоваяСтрока = РаспределениеТовара.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
				НоваяСтрока.Количество = Колво;
				
				ОсталосьРаспределить = ОсталосьРаспределить - Колво;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

	
//	ЭлементыФормы.ОсновнаяПанель.Страницы.РаспределениеТовара.Заголовок = "Распределение товара ("+РаспределениеТовара.Количество()+" поз.)";
	ЗаполнитьНезаказанные();  
	
КонецПроцедуры 

Процедура ЗаполнитьНезаказанные(ТолькоТаблица = Ложь)
	
	ТЗНезаказанных = Незаказанные();
	НезаказанныеТовары = ТЗНезаказанных;
	
	//ЭлементыФормы.ОсновнаяПанель.Страницы.СтраницаНеЗакаказанные.Заголовок = "Незаказанные ("+НезаказанныеТовары.Количество()+" поз.)";
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

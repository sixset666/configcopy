
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПроверитьДублированиеДокументов(Отказ);
	ПроверитьКомментарии(Отказ, РежимЗаписи);
	
	Если ЗначениеЗаполнено(ДокументОснование)
		И НЕ Период = ДокументОснование.ПериодПланирования Тогда
		
		Сообщить("Период расчета документа не может отличаться от периода расчета документа-основания!");
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДублированиеДокументов(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Период",Период);
	Запрос.УстановитьПараметр("Периодичность",Периодичность);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(КорректировкиРасчетаKPI.Ссылка) КАК Ссылка
	|ИЗ
	|	Документ.КорректировкиРасчетаKPI КАК КорректировкиРасчетаKPI
	|ГДЕ
	|	НЕ КорректировкиРасчетаKPI.ПометкаУдаления
	|	И КорректировкиРасчетаKPI.Ссылка <> &Ссылка
	|	И КорректировкиРасчетаKPI.Организация = &Организация
	|	И КорректировкиРасчетаKPI.ПодразделениеКомпании = &ПодразделениеКомпании
	|	И КорректировкиРасчетаKPI.Период = &Период
	|	И КорректировкиРасчетаKPI.Периодичность = &Периодичность";
				   
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Сообщить("Для выбранного периода уже создан документ: "+Выборка.Ссылка+Символы.ПС+"Новый документ не может быть создан.",СтатусСообщения.Важное); 
		Отказ = Истина;
		
	КонецЕсли;	
				   	
КонецПроцедуры

Процедура ПроверитьКомментарии(Отказ, РежимЗаписи)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
		СпСотр=Корректировки.Выгрузить();
		
		ФИО="";
		
		СпСотр.Свернуть("Сотрудник, СуммаКорректировки");
		
		Для Каждого ТекСтр из СпСотр Цикл
			
			Если НЕ ТекСтр.СуммаКорректировки = 0 Тогда
				
				Если ПустаяСтрока(КомментарииПоСотруднику(ТекСтр.Сотрудник)) Тогда
					ФИО=ФИО+ТекСтр.Сотрудник+";";
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЦикла;
		
		Если СтрДлина(ФИО)>0 Тогда
			ФИО=Лев(ФИО,СтрДлина(ФИО)-1);
		КонецЕсли;
		
		Если Не ПустаяСтрока(ФИО) Тогда
			
			Сообщить("Заполните причину корректировки (комментарий) по сотрудникам:"+ФИО);
			
			Отказ=истина;
			
			Возврат;
			
		КонецЕсли;
		
КонецПроцедуры	
	
Функция КомментарииПоСотруднику(Сотр)
	
	МС=Корректировки.НайтиСтроки(Новый Структура("Сотрудник",Сотр));

	Комм="";
		
	Для Каждого ТекЭл из МС Цикл
		
		Если Не ПустаяСтрока(ТекЭл.Комментарий) Тогда
			
			Комм=Комм+СокрЛП(ТекЭл.Комментарий)+";"+Символы.ПС;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрДлина(Комм)>0 Тогда
		Комм=Лев(Комм,СтрДлина(Комм)-1);
	КонецЕсли;
	
	Возврат Комм;
	
КонецФункции

Процедура ПроверитьЗаполнениеРеквизитовШапки(Отказ)
	
	СтруктураОбязательныхРеквизитов = Новый Структура();
	СтруктураОбязательныхРеквизитов.Вставить("Организация","Организация");
	СтруктураОбязательныхРеквизитов.Вставить("ПодразделениеКомпании","Подразделение компании");
	СтруктураОбязательныхРеквизитов.Вставить("Периодичность","Периодичность");
	СтруктураОбязательныхРеквизитов.Вставить("Период","Период расчета");
	СтруктураОбязательныхРеквизитов.Вставить("Автор","Автор");
	
	ЗаголовокВыведен = Ложь;	
	Для Каждого Элемент Из СтруктураОбязательныхРеквизитов Цикл
		Если Не ЗначениеЗаполнено(ЭтотОбъект[Элемент.Ключ]) Тогда
			Если Не ЗаголовокВыведен Тогда
				Сообщить("При проведении документа обнаружены ошибки!",СтатусСообщения.Важное);
				ЗаголовокВыведен = Истина;
			КонецЕсли;
			Сообщить("Не заполнено поле: """+Элемент.Значение+"""",СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроверитьЗаполнениеРеквизитовШапки(Отказ);
	
	Если Не Отказ Тогда				

		// регистр Начисления
		Движения.Начисления.Записывать = Истина;
		Движения.Начисления.Очистить();
		Для Каждого ТекСтрокаКорректировки Из Корректировки Цикл
			
			Если НЕ ТекСтрокаКорректировки.СуммаКорректировки = 0 Тогда
				
				Движение = Движения.Начисления.Добавить();
				Движение.Период = Период;
				Движение.Сотрудник = ТекСтрокаКорректировки.Сотрудник;	
				Движение.Должность = ТекСтрокаКорректировки.Должность;
				Движение.ПодразделениеКомпании = ПодразделениеКомпании;
				Движение.Периодичность = Периодичность;
				Движение.Организация = Организация;
				Движение.ДопПремия = ТекСтрокаКорректировки.СуммаКорректировки;
				
			КонецЕсли;
			
		КонецЦикла;

		Попытка
			
			ДокументРасчета = ДокументОснование.ПолучитьОбъект();
			
			ДокументРасчета.КорректировкаОкладов.Очистить();
			Для Каждого ТекСтрокаКорректировки Из Корректировки Цикл
				
				Если НЕ ТекСтрокаКорректировки.СуммаКорректировки = 0 Тогда
					
					//Заполняем ТЧ корректировки
					НоваяСтрока = ДокументРасчета.КорректировкаОкладов.Добавить();
					НоваяСтрока.Сотрудник = ТекСтрокаКорректировки.Сотрудник;
					НоваяСтрока.СуммаКорректировки = ТекСтрокаКорректировки.СуммаКорректировки;
					
					ПараметрыОтбораСотрудника = Новый Структура;
					ПараметрыОтбораСотрудника.Вставить("Сотрудник", ТекСтрокаКорректировки.Сотрудник);
					ПараметрыОтбораСотрудника.Вставить("Должность",ТекСтрокаКорректировки.Должность);
					
					НайденаяСтрокаФОТ = ДокументРасчета.ФОТ.НайтиСтроки(ПараметрыОтбораСотрудника);
					
					Если НЕ НайденаяСтрокаФОТ.Количество() = 0 Тогда 
						
						НайденаяСтрокаФОТ[0].ДопПремия = ТекСтрокаКорректировки.СуммаКорректировки;
						НайденаяСтрокаФОТ[0].СуммаСКорректировкой = ТекСтрокаКорректировки.СуммаНачислено + НайденаяСтрокаФОТ[0].ДопПремия;

					КонецЕсли;
				
				КонецЕсли;
			
	     	КонецЦикла;
			
			ДокументРасчета.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			
		КонецПопытки;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	#Если Клиент Тогда		
		Сообщить("Копирование запрещено. Будет создан новый документ.");
		НовыйДокумент = Документы.КорректировкиРасчетаKPI.СоздатьДокумент();
		Форма = НовыйДокумент.ПолучитьФорму("ФормаДокумента");
		Форма.Открыть();
	#КонецЕсли	

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ДанныеЗаполнения = Неопределено Тогда 
		
		Если Не ДанныеЗаполнения.Проведен Тогда
			Сообщить("Новый документ может быть введен на основании только проведенного документа.");
			Возврат;
		КонецЕсли; 	
		
		ДокументОснование = ДанныеЗаполнения;
		Организация = ДанныеЗаполнения.ПодразделениеКомпании.Организация;
		ПодразделениеКомпании = ДанныеЗаполнения.ПодразделениеКомпании;
		Период = ДанныеЗаполнения.ПериодПланирования;
		Периодичность = ДанныеЗаполнения.Периодичность;
		
		ЗаполнитьКорректировкиПоОснованию();
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьКорректировкиПоОснованию() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", ДокументОснование);
	Запрос.Текст = "ВЫБРАТЬ
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Должность КАК Должность,
	|	СУММА(Начисления.Сумма) КАК СуммаНачислено,
	|	Начисления.Оклад КАК Оклад,
	|	Начисления.Премия КАК Премия,
	|	Начисления.Мотивация КАК Мотивация
	|ИЗ
	|	РегистрСведений.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|	И Начисления.Сумма > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.Сотрудник,
	|	Начисления.Должность,
	|	Начисления.Оклад,
	|	Начисления.Премия,
	|	Начисления.Мотивация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Корректировки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		КонецЦикла;	
	КонецЕсли; 	
	
КонецПроцедуры	

//Печать
Функция ПечатьВедомости() Экспорт 

	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("МакетКарректировкаРасчета");
			
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");						
	ОбластьШапка.Параметры.Организация = Организация;
	ОбластьШапка.Параметры.ДокументОснования = "Расчет выполнения KPI " + ДокументОснование.Номер + " от " + ДокументОснование.Дата;
	
	Если Периодичность = Перечисления.Периодичность.Месяц Тогда
		ОбластьШапка.Параметры.Период = ПредставлениеПериода(Период,КонецМесяца(Период));
	ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда	
		ОбластьШапка.Параметры.Период = ПредставлениеПериода(НачалоКвартала(Период),КонецКвартала(Период));
	ИначеЕсли Периодичность = Перечисления.Периодичность.Год Тогда	
		ОбластьШапка.Параметры.Период = ПредставлениеПериода(НачалоГода(Период),КонецГода(Период));	
	КонецЕсли;	
	
	СтруктураОтбора=Новый Структура("Организация,Объект", Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(Дата,СтруктураОтбора);
	
	Если ЗначениеЗаполнено(СтруктураСведений.Значение) тогда
		ОбластьШапка.Параметры.Долж = СтруктураСведений.Значение.Должность;
	КонецЕсли;

	ОбластьШапка.Параметры.ГенДиректор = Мотивация.ФИО(СтруктураСведений.Значение);

	ТабДок.Вывести(ОбластьШапка);				
	
	// ------ строка ------	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ТабКорректировки = Корректировки.Выгрузить();
	
	ТабКорректировки.Свернуть("Сотрудник","Результат,СуммаНачислено,СуммаКорректировки");
	
	Сч=1;
	 
	Для Каждого СтрокаТаблицы Из ТабКорректировки Цикл
		
		Если НЕ СтрокаТаблицы.СуммаКорректировки = 0 Тогда
			
			ОбластьСтрока.Параметры.Номер = Сч;
			Сч=Сч+1;
			ОбластьСтрока.Параметры.ФИО = Мотивация.ФИО(СтрокаТаблицы.Сотрудник);
			ОбластьСтрока.Параметры.Должность = СтрокаТаблицы.Сотрудник.Должность;
			ОбластьСтрока.Параметры.Сумма = СтрокаТаблицы.Результат;
			ОбластьСтрока.Параметры.Начислено = СтрокаТаблицы.СуммаНачислено;
			ОбластьСтрока.Параметры.Корректировка = СтрокаТаблицы.СуммаКорректировки;
			Коммент=КомментарииПоСотруднику(СтрокаТаблицы.Сотрудник);
			ОбластьСтрока.Параметры.Комментарий=Коммент;
			ТабДок.Вывести(ОбластьСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбластьПодписи = Макет.ПолучитьОбласть("Подпись");
	ТабДок.Вывести(ОбластьПодписи);
	
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ТолькоПросмотр = Истина;

	Возврат ТабДок;
	
КонецФункции


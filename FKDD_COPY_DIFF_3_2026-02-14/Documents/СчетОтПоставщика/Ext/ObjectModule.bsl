// Модуль документа СчетОтПоставщика

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("РасчетныйСчетКонтрагента",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только в случае
	// если все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;     	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СчетОтПоставщика","Счет от поставщика",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя = "Контрагент" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ Контрагент.Пустая() Тогда
			Если (РасчетныйСчетКонтрагента.Пустая()) ИЛИ (НЕ РасчетныйСчетКонтрагента.Владелец = Контрагент) Тогда
				РасчетныйСчетКонтрагента = Контрагент.ОсновнойБанковскийСчет;	
			КонецЕсли;
		Иначе
			РасчетныйСчетКонтрагента = Справочники.БанковскиеСчета.ПустаяСсылка();
		КонецЕсли;
		Возврат Рез;

	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "СчетОтПоставщика"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьСчетОтПоставщика(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("СчетОтПоставщика");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод образца заполнения платёжного поручения
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета"); 
	ОбластьМакета.Параметры.БанкПолучателя						= Символы.НПП+РасчетныйСчетКонтрагента.Банк;
	ОбластьМакета.Параметры.БанкПолучателяПредставление			= спПолучитьНаименование(РасчетныйСчетКонтрагента.Банк);
	ОбластьМакета.Параметры.БИКБанкаПолучателя					= Символы.НПП+СокрЛП(РасчетныйСчетКонтрагента.Банк.Код);
	ОбластьМакета.Параметры.СчетБанкаПолучателя					= Символы.НПП+СокрЛП(РасчетныйСчетКонтрагента.Банк.КоррСчет);
	ОбластьМакета.Параметры.ИНН									= Символы.НПП+спПолучитьПредставление(Контрагент,Новый Структура("ИНН"));
	ОбластьМакета.Параметры.КПП									= Символы.НПП+спПолучитьПредставление(Контрагент,Новый Структура("КПП"));
	ОбластьМакета.Параметры.Получатель							= Контрагент;
	ОбластьМакета.Параметры.ПолучательПредставление				= спПолучитьПредставление(Контрагент,Новый Структура("Наименование"));
	ОбластьМакета.Параметры.СчетПолучателя						= Символы.НПП+СокрЛП(РасчетныйСчетКонтрагента.НомерСчета);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтруктураПредставления=Новый Структура(
		"ИНН,КПП,Наименование,АдресЮридический,ТелефонРабочий",
		"ИНН ","КПП ","","","тел.: "
	);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Контрагент,СтруктураПредставления);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Организация,СтруктураПредставления, ДополнительныеПараметры);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//вывод документа основания
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ДокументОснование");
		ОбластьМакета.Параметры.ДокументОснованиеПредставление=дкПолучитьПредставление(ДокументОснование);
		ОбластьМакета.Параметры.ДокументОснование=ДокументОснование;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтрокаКомментарий=СокрЛП(СтрокаТабличнойЧасти.Комментарий);
		Если НЕ ПустаяСтрока(СтрокаКомментарий) Тогда
			СтруктураСтроки.ТоварНаименование = СокрЛП(СтрокаКомментарий);	
		КонецЕсли;
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	//Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	//Руководитель.РуководительПредставление = ?(обЗначениеНеЗаполнено(Руководитель.Руководитель),
	//"","/"+Руководитель.РуководительПредставление+"/");
	//ОбластьПодвал.Параметры.Заполнить(Руководитель);
	//ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	//ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),
	//"","/"+ГлавныйБухгалтер.ГлавныйБухгалтерПредставление+"/");
	//ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьСчетОтПоставщика()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	Если РасчетныйСчетКонтрагента.Пустая() И НЕ Контрагент.Пустая() Тогда
		РасчетныйСчетКонтрагента = Контрагент.ОсновнойБанковскийСчет;
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		ТекстЗапроса="ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЗаказыПоставщикамОстатки.Заказано КАК ЗаказаноОстаток,
		|	ЗаказыПоставщикамОстатки.Сумма КАК Сумма,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.Количество * ЗаказПоставщикуТовары.Коэффициент, 1) КАК КоличествоБазовое,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.ЕдиницаИзмерения, Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.Коэффициент, 1) КАК Коэффициент,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.СтавкаНДС, ЗаказыПоставщикамОстатки.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
		|	ЕСТЬNULL(ЗаказПоставщикуТовары.НомерСтроки, 1000000) КАК НомерСтроки
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
		|		ЗаказыПоставщикам.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		СУММА(ЗаказыПоставщикам.Заказано) КАК Заказано,
		|		СУММА(ЗаказыПоставщикам.Сумма) КАК Сумма
		|		ИЗ
		|			РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|		ГДЕ
		|			ЗаказыПоставщикам.Период<=&Момент И 
		|			(НЕ ЗаказыПоставщикам.Регистратор ССЫЛКА Документ.ПоступлениеТоваров) И 
		|			ЗаказыПоставщикам.ЗаказПоставщику = &ВыбЗаказПоставщику И 
		|			ЗаказыПоставщикам.Контрагент = &ВыбКонтрагент
		|		СГРУППИРОВАТЬ ПО
		|			ЗаказыПоставщикам.Номенклатура,
		|			ЗаказыПоставщикам.ХарактеристикаНоменклатуры
		|		ИМЕЮЩИЕ
		|			(НЕ СУММА(ЗаказыПоставщикам.Заказано) = 0) ИЛИ 
		|			(НЕ СУММА(ЗаказыПоставщикам.Сумма) = 0)) КАК ЗаказыПоставщикамОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ПО
		|	ЗаказПоставщикуТовары.Ссылка                        = &ВыбЗаказПоставщику И 
		|	ЗаказыПоставщикамОстатки.Номенклатура               = ЗаказПоставщикуТовары.Номенклатура И 
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ
		|	СУММА(КоличествоБазовое),
		|	МАКСИМУМ(ЗаказаноОстаток),
		|	МАКСИМУМ(Сумма)
		|ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры";
		
		
		//ТекстЗапроса="ВЫБРАТЬ
		//|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		//|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		//|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток КАК ЗаказаноОстаток,
		//|	ЗаказыПоставщикамОстатки.СуммаОстаток КАК Сумма,
		//|	ЕСТЬNULL(ЗаказПоставщикуТовары.Количество * ЗаказПоставщикуТовары.Коэффициент, 1) КАК КоличествоБазовое,
		//|	ЕСТЬNULL(ЗаказПоставщикуТовары.ЕдиницаИзмерения, Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		//|	ЕСТЬNULL(ЗаказПоставщикуТовары.Коэффициент, 1) КАК Коэффициент,
		//|	ЕСТЬNULL(ЗаказПоставщикуТовары.СтавкаНДС, ЗаказыПоставщикамОстатки.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
		//|	ЕСТЬNULL(ЗаказПоставщикуТовары.НомерСтроки, 1000000) КАК НомерСтроки
		//|ИЗ
		//|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
		//|				&Момент,
		//|				ЗаказПоставщику = &ВыбЗаказПоставщику
		//|					И Контрагент = &ВыбКонтрагент) КАК ЗаказыПоставщикамОстатки
		//|ЛЕВОЕ СОЕДИНЕНИЕ
		//|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		//|ПО
		//|	ЗаказыПоставщикамОстатки.ЗаказПоставщику            = ЗаказПоставщикуТовары.Ссылка И 
		//|	ЗаказыПоставщикамОстатки.Номенклатура               = ЗаказПоставщикуТовары.Номенклатура И 
		//|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры
		//|УПОРЯДОЧИТЬ ПО
		//|	НомерСтроки
		//|ИТОГИ
		//|	СУММА(КоличествоБазовое),
		//|	МАКСИМУМ(ЗаказаноОстаток),
		//|	МАКСИМУМ(Сумма)
		//|ПО
		//|	Номенклатура,
		//|	ХарактеристикаНоменклатуры";
		
		Запрос=Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Момент", КонецДня(Дата));
		Запрос.УстановитьПараметр("ВыбКонтрагент", Контрагент);
		Запрос.УстановитьПараметр("ВыбЗаказПоставщику", ДокументОснование);
		Товары.Очистить(); // нужные только скорректированные позиции
		
		ВалютаЗаказа	= Основание.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		СтруктураКурса	= обКурсДляВалюты(ВалютаЗаказа,Дата);
		КурсЗаказа		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатуры.Следующий() Цикл
			ВыборкаХарактеристик = ВыборкаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристик.Следующий() Цикл
				ВсегоОсталось = ВыборкаХарактеристик.ЗаказаноОстаток;
				КоличествоБазовоеПоЗаказу = ВыборкаХарактеристик.КоличествоБазовое;
				СуммаОсталось = обПересчет(ВыборкаХарактеристик.Сумма, ВалютаЗаказа, КурсЗаказа, ВалютаДокумента, КурсДокумента);
				ВыборкаДетали = ВыборкаХарактеристик.Выбрать(ОбходРезультатаЗапроса.Прямой);
				НоваяСтрока	  = Неопределено;
				Пока ВыборкаДетали.Следующий() Цикл
					Если ВсегоОсталось=0 Тогда
						Прервать;
					КонецЕсли;
					Если ВыборкаДетали.ЗаказаноОстаток = 0 Тогда
						Продолжить;
					КонецЕсли;
					Если КоличествоБазовоеПоЗаказу = 1 Тогда
						КоличествоСтроки = ВыборкаДетали.ЗаказаноОстаток;
					Иначе
						КоличествоСтроки = ВыборкаДетали.ЗаказаноОстаток*(ВыборкаДетали.КоличествоБазовое/КоличествоБазовоеПоЗаказу);
					КонецЕсли;
					
					ТекущееКоличество = Мин(ВсегоОсталось, КоличествоСтроки);
					НоваяСтрока=Товары.Добавить();
					НоваяСтрока.Номенклатура               = ВыборкаДетали.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаДетали.ХарактеристикаНоменклатуры;
					НоваяСтрока.ЕдиницаИзмерения           = ВыборкаДетали.ЕдиницаИзмерения;
					НоваяСтрока.Коэффициент                = ВыборкаДетали.Коэффициент;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
					НоваяСтрока.СтавкаНДС                  = ВыборкаДетали.СтавкаНДС;
					НоваяСтрока.Количество                 = ТекущееКоличество/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);;
					
					ТекСумма = Окр(СуммаОсталось/ВсегоОсталось*ТекущееКоличество,2);
					
					СуммаОсталось = СуммаОсталось - ТекСумма;
					НоваяСтрока.СуммаВсего                 = ТекСумма;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
					
					ВсегоОсталось = ВсегоОсталось - ТекущееКоличество;
				КонецЦикла;
				
				Если ВсегоОсталось>0 ИЛИ СуммаОсталось>0 Тогда
					Если НЕ НоваяСтрока = Неопределено Тогда
						НоваяСтрока.Количество		  = НоваяСтрока.Количество + (ВсегоОсталось/НоваяСтрока.Коэффициент);
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						НоваяСтрока.СуммаВсего        = НоваяСтрока.СуммаВсего + СуммаОсталось;
					ИначеЕсли ВсегоОсталось>0 Тогда
						НоваяСтрока=Товары.Добавить();
						НоваяСтрока.Номенклатура=ВыборкаХарактеристик.Номенклатура;
						НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаХарактеристик.ХарактеристикаНоменклатуры;
						ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
						НоваяСтрока.СтавкаНДС  = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
						НоваяСтрока.Количество = ВсегоОсталось/?(обЗначениеНеЗаполнено(НоваяСтрока.Коэффициент),1,НоваяСтрока.Коэффициент);
						ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
						НоваяСтрока.СуммаВсего = СуммаОсталось;
					КонецЕсли;
					ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Товары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,ХарактеристикаНоменклатуры,Комментарий,СтавкаНДС", "Количество,Сумма,СуммаНДС,СуммаВсего");
		Для Каждого ТекСтрока Из Товары Цикл
			СуммаНДС = ТекСтрока.СуммаНДС;
			ОбработкаРеквизита("Товары.СуммаВсего", ТекСтрока);
			ТекСтрока.СуммаНДС = СуммаНДС;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

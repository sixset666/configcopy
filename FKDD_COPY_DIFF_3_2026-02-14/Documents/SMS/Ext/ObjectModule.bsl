Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Формирование текста сообщения по шаблону
Функция СформироватьСообщениеПоШаблону(Шаблон,ТекКонтакт, СтрокаОшибкиФункции = Неопределено) Экспорт
	
	ШаблонТекст = Шаблон;//.Содержание;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Действия.Действие,
	               |	Действия.ПредставлениеДействия КАК Представление
	               |ИЗ
	               |	Справочник.Действия КАК Действия
	               |ГДЕ
	               |	Действия.Автотекст = &Автотекст";
	
	Запрос.УстановитьПараметр("Автотекст",Истина);			   
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Найти(ШаблонТекст,Выборка.Представление) Тогда
			
			ЗначениеМакроса = "";
			Выражение = Выборка.Действие;
			Автотекст = ""; //переменной при выполнении макроса передается результат 
			Попытка
				Выполнить(Выражение);	
				ШаблонТекст = СтрЗаменить(ШаблонТекст, Выборка.Представление, Автотекст);
			Исключение
				ШаблонТекст = СтрЗаменить(ШаблонТекст,Выборка.Представление,"");
				Если СтрокаОшибкиФункции = Неопределено Тогда
					Сообщить(ОписаниеОшибки());
					Сообщить("Заполнение по автотексту " + Выборка.Представление  +" не выполнено!");
				Иначе
					СтрокаОшибкиФункции = ОписаниеОшибки() + Символы.ПС + "Заполнение по автотексту " + Выборка.Представление  +" не выполнено!";
				КонецЕсли;
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
	Возврат  ШаблонТекст;
	
КонецФункции

// Функция заполненность обязательных полей
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Заполненность обязательных полей
//
Функция ПроверитьЗаполнениеПолей(СтрокаОшибкиФункции = Неопределено)
	Отказ = Ложь;
	СтруктураОбязательныхПолей = Новый Структура("Входящее_Исходящее, НомерОтправителя, Автор, НачалоОтправки");
	смсРаботаССообщениями.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, "");
	Если ФлагАктуальность И НЕ ЗначениеЗаполнено(Актуальность) Тогда
		Отказ = Истина;
	КонецЕсли; 
	Если Получатели.Количество() = 0 Тогда
		Если СтрокаОшибкиФункции = Неопределено Тогда
			Сообщить(НСтр("ru = 'Не указаны получатели сообщения!'"), СтатусСообщения.Важное);
		Иначе
			СтрокаОшибкиФункции = "Не указаны получатели сообщения!";
		КонецЕсли;
		Отказ = Истина;
	Иначе
		СписокТелефонов = Новый СписокЗначений;
		Для Каждого Получатель Из Получатели Цикл
			ПредставлениеПолучателя = ?(ТипЗнч(Получатель.Контрагент) = Тип("Строка") ИЛИ НЕ ЗначениеЗаполнено(Получатель.Контрагент), Получатель.Контрагент, Получатель.Контрагент.Наименование);
			Если ПустаяСтрока(Получатель.НомерТелефона) Тогда
				ТекстСообщенияОшибки = СтрШаблон(НСтр("ru = 'Для контрагента %1 не указан номер телефон!'"), ПредставлениеПолучателя);
				Если СтрокаОшибкиФункции = Неопределено Тогда
					Сообщить(ТекстСообщенияОшибки, СтатусСообщения.Важное);
				Иначе
					СтрокаОшибкиФункции = ТекстСообщенияОшибки;
				КонецЕсли;
				Отказ = Истина;
			Иначе
				Если СписокТелефонов.НайтиПоЗначению(Получатель.НомерТелефона) = Неопределено Тогда
					СписокТелефонов.Добавить(Получатель.НомерТелефона);
				Иначе
					ТекстСообщенияОшибки = СтрШаблон(НСтр("ru = 'Номер телефона %1 контрагента %2 уже присутствует в таблице получателей!'"), Получатель.НомерТелефона, ПредставлениеПолучателя);
					Если СтрокаОшибкиФункции = Неопределено Тогда
						Сообщить(ТекстСообщенияОшибки, СтатусСообщения.Важное);
					Иначе
						СтрокаОшибкиФункции = ТекстСообщенияОшибки;
					КонецЕсли;
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекстСообщения) Тогда
				Если НЕ ЗначениеЗаполнено(Получатель.ТекстСообщения) Тогда
					Если СтрокаОшибкиФункции = Неопределено Тогда
						Сообщить(НСтр("ru = 'Не заполнен текст сообщения в строке " + Строка(Получатель.НомерСтроки)+"'"));
					Иначе
						СтрокаОшибкиФункции = НСтр("ru = 'Не заполнен текст сообщения в строке " + Строка(Получатель.НомерСтроки)+"'");
					КонецЕсли;
					Отказ = Истина;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции // ПроверитьЗаполнениеПолей()

// Функция проверяет возможность отправки сообщения по введенным интервалам
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Возможность отправки сообщения
//
Функция ПроверитьВозможностьОтправки(СтрокаОшибкиФункции = Неопределено)
	
	Если ЗначениеЗаполнено(Актуальность) И Актуальность < НачалоОтправки + 900 Тогда
		Если СтрокаОшибкиФункции = Неопределено Тогда
			Сообщить(НСтр("ru = 'Дата актуальности сообщения должна быть больше даты отправки не менее чем на 15 минут!'"), СтатусСообщения.Важное);
		Иначе
			СтрокаОшибкиФункции = НСтр("ru = 'Дата актуальности сообщения должна быть больше даты отправки не менее чем на 15 минут!'");
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ НеОтправлятьSMS Тогда Возврат Истина; КонецЕсли;
	
	Если КонецПериодаЗапрета > НачалоПериодаЗапрета Тогда
		ДлинаПериодаЗапрета = КонецПериодаЗапрета - НачалоПериодаЗапрета;
	Иначе	
		ДлинаПериодаЗапрета = КонецПериодаЗапрета - НачалоПериодаЗапрета + 86400;
	КонецЕсли;
	
	Если (КонецПериодаЗапрета = НачалоПериодаЗапрета) ИЛИ (ДлинаПериодаЗапрета = 86399) Тогда
		Если СтрокаОшибкиФункции = Неопределено Тогда
			Сообщить(НСтр("ru = 'Период запрета отправки составляет полные сутки!'"), СтатусСообщения.Важное);
		Иначе
			СтрокаОшибкиФункции = НСтр("ru = 'Период запрета отправки составляет полные сутки!'");
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Если (Актуальность - НачалоОтправки) > ДлинаПериодаЗапрета Тогда Возврат Истина; КонецЕсли;
	
	Если (НачалоОтправки - НачалоДня(НачалоОтправки)) < (НачалоПериодаЗапрета - Дата('00010101')) Тогда Возврат Истина; КонецЕсли;
	
	Если КонецПериодаЗапрета > НачалоПериодаЗапрета Тогда
		Если (НачалоОтправки - НачалоДня(НачалоОтправки)) > (КонецПериодаЗапрета - Дата('00010101')) Тогда
			Возврат Истина;
		ИначеЕсли (Актуальность - НачалоДня(Актуальность)) > (КонецПериодаЗапрета - Дата('00010101')) Тогда
			Возврат Истина;
		Иначе
			Если СтрокаОшибкиФункции = Неопределено Тогда
				Сообщить(НСтр("ru = 'Начало отправки и актуальность находятся внутри периода запрета отправки!'"), СтатусСообщения.Важное);
			Иначе
				СтрокаОшибкиФункции = НСтр("ru = 'Начало отправки и актуальность находятся внутри периода запрета отправки!'");
			КонецЕсли;
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Если (Актуальность - НачалоДня(Актуальность)) > (НачалоПериодаЗапрета - Дата('00010101')) Тогда
			Если СтрокаОшибкиФункции = Неопределено Тогда
				Сообщить(НСтр("ru = 'Начало отправки и актуальность находятся внутри периода запрета отправки!'"), СтатусСообщения.Важное);
			Иначе
				СтрокаОшибкиФункции = НСтр("ru = 'Начало отправки и актуальность находятся внутри периода запрета отправки!'");
			КонецЕсли;
			Возврат Ложь;
		ИначеЕсли (Актуальность - НачалоДня(Актуальность)) < (КонецПериодаЗапрета - Дата('00010101')) Тогда
			Если СтрокаОшибкиФункции = Неопределено Тогда
				Сообщить(НСтр("ru = 'Начало отправки и актуальность находятся внутри периода запрета отправки!'"), СтатусСообщения.Важное);
			Иначе
				СтрокаОшибкиФункции = НСтр("ru = 'Начало отправки и актуальность находятся внутри периода запрета отправки!'");
			КонецЕсли;
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции // ПроверитьВозможностьОтправки()

// Записывает в регистр статусов статусы новых сообщений
//
Процедура ЗаписатьСтатусыВРегистр() Экспорт
	
	НаборЗаписей = РегистрыСведений.смсСостоянияСообщений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сообщение.Значение = ЭтотОбъект.Ссылка;
	НаборЗаписей.Отбор.Сообщение.Использование = Истина;
	
	Для Каждого Строка Из Получатели Цикл
		НовСтрока = НаборЗаписей.Добавить();
		НовСтрока.Сообщение = ЭтотОбъект.Ссылка;
		НовСтрока.НомерСтрокиДокумента = Строка.НомерСтроки;
		НовСтрока.GUID = Строка.GUID_SMS;
		НовСтрока.Статус = Перечисления.смсСостоянияСообщений.Доставка;
	КонецЦикла; 
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

//отправка SMS получателям
Функция ОтправитьSMS() Экспорт
	
	Результат = "";
	Если НЕ ПроверитьЗаполнениеПолей(Результат) Тогда
		Возврат Результат;
	КонецЕсли;
	Если НЕ ПроверитьВозможностьОтправки(Результат) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого ТекущееСообщение Из Получатели Цикл
		ТекущееСообщение.GUID_SMS = Новый УникальныйИдентификатор;
	КонецЦикла;
	ЗаписатьСтатусыВРегистр();
	
	смсРаботаССообщениями.РегламентноеЗаданиеПроверкаСМС(Истина);
	
	СтруктураСтатуса = ПолучитьСтатусДокумента();
	СтатусСтрокой    = СтруктураСтатуса.СтатусСтрокой;
	Статус           = СтруктураСтатуса.Статус;
	
	Записать();
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает статус документа
//
// Параметры:
//	Нет.
//
//
Функция ПолучитьСтатусДокумента() Экспорт
	
	СтруктураСтатуса = Новый Структура;
	СтруктураСтатуса.Вставить("Статус", Перечисления.смсСостоянияСообщений.ПустаяСсылка());
	СтруктураСтатуса.Вставить("СтатусСтрокой", "");
	
	ВсегоКоличество 			= Получатели.Количество();
	КоличествоОтправляемых		= 0;
	КоличествоВОчереди 			= 0;
	КоличествоОтправленных 		= 0;
	КоличествоНеОтправленных	= 0;
	КоличествоДоставленных 		= 0;
	КоличествоНеДоставленных	= 0;
	КоличествоОшибок 			= 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	смсСостоянияСообщений.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.смсСостоянияСообщений КАК смсСостоянияСообщений
	|ГДЕ
	|	смсСостоянияСообщений.Сообщение = &Док";
	Запрос.УстановитьПараметр("Док", ЭтотОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрокаДокумента Из РезультатЗапроса Цикл
		Если ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.ПустаяСсылка() Тогда
			Продолжить;	
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.Доставка Тогда
			КоличествоОтправляемых = КоличествоОтправляемых + 1;
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.ВОчереди Тогда
			КоличествоВОчереди = КоличествоВОчереди + 1;
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.Отправлено Тогда
			КоличествоОтправленных 	= КоличествоОтправленных + 1;
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.НеОтправлено Тогда
			КоличествоНеОтправленных = КоличествоНеОтправленных + 1;
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.Доставлено Тогда
			КоличествоДоставленных 	= КоличествоДоставленных + 1;
			КоличествоОтправленных 	= КоличествоОтправленных + 1;
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.НеДоставлено Тогда
			КоличествоНеДоставленных = КоличествоНеДоставленных + 1;
			КоличествоОтправленных 	= КоличествоОтправленных + 1;
		ИначеЕсли ТекСтрокаДокумента.Статус = Перечисления.смсСостоянияСообщений.Ошибка Тогда
			КоличествоОшибок = КоличествоОшибок + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоОтправляемых >  0 Тогда
		СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.Доставка; 	
	ИначеЕсли КоличествоВОчереди > 0 Тогда
		СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.ВОчереди; 	
	ИначеЕсли КоличествоОтправленных > 0 Тогда
		Если КоличествоОтправленных = КоличествоДоставленных Тогда
			СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.Доставлено; 	
		Иначе
			СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.Отправлено; 	
		КонецЕсли;
	ИначеЕсли КоличествоНеДоставленных > 0 Тогда	
		СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.НеДоставлено; 	
	ИначеЕсли КоличествоНеОтправленных > 0 Тогда	
		СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.НеОтправлено; 	
	ИначеЕсли КоличествоОшибок > 0 Тогда	
		СтруктураСтатуса.Статус = Перечисления.смсСостоянияСообщений.Ошибка; 	
	КонецЕсли;
	
	СтруктураСтатуса.СтатусСтрокой = НСтр("ru = 'Всего: '") + ВсегоКоличество + ";"
	+ ?((КоличествоВОчереди + КоличествоОтправленных + КоличествоНеОтправленных + КоличествоДоставленных 
	+ КоличествоНеДоставленных + КоличествоОшибок = 0), НСтр("ru = 'SMS записано, но не отправлено'"), НСтр("ru = ' из них:'"))
	+ ?(КоличествоВОчереди = 0, "", НСтр("ru = ' в очереди-'") + КоличествоВОчереди + ";")
	+ ?(КоличествоОтправленных = 0, "", НСтр("ru = ' отправлено-'") + КоличествоОтправленных + ";")
	+ ?(КоличествоНеОтправленных = 0, "", НСтр("ru = ' не отправлено-'") + КоличествоНеОтправленных + ";")
	+ ?(КоличествоДоставленных = 0, "", НСтр("ru = ' доставлено-'") + КоличествоДоставленных + ";")
	+ ?(КоличествоНеДоставленных = 0, "", НСтр("ru = ' не доставлено-'") + КоличествоНеДоставленных + ";")
	+ ?(КоличествоОшибок = 0, "", НСтр("ru = ' ошибок-'") + КоличествоОшибок);
	
	Возврат СтруктураСтатуса;
	
КонецФункции // СтатусСтрокойДокумента()

//Процедура заполняет строку ТЧ из переданной записи КИ
//
Процедура ЗаполнитьСтрокуИзКонтактнойИнформации(Значение,Строка)Экспорт
	Строка.НомерТелефона = кмУбратьЛишниеСимволыТелефона(СокрЛП(Значение.Поле1)+СокрЛП(Значение.Поле2)+СокрЛП(Значение.Поле3));
	Строка.Статус = "НеОтправлено";
КонецПроцедуры	

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
КонецФункции // ПроверитьКорректность()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("SMS", "SMS", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

//Функция предназначена для обработки изменения реквизита объекта
// Возвращает признак удачной обработки
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
	
КонецФункции

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// ПолучитьСтатусыСообщений
//
Процедура ПолучитьСтатусыСообщений() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	смсСостоянияСообщений.GUID,
	|	смсСостоянияСообщений.Статус,
	|	смсСостоянияСообщений.ОписаниеОшибки,
	|	смсСостоянияСообщений.НомерСтрокиДокумента
	|ИЗ
	|	РегистрСведений.смсСостоянияСообщений КАК смсСостоянияСообщений
	|ГДЕ
	|	смсСостоянияСообщений.Сообщение = &СсылкаНаДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	смсСостоянияСообщений.НомерСтрокиДокумента";
	Запрос.УстановитьПараметр("СсылкаНаДокумент", ЭтотОбъект.Ссылка);
	ВыборкаСтатусов=Запрос.Выполнить().Выбрать();
	Пока ВыборкаСтатусов.Следующий() Цикл
		СтрокаТЧ=Получатели.Найти(ВыборкаСтатусов.GUID,"GUID_SMS");
		Если СтрокаТЧ<>Неопределено Тогда
			Если СтрокаТЧ.Статус<>Строка(ВыборкаСтатусов.Статус) Тогда
				СтрокаТЧ.Статус=ВыборкаСтатусов.Статус;
			КонецЕсли; 
			Если СтрокаТЧ.ОписаниеОшибки<>ВыборкаСтатусов.ОписаниеОшибки Тогда
				СтрокаТЧ.ОписаниеОшибки=ВыборкаСтатусов.ОписаниеОшибки;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры


#Если Клиент Тогда
	
	////////////////////////////////////////////////////////////////////////////////
	// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
	
	// Функция осуществляет печать произвольного документа.
	// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
	//
	// Параметры:
	//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
	//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
	//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
	//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
	//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
	//
	// Возвращаемое значение:
	//   ТабличныйДокумент	- Сформированная печатная форма
	//
	Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
		// Вызов конкретно печатной формы и печать результата прописано в общем модуле
		// здесь могут быть вставлены дополнительные механизмы
		Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
	КонецФункции // Печать()
	
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

//Обработчик установки нового номера документа
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

//Обработка заполнения нового документа
Процедура ОбработкаЗаполнения(Основание)
	
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если Основание <> Неопределено Тогда
		Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.Событие") Тогда
			// Заполнение шапки
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаБонусов") Тогда
			Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;
			
			Получатели.Очистить();
			
			// заполним получателями из документа основания
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	КорректировкаБонусовБонусыКНачислению.Карточка.Объект КАК Контрагент
			|ПОМЕСТИТЬ Контрагенты
			|ИЗ
			|	Документ.КорректировкаБонусов.БонусыКНачислению КАК КорректировкаБонусовБонусыКНачислению
			|ГДЕ
			|	КорректировкаБонусовБонусыКНачислению.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Представление
			|ПОМЕСТИТЬ КИСотовый
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Вид = &Вид
			|
			|СГРУППИРОВАТЬ ПО
			|	КонтактнаяИнформация.Представление,
			|	КонтактнаяИнформация.Объект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Представление
			|ПОМЕСТИТЬ КИКонтактный
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|ГДЕ
			|	КонтактнаяИнформация.Вид = &ВидКонтактный
			|
			|СГРУППИРОВАТЬ ПО
			|	КонтактнаяИнформация.Представление,
			|	КонтактнаяИнформация.Объект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Контрагенты.Контрагент,
			|	ЕСТЬNULL(КИ.Представление, ЕСТЬNULL(КИКонтактный.Представление, """")) КАК НомерТелефона
			|ИЗ
			|	Контрагенты КАК Контрагенты
			|		ЛЕВОЕ СОЕДИНЕНИЕ КИСотовый КАК КИ
			|		ПО Контрагенты.Контрагент = КИ.Объект
			|		ЛЕВОЕ СОЕДИНЕНИЕ КИКонтактный КАК КИКонтактный
			|		ПО Контрагенты.Контрагент = КИКонтактный.Объект";
			Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
			Запрос.УстановитьПараметр("ВидКонтактный", Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
			Запрос.УстановитьПараметр("Ссылка", Основание);
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(Получатели.Добавить(), Выборка);
			КонецЦикла;
						
		Иначе
			НоваяСтрока = Получатели.Добавить();
			НоваяСтрока.Контрагент = Основание.Контрагент;
			НоваяСтрока.ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
			//поищем основной мобильный телефон
			НоваяСтрока.КонтактнаяИнформация = кмПолучитьОсновнойМобильныйТелефонОбъекта(НоваяСтрока.Контрагент);
			НоваяСтрока.НомерТелефона = НоваяСтрока.КонтактнаяИнформация;		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//Обработчик копирования документа
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из Получатели Цикл
		Если НЕ ПустаяСтрока(СтрокаТЧ.ТекстСообщения) Тогда
			СтрокаТЧ.ТекстСообщения="";
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры // ПриКопировании()

//Обработчик перед записью документа
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее Тогда
		СтруктураСтатуса = ПолучитьСтатусДокумента();
		СтатусСтрокой = СтруктураСтатуса.СтатусСтрокой;
		Статус        = СтруктураСтатуса.Статус;
	КонецЕсли;
	
	СписокПолучателей="";
	Для Каждого ТекущаяСтрока Из Получатели Цикл 
		Если (ТекущаяСтрока.Контрагент = Неопределено) ИЛИ (обЗначениеНеЗаполнено(ТекущаяСтрока.Контрагент)) ИЛИ (ТипЗнч(ТекущаяСтрока.Контрагент) = Тип("Строка")) Тогда 
			СписокПолучателей=ТекущаяСтрока.НомерТелефона+СписокПолучателей;
		Иначе	
			СписокПолучателей=ТекущаяСтрока.Контрагент.Наименование+СписокПолучателей;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()

//Обработчик при записи документа
Процедура ПриЗаписи(Отказ)
	
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее Тогда
	КонецЕсли;
	#Если Клиент Тогда
		Оповестить("ОбновитьДеревоСобытий", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры

//Обработчик перед удалением документа
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли